# $Id: PKGBUILD 80410 2010-05-17 07:53:48Z tpowa $
# Maintainer: James Rayner <iphitus@gmail.com>
# Contributor: Francesco Piccinno <stack.box@gmail.com>

pkgname=tiacx
pkgver=20080210
pkgrel=15
pkgdesc="OpenSource module for Texas Instruments ACX100/ACX111 wireless chips. For stock arch 2.6 kernel"
arch=(i686 x86_64)
url="http://acx100.sourceforge.net/"
license=('MPL')
depends=('wireless_tools' 'kernel26>=2.6.34' 'kernel26<2.6.35' 'tiacx-firmware')
makedepends=('kernel26-headers>=2.6.34' 'kernel26-headers<2.6.35')
install=acx.install
source=(http://downloads.sourceforge.net/sourceforge/acx100/acx-$pkgver.tar.bz2
        http://patches.aircrack-ng.org/acx-20070101.patch
        kernel-2.6.27.patch
        kernel-2.6.30.patch
        kernel-2.6.31.patch
        kernel-2.6.33.patch)
_kernver=2.6.34-ARCH

build() {
    cd $startdir/src/acx-$pkgver
    patch -Np1 -i ../acx-20070101.patch || return 1
    patch -Np1 -i ../kernel-2.6.27.patch || return 1
    patch -Np0 -i ../kernel-2.6.30.patch || return 1
    patch -Np1 -i ../kernel-2.6.31.patch || return 1
    patch -Np0 -i ../kernel-2.6.33.patch || return 1
    make -C /lib/modules/${_kernver}/build M=`pwd` || return 1
    install -D acx.ko $startdir/pkg/lib/modules/${_kernver}/kernel/drivers/net/wireless/tiacx/acx.ko
    sed -i -e "s/KERNEL_VERSION='.*'/KERNEL_VERSION='${_kernver}'/" $startdir/*.install
}
md5sums=('7d5ce3215708e4e9f95cf567a9ee3a12'
         '786344c994b8e36c91b2d81490c770b1'
         '9895f72f8d0c84956b0f6c3b16df0fe8'
         'e2900aee839ce711a7f7606b3e3de36e'
         'dbcb276d77df7801cef58a85c18dfe94'
         '9cd35ea33b8abdd2e4efec485b05477e')
