# TODO: Add to package groups.
# old groups: archtrack archtrack-forensics
groups=(blackarch)
pkgname=rkhunter-blackarch
_realname=rkhunter
pkgver=1.4.0
provides=("rkhunter=$pkgver")
conflicts=(rkhunter)
pkgrel=1
pkgdesc='Checks machines for the presence of rootkits and other unwanted tools.'
arch=('any')
url='http://rkhunter.sourceforge.net/'
license=('GPL')
depends=('bash' 'perl' 'wget')
optdepends=('unhide')
backup=('etc/rkhunter.conf')
source=("http://downloads.sourceforge.net/sourceforge/${_realname}/${_realname}-${pkgver}.tar.gz"{,.asc})
md5sums=('37b1ceb79a5ff3debca335d6550ac6b0'
         'e83221c3eb3f417357059731c809a9de')

package() {
  cd "${srcdir}/${_realname}-${pkgver}"

  # fix installer for x86_64
  sed -i -e "s/lib64/lib/g" installer.sh

  # install to "/usr"
  install -dm0755 "${pkgdir}/usr"
  ./installer.sh --layout custom "${pkgdir}/usr" --striproot "${pkgdir}" --install

  # fix paths
  mv "${pkgdir}/usr/etc" "${pkgdir}"
  mv "${pkgdir}/usr/var" "${pkgdir}"

  sed -i -e "s#/usr/etc/rkhunter.conf#/etc/rkhunter.conf#g" "${pkgdir}/usr/bin/${_realname}"
  sed -i -e "s#/usr/etc/rkhunter.conf#/etc/rkhunter.conf#g" "${pkgdir}/etc/${_realname}.conf"
  sed -i -e "s#/usr/var/lib/rkhunter/db#/var/lib/rkhunter/db#g" "${pkgdir}/etc/${_realname}.conf"
  sed -i -e "s#/usr/var/lib/rkhunter/tmp#/var/lib/rkhunter/tmp#g" "${pkgdir}/etc/${_realname}.conf"

  # cleanup
  rm -f "${pkgdir}/var/lib/${_realname}/tmp/"{group,passwd}

  # we trust in udev
  sed -i 's|^#ALLOWHIDDENDIR=/dev/.udev$|ALLOWHIDDENDIR=/dev/.udev|' \
    "${pkgdir}/etc/${_realname}.conf"
}
