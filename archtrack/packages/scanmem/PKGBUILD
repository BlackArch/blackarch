# Archtrack maintainer: Evan Teitelman <teitelmanevan at gmail dot com>

pkgname=scanmem
pkgver=0.13
pkgrel=1
pkgdesc="A utility used to locate the address of a variable in an executing process."
arch=('i686' 'x86_64')
url="http://code.google.com/p/scanmem/"
license=('GPL3')
depends=('python2' 'pygtk')
optdepends=('gksu: needed for the GameConqueror GUI.')
source=(http://scanmem.googlecode.com/files/$pkgname-$pkgver.tar.gz)
md5sums=('1c23eda15db242ff0aac96d94be11db1')

groups=(archtrack archtrack-reverse-engineering)

build() {
  cd "$srcdir/$pkgname-$pkgver"
  chmod 755 configure
  ./configure --enable-gui --prefix=/usr
  msg2 "Building..."
  make -s
}

package() {
  cd "$srcdir/$pkgname-$pkgver"

  make -s DESTDIR="$pkgdir" install

  # Replacing calls to the Python 3.x interpreter to the Python 2.x one:
  sed 's|python|python2|g' -i "$pkgdir/usr/bin/gameconqueror"
  sed 's|#!/usr/bin/env python|#!/usr/bin/env python2|g' -i \
      "$pkgdir/usr/share/gameconqueror/"{backend.py,GameConqueror.py,hexview.py}
}

