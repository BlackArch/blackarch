pkgname=ophcrack-archtrack
_realname=ophcrack
pkgver=3.5.0
provides=("ophcrack=$pkgver")
conflicts=(ophcrack)
pkgrel=1
pkgdesc='A free Windows password cracker based on rainbow tables'
arch=('i686' 'x86_64')
url="http://ophcrack.sourceforge.net"
license=('GPL')
groups=(archtrack archtrack-password-attacks)
depends=('qt4')
optdepends=('qwt: enable graph')
source=(http://downloads.sourceforge.net/project/$_realname/$_realname/$pkgver/$_realname-$pkgver.tar.bz2
        ophcrack.desktop)
md5sums=('1c61adde21c5dc226515cbd1dc654c60'
         '664599c4fd7fd210e6c421459f60e20d')

build() {
  cd "$srcdir"/$_realname-$pkgver

  ./configure --prefix=/usr --enable-gui --enable-graph

  make
}

package() {
  cd "$srcdir"/$_realname-$pkgver

  make DESTDIR="$pkgdir"/ install

  install -Dm644 "$srcdir"/ophcrack.desktop "$pkgdir"/usr/share/applications/ophcrack.desktop
  install -Dm644 src/gui/pixmaps/os.xpm "$pkgdir"/usr/share/$_realname/pixmaps/os.xpm
}
