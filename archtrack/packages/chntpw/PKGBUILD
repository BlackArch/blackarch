# Archtrack maintainer: Evan Teitelman <teitelmanevan at gmail dot com>

pkgname=chntpw-archtrack
_realname=chntpw
groups=(archtrack archtrack-forensics archtrack-password-attacks)
pkgver=110511
pkgrel=2
provides=("chntpw=$pkgver")
conflicts=(chntpw)
pkgdesc='Offline NT Password Editor - reset passwords in a Windows NT SAM user database file.'
arch=('i686' 'x86_64')
url='http://pogostick.net/~pnh/ntpasswd/'
license=('GPL' 'LGPL')
makedepends=('openssl')
source=("http://pogostick.net/~pnh/ntpasswd/${_realname}-source-${pkgver}.zip")
md5sums=('a3fb358d1adec589cd6bc8dedf68896c')

build() {
  cd "${srcdir}/${_realname}-${pkgver}"

  [ "$CARCH" = "x86_64" ] && sed -i "s|CFLAGS= -DUSEOPENSSL -g -I. -I\$(OSSLINC) -Wall -m32|CFLAGS= -DUSEOPENSSL -g -I. -I\$(OSSLINC) -Wall|" Makefile
  make
}

package() {
  cd "${srcdir}/${_realname}-${pkgver}"

  # install binaries, don't install static binaries!
  install -Dm0755 "${srcdir}/${_realname}-${pkgver}/${_realname}" "${pkgdir}/usr/bin/${_realname}"
  install -Dm0755 "${srcdir}/${_realname}-${pkgver}/reged" "${pkgdir}/usr/bin/reged"
  install -Dm0755 "${srcdir}/${_realname}-${pkgver}/cpnt" "${pkgdir}/usr/bin/cpnt"

  for _f in *.txt; do
    install -Dm0644 "${_f}" "${pkgdir}/usr/share/doc/${_realname}/${_f}"
  done
}
