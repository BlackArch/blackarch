# Archtrack maintainer: Evan Teitelman <teitelmanevan at gmail dot com>

pkgname=wget-archtrack
_realname=wget
groups=(archtrack archtrack-info-gathering)
pkgver=1.14
pkgrel=2
provides=("wget=$pkgver")
conflicts=(wget)
pkgdesc='A network utility to retrieve files from the Web'
arch=('i686' 'x86_64')
url="http://www.gnu.org/software/wget/wget.html"
license=('GPL3')
depends=('openssl' 'libidn' 'pcre' 'util-linux')
optdepends=('ca-certificates: HTTPS downloads')
backup=('etc/wgetrc')
install=wget.install
source=(ftp://ftp.gnu.org/gnu/${_realname}/${_realname}-${pkgver}.tar.xz{,.sig})
md5sums=('316f6f59292c9098ad81fd54f658c579'
         '3e121933d69f32fa58776bff76bcebd0')

build() {
  cd "${srcdir}/${_realname}-${pkgver}"

  ./configure -with-ssl=openssl --prefix=/usr --sysconfdir=/etc --enable-nls
  make
}

package() {
  cd "${srcdir}/${_realname}-${pkgver}"
  make DESTDIR="${pkgdir}" install

  cat >> "$pkgdir/etc/wgetrc" <<EOF
# default root certs location
ca_certificate=/etc/ssl/certs/ca-certificates.crt
EOF
}
