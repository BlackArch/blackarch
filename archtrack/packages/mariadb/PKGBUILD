# TODO: Add to package groups.
# old groups: 
pkgbase=mariadb
pkgname=('libmariadbclient-blackarch' 'mariadb-clients-blackarch' 'mariadb-blackarch')
pkgver=5.5.30
pkgrel=2
arch=('i686' 'x86_64')
license=('GPL')
url='http://mariadb.org/'
makedepends=('cmake' 'openssl' 'zlib')
options=('!libtool')
source=(http://mirrors.supportex.net/$pkgbase/$pkgbase-$pkgver/kvm-tarbake-jaunty-x86/$pkgbase-$pkgver.tar.gz
        mariadb.service
        mariadb-post.sh
        mariadb-tmpfile.conf)
sha256sums=('64dba5f1819f1d4ebf7ed31d74e4106c981a24b88259995deba3734fb7c4635f'
            'a6cf5446159fff16598d0d787dd3a3c30a53a656a5f876ddb608bc60e234d334'
            '368f9fd2454d80eb32abb8f29f703d1cf9553353fb9e1ae4529c4b851cb8c5dd'
            '2af318c52ae0fe5428e8a9245d1b0fc3bc5ce153842d1563329ceb1edfa83ddd')

build() {
  cd $srcdir
  mkdir build
  cd build

  cmake ../$pkgbase-$pkgver \
    -DCMAKE_BUILD_TYPE=Release \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DSYSCONFDIR=/etc/mysql \
    -DMYSQL_DATADIR=/var/lib/mysql \
    -DMYSQL_UNIX_ADDR=/run/mysqld/mysqld.sock \
    -DDEFAULT_CHARSET=utf8 \
    -DDEFAULT_COLLATION=utf8_general_ci \
    -DENABLED_LOCAL_INFILE=ON \
    -DINSTALL_INFODIR=share/mysql/docs \
    -DINSTALL_MANDIR=share/man \
    -DINSTALL_PLUGINDIR=/usr/lib/mysql/plugin \
    -DINSTALL_SCRIPTDIR=bin \
    -DINSTALL_INCLUDEDIR=include/mysql \
    -DINSTALL_DOCREADMEDIR=share/mysql \
    -DINSTALL_SUPPORTFILESDIR=share/mysql \
    -DINSTALL_MYSQLSHAREDIR=share/mysql \
    -DINSTALL_DOCDIR=share/mysql/docs \
    -DINSTALL_SHAREDIR=share/mysql \
    -DWITH_READLINE=ON \
    -DWITH_ZLIB=system \
    -DWITH_SSL=system \
    -DWITH_LIBWRAP=OFF \
    -DWITH_EXTRA_CHARSETS=complex \
    -DWITH_EMBEDDED_SERVER=ON \
    -DWITH_ARCHIVE_STORAGE_ENGINE=1 \
    -DWITH_BLACKHOLE_STORAGE_ENGINE=1 \
    -DWITH_INNOBASE_STORAGE_ENGINE=1 \
    -DWITH_PARTITION_STORAGE_ENGINE=1 \
    -DWITHOUT_EXAMPLE_STORAGE_ENGINE=1 \
    -DWITHOUT_FEDERATED_STORAGE_ENGINE=1 \
    -DWITHOUT_PBXT_STORAGE_ENGINE=1 \
    -DCMAKE_C_FLAGS="-fPIC ${CFLAGS} -fno-strict-aliasing -DBIG_JOINS=1 -fomit-frame-pointer" \
    -DCMAKE_CXX_FLAGS="-fPIC ${CXXFLAGS} -fno-strict-aliasing -DBIG_JOINS=1 -felide-constructors -fno-rtti" \
    -DWITH_MYSQLD_LDFLAGS="${LDFLAGS}"

  make
}

package_libmariadbclient-blackarch() {
  pkgdesc='MariaDB client libraries'
  depends=('openssl')
  conflicts=('libmysqlclient' 'libmariadbclient')
  provides=("libmysqlclient=$pkgver" "libmariadbclient=$pkgver")

  cd "$srcdir"/build
  for dir in include libmysql libmysqld libservices; do
    make -C $dir DESTDIR="$pkgdir" install
  done

  install -d "$pkgdir"/usr/bin
  install -m755 scripts/mysql_config "$pkgdir"/usr/bin/
  install -d "$pkgdir"/usr/share/man/man1
  for man in mysql_config mysql_client_test_embedded mysqltest_embedded; do
    install -m644 "$srcdir"/$pkgbase-$pkgver/man/$man.1 "$pkgdir"/usr/share/man/man1/$man.1
  done
}

package_mariadb-clients-blackarch() {
  pkgdesc='MariaDB client tools'
  depends=('libmariadbclient')
  conflicts=('mysql-clients' 'mariadb-clients')
  provides=("mysql-clients=$pkgver" "mariadb-clients=$pkgver")

  cd "$srcdir"/build
  make -C client DESTDIR="$pkgdir" install

  # install man pages
  install -d "$pkgdir"/usr/share/man/man1
  for man in mysql mysqladmin mysqlcheck mysqldump mysqlimport mysqlshow mysqlslap; do
    install -m644 "$srcdir"/$pkgbase-$pkgver/man/$man.1 "$pkgdir"/usr/share/man/man1/$man.1
  done

  # provided by mariadb
  rm "$pkgdir"/usr/bin/{mysql_{plugin,upgrade},mysqlbinlog,mysqltest}
}

package_mariadb-blackarch() {
  pkgdesc='Fast SQL database server, drop-in replacement for MySQL'
  backup=('etc/mysql/my.cnf')
  install=mariadb.install
  depends=('mariadb-clients')
  optdepends=('perl-dbd-mysql: mytop dependency'
              'perl-term-readkey: mytop dependency')
  options=('emptydirs')
  conflicts=('mysql' 'mytop' 'mariadb')
  provides=("mysql=$pkgver" 'mytop' "mariadb=$pkgver")
  groups=(blackarch)

  cd "$srcdir"/build
  make DESTDIR="$pkgdir" install

  install -Dm644 "$pkgdir"/usr/share/mysql/my-medium.cnf "$pkgdir"/etc/mysql/my.cnf
  install -Dm755 "$srcdir"/mariadb-post.sh "$pkgdir"/usr/bin/mysqld-post
  install -Dm644 "$srcdir"/mariadb.service "$pkgdir"/usr/lib/systemd/system/mysqld.service
  install -Dm644 "$srcdir"/mariadb-tmpfile.conf "$pkgdir"/usr/lib/tmpfiles.d/mysql.conf

  # provided by libmariadbclient
  rm "$pkgdir"/usr/bin/{mysql_config,mysql_client_test_embedded,mysqltest_embedded}
  rm "$pkgdir"/usr/lib/libmysql*
  rm -r "$pkgdir"/usr/include/
  rm "$pkgdir"/usr/share/man/man1/{mysql_config,mysql_client_test_embedded,mysqltest_embedded}.1

  # provided by mariadb-clients
  rm "$pkgdir"/usr/bin/{mysql,mysqladmin,mysqlcheck,mysqldump,mysqlimport,mysqlshow,mysqlslap}
  rm "$pkgdir"/usr/share/man/man1/{mysql,mysqladmin,mysqlcheck,mysqldump,mysqlimport,mysqlshow,mysqlslap}.1

  # not needed
  rm -r "$pkgdir"/usr/{data,mysql-test,sql-bench}
  rm "$pkgdir"/usr/share/man/man1/mysql-test-run.pl.1
}
