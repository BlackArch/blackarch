pkgname=jad-archtrack
_realname=jad
groups=(archtrack archtrack-reverse-engineering)
pkgver=1.5.8e
pkgrel=4
provides=("jad=$pkgver")
conflicts=(jad)
pkgdesc='Java decompiler'
license=('custom')
arch=('x86_64' 'i686')
url='http://www.varaneckas.com/jad'
source=("http://www.varaneckas.com/$_realname/${_realname}${pkgver//./}.linux.static.zip"
        'jad.html::http://www.varaneckas.com/jad')
sha256sums=('2878e19fc1fdd725b516f538a57b02aaec1b2d1e4b106d550230381ffa9c0c81'
            '4acee94ff29fe01ea2a9c4c0b47f22fb5a93fc667ee2dd8b6740f7f36bcbcdf2')

build() {
  cd "$srcdir"

  grep '<code>' jad.html -A18 | sed 's:<code>::' | sed 's:</code>::' > LICENSE
}

package() {
  cd "$srcdir"

  install -Dm755 "$srcdir/$_realname" "$pkgdir/usr/bin/$_realname"
  install -Dm644 LICENSE "$pkgdir/usr/share/licenses/$_realname/LICENSE"
  install -Dm644 Readme.txt "$pkgdir/usr/share/doc/$_realname/README"
}

