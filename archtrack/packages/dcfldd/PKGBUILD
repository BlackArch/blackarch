# $Id: PKGBUILD 83134 2013-01-27 16:12:28Z pierre $
# Archtrack maintainer: Evan Teitelman <teitelmanevan at gmail dot com>

pkgname=dcfldd-archtrack
_realname=dcfldd
groups=(archtrack archtrack-forensics)
pkgver=1.3.4.1
pkgrel=3
provides=("dcfldd=$pkgver")
conflicts=(dcfldd)
pkgdesc='DCFL (DoD Computer Forensics Lab) dd replacement with hashing'
arch=('i686' 'x86_64')
url="http://dcfldd.sourceforge.net/"
license=('GPL')
depends=('glibc')
source=("http://downloads.sourceforge.net/sourceforge/$_realname/$_realname-${pkgver%.*}-${pkgver##*.}.tar.gz")
md5sums=('952026c872f11b53ce0ec6681a3eef0a')
sha1sums=('fb1c55f107a6af5ef8703a44d33476e508815913')

build() {
  cd "$srcdir/$_realname-${pkgver%.*}-${pkgver##*.}"
  ./configure --prefix=/usr --mandir=/usr/share/man
  make
}

package() {
  cd "$srcdir/$_realname-${pkgver%.*}-${pkgver##*.}"
  make DESTDIR="$pkgdir" install
}
