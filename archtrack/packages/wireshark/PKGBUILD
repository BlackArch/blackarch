# $Id: PKGBUILD 186024 2013-05-20 14:35:40Z guillaume $
# Archtrack maintainer: Evan Teitelman <teitelmanevan at gmail dot com>
pkgname=('wireshark-cli-archtrack' 'wireshark-gtk-archtrack')
pkgbase=wireshark
pkgver=1.8.7
pkgrel=1
arch=('i686' 'x86_64')
license=('GPL2')
makedepends=('bison' 'flex' 'gtk2' 'krb5' 'libcap' 'libpcap' 'bash' 'gnutls'
             'libgcrypt' 'lua51' 'python')
url='http://www.wireshark.org/'
options=(!libtool)
source=(http://www.wireshark.org/download/src/${pkgbase}-${pkgver}.tar.bz2
        02_enable-version.diff)
sha256sums=('5c7e429c1e410289b49d65a2e635e00c0becdcb56fe703a1d3e7b5e275fca842'
            'dd71a9fc3443b5e586d9e50c31208d031f20d7b96cf72d9e4e0f02f2f175d00b')
groups=(archtrack archtrack-info-gathering archtrack-sniffing-spoofing)

build() {
  cd "${srcdir}/${pkgbase}-${pkgver}"

  patch -p0 < ${srcdir}/02_enable-version.diff

  ./autogen.sh
  ./configure \
      --prefix=/usr \
      --with-ssl \
      --with-zlib=yes \
      --with-lua
  make all
}

package_wireshark-cli-archtrack() {
  pkgdesc='a free network protocol analyzer for Unix/Linux and Windows - CLI version'
  depends=('krb5' 'libcap' 'libpcap' 'bash' 'gnutls' 'libgcrypt' 'glib2' 'lua51')
  install=wireshark.install
  conflicts=(wireshark wireshark-cli)
  provides=("wireshark-cli=$pkgver")

  cd "${srcdir}/${pkgbase}-${pkgver}"

  make DESTDIR="${pkgdir}" install

  #wireshark uid group is 150
  chgrp 150 "${pkgdir}/usr/bin/dumpcap"
  chmod 754 "${pkgdir}/usr/bin/dumpcap"
  rm "${pkgdir}/usr/bin/wireshark"

  # Headers
  install -dm755 ${pkgdir}/usr/include/${pkgbase}/{epan/{crypt,dfilter,dissectors,ftypes},wiretap,wsutil}

  install -m644 color.h config.h register.h "${pkgdir}/usr/include/${pkgbase}"
  for d in epan epan/crypt epan/dfilter epan/dissectors epan/ftypes wiretap wsutil; do
    install -m644 ${d}/*.h ${pkgdir}/usr/include/${pkgbase}/${d}
  done
}

package_wireshark-gtk-archtrack() {
  pkgdesc='a free network protocol analyzer for Unix/Linux and Windows - GTK frontend'
  depends=('gtk2' 'wireshark-cli' 'desktop-file-utils' 'hicolor-icon-theme')
  install=wireshark-gtk.install
  replaces=(wireshark)
  conflicts=(wireshark)
  conflicts=(wireshark wireshark-gtk)
  provides=("wireshark-gtk=$pkgver")

  cd "${srcdir}/${pkgbase}-${pkgver}"

  install -Dm755 .libs/wireshark "${pkgdir}/usr/bin/wireshark"
  for d in 16 32 48; do
    install -Dm644 image/hi${d}-app-wireshark.png  \
                   "${pkgdir}/usr/share/icons/hicolor/${d}x${d}/apps/wireshark.png"
  done

  for d in 16 24 32 48 64 128 256 ; do
    install -Dm644 image/WiresharkDoc-${d}.png \
                   "${pkgdir}/usr/share/icons/hicolor/${d}x${d}/mimetypes/application-vnd.tcpdump.pcap.png"
  done
  install -Dm644 wireshark.desktop "${pkgdir}/usr/share/applications/wireshark.desktop"
}
