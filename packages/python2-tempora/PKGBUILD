# This file is part of BlackArch Linux ( https://www.blackarch.org/ ).
# See COPYING for license details.

pkgname=python2-tempora
_pkgname=tempora
pkgver=1.14.1
pkgrel=6
pkgdesc='Objects and routines pertaining to date and time (tempora).'
arch=('any')
url='https://pypi.org/project/tempora/#files'
license=('MIT')
depends=('python2' 'python2-six' 'python2-pytz' 'python2-jaraco')
makedepends=('python2-setuptools' 'python2-setuptools-scm' 'python2-six'
             'python2-pytz' 'python2-jaraco')
source=("https://files.pythonhosted.org/packages/2f/b5/5b0464385454c5ca93a39a1c6acefdf574aeb10ef45fa8958b3832cc7d96/$_pkgname-$pkgver.tar.gz"
        'LICENSE')
sha512sums=('4ecd5430d4d90959b0985908a1e4f239a56ce0026cd315ef6b8c204b645caccb135db76bb2ca9cb95bc65bdf337b61e7c31514eb9c85036771469b6a058edfbb'
            '1106afed483b7258e4ae89c5d9459c3834412b31aac90169725ed62d2ab44f61f6f79e894d4c9e4d8bd99e14530ab778df2187784f0b25eaab86d312fad68944')

build() {
  export SETUPTOOLS_SCM_PRETEND_VERSION="$pkgver"

  cd "$_pkgname-$pkgver"

  python2 setup.py build
}

package() {
  cd "$_pkgname-$pkgver"

  python2 setup.py install --root="$pkgdir" --prefix=/usr -O1 --skip-build

  mv "$pkgdir/usr/bin/calc-prorate" "$pkgdir/usr/bin/calc-prorate2"

  install -Dm 644 "$srcdir/LICENSE" \
    "$pkgdir/usr/share/licenses/$pkgname/LICENSE"
}

