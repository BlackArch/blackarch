# This file is part of BlackArch Linux ( https://www.blackarch.org/ ).
# See COPYING for license details.

pkgbase=python-pylzma
pkgname=('python2-pylzma' 'python-pylzma')
_pkgname=pylzma
pkgver=0.5.0
pkgrel=5
pkgdesc='Python bindings for the LZMA library.'
arch=('x86_64' 'aarch64')
url='https://pypi.org/project/pylzma/#files'
license=('LGPL')
makedepends=('python2-setuptools' 'python-setuptools')
source=("https://files.pythonhosted.org/packages/03/d8/10ef072c3cd4301a65a1b762b09eefa02baf8da23b9ea77ebe9546499975/$_pkgname-$pkgver.tar.gz")
sha512sums=('c4956d84d9e1cbc152c68208acf21f11e6e4ba094b914bf8baeb0522bf8ab7b18dcdbcf114a1b2d1bf29c1cd91808d66c7dd61758686d340ff89cd82fba53ad2')

prepare() {
  cp -a "$_pkgname-$pkgver"{,-2}
}

build() {
  cd "$_pkgname-$pkgver-2"

  python2 setup.py build

  cd "$srcdir/$_pkgname-$pkgver"

  python setup.py build
}

package_python2-pylzma() {
  depends=('python2')

  cd "$_pkgname-$pkgver-2"

  python2 setup.py install --prefix=/usr --root="$pkgdir" --optimize=1 \
    --skip-build
}

package_python-pylzma() {
  depends=('python')

  cd "$_pkgname-$pkgver"

  python setup.py install --prefix=/usr --root="$pkgdir" --optimize=1 \
    --skip-build
}

