# This file is part of BlackArch Linux ( https://www.blackarch.org/ ).
# See COPYING for license details.

pkgbase=python-plumbum
pkgname=('python2-plumbum' 'python-plumbum')
_pkgname=plumbum
pkgver=1.6.7
pkgrel=3
pkgdesc='Shell combinators library.'
arch=('any')
url='https://pypi.org/project/plumbum/#files'
license=('GPL')
makedepends=('python2-setuptools' 'python-setuptools')
options=(!emptydirs)
source=("https://files.pythonhosted.org/packages/78/4c/1fb849111fda37d65358be1859e062278b2f94b5269f19838115966e4493/$_pkgname-$pkgver.tar.gz")
sha512sums=('70a80a77eb64e483cf0444745a4c05c8599711ea578a61fe3969edceba4a9f68ca226c6d8cb65995ad36f3d6423326af887816c5ffd337f20279dfff47cb59b4')

prepare() {
  cp -a "$_pkgname-$pkgver"{,-2}
}

build() {
  cd "$_pkgname-$pkgver-2"

  python2 setup.py build

  cd "$srcdir/$_pkgname-$pkgver"

  python setup.py build
}

package_python2-plumbum() {
  depends=('python2' 'python2-six')

  cd "$_pkgname-$pkgver-2"

  python2 setup.py install --prefix=/usr --root="$pkgdir" --optimize=1 \
    --skip-build
}

package_python-plumbum() {
  depends=('python' 'python-six')

  cd "$_pkgname-$pkgver"

  python setup.py install --prefix=/usr --root="$pkgdir" --optimize=1 \
    --skip-build
}

