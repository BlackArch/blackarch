# This file is part of BlackArch Linux ( https://www.blackarch.org/ ).
# See COPYING for license details.

pkgbase=python-plumbum
pkgname=('python2-plumbum' 'python-plumbum')
_pkgname=plumbum
pkgver=1.7.0
pkgrel=1
pkgdesc='Shell combinators library.'
arch=('any')
url='https://pypi.org/project/plumbum/#files'
license=('GPL')
makedepends=('python2-setuptools' 'python-setuptools')
options=(!emptydirs)
source=("https://files.pythonhosted.org/packages/ed/ba/431d7f420cd93c4b8ccb15ed8f1c6c76c81965634fd70345af0b19c2b7bc/$_pkgname-$pkgver.tar.gz")
sha512sums=('0a12891278c12bb98b8bbb825448375240ee347eb3ca166b9cbb4da3e083979d947a7bf0a843fd9a0b7c9a13d09f24e988ee877f29420a3f29f40e387eec8826')

prepare() {
  cp -a "$_pkgname-$pkgver"{,-2}
}

build() {
  cd "$_pkgname-$pkgver-2"

  python2 setup.py build

  cd "$srcdir/$_pkgname-$pkgver"

  python setup.py build
}

package_python2-plumbum() {
  depends=('python2' 'python2-six')

  cd "$_pkgname-$pkgver-2"

  python2 setup.py install --prefix=/usr --root="$pkgdir" --optimize=1 \
    --skip-build
}

package_python-plumbum() {
  depends=('python' 'python-six')

  cd "$_pkgname-$pkgver"

  python setup.py install --prefix=/usr --root="$pkgdir" --optimize=1 \
    --skip-build
}

