# This file is part of BlackArch Linux ( https://www.blackarch.org/ ).
# See COPYING for license details.

pkgbase=python-plumbum
pkgname=('python2-plumbum' 'python-plumbum')
_pkgname=plumbum
pkgver=1.10.0
pkgrel=1
pkgdesc='Shell combinators library.'
arch=('any')
url='https://pypi.org/project/plumbum/#files'
license=('GPL')
makedepends=('python2-setuptools' 'python-setuptools')
options=(!emptydirs)
source=("https://files.pythonhosted.org/packages/source/${_pkgname::1}/$_pkgname/$_pkgname-$pkgver.tar.gz")
sha512sums=('c290ae9bb4475380ae6cc32d801050642d1ef2806df6b1be9e45ed61f511ac5e3a4da2f45104d55b0eb34b6a73c649bab05b8ea9bb84d666adf8d921f8c503e2')

prepare() {
  cp -a "$_pkgname-$pkgver"{,-2}
}

build() {
  cd "$_pkgname-$pkgver-2"

  python2 setup.py build

  cd "$srcdir/$_pkgname-$pkgver"

  python setup.py build
}

package_python2-plumbum() {
  depends=('python2' 'python2-six')

  cd "$_pkgname-$pkgver-2"

  python2 setup.py install --root="$pkgdir" --prefix=/usr -O1 --skip-build
}

package_python-plumbum() {
  depends=('python' 'python-six')

  cd "$_pkgname-$pkgver"

  python setup.py install --root="$pkgdir" --prefix=/usr -O1 --skip-build
}

