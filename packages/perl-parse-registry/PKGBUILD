# This file is part of BlackArch Linux ( https://www.blackarch.org/ ).
# See COPYING for license details.

pkgname=perl-parse-registry
pkgver=1.1
pkgrel=3
pkgdesc="Parse::Win32Registry is a module for parsing Windows Registry files, allowing you to read the keys and values of a registry file without going through the Windows API."
arch=('any')
url="https://metacpan.org/pod/Parse::Win32Registry"
license=('GPL' 'PerlArtistic')
depends=(perl)
options=('!emptydirs' purge)
_dist="Parse-Win32Registry"
source=("https://cpan.metacpan.org/authors/id/J/JM/JMACFARLA/$_dist-$pkgver.tar.gz")
md5sums=('cdd75a02719f2baa649b044bd86a9b95')


build() {
  cd "$srcdir/$_dist-$pkgver"

  unset PERL5LIB PERL_MM_OPT PERL_LOCAL_LIB_ROOT
  export PERL_MM_USE_DEFAULT=1 PERL_AUTOINSTALL=--skipdeps \
    PERL_MM_OPT="INSTALLDIRS=vendor DESTDIR='$pkgdir'" \
    PERL_MB_OPT="--installdirs vendor --destdir '$pkgdir'" \
    MODULEBUILDRC=/dev/null

  /usr/bin/perl Makefile.PL
  make
}

check() {
  cd "$srcdir/$_dist-$pkgver"

  unset PERL5LIB PERL_MM_OPT PERL_LOCAL_LIB_ROOT
  export PERL_MM_USE_DEFAULT=1

  #make test
}

package() {
  cd "$srcdir/$_dist-$pkgver"

  unset INSTALLDIRS DESTDIR PERL5LIB PERL_MM_OPT PERL_LOCAL_LIB_ROOT

  make install INSTALLDIRS=vendor DESTDIR="$pkgdir"
}
