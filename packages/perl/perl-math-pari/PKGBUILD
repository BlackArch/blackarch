# This file is part of BlackArch Linux ( https://www.blackarch.org/ ).
# See COPYING for license details.

pkgname=perl-math-pari
_pkgname=Math-Pari
pkgver=2.030518
pkgrel=2
epoch=4
pkgdesc='Perl interface to PARI.'
arch=('x86_64' 'aarch64')
url='https://metacpan.org/pod/Math::Pari'
license=('GPL Artistic')
depends=('perl')
options=(!emptydirs)
source=("https://cpan.metacpan.org/authors/id/I/IL/ILYAZ/modules/$_pkgname-$pkgver.zip")
sha512sums=('9bb5d789b5789b38c1d2f07b400f394f8b94872321e9b4de8177e0546fde5f13c277a268f707fbd4c35b336d5f1f86c62744e8a8d0a7d54acece89f1125a89a2')
_distdir="$_pkgname-$pkgver"

build() {
  ( export PERL_MM_USE_DEFAULT=1 PERL5LIB=""                 \
    PERL_AUTOINSTALL=--skipdeps                            \
    PERL_MM_OPT="INSTALLDIRS=vendor DESTDIR='$pkgdir'"     \
    PERL_MB_OPT="--installdirs vendor --destdir '$pkgdir'" \
    MODULEBUILDRC=/dev/null

    cd $_distdir

    /usr/bin/perl Makefile.PL

    make
  )
}

package() {
  cd $_distdir

  make install

  find "$pkgdir" -name .packlist -o -name perllocal.pod -delete
}

