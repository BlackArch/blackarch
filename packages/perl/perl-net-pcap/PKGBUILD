# This file is part of BlackArch Linux ( https://www.blackarch.org/ ).
# See COPYING for license details.

pkgname=perl-net-pcap
_realname=Net-Pcap
pkgver=0.18
pkgrel=6
pkgdesc='Perl/CPAN Module Net::Pcap.'
arch=('x86_64' 'aarch64')
url='https://metacpan.org/release/Net-Pcap'
license=('GPL' 'PerlArtistic')
options=('!emptydirs')
source=("http://cpan.metacpan.org/authors/id/S/SA/SAPER/$_realname-$pkgver.tar.gz"
        'stubs.inc.patch')
depends=('perl' 'libpcap')
sha512sums=('63cc216345eb65b1e76efa657623c9d444d20f4b54ab0c75e787483baaa4f1de7e3a9294967aa7eccbdca04c60b089194dd7e0d19838f899b332c5881a8c7f6d'
            'd1085b6495e0864bf4cff7a22ed0607f81f87e52a918a5dc54da3a451838a92e904ebd71946b0d2d57cea8b999bfb559c3c85a0601f2da424b406d51f552ebbe')

prepare() {
  patch -p1 < stubs.inc.patch
}

build() {
  cd "$_realname-$pkgver"

  PERL_MM_USE_DEFAULT=1 perl Makefile.PL INSTALLDIRS=vendor

  make
}

package() {
  cd "$_realname-$pkgver"

  make DESTDIR="$pkgdir" install

  find $pkgdir -name perllocal.pod -delete
  find $pkgdir -name .packlist -delete
}

