# This file is part of BlackArch Linux ( https://www.blackarch.org/ ).
# See COPYING for license details.
# Originally from the AUR.

pkgname=perl-io-socket-ip
_pkgname=IO-Socket-IP
pkgver=0.39
pkgrel=5
pkgdesc='Family-neutral IP socket supporting both IPv4 and IPv6.'
arch=('any')
license=('PerlArtistic' 'GPL')
options=('!emptydirs')
depends=('perl>=0')
url='http://search.cpan.org/dist/IO-Socket-IP'
source=("http://search.cpan.org/CPAN/authors/id/P/PE/PEVANS/$_pkgname-$pkgver.tar.gz")
sha512sums=('8bc700ef0b4ae42887289947490354e807a47547b9f2473b194db25948c3b552485b63c4b4c7e0262d07195556a5d9cb71796e5eceb2b890af551c99967b910a')

build() {
  cd "$_pkgname-$pkgver"

  ( export PERL_MM_USE_DEFAULT=1 PERL5LIB=""                 \
      PERL_AUTOINSTALL=--skipdeps                            \
      PERL_MM_OPT="INSTALLDIRS=vendor DESTDIR='$pkgdir'"     \
      PERL_MB_OPT="--installdirs vendor --destdir '$pkgdir'" \
      MODULEBUILDRC=/dev/null

    /usr/bin/perl Makefile.PL

    make
  )
}

package() {
  cd "$_pkgname-$pkgver"

  make install

  find "$pkgdir" -name .packlist -o -name perllocal.pod -delete
}

