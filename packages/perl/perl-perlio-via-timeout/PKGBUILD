# This file is part of BlackArch Linux ( https://www.blackarch.org/ ).
# See COPYING for license details.

pkgname=perl-perlio-via-timeout
_pkgname=PerlIO-via-Timeout
pkgver=0.32
pkgrel=3
pkgdesc='A PerlIO layer that adds read & write timeout to a handle.'
arch=('any')
license=('PerlArtistic' 'GPL')
options=('!emptydirs')
depends=('perl')
makedepends=('perl-module-build-tiny>=0.039')
checkdepends=('perl-test-tcp')
url='https://metacpan.org/release/PerlIO-via-Timeout'
source=("http://search.cpan.org/CPAN/authors/id/D/DA/DAMS/$_pkgname-$pkgver.tar.gz")
sha512sums=('52200c0d19da7a19b9b328473b0e7b57a1a3102fd3ace947af8fef8f670aaebe37811daaa2e2b6cf2f6397855a4cfb2424af053322146a8acefd71e1bb30f681')
_distdir="PerlIO-via-Timeout-${pkgver}"

build() {
  ( export PERL_MM_USE_DEFAULT=1 PERL5LIB=""                 \
      PERL_AUTOINSTALL=--skipdeps                            \
      PERL_MM_OPT="INSTALLDIRS=vendor DESTDIR='$pkgdir'"     \
      PERL_MB_OPT="--installdirs vendor --destdir '$pkgdir'" \
      MODULEBUILDRC=/dev/null

    cd $_distdir

    /usr/bin/perl Makefile.PL

    make
  )
}

check() {
  cd $_distdir

  ( export PERL_MM_USE_DEFAULT=1 PERL5LIB=""
    make test
  )
}

package() {
  cd $_distdir

  make install

  find "$pkgdir" -name .packlist -o -name perllocal.pod -delete
}

