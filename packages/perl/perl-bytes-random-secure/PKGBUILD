# This file is part of BlackArch Linux ( https://www.blackarch.org/ ).
# See COPYING for license details.

pkgname=perl-bytes-random-secure
pkgver=0.29
pkgrel=6
pkgdesc='Perl extension to generate cryptographically-secure random bytes.'
_dist=Bytes-Random-Secure
arch=('any')
url="https://metacpan.org/release/$_dist"
license=('GPL' 'PerlArtistic')
depends=('perl' 'perl-math-random-isaac' 'perl-crypt-random-seed')
options=('!emptydirs' 'purge')
source=("http://search.cpan.org/CPAN/authors/id/D/DA/DAVIDO/$_dist-$pkgver.tar.gz")
sha512sums=('72faf9e9fff1cc9641845d47b8dd1efb39861b4015246b169167d4f6050998e91d30b53d6a3e08daf91a838fcf29a05042073064204270de05b2f2ca9990fe64')

build() {
  cd "$_dist-$pkgver"

  unset PERL5LIB PERL_MM_OPT PERL_LOCAL_LIB_ROOT

  export PERL_MM_USE_DEFAULT=1 PERL_AUTOINSTALL=--skipdeps

  /usr/bin/perl Makefile.PL

  make
}

package() {
  cd "$_dist-$pkgver"

  unset PERL5LIB PERL_MM_OPT PERL_LOCAL_LIB_ROOT

  make install INSTALLDIRS=vendor DESTDIR="$pkgdir"
}

