# This file is part of BlackArch Linux ( https://www.blackarch.org/ ).
# See COPYING for license details.

pkgname=outguess
pkgver=0.4
pkgrel=1
pkgdesc='A universal steganographic tool.'
groups=('blackarch' 'blackarch-crypto' 'blackarch-misc')
arch=('x86_64' 'aarch64')
url="https://github.com/resurrecting-open-source-projects/outguess"
license=('BSD')
depends=('bash')
source=("$url/archive/refs/tags/$pkgver.tar.gz")
sha512sums=('9ed932ca6badd870242f1af7667f0e9cf910de8dddac8bf71534c05ae2056bc46f78ef5fedf1faf448cb16e8b410e19c63f68b93a4f24cd419f42857780e54c7')

build() {
  cd "$pkgname-$pkgver"

  ./autogen.sh
  ./configure --prefix=/usr --mandir=/usr/share/man --with-generic-jconfig
  make
}

package() {
  cd "$pkgname-$pkgver"

  make DESTDIR="$pkgdir" install

  install -Dm 644 LICENSE -t "$pkgdir/usr/share/licenses/$pkgname/"
  install -Dm 644 -t "$pkgdir/usr/share/doc/$pkgname" README.md ChangeLog
}

