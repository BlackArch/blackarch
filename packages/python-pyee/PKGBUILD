# This file is part of BlackArch Linux ( https://www.blackarch.org/ ).
# See COPYING for license details.

pkgbase=python-pyee
pkgname=('python2-pyee' 'python-pyee')
_pkgname=pyee
pkgver=6.0.0
pkgrel=1
pkgdesc='A port of node.js EventEmitter to python.'
arch=('any')
url='https://pypi.org/project/pyee/#files'
license=('MIT')
makedepends=('python2-setuptools' 'python2-pytest-runner' 'python-setuptools'
			 'python-pytest-runner' 'python-pytest-asyncio'
             'python-vcversioner')
source=("https://files.pythonhosted.org/packages/1f/8e/ec9a9d07bcf316ebe8ef2c30b5c597988f5b8d324f8273c307843e63f49e/$_pkgname-$pkgver.tar.gz")
sha512sums=('31d52a88b8bcb6d31d8898ead7994d42daf866e601aad98919a537a6e416a037d978cdc689c20e305c27d4807e885d6a85d9b429df38156d02d54c2cd53a550c')

prepare() {
  cp -a "$_pkgname-$pkgver"{,-2}
}

build() {
  cd "$_pkgname-$pkgver-2"

  python2 setup.py build

  cd "$srcdir/$_pkgname-$pkgver"

  python setup.py build
}

package_python2-pyee() {
  depends=('python2')

  cd "$_pkgname-$pkgver-2"

  python2 setup.py install --prefix=/usr --root="$pkgdir" -O1 --skip-build
}

package_python-pyee() {
  depends=('python')

  cd "$_pkgname-$pkgver"

  python setup.py install --prefix=/usr --root="$pkgdir" -O1 --skip-build
}

