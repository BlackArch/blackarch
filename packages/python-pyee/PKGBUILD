# This file is part of BlackArch Linux ( https://www.blackarch.org/ ).
# See COPYING for license details.

pkgbase=python-pyee
pkgname=('python2-pyee' 'python-pyee')
_pkgname=pyee
pkgver=7.0.3
pkgrel=1
pkgdesc='A port of node.js EventEmitter to python.'
arch=('any')
url='https://pypi.org/project/pyee/#files'
license=('MIT')
makedepends=('python2-setuptools' 'python2-pytest-runner' 'python-setuptools'
			 'python-pytest-runner' 'python-pytest-asyncio'
             'python-vcversioner')
source=("https://files.pythonhosted.org/packages/03/b1/95414fbe485cead3836b4638f6df51a2ca5a4292d83f9c0b97628c17e291/$_pkgname-$pkgver.tar.gz")
sha512sums=('fddc29d94f7a53bfbca5b32da5e2845c5d1f489e83b37f589179de83a18a6d3561a6ad36baaa1ed2286995ae04aa1adb3356db280042808f33b8482fbe12d662')

prepare() {
  cp -a "$_pkgname-$pkgver"{,-2}
}

build() {
  cd "$_pkgname-$pkgver-2"

  python2 setup.py build

  cd "$srcdir/$_pkgname-$pkgver"

  python setup.py build
}

package_python2-pyee() {
  depends=('python2')

  cd "$_pkgname-$pkgver-2"

  python2 setup.py install --prefix=/usr --root="$pkgdir" -O1 --skip-build
}

package_python-pyee() {
  depends=('python')

  cd "$_pkgname-$pkgver"

  python setup.py install --prefix=/usr --root="$pkgdir" -O1 --skip-build
}

