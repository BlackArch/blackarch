# This file is part of BlackArch Linux ( https://www.blackarch.org/ ).
# See COPYING for license details.

pkgbase=python-pyee
pkgname=('python2-pyee' 'python-pyee')
_pkgname=pyee
pkgver=9.0.4
pkgrel=1
pkgdesc='A port of node.js EventEmitter to python.'
arch=('any')
url='https://pypi.org/project/pyee/#files'
license=('MIT')
makedepends=('python2-setuptools' 'python2-pytest-runner' 'python-setuptools'
			 'python-pytest-runner' 'python-pytest-asyncio'
             'python-vcversioner')
source=("https://files.pythonhosted.org/packages/99/d0/32803671d5d9dc032c766ad6c0716db98fa9b2c6ad9ec544f04849e9d3c7/$_pkgname-$pkgver.tar.gz")
sha512sums=('87e85a5d2cb81f6d14e0eda2e9a72d300eee18379fb317e9f1ccc6a8231c5cc2f6fe930fea93e05405c15878da136be76855cddaf019e426c1e19f3fdf1d82b9')

prepare() {
  cp -a "$_pkgname-$pkgver"{,-2}
}

build() {
  cd "$_pkgname-$pkgver-2"

  python2 setup.py build

  cd "$srcdir/$_pkgname-$pkgver"

  python setup.py build
}

package_python2-pyee() {
  depends=('python2')

  cd "$_pkgname-$pkgver-2"

  python2 setup.py install --prefix=/usr --root="$pkgdir" -O1 --skip-build
}

package_python-pyee() {
  depends=('python')

  cd "$_pkgname-$pkgver"

  python setup.py install --prefix=/usr --root="$pkgdir" -O1 --skip-build
}

