# This file is part of BlackArch Linux ( https://www.blackarch.org/ ).
# See COPYING for license details.

pkgbase=python-pyee
pkgname=('python2-pyee' 'python-pyee')
_pkgname=pyee
pkgver=9.0.3
pkgrel=1
pkgdesc='A port of node.js EventEmitter to python.'
arch=('any')
url='https://pypi.org/project/pyee/#files'
license=('MIT')
makedepends=('python2-setuptools' 'python2-pytest-runner' 'python-setuptools'
			 'python-pytest-runner' 'python-pytest-asyncio'
             'python-vcversioner')
source=("https://files.pythonhosted.org/packages/2c/09/e870d17ccfd8d4ee31fdf870c9c901eb5ad01f5279e7a9e92cbd907abebb/$_pkgname-$pkgver.tar.gz")
sha512sums=('eb605e75841788070ea1248ecb2b58b541c764eac267f419910e2b4804bae57ea75f269fc3d3827e0fa35c3929d115cbda6b7ea472b965ad83e84b63e73561b6')

prepare() {
  cp -a "$_pkgname-$pkgver"{,-2}
}

build() {
  cd "$_pkgname-$pkgver-2"

  python2 setup.py build

  cd "$srcdir/$_pkgname-$pkgver"

  python setup.py build
}

package_python2-pyee() {
  depends=('python2')

  cd "$_pkgname-$pkgver-2"

  python2 setup.py install --prefix=/usr --root="$pkgdir" -O1 --skip-build
}

package_python-pyee() {
  depends=('python')

  cd "$_pkgname-$pkgver"

  python setup.py install --prefix=/usr --root="$pkgdir" -O1 --skip-build
}

