# This file is part of BlackArch Linux ( http://blackarch.org ).
# See COPYING for license details.

pkgname='wpscan'
pkgver=2011.a78a13b
pkgrel=1
epoch=4
groups=('blackarch' 'blackarch-scanner' 'blackarch-webapp' 'blackarch-fuzzer')
pkgdesc='A vulnerability scanner which checks the security of WordPress installations using a black box approach.'
arch=('any')
url='http://wpscan.org'
license=('GPL3')
makedepends=('git' 'unzip')
depends=('ruby' 'libxslt' 'ruby-bundler')
install='wpscan.install'
options=(!strip)
source=('git+http://github.com/wpscanteam/wpscan')
sha1sums=('SKIP')

pkgver() {
  cd "$srcdir/wpscan"

  echo $(git rev-list --count HEAD).$(git rev-parse --short HEAD)

}

prepare() {
  cd "$srcdir/wpscan"

  unzip -f data.zip > /dev/null
  rm data.zip
}

package() {
  cd "$srcdir/wpscan"

  mkdir -p "$pkgdir/usr/bin"
  mkdir -p "$pkgdir/usr/share/wpscan"

  cp -a * "$pkgdir/usr/share/wpscan"

  cat > "$pkgdir/usr/bin/wpscan" << EOF
#!/bin/sh
cd /usr/share/wpscan
exec ruby wpscan.rb "\$@"
EOF

  sed -i "s/ROOT_DIR, 'log.txt'/ENV['HOME'], 'wpscan.log'/" "$pkgdir/usr/share/wpscan/lib/common/common_helper.rb"

  sed -i "s/ROOT_DIR, 'cache'/ENV['HOME'], '.wpscan-cache'/" "$pkgdir/usr/share/wpscan/lib/common/common_helper.rb"

  chmod +x "$pkgdir/usr/bin/wpscan"
}
