pkgname=wpscan
pkgver=1146.f1ec85a
pkgrel=2
epoch=4
groups=('blackarch' 'blackarch-scanner' 'blackarch-webapp')
pkgdesc='A vulnerability scanner which checks the security of WordPress installations using a black box approach'
arch=('i686' 'x86_64' 'armv6h' 'armv7h')
url="http://wpscan.org"
license=('GPL3')
makedepends=('git' 'rubygems' 'libxslt' 'ruby-typhoeus' 'ruby-nokogiri' 'ruby-terminal-table' 'ruby-progressbar' 'ruby-webmock' 'ruby-simplecov' 'ruby-rspec' 'ruby-rspec-mocks')
optdepends=('bundle')
options=(!strip)
source=('git+http://github.com/wpscanteam/wpscan')
md5sums=('SKIP')

pkgver() {
  cd wpscan
  echo $(git rev-list --count HEAD).$(git rev-parse --short HEAD)
}

package() {
  cd "$srcdir/wpscan"

  install -dm755 "$pkgdir/usr/share/wpscan"
  install -dm755 "$pkgdir/usr/bin"

  cp -a * "$pkgdir/usr/share/wpscan"

  cat > "$pkgdir/usr/bin/wpscan" <<EOF
#!/bin/sh
ruby /usr/share/wpscan/wpscan.rb "\$@"
EOF
  sed -i "s/ROOT_DIR + '\/log.txt/'\/home\/' + ENV['USER'] + '\/wpscan.log/" "$pkgdir/usr/share/wpscan/lib/common/common_helper.rb"
  sed -i "s/ROOT_DIR + '\/cache/'\/home\/' + ENV['USER'] + '\/cache/" "$pkgdir/usr/share/wpscan/lib/common/common_helper.rb"


  chmod +x "$pkgdir/usr/bin/wpscan"
}
