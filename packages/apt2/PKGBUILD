# This file is part of BlackArch Linux ( http://blackarch.org ).
# See COPYING for license details.

pkgname=apt2
pkgver=175.6732505
pkgrel=2
pkgdesc='Automated penetration toolkit.'
groups=('blackarch' 'blackarch-automation')
arch=('any')
url='https://github.com/MooseDojo/apt2'
license=('custom:unknown')
depends=('python2' 'python2-argparse' 'python2-yattag' 'python2-scapy')
makedepends=('git')
source=('git+https://github.com/MooseDojo/apt2.git')
sha512sums=('SKIP')

pkgver() {
  cd $pkgname

  echo $(git rev-list --count HEAD).$(git rev-parse --short HEAD)
}

package() {
  cd $pkgname

  install -dm 755 "$pkgdir/usr/bin"
  install -dm 755 "$pkgdir/usr/share/$pkgname"

  install -Dm644 -t "$pkgdir/usr/share/doc/$pkgname/" README.md TODO.txt
  install -Dm644 LICENSE.txt "$pkgdir/usr/share/licenses/$pkgname/LICENSE.txt"

  rm README.md TODO.txt

  cp -a * "$pkgdir/usr/share/$pkgname"

  cat > "$pkgdir/usr/bin/$pkgname" << EOF
#!/bin/sh
cd /usr/share/apt2
exec python2 apt2.py "\$@"
EOF

  chmod a+x "$pkgdir/usr/bin/$pkgname"
}

