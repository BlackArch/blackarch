# This file is part of BlackArch Linux ( https://www.blackarch.org/ ).
# See COPYING for license details.

pkgname=stegoveritas
_pkgname=stegoVeritas
pkgver=1.9
pkgrel=2
pkgdesc='Automatic image steganography analysis tool.'
arch=('any')
groups=('blackarch' 'blackarch-stego')
url='https://github.com/bannsec/stegoVeritas/'
license=('MIT')
depends=('python' 'python-pillow' 'python-numpy' 'python-magic' 'python-pypng'
         'python-prettytable' 'python-exifread' 'python-xmp-toolkit' 'binwalk'
         'python-apng' 'perl-image-exiftool' 'p7zip' 'foremost' 'exempi'
         'python-stegoveritas-pfp' 'python-distro' 'python-pyexiftool'
         'steghide')
makedepends=('python-setuptools')
source=("https://github.com/bannsec/stegoVeritas/archive/$pkgver.tar.gz"
        'setup.py.patch')
sha512sums=('b6070f6a25762d0757ba4be502491dbd357c86f399ead951e3a7cc072cdd3e204aa79b062bc6ebf6261a42f34676bcbc545eab3726f13725b1c58fcc54c72fcc'
            '32c01327eb03c958e348edfac7cfdc0ae04ad4b8792c794a87a0d868634d43ae6ea3b7aed9536623dbb70918cd69eea87c87dc4a472d9beb58bbd1aae1d70e95')

prepare() {
  cd "$_pkgname-$pkgver"

  patch -p0 < ../setup.py.patch
}

build() {
  cd "$_pkgname-$pkgver"

  python setup.py build
}

package() {
  cd "$_pkgname-$pkgver"

  python setup.py install --root="$pkgdir" --prefix=/usr -O1 --skip-build
}

