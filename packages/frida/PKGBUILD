# This file is part of BlackArch Linux ( https://www.blackarch.org/ ).
# See COPYING for license details.

pkgbase=frida
pkgname=('python2-frida' 'python-frida')
_pkgname=frida
pkgver=12.8.8
pkgrel=1
pkgdesc='Dynamic instrumentation toolkit for developers, reverse-engineers, and security researchers.'
groups=('blackarch' 'blackarch-reversing' 'blackarch-binary')
arch=('x86_64' 'aarch64')
url='https://pypi.org/project/frida/#files'
makedepends=('python-setuptools' 'python2-setuptools')
license=('custom:unknown')
source=("https://files.pythonhosted.org/packages/c4/1c/2a4fced21c20d47979ccd26e2b4e84ae0c9d701a06c998697381d5d2e44b/$pkgbase-$pkgver.tar.gz"
        'https://raw.githubusercontent.com/frida/frida/master/COPYING')
sha512sums=('2ce6df4370efc65a98613de0a29cd592c156940cc9c814b17fa0c92d9b0d3342c73a7593aceb940b5a93ad7d4e48c6132f638f3d3c0dd5d9b9ef182ab4db5fcf'
            'e36c8d5a0538fa4bb3575a60322057f7a687aab04c13deed25399462ba0be74ce6071cc41d967e23b9a96e552462d210280427a945c6cc956919b062878efc9a')

prepare() {
  cp -a "$pkgbase-$pkgver"{,-py2}
}

build() {
  cd "$pkgbase-$pkgver-py2"

  python2 setup.py build

  cd "$srcdir/$pkgbase-$pkgver"

  python setup.py build
}

package_python2-frida() {
  depends=('python2' 'python2-colorama' 'python2-pygments'
           'python2-prompt_toolkit')

  cd "$pkgbase-$pkgver-py2"

  python2 setup.py install --root="$pkgdir" --optimize=1 --skip-build

  install -Dm 644 "$srcdir/COPYING" \
    "$pkgdir/usr/share/licenses/$pkgname/LICENSE"
}

package_python-frida() {
  depends=('python' 'python-colorama' 'python-pygments' 'python-prompt_toolkit')

  cd "$pkgbase-$pkgver"

  python setup.py install --root="$pkgdir" --optimize=1 --skip-build

  install -Dm 644 "$srcdir/COPYING" \
    "$pkgdir/usr/share/licenses/$pkgname/LICENSE"
}

