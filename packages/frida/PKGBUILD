# This file is part of BlackArch Linux ( https://www.blackarch.org/ ).
# See COPYING for license details.

pkgbase=frida
pkgname=('python2-frida' 'python-frida')
_pkgname=frida
pkgver=12.7.4
pkgrel=1
pkgdesc='Dynamic instrumentation toolkit for developers, reverse-engineers, and security researchers.'
groups=('blackarch' 'blackarch-reversing' 'blackarch-binary')
arch=('x86_64' 'aarch64')
url='https://pypi.org/project/frida/#files'
makedepends=('python-setuptools' 'python2-setuptools')
license=('custom:unknown')
source=("https://files.pythonhosted.org/packages/12/45/792340fa52796b709f270f43e12c2ffcaaa3dd1827da903f8dc437fae878/$pkgbase-$pkgver.tar.gz"
        'https://raw.githubusercontent.com/frida/frida/master/COPYING')
sha512sums=('14316ae2870d5978172f9c65dba3eac793000d88b2524666851b01d06bec76b27db66d006012e984b20e6368f208678583c2b28d43eb43fa2d76210f21015807'
            'e36c8d5a0538fa4bb3575a60322057f7a687aab04c13deed25399462ba0be74ce6071cc41d967e23b9a96e552462d210280427a945c6cc956919b062878efc9a')

prepare() {
  cp -a $pkgbase-$pkgver{,-py2}
}

build() {
  cd "$pkgbase-$pkgver-py2"

  python2 setup.py build

  cd "$srcdir/$pkgbase-$pkgver"

  python setup.py build
}

package_python2-frida() {
  depends=('python2' 'python2-colorama' 'python2-pygments'
           'python2-prompt_toolkit')

  cd "$pkgbase-$pkgver-py2"

  python2 setup.py install --root="$pkgdir" --optimize=1 --skip-build

  install -Dm 644 "$srcdir/COPYING" \
    "$pkgdir/usr/share/licenses/$pkgname/LICENSE"
}

package_python-frida() {
  depends=('python' 'python-colorama' 'python-pygments' 'python-prompt_toolkit')

  cd "$pkgbase-$pkgver"

  python setup.py install --root="$pkgdir" --optimize=1 --skip-build

  install -Dm 644 "$srcdir/COPYING" \
    "$pkgdir/usr/share/licenses/$pkgname/LICENSE"
}

