# This file is part of BlackArch Linux ( https://www.blackarch.org/ ).
# See COPYING for license details.

pkgbase=frida
pkgname=('python2-frida' 'python-frida')
_pkgname=frida
pkgver=12.11.10
pkgrel=1
pkgdesc='Dynamic instrumentation toolkit for developers, reverse-engineers, and security researchers.'
groups=('blackarch' 'blackarch-reversing' 'blackarch-binary')
arch=('x86_64' 'aarch64')
url='https://pypi.org/project/frida/#files'
makedepends=('python-setuptools' 'python2-setuptools')
license=('custom:unknown')
source=("https://files.pythonhosted.org/packages/be/e7/2332b7d404487802d7d6fcdb19ee0c503f1f6c652e178d49b48e4af390df/$pkgbase-$pkgver.tar.gz"
        'https://raw.githubusercontent.com/frida/frida/master/COPYING')
sha512sums=('1f83114884711d1200b38361f40a669206d608117f9215c514e58bf3f24979f00074eb4f0ae94fc1f81206319dc4627029e95a49a15cf5c863dbac68cebe464b'
            'e36c8d5a0538fa4bb3575a60322057f7a687aab04c13deed25399462ba0be74ce6071cc41d967e23b9a96e552462d210280427a945c6cc956919b062878efc9a')

prepare() {
  cp -a "$pkgbase-$pkgver"{,-2}
}

build() {
  cd "$pkgbase-$pkgver-2"

  python2 setup.py build

  cd "$srcdir/$pkgbase-$pkgver"

  python setup.py build
}

package_python2-frida() {
  depends=('python2' 'python2-colorama' 'python2-pygments'
           'python2-prompt_toolkit')

  cd "$pkgbase-$pkgver-2"

  python2 setup.py install --root="$pkgdir" --prefix=/usr -O1 --skip-build

  install -Dm 644 "$srcdir/COPYING" \
    "$pkgdir/usr/share/licenses/$pkgname/LICENSE"
}

package_python-frida() {
  depends=('python' 'python-colorama' 'python-pygments' 'python-prompt_toolkit')

  cd "$pkgbase-$pkgver"

  python setup.py install --root="$pkgdir" --prefix=/usr -O1 --skip-build

  install -Dm 644 "$srcdir/COPYING" \
    "$pkgdir/usr/share/licenses/$pkgname/LICENSE"
}

