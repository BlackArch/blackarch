# This file is part of BlackArch Linux ( https://www.blackarch.org/ ).
# See COPYING for license details.

pkgbase=frida
pkgname=('python2-frida' 'python-frida')
_pkgname=frida
pkgver=12.8.11
pkgrel=1
pkgdesc='Dynamic instrumentation toolkit for developers, reverse-engineers, and security researchers.'
groups=('blackarch' 'blackarch-reversing' 'blackarch-binary')
arch=('x86_64' 'aarch64')
url='https://pypi.org/project/frida/#files'
makedepends=('python-setuptools' 'python2-setuptools')
license=('custom:unknown')
source=("https://files.pythonhosted.org/packages/63/ec/df3fcf52d893c1562be4073363d760d06d157fddeaad7dd526e169e19748/$pkgbase-$pkgver.tar.gz"
        'https://raw.githubusercontent.com/frida/frida/master/COPYING')
sha512sums=('fc5b7706c84060889951d4368e5ea720009ca7364e384c91f302168a2fd41c29e983cfd43096080837559121006fb9aef6c973386c639f1e31ea19b552b48de1'
            'e36c8d5a0538fa4bb3575a60322057f7a687aab04c13deed25399462ba0be74ce6071cc41d967e23b9a96e552462d210280427a945c6cc956919b062878efc9a')

prepare() {
  cp -a "$pkgbase-$pkgver"{,-2}
}

build() {
  cd "$pkgbase-$pkgver-2"

  python2 setup.py build

  cd "$srcdir/$pkgbase-$pkgver"

  python setup.py build
}

package_python2-frida() {
  depends=('python2' 'python2-colorama' 'python2-pygments'
           'python2-prompt_toolkit')

  cd "$pkgbase-$pkgver-2"

  python2 setup.py install --root="$pkgdir" --prefix=/usr -O1 --skip-build

  install -Dm 644 "$srcdir/COPYING" \
    "$pkgdir/usr/share/licenses/$pkgname/LICENSE"
}

package_python-frida() {
  depends=('python' 'python-colorama' 'python-pygments' 'python-prompt_toolkit')

  cd "$pkgbase-$pkgver"

  python setup.py install --root="$pkgdir" --prefix=/usr -O1 --skip-build

  install -Dm 644 "$srcdir/COPYING" \
    "$pkgdir/usr/share/licenses/$pkgname/LICENSE"
}

