# This file is part of BlackArch Linux ( https://www.blackarch.org/ ).
# See COPYING for license details.

pkgname=frida
pkgver=12.2.25
pkgrel=1
pkgdesc='Dynamic instrumentation toolkit for developers, reverse-engineers, and security researchers.'
groups=('blackarch' 'blackarch-reversing' 'blackarch-binary')
arch=('x86_64' 'armv6h' 'armv7h' 'aarch64')
url='https://pypi.org/project/frida/#files'
depends=('python' 'python-colorama' 'python-pygments' 'python-prompt_toolkit')
makedepends=('python-setuptools')
license=('custom:unknown')
source=("https://files.pythonhosted.org/packages/95/e3/cf1d8054764e92543d8fac584eede3252299d7f0416620ae4621654390ac/frida-$pkgver.tar.gz"
        "https://raw.githubusercontent.com/frida/frida/master/COPYING")
sha512sums=('7090fbd87a24e52b642c58125f06c9448019803010fdef740447508689c0994e959cece99f67f85feb82e66bf2cfe0c9b0c9e1d1079ad1998b0507d0283bd61f'
            'e36c8d5a0538fa4bb3575a60322057f7a687aab04c13deed25399462ba0be74ce6071cc41d967e23b9a96e552462d210280427a945c6cc956919b062878efc9a')

build() {
  cd "$pkgname-$pkgver"

  python setup.py build
}

package() {
  cd "$pkgname-$pkgver"

  python setup.py install --root="$pkgdir" --optimize=1 --skip-build

  install -Dm 644 "$srcdir/COPYING" "$pkgdir/usr/share/licenses/$pkgname/LICENSE"
}

