# This file is part of BlackArch Linux ( https://www.blackarch.org/ ).
# See COPYING for license details.

pkgname=frida
pkgver=12.3.2
pkgrel=1
pkgdesc='Dynamic instrumentation toolkit for developers, reverse-engineers, and security researchers.'
groups=('blackarch' 'blackarch-reversing' 'blackarch-binary')
arch=('x86_64' 'armv6h' 'armv7h' 'aarch64')
url='https://pypi.org/project/frida/#files'
depends=('python' 'python-colorama' 'python-pygments' 'python-prompt_toolkit')
makedepends=('python-setuptools')
license=('custom:unknown')
source=("https://files.pythonhosted.org/packages/bf/e9/2d0e8f0fc21800cdc3b146a1089a2c05711fc9c61400da2c877edd51a80d/frida-$pkgver.tar.gz"
        "https://raw.githubusercontent.com/frida/frida/master/COPYING")
sha512sums=('99287a691f8a1d47ffb046f9c7290739dce40abd8d79333b70ddb6a703f0ef451c15fc0aab92172ba8e16fcf4dee7ee81e7091a15db1cfd8e65e7ae7245974e3'
            'e36c8d5a0538fa4bb3575a60322057f7a687aab04c13deed25399462ba0be74ce6071cc41d967e23b9a96e552462d210280427a945c6cc956919b062878efc9a')

build() {
  cd "$pkgname-$pkgver"

  python setup.py build
}

package() {
  cd "$pkgname-$pkgver"

  python setup.py install --root="$pkgdir" --optimize=1 --skip-build

  install -Dm 644 "$srcdir/COPYING" "$pkgdir/usr/share/licenses/$pkgname/LICENSE"
}

