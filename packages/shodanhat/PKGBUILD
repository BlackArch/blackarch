# This file is part of BlackArch Linux ( http://blackarch.org ).
# See COPYING for license details.

pkgname='shodanhat'
pkgver=13.e5e7e68
pkgrel=2
pkgdesc='Search for hosts info with shodan.'
groups=('blackarch' 'blackarch-recon')
arch=('any')
url='https://github.com/HatBashBR/ShodanHat'
license=('MIT')
depends=('python2' 'python2-python-nmap' 'shodan')
makedepends=('git')
source=('git+https://github.com/HatBashBR/ShodanHat.git')
sha1sums=('SKIP')

pkgver() {
  cd "$srcdir/ShodanHat"

  echo $(git rev-list --count HEAD).$(git rev-parse --short HEAD)
}

package() {
  cd "$srcdir/ShodanHat"

  mkdir -p "$pkgdir/usr/bin"
  mkdir -p "$pkgdir/usr/share/shodanhat"

  install -Dm644 -t "$pkgdir/usr/share/doc/shodanhat/" README.md
  install -Dm644 LICENSE "$pkgdir/usr/share/licenses/shodanhat/LICENSE"

  rm README.md LICENSE

  cp -a * "$pkgdir/usr/share/shodanhat/"

  cat > "$pkgdir/usr/bin/shodanhat" << EOF
#!/bin/sh
cd /usr/share/shodanhat
exec python2 shodanhat.py "\${@}"
EOF

  chmod a+x "$pkgdir/usr/bin/shodanhat"
}
