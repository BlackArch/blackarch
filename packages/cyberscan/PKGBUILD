# This file is part of BlackArch Linux ( http://blackarch.org ).
# See COPYING for license details.

pkgname='cyberscan'
pkgver=72.7da9ba4
pkgrel=1
pkgdesc='A Network Pentesting Tool'
groups=('blackarch' 'blackarch-networking' 'blackarch-scanner')
arch=('any')
url='https://github.com/medbenali/CyberScan'
license=('GPL3')
depends=('python2' 'python2-pygeoip' 'python2-argparse' 'python2-asn1crypto'
         'scapy')
makedepends=('git')
source=('git+https://github.com/medbenali/CyberScan')
sha1sums=('SKIP')

pkgver() {
  cd "$srcdir/CyberScan"

  echo $(git rev-list --count HEAD).$(git rev-parse --short HEAD)
}

package() {
  cd "$srcdir/CyberScan"

  mkdir -p "$pkgdir/usr/bin"
  mkdir -p "$pkgdir/usr/share/cyberscan"

  install -Dm644 -t "$pkgdir/usr/share/doc/cyberscan/" README.md
  install -Dm644 LICENSE "$pkgdir/usr/share/licenses/cyberscan/LICENSE"

  rm README.md LICENSE

  cp -a * "$pkgdir/usr/share/cyberscan"

  cat > "$pkgdir/usr/bin/cyberscan" << EOF
#!/bin/sh
cd /usr/share/cyberscan
exec python2 CyberScan.py "\${@}"
EOF

  chmod a+x "$pkgdir/usr/bin/cyberscan"
}
