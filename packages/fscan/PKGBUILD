# This file is part of BlackArch Linux ( https://www.blackarch.org/ ).
# See COPYING for license details.

pkgname=fscan
pkgver=1.8.2.r17.geb5558e
pkgrel=1
pkgdesc='A Security Auditing Tool.'
arch=('x86_64' 'aarch64')
groups=('blackarch' 'blackarch-scanner')
url='https://github.com/shadow1ng/fscan'
license=('MIT')
makedepends=('git' 'go')
source=("git+https://github.com/shadow1ng/$pkgname.git")
sha512sums=('SKIP')

pkgver() {
  cd $pkgname

  git describe --long --tags | sed 's/\([^-]*-g\)/r\1/;s/-/./g'
}

build() {
  cd $pkgname

  GOPATH="$srcdir" go mod download
  GOPATH="$srcdir" go build \
    -trimpath \
    -buildmode=pie \
    -mod=readonly \
    -modcacherw \
    -ldflags "-s -w" \
    -o $pkgname .
}

package() {
  cd $pkgname

  install -Dm 755 $pkgname "$pkgdir/usr/bin/$pkgname"
  install -Dm 644 -t "$pkgdir/usr/share/doc/$pkgname/" *.md
}

