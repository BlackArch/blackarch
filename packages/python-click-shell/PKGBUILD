# This file is part of BlackArch Linux ( https://www.blackarch.org/ ).
# See COPYING for license details.

pkgbase=python-click-shell
pkgname=('python2-click-shell' 'python-click-shell')
_pkgname=click-shell
pkgver=1.0
pkgrel=1
pkgdesc='An extension to click that easily turns your click app into a shell utility.'
arch=('any')
url='https://pypi.org/project/click-shell/#files'
license=('BSD')
makedepends=('python2-setuptools' 'python-setuptools')
source=("https://files.pythonhosted.org/packages/05/41/fd7d054f936f1868016d0c0ab4d134d007c375e44620b31a29efef15b146/$_pkgname-$pkgver.tar.gz")
sha512sums=('531c707135b9d6e38597a0d477e29a6171fa2c61304173df168318db5961d1563a31bc8e26fb444eb97fe729d73e0c1cd3104fa1d8d8e9a90876e9b67082676c')

prepare() {
  cp -a "$_pkgname-$pkgver"{,-2}
}

build() {
  cd "$_pkgname-$pkgver-2"

  python2 setup.py build

  cd "$srcdir/$_pkgname-$pkgver"

  python setup.py build
}

package_python2-click-shell() {
  depends=('python2' 'python2-click')

  cd "$_pkgname-$pkgver-2"

  python2 setup.py install --prefix=/usr --root="$pkgdir" -O1 --skip-build
}

package_python-click-shell() {
  depends=('python' 'python-click')

  cd "$_pkgname-$pkgver"

  python setup.py install --prefix=/usr --root="$pkgdir" -O1 --skip-build
}

