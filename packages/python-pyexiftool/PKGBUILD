# This file is part of BlackArch Linux ( https://www.blackarch.org/ ).
# See COPYING for license details.

pkgbase=python-pyexiftool
pkgname=('python2-pyexiftool' 'python-pyexiftool')
_pkgname=pyexiftool
pkgver=0.2.0
pkgrel=5
pkgdesc="Python library to communicate with an instance of Phil Harvey's excellent ExifTool command-line application."
arch=('any')
url='https://github.com/smarnach/pyexiftool'
license=('GPL' 'BSD')
makedepends=('python2-setuptools' 'python-setuptools')
source=("https://github.com/smarnach/pyexiftool/archive/v$pkgver.tar.gz")
sha512sums=('88595c01fa68f7c4d0b604182b6278a53e6a61832b16dfda306a5bb89e767a3629d030a0f0c6a8566e0cf2df4a449b87d39aa40baea91b4267a85824f61d492a')

prepare() {
  cp -a "$_pkgname-$pkgver"{,-2}
}

build() {
  cd "$_pkgname-$pkgver-2"

  python2 setup.py build

  cd "$srcdir/$_pkgname-$pkgver"

  python setup.py build
}

package_python2-pyexiftool() {
  depends=('python2')

  cd "$_pkgname-$pkgver-2"

  python2 setup.py install --prefix=/usr --root="$pkgdir" --optimize=1 \
    --skip-build
}

package_python-pyexiftool() {
  depends=('python')

  cd "$_pkgname-$pkgver"

  python setup.py install --prefix=/usr --root="$pkgdir" --optimize=1 \
    --skip-build
}

