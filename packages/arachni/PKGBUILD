# This file is part of BlackArch Linux ( http://blackarch.org ).
# See COPYING for license details.

pkgname=arachni
pkgver=1.5.1
pkgrel=3
groups=('blackarch' 'blackarch-webapp')
pkgdesc='A feature-full, modular, high-performance Ruby framework aimed towards helping penetration testers and administrators evaluate the security of web applications.'
arch=('any')
license=('apache')
makedepends=('git')
depends=('ruby2.3' 'ruby2.3-bundler' 'curl' 'phantomjs' 'libxslt' 'libxml2')
url='https://www.arachni-scanner.com'
source=('git+git://github.com/Arachni/arachni.git')
install='arachni.install'
sha512sums=('SKIP')

pkgver() {
  cd arachni

  git describe --always | sed 's|-|.|g' | sed 's/^v//'
}

prepare() {
  cd arachni

  sed -i "s/\('puma',[ ]*\)'2.14.0'/\1'3.10.0'/g" arachni.gemspec
  sed -i "s/\('msgpack',[ ]*\)'0.7.0'/\1'1.1.0'/g" arachni.gemspec
}

package() {
  cd arachni

  progs="arachni arachni_console arachni_multi arachni_reporter arachni_restore"
  progs="$progs arachni_rest_server arachni_rpc arachni_rpcd"
  progs="$progs arachni_rpcd_monitor arachni_script"

  install -dm 755 "$pkgdir/usr/bin"
  install -dm 755 "$pkgdir/usr/share/$pkgname"

  install -Dm 644 -t "$pkgdir/usr/share/doc/$pkgname" *.md
  install -Dm 644 LICENSE.md "$pkgdir/usr/share/licenses/$pkgname/LICENSE.md"

  cp --no-preserve=ownership -a * "$pkgdir/usr/share/$pkgname"

  for prog in $progs
  do
    cat > "$pkgdir/usr/bin/${prog//_/-}" << EOF
#!/bin/sh
cd /usr/share/arachni
exec ruby-2.3 /usr/share/arachni/bin/$prog "\${@}"
EOF
  done

  chmod +x "$pkgdir"/usr/bin/*
}

