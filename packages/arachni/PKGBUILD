# This file is part of BlackArch Linux ( http://blackarch.org ).
# See COPYING for license details.

pkgname='arachni'
pkgver=1.5.1
pkgrel=1
groups=('blackarch' 'blackarch-webapp')
pkgdesc='A feature-full, modular, high-performance Ruby framework aimed towards helping penetration testers and administrators evaluate the security of web applications.'
arch=('any')
license=('apache')
makedepends=('git')
depends=('ruby2.3' 'ruby2.3-bundler')
url='https://www.arachni-scanner.com'
source=('git+git://github.com/Arachni/arachni.git')
install='arachni.install'
sha1sums=('SKIP')

pkgver() {
  cd "$srcdir/arachni"

  git describe --always | sed 's|-|.|g' | sed 's/^v//'
}

package() {
  cd "$srcdir/arachni"

  progs="arachni arachni_console arachni_multi arachni_reporter arachni_restore"
  progs="${progs} arachni_rest_server arachni_rpc arachni_rpcd"
  progs="$progs arachni_rpcd_monitor arachni_script"

  mkdir -p "$pkgdir/usr/bin"
  mkdir -p "$pkgdir/usr/share/arachni"

  install -Dm644 -t "$pkgdir/usr/share/doc/arachni" *.md
  install -Dm644 LICENSE.md "$pkgdir/usr/share/licenses/arachni/LICENSE.md"

  cp --no-preserve=ownership -a * "$pkgdir/usr/share/arachni"

  for prog in ${progs}
  do
    cat > "$pkgdir/usr/bin/${prog//_/-}" << EOF
#!/bin/sh
cd /usr/share/arachni
exec ruby-2.3 /usr/share/arachni/bin/$prog "\$@"
EOF
  done

  chmod +x "$pkgdir"/usr/bin/*
}
