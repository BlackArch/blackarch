# This file is part of BlackArch Linux ( https://www.blackarch.org/ ).
# See COPYING for license details.

pkgname=fakedns
pkgver=101.842dc5d
pkgrel=1
groups=('blackarch' 'blackarch-proxy' 'blackarch-spoof')
pkgdesc='A regular-expression based python MITM DNS server with correct DNS request passthrough and "Not Found" responses.'
arch=('any')
url="https://github.com/Crypt0s/FakeDns"
license=('GPL3')
depends=('python2')
makedepends=('git')
source=('git+https://github.com/Crypt0s/FakeDns.git')
sha512sums=('SKIP')

pkgver() {
  cd FakeDns

  echo $(git rev-list --count HEAD).$(git rev-parse --short HEAD)
}

prepare() {
  cd FakeDns

  sed -i 's|bin/env python|bin/env python2|' *.py
}

package() {
  cd FakeDns

  install -dm 755 "$pkgdir/usr/bin/"
  install -dm 755 "$pkgdir/usr/share/$pkgname"
  install -dm 755 "$pkgdir/usr/share/doc/$pkgname"

  cp -a dns.conf.example fakedns.py "$pkgdir/usr/share/$pkgname/"
  cp -a README.md "$pkgdir/usr/share/doc/$pkgname/"

  chmod +x "$pkgdir/usr/share/$pkgname/fakedns.py"

  ln -s "/usr/share/fakedns/fakedns.py" "$pkgdir/usr/bin/$pkgname"
}

