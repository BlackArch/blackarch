# This file is part of BlackArch Linux ( http://blackarch.org ).
# See COPYING for license details.

pkgname=naft
pkgver=0.0.9
pkgrel=2
groups=('blackarch' 'blackarch-forensic')
pkgdesc='Network Appliance Forensic Toolkit.'
arch=('any')
url='https://blog.didierstevens.com/my-software/#NAFT'
license=('custom:unknown')
depends=('python2' 'python2-yara')
source=("http://didierstevens.com/files/software/NAFT_V${pkgver//./_}.zip")
sha512sums=('fefefebb09a61fbbe7dca4febded14ac7fe4d85db999f7ee7b63cde2111bce09908a01f3990e1d25f0e6703923544db3daf88d7255c12d13516baf4b60b9ef79')

prepare() {
  cd "$srcdir"

  find . -type f -name '*.py' -exec sed -i 's|/env python|/env python2|' {} \;
}

package() {
  cd "$srcdir"

  install -dm 755 "$pkgdir/usr/bin"

  install -Dm755 -t "$pkgdir/usr/share/naft/" *.py
  install -Dm644 readme.pdf "$pkgdir/usr/share/doc/naft/readme.pdf"

  ln -s "/usr/share/naft/naft-gfe.py" "$pkgdir/usr/bin/naft-gfe"
  ln -s "/usr/share/naft/naft-icd.py" "$pkgdir/usr/bin/naft-icd"
  ln -s "/usr/share/naft/naft-ii.py" "$pkgdir/usr/bin/naft-ii"
}
