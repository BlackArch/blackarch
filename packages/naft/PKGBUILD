# This file is part of BlackArch Linux ( http://blackarch.org ).
# See COPYING for license details.

pkgname='naft'
pkgver='0.0.9'
pkgrel=2
groups=('blackarch' 'blackarch-forensic')
pkgdesc='Network Appliance Forensic Toolkit.'
arch=('any')
url='https://blog.didierstevens.com/my-software/#NAFT'
license=('custom:unknown')
depends=('python2' 'python2-yara')
source=("http://didierstevens.com/files/software/NAFT_V${pkgver//./_}.zip")
sha1sums=('ed73727a9599287d546b15b1aa267973be5bbdff')

prepare() {
  cd "$srcdir"

  find . -type f -name '*.py' -exec sed -i 's|/env python|/env python2|' {} \;
}

package() {
  cd "$srcdir"

  mkdir -p "$pkgdir/usr/bin"

  install -Dm755 -t "$pkgdir/usr/share/naft/" *.py
  install -Dm644 readme.pdf "$pkgdir/usr/share/doc/naft/readme.pdf"

  ln -s "/usr/share/naft/naft-gfe.py" "$pkgdir/usr/bin/naft-gfe"
  ln -s "/usr/share/naft/naft-icd.py" "$pkgdir/usr/bin/naft-icd"
  ln -s "/usr/share/naft/naft-ii.py" "$pkgdir/usr/bin/naft-ii"
}
