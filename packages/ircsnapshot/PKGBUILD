# This file is part of BlackArch Linux ( http://blackarch.org ).
# See COPYING for license details.

pkgname=ircsnapshot
pkgver=94.cb02a85
pkgrel=2
groups=('blackarch' 'blackarch-recon' 'blackarch-scanner')
pkgdesc='Tool to gather information from IRC servers.'
arch=('any')
url='https://github.com/bwall/ircsnapshot'
license=('MIT')
depends=('python2')
makedepends=('git')
source=('git+https://github.com/bwall/ircsnapshot.git')
sha512sums=('SKIP')

pkgver() {
  cd "$srcdir/ircsnapshot"

  echo $(git rev-list --count HEAD).$(git rev-parse --short HEAD)
}

prepare() {
  cd "$srcdir/ircsnapshot"

  sed -i 's|/usr/bin/env python|/usr/bin/env python2|g' \
    ircsnapshot/ircsnapshot.py
}

package() {
  cd "$srcdir/ircsnapshot"

  install -Dm755 ircsnapshot/ircsnapshot.py "$pkgdir/usr/bin/ircsnapshot"
  install -Dm744 README.md "$pkgdir/usr/share/doc/ircsnapshot/README.md"
  install -Dm744 LICENSE.TXT "$pkgdir/usr/share/licenses/ircsnapshot/LICENSE.TXT"
}
