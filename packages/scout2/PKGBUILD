# This file is part of BlackArch Linux ( http://blackarch.org ).
# See COPYING for license details.

pkgname='scout2'
pkgver=1147.15d20ce
pkgrel=1
pkgdesc='Security auditing tool for AWS environments.'
groups=('blackarch' 'blackarch-scanner' 'blackarch-fuzzer')
url='http://isecpartners.github.io/Scout2/'
arch=('any')
license=('GPL2')
depends=('python2' 'python2-boto3' 'python2-dateutil' 'python2-netaddr'
         'python2-opinel' 'python2-botocore' 'python2-jmespath'
         'python2-iampoliciesgonewild')
makedepends=('git')
source=('git+https://github.com/iSECPartners/Scout2.git')
sha1sums=('SKIP')

pkgver() {
  cd "$srcdir/Scout2"

  echo $(git rev-list --count HEAD).$(git rev-parse --short HEAD)
}

package() {
  cd "$srcdir/Scout2"

  mkdir -p "$pkgdir/usr/bin/"
  mkdir -p "$pkgdir/usr/share/scout2"

  install -Dm644 -t "$pkgdir/usr/share/doc/scout2/" README.rst
  install -Dm644 LICENSE "$pkgdir/usr/share/licenses/scout2/LICENSE"
  install -Dm644 Scout2.py "$pkgdir/usr/share/scout2/Scout2.py"

  rm LICENSE README.rst

  cp -a * "$pkgdir/usr/share/scout2"

  cat > "$pkgdir/usr/bin/scout2" << EOF
#!/bin/sh
cd /usr/share/scout2
exec python2 Scout2.py "\${@}"
EOF

  chmod +x "$pkgdir/usr/bin/scout2"
}
