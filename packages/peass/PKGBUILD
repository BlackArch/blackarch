# This file is part of BlackArch Linux ( https://www.blackarch.org/ ).
# See COPYING for license details.

pkgname=peass
pkgver=216.e57666a
pkgrel=2
pkgdesc='Privilege Escalation Awesome Scripts SUITE (with colors).'
arch=('any')
groups=('blackarch' 'blackarch-scanner')
url='https://github.com/carlospolop/privilege-escalation-awesome-scripts-suite'
license=('MIT')
depends=('bash')
makedepends=('git')
optdepends=('fping' 'nmap' 'ping' 'gnu-netcat')
source=("$pkgname::git+https://github.com/carlospolop/privilege-escalation-awesome-scripts-suite.git")
sha512sums=('SKIP')

pkgver() {
  cd $pkgname

  echo $(git rev-list --count HEAD).$(git rev-parse --short HEAD)
}

package() {
  cd $pkgname

  install -dm 755 "$pkgdir/usr/bin"
  install -dm 755 "$pkgdir/usr/share/$pkgname"

  install -Dm 644 -t "$pkgdir/usr/share/doc/$pkgname/" README.md
  install -Dm 644 -t "$pkgdir/usr/share/licenses/$pkgname/" LICENSE
  install -Dm 644 linPEAS/README.md \
    "$pkgdir/usr/share/doc/$pkgname/README_linPEAS.md"
  install -Dm 644 winPEAS/README.md \
    "$pkgdir/usr/share/doc/$pkgname/README_winPEAS.md"

  rm README.md LICENSE .gitignore linPEAS/README.md winPEAS/README.md

  cp -a --no-preserve=ownership * "$pkgdir/usr/share/$pkgname/"

  ln -sf /usr/share/peass/linPEAS/linpeas.sh "$pkgdir/usr/bin/linpeas"
}

