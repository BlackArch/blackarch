# This file is part of BlackArch Linux ( https://www.blackarch.org/ ).
# See COPYING for license details.

pkgbase=python-jinja-time
pkgname=('python2-jinja-time' 'python-jinja-time')
_pkgname=jinja2-time
pkgver=0.2.0
pkgrel=3
pkgdesc='Jinja2 Extension for Dates and Times.'
arch=('any')
url='https://pypi.org/project/jinja2-time/#files'
license=('MIT')
makedepends=('python2-setuptools' 'python-setuptools')
source=("https://files.pythonhosted.org/packages/de/7c/ee2f2014a2a0616ad3328e58e7dac879251babdb4cb796d770b5d32c469f/$_pkgname-$pkgver.tar.gz")
sha512sums=('8ac4686312cde689f7a431001833d5d477030c4f5e9470d949bd07a40fcb9521f6ddcde37a72468093121ff9451e352a0bb43d20a796d7a204cf94e8a7545f2c')

prepare() {
  cp -a "$_pkgname-$pkgver"{,-2}
}

build() {
  cd "$_pkgname-$pkgver-2"

  python2 setup.py build

  cd "$srcdir/$_pkgname-$pkgver"

  python setup.py  build
}

package_python2-jinja-time() {
  depends=('python2' 'python2-jinja' 'python2-arrow')

  cd "$_pkgname-$pkgver-2"

  python2 setup.py install --prefix=/usr --root="$pkgdir" --optimize=1 \
    --skip-build
}

package_python-jinja-time() {
  depends=('python' 'python-jinja' 'python-arrow')

  cd "$_pkgname-$pkgver"

  python setup.py install --prefix=/usr --root="$pkgdir" --optimize=1 \
    --skip-build
}

