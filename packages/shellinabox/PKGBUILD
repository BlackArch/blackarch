# This file is part of BlackArch Linux ( http://blackarch.org ).
# See COPYING for license details.

pkgname='shellinabox'
pkgver=428.98e6eeb
pkgrel=1
pkgdesc='Implements a web server that can export arbitrary command line tools to a web based terminal emulator.'
groups=('blackarch' 'blackarch-backdoor' 'blackarch-webapp')
arch=('x86_64' 'armv6h' 'armv7h' 'aarch64')
url='https://github.com/shellinabox/shellinabox'
license=('GPL2')
depends=('openssl' 'zlib')
makedepends=('git' 'autoconf')
source=('git+https://github.com/shellinabox/shellinabox.git')
sha1sums=('SKIP')

pkgver() {
  cd "$srcdir/shellinabox"

  echo $(git rev-list --count HEAD).$(git rev-parse --short HEAD)
}

build() {
  cd "$srcdir/shellinabox"

  unset CPPFLAGS
  autoreconf -i
  ./configure --prefix=/usr

  make
}

package() {
  cd "$srcdir/shellinabox"

  make DESTDIR="$pkgdir" install
}
