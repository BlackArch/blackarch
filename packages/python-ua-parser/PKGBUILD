# This file is part of BlackArch Linux ( https://www.blackarch.org/ ).
# See COPYING for license details.

pkgbase=python-ua-parser
pkgname=('python2-ua-parser' 'python-ua-parser')
_pkgname=ua-parser
pkgver=0.15.0
pkgrel=1
pkgdesc="Python port of Browserscope's user agent parser."
url='https://pypi.org/project/ua-parser/#files'
license=('APACHE')
arch=('any')
makedepends=('python2-setuptools' 'python-setuptools')
source=("https://files.pythonhosted.org/packages/52/82/c2ba0235f7aacc1f25cf1dd8051c08a379f07d799d9d89091b7faeae5200/$_pkgname-$pkgver.tar.gz")
sha512sums=('30f00406f1919f76774f15403ea430627d8b4348923003bb2209cefee5bcf0744c0359dee3fe573624a9030211ad806c25d367fbd4c6a3d6068756cac1927b2a')

prepare() {
  cp -a "$_pkgname-$pkgver"{,-2}
}

build() {
  cd "$_pkgname-$pkgver-2"

  python2 setup.py build

  cd "$srcdir/$_pkgname-$pkgver"

  python setup.py build
}

package_python2-ua-parser() {
  depends=('python2' 'python2-yaml')

  cd "$_pkgname-$pkgver-2"

  python2 setup.py install --prefix=/usr --root="$pkgdir" --optimize=1 \
    --skip-build

  find "$pkgdir" -type f -name '*.py' | xargs sed -i \
    's|#!/usr/bin/python2.5|#!/usr/bin/python2|'
  find "$pkgdir" -type f -name '*.py' | xargs sed -i \
    's|#!/usr/bin/env python|#!/usr/bin/env python2|'
}

package_python-ua-parser() {
  depends=('python' 'python-yaml')

  cd "$_pkgname-$pkgver"

  python setup.py install --prefix=/usr --root="$pkgdir" --optimize=1 \
    --skip-build

  find "$pkgdir" -type f -name '*.py' | xargs sed -i \
    's|#!/usr/bin/python2.5|#!/usr/bin/python|'
  find "$pkgdir" -type f -name '*.py' | xargs sed -i \
    's|#!/usr/bin/env python|#!/usr/bin/env python|'
}

