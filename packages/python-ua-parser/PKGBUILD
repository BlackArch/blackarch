# This file is part of BlackArch Linux ( https://www.blackarch.org/ ).
# See COPYING for license details.

pkgbase=python-ua-parser
pkgname=('python2-ua-parser' 'python-ua-parser')
_pkgname=ua-parser
pkgver=0.10.0
pkgrel=2
pkgdesc="Python port of Browserscope's user agent parser."
url='https://pypi.org/project/ua-parser/#files'
license=('APACHE')
arch=('any')
makedepends=('python2-setuptools' 'python-setuptools')
source=("https://files.pythonhosted.org/packages/92/68/b5c60fc7386d95de9d66a42b9a9d4898d74de895368964b198003042e297/$_pkgname-$pkgver.tar.gz")
sha512sums=('5fe1b1c5f0c990b351d31542b1975e34750de265257f10f69b9158392c3934605bc39948bd56fa97757dae314524bc4ceb2d71dfada55ff7625bec6685f71552')

prepare() {
  cp -a "$_pkgname-$pkgver"{,-2}
}

build() {
  cd "$_pkgname-$pkgver-2"

  python2 setup.py build

  cd "$srcdir/$_pkgname-$pkgver"

  python setup.py build
}

package_python2-ua-parser() {
  depends=('python2' 'python2-yaml')

  cd "$_pkgname-$pkgver-2"

  python2 setup.py install --prefix=/usr --root="$pkgdir" --optimize=1 \
    --skip-build

  find "$pkgdir" -type f -name '*.py' | xargs sed -i \
    's|#!/usr/bin/python2.5|#!/usr/bin/python2|'
  find "$pkgdir" -type f -name '*.py' | xargs sed -i \
    's|#!/usr/bin/env python|#!/usr/bin/env python2|'
}

package_python-ua-parser() {
  depends=('python' 'python-yaml')

  cd "$_pkgname-$pkgver"

  python setup.py install --prefix=/usr --root="$pkgdir" --optimize=1 \
    --skip-build

  find "$pkgdir" -type f -name '*.py' | xargs sed -i \
    's|#!/usr/bin/python2.5|#!/usr/bin/python|'
  find "$pkgdir" -type f -name '*.py' | xargs sed -i \
    's|#!/usr/bin/env python|#!/usr/bin/env python|'
}

