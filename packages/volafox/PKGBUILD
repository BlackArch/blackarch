# This file is part of BlackArch Linux ( http://blackarch.org ).
# See COPYING for license details.

pkgname='volafox'
pkgver=143.5b42987
pkgrel=1
groups=('blackarch' 'blackarch-forensic' 'blackarch-binary')
pkgdesc='Mac OS X Memory Analysis Toolkit.'
arch=('any')
url='https://github.com/n0fate/volafox'
license=('GPL2')
depends=('python2')
makedepends=('git')
source=('git+https://github.com/n0fate/volafox.git')
sha1sums=('SKIP')

pkgver() {
  cd "$srcdir/volafox"

  echo $(git rev-list --count HEAD).$(git rev-parse --short HEAD)
}

package() {
  cd "$srcdir/volafox"

  mkdir -p "$pkgdir/usr/bin"
  mkdir -p "$pkgdir/usr/share/volafox"

  install -Dm644 -t "$pkgdir/usr/share/doc/volafox" README.md
  install -Dm644 LICENSE "$pkgdir/usr/share/licenses/volafox/LICENSE"

  rm README.md LICENSE

  cp -a --no-preserve=ownership * "$pkgdir/usr/share/volafox"

  cat > "$pkgdir/usr/bin/volafox" << EOF
#!/usr/bin/env bash
cd /usr/share/volafox
exec python2 vol.py "\${@}"
EOF

  chmod a+x "$pkgdir/usr/bin/volafox"
}
