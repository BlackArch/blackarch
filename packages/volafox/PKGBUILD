# This file is part of BlackArch Linux ( https://www.blackarch.org/ ).
# See COPYING for license details.

pkgname=volafox
pkgver=143.5b42987
pkgrel=1
groups=('blackarch' 'blackarch-forensic' 'blackarch-binary')
pkgdesc='Mac OS X Memory Analysis Toolkit.'
arch=('any')
url='https://github.com/n0fate/volafox'
license=('GPL2')
depends=('python2')
makedepends=('git')
source=("git+https://github.com/n0fate/$pkgname.git")
sha512sums=('SKIP')

pkgver() {
  cd $pkgname

  echo $(git rev-list --count HEAD).$(git rev-parse --short HEAD)
}

package() {
  cd $pkgname

  install -dm 755 "$pkgdir/usr/bin"
  install -dm 755 "$pkgdir/usr/share/$pkgname"

  install -Dm 644 -t "$pkgdir/usr/share/doc/$pkgname/" README.md
  install -Dm 644 LICENSE "$pkgdir/usr/share/licenses/$pkgname/LICENSE"

  rm README.md LICENSE

  cp -a --no-preserve=ownership * "$pkgdir/usr/share/$pkgname/"

  cat > "$pkgdir/usr/bin/$pkgname" << EOF
#!/bin/sh
cd /usr/share/$pkgname
exec python2 vol.py "\$@"
EOF

  chmod +x "$pkgdir/usr/bin/$pkgname"
}

