# This file is part of BlackArch Linux ( https://www.blackarch.org/ ).
# See COPYING for license details.

pkgname=pasv-agrsv
pkgver=57.6bb54f7
pkgrel=1
groups=('blackarch' 'blackarch-automation' 'blackarch-recon')
pkgdesc='Passive recon / OSINT automation script.'
arch=('any')
url='https://github.com/isaudits/pasv-agrsv'
license=('GPL3')
depends=('python2' 'python2-dnspython' 'python2-elixir' 'python2-sqlalchemy')
makedepends=('git')
source=("git+https://github.com/isaudits/$pkgname.git")
sha512sums=('SKIP')

pkgver() {
  cd $pkgname

  echo $(git rev-list --count HEAD).$(git rev-parse --short HEAD)
}

package() {
  cd $pkgname

  install -dm 755 "$pkgdir/usr/bin"
  install -dm 755 "$pkgdir/usr/share/$pkgname"

  install -Dm 644 -t "$pkgdir/usr/share/doc/$pkgname/" README.md
  install -Dm 644 LICENSE "$pkgdir/usr/share/licenses/$pkgname/LICENSE"

  rm README.md LICENSE

  cp -a * "$pkgdir/usr/share/$pkgname/"

  cat > "$pkgdir/usr/bin/$pkgname" << EOF
#!/bin/sh
cd /usr/share/$pkgname
exec python2 $pkgname.py "\$@"
EOF

  chmod +x "$pkgdir/usr/bin/$pkgname"
}

