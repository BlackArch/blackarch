# This file is part of BlackArch Linux ( http://blackarch.org ).
# See COPYING for license details.

pkgname='pasv-agrsv'
pkgver=56.70822a2
pkgrel=2
groups=('blackarch' 'blackarch-automation' 'blackarch-recon')
pkgdesc='Passive recon / OSINT automation script.'
arch=('any')
url='https://github.com/isaudits/pasv-agrsv'
license=('GPL3')
depends=('python2' 'python2-dnspython' 'python2-elixir' 'python2-sqlalchemy')
makedepends=('git')
source=('git+https://github.com/isaudits/pasv-agrsv.git')
sha1sums=('SKIP')

pkgver() {
  cd "$srcdir/pasv-agrsv"

  echo $(git rev-list --count HEAD).$(git rev-parse --short HEAD)
}

package() {
  cd "$srcdir/pasv-agrsv"

  mkdir -p "$pkgdir/usr/bin"
  mkdir -p "$pkgdir/usr/share/pasv-agrsv"

  install -Dm644 -t "$pkgdir/usr/share/doc/pasv-agrsv/" README.md
  install -Dm644 LICENSE "$pkgdir/usr/share/licenses/pasv-agrsv/LICENSE"

  rm README.md LICENSE

  cp -a * "$pkgdir/usr/share/pasv-agrsv"

  cat > "$pkgdir/usr/bin/pasv-agrsv" << EOF
#!/bin/sh
cd /usr/share/pasv-agrsv
exec python2 pasv-agrsv.py "\$@"
EOF

  chmod a+x "$pkgdir/usr/bin/pasv-agrsv"
}
