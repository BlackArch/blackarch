pkgname=nipper
pkgver=0.11.7
pkgrel=3
groups=('blackarch' 'blackarch-networking' 'blackarch-recon')
pkgdesc="Network Infrastructure Parser"
arch=('i686' 'x86_64' 'armv6h' 'armv7h')
url="https://www.titania-security.com/"
license=('GPL3')
source=("http://packetstorm.foofus.com/cisco/nipper-$pkgver.tgz")
md5sums=('cc6e500d2cefef2322ad8b4a1102aae1')

build(){
  cd "$srcdir/nipper-$pkgver"
  make
}

package() {
  cd "$srcdir/nipper-$pkgver"

  # Base directories.
  install -dm755 "$pkgdir/usr/share/licenses/nipper"
  install -dm755 "$pkgdir/usr/share/nipper"
  install -dm755 "$pkgdir/usr/share/man"
  install -dm755 "$pkgdir/usr/share/man/man1"
  install -dm755 "$pkgdir/usr/share/man/man5"
  install -dm755 "$pkgdir/usr/bin/"
  install -dm755 "$pkgdir/etc/"

  # License.
  install -m644 LICENSE "$pkgdir/usr/share/licenses/nipper"

  # Man.
  install -m644 man/nipper.1 "$pkgdir/usr/share/man/man1"
  install -m644 man/nipper.conf.5 "$pkgdir/usr/share/man/man5"

  # Docs.
  cp -a --no-preserve=ownership CSS Changelog FW1 IOS NMP PIX Passport \
    Readme ScreenOS SonicOS Template cisco common docs \
    "$pkgdir/usr/share/nipper/"

  # Bin.
  install -Dm755 nipper "$pkgdir/usr/bin/nipper"
  install -m644 nipper.conf "$pkgdir/etc/"
}
