# This file is part of BlackArch Linux ( https://www.blackarch.org/ ).
# See COPYING for license details.

pkgname=nsearch
pkgver=353.bd8205b
pkgrel=3
pkgdesc='Minimal script to help find script into the nse database.'
groups=('blackarch' 'blackarch-misc')
arch=('any')
url='https://github.com/JKO/nsearch'
license=('APACHE')
depends=('python2' 'python2-python-nmap' 'python2-pysqlite' 'python2-yaml'
         'python2-python-i18n')
makedepends=('git')
source=("git+https://github.com/JKO/$pkgname.git")
sha512sums=('SKIP')

pkgver() {
  cd $pkgname

  echo $(git rev-list --count HEAD).$(git rev-parse --short HEAD)
}

package() {
  cd $pkgname

  install -dm 755 "$pkgdir/usr/bin"
  install -dm 755 "$pkgdir/usr/share/$pkgname"

  install -Dm 644 -t "$pkgdir/usr/share/doc/$pkgname/" README.md
  install -Dm 644 LICENSE "$pkgdir/usr/share/licenses/$pkgname/LICENSE"

  rm README.md LICENSE

  cp -a * "$pkgdir/usr/share/$pkgname/"

  cp -a "$pkgdir/usr/share/$pkgname/config.yaml.example" \
    "$pkgdir/usr/share/$pkgname/config.yaml"

  cat > "$pkgdir/usr/bin/$pkgname" << EOF
#!/bin/sh
cd /usr/share/$pkgname
exec python2 $pkgname.py "\$@"
EOF

  chmod +x "$pkgdir/usr/bin/$pkgname"
}

