# This file is part of BlackArch Linux ( http://blackarch.org ).
# See COPYING for license details.

pkgname=nsearch
pkgver=353.bd8205b
pkgrel=2
pkgdesc='Minimal script to help find script into the nse database.'
groups=('blackarch' 'blackarch-misc')
arch=('any')
url='https://github.com/JKO/nsearch'
license=('Apache2')
depends=('python2' 'python2-python-nmap' 'python2-pysqlite' 'python2-yaml'
         'python2-python-i18n')
makedepends=('git')
source=('git+https://github.com/JKO/nsearch.git')
sha512sums=('SKIP')

pkgver() {
  cd nsearch

  echo $(git rev-list --count HEAD).$(git rev-parse --short HEAD)
}

package() {
  cd nsearch

  install -dm 755 "$pkgdir/usr/bin"
  install -dm 755 "$pkgdir/usr/share/nsearch"

  install -Dm644 -t "$pkgdir/usr/share/doc/nsearch/" README.md
  install -Dm644 LICENSE "$pkgdir/usr/share/licenses/nsearch/LICENSE"

  rm README.md LICENSE

  cp -a * "$pkgdir/usr/share/nsearch/"

  cp -a "$pkgdir/usr/share/nsearch/config.yaml.example" \
    "$pkgdir/usr/share/nsearch/config.yaml"

  cat > "$pkgdir/usr/bin/nsearch" << EOF
#!/bin/sh
cd /usr/share/nsearch
exec python2 nsearch.py "\${@}"
EOF

  chmod a+x "$pkgdir/usr/bin/nsearch"
}

