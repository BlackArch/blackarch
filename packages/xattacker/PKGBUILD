# This file is part of BlackArch Linux ( http://blackarch.org ).
# See COPYING for license details.

pkgname=xattacker
pkgver=116.5c8b08c
pkgrel=1
pkgdesc='Website Vulnerability Scanner & Auto Exploiter.'
arch=('any')
groups=('blackarch' 'blackarch-webapp' 'blackarch-scanner'
        'blackarch-exploitation')
url='https://github.com/Moham3dRiahi/XAttacker'
license=('custom:unknown')
depends=('perl' 'perl-lwp-protocol-https' 'perl-uri')
makedepends=('git')
source=("$pkgname::git+https://github.com/Moham3dRiahi/XAttacker.git")
sha512sums=('SKIP')

pkgver() {
  cd $pkgname

  echo $(git rev-list --count HEAD).$(git rev-parse --short HEAD)
}

package() {
  cd $pkgname

  install -dm 755 "$pkgdir/usr/bin"
  install -dm 755 "$pkgdir/usr/share/$pkgname"

  install -Dm 644 -t "$pkgdir/usr/share/doc/$pkgname/" README.md

  cp -a --no-preserve=owner * "$pkgdir/usr/share/$pkgname/"

  cat > "$pkgdir/usr/bin/$pkgname" << EOF
#!/bin/sh
cd /usr/share/$pkgname
exec perl XAttacker.pl "\$@"
EOF

  chmod +x "$pkgdir/usr/bin/$pkgname"
}

