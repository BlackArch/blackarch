# This file is part of BlackArch Linux ( http://blackarch.org ).
# See COPYING for license details.

pkgname=tlspretense
pkgver=0.7.0
pkgrel=1
pkgdesc='SSL/TLS client testing framework.'
groups=('blackarch' 'blackarch-crypto' 'blackarch-scanner')
arch=('any')
url='https://github.com/iSECPartners/tlspretense'
license=('MIT')
depends=('ruby' 'ruby-ruby-termios' 'ruby-eventmachine')
makedepends=('rubygems')
options=(!emptydirs)
source=("https://rubygems.org/downloads/tlspretense-${pkgver}.gem")
noextract=("tlspretense-${pkgver}.gem")
sha512sums=('29b0e7f416c12aa13991110194c1f1dfb47ddcb6b346ab90f81ebdc65aadc54affc53a20c06610cf53e8fd4c1573e99fd8900772aa3eb659dd52ec6eb8234fe8')

package() {
  cd "$srcdir"

  local _gemdir="$(ruby -e'puts Gem.default_dir')"

  gem install --ignore-dependencies --no-user-install \
    -i "$pkgdir/$_gemdir" -n "$pkgdir/usr/bin" "tlspretense-${pkgver}.gem"

  rm "$pkgdir/$_gemdir/cache/tlspretense-${pkgver}.gem"

  install -Dm644 "$pkgdir/$_gemdir/gems/tlspretense-$pkgver/LICENSE.txt" \
    "$pkgdir/usr/share/licenses/tlspretense/LICENSE.txt"
}
