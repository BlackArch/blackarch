# This file is part of BlackArch Linux ( http://blackarch.org ).
# See COPYING for license details.

pkgname='arachni'
pkgver=1.5.1.4.g8ee5bbe15
pkgrel=1
groups=('blackarch' 'blackarch-webapp')
pkgdesc='A feature-full, modular, high-performance Ruby framework aimed towards helping penetration testers and administrators evaluate the security of web applications.'
arch=('i686' 'x86_64' 'armv6h' 'armv7h' 'aarch64')
license=('apache')
makedepends=('git')
depends=('ruby' 'ruby-bundler' 'libxml2' 'libxslt')
source=('git+git://github.com/Arachni/arachni.git#branch=experimental'
        'setup_fix.patch')
url='https://www.arachni-scanner.com'
sha1sums=('SKIP'
          '5c0ed7ee946271d42747121d108a73ac5de377c3')

pkgver() {
  cd "$srcdir/arachni"

  git describe --always | sed 's|-|.|g' | sed 's/^v//'
}

package() {
  cd "$srcdir/arachni"

  progs="arachni arachni_console arachni_multi arachni_reporter arachni_restore"
  progs="${progs} arachni_rest_server arachni_rpc arachni_rpcd"
  progs="$progs arachni_rpcd_monitor arachni_script"

  mkdir -p "$pkgdir/usr/bin"
  mkdir -p "$pkgdir/usr/share/arachni"

  install -Dm644 -t "$pkgdir/usr/share/doc/arachni" *.md
  install -Dm644 LICENSE.md "$pkgdir/usr/share/licenses/arachni/LICENSE.md"

  patch -Np1 -i ../setup_fix.patch

  bundle config build.nokogiri --use-system-libraries
  bundle install --path vendor/bundle --without prof
  cp --no-preserve=ownership -a * "$pkgdir/usr/share/arachni"
  cp --no-preserve=ownership -a .bundle "$pkgdir/usr/share/arachni"

  for prog in ${progs}
  do
    cat > "$pkgdir/usr/bin/${prog//_/-}" << EOF
#!/bin/sh
cd /usr/share/arachni
exec ruby /usr/share/arachni/bin/$prog "\$@"
EOF
  done

  chmod +x "$pkgdir"/usr/bin/*
}


