# This file is part of BlackArch Linux ( https://www.blackarch.org/ ).
# See COPYING for license details.

pkgname=visualize-logs
pkgver=118.d2e370e
pkgrel=3
pkgdesc='A Python library and command line tools to provide interactive log visualization.'
arch=('x86_64')
groups=('blackarch' 'blackarch-misc')
url='https://github.com/keithjjones/visualize_logs'
license=('custom:unknown')
depends=('graphviz' 'jupyter-nbformat' 'python' 'python-networkx'
         'python-pandas' 'python-plotly' 'python-pydotplus'
         'python-setuptools')
makedepends=('git' 'python' 'python-setuptools')
source=("$pkgname::git+https://github.com/keithjjones/visualize_logs.git")
sha512sums=('SKIP')

pkgver() {
  cd $pkgname

  echo $(git rev-list --count HEAD).$(git rev-parse --short HEAD)
}

build() {
  cd $pkgname

  python setup.py build
}

package() {
  cd $pkgname

  python setup.py install --root="$pkgdir" --prefix=/usr -O1 --skip-build
}

