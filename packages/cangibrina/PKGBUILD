# This file is part of BlackArch Linux ( http://blackarch.org ).
# See COPYING for license details.

pkgname=cangibrina
pkgver=121.d3da43f
pkgrel=1
groups=('blackarch' 'blackarch-scanner' 'blackarch-webapp')
pkgdesc='Dashboard Finder.'
arch=('any')
url='https://github.com/fnk0c/cangibrina'
license=('GPL2')
depends=('python2' 'python2-beautifulsoup4' 'python2-pysocks'
         'python2-mechanize' 'python2-html5lib')
optdepends=('tor' 'nmap')
makedepends=('git')
source=('git+https://github.com/fnk0c/cangibrina.git')
sha512sums=('SKIP')

pkgver() {
  cd "$srcdir/cangibrina"

  echo $(git rev-list --count HEAD).$(git rev-parse --short HEAD)
}

package() {
  cd "$srcdir/cangibrina"

  install -dm 755 "$pkgdir/usr/bin"
  install -dm 755 "$pkgdir/usr/share/cangibrina"

  install -Dm644 README.md "$pkgdir/usr/share/doc/cangibrina/README.md"
  install -Dm644 LICENSE "$pkgdir/usr/share/licenses/cangibrina/LICENSE"

  rm README.md LICENSE

  cp -a * "$pkgdir/usr/share/cangibrina"

  cat > "$pkgdir/usr/bin/cangibrina" << EOF
#!/bin/sh
cd /usr/share/cangibrina
exec python2 cangibrina.py "\$@"
EOF

  chmod a+x "$pkgdir/usr/bin/cangibrina"
}
