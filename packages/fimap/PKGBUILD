# This file is part of BlackArch Linux ( https://www.blackarch.org/ ).
# See COPYING for license details.

pkgname=fimap
pkgver=230.41eca15
pkgrel=1
epoch=2
pkgdesc='A little tool for local and remote file inclusion auditing and exploitation.'
arch=('any')
groups=('blackarch' 'blackarch-webapp' 'blackarch-exploitation'
        'blackarch-fuzzer')
url='https://gitlab.tha-imax.de/root/fimap'
license=('GPL-2.0-or-later')
depends=('python2' 'python-urllib3' 'python2-httplib2')
makedepends=('git')
options=('!emptydirs')
source=("git+https://gitlab.tha-imax.de/root/$pkgname.git")
sha512sums=('SKIP')

pkgver() {
  cd $pkgname

  ( set -o pipefail
    git describe --long --tags --abbrev=7 2>/dev/null |
      sed 's/\([^-]*-g\)/r\1/;s/-/./g' ||
    printf "%s.%s" "$(git rev-list --count HEAD)" \
      "$(git rev-parse --short=7 HEAD)"
  )
}

prepare() {
  cd $pkgname

  grep -rl 'python$' . | xargs sed -i 's|python$|python2|'
}

package() {
  cd "$pkgname/src"

  install -dm 755 "$pkgdir/usr/bin"
  install -dm 755 "$pkgdir/usr/share/$pkgname"

  cp --no-preserve=ownership -a * "$pkgdir/usr/share/fimap/"

  cat > "$pkgdir/usr/bin/$pkgname" << EOF
#!/bin/sh
exec python2 /usr/share/$pkgname/$pkgname.py "\$@"
EOF

  chmod +x "$pkgdir/usr/bin/$pkgname"
}

