# This file is part of BlackArch Linux ( http://blackarch.org ).
# See COPYING for license details.

pkgname='fimap'
pkgver='1.00'
pkgrel=5
epoch=2
groups=('blackarch' 'blackarch-exploitation' 'blackarch-fuzzer')
pkgdesc="A little tool for local and remote file inclusion auditing and exploitation"
arch=('any')
url='http://code.google.com/p/fimap/'
license=('GPL2')
depends=('python2' 'python-urllib3' 'python2-httplib2')
makedepends=('subversion')
options=('!emptydirs')
source=("svn+http://fimap.googlecode.com/svn/trunk/")
sha1sums=('SKIP')

prepare() {
  # Fix python shebang lines.
  grep -rl 'python$' $srcdir/trunk/src | xargs sed -i 's|python$|python2|'
}

package() {
  cd $srcdir/trunk/src

  # Base directories.
  mkdir -p $pkgdir/usr/share/fimap
  mkdir -p $pkgdir/usr/bin

  cp --no-preserve=ownership -R * $pkgdir/usr/share/fimap

  # Make joint script.
  cat > $pkgdir/usr/bin/fimap <<EOF
#!/bin/sh
exec python2 /usr/share/fimap/fimap.py "\$@"
EOF
  chmod +x $pkgdir/usr/bin/fimap
}
