# This file is part of BlackArch Linux ( https://www.blackarch.org/ ).
# See COPYING for license details.

pkgbase=python-libarchive
pkgname=('python2-libarchive' 'python-libarchive')
_pkgname=libarchive
pkgver=0.4.6
pkgrel=3
pkgdesc='Python adapter for universal, libarchive-based archive access.'
arch=('any')
url='https://pypi.python.org/pypi/libarchive/'
license=('GPL2')
makedepends=('python2-setuptools' 'python-setuptools')
source=("https://files.pythonhosted.org/packages/b8/89/2952eefad82d9361f539de16ea85a2d98a7fb68eae2ba9ab0869aa2cee0f/$_pkgname-$pkgver.tar.gz")
sha512sums=('df77b4fd43a272938f55fa13fc391c5eccdaa6ffc14abcc9a2212357054074b818880f7f60c2ed24c9acfc7e4ee087a0680f2ad95c7b8fce725c8c5ef7ac86f5')

prepare() {
  cp -a "$_pkgname-$pkgver"{,-2}
}

build() {
  cd "$_pkgname-$pkgver-2"

  python2 setup.py build

  cd "$srcdir/$_pkgname-$pkgver"

  python setup.py build
}

package_python2-libarchive() {
  depends=('python2' 'python2-nose')

  cd "$_pkgname-$pkgver-2"

  python2 setup.py install --prefix=/usr --root="$pkgdir" --optimize=1 \
    --skip-build
}

package_python-libarchive() {
  depends=('python' 'python-nose')

  cd "$_pkgname-$pkgver"

  python setup.py install --prefix=/usr --root="$pkgdir" --optimize=1 \
    --skip-build
}

