# This file is part of BlackArch Linux ( https://www.blackarch.org/ ).
# See COPYING for license details.

pkgname=obfs4proxy
pkgver=0.0.11
pkgrel=2
pkgdesc='A pluggable transport proxy written in Go.'
groups=('blackarch' 'blackarch-proxy' 'blackarch-networking')
arch=('x86_64' 'aarch64')
url='https://gitweb.torproject.org/pluggable-transports/obfs4.git'
license=('BSD')
depends=('glibc')
makedepends=('git' 'go-pie')
optdepends=('tor')
source=("git+https://git.torproject.org/pluggable-transports/obfs4.git#tag=obfs4proxy-$pkgver")
sha512sums=('SKIP')

build() {
  cd "obfs4/$pkgname"

  GOPATH="$srcdir" go get -d
  GOPATH="$srcdir" go build -o $pkgname
}

package() {
  cd obfs4

  install -Dm 755 "$pkgname/$pkgname" "$pkgdir/usr/bin/$pkgname"
  install -Dm 644 "doc/$pkgname.1" "$pkgdir/usr/share/man/man1/$pkgname.1"
  install -Dm 644 -t "$pkgdir/usr/share/doc/$pkgname/ChangeLog" ChangeLog
  install -Dm 644 LICENSE "$pkgdir/usr/share/licenses/$pkgname/LICENSE"
}

