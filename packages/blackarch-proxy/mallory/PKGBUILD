# This file is part of BlackArch Linux ( https://www.blackarch.org/ ).
# See COPYING for license details.

pkgname=mallory
pkgver=132.8db35b9
pkgrel=1
groups=('blackarch' 'blackarch-proxy' 'blackarch-networking')
pkgdesc='HTTP/HTTPS proxy over SSH.'
arch=('x86_64' 'aarch64')
url='https://github.com/justmao945/mallory'
license=('MIT')
depends=('glibc')
makedepends=('git' 'go-pie')
source=("git+https://github.com/justmao945/$pkgname.git")
sha512sums=('SKIP')
_url="github.com/justmao945/$pkgname"

pkgver() {
  cd $pkgname

  echo $(git rev-list --count HEAD).$(git rev-parse --short HEAD)
}

build() {
  mkdir -p "src/$_url" && rm -rf "src/$_url" && mv $pkgname "src/$_url" &&
    cd "src/$_url"

  GOPATH="$srcdir" go get -d -t "$_url/cmd/$pkgname"
}

package() {
  cd "src/$_url"

  install -dm 755 "$pkgdir/usr/bin"

  GOPATH="$srcdir" go build -v "$_url/cmd/$pkgname"

  install -Dm 755 $pkgname "$pkgdir/usr/bin/$pkgname"
  install -Dm 644 -t "$pkgdir/usr/share/doc/$pkgname/" README.md
  install -Dm 644 LICENSE "$pkgdir/usr/share/licenses/$pkgname/LICENSE"
}

