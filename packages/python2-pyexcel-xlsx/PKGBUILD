# This file is part of BlackArch Linux ( https://www.blackarch.org/ ).
# See COPYING for license details.

pkgname=python2-pyexcel-xlsx
pkgver=0.5.7
pkgrel=2
pkgdesc='A wrapper library to read, manipulate and write data in xlsx and xlsmformat.'
arch=('any')
url='https://pypi.org/project/pyexcel-xlsx/#files'
license=('BSD')
depends=('python2' 'python2-pyexcel-io' 'python2-openpyxl')
makedepends=('python2-setuptools')
source=("https://files.pythonhosted.org/packages/ba/57/05781192029c43dc0ebadc14883892fb28aca89638345e75ceb16b57a5e0/pyexcel-xlsx-0.5.7.tar.gz")
sha512sums=('a8f0b8c95713b9811e15045fc568cd156450828536e8492e24ae04374a8f6f820282e1634e77bb72dd33d861e1814b45e439c9b0801d99afce2dbc28f0f41db7')

prepare(){
  cd "pyexcel-xlsx-$pkgver"

  sed -i 's/openpyxl>=2.5.0,<2.6.0/openpyxl/g' setup.py
  sed -i 's/pyexcel-io>=0.5.3/pyexcel-io/g' setup.py
}

build() {
  cd "pyexcel-xlsx-$pkgver"

  python2 setup.py build
}

package() {
  cd "pyexcel-xlsx-$pkgver"

  python2 setup.py install --prefix=/usr --root="$pkgdir" --optimize=1 \
    --skip-build
}

