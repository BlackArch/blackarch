# This file is part of BlackArch Linux ( https://www.blackarch.org/ ).
# See COPYING for license details.

pkgname=libpff
pkgver=167.b736acd
pkgrel=1
pkgdesc='A library to access the Personal Folder File (PFF) and the Offline Folder File (OFF) format.'
arch=('x86_64' 'aarch64')
url='https://github.com/libyal/libpff'
license=('LGPL3')
depends=('libbfio')
makedepends=('git' 'autoconf' 'automake' 'libtool' 'gettext' 'pkg-config'
             'bison' 'flex')
source=("git+https://github.com/libyal/$pkgname.git")
sha512sums=('SKIP')

pkgver() {
  cd $pkgname

  echo $(git rev-list --count HEAD).$(git rev-parse --short HEAD)
}

build() {
  cd $pkgname

  ./synclibs.sh
  ./autogen.sh
  ./configure --prefix=/usr --sysconfdir=/etc

  make
}

package() {
  cd $pkgname

  make DESTDIR="$pkgdir" install
}

