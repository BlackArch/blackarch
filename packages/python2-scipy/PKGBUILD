# This file is part of BlackArch Linux ( https://www.blackarch.org/ ).
# See COPYING for license details.

pkgname=python2-scipy
_pkgname=scipy
pkgver=1.2.2
pkgrel=1
pkgdesc='Scientific Library for Python.'
url='https://pypi.org/project/scipy/#files'
arch=('x86_64' 'aarch64')
license=('BSD')
depends=('python2' 'python2-numpy')
makedepends=('python2-setuptools' 'gcc-fortran')
source=("https://files.pythonhosted.org/packages/a4/32/48b7882953d6dbbeea9e4259a5d42258d6e77d1d6f80f9298d82af444af4/$_pkgname-$pkgver.tar.gz")
sha512sums=('ba4f73a6844992d12515a40b2458f91fb1cb339c0d1190309e8d7b3c168b6ff8af0eca0252b3335b97c06b44e669847448d4c2bc5a836df3075daf1c33010468')

build() {
  cd "$_pkgname-$pkgver"

  export LDFLAGS='-shared'
  python2 setup.py config_fc --fcompiler=gnu95 build
}

package() {
  cd "$_pkgname-$pkgver"

  python2 setup.py config_fc --fcompiler=gnu95 install --root="$pkgdir" \
    --prefix=/usr -O1 --skip-build
}

