# This file is part of BlackArch Linux ( https://www.blackarch.org/ ).
# See COPYING for license details.

pkgname=fuzzap
pkgver=17.057002b
pkgrel=3
groups=('blackarch' 'blackarch-wireless')
pkgdesc='A python script for obfuscating wireless networks.'
arch=('any')
url='https://github.com/lostincynicism/FuzzAP'
license=('custom')
depends=('python2' 'python2-scapy')
makedepends=('git')
source=('git+https://github.com/lostincynicism/FuzzAP.git')
sha512sums=('SKIP')

pkgver() {
  cd FuzzAP

  echo $(git rev-list --count HEAD).$(git rev-parse --short HEAD)
}

prepare() {
  cd FuzzAP

  sed -i 's|python$|python2|' *.py
}

package() {
  cd FuzzAP

  install -dm 755 "$pkgdir/usr/bin"
  install -dm 755 "$pkgdir/usr/share/$pkgname"

  install -Dm 755 fuzzap.py "$pkgdir/usr/share/$pkgname/fuzzap.py"
  install -Dm 644 common.txt "$pkgdir/usr/share/$pkgname/common.txt"
  install -m 644 ssid.txt "$pkgdir/usr/share/$pkgname/ssid.txt"
  install -Dm 644 README.md "$pkgdir/usr/share/doc/$pkgname/README.md"
  install -Dm 644 COPYING "$pkgdir/usr/share/licenses/$pkgname/COPYING"

  cat > "$pkgdir/usr/bin/$pkgname" << EOF
#!/bin/sh
cd /usr/share/$pkgname
exec python2 fuzzap.py "\$@"
EOF

  chmod +x "$pkgdir/usr/bin/$pkgname"
}

