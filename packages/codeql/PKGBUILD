# This file is part of BlackArch Linux ( https://www.blackarch.org/ ).
# See COPYING for license details.

pkgname=codeql
pkgver=2.2.4
pkgrel=1
epoch=1
groups=('blackarch' 'blackarch-automation' 'blackarch-code-audit')
pkgdesc='The CLI tool for GitHub CodeQL'
arch=('i686' 'x86_64')
url='https://github.com/codeql'
license=('custom')
depends=('java-runtime-headless' 'bash')
source=("$pkgname-v$pkgver.zip::https://github.com/github/codeql-cli-binaries/releases/download/v$pkgver/$pkgname-linux64.zip")
sha512sums=('817fec619bf8d8377a8e17f74e45059040dedcf441b44bfb5c7b507ece0991d998a1dfcaf3fac24fbcf56361be9e5ca917d526fc684e4a5ebe3908bc2bbed202')

prepare() {
  cd "$srcdir"

  ln -sf "$pkgname/tools/linux64/lib64trace.so" "$pkgname/tools/linux64/libtrace.so"
}

package() {
  cd "$srcdir"

  install -dm 755 "$pkgdir/usr/bin"
  install -dm 755 "$pkgdir/usr/share/$pkgname"

  install -Dm644 $pkgname/LICENSE.md "$pkgdir/usr/share/licenses/$pkgname/LICENSE"

  cp -r $pkgname "$pkgdir/usr/share/"

  cat > "$pkgdir/usr/bin/$pkgname" << EOF
#!/bin/sh
exec /usr/share/$pkgname/$pkgname "\$@"
EOF

  chmod +x "$pkgdir/usr/bin/$pkgname"

}

