# This file is part of BlackArch Linux ( https://www.blackarch.org/ ).
# See COPYING for license details.

pkgname=codeql
pkgver=2.5.5
pkgrel=1
epoch=1
groups=('blackarch' 'blackarch-automation' 'blackarch-code-audit')
pkgdesc='The CLI tool for GitHub CodeQL'
arch=('x86_64')
url='https://github.com/codeql'
license=('custom')
depends=('java-runtime-headless' 'bash')
source=("$pkgname-v$pkgver.zip::https://github.com/github/codeql-cli-binaries/releases/download/v$pkgver/$pkgname-linux64.zip")
sha512sums=('35f5edd47bf35a0086d9a90d50dfe6934c560fb6d14faa23beb9502913a079eee3b2f22560982b5849b9be7a19508bd2711e2f7fe810ef8b8c8f8b99c33e212f')

prepare() {
  ln -sf "$pkgname/tools/linux64/lib64trace.so" \
    "$pkgname/tools/linux64/libtrace.so"
}

package() {
  install -dm 755 "$pkgdir/usr/bin"
  install -dm 755 "$pkgdir/usr/share/$pkgname"

  install -Dm 644 "$pkgname/LICENSE.md" \
    "$pkgdir/usr/share/licenses/$pkgname/LICENSE"

  cp -a $pkgname "$pkgdir/usr/share/"

  cat > "$pkgdir/usr/bin/$pkgname" << EOF
#!/bin/sh
exec /usr/share/$pkgname/$pkgname "\$@"
EOF

  chmod +x "$pkgdir/usr/bin/$pkgname"
}

