# This file is part of BlackArch Linux ( https://www.blackarch.org/ ).
# See COPYING for license details.

pkgname=python2-pyexcel-io
pkgver=0.5.11
pkgrel=1
pkgdesc='A python library to read and write structured data in csv, zipped csvformat and to/from databases.'
arch=('any')
url='https://pypi.org/project/pyexcel-io/#files'
license=('BSD')
depends=('python2' 'python2-lml')
makedepends=('python2-setuptools')
source=("https://files.pythonhosted.org/packages/3d/a6/a3123106d24b8a08bc26a4272e125f4e614468c9543378188add368f0199/pyexcel-io-$pkgver.tar.gz")
sha512sums=('02d1676254eb6319fb2c3f6f89865e939fe20319dda0713812d56d56f0b430c6e379c957bde3abfe2ee729f6679100acdb6c0e3d41e5da6d9acb1cb0b79bf2e4')

build() {
  cd "pyexcel-io-$pkgver"

  python2 setup.py build
}

prepare() {
  cd "pyexcel-io-$pkgver"

  sed -i 's|lml==0.0.1|lml|g' setup.py
}

package() {
  cd "pyexcel-io-$pkgver"

  python2 setup.py install --prefix=/usr --root="$pkgdir" --optimize=1 \
    --skip-build
}

