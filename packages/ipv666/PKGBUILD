# This file is part of BlackArch Linux ( https://www.blackarch.org/ ).
# See COPYING for license details.

pkgname=ipv666
pkgver=182.ad45ae8
pkgrel=1
pkgdesc='Golang IPv6 address enumeration.'
groups=('blackarch' 'blackarch-recon' 'blackarch-networking')
arch=('any')
url='https://github.com/lavalamp-/ipv666'
license=('BSD')
depends=('glibc')
makedepends=('git' 'go-pie')
source=("git+https://github.com/lavalamp-/$pkgname.git")
sha512sums=('SKIP')

pkgver() {
  cd $pkgname

  echo $(git rev-list --count HEAD).$(git rev-parse --short HEAD)
}

build() {
  cd $pkgname

  export GOCACHE="$srcdir/$pkgname/gocache"

  make build
}

package() {
  cd $pkgname

  install -Dm 755 "bin/$pkgname" "$pkgdir/usr/bin/$pkgname"
  install -Dm 644 -t "$pkgdir/usr/share/doc/$pkgname/" README.md CHANGELOG.md
  install -Dm 644 LICENSE.md "$pkgdir/usr/share/licenses/$pkgname/LICENSE"
}

