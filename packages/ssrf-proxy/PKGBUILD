# This file is part of BlackArch Linux ( https://www.blackarch.org/ ).
# See COPYING for license details.

pkgname=ssrf-proxy
pkgver=293.e79da7a
pkgrel=4
pkgdesc='Facilitates tunneling HTTP communications through servers vulnerable to Server-Side Request Forgery.'
groups=('blackarch' 'blackarch-proxy' 'blackarch-tunnel')
arch=('any')
url='https://github.com/bcoles/ssrf_proxy'
license=('MIT')
depends=('ruby' 'ruby-bundler')
makedepends=('git' 'ruby-rdoc')
source=('git+https://github.com/bcoles/ssrf_proxy.git')
install='ssrf-proxy.install'
sha512sums=('SKIP')

pkgver() {
  cd ssrf_proxy

  echo $(git rev-list --count HEAD).$(git rev-parse --short HEAD)
}

build() {
  cd ssrf_proxy

  gem build ssrf_proxy.gemspec
}

package() {
  cd ssrf_proxy

  install -dm 755 "$pkgdir/usr/bin"
  install -dm 755 "$pkgdir/usr/share/$pkgname"

  _gemdir="$(ruby -e'puts Gem.default_dir')"

  gem install --ignore-dependencies --no-user-install -n "$pkgdir/usr/bin" \
    -i "$pkgdir/$_gemdir" ssrf_proxy*.gem

  install -Dm 644 -t "$pkgdir/usr/share/doc/$pkgname" README.md
  install -Dm 644 LICENSE.md "$pkgdir/usr/share/licenses/$pkgname/LICENSE.md"

  cp -a * "$pkgdir/usr/share/$pkgname/"

  cat > "$pkgdir/usr/bin/$pkgname" << EOF
#!/bin/sh
cd /usr/share/$pkgname
exec ruby bin/$pkgname "\$@"
EOF

  chmod +x "$pkgdir/usr/bin/$pkgname"
}

