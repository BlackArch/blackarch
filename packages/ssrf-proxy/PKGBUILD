# This file is part of BlackArch Linux ( http://blackarch.org ).
# See COPYING for license details.

pkgname='ssrf-proxy'
pkgver=293.e79da7a
pkgrel=2
pkgdesc='Facilitates tunneling HTTP communications through servers vulnerable to Server-Side Request Forgery.'
groups=('blackarch' 'blackarch-proxy' 'blackarch-tunnel')
arch=('any')
url='https://github.com/bcoles/ssrf_proxy'
license=('MIT')
depends=('ruby' 'ruby-bundler')
makedepends=('git')
source=('git+https://github.com/bcoles/ssrf_proxy.git')
install='ssrf-proxy.install'
sha1sums=('SKIP')

pkgver() {
	cd "$srcdir/ssrf_proxy"

  echo $(git rev-list --count HEAD).$(git rev-parse --short HEAD)
}

build() {
  cd "$srcdir/ssrf_proxy"

  gem build ssrf_proxy.gemspec
}

package() {
	cd "$srcdir/ssrf_proxy"

  mkdir -p "$pkgdir/usr/bin"
  mkdir -p "$pkgdir/usr/share/ssrf-proxy"

  local _gemdir="$(ruby -e'puts Gem.default_dir')"

  gem install --no-user-install -n "$pkgdir/usr/bin" \
    -i "$pkgdir/$_gemdir" ssrf_proxy*.gem

  install -Dm644 -t "$pkgdir/usr/share/doc/ssrf-proxy" README.md
  install -Dm644 LICENSE.md "$pkgdir/usr/share/licenses/ssrf-proxy/LICENSE.md"

  cp -a * "$pkgdir/usr/share/ssrf-proxy"

  cat > "$pkgdir/usr/bin/ssrf-proxy" << EOF
#!/bin/sh
cd /usr/share/ssrf-proxy
exec ruby bin/ssrf-proxy "\$@"
EOF

  chmod a+x "$pkgdir/usr/bin/ssrf-proxy"
}
