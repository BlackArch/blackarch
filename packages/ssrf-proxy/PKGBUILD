# This file is part of BlackArch Linux ( https://www.blackarch.org/ ).
# See COPYING for license details.

pkgname=ssrf-proxy
pkgver=293.e79da7a
pkgrel=7
pkgdesc='Facilitates tunneling HTTP communications through servers vulnerable to Server-Side Request Forgery.'
groups=('blackarch' 'blackarch-proxy' 'blackarch-tunnel')
arch=('any')
url='https://github.com/bcoles/ssrf_proxy'
license=('MIT')
depends=('ruby2.3' 'ruby2.3-bundler')
makedepends=('git' 'ruby-rdoc')
source=("$pkgname::git+https://github.com/bcoles/ssrf_proxy.git")
install="$pkgname.install"
sha512sums=('SKIP')

pkgver() {
  cd $pkgname

  echo $(git rev-list --count HEAD).$(git rev-parse --short HEAD)
}

prepare() {
  cd $pkgname

  #sed -i "s|require 'ssrf-proxy'|require 'ssrf_proxy'|" bin/ssrf-proxy
}

build() {
  cd $pkgname

  gem-2.3 build ssrf_proxy.gemspec
}

package() {
  cd $pkgname

  install -dm 755 "$pkgdir/usr/bin"
  install -dm 755 "$pkgdir/usr/share/$pkgname"

  _gemdir="$(ruby-2.3 -e'puts Gem.default_dir')"

  gem-2.3 install --ignore-dependencies --no-user-install -n "$pkgdir/usr/bin" \
    -i "$pkgdir/$_gemdir" ssrf_proxy*.gem

  install -Dm 644 -t "$pkgdir/usr/share/doc/$pkgname" README.md
  install -Dm 644 LICENSE.md "$pkgdir/usr/share/licenses/$pkgname/LICENSE"

  cp -a * "$pkgdir/usr/share/$pkgname/"

  cat > "$pkgdir/usr/bin/$pkgname" << EOF
#!/bin/sh
cd /usr/share/$pkgname
exec ruby-2.3 bin/$pkgname "\$@"
EOF

  chmod +x "$pkgdir/usr/bin/$pkgname"
}

