# This file is part of BlackArch Linux ( https://www.blackarch.org/ ).
# See COPYING for license details.

pkgname=airgraph-ng
pkgver=2.0.2
pkgrel=1
epoch=2
pkgdesc='Graphing tool for the aircrack suite.'
groups=('blackarch' 'blackarch-misc')
arch=('any')
url='http://www.aircrack-ng.org'
license=('GPL2')
depends=('python2' 'graphviz' 'aircrack-ng')
makedepends=('git')
source=('git+https://github.com/aircrack-ng/aircrack-ng')
#source=('svn+http://svn.aircrack-ng.org/trunk/scripts/airgraph-ng')
sha512sums=('SKIP')

pkgver() {
  cd "aircrack-ng/scripts/$pkgname"

  grep  '__version__=' airgraph-ng | cut -d '=' -f 2 | tr -d " '"
}

build() {
  cd "aircrack-ng/scripts/$pkgname"

  python2 setup.py build
}

package() {
  cd "aircrack-ng/scripts/$pkgname"

  install -dm 755 "$pkgdir/usr/share/airgraph-ng/"

  python2 setup.py install --root="$pkgdir" --prefix=/usr -O1 --skip-build
}

