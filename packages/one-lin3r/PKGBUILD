# This file is part of BlackArch Linux ( https://www.blackarch.org/ ).
# See COPYING for license details.

pkgname=one-lin3r
pkgver=66.711a040
pkgrel=3
pkgdesc='Gives you one-liners that aids in penetration testing and more.'
arch=('any')
groups=('blackarch' 'blackarch-misc')
url='https://github.com/D4Vinci/One-Lin3r'
license=('GPL-1.0-or-later')
depends=('python' 'python-terminaltables' 'python-pyperclip'
         'python-prompt_toolkit')
makedepends=('git' 'python-setuptools')
source=("$pkgname::git+https://github.com/D4Vinci/One-Lin3r.git")
sha512sums=('SKIP')

pkgver() {
  cd $pkgname

  ( set -o pipefail
    git describe --long --tags --abbrev=7 2>/dev/null |
      sed 's/\([^-]*-g\)/r\1/;s/-/./g' ||
    printf "%s.%s" "$(git rev-list --count HEAD)" \
      "$(git rev-parse --short=7 HEAD)"
  )
}

build() {
  cd $pkgname

  python setup.py build
}

package() {
  cd $pkgname

  install -Dm 644 -t "$pkgdir/usr/share/doc/$pkgname/" README.md
  install -Dm 644 -t "$pkgdir/usr/share/licenses/$pkgname/" LICENSE

  python setup.py install --root="$pkgdir" --prefix=/usr -O1 --skip-build
}

