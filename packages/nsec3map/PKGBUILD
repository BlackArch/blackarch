# This file is part of BlackArch Linux ( https://www.blackarch.org/ ).
# See COPYING for license details.

pkgname=nsec3map
pkgver=20.1263537
pkgrel=1
pkgdesc='A tool to enumerate the resource records of a DNS zone using its DNSSEC NSEC or NSEC3 chain.'
groups=('blackarch' 'blackarch-scanner' 'blackarch-recon')
arch=('any')
url='https://github.com/anonion0/nsec3map'
license=('GPL3')
depends=('python2' 'python2-dnspython' 'python2-numpy' 'python2-scipy' 'openssl')
makedepends=('python2-setuptools' 'git')
source=("git+https://github.com/anonion0/$pkgname.git")
sha512sums=('SKIP')

pkgver() {
  cd $pkgname

  echo $(git rev-list --count HEAD).$(git rev-parse --short HEAD)
}

prepare() {
  cd $pkgname

  sed -i "s|/usr/local/share/man/man1/|/usr/share/man/man1|" setup.py
}

build() {
  cd $pkgname

  python2 setup.py build_ext
}

package() {
  cd $pkgname

  install -dm 755 "$pkgdir/usr/share/bash-completion/completions"
  install -Dm 644 n3map.bash_completion \
    "$pkgdir/usr/share/bash-completion/completions/"

  python2 setup.py install --prefix="/usr" --root="$pkgdir" --optimize=1
}

