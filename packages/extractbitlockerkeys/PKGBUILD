# This file is part of BlackArch Linux ( https://www.blackarch.org/ ).
# See COPYING for license details.

pkgname=extractbitlockerkeys
pkgver=1.2.r19.g524c128
pkgrel=2
pkgdesc='Script to automatically extract the bitlocker recovery keys from a domain.'
arch=('any')
groups=('blackarch' 'blackarch-windows')
url='https://github.com/p0dalirius/extractbitlockerkeys'
license=('GPL-2.0-or-later')
depends=('python' 'python-sectools>=1.5.0' 'python-xlsxwriter')
makedepends=('git')
source=("git+$url.git")
sha512sums=('SKIP')

pkgver() {
  cd $pkgname

  ( set -o pipefail
    git describe --long --tags --abbrev=7 2>/dev/null |
      sed 's/\([^-]*-g\)/r\1/;s/-/./g' ||
    printf "%s.%s" "$(git rev-list --count HEAD)" \
      "$(git rev-parse --short=7 HEAD)"
  )
}

package() {
  cd $pkgname

  install -dm 755 "$pkgdir/usr/bin"
  install -dm 755 "$pkgdir/usr/share/$pkgname"

  install -Dm 644 -t "$pkgdir/usr/share/doc/$pkgname/" README.md

  cp -a python/* "$pkgdir/usr/share/$pkgname/"

  cat > "$pkgdir/usr/bin/$pkgname" << EOF
#!/bin/sh
exec python /usr/share/$pkgname/ExtractBitlockerKeys.py "\$@"
EOF

  chmod +x "$pkgdir/usr/bin/$pkgname"
}

