# This file is part of BlackArch Linux ( http://blackarch.org ).
# See COPYING for license details.
pkgname='ruby-ciphr'
pkgver=120.f1f049d
_gemname='ciphr'
_gemver='0.0.3'
pkgrel=6
pkgdesc="a CLI tool for performing and composing encoding, decoding, encryption, decryption, hashing, and other various operations on streams of data from the command line; mostly intended for infosec uses."
groups=('blackarch' 'blackarch-crypto')
url='https://github.com/frohoff/ciphr'
arch=('any')
license=('MIT')
depends=('ruby' 'ruby-parslet' 'ruby-base32' 'ruby-slop')
makedepends=('git' 'ruby-bundler')
source=('git+https://github.com/frohoff/ciphr.git')
sha256sums=('SKIP')
replaces=($_gemname)
conflicts=($_gemname)
provides=($_gemname)

pkgver() {
  cd "$srcdir/ciphr"

  echo $(git rev-list --count master).$(git rev-parse --short master)
}

build() {
  cd "$srcdir/ciphr"

  gem build $_gemname.gemspec
}

package() {
  cd "$srcdir/ciphr"
  local _gemdir="$(ruby -rubygems -e 'puts Gem.default_dir')"

  gem install --ignore-dependencies --no-document --no-user-install -i "$pkgdir/$_gemdir" -n "$pkgdir"/usr/bin $_gemname-$_gemver.gem
  rm "$pkgdir/$_gemdir/cache/$_gemname-$_gemver.gem"

  install -D -m644 "$pkgdir/$_gemdir/gems/$_gemname-$_gemver/LICENSE.txt" "$pkgdir/usr/share/licenses/$pkgname/LICENSE.txt"
  install -D -m644 -t "$pkgdir/usr/share/doc/$pkgname" README.md

  rm -rf "$pkgdir/$_gemdir/gems/$_gemname-$_gemver"/{Gemfile,Gemfile.lock,LICENSE.txt,README.md,Rakefile,TODO,ciphr.gemspec,pkg,spec,tests}
}
