# This file is part of BlackArch Linux ( https://www.blackarch.org/ ).
# See COPYING for license details.

pkgname=altdns
pkgver=78.6728272
pkgrel=3
pkgdesc='Generates permutations, alterations and mutations of subdomains and then resolves them.'
groups=('blackarch' 'blackarch-recon')
arch=('any')
url='https://github.com/infosec-au/altdns'
license=('Apache-2.0')
depends=('python' 'python-dnspython' 'python-tldextract' 'python-termcolor')
makedepends=('git' 'python-setuptools')
source=("git+https://github.com/infosec-au/$pkgname.git")
sha512sums=('SKIP')

pkgver() {
  cd $pkgname

  ( set -o pipefail
    git describe --long --tags --abbrev=7 2>/dev/null |
      sed 's/\([^-]*-g\)/r\1/;s/-/./g' ||
    printf "%s.%s" "$(git rev-list --count HEAD)" \
      "$(git rev-parse --short=7 HEAD)"
  )
}

build() {
  cd $pkgname

  python setup.py build
}

package() {
  cd $pkgname

  python setup.py install --root="$pkgdir" --prefix=/usr -O1 --skip-build

  install -Dm 644 -t "$pkgdir/usr/share/doc/$pkgname/" README.md
}

