# This file is part of BlackArch Linux ( https://www.blackarch.org/ ).
# See COPYING for license details.

pkgname=altdns
pkgver=68.689cc81
pkgrel=1
pkgdesc='Generates permutations, alterations and mutations of subdomains and then resolves them.'
groups=('blackarch' 'blackarch-recon')
arch=('any')
url='https://github.com/infosec-au/altdns'
license=('Apache')
depends=('python2' 'python2-dnspython' 'python2-tldextract' 'python2-termcolor'
         'python2-argparse')
makedepends=('git' 'python2-setuptools')
source=("git+https://github.com/infosec-au/$pkgname.git")
sha512sums=('SKIP')

pkgver() {
  cd $pkgname

  echo $(git rev-list --count HEAD).$(git rev-parse --short HEAD)
}

build() {
  cd $pkgname

  python2 setup.py build
}

package() {
  cd $pkgname

  python2 setup.py install --root="$pkgdir" --prefix=/usr -O1 --skip-build

  install -Dm 644 -t "$pkgdir/usr/share/doc/$pkgname/" README.md
  install -Dm 644 LICENSE "$pkgdir/usr/share/licenses/$pkgname/LICENSE"
}

