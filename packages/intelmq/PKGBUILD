# This file is part of BlackArch Linux ( https://www.blackarch.org/ ).
# See COPYING for license details.

pkgname=intelmq
pkgver=3.0.2.r184.g469dc4951
pkgrel=1
pkgdesc='A tool for collecting and processing security feeds using a message queuing protocol.'
arch=('any')
groups=('blackarch' 'blackarch-misc')
url='https://github.com/certtools/intelmq'
license=('AGPL3')
depends=('python' 'python-dnspython' 'python-psutil' 'python-dateutil'
         'python-termstyle' 'python-pytz' 'python-redis' 'python-requests'
         'python-ruamel-yaml' 'python-psycopg2')
makedepends=('git' 'python-setuptools')
source=("git+https://github.com/certtools/$pkgname.git"
        'setup.py.patch')
sha512sums=('SKIP'
            '692e7ece47dca57fc41a580f759d2d313f9c299bf01ef807aecea1bd9d76e1394d0e004056788b4e9bde4c845fe75c1d95137667ca26a6680b35430570d27fc8')

pkgver() {
  cd $pkgname

  git describe --long --tags | sed 's/\([^-]*-g\)/r\1/;s/-/./g'
}

prepare() {
  cd $pkgname

  patch -p0 < ../setup.py.patch
}

build() {
  cd $pkgname

  python setup.py build
}

package() {
  cd $pkgname

  python setup.py install --root="$pkgdir" --prefix=/usr -O1 --skip-build

  install -Dm 644 -t "$pkgdir/usr/share/doc/$pkgname" *.rst *.md
}

