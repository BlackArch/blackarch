# This file is part of BlackArch Linux ( https://www.blackarch.org/ ).
# See COPYING for license details.

pkgname=ansible-runner
pkgver=2.4.0.r14.g9e0eac5
pkgrel=1
pkgdesc='A tool and Python library that helps when interfacing with Ansible directly.'
arch=('any')
url='https://github.com/ansible/ansible-runner'
license=('Apache-2.0')
depends=('python' 'python-psutil' 'python-pexpect' 'python-daemon'
         'python-yaml' 'python-six')
makedepends=('git' 'python-build' 'python-pip')
source=("git+https://github.com/ansible/$pkgname.git")
sha512sums=('SKIP')

pkgver() {
  cd $pkgname

  ( set -o pipefail
    git describe --long --tags --abbrev=7 2>/dev/null |
      sed 's/\([^-]*-g\)/r\1/;s/-/./g' ||
    printf "%s.%s" "$(git rev-list --count HEAD)" \
      "$(git rev-parse --short=7 HEAD)"
  )
}

build() {
  cd $pkgname

  python -m build --wheel --outdir="$startdir/dist"
}

package() {
  cd $pkgname

  pip install \
    --verbose \
    --disable-pip-version-check \
    --no-warn-script-location \
    --ignore-installed \
    --no-compile \
    --no-deps \
    --root="$pkgdir" \
    --prefix=/usr \
    --no-index \
    --find-links="file://$startdir/dist" \
    $pkgname
}

