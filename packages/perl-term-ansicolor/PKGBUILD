# This file is part of BlackArch Linux ( https://www.blackarch.org/ ).
# See COPYING for license details.

pkgname=perl-term-ansicolor
pkgver=4.06
pkgrel=4
pkgdesc='Color screen output using ANSI escape sequences.'
arch=('any')
license=('PerlArtistic' 'GPL')
options=('!emptydirs')
depends=('perl')
url='https://metacpan.org/pod/Term::ANSIColor'
source=("http://search.cpan.org/CPAN/authors/id/R/RR/RRA/Term-ANSIColor-$pkgver.tar.gz")
sha512sums=('1aeb282ff2cb0117efefcc91023b6c3f031f5f93569330d681dc7d042931126e58d9bec28603bed5b2ec2067ed2af606a37b7e3a7d50f32ee5a99267198c5ad7')

build() {
  cd "Term-ANSIColor-$pkgver"

  (
    export PERL_MM_USE_DEFAULT=1 PERL5LIB="" PERL_AUTOINSTALL=--skipdeps \
    PERL_MM_OPT="INSTALLDIRS=vendor DESTDIR='$pkgdir'"     \
    PERL_MB_OPT="--installdirs vendor --destdir '$pkgdir'" \
    MODULEBUILDRC=/dev/null

    perl Makefile.PL

    make
  )
}

package() {
  cd "Term-ANSIColor-$pkgver"

  make install

  find "$pkgdir" -name .packlist -o -name perllocal.pod -delete
}

