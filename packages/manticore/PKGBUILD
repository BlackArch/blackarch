# This file is part of BlackArch Linux ( http://blackarch.org ).
# See COPYING for license details.

pkgname=manticore
pkgver=0.2.1.1.r41.g98190d89
pkgrel=1
pkgdesc='Symbolic execution tool.'
arch=('any')
groups=('blackarch' 'blackarch-binary')
url='https://github.com/trailofbits/manticore'
license=('GAGPL3')
depends=('python' 'python-pyevmasm' 'python-capstone' 'python-keystone'
         'python-unicorn' 'python-ply' 'binaryninja-python' 'python-typing'
         'python-pyelftools' 'python-z3-solver' 'python-pysha3' 'solidity')
makedepends=('git' 'python-setuptools')
source=('git+https://github.com/trailofbits/manticore.git')
md5sums=('SKIP')

pkgver() {
  cd manticore

  git describe --long | sed 's/\([^-]*-g\)/r\1/;s/-/./g'
}

build() {
  cd manticore

  python setup.py build
}

package() {
  cd manticore

  python setup.py install --root="${pkgdir}" --prefix=/usr

  install -Dm644 -t "$pkgdir/usr/share/doc/${pkgname}" *.md docs/*.rst
  cp -a docs/images "$pkgdir/usr/share/doc/${pkgname}"

  find "$pkgdir" -type f -iname '*.pyc' -print -exec rm -f {} \;
}

