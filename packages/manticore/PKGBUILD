# This file is part of BlackArch Linux ( https://www.blackarch.org/ ).
# See COPYING for license details.

pkgname=manticore
pkgver=0.3.7.r87.g066bfcf
pkgrel=1
epoch=1
pkgdesc='Symbolic execution tool.'
arch=('any')
groups=('blackarch' 'blackarch-binary')
url='https://github.com/trailofbits/manticore'
license=('AGPL-3.0-or-later')
depends=('python' 'python-yaml' 'python-protobuf' 'python-pysha3' 'python-cbor2'
         'python-prettytable' 'python-ply' 'python-rlp' 'python-intervaltree'
         'python-crytic-compile' 'python-wasm' 'python-pyevmasm'
         'python-z3-solver' 'python-capstone' 'python-unicorn'
         'python-pyelftools' 'python-redis')
makedepends=('git' 'python-setuptools')
source=("git+https://github.com/trailofbits/$pkgname.git")
sha512sums=('SKIP')

pkgver() {
  cd $pkgname

  ( set -o pipefail
    git describe --long --tags --abbrev=7 2>/dev/null |
      sed 's/\([^-]*-g\)/r\1/;s/-/./g' ||
    printf "%s.%s" "$(git rev-list --count HEAD)" \
      "$(git rev-parse --short=7 HEAD)"
  )
}

build() {
  cd $pkgname

  python setup.py build
}

package() {
  cd $pkgname

  python setup.py install --root="$pkgdir" --prefix=/usr -O1 --skip-build

  install -Dm 644 -t "$pkgdir/usr/share/doc/$pkgname" *.md docs/*.rst

  cp -a docs/images "$pkgdir/usr/share/doc/$pkgname/"

  find "$pkgdir" -type f -iname '*.pyc' -print -exec rm -f {} \;
}

