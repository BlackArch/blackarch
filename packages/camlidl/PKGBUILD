# This file is part of BlackArch Linux ( https://www.blackarch.org/ ).
# See COPYING for license details.

pkgname=camlidl
pkgver=1.07
_pkgver=${pkgver/\./}
pkgrel=1
pkgdesc='A stub code generator and COM binding for Objective Caml (OCaml).'
arch=('x86_64' 'aarch64')
url='http://caml.inria.fr/pub/old_caml_site/camlidl/'
license=('custom:unknown')
depends=('ocaml')
source=("https://github.com/xavierleroy/$pkgname/archive/$pkgname$_pkgver.tar.gz"
        'arch-build-system-fix.patch'
        'META.camlidl')
sha512sums=('cf29d2e4cfb0134aa01d7805dbac150edf31e9f347dc4f2ec56cdee866e7a601f3f099048481452bc2cf8d91937d64f917110d75cc397f7ad44ad546cac66c1f'
            'f33b2f2ada43a1d178c5bb8a389f7bf936931a11159b06fa38f9eb22b5d816ed4eff82210146cff669c3a5c5950b329b9f3efc0f79d69b0a9924dfb25e9ccbe2'
            'c62d52405d02d1fd59cd7f91494183f8677f52d76a49509e56375ff5ba2645e0cc71adbc2540e06da9dd2fe324a6c2f1990345a1f15d087e1f0b7ef216ca3f43')

prepare() {
  cd "$pkgname-$pkgname$_pkgver"

  #patch -Np1 -i "$srcdir/arch-build-system-fix.patch"

  cp config/Makefile.unix config/Makefile
}

build() {
  cd "$pkgname-$pkgname$_pkgver"

  make all
}

package() {
  cd "$pkgname-$pkgname$_pkgver"

  _bindir="$pkgdir/usr/bin"
  _ocamldir="$pkgdir/$(ocamlc -where)"

  mkdir -p "$_bindir" "$_ocamldir"/{caml,stublibs,$pkgname}

  make BINDIR="$_bindir" OCAMLLIB="$_ocamldir" install

  install -Dm 644 "$srcdir/META.camlidl" "$_ocamldir/$pkgname/META"
  install -Dm 644 LICENSE "$pkgdir/usr/share/licenses/$pkgname/LICENSE"
}

