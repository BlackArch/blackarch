# This file is part of BlackArch Linux ( https://www.blackarch.org/ ).
# See COPYING for license details.

pkgname=python-trackerjacker
pkgver=1.9.0
pkgrel=3
pkgdesc='Finds and tracks wifi devices through raw 802.11 monitoring.'
groups=('blackarch' 'blackarch-wireless')
arch=('any')
url='https://github.com/calebmadrigal/trackerjacker'
license=('MIT')
depends=('python' 'python-scapy' 'python-pyaml' 'python-ruamel-yaml')
makedepends=('python-setuptools')
source=("https://files.pythonhosted.org/packages/95/d7/74790c2071d9dfeb8cc3b3cf401350b880c0533d5eac584ef10c35bfc640/trackerjacker-$pkgver.tar.gz")
sha512sums=('26e067805f522aca75a9bf99f73c64a6c1c451c65a473f23cbcc6b975ac4e3490e462671967a306e80daa42d9109e7c0a2a5b271446eb01d7ba77ba423af58a2')

prepare() {
  cd "trackerjacker-$pkgver"

  sed 's|scapy.*|scapy|g' -i requirements.txt # fix for scapy version
}

build() {
  cd "trackerjacker-$pkgver"

  python setup.py build
}

package() {
  cd "trackerjacker-$pkgver"

  python setup.py install --prefix=/usr --root="$pkgdir" --optimize=1 \
    --skip-build
}

