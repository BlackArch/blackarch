# This file is part of BlackArch Linux ( https://www.blackarch.org/ ).
# See COPYING for license details.

pkgname=python-trackerjacker
_pkgname=trackerjacker
pkgver=1.9.0
pkgrel=5
pkgdesc='Finds and tracks wifi devices through raw 802.11 monitoring.'
groups=('blackarch' 'blackarch-wireless')
arch=('any')
url='https://github.com/calebmadrigal/trackerjacker'
license=('MIT')
depends=('python' 'python-scapy' 'python-pyaml' 'python-ruamel-yaml')
makedepends=('python-pip' 'python-build' 'python-wheel')
source=("https://files.pythonhosted.org/packages/source/${_pkgname::1}/$_pkgname/trackerjacker-$pkgver.tar.gz")
sha512sums=('26e067805f522aca75a9bf99f73c64a6c1c451c65a473f23cbcc6b975ac4e3490e462671967a306e80daa42d9109e7c0a2a5b271446eb01d7ba77ba423af58a2')

prepare() {
  cd "trackerjacker-$pkgver"

  sed 's|scapy.*|scapy|g' -i requirements.txt # fix for scapy version
}

build() {
  cd "$_pkgname-$pkgver"

  python -m build --wheel --outdir="$startdir/dist"
}

package() {
  cd "$_pkgname-$pkgver"

  pip install \
    --verbose \
    --disable-pip-version-check \
    --no-warn-script-location \
    --ignore-installed \
    --no-compile \
    --no-deps \
    --root="$pkgdir" \
    --prefix=/usr \
    --no-index \
    --find-links="file://$startdir/dist" \
    $_pkgname
}

