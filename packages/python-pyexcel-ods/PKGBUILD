# This file is part of BlackArch Linux ( https://www.blackarch.org/ ).
# See COPYING for license details.

pkgname=python-pyexcel-ods
_pkgname=pyexcel-ods
pkgver=0.6.0
pkgrel=5
pkgdesc='A wrapper library to read, manipulate and write data in ods format.'
arch=('any')
url='https://pypi.org/project/pyexcel-ods/#files'
license=('BSD')
depends=('python' 'python-odfpy' 'python-pyexcel')
makedepends=('python-setuptools')
source=("https://files.pythonhosted.org/packages/source/${_pkgname::1}/$_pkgname/$_pkgname-$pkgver.tar.gz")
sha512sums=('e2043f687a50d93c2d97f973b010cdcf987cd3ffb2ec0cc4cd5257a02f4f563cc82f09b16aa1855e8d98ba855b05ea91b95e8eb0c00ea6ee95ea96cb7d05a7e9')

prepare() {
  cd "$_pkgname-$pkgver"

  sed -i 's|>=0.5.10||g' setup.py
  sed -i 's|==1.3.5||g' setup.py
}

build() {
  cd "$_pkgname-$pkgver"

  python setup.py build
}

package() {
  cd "$_pkgname-$pkgver"

  python setup.py install --root="$pkgdir" --prefix=/usr -O1 --skip-build
}

