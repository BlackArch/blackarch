# This file is part of BlackArch Linux ( https://www.blackarch.org/ ).
# See COPYING for license details.

pkgname=python-pyexcel-ods
_pkgname=pyexcel-ods
pkgver=0.5.6
pkgrel=1
pkgdesc='A wrapper library to read, manipulate and write data in ods format.'
arch=('any')
url='https://pypi.org/project/pyexcel-ods/#files'
license=('BSD')
depends=('python' 'python-odfpy' 'python-pyexcel')
makedepends=('python-setuptools')
source=("https://files.pythonhosted.org/packages/1a/d0/cb060a51d191accf52888b1f7ae7e0e8914c035eda1a3e2f1d8bb8cf0ed4/$_pkgname-$pkgver.tar.gz")
sha512sums=('7600b613af493c2ce003805d41cd1ac8d8160677c2412f51b23263afae4a77f16479021ba6d345b517b55d147aa3a097b65e5bdc932de8063c49406f0e1f7e41')

prepare() {
  cd "$_pkgname-$pkgver"

  sed -i 's|>=0.5.10||g' setup.py
  sed -i 's|==1.3.5||g' setup.py
}

build() {
  cd "$_pkgname-$pkgver"

  python setup.py build
}

package() {
  cd "$_pkgname-$pkgver"

  python setup.py install --root="$pkgdir" --prefix=/usr -O1 --skip-build
}

