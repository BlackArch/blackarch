# This file is part of BlackArch Linux ( http://blackarch.org ).
# See COPYING for license details.

pkgname=tchunt-ng
pkgver=208.b8cf7fc
pkgrel=1
pkgdesc='Reveal encrypted files stored on a filesystem.'
groups=('blackarch' 'blackarch-forensic' 'blackarch-crypto')
arch=('any')
url='https://github.com/antagon/TCHunt-ng'
license=('GPL3')
depends=()
makedepends=('git')
source=('git+https://github.com/antagon/TCHunt-ng.git')
sha512sums=('SKIP')

pkgver() {
  cd "$srcdir/TCHunt-ng"

  echo $(git rev-list --count HEAD).$(git rev-parse --short HEAD)
}

prepare() {
  cd "$srcdir/TCHunt-ng/src"

  sed -i 's|/usr/local/bin|/usr/bin|' Makefile
}

build() {
  cd "$srcdir/TCHunt-ng"

  make
}

package() {
  cd "$srcdir/TCHunt-ng"

  install -Dm755 src/tchuntng "$pkgdir/usr/bin/tchunt-ng"
  install -Dm644 -t "$pkgdir/usr/share/doc/tchunt-ng/" README.md ChangeLog
  install -Dm644 COPYING "$pkgdir/usr/share/licenses/tchunt-ng/COPYING"
}
