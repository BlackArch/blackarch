# This file is part of BlackArch Linux ( https://www.blackarch.org/ ).
# See COPYING for license details.

pkgbase=python-humanfriendly
pkgname=('python2-humanfriendly' 'python-humanfriendly')
_pkgname=humanfriendly
pkgver=4.18
pkgrel=2
pkgdesc='Human friendly output for text interfaces using Python.'
arch=('any')
url='https://pypi.org/project/humanfriendly/#files'
license=('MIT')
options=(!emptydirs)
makedepends=('python2-setuptools' 'python-setuptools')
source=("https://files.pythonhosted.org/packages/26/71/e7daf57e819a70228568ff5395fdbc4de81b63067b93167e07825fcf0bcf/$_pkgname-$pkgver.tar.gz")
sha512sums=('01c7ee50d35154fe0b7ad39662ca6ff2dcd3e0f2a2eacc62f0184102c9db1c0fd99257273982aff56710b4e59024e8c20ee77ddd10c68e427c077ed4ca7e0022')

prepare() {
  cp -a "$_pkgname-$pkgver"{,-2}
}

build() {
  cd "$_pkgname-$pkgver-2"

  python2 setup.py build

  cd "$srcdir/$_pkgname-$pkgver"

  python setup.py build
}

package_python2-humanfriendly() {
  depends=('python2' 'python2-monotonic')

  cd "$_pkgname-$pkgver-2"

  python2 setup.py install --prefix=/usr --root="$pkgdir" --optimize=1 \
    --skip-build

  mv "$pkgdir/usr/bin/humanfriendly" "$pkgdir/usr/bin/humanfriendly2"
}

package_python-humanfriendly() {
  depends=('python')

  cd "$_pkgname-$pkgver"

  python setup.py install --prefix=/usr --root="$pkgdir" --optimize=1 \
    --skip-build
}

