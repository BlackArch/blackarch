# This file is part of BlackArch Linux ( https://www.blackarch.org/ ).
# See COPYING for license details.

pkgbase=python-humanfriendly
pkgname=('python2-humanfriendly' 'python-humanfriendly')
_pkgname=humanfriendly
pkgver=8.2
pkgrel=2
pkgdesc='Human friendly output for text interfaces using Python.'
arch=('any')
url='https://pypi.org/project/humanfriendly/#files'
license=('MIT')
options=(!emptydirs)
makedepends=('python2-setuptools' 'python-setuptools')
source=("https://files.pythonhosted.org/packages/6c/19/8e3b4c6fa7cca4788817db398c05274d98ecc6a35e0eaad2846fde90c863/$_pkgname-$pkgver.tar.gz")
sha512sums=('123d8592e6613a70421740648bb8998733b118927a0bd6182af848612e46eec8e35cae95168219c9b7672bed8e18e1c4a493a80248eabe47f8a4cb1813c8b591')

prepare() {
  cp -a "$_pkgname-$pkgver"{,-2}
}

build() {
  cd "$_pkgname-$pkgver-2"

  python2 setup.py build

  cd "$srcdir/$_pkgname-$pkgver"

  python setup.py build
}

package_python2-humanfriendly() {
  depends=('python2' 'python2-monotonic')

  cd "$_pkgname-$pkgver-2"

  python2 setup.py install --prefix=/usr --root="$pkgdir" --optimize=1 \
    --skip-build

  mv "$pkgdir/usr/bin/humanfriendly" "$pkgdir/usr/bin/humanfriendly2"
}

package_python-humanfriendly() {
  depends=('python')

  cd "$_pkgname-$pkgver"

  python setup.py install --prefix=/usr --root="$pkgdir" --optimize=1 \
    --skip-build
}

