# This file is part of BlackArch Linux ( https://www.blackarch.org/ ).
# See COPYING for license details.

pkgbase=python-libsigscan-python
pkgname=('python2-libsigscan-python' 'python-libsigscan-python')
_pkgname=libsigscan
__pkgname=libsigscan-python
pkgver=20201117
pkgrel=2
pkgdesc='Python bindings module for libsigscan.'
arch=('x86_64' 'aarch64')
url='https://pypi.python.org/pypi/libsigscan-python/'
license=('LGPL3')
makedepends=('python2-setuptools' 'python-setuptools')
source=("https://files.pythonhosted.org/packages/5f/f9/67bb01750c665ae6f05ffca879845014a5a594f9b8a838b6180d6051b248/$__pkgname-$pkgver.tar.gz")
sha512sums=('dafdda28fd424e48b48a4c53f0f9e4cc0add65a497b859d87d6318fc0b6aee560498f8753b545c2e8a459b9ba677b93da645f0b04088cbc2b22d94393bb1a89c')

prepare() {
  cp -a "$_pkgname-$pkgver"{,-2}
}

build() {
  cd "$_pkgname-$pkgver-2"

  python2 setup.py build

  cd "$srcdir/$_pkgname-$pkgver"

  python setup.py build
}

package_python2-libsigscan-python() {
  depends=('python2')

  cd "$_pkgname-$pkgver-2"

  python2 setup.py install --root="$pkgdir" --prefix=/usr -O1 --skip-build
}

package_python-libsigscan-python() {
  depends=('python')

  cd "$_pkgname-$pkgver"

  python setup.py install --root="$pkgdir" --prefix=/usr -O1 --skip-build
}

