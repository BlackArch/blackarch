# This file is part of BlackArch Linux ( https://www.blackarch.org/ ).
# See COPYING for license details.

pkgbase=python-libsigscan-python
pkgname=('python2-libsigscan-python' 'python-libsigscan-python')
_pkgname=libsigscan
__pkgname=libsigscan-python
pkgver=20210419
pkgrel=1
pkgdesc='Python bindings module for libsigscan.'
arch=('x86_64' 'aarch64')
url='https://pypi.python.org/pypi/libsigscan-python/'
license=('LGPL3')
makedepends=('python2-setuptools' 'python-setuptools')
source=("https://files.pythonhosted.org/packages/10/f8/076077c3a223bfd3dddeb14ff408b722d12952925f4fc5d976a1c795a840/$_pkgname-$pkgver.tar.gz")
sha512sums=('5ea0df353d24b31af04ce24ddd1116f6ba467e92114220aecd2542c0a4cd558a2a006706aadbfbf2a34bdc4bd82bcd9c440e444d296a4d10e436c856176326fc')

prepare() {
  cp -a "$_pkgname-$pkgver"{,-2}
}

build() {
  cd "$_pkgname-$pkgver-2"

  python2 setup.py build

  cd "$srcdir/$_pkgname-$pkgver"

  python setup.py build
}

package_python2-libsigscan-python() {
  depends=('python2')

  cd "$_pkgname-$pkgver-2"

  python2 setup.py install --root="$pkgdir" --prefix=/usr -O1 --skip-build
}

package_python-libsigscan-python() {
  depends=('python')

  cd "$_pkgname-$pkgver"

  python setup.py install --root="$pkgdir" --prefix=/usr -O1 --skip-build
}

