# This file is part of BlackArch Linux ( https://www.blackarch.org/ ).
# See COPYING for license details.

pkgbase=python-python-pushsafer
pkgname=('python2-python-pushsafer' 'python-python-pushsafer')
pkgver=0.4
pkgrel=3
pkgdesc='Comprehensive bindings for the Pushsafer.com notification service.'
arch=('any')
url='https://pypi.org/project/python-pushsafer/#files'
license=('GPL3')
makedepends=('python2-setuptools' 'python-setuptools')
source=("https://files.pythonhosted.org/packages/e7/f0/951014efae1637e78962194519b60aeb8f7997427d4313217fb224f62036/python-pushsafer-$pkgver.tar.gz")
sha512sums=('daa5db89832f2db5f8973be343d80efec492f8d78c2dc2fd068d62b747f78986f227db5429311ff3879784f4fc6d9f9a876591de436e32631a96a5802732e123')

prepare() {
  cp -a "python-pushsafer-$pkgver" "python-pushsafer2-$pkgver"
}

build() {
  cd "python-pushsafer2-$pkgver"

  python2 setup.py build

  cd "$srcdir/python-pushsafer-$pkgver"

  python setup.py build
}

package_python2-python-pushsafer() {
  depends=('python2-requests')

  cd "python-pushsafer2-$pkgver"

  python2 setup.py install --prefix=/usr --root="$pkgdir" --optimize=1 \
    --skip-build
}

package_python-python-pushsafer() {
  depends=('python-requests')

  cd "python-pushsafer-$pkgver"

  python setup.py install --prefix=/usr --root="$pkgdir" --optimize=1 \
    --skip-build
}

