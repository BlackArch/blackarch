# This file is part of BlackArch Linux ( https://www.blackarch.org/ ).
# See COPYING for license details.

pkgbase=python-python-pushsafer
pkgname=('python2-python-pushsafer' 'python-python-pushsafer')
_pkgname=python-pushsafer
pkgver=1.0
pkgrel=1
pkgdesc='Comprehensive bindings for the Pushsafer.com notification service.'
arch=('any')
url='https://pypi.org/project/python-pushsafer/#files'
license=('GPL3')
makedepends=('python2-setuptools' 'python-setuptools' 'python2-pip' 'python-pip'
             'python2-wheel' 'python-wheel')
source=("https://files.pythonhosted.org/packages/source/${_pkgname::1}/$_pkgname/$_pkgname-$pkgver.tar.gz")
sha512sums=('be6eef9fb394049cd1d999dd199c63d4e63c7679ac67b92cbd3dbefae0fdb3683ff42ac320d0aaf89c01db214b75dfb9c54728823490827b2bf74a71cacdcd4a')

prepare() {
  cp -a "python-pushsafer-$pkgver" "python-pushsafer2-$pkgver"
}

build() {
  cd "${_pkgname}2-$pkgver"

  python2 setup.py build

  cd "$srcdir/$_pkgname-$pkgver"

  python setup.py build
}

package_python2-python-pushsafer() {
  depends=('python2-requests')

  cd "${_pkgname}2-$pkgver"

  python2 setup.py install --root="$pkgdir" --prefix=/usr -O1 --skip-build
}

package_python-python-pushsafer() {
  depends=('python-requests')

  cd "$_pkgname-$pkgver"

  python setup.py install --root="$pkgdir" --prefix=/usr -O1 --skip-build
}

