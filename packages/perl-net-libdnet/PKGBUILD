# This file is part of BlackArch Linux ( http://blackarch.org ).
# See COPYING for license details.

pkgname=perl-net-libdnet
_cpanname="Net-Libdnet"
pkgver=0.98
pkgrel=7
pkgdesc="Binding for Dug Song's libdnet."
arch=('x86_64' 'armv6h' 'armv7h' 'aarch64')
url='http://search.cpan.org/~gomor/Net-Libdnet/'
license=('GPL' 'PerlArtistic')
depends=('perl' 'perl-class-gomor' 'libdnet')
makedepends=('perl-extutils-makemaker')
options=('!emptydirs')
source=("http://www.cpan.org/authors/id/G/GO/GOMOR/${_cpanname}-${pkgver}.tar.gz")
sha512sums=('bcfdee6da4df667fe73f9772adfe81279a0a2a0a1a3a69abff03fb6e43a1e9a8d2abb2373c3470048f134cfcef5e3581a4a422dfdcd63589cb28c99e8fc096db')

prepare() {
  cd "$srcdir/$_cpanname-$pkgver"

  export PERL_MM_USE_DEFAULT=1 PERL_AUTOINSTALL=--skipdeps \
    PERL_MM_OPT="INSTALLDIRS=vendor DESTDIR='$pkgdir'" \
    PERL_MB_OPT="--installdirs vendor --destdir '$pkgdir'" \
    MODULEBUILDRC=/dev/null

  /usr/bin/perl Makefile.PL
}

build() {
  cd "$srcdir/$_cpanname-$pkgver"

  make
}

package() {
  cd "$srcdir/$_cpanname-$pkgver"

  make install

  find "$pkgdir" -name .packlist -o -name perllocal.pod -delete
}
