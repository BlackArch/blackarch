# This file is part of BlackArch Linux ( http://blackarch.org ).
# See COPYING for license details.

pkgname='crackhor'
pkgver=9.5cfc8f8
pkgrel=1
groups=('blackarch' 'blackarch-cracker')
pkgdesc='A Password cracking utility.'
arch=('any')
url='https://github.com/CoalfireLabs/crackHOR'
license=('GPL2')
depends=('python2' 'ophcrack' 'hashcat')
makedepends=('git')
provides=('crackhor')
source=('git+https://github.com/CoalfireLabs/crackHOR.git')
sha1sums=('SKIP')

pkgver() {
  cd "$srcdir/crackHOR"

  echo $(git rev-list --count HEAD).$(git rev-parse --short HEAD)
}

prepare() {
  cd "$srcdir/crackHOR"

  sed -i 's|python$|python2|' *.py
}

package() {
  cd "$srcdir/crackHOR"

  install -Dm755 crackHOR.py "$pkgdir/usr/bin/crackhor"
  install -Dm644 README.md "$pkgdir/usr/share/doc/crackhor/README.md"
}
