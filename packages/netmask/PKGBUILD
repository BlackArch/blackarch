# This file is part of BlackArch Linux ( https://www.blackarch.org/ ).
# See COPYING for license details.

pkgname=netmask
pkgver=2.4.4
pkgrel=1
groups=('blackarch' 'blackarch-recon')
pkgdesc='Helps determine network masks.'
arch=('x86_64' 'aarch64')
url='http://packages.qa.debian.org/n/netmask.html'
license=('GPL2')
source=("http://mirror.de.leaseweb.net/debian/pool/main/n/netmask/netmask_$pkgver.orig.tar.gz")
sha512sums=('57beddd0671dfa8102a68a2e8194d04405358b188b10da215768580f022d040ab990089013816500aafe2090eba8a79aef1fafbd160526c985fbbf09ff69ffce')

build() {
  cd "$pkgname-$pkgver"

  ./autogen

  ./configure --prefix=/usr --mandir=/usr/share/man

  make
}

package() {
  cd "$pkgname-$pkgver"

  make DESTDIR="$pkgdir" install

  install -Dm 644 -t "$pkgdir/usr/share/doc/$pkgname/" README AUTHORS ChangeLog
  install -Dm 644 COPYING "$pkgdir/usr/share/licenses/$pkgname/COPYING"
}

