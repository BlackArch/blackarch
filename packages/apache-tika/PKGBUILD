# This file is part of BlackArch Linux ( https://www.blackarch.org/ ).
# See COPYING for license details.

pkgname=apache-tika
_srvname=tika-server
_appname=tika-app
pkgver=1.28.5
pkgrel=1
pkgdesc='Toolkit for detecting and extracting metadata and structured text content.'
arch=('any')
license=('Apache')
url='https://tika.apache.org/download.html'
depends=('java-environment' 'bash')
source=("https://www.apache.org/dyn/closer.lua/tika/$pkgver/$_srvname-$pkgver.jar"
        "https://www.apache.org/dyn/closer.lua/tika/$pkgver/$_appname-$pkgver.jar"
        "$pkgname.service")
noextract=("$_srvname-$pkgver.jar" "$_appname-$pkgver.jar")
sha512sums=('240a06adc1a0f5a9665dc8a11199d5e2a328a588bf26671e29fc28e3d62637c6ecd61b3eeacaf402a9dffa1e80bb08916179baec7a92d08edcf160356d16df59'
            '9d1222ad39e8bb0b371ff0f06308cf82dea8d6f996ec7bdb398c359a29cd3181528f2179835ded0531105970706a213541991a25ed177db6b8a3915479badf52'
            '7eb7ce2a2ed76ad197e247689484cf1e241f26b8cbe41e4663f42d98a22f6df5505a3df9e491e70ee3d059db4ea960ab58ca12c5593f69f97a575e46dcc2fcbf')

package() {
  install -dm 755 "$pkgdir/usr/bin"

  install -Dm 755 -t "$pkgdir/usr/share/java/$pkgname/" \
    "$_srvname-$pkgver.jar" "$_appname-$pkgver.jar"
  install -Dm 644 "$pkgname.service" \
    "$pkgdir/usr/lib/systemd/system/$pkgname.service"

  cat > "$pkgdir/usr/bin/tika-server" << EOF
#!/bin/sh
"$JAVA_HOME/bin/java" -jar \
  "/usr/share/java/$pkgname/$_srvname-$pkgver.jar" "\$@"
EOF

  chmod +x "$pkgdir/usr/bin/tika-server"

  cat > "$pkgdir/usr/bin/tika-app" << EOF
#!/bin/sh
"$JAVA_HOME/bin/java" -jar \
  "/usr/share/java/$pkgname/$_appname-$pkgver.jar" "\$@"
EOF

  chmod +x "$pkgdir/usr/bin/tika-app"
}

