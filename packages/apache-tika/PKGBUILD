# This file is part of BlackArch Linux ( https://www.blackarch.org/ ).
# See COPYING for license details.

pkgname=apache-tika
_srvname=tika-server
_appname=tika-app
pkgver=1.24
pkgrel=1
pkgdesc='Toolkit for detecting and extracting metadata and structured text content.'
arch=('any')
license=('Apache')
url='https://tika.apache.org/download.html'
depends=('java-environment' 'bash')
source=("http://mirror.checkdomain.de/apache/tika/$_srvname-$pkgver.jar"
        "http://mirror.checkdomain.de/apache/tika/$_appname-$pkgver.jar"
        "$pkgname.service")
noextract=("$_srvname-$pkgver.jar" "$_appname-$pkgver.jar")
sha512sums=('496bac44144076185506e3693d57de8633e164d1e6ad6c01e69d6f60be8c58350e54f681a2e7f42ac699d3d64f9826c0c3d8e3bc8dbaf718ae0d0b1e6fd2aff2'
            'b31b10cb3cd2b5e15a0798f2119f4ae5435e9e4af82619cf1a64121415035bc73a51923a8144c9f5245492a093a9630f18eeee4483d896672a9dfab80b203981'
            '7eb7ce2a2ed76ad197e247689484cf1e241f26b8cbe41e4663f42d98a22f6df5505a3df9e491e70ee3d059db4ea960ab58ca12c5593f69f97a575e46dcc2fcbf')

package() {
  install -dm 755 "$pkgdir/usr/bin"

  install -Dm 755 -t "$pkgdir/usr/share/java/$pkgname/" \
    "$_srvname-$pkgver.jar" "$_appname-$pkgver.jar"
  install -Dm 644 "$pkgname.service" \
    "$pkgdir/usr/lib/systemd/system/$pkgname.service"

  cat > "$pkgdir/usr/bin/tika-server" << EOF
#!/bin/sh
"$JAVA_HOME/bin/java" -jar \
  "/usr/share/java/$pkgname/$_srvname-$pkgver.jar" "\$@"
EOF

  chmod +x "$pkgdir/usr/bin/tika-server"

  cat > "$pkgdir/usr/bin/tika-app" << EOF
#!/bin/sh
"$JAVA_HOME/bin/java" -jar \
  "/usr/share/java/$pkgname/$_appname-$pkgver.jar" "\$@"
EOF

  chmod +x "$pkgdir/usr/bin/tika-app"
}

