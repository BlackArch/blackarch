# This file is part of BlackArch Linux ( https://www.blackarch.org/ ).
# See COPYING for license details.

pkgname=apache-tika
_srvname=tika-server
_appname=tika-app
pkgver=1.27
pkgrel=1
pkgdesc='Toolkit for detecting and extracting metadata and structured text content.'
arch=('any')
license=('Apache')
url='https://tika.apache.org/download.html'
depends=('java-environment' 'bash')
source=("https://www.apache.org/dyn/closer.cgi/tika/$pkgver/$_srvname-$pkgver.jar"
        "https://www.apache.org/dyn/closer.cgi/tika/$pkgver/$_appname-$pkgver.jar"
        "$pkgname.service")
noextract=("$_srvname-$pkgver.jar" "$_appname-$pkgver.jar")
sha512sums=('f4487f88c5172069dfb42d33253d34201a586c9f76ba46c224bbc63ea47c800918e0809e7de3383971119f3339408beac976344e222352b774035947bfd4310e'
            'f57bc053495a539585655b40e4936508acc4b4dac5efebf25ffb99a41418cdeaad33a6f67b35a18449b0b3a6ed8703a81932f80b7eaffe4ccaee67a9a17f7eb1'
            '7eb7ce2a2ed76ad197e247689484cf1e241f26b8cbe41e4663f42d98a22f6df5505a3df9e491e70ee3d059db4ea960ab58ca12c5593f69f97a575e46dcc2fcbf')

package() {
  install -dm 755 "$pkgdir/usr/bin"

  install -Dm 755 -t "$pkgdir/usr/share/java/$pkgname/" \
    "$_srvname-$pkgver.jar" "$_appname-$pkgver.jar"
  install -Dm 644 "$pkgname.service" \
    "$pkgdir/usr/lib/systemd/system/$pkgname.service"

  cat > "$pkgdir/usr/bin/tika-server" << EOF
#!/bin/sh
"$JAVA_HOME/bin/java" -jar \
  "/usr/share/java/$pkgname/$_srvname-$pkgver.jar" "\$@"
EOF

  chmod +x "$pkgdir/usr/bin/tika-server"

  cat > "$pkgdir/usr/bin/tika-app" << EOF
#!/bin/sh
"$JAVA_HOME/bin/java" -jar \
  "/usr/share/java/$pkgname/$_appname-$pkgver.jar" "\$@"
EOF

  chmod +x "$pkgdir/usr/bin/tika-app"
}

