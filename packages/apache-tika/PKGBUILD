# This file is part of BlackArch Linux ( https://www.blackarch.org/ ).
# See COPYING for license details.

pkgname=apache-tika
pkgver=1.23
pkgrel=1
pkgdesc='Toolkit for detecting and extracting metadata and structured text content.'
arch=('any')
license=('Apache')
url='https://tika.apache.org/download.html'
depends=('java-environment' 'bash')
noextract=("tika-server-$pkgver.jar")
source=("http://central.maven.org/maven2/org/apache/tika/tika-server/$pkgver/tika-server-$pkgver.jar"
        'apache-tika.service')
sha512sums=('4e1b8418aed496eb1ee5bf32921580e855672ab701bffdd642261ac27ae011913f36fdd70b725adef4dec55a53fa04cf0f3b8a3b45e15026dd76c8d312b62291'
            'e46522bfcc86ada8776c4f12cf645a81c34492b1c86e9bd1258d7fa248fb8fed9c2bd57e5971c5c9a4687f35626f872f63920649279cfaba5498a55c3331bcf5')

package() {
  install -dm 755 "$pkgdir/usr/bin"

  install -Dm 755 "tika-server-$pkgver.jar" \
    "$pkgdir/usr/share/java/$pkgname/tika-server-$pkgver.jar"
  install -Dm 644 "$pkgname.service" \
    "$pkgdir/usr/lib/systemd/system/$pkgname.service"

  cat > "$pkgdir/usr/bin/tika" << EOF
#!/bin/sh
"$JAVA_HOME/bin/java" -jar \
  "/usr/share/java/$pkgname/tika-server-$pkgver.jar" "\$@"
EOF

  chmod +x "$pkgdir/usr/bin/tika"
}

