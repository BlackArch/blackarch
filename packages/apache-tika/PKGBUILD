# This file is part of BlackArch Linux ( https://www.blackarch.org/ ).
# See COPYING for license details.

pkgname=apache-tika
_srvname=tika-server
_appname=tika-app
pkgver=1.27
pkgrel=1
pkgdesc='Toolkit for detecting and extracting metadata and structured text content.'
arch=('any')
license=('Apache')
url='https://tika.apache.org/download.html'
depends=('java-environment' 'bash')
source=("https://www.apache.org/dyn/closer.cgi/tika/$pkgver/$_srvname-$pkgver.jar"
        "https://www.apache.org/dyn/closer.cgi/tika/$pkgver/$_appname-$pkgver.jar"
        "$pkgname.service")
noextract=("$_srvname-$pkgver.jar" "$_appname-$pkgver.jar")
sha512sums=('5d355f1750b4025e0955f350e5792e63514823f576e24228dcf6e7e2888617e916b4e223b654a6dd041fe99c6eed637ee8ba31fc52b8cee498bc2ec21687d344'
            'b7c24528ab49219ddb8a56ee0a5ad64aef53c56f98bdca2aeb55943c1c565c76a44fe2be0470e1303a8ee3bbee8651433084466ccce8f7ae4a092a0fe2334614'
            '7eb7ce2a2ed76ad197e247689484cf1e241f26b8cbe41e4663f42d98a22f6df5505a3df9e491e70ee3d059db4ea960ab58ca12c5593f69f97a575e46dcc2fcbf')

package() {
  install -dm 755 "$pkgdir/usr/bin"

  install -Dm 755 -t "$pkgdir/usr/share/java/$pkgname/" \
    "$_srvname-$pkgver.jar" "$_appname-$pkgver.jar"
  install -Dm 644 "$pkgname.service" \
    "$pkgdir/usr/lib/systemd/system/$pkgname.service"

  cat > "$pkgdir/usr/bin/tika-server" << EOF
#!/bin/sh
"$JAVA_HOME/bin/java" -jar \
  "/usr/share/java/$pkgname/$_srvname-$pkgver.jar" "\$@"
EOF

  chmod +x "$pkgdir/usr/bin/tika-server"

  cat > "$pkgdir/usr/bin/tika-app" << EOF
#!/bin/sh
"$JAVA_HOME/bin/java" -jar \
  "/usr/share/java/$pkgname/$_appname-$pkgver.jar" "\$@"
EOF

  chmod +x "$pkgdir/usr/bin/tika-app"
}

