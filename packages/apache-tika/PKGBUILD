# This file is part of BlackArch Linux ( http://blackarch.org ).
# See COPYING for license details.

pkgname=apache-tika
pkgver=1.18
pkgrel=1
pkgdesc='Toolkit for detecting and extracting metadata and structured text content.'
arch=('any')
license=('Apache')
url='https://tika.apache.org/'
depends=('java-environment' 'bash')
noextract=("tika-server-${pkgver}.jar")
source=("http://central.maven.org/maven2/org/apache/tika/tika-server/${pkgver}/tika-server-${pkgver}.jar"
        'apache-tika.service')
sha512sums=('e8ea97bccb9d7edadf6d0bc8006c542f539ab5cfa81bc38578687c46f62202e9ebd4ab55314cd54723c49b17f351571bcfc55914764425bf608c3e8d93598d8a'
            'e46522bfcc86ada8776c4f12cf645a81c34492b1c86e9bd1258d7fa248fb8fed9c2bd57e5971c5c9a4687f35626f872f63920649279cfaba5498a55c3331bcf5')

package() {
  cd "$srcdir"

  install -dm 755 "$pkgdir/usr/bin"

  install -Dm755 "tika-server-${pkgver}.jar" \
    "$pkgdir/usr/share/java/apache-tika/tika-server-${pkgver}.jar"
  install -Dm644 "apache-tika.service" \
    "$pkgdir/usr/lib/systemd/system/apache-tika.service"

  cat > "$pkgdir/usr/bin/tika" << EOF
#!/bin/sh
"$JAVA_HOME/bin/java" -jar \
  "/usr/share/java/apache-tika/tika-server-${pkgver}.jar" "\${@}"
EOF

  chmod +x "$pkgdir/usr/bin/tika"
}
