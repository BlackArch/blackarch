# This file is part of BlackArch Linux ( https://www.blackarch.org/ ).
# See COPYING for license details.

pkgname=python2-msoffcrypto-tool
_pkgname=${pkgname#python2-}
pkgver=5.0.0
pkgrel=4
epoch=1
pkgdesc='A Python tool and library for decrypting MS Office files with passwords or other keys.'
arch=('any')
url='https://pypi.org/project/msoffcrypto-tool/#files'
license=('MIT')
depends=('python2' 'python2-olefile' 'python2-cryptography')
makedepends=('python2-setuptools')
source=("https://files.pythonhosted.org/packages/source/${_pkgname::1}/$_pkgname/$_pkgname-$pkgver.tar.gz")
sha512sums=('6e9cceb2c49fb6a26427fb9668b6493861a002c2972d8ee902978a86a3d99e942145eefe0575a3827f9527d9db0b209a137223841d1ab6c86cb058ba141e32bc')

build() {
  cd "$_pkgname-$pkgver"

  python2 setup.py build
}

package() {
  cd "$_pkgname-$pkgver"

  python2 setup.py install --prefix=/usr --root="$pkgdir" -O1 --skip-build

  mv "$pkgdir/usr/bin/$_pkgname" "$pkgdir/usr/bin/${_pkgname}-2"
}

