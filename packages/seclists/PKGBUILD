# This file is part of BlackArch Linux ( https://www.blackarch.org/ ).
# See COPYING for license details.

pkgname=seclists
pkgver=2025.3.r376.g57a919b
pkgrel=1
epoch=1
pkgdesc='A collection of multiple types of lists used during security assessments.'
groups=('blackarch-wordlist')
arch=('any')
url='https://github.com/danielmiessler/SecLists/'
license=('custom:unknown')
makedepends=('git')
source=("git+https://github.com/danielmiessler/$pkgname.git")
sha512sums=('SKIP')

pkgver() {
  cd $pkgname

  ( set -o pipefail
    git describe --long --tags --abbrev=7 2>/dev/null |
      sed 's/\([^-]*-g\)/r\1/;s/-/./g' ||
    printf "%s.%s" "$(git rev-list --count HEAD)" \
      "$(git rev-parse --short=7 HEAD)"
  )
}

package() {
  cd $pkgname

  install -dm 755 "$pkgdir/usr/share/$pkgname"

  find -maxdepth 1 | grep -v \\.git |
  xargs -I{} cp -r {} "$pkgdir/usr/share/$pkgname"
}

