# This file is part of BlackArch Linux ( http://blackarch.org ).
# See COPYING for license details.

pkgname=vbscan
pkgver=37.f11f1f0
pkgrel=1
epoch=1
groups=('blackarch' 'blackarch-webapp' 'blackarch-fuzzer' 'blackarch-scanner')
pkgdesc='A black box vBulletin vulnerability scanner written in perl.'
arch=('any')
url='https://github.com/rezasp/vbscan'
license=('GPL3')
depends=('perl')
makedepends=('git')
source=('git+https://github.com/rezasp/vbscan.git')
sha512sums=('SKIP')

pkgver() {
  cd "$srcdir/vbscan"

  echo $(git rev-list --count HEAD).$(git rev-parse --short HEAD)
}

package() {
  cd "$srcdir/vbscan"

  install -dm 755 "$pkgdir/usr/bin"
  install -dm 755 "$pkgdir/usr/share/vbscan"

  install -Dm644 README.md "$pkgdir/usr/share/doc/vbscan/README.md"
  install -Dm644 -t "$pkgdir/usr/share/licenses/vbscan/" CHANGELOG.md \
    COPYING.GPL LICENCE

  rm README.md CHANGELOG.md COPYING.GPL LICENCE

  cp -a * "$pkgdir/usr/share/vbscan/"

  cat > "$pkgdir/usr/bin/vbscan" << EOF
#!/bin/sh
cd /usr/share/vbscan
exec perl vbscan.pl "\$@"
EOF

  chmod a+x "$pkgdir/usr/bin/vbscan"
}
