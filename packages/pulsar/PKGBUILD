# This file is part of BlackArch Linux ( https://www.blackarch.org/ ).
# See COPYING for license details.

pkgname=pulsar
pkgver=55.3c61178
pkgrel=3
pkgdesc='Protocol Learning and Stateful Fuzzing.'
groups=('blackarch' 'blackarch-fuzzer')
arch=('any')
url='https://github.com/hgascon/pulsar'
license=('BSD')
depends=('python' 'cuckoo' 'python-pygraphviz' 'python-levenshtein'
         'python-progressbar' 'python-numpy' 'python-twisted')
makedepends=('git')
source=("git+https://github.com/hgascon/$pkgname.git")
sha512sums=('SKIP')

pkgver() {
  cd $pkgname

  echo $(git rev-list --count HEAD).$(git rev-parse --short HEAD)
}

package() {
  cd $pkgname

  install -dm 755 "$pkgdir/usr/bin"
  install -dm 755 "$pkgdir/usr/share/$pkgname"

  install -Dm 644 -t "$pkgdir/usr/share/doc/$pkgname/" README.md
  install -Dm 644 LICENSE "$pkgdir/usr/share/licenses/$pkgname/LICENSE"

  rm README.md LICENSE

  cp -a * "$pkgdir/usr/share/$pkgname/"

  cat > "$pkgdir/usr/bin/$pkgname" << EOF
#!/bin/sh
cd /usr/share/$pkgname
exec python $pkgname.py "\$@"
EOF

  chmod +x "$pkgdir/usr/bin/$pkgname"
}

