# This file is part of BlackArch Linux ( http://blackarch.org ).
# See COPYING for license details.

pkgname='pulsar'
pkgver=34.633c8a6
pkgrel=1
pkgdesc='Protocol Learning and Stateful Fuzzing.'
groups=('blackarch' 'blackarch-fuzzer')
arch=('any')
url='https://github.com/hgascon/pulsar'
license=('BSD')
depends=('python2' 'cuckoo' 'python2-pygraphviz' 'python2-levenshtein')
makedepends=('git')
source=('git+https://github.com/hgascon/pulsar.git')
sha1sums=('SKIP')

pkgver() {
  cd "$srcdir/pulsar"

  echo $(git rev-list --count HEAD).$(git rev-parse --short HEAD)
}

package() {
  cd "$srcdir/pulsar"

  mkdir -p "$pkgdir/usr/bin"
  mkdir -p "$pkgdir/usr/share/pulsar"

  install -Dm644 README.md "$pkgdir/usr/share/doc/pulsar/README.md"
  install -Dm644 LICENSE "$pkgdir/usr/share/licenses/pulsar/LICENSE"

  rm README.md LICENSE

  cp -a * "$pkgdir/usr/share/pulsar/"

  cat > "$pkgdir/usr/bin/pulsar" << EOF
#!/bin/sh
cd /usr/share/pulsar
exec python2 pulsar.py "\$@"
EOF

  chmod a+x "$pkgdir/usr/bin/pulsar"
}
