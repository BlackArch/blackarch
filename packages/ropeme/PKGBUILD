# This file is part of BlackArch Linux ( http://blackarch.org ).
# See COPYING for license details.

pkgname=ropeme
pkgver=1.0
pkgrel=6
groups=('blackarch' 'blackarch-exploitation')
pkgdesc='A set of python scripts to generate ROP gadgets and payload.'
arch=('any')
url='http://www.vnsecurity.net/2010/08/ropeme-rop-exploit-made-easy/'
license=('GPL')
depends=('python2' 'distorm')
source=('https://github.com/packz/ropeme/archive/master.zip')
sha512sums=('c8546e7f3450e8f797065670d352ac99e30ef1cc109ef679388622a8f793c383c35a8163cfeae82385b604043f28437bde5ee753146c14bc81b4df3bc7822bdc')

prepare(){
  cd "$srcdir/ropeme-master"

  find "$srcdir/" -name "*.py" -exec sed -i \
    's/import distorm/import distorm3 as distorm/' {} \;
  find "$srcdir/" -name "*.py" -exec sed -i '1s|env python|env python2|' {} \;
}

package() {
  cd "$srcdir/ropeme-master"

  install -dm 755 "$pkgdir/usr/bin/"
  install -dm 755 "$pkgdir/usr/share/ropeme"

  cp -a --no-preserve=ownership * "$pkgdir/usr/share/ropeme"

  chmod +x "$pkgdir/usr/share/ropeme/exploit.py"
  chmod +x "$pkgdir/usr/share/ropeme/ropeme/readelf.py"
  chmod +x "$pkgdir/usr/share/ropeme/ropeme/search-gadgets.py"
  chmod +x "$pkgdir/usr/share/ropeme/ropeme/ropshell.py"

  ln -s "/usr/share/ropeme/ropeme/ropshell.py" "$pkgdir/usr/bin/ropeme-ropshell"
  ln -s "/usr/share/ropeme/ropeme/readelf.py" "$pkgdir/usr/bin/ropeme-readelf"
  ln -s "/usr/share/ropeme/ropeme/search-gadgets.py" \
    "$pkgdir/usr/bin/ropeme-search-gadgets"
  ln -s "/usr/share/ropeme/exploit.py" "$pkgdir/usr/bin/ropeme-exploit"
}
