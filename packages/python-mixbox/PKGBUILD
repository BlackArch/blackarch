# This file is part of BlackArch Linux ( https://www.blackarch.org/ ).
# See COPYING for license details.

pkgbase=python-mixbox
pkgname=('python2-mixbox' 'python-mixbox')
_pkgname=mixbox
pkgver=1.0.4
pkgrel=1
pkgdesc='Utility library for cybox, maec, and stix packages.'
arch=('any')
url='https://pypi.org/project/mixbox/#files'
license=('BSD')
makedepends=('python2-setuptools' 'python-setuptools')
source=("https://files.pythonhosted.org/packages/d4/85/89bb1abaedaf0574c94329e1d7a13c37a18d13ff144649d6a453457256e9/$_pkgname-$pkgver.tar.gz")
sha512sums=('f7feec0c08a2476765a2c621dbad29bd473d9f0cd4530e271bef67579dc41cf6be0652b0400f8a9e89e7b88ee1ada3fca3be09653791991f9e9c0176f41d0ec4')

prepare() {
  cp -a "$_pkgname-$pkgver"{,-2}
}

build() {
  cd "$_pkgname-$pkgver-2"

  python2 setup.py build

  cd "$srcdir/$_pkgname-$pkgver"

  python setup.py build
}

package_python2-mixbox() {
  depends=('python2' 'python2-dateutil' 'python2-lxml' 'python2-ordered-set')

  cd "$_pkgname-$pkgver-2"

  python2 setup.py install --prefix=/usr --root="$pkgdir" --optimize=1 \
    --skip-build
}

package_python-mixbox() {
  depends=('python' 'python-dateutil' 'python-lxml' 'python-ordered-set')

  cd "$_pkgname-$pkgver"

  python setup.py install --prefix=/usr --root="$pkgdir" --optimize=1 \
    --skip-build
}

