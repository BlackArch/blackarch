# This file is part of BlackArch Linux ( https://www.blackarch.org/ ).
# See COPYING for license details.

pkgbase=python-mixbox
pkgname=('python2-mixbox' 'python-mixbox')
_pkgname=mixbox
pkgver=1.0.3
pkgrel=4
pkgdesc='Utility library for cybox, maec, and stix packages.'
arch=('any')
url='https://pypi.org/project/mixbox/#files'
license=('BSD')
makedepends=('python2-setuptools' 'python-setuptools')
source=("https://files.pythonhosted.org/packages/fa/c0/be7e3726f314d1d0f61dede33b14e552cbe7c62354753e93f0c4247e38a2/$_pkgname-$pkgver.tar.gz")
sha512sums=('4051879fec46df14c518ef37ec679936d4a8c2200ad5c675a93c1343307d981ca4b3895905311f45eb56f2c78482dc206ed406be1f3fb8819519f4019db1141c')

prepare() {
  cp -a "$_pkgname-$pkgver"{,-2}
}

build() {
  cd "$_pkgname-$pkgver-2"

  python2 setup.py build

  cd "$srcdir/$_pkgname-$pkgver"

  python setup.py build
}

package_python2-mixbox() {
  depends=('python2' 'python2-dateutil' 'python2-lxml' 'python2-ordered-set')

  cd "$_pkgname-$pkgver-2"

  python2 setup.py install --prefix=/usr --root="$pkgdir" --optimize=1 \
    --skip-build
}

package_python-mixbox() {
  depends=('python' 'python-dateutil' 'python-lxml' 'python-ordered-set')

  cd "$_pkgname-$pkgver"

  python setup.py install --prefix=/usr --root="$pkgdir" --optimize=1 \
    --skip-build
}

