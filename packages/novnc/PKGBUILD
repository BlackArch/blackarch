# This file is part of BlackArch Linux ( https://www.blackarch.org/ ).
# See COPYING for license details.
#
# AUR Maintainer: seiuneko <chfsefefgesfen foxmail>
# AUR Contributor: Radek Podgorny <radek@podgorny.cz>

pkgname=novnc
_pkgname=noVNC
pkgver=1.3.0
pkgrel=1
pkgdesc='HTML VNC Client Library and Application.'
arch=('any')
url='https://github.com/novnc/noVNC'
license=('custom:unknown')
depends=('bash' 'websockify' 'python-numpy')
source=("$pkgname-$pkgver.tar.gz::https://github.com/noVNC/$_pkgname/archive/v$pkgver.tar.gz")
sha512sums=('2c9eb019bd9c86a0ffb716eba06e8f05bc522c37561e0acc7f88a66188c617ebc54f4cc9220897c241280ba2ed5919a2050a94eeb3da2c030bde43af0bd92c51')

prepare() {
  cd "$_pkgname-$pkgver"

  sed \
    -e 's#\(share\)#\1/webapps#g' \
    -e '2 i WEBSOCKIFY=/usr/bin/websockify' \
    -i utils/launch.sh
}

package() {
  cd "$_pkgname-$pkgver"

  install -dm 644 "$pkgdir/usr/share/doc/$pkgname"
  install -dm 755 "$pkgdir/usr/share/webapps/$pkgname"

  install -Dm 644 LICENSE.txt "$pkgdir/usr/share/licenses/$pkgname/LICENSE"
  install -Dm 644 vendor/pako/LICENSE \
    "$pkgdir/usr/share/licenses/$pkgname/LICENSE.MIT"
  install -Dm 755 utils/launch.sh "$pkgdir/usr/bin/$pkgname"

  ( cd docs; find . -name 'LICENSE*' -type f -exec cp -a {} \
    "$pkgdir/usr/share/licenses/$pkgname/{}" \; )
  ( cd docs; find . ! -name 'LICENSE*' -type f -exec cp -a {} \
    "$pkgdir/usr/share/doc/$pkgname/{}" \; )

  cp -a app core po vendor vnc.html karma.conf.js package.json vnc_lite.html \
    "$pkgdir/usr/share/webapps/$pkgname"
}

