# This file is part of BlackArch Linux ( https://www.blackarch.org/ ).
# See COPYING for license details.

pkgbase=python-minidump
pkgname=('python2-minidump' 'python-minidump')
pkgver=0.0.6
pkgrel=1
epoch=1
pkgdesc='Python library to parse and read Microsoft minidump file format.'
groups=('blackarch' 'blackarch-windows')
arch=('any')
url='https://pypi.org/project/minidump/#files'
license=('MIT')
makedepends=('python-setuptools' 'python2-setuptools')
source=("https://files.pythonhosted.org/packages/9c/ea/7695e3741cb455934ae8755cf38a71981681cce753bce93469917667e033/minidump-0.0.6.tar.gz")
sha512sums=('ed3972ed4e107cca724642948dedc7089674b73fb5164230973c0be76a2d4fe75ef4eeca62cb5c66ff27258b5b26ab9888062b5f4e54b4468539f7cecdda25e3')

prepare() {
  cp -a "minidump-$pkgver" "minidump-py2-$pkgver"
}

build() {
  cd "minidump-py2-$pkgver"

  python2 setup.py build

  cd "$srcdir/minidump-$pkgver"

  python setup.py build
}

package_python2-minidump() {
  depends=('python2')

  cd "minidump-$pkgver"

  python2 setup.py install --prefix=/usr --root="$pkgdir" --optimize=1 \
    --skip-build

  mv "$pkgdir/usr/bin/minidump" "$pkgdir/usr/bin/minidump2"
}

package_python-minidump() {
  depends=('python')

  cd "minidump-$pkgver"

  python setup.py install --prefix=/usr --root="$pkgdir" --optimize=1 \
    --skip-build
}

