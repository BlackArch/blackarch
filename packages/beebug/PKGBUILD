# This file is part of BlackArch Linux ( https://www.blackarch.org/ ).
# See COPYING for license details.

pkgname=beebug
pkgver=25.cddb375
pkgrel=2
pkgdesc='A tool for checking exploitability.'
arch=('any')
groups=('blackarch' 'blackarch-decompiler' 'blackarch-disassembler'
        'blackarch-reversing')
url='https://github.com/invictus1306/beebug'
license=('GPL3')
depends=('python' 'python-r2pipe' 'python-pydot' 'python-pygraphviz'
         'python-pyqtgraph' 'radare2')
makedepends=('git')
source=("git+https://github.com/invictus1306/$pkgname.git")
sha512sums=('SKIP')

pkgver() {
  cd $pkgname

  echo $(git rev-list --count HEAD).$(git rev-parse --short HEAD)
}

package() {
  cd $pkgname

  install -dm 755 "$pkgdir/usr/bin"
  install -dm 755 "$pkgdir/usr/share/$pkgname"

  install -Dm 644 -t "$pkgdir/usr/share/doc/$pkgname/" README.md
  install -Dm 644 LICENSE "$pkgdir/usr/share/licenses/$pkgname/LICENSE"

  cp -a --no-preserve=ownership * "$pkgdir/usr/share/$pkgname/"

  cat > "$pkgdir/usr/bin/$pkgname" << EOF
#!/bin/sh
cd /usr/share/$pkgname
exec python $pkgname.py "\$@"
EOF

  chmod +x "$pkgdir/usr/bin/$pkgname"
}

