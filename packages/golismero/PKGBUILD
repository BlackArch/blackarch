# This file is part of BlackArch Linux ( http://blackarch.org ).
# See COPYING for license details.

pkgname='golismero'
pkgver='35.36ed3d4'
pkgrel=1
groups=('blackarch' 'blackarch-webapp')
pkgdesc='Opensource web security testing framework.'
url='https://github.com/golismero/golismero'
license=('custom')
arch=('any')
depends=('python2' 'python2-beautifulsoup3' 'python2-colorizer'
         'python2-decorator' 'python2-django>=1.5' 'python2-dnspython'
         'python2-netaddr' 'python2-nltk' 'python2-cjson' 'python2-requests'
         'python2-requests_ntlm' 'python2-texttable' 'python2-tldextract')
makedepends=('git')
options=('!strip')
source=('git+https://github.com/golismero/golismero.git')
sha1sums=('SKIP')

pkgver() {
  cd "$srcdir/golismero"

  echo $(git rev-list --count HEAD).$(git rev-parse --short HEAD)
}

package() {
  cd "$srcdir/golismero"

  mkdir -p "$pkgdir/usr/share/golismero"
  mkdir -p "$pkgdir/usr/bin"

  cp -a * "$pkgdir/usr/share/golismero"

  cat > "$pkgdir/usr/bin/golismero" << EOF
#!/bin/sh
cd /usr/share/golismero
exec python2 golismero.py "\$@"
EOF

  chmod +x "$pkgdir/usr/bin/golismero"
}
