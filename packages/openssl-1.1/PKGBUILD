# This file is part of BlackArch Linux ( https://www.blackarch.org/ ).
# See COPYING for license details.

pkgname=openssl-1.1
_pkgname=${pkgname%-*}
pkgver=1.1.1.w
_pkgver=1.1.1w
pkgrel=4
pkgdesc='OpenSSL 1.1 legacy branch (deprecated).'
arch=('x86_64' 'aarch64')
url='https://openssl-library.org/source/old/1.1.1'
license=('custom:OpenSSL')
provides=('libcrypto.so=1.1-64' 'libssl.so=1.1-64')
depends=('glibc')
makedepends=('perl')
source=("https://github.com/openssl/openssl/releases/download/OpenSSL_1_1_1w/$_pkgname-$_pkgver.tar.gz")
sha512sums=('b4c625fe56a4e690b57b6a011a225ad0cb3af54bd8fb67af77b5eceac55cc7191291d96a660c5b568a08a2fbf62b4612818e7cca1bb95b2b6b4fc649b0552b6d')

build() {
  cd "$_pkgname-$_pkgver"

  ./config \
    --prefix=/usr \
    --libdir="lib/$pkgname"

  make
}

package() {
  cd "$_pkgname-$_pkgver"

  make DESTDIR="$pkgdir" install_sw

  install -Dm 644 -t "$pkgdir/usr/share/licenses/$pkgname/" LICENSE

  mv "$pkgdir/usr/bin/$_pkgname" "$pkgdir/usr/bin/$pkgname"
  mv "$pkgdir/usr/bin/c_rehash" "$pkgdir/usr/bin/c_rehash-1.1"

  mkdir "$pkgdir/usr/include/$pkgname/"
  mv "$pkgdir/usr/include/$_pkgname/" "$pkgdir/usr/include/$pkgname/"

  mv "$pkgdir/usr/lib/$pkgname/"{libssl.so.1.1,libcrypto.so.1.1} \
    "$pkgdir/usr/lib/"
}

