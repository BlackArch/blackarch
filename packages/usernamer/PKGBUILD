# This file is part of BlackArch Linux ( http://blackarch.org ).
# See COPYING for license details.

pkgname='usernamer'
pkgver='7.813139d'
pkgrel=3
groups=('blackarch' 'blackarch-misc' 'blackarch-automation')
pkgdesc='Pentest Tool to generate usernames/logins based on supplied names.'
arch=('any')
url='https://github.com/jseidl/usernamer'
license=('GPL3')
depends=('python2')
makedepends=('git')
source=('git+https://github.com/jseidl/usernamer.git')
sha1sums=('SKIP')

pkgver() {
  cd "$srcdir/usernamer"

  echo $(git rev-list --count HEAD).$(git rev-parse --short HEAD)
}

prepare() {
  cd "$srcdir/usernamer"

  sed -i 's|/usr/bin/env python|/usr/bin/env python2|' usernamer.py
}

package() {
  cd "$srcdir/usernamer"

  install -Dm755 usernamer.py "$pkgdir/usr/bin/usernamer"
  install -Dm644 README.md "$pkgdir/usr/share/doc/usernamer/README.md"
}
