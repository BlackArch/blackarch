# This file is part of BlackArch Linux ( https://www.blackarch.org/ ).
# See COPYING for license details.

pkgname=firefox-security-toolkit
pkgver=14.f79344c
pkgrel=1
pkgdesc='A tool that transforms Firefox browsers into a penetration testing suite.'
groups=('blackarch' 'blackarch-misc')
arch=('any')
url='https://github.com/mazen160/Firefox-Security-Toolkit'
license=('MIT')
depends=('bash' 'wget' 'firefox')
makedepends=('git')
source=('git+https://github.com/mazen160/Firefox-Security-Toolkit')
sha512sums=('SKIP')

pkgver() {
  cd Firefox-Security-Toolkit

  echo $(git rev-list --count HEAD).$(git rev-parse --short HEAD)
}

package() {
  cd Firefox-Security-Toolkit

  install -dm 755 "$pkgdir/usr/bin"
  install -dm 755 "$pkgdir/usr/share/$pkgname"

  install -Dm644 -t "$pkgdir/usr/share/doc/$pkgname/" README.md CHANGELOG.txt
  install -Dm644 LICENSE "$pkgdir/usr/share/licenses/$pkgname/LICENSE"

  rm README.md *.txt LICENSE

  cp -a * "$pkgdir/usr/share/$pkgname/"

  cat > "$pkgdir/usr/bin/$pkgname" << EOF
#!/bin/sh
cd /usr/share/$pkgname
exec bash firefox_security_toolkit.sh "\$@"
EOF

  chmod a+x "$pkgdir/usr/bin/$pkgname"
}

