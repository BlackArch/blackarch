# This file is part of BlackArch Linux ( http://blackarch.org ).
# See COPYING for license details.

pkgname='snort'
pkgver='2.9.11.1'
pkgrel=2
pkgdesc='A lightweight network intrusion detection system.'
arch=('x86_64' 'armv6h' 'armv7h' 'aarch64')
groups=('blackarch' 'blackarch-defensive' 'blackarch-networking'
        'blackarch-forensic')
url='http://www.snort.org'
license=('GPL')
depends=('libdaq' 'libdnet' 'libpcap' 'openssl' 'pcre' 'zlib' 'libutil-linux')
makedepends=('ca-certificates')
backup=('etc/conf.d/snort'
        'etc/snort/snort.conf'
        'etc/snort/threshold.conf'
        'etc/snort/reference.config'
        'etc/snort/classification.config')
options=('!makeflags' '!libtool')
install='snort.install'
source=("https://www.snort.org/downloads/snort/snort-${pkgver}.tar.gz"
        'http://rules.emergingthreats.net/open/snort-2.9.0/emerging.rules.tar.gz'
        'snort'
        'snort.conf.d'
        'snort.service')
sha1sums=('ef5103ff8d2c552ec7e8baf76c126cb978fa3eed'
          'a289d9f8c15c71f28e9f280f8ced85d235deebb3'
          '10ba5152a1d46c5fe7a3fb67cc67e03442827552'
          '7bc4f538667f17b8d5e1099d3d9d01c3a306e4ad'
          '7b76af310e4f14dd5e938fb6d01b6d006df1a41d')

build() {
  cd "$srcdir/snort-$pkgver"

  ./configure --prefix=/usr --sysconfdir=/etc/snort \
    --with-libpcap-includes=/usr/include/pcap \
    --with-daq-includes=/usr/include --with-daq-libraries=/usr/lib --enable-zlib

  make
}

package() {
  cd "$srcdir/snort-$pkgver"

  make DESTDIR="$pkgdir" install

  # Base directories.
  mkdir -p "$pkgdir/etc/rc.d"
  mkdir -p "$pkgdir/etc/snort/rules"
  mkdir -p "$pkgdir/var/log/snort"

  install -Dm644 etc/{*.conf*,*.map} "$pkgdir/etc/snort/"
  install -Dm644 "$srcdir/snort.conf.d" "$pkgdir/etc/conf.d/snort"
  install -Dm755 "$srcdir/snort" "$pkgdir/etc/rc.d/snort"

  # Service file.
  install -Dm644 ../snort.service "$pkgdir/usr/lib/systemd/system/snort.service"

  sed -i 's#/usr/local/lib/#/usr/lib/#' "$pkgdir/etc/snort/snort.conf"

  # Emerging threats rules.
  echo 'include $RULE_PATH/emerging.conf' >> "${pkgdir}/etc/snort/snort.conf"
  cp "$srcdir/rules"/* "$pkgdir/etc/snort/rules"
}
