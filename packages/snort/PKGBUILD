# This file is part of BlackArch Linux ( http://blackarch.org ).
# See COPYING for license details.

pkgname=snort
pkgver=2.9.11.1
pkgrel=3
pkgdesc='A lightweight network intrusion detection system.'
arch=('x86_64' 'armv6h' 'armv7h' 'aarch64')
groups=('blackarch' 'blackarch-defensive' 'blackarch-networking'
        'blackarch-forensic' 'blackarch-ids')
url='http://www.snort.org'
license=('GPL')
depends=('libdaq' 'libdnet' 'libpcap' 'openssl' 'pcre' 'zlib' 'libutil-linux'
         'libtirpc')
makedepends=('ca-certificates')
backup=('etc/conf.d/snort'
        'etc/snort/snort.conf'
        'etc/snort/threshold.conf'
        'etc/snort/reference.config'
        'etc/snort/classification.config')
options=('!makeflags' '!libtool')
install='snort.install'
source=("https://www.snort.org/downloads/snort/snort-${pkgver}.tar.gz"
        'http://rules.emergingthreats.net/open/snort-2.9.0/emerging.rules.tar.gz'
        'snort'
        'snort.conf.d'
        'snort.service'
        'configure.patch')
sha512sums=('7d3b7690694556512a6a43463529626f4ace4f85259ac7f50c6aa095bc3d65eca2270ded98b10a9a7dffc730c0e5b4ea0d42f6a3b16b437a3155262ba3394e10'
            '73c076de02fcac374b84651ba359216a32b4991d33b68dd361d1d19f92d4bfd48fa7033b3c2f73c5a72928f6833dcc83b9c3e44ffe8dff8d82f5a97b2adb048d'
            '96d3a27752c60cec9b188cdc358f3a38c0f6f0a179e16f3778abaf6385cc82169cdf0b41ea9bf39cfa64455e6ca5e484c2d952c72ced0901512b4750343ec5d9'
            '08341de9f302622b40917914e5e67c0f79c1800c463be7675bb019a3c033c3302a1be522ffd7a5b4759211f962ce4557557ea2653c4c89843dbd33b95a2e2e35'
            '8cd77b2046e5efd069aa4e236caa701391b2e3a91eac2e4ba21a60157c6f7b618d6552b831d5a4f7a2ba77612cf203ba7da1b672dade19c3396594e29aa7706f'
            'bf7c45400c955277cc3e460f9b9022cb6f8d1d21e38abdcd538dd95db8f92f475279a9947a5d31cb0b5aa3782fdd080939d5c69a36aeab7fa0428d5745a4d151')

prepare() {
  cd "$srcdir/"

  # temp fix for the fucking glibc upgrade by arch community...
  patch -p1 < configure.patch
}

build() {
  cd "$srcdir/snort-$pkgver"

  ./configure --prefix=/usr --sysconfdir=/etc/snort \
    --with-libpcap-includes=/usr/include/pcap \
    --with-daq-includes=/usr/include --with-daq-libraries=/usr/lib --enable-zlib

  make
}

package() {
  cd "$srcdir/snort-$pkgver"

  make DESTDIR="$pkgdir" install

  # Base directories.
  install -dm 755 "$pkgdir/etc/rc.d"
  install -dm 755 "$pkgdir/etc/snort/rules"
  install -dm 755 "$pkgdir/var/log/snort"

  install -Dm644 etc/{*.conf*,*.map} "$pkgdir/etc/snort/"
  install -Dm644 "$srcdir/snort.conf.d" "$pkgdir/etc/conf.d/snort"
  install -Dm755 "$srcdir/snort" "$pkgdir/etc/rc.d/snort"

  # Service file.
  install -Dm644 ../snort.service "$pkgdir/usr/lib/systemd/system/snort.service"

  sed -i 's#/usr/local/lib/#/usr/lib/#' "$pkgdir/etc/snort/snort.conf"

  # Emerging threats rules.
  echo 'include $RULE_PATH/emerging.conf' >> "${pkgdir}/etc/snort/snort.conf"
  cp "$srcdir/rules"/* "$pkgdir/etc/snort/rules"
}
