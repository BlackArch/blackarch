# This file is part of BlackArch Linux ( https://www.blackarch.org/ ).
# See COPYING for license details.

pkgname=snort
pkgver=2.9.15.1
_pkgver=2.9.15.1
pkgrel=1
pkgdesc='A lightweight network intrusion detection system.'
arch=('x86_64' 'aarch64')
groups=('blackarch' 'blackarch-defensive' 'blackarch-networking'
        'blackarch-forensic' 'blackarch-ids')
url='http://www.snort.org/'
license=('GPL')
depends=('libdaq' 'libdnet' 'libpcap' 'openssl' 'pcre' 'zlib' 'libutil-linux'
         'libtirpc' 'luajit')
makedepends=('ca-certificates')
backup=('etc/conf.d/snort'
        'etc/snort/snort.conf'
        'etc/snort/threshold.conf'
        'etc/snort/reference.config'
        'etc/snort/classification.config')
options=('!makeflags' '!libtool')
source=("https://www.snort.org/downloads/snort/$pkgname-$_pkgver.tar.gz"
        'http://rules.emergingthreats.net/open/snort-2.9.0/emerging.rules.tar.gz'
        'snort'
        'snort.conf.d'
        'snort.service')
sha512sums=('9940f5bcdcf04823f9cb5c3f8efda72f98f6a47bce9f40399dec9ec41d23a386c7f7e44861d82368de31546123b920f9fc617197bbf9c5e750724bf8b9e19590'
            'f6d62128ec75e0ccb55beb6c20a1934edda69be45884b68f6a5ce6ce8802972d54a9294f45c0269e3b4b00ed217e8746d1a41d040ca64d4292022a3e86b494f5'
            '96d3a27752c60cec9b188cdc358f3a38c0f6f0a179e16f3778abaf6385cc82169cdf0b41ea9bf39cfa64455e6ca5e484c2d952c72ced0901512b4750343ec5d9'
            '08341de9f302622b40917914e5e67c0f79c1800c463be7675bb019a3c033c3302a1be522ffd7a5b4759211f962ce4557557ea2653c4c89843dbd33b95a2e2e35'
            '8cd77b2046e5efd069aa4e236caa701391b2e3a91eac2e4ba21a60157c6f7b618d6552b831d5a4f7a2ba77612cf203ba7da1b672dade19c3396594e29aa7706f'
            '0e10f6e67a87834dea746aeba2fb5894fa52f1e21de0b1dc315fb36d169b1138f7a7af5354d7d0750f22103ec62722b78c746d74cf5ffcda791fbed84a1de6d2')
install="$pkgname.install"

prepare() {
  cd "$pkgname-$_pkgver"

  autoreconf -fi
}

build() {
  cd "$pkgname-$_pkgver"

  CPPFLAGS="$CPPFLAGS -I/usr/include/tirpc/" ./configure --prefix=/usr \
    --sysconfdir="/etc/$pkgname" --with-libpcap-includes=/usr/include/pcap \
    --with-daq-includes=/usr/include --with-daq-libraries=/usr/lib/daq \
    --disable-static-daq --enable-zlib --enable-sourcefire --enable-large-pcap \
    --enable-64bit-gcc --enable-linux-smp-stats --enable-large-pcap

  make
}

package() {
  cd "$pkgname-$_pkgver"

  make DESTDIR="$pkgdir" install

  install -dm 755 "$pkgdir/etc/rc.d"
  install -dm 755 "$pkgdir/etc/$pkgname/rules"
  install -dm 755 "$pkgdir/var/log/snort"

  install -Dm 644 etc/{*.conf*,*.map} "$pkgdir/etc/$pkgname/"
  install -Dm 644 "$srcdir/snort.conf.d" "$pkgdir/etc/conf.d/$pkgname"
  install -Dm 755 "$srcdir/snort" "$pkgdir/etc/rc.d/$pkgname"

  install -Dm 644 ../snort.service \
    "$pkgdir/usr/lib/systemd/system/snort.service"

  sed -i 's#/usr/local/lib/#/usr/lib/#' "$pkgdir/etc/$pkgname/snort.conf"

  # Emerging threats rules.
  echo 'include $RULE_PATH/emerging.conf' >> "$pkgdir/etc/$pkgname/snort.conf"
  cp "$srcdir/rules/"* "$pkgdir/etc/$pkgname/rules"
}

