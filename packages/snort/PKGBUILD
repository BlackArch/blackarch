# This file is part of BlackArch Linux ( http://blackarch.org ).
# See COPYING for license details.

pkgname=snort
pkgver=2.9.6.0
pkgrel=1
pkgdesc="A lightweight network intrusion detection system."
arch=('i686' 'x86_64' 'armv6h' 'armv7h')
url='http://www.snort.org'
license=('GPL')
depends=('libdaq-static' 'libdnet' 'libpcap' 'openssl' 'pcre' 'zlib')
makedepends=('ca-certificates')
backup=('etc/conf.d/snort'
        'etc/snort/snort.conf'
        'etc/snort/threshold.conf'
        'etc/snort/reference.config'
        'etc/snort/classification.config')
options=('!makeflags' '!libtool')
install='snort.install'
source=("http://www.snort.org/dl/snort-current/snort-${pkgver}.tar.gz"
        "http://rules.emergingthreats.net/open/snort-2.9.0/emerging.rules.tar.gz"
        'snort'
        'snort.conf.d'
        'snort.service')
md5sums=('18111f6de3989ca89add36077a7c2659'
         '69094bbcda469278b32528192cb10386'
         '361b8b9e40b9af0164f6b3e3da2e8277'
         'b4fb8a68490589cd34df93de7609bfac'
         '0d898dfe906fe45ad8562c500c36facb')

build() {
  cd "${srcdir}/snort-${pkgver}"

  ./configure --prefix=/usr --sysconfdir=/etc/snort --with-libpcap-includes=/usr/include/pcap \
              --with-daq-includes=/usr/include --with-daq-libraries=/usr/lib \
              --enable-zlib
  make
}

package() {
  cd "${srcdir}/snort-${pkgver}"

  make DESTDIR="${pkgdir}" install

  # Base directories.
  install -dm755 "${pkgdir}/etc/rc.d"
  install -dm755 "${pkgdir}/etc/snort/rules"

  install -dm755 "${pkgdir}/var/log/snort"
  install -Dm644 etc/{*.conf*,*.map} "${pkgdir}/etc/snort/"
  install -Dm644 "${srcdir}/snort.conf.d" "${pkgdir}/etc/conf.d/snort"
  install -Dm755 "${srcdir}/snort" "${pkgdir}/etc/rc.d/snort"

  # Service file.
  install -Dm644 ../snort.service $pkgdir/usr/lib/systemd/system/snort.service

  sed -i 's#/usr/local/lib/#/usr/lib/#' "${pkgdir}/etc/snort/snort.conf"

  # Emerging threats rules.
  echo 'include $RULE_PATH/emerging.conf' >> "${pkgdir}/etc/snort/snort.conf"
  cp ${srcdir}/rules/* "${pkgdir}/etc/snort/rules"
}
