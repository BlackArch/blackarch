# This file is part of BlackArch Linux ( http://blackarch.org ).
# See COPYING for license details.

pkgname='empire'
pkgver=1006.c8217e8
pkgrel=3
epoch=1
pkgdesc='A PowerShell and Python post-exploitation agent.'
groups=('blackarch' 'blackarch-automation')
arch=('any')
url='https://github.com/adaptivethreat/Empire'
license=('custom:unknown')
depends=('python2-m2crypto' 'swig' 'python2-iptools' 'python2-crypto'
         'python2-pydispatcher' 'python2-flask' 'python2-pyopenssl'
         'python2-zlib_wrapper' 'python2-pysqlite' 'python2-netifaces'
         'python2-argparse' 'python2-lxml' 'python2-dropbox' 'python2-macholib')
makedepends=('git')
source=('git+https://github.com/adaptivethreat/Empire.git'
        'pyopenssl.patch')
sha1sums=('SKIP'
          'f4c840060f6edd6fe688e7cceefa606fd09b786d')
install='empire.install'

pkgver() {
  cd "$srcdir/Empire"

  echo $(git rev-list --count HEAD).$(git rev-parse --short HEAD)
}

package() {
  cd "$srcdir/Empire"
  # fix deprecated calls to OpenSSL.rand.bytes until fixed upstream
  # ...fixed in a better way then restricting the pyopenssl version..
  patch -Np1 -i ../pyopenssl.patch

  mkdir -p "$pkgdir/usr/bin/"
  mkdir -p "$pkgdir/usr/share/empire"

  install -Dm644 -t "$pkgdir/usr/share/doc/empire/" README.md changelog
  install -Dm644 LICENSE "$pkgdir/usr/share/licenses/empire/LICENSE"

  rm changelog LICENSE README.md

  cp -a * "$pkgdir/usr/share/empire"

  cat > "$pkgdir/usr/bin/empire" << EOF
#!/bin/sh
cd /usr/share/empire/
exec python2 empire "\${@}"
EOF

  chmod +x "$pkgdir/usr/bin/empire"
}
