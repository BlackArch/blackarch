# This file is part of BlackArch Linux ( https://www.blackarch.org/ ).
# See COPYING for license details.

pkgname=empire
pkgver=1920.998b490
pkgrel=1
epoch=2
pkgdesc='A PowerShell and Python post-exploitation agent.'
groups=('blackarch' 'blackarch-automation')
arch=('any')
url='https://github.com/BC-SECURITY/Empire'
license=('BSD')
depends=('python' 'python-urllib3' 'python-requests' 'python-iptools'
         'python-pydispatcher' 'python-flask' 'python-macholib' 'python-dropbox'
         'python-zlib_wrapper' 'python-netifaces' 'python-m2crypto'
         'python-jinja' 'python-cryptography' 'python-pefile' 'python-xlutils'
         'python-pyminifier' 'python-pycryptodome' 'python-simplejson'
         'python-xlrd' 'python-xlwt' 'java-environment')
makedepends=('git')
source=("$pkgname::git+https://github.com/BC-SECURITY/Empire.git"
        'https://files.pythonhosted.org/packages/40/d0/8efd61531f338a89b4efa48fcf1972d870d2b67a7aea9dcf70783c8464dc/pyOpenSSL-19.0.0.tar.gz'
        'setup_database.py.patch')
sha512sums=('SKIP'
            'a7e12bf5be725ccebd1223d567a9b9e8bd35f79cbf3a0409108fa6f00e10ab137dd6876369e9dede02b2abc7d700786cc905532ca5c31b6e246f6df8b24e2818'
            '9ffc3dc5d6a1dfd5b71cd22a0878a0231958938cb939e5fda5467c301fb4837ff96a299c2cbf2b2cc232fd8dd340c9086130e1067870c8c4272d7cc05e3954b4')
install="$pkgname.install"

pkgver() {
  cd $pkgname

  echo $(git rev-list --count HEAD).$(git rev-parse --short HEAD)
}

prepare() {
  #patch -p1  < "$srcdir/setup_database.py.patch"

  cd $pkgname
  # temp dirty fix
  cp -r "$srcdir/pyOpenSSL-19.0.0/src/OpenSSL" OpenSSL
}

package() {
  cd $pkgname

  install -dm 755 "$pkgdir/usr/bin/"
  install -dm 755 "$pkgdir/usr/share/$pkgname"

  install -Dm 644 -t "$pkgdir/usr/share/doc/$pkgname/" README.md changelog
  install -Dm 644 LICENSE "$pkgdir/usr/share/licenses/$pkgname/LICENSE"

  rm changelog LICENSE README.md

  cp -a * "$pkgdir/usr/share/$pkgname/"

  cat > "$pkgdir/usr/bin/$pkgname" << EOF
#!/bin/sh
cd /usr/share/$pkgname
exec python $pkgname "\$@"
EOF

  chmod +x "$pkgdir/usr/bin/$pkgname"
}

