# This file is part of BlackArch Linux ( https://www.blackarch.org/ ).
# See COPYING for license details.

pkgname=empire
pkgver=2289.9259e510
pkgrel=1
epoch=2
pkgdesc='A PowerShell and Python post-exploitation agent.'
groups=('blackarch' 'blackarch-automation')
arch=('any')
url='https://github.com/BC-SECURITY/Empire'
license=('BSD')
depends=('python' 'python-urllib3' 'python-requests' 'python-iptools'
         'python-pydispatcher' 'python-flask' 'python-macholib' 'python-dropbox'
         'python-zlib_wrapper' 'python-netifaces' 'python-m2crypto'
         'python-jinja' 'python-cryptography' 'python-pefile' 'python-xlutils'
         'python-pyminifier' 'python-pycryptodome' 'python-simplejson'
         'python-xlrd' 'python-xlwt' 'python-bcrypt' 'java-environment'
         'python-flask-socketio')
makedepends=('git')
source=("$pkgname::git+https://github.com/BC-SECURITY/Empire.git"
        'https://files.pythonhosted.org/packages/0d/1d/6cc4bd4e79f78be6640fab268555a11af48474fac9df187c3361a1d1d2f0/pyOpenSSL-19.1.0.tar.gz'
        'setup_database.py.patch')
sha512sums=('SKIP'
            '4acd96f287d72eb11bd812697d28cd6eb6a96a4653248b65f967187830a6b17cc1254775a18a3405469f3d45abdae6f02d165f2f35f035f3174c2826fba82916'
            '9ffc3dc5d6a1dfd5b71cd22a0878a0231958938cb939e5fda5467c301fb4837ff96a299c2cbf2b2cc232fd8dd340c9086130e1067870c8c4272d7cc05e3954b4')
install="$pkgname.install"

pkgver() {
  cd $pkgname

  echo $(git rev-list --count HEAD).$(git rev-parse --short HEAD)
}

prepare() {
  #patch -p1  < "$srcdir/setup_database.py.patch"

  cd $pkgname

  # need by empire
  cp -r "$srcdir/pyOpenSSL-19.1.0/src/OpenSSL" OpenSSL
}

package() {
  cd $pkgname

  install -dm 755 "$pkgdir/usr/bin/"
  install -dm 755 "$pkgdir/usr/share/$pkgname"

  install -Dm 644 -t "$pkgdir/usr/share/doc/$pkgname/" README.md changelog
  install -Dm 644 LICENSE "$pkgdir/usr/share/licenses/$pkgname/LICENSE"

  rm changelog LICENSE README.md

  cp -a * "$pkgdir/usr/share/$pkgname/"

  cat > "$pkgdir/usr/bin/$pkgname" << EOF
#!/bin/sh
cd /usr/share/$pkgname
exec python $pkgname "\$@"
EOF

  chmod +x "$pkgdir/usr/bin/$pkgname"
}

