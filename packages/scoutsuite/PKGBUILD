# This file is part of BlackArch Linux ( https://www.blackarch.org/ ).
# See COPYING for license details.

pkgname=scoutsuite
pkgver=3690.5a498fbd
pkgrel=1
pkgdesc='Multi-Cloud Security Auditing Tool.'
groups=('blackarch' 'blackarch-scanner')
arch=('any')
url='https://github.com/nccgroup/ScoutSuite/releases'
license=('GPL3')
depends=('python' 'python-dateutil' 'python-netaddr'
         'python-boto3' 'python-opinel' 'python-grpcio'
         'python-oauth2client' 'python-google-api-python-client'
         'python-google-cloud-resource-manager' 'python-google-cloud-storage'
         'python-google-cloud-container' 'python-google-cloud-logging'
         'python-google-cloud-monitoring' 'python-azure-mgmt-storage')
makedepends=('git' 'python-setuptools')
source=('git+https://github.com/nccgroup/ScoutSuite.git')
sha512sums=('SKIP')
_dir_name_=ScoutSuite

pkgver() {
  cd $_dir_name_

  echo $(git rev-list --count HEAD).$(git rev-parse --short HEAD)
}

build() {
  cd $_dir_name_

  python setup.py build
}

package() {
  cd $_dir_name_

  python setup.py install --prefix=/usr --root="$pkgdir" --optimize=1 \
    --skip-build

  mv "$pkgdir/usr/bin/Scout" "$pkgdir/usr/bin/scout"
  mv "$pkgdir/usr/bin/Scout2Listall" "$pkgdir/usr/bin/scout2listall"
  mv "$pkgdir/usr/bin/Scout2RulesGenerator" \
    "$pkgdir/usr/bin/scout2rulesgenerator"

  rm -rf "$pkgdir/usr/lib/python3.7/site-packages/opinel"
}

