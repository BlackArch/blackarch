# This file is part of BlackArch Linux ( https://www.blackarch.org/ ).
# See COPYING for license details.

pkgname=jefferson
pkgver=v0.4.6.r6.gbacba15
pkgrel=1
pkgdesc='JFFS2 filesystem extraction tool.'
arch=('any')
groups=('blackarch' 'blackarch-forensic' 'blackarch-reversing')
url='https://github.com/onekey-sec/jefferson'
license=('MIT')
depends=('python' 'python-click' 'python-cstruct' 'python-lzallright')
makedepends=('git' 'python-build' 'python-pip')
source=("git+https://github.com/onekey-sec/$pkgname.git")
sha512sums=('SKIP')

pkgver() {
  cd $pkgname

  ( set -o pipefail
    git describe --long --tags --abbrev=7 2>/dev/null |
      sed 's/\([^-]*-g\)/r\1/;s/-/./g' ||
    printf "%s.%s" "$(git rev-list --count HEAD)" \
      "$(git rev-parse --short=7 HEAD)"
  )
}

build() {
  cd $pkgname

  python -m build --wheel --outdir="$startdir/dist"
}

package() {
  cd $pkgname

  pip install \
    --verbose \
    --disable-pip-version-check \
    --no-warn-script-location \
    --ignore-installed \
    --no-compile \
    --no-deps \
    --root="$pkgdir" \
    --prefix=/usr \
    --no-index \
    --find-links="file://$startdir/dist" \
    $pkgname
}

