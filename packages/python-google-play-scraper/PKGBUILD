# This file is part of BlackArch Linux ( https://www.blackarch.org/ ).
# See COPYING for license details.

pkgbase=python-google-play-scraper
pkgname=('python2-google-play-scraper' 'python-google-play-scraper')
_pkgname=google-play-scraper
pkgver=0.0.2.0
pkgrel=1
pkgdesc='Provides APIs to easily crawl the Google Play Store for Python without no external dependencies.'
arch=('any')
url='https://pypi.org/project/google-play-scraper/#files'
license=('MIT')
makedepends=('python2-setuptools' 'python-setuptools')
source=("https://files.pythonhosted.org/packages/f0/c9/909a39e097dd0bb77d8641b617fd303bc571577963925b4d35106a9486c5/$_pkgname-$pkgver.tar.gz")
sha512sums=('59f43232e71234626c511efcf212cc3af7b55aacf4f6fb1204e79a9228b3a6c99e3272c21bd4cb69072b7e9873b3d1e29a98a7018c1d075be8c8ccbe77683eef')

prepare() {
  cp -a "$_pkgname-$pkgver"{,-2}
}

build() {
  cd "$_pkgname-$pkgver-2"

  python2 setup.py build

  cd "$srcdir/$_pkgname-$pkgver"

  python setup.py build
}

package_python2-google-play-scraper() {
  depends=('python2')

  cd "$_pkgname-$pkgver-2"

  python2 setup.py install --prefix=/usr --root="$pkgdir" -O1 --skip-build
}

package_python-google-play-scraper() {
  depends=('python')

  cd "$_pkgname-$pkgver"

  python setup.py install --prefix=/usr --root="$pkgdir" -O1 --skip-build
}

