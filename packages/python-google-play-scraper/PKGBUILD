# This file is part of BlackArch Linux ( https://www.blackarch.org/ ).
# See COPYING for license details.

pkgbase=python-google-play-scraper
pkgname=('python2-google-play-scraper' 'python-google-play-scraper')
_pkgname=google-play-scraper
pkgver=0.0.1.1
pkgrel=1
pkgdesc='Provides APIs to easily crawl the Google Play Store for Python without no external dependencies.'
arch=('any')
url='https://pypi.org/project/google-play-scraper/#files'
license=('MIT')
makedepends=('python2-setuptools' 'python-setuptools')
source=("https://files.pythonhosted.org/packages/3b/44/c5aa28b23ddb81177fec531a543a9dc9ed4066aa74c400dc2cc677d6a0eb/$_pkgname-$pkgver.tar.gz")
sha512sums=('de4d63234a8b829e4e51b1735bd4c2246df83756bb6a8a4e770861472c31f3a3eda33c37b98c232feefb5224a87e45e45028c5bb68b6af4fd6994e595c4ec89f')

prepare() {
  cp -a "$_pkgname-$pkgver"{,-2}
}

build() {
  cd "$_pkgname-$pkgver-2"

  python2 setup.py build

  cd "$srcdir/$_pkgname-$pkgver"

  python setup.py build
}

package_python2-google-play-scraper() {
  depends=('python2')

  cd "$_pkgname-$pkgver-2"

  python2 setup.py install --prefix=/usr --root="$pkgdir" -O1 --skip-build
}

package_python-google-play-scraper() {
  depends=('python')

  cd "$_pkgname-$pkgver"

  python setup.py install --prefix=/usr --root="$pkgdir" -O1 --skip-build
}

