# This file is part of BlackArch Linux ( https://www.blackarch.org/ ).
# See COPYING for license details.

pkgbase=python-google-play-scraper
pkgname=('python2-google-play-scraper' 'python-google-play-scraper')
_pkgname=google-play-scraper
pkgver=0.0.2.4
pkgrel=1
pkgdesc='Provides APIs to easily crawl the Google Play Store for Python without no external dependencies.'
arch=('any')
url='https://pypi.org/project/google-play-scraper/#files'
license=('MIT')
makedepends=('python2-setuptools' 'python-setuptools')
source=("https://files.pythonhosted.org/packages/c8/40/92d6ce72538a26fdec7968dab42f60dab359d1f28d8589bb189968f984a4/$_pkgname-$pkgver.tar.gz")
sha512sums=('ab0ae7737fd2075cdf2abd7cf70d38a84bdadb563175c2d8cbfa1367735398e8fca4f0d099ee88887c51cf347037a99d6134252a3baa1496b34990776ff3dd9d')

prepare() {
  cp -a "$_pkgname-$pkgver"{,-2}
}

build() {
  cd "$_pkgname-$pkgver-2"

  python2 setup.py build

  cd "$srcdir/$_pkgname-$pkgver"

  python setup.py build
}

package_python2-google-play-scraper() {
  depends=('python2')

  cd "$_pkgname-$pkgver-2"

  python2 setup.py install --prefix=/usr --root="$pkgdir" -O1 --skip-build
}

package_python-google-play-scraper() {
  depends=('python')

  cd "$_pkgname-$pkgver"

  python setup.py install --prefix=/usr --root="$pkgdir" -O1 --skip-build
}

