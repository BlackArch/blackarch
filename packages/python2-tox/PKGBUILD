# This file is part of BlackArch Linux ( https://www.blackarch.org/ ).
# See COPYING for license details.

pkgname=python2-tox
_pkgname=tox
pkgver=3.14.1
pkgrel=1
pkgdesc='A generic virtualenv management and test command line tool.'
url='https://pypi.org/project/tox/#files'
arch=('any')
license=('MIT')
depends=('python2' 'python2-virtualenv' 'python2-pluggy' 'python2-toml'
         'python2-py' 'python2-filelock')
makedepends=('python2-setuptools')
source=("https://files.pythonhosted.org/packages/77/de/a0cd7de91f362a59ec689808ed2993b0578642247418840ae240cfdc4c66/$_pkgname-$pkgver.tar.gz")
sha512sums=('a5929d41f52bff3bc1dcab852b321e268786dcb69d97f4d9acfd661241a64dfdf342cd43c0a393683978091c8b77d9a5caea4550ca9f30590097f750d8c1d914')

build() {
  cd "$_pkgname-$pkgver"

  export LC_CTYPE=en_US.UTF-8

  python2 setup.py build
}

package() {
  cd "$_pkgname-$pkgver"

  python2 setup.py install --root="$pkgdir" --prefix=/usr -O1 --skip-build

  for i in "$pkgdir/usr/bin/"*; do
    mv $i "${i}2"
  done
}

