# This file is part of BlackArch Linux ( http://blackarch.org ).
# See COPYING for license details.

pkgname='fakenet-ng'
pkgver=264.3f096a1
pkgrel=1
pkgdesc='Next Generation Dynamic Network Analysis Tool.'
groups=('blackarch' 'blackarch-malware' 'blackarch-networking'
        'blackarch-sniffer' 'blackarch-proxy')
arch=('any')
url='https://github.com/fireeye/flare-fakenet-ng'
license=('Apache2')
depends=('python2' 'python2-dpkt' 'python2-pyopenssl' 'python2-dnspython'
         'python2-dnslib' 'python2-pydivert' 'python2-win_inet_pton'
         'python2-netfilterqueue' 'python2-pyftpdlib')
makedepends=('git')
source=('git+https://github.com/fireeye/flare-fakenet-ng.git')
sha1sums=('SKIP')

pkgver() {
  cd "$srcdir/flare-fakenet-ng"

  echo $(git rev-list --count HEAD).$(git rev-parse --short HEAD)
}

build() {
  cd "$srcdir/flare-fakenet-ng"

  python2 setup.py build
}

package() {
  cd "$srcdir/flare-fakenet-ng"

  python2 setup.py install --root="$pkgdir"

  install -Dm644 -t "$pkgdir/usr/share/doc/fakenet-ng/" README.md
  install -Dm644 LICENSE.txt "$pkgdir/usr/share/licenses/fakenet-ng/LICENSE.txt"
}
