# This file is part of BlackArch Linux ( https://www.blackarch.org/ ).
# See COPYING for license details.

pkgname=gvmd
pkgver=22.4.0
pkgrel=2
pkgdesc='The Greenbone Vulnerability Manager is the central management service between security scanners and the user clients.'
arch=('x86_64')
groups=('blackarch' 'blackarch-scanner')
url='https://github.com/greenbone/'
license=('GPL')
depends=('gvm-libs' 'glib2' 'python' 'gnutls' 'libbsd' 'libical' 'postgresql'
  'rsync' 'gpgme')
makedepends=('git' 'cmake')
source=("${pkgname}-${pkgver}.tar.gz::https://github.com/greenbone/gvmd/archive/refs/tags/v$pkgver.tar.gz")
sha512sums=('b28ad93842cb408ebbd85e9cc924c87fd27cc7c1f2f92cde5c8623b1782bcfe795227f31b5f80db7b56ea450670223c4c9c63df298f32b7d5cc633a76be32549')

build() {
  cd "${pkgname}-${pkgver}"

  export INSTALL_PREFIX=/usr
  export PKG_CONFIG_PATH=/usr/local/lib/pkgconfig

  sed -i 's/#include <postgresql\/libpq-fe.h>/#include <libpq-fe.h>/' ./src/sql_pg.c

  cmake "-DCMAKE_BUILD_TYPE=Release \
    -DCMAKE_INSTALL_PREFIX=$INSTALL_PREFIX \
    -DLOCALSTATEDIR=/var \
    -DSYSCONFDIR=/etc \
    -DGVM_DATA_DIR=/var \
    -DGVMD_RUN_DIR=/run/gvmd \
    -DOPENVAS_DEFAULT_SOCKET=/run/ospd/ospd-openvas.sock \
    -DGVM_FEED_LOCK_PATH=/var/lib/gvm/feed-update.lock \
    -DSYSTEMD_SERVICE_DIR=/usr/lib/systemd/system \
    -DLOGROTATE_DIR=/etc/logrotate.d \
    -DCMAKE_SKIP_INSTALL_RPATH=YES \
    --debug-out ."

  make "-j$(nproc)"
}

package() {
  cd "${pkgname}-${pkgver}"

  make DESTDIR="$pkgdir" install

  install -dm 755 "$pkgdir/usr/lib/systemd/system"
  install -m 644 "$pkgdir/lib/systemd/system/gvmd.service" "$pkgdir/usr/lib/systemd/system/"

  install -Dm 644 -t "$pkgdir/usr/share/doc/$pkgname/" *.md

  rm -fR "$pkgdir/usr/local/share/man"
  rm -fR "$pkgdir/lib"
}
