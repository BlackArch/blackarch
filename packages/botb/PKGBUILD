# This file is part of BlackArch Linux ( https://www.blackarch.org/ ).
# See COPYING for license details.

pkgname=botb
pkgver=61.585e741
pkgrel=2
pkgdesc='A container analysis and exploitation tool for pentesters and engineers.'
arch=('x86_64' 'aarch64')
groups=('blackarch' 'blackarch-exploitation' 'blackarch-scanner')
url='https://github.com/brompwnie/botb'
license=('GPL3')
depends=('glibc')
makedepends=('git' 'go')
source=("git+https://github.com/brompwnie/$pkgname.git")
sha512sums=('SKIP')

pkgver() {
  cd $pkgname

  echo $(git rev-list --count HEAD).$(git rev-parse --short HEAD)
}

build() {
  cd $pkgname

  GOPATH="$srcdir" go mod download
  GOPATH="$srcdir" go build \
    -trimpath \
    -buildmode=pie \
    -mod=readonly \
    -modcacherw \
    -ldflags "-s -w" \
    -o $pkgname .
}

package() {
  cd $pkgname

  install -dm 755 "$pkgdir/usr/share/$pkgname"

  install -Dm 755 $pkgname "$pkgdir/usr/bin/$pkgname"
  install -Dm 644 -t "$pkgdir/usr/share/$pkgname/" cfg.yml
  install -Dm 644 -t "$pkgdir/usr/share/doc/$pkgname/" README.md
}

