# This file is part of BlackArch Linux ( https://www.blackarch.org/ ).
# See COPYING for license details.

pkgname=creds
pkgver=17.1ec8297
pkgrel=4
epoch=1
groups=('blackarch' 'blackarch-sniffer')
pkgdesc='Harvest FTP/POP/IMAP/HTTP/IRC credentials along with interesting data from each of the protocols.'
arch=('any')
url='https://github.com/DanMcInerney/creds.py'
license=('custom')
depends=('python2' 'python2-scapy')
makedepends=('git')
source=('git+https://github.com/DanMcInerney/creds.py.git')
sha512sums=('SKIP')

pkgver() {
  cd creds.py

  echo $(git rev-list --count HEAD).$(git rev-parse --short HEAD)
}

package() {
  cd creds.py

  install -dm 755 "$pkgdir/usr/bin"

  install -Dm 755 creds.py "$pkgdir/usr/bin/creds-py"
  install -Dm 744 README.md "$pkgdir/usr/share/doc/$pkgname/README.md"
}

