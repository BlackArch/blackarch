# This file is part of BlackArch Linux ( http://blackarch.org ).
# See COPYING for license details.
pkgname='ruby-binproxy'
_gemname='binproxy'
pkgver='1.0.0'
pkgrel=3
pkgdesc="BinProxy"
arch=('any')
url='https://github.com/nccgroup/BinProxy'
license=("")
depends=('ruby' 'ruby-activesupport-4' 'ruby-bindata-2.1' 'ruby-clipboard-1.0' 'ruby-colorize-0.7' 'ruby-eventmachine-1.0' 'ruby-haml' 'ruby-msgpack' 'ruby-rbkb' 'ruby-sass' 'ruby-sinatra' 'ruby-sinatra-websocket' 'ruby-thin-1.6' 'ruby-trollop')
groups=('blackarch' 'blackarch-proxy' 'blackarch-networking')
replaces=('binproxy')
conflicts=('binproxy')
provides=('binproxy')
options=('!emptydirs')
source=("https://rubygems.org/downloads/$_gemname-$pkgver.gem" )
noextract=("$_gemname-$pkgver.gem")
sha1sums=('65d941c8b99bd5ea0b1f296602c8e85f7416206d' )


package() {
  local _gemdir="$(ruby -rubygems -e 'puts Gem.default_dir')"
  gem install --ignore-dependencies --no-document --no-user-install -i "$pkgdir/$_gemdir" -n "$pkgdir"/usr/bin $_gemname-$pkgver.gem 
  rm "$pkgdir/$_gemdir/cache/$_gemname-$pkgver.gem"

  # Preserve library and other required directories. Remove junk that gem installs.
  local _copydir=`mktemp -d -t $_gemname.XXX`
  chmod 755 $_copydir
  (cd "$pkgdir/$_gemdir/gems/$_gemname-$pkgver"/ && cp -r --parents lib bin $_copydir)
  rm -rf "$pkgdir/$_gemdir/gems/$_gemname-$pkgver"
  mv $_copydir "$pkgdir/$_gemdir/gems/$_gemname-$pkgver"

}
