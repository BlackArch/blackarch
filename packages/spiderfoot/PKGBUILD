# This file is part of BlackArch Linux ( http://blackarch.org ).
# See COPYING for license details.

pkgname=spiderfoot
pkgver=2.12.0
pkgrel=1
groups=('blackarch' 'blackarch-recon')
pkgdesc='The Open Source Footprinting Tool.'
url='http://spiderfoot.net/'
arch=('any')
license=('GPL2')
depends=('python2' 'python2-mako' 'python2-cherrypy' 'python2-m2crypto'
         'python2-lxml' 'python2-netaddr' 'swig')
source=("https://github.com/smicallef/spiderfoot/archive/v${pkgver}-final.tar.gz")
sha512sums=('5657eecdf68078019dbae490f35ba868483dd92993718b86bdbe819d5fc0c01e226bfadf7e6f0dc900d7f017351bd66e1855ebfb4e6964b5f3b39bd5de34c0dd')

prepare() {
  cd "$srcdir/spiderfoot-$pkgver-final"

  #sed -i 's|treebuilders._base|treebuilders.base|g' ext/bs4/builder/_html5lib.py
}

package() {
  cd "$srcdir/spiderfoot-$pkgver-final"

  install -dm 755 "$pkgdir/usr/bin"
  install -dm 755 "$pkgdir/usr/share/spiderfoot"

  install -Dm644 -t "$pkgdir/usr/share/doc/spiderfoot/" README.md THANKYOU \
    VERSION
  install -Dm644 LICENSE "$pkgdir/usr/share/licenses/spiderfoot/LICENSE"

  rm README.md LICENSE THANKYOU VERSION

  cp -a * "$pkgdir/usr/share/spiderfoot"

cat > "$pkgdir/usr/bin/spiderfoot" << EOF
#!/bin/sh
cd /usr/share/spiderfoot/
exec python2 sf.py "\${@}"
EOF

  chmod +x "$pkgdir/usr/bin/spiderfoot"
}
