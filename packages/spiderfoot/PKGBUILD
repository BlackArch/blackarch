# This file is part of BlackArch Linux ( https://www.blackarch.org/ ).
# See COPYING for license details.

pkgname=spiderfoot
pkgver=3.5
pkgrel=1
groups=('blackarch' 'blackarch-recon')
pkgdesc='The Open Source Footprinting Tool.'
url='https://github.com/smicallef/spiderfoot'
arch=('any')
license=('GPL2')
depends=('python' 'python-adblockparser' 'python-dnspython' 'python-exifread'
         'python-cherrypy' 'python-mako' 'python-beautifulsoup4' 'python-lxml'
         'python-netaddr' 'python-pysocks' 'python-requests' 'python-ipwhois'
         'python-ipaddr' 'python-phonenumbers' 'python-pygexf' 'python-pypdf2'
         'python-stem' 'python-python-whois' 'python-future' 'python-secure'
         'python-pyopenssl' 'python-python-docx' 'python-python-pptx'
         'python-networkx' 'python-cryptography' 'python-publicsuffixlist'
         'python-cherrypy-cors' 'python-httpagentparser')
source=("https://github.com/smicallef/$pkgname/archive/v$pkgver.tar.gz")
sha512sums=('9c9c1e1c22d95cccac0a2fcbd41db8ecb2a7807864fe67be0c5d2f56fa9a09aefb7b8c9d4cd2778b8bf8bd9f22a4efc9836a34398903b4464c38c49861765539')

prepare() {
  cd "$pkgname-$pkgver"

  #sed -i 's|treebuilders._base|treebuilders.base|g' ext/bs4/builder/_html5lib.py
}

build() {
  cd "$pkgname-$pkgver"

  python -m compileall *.py
}

package() {
  cd "$pkgname-$pkgver"

  install -dm 755 "$pkgdir/usr/bin"
  install -dm 755 "$pkgdir/usr/share/$pkgname"

  install -Dm 644 -t "$pkgdir/usr/share/doc/$pkgname/" README.md THANKYOU \
    VERSION
  install -Dm 644 LICENSE "$pkgdir/usr/share/licenses/$pkgname/LICENSE"

  rm README.md LICENSE THANKYOU VERSION

  cp -a * "$pkgdir/usr/share/$pkgname/"

  cat > "$pkgdir/usr/bin/$pkgname" << EOF
#!/bin/sh
cd /usr/share/$pkgname
exec python sf.py "\$@"
EOF

  chmod +x "$pkgdir/usr/bin/$pkgname"

  cat > "$pkgdir/usr/bin/$pkgname-cli" << EOF
#!/bin/sh
cd /usr/share/$pkgname
exec python sfcli.py "\$@"
EOF

  chmod +x "$pkgdir/usr/bin/$pkgname-cli"
}

