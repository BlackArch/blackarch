# This file is part of BlackArch Linux ( https://www.blackarch.org/ ).
# See COPYING for license details.

pkgname=spiderfoot
pkgver=3.2.1
pkgrel=3
groups=('blackarch' 'blackarch-recon')
pkgdesc='The Open Source Footprinting Tool.'
url='https://github.com/smicallef/spiderfoot'
arch=('any')
license=('GPL2')
depends=('python' 'python-adblockparser' 'python-dnspython' 'python-exifread'
         'python-cherrypy' 'python-mako' 'python-beautifulsoup4' 'python-lxml'
         'python-netaddr' 'python-pysocks' 'python-requests' 'python-ipwhois'
         'python-ipaddr' 'python-phonenumbers' 'python-pygexf' 'python-pypdf2'
         'python-stem' 'python-python-whois' 'python-future' 'python-secure'
         'python-pyopenssl' 'python-python-docx' 'python-python-pptx'
         'python-networkx' 'python-cryptography' 'python-publicsuffixlist')
source=("https://github.com/smicallef/$pkgname/archive/v$pkgver.tar.gz")
sha512sums=('f5fe0c69fb3d61536152eef3f3f07cb2ab924cc913e20d623193a523bf7491bb7103cd6837720c19a4306404082573692df85ee3489258b6369a2c92ca543c2d')

prepare() {
  cd "$pkgname-$pkgver"

  #sed -i 's|treebuilders._base|treebuilders.base|g' ext/bs4/builder/_html5lib.py
}

package() {
  cd "$pkgname-$pkgver"

  install -dm 755 "$pkgdir/usr/bin"
  install -dm 755 "$pkgdir/usr/share/$pkgname"

  install -Dm 644 -t "$pkgdir/usr/share/doc/$pkgname/" README.md THANKYOU \
    VERSION
  install -Dm 644 LICENSE "$pkgdir/usr/share/licenses/$pkgname/LICENSE"

  rm README.md LICENSE THANKYOU VERSION

  cp -a * "$pkgdir/usr/share/$pkgname/"

  cat > "$pkgdir/usr/bin/$pkgname" << EOF
#!/bin/sh
cd /usr/share/$pkgname
exec python sf.py "\$@"
EOF

  chmod +x "$pkgdir/usr/bin/$pkgname"

  cat > "$pkgdir/usr/bin/$pkgname-cli" << EOF
#!/bin/sh
cd /usr/share/$pkgname
exec python sfcli.py "\$@"
EOF

  chmod +x "$pkgdir/usr/bin/$pkgname-cli"
}

