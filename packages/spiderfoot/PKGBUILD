# This file is part of BlackArch Linux ( https://www.blackarch.org/ ).
# See COPYING for license details.

pkgname=spiderfoot
pkgver=3.0
pkgrel=1
groups=('blackarch' 'blackarch-recon')
pkgdesc='The Open Source Footprinting Tool.'
url='http://spiderfoot.net/'
arch=('any')
license=('GPL2')
depends=('python' 'python-adblockparser' 'python-dnspython' 'python-exifread'
         'python-cherrypy' 'python-mako' 'python-beautifulsoup4' 'python-lxml'
         'python-netaddr' 'python-pysocks' 'python-requests' 'python-ipwhois'
         'python-ipaddr' 'python-phonenumbers' 'python-pygexf' 'python-pypdf2'
         'python-stem' 'python-python-whois' 'python-future' 'python-secure'
         'python-pyopenssl' 'python-python-docx' 'python-python-pptx'
         'python-networkx' 'python-cryptography')
source=("https://github.com/smicallef/$pkgname/archive/v$pkgver.tar.gz")
sha512sums=('abf64b6b819f8535f87237649be677af27aaddc729897eff8d4c27bd441e32862a42f8b84cf61ad3a0b9e1fc940fddf9d2e47d7decfcb62812239c5a9e01850a')

prepare() {
  cd "$pkgname-$pkgver"

  #sed -i 's|treebuilders._base|treebuilders.base|g' ext/bs4/builder/_html5lib.py
}

package() {
  cd "$pkgname-$pkgver"

  install -dm 755 "$pkgdir/usr/bin"
  install -dm 755 "$pkgdir/usr/share/$pkgname"

  install -Dm 644 -t "$pkgdir/usr/share/doc/$pkgname/" README.md THANKYOU \
    VERSION
  install -Dm 644 LICENSE "$pkgdir/usr/share/licenses/$pkgname/LICENSE"

  rm README.md LICENSE THANKYOU VERSION

  cp -a * "$pkgdir/usr/share/$pkgname/"

  cat > "$pkgdir/usr/bin/$pkgname" << EOF
#!/bin/sh
cd /usr/share/$pkgname
exec python sf.py "\$@"
EOF

  chmod +x "$pkgdir/usr/bin/$pkgname"

  cat > "$pkgdir/usr/bin/$pkgname-cli" << EOF
#!/bin/sh
cd /usr/share/$pkgname
exec python sfcli.py "\$@"
EOF

  chmod +x "$pkgdir/usr/bin/$pkgname-cli"
}

