# This file is part of BlackArch Linux ( https://www.blackarch.org/ ).
# See COPYING for license details.

pkgname=killerbee
pkgver=313.9bfe694
pkgrel=1
groups=('blackarch' 'blackarch-exploitation' 'blackarch-wireless')
pkgdesc='Framework and tools for exploiting ZigBee and IEEE 802.15.4 networks.'
arch=('x86_64' 'aarch64')
url='https://github.com/riverloopsec/killerbee'
license=('BSD')
depends=('python2' 'python2-pycryptodome' 'python2-pyusb' 'pygtk' 'python2-cairo'
         'python2-pyserial' 'python2-scapy' 'python2-rangeparser')
makedepends=('git' 'python2-setuptools')
conflicts=('killerbee-svn')
provides=('killerbee-svn')
options=(!emptydirs)
source=("git+https://github.com/riverloopsec/$pkgname.git")
sha512sums=('SKIP')

pkgver() {
  cd $pkgname

  echo $(git rev-list --count HEAD).$(git rev-parse --short HEAD)
}

build() {
  cd $pkgname

  python2 setup.py build
}

package() {
  cd $pkgname

  python2 setup.py install --root="$pkgdir" --prefix=/usr -O1 --skip-build
}

