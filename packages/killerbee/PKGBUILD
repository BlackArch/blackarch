# This file is part of BlackArch Linux ( http://blackarch.org ).
# See COPYING for license details.

pkgname='killerbee'
pkgrel=2
pkgver=99
groups=('blackarch' 'blackarch-exploitation' 'blackarch-wireless')
pkgdesc='Framework and tools for exploiting ZigBee and IEEE 802.15.4 networks.'
arch=('x86_64' 'armv6h' 'armv7h' 'aarch64')
url='https://code.google.com/p/killerbee/'
license=('BSD')
depends=('python2' 'python2-crypto' 'python2-pyusb' 'pygtk' 'python2-cairo'
         'python2-pyserial')
makedepends=('subversion')
conflicts=('killerbee-svn')
provides=('killerbee-svn')
options=(!emptydirs)
source=('svn+http://killerbee.googlecode.com/svn/trunk/')
sha1sums=('SKIP')

pkgver() {
  cd "$srcdir/trunk"

  svnversion | tr -d [A-z]
}

prepare() {
  cd "$srcdir/trunk/killerbee"

  sed -i 's|#!/usr/bin/env python|#!/usr/bin/env python2|' tools/*
}

package() {
  cd "$srcdir/trunk/killerbee"

  python2 setup.py install --root="$pkgdir" --optimize=1
}
