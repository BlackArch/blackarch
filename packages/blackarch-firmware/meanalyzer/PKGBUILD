# This file is part of BlackArch Linux ( https://www.blackarch.org/ ).
# See COPYING for license details.

pkgname=meanalyzer
pkgver=1.148.4
_rev=209
pkgrel=1
pkgdesc='Intel Engine Firmware Analysis Tool.'
arch=('x86_64')
groups=('blackarch' 'blackarch-firmware')
url='https://github.com/platomav/MEAnalyzer'
license=('GPL3')
depends=('python' 'python-colorama' 'python-crccheck' 'python-ptable-boxchar')
source=("https://github.com/platomav/MEAnalyzer/archive/v$pkgver-r$_rev.tar.gz")
sha512sums=('db88d59cc63b6688a864d305d0f67aef6faf6efe689dc6c533188d0c1dccf8ebc65556b7aaea20e9d84afe8a934012c14ec8f113788d57f3ed9def570985130a')
noextract=("ME_Analyzer_v${pkgver}_r$_rev.rar")

package() {
  cd "MEAnalyzer-$pkgver-r$_rev"

  install -dm 755 "$pkgdir/usr/bin"
  install -dm 755 "$pkgdir/usr/share/$pkgname"

  install -Dm 644 -t "$pkgdir/usr/share/doc/$pkgname/" Changelog.txt README.md
  install -Dm 644 LICENSE "$pkgdir/usr/share/licenses/$pkgname/LICENSE"

  rm -rf Changelog.txt README.md LICENSE

  cp -a --no-preserve=owner * "$pkgdir/usr/share/$pkgname/"

  cat > "$pkgdir/usr/bin/$pkgname" << EOF
#!/bin/sh
cd /usr/share/$pkgname
exec python MEA.py "\$@"
EOF
   chmod +x "$pkgdir/usr/bin/$pkgname"
}

