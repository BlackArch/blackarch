# This file is part of BlackArch Linux ( https://www.blackarch.org/ ).
# See COPYING for license details.

pkgname=halcyon-ide
pkgver=2.0.1
pkgrel=1
groups=('blackarch' 'blackarch-misc')
arch=('any')
pkgdesc='First IDE for Nmap Script (NSE) Development.'
url='https://halcyon-ide.org/'
license=('MIT')
depends=('java-environment' 'bash')
source=("https://github.com/s4n7h0/Halcyon/releases/download/$pkgver/Halcyon_IDE_v$pkgver.jar")
noextract=("Halcyon_IDE_v$pkgver.jar")
sha512sums=('5bf092665cc4559910aa7cff0d5658f15a8e426efe135922f62defe65d9f32dc8765524283b041226f1107cc1d6b0aff38c5c7f988272b43ce1a7f7094bbb063')

package() {
  install -dm 755 "$pkgdir/usr/bin"
  install -Dm 644 "Halcyon_IDE_v$pkgver.jar" \
    "$pkgdir/usr/share/$pkgname/$pkgname.jar"

  cat > "$pkgdir/usr/bin/$pkgname" << EOF
#!/bin/sh
exec java -jar /usr/share/$pkgname/$pkgname.jar "\$@"
EOF

  chmod +x "$pkgdir/usr/bin/$pkgname"
}

