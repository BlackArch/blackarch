# This file is part of BlackArch Linux ( https://www.blackarch.org/ ).
# See COPYING for license details.

pkgbase=python-passivetotal
pkgname=('python2-passivetotal' 'python-passivetotal')
_pkgname=passivetotal
pkgver=2.3.0
pkgrel=1
pkgdesc='Client for the PassiveTotal REST API.'
arch=('any')
url='https://pypi.org/project/passivetotal/#files'
license=('GPL2')
makedepends=('python2-setuptools' 'python-setuptools')
source=("https://files.pythonhosted.org/packages/1a/b5/4f03952dd00e60d6f33730fe66a6057b2b8242843897b5c1dc6e456d3f95/$_pkgname-$pkgver.tar.gz")
sha512sums=('167d78d84ccb84521227c125a931efcec6a5ae562c4d84e944f3dd57c6e440d59a4572e0fe6f9304e820fac1713878cc3aac33498bc008f55e034e58f4ce4ab7')

prepare() {
  cp -a "$_pkgname-$pkgver"{,-2}
}

build() {
  cd "$_pkgname-$pkgver-2"

  python2 setup.py build

  cd "$srcdir/$_pkgname-$pkgver"

  python setup.py build
}

package_python2-passivetotal() {
  depends=('python2' 'python2-dateutil' 'python2-future' 'python2-requests')

  cd "$_pkgname-$pkgver-2"

  python2 setup.py install --root="$pkgdir" --prefix=/usr -O1 --skip-build

  rm -rf "$pkgdir/usr/lib/python2.7/site-packages/tests/"

  for i in "$pkgdir/usr/bin/"*; do
    mv $i "${i}2"
  done
}

package_python-passivetotal() {
  depends=('python' 'python-dateutil' 'python-future' 'python-requests')

  cd "$_pkgname-$pkgver"

  python setup.py install --root="$pkgdir" -O1 --prefix=/usr --skip-build

  rm -rf "$pkgdir/usr/lib/python3.9/site-packages/tests/"
}

