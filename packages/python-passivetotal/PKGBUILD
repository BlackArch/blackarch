# This file is part of BlackArch Linux ( https://www.blackarch.org/ ).
# See COPYING for license details.

pkgbase=python-passivetotal
pkgname=('python2-passivetotal' 'python-passivetotal')
_pkgname=passivetotal
pkgver=2.1.0
pkgrel=2
pkgdesc='Client for the PassiveTotal REST API.'
arch=('any')
url='https://pypi.org/project/passivetotal/#files'
license=('GPL2')
makedepends=('python2-setuptools' 'python-setuptools')
source=("https://files.pythonhosted.org/packages/45/03/515dcda8fde03f2ee8fc2260c9153d1350045f4311137dd3d2bc446fc463/$_pkgname-$pkgver.tar.gz")
sha512sums=('61ac414df7d7a9a2a514c25f279255d48a5d16566ed2081d66e67a1f38091ab86d0971cc7e1e979140e63cb1136cf285ca04a0f9e9bddf46b5c38a800e4fc095')

prepare() {
  cp -a "$_pkgname-$pkgver"{,-2}
}

build() {
  cd "$_pkgname-$pkgver-2"

  python2 setup.py build

  cd "$srcdir/$_pkgname-$pkgver"

  python setup.py build
}

package_python2-passivetotal() {
  depends=('python2' 'python2-dateutil' 'python2-future' 'python2-requests')

  cd "$_pkgname-$pkgver-2"

  python2 setup.py install --root="$pkgdir" --prefix=/usr -O1 --skip-build

  rm -rf "$pkgdir/usr/lib/python2.7/site-packages/tests/"

  for i in "$pkgdir/usr/bin/"*; do
    mv $i "${i}2"
  done
}

package_python-passivetotal() {
  depends=('python' 'python-dateutil' 'python-future' 'python-requests')

  cd "$_pkgname-$pkgver"

  python setup.py install --root="$pkgdir" -O1 --prefix=/usr --skip-build

  rm -rf "$pkgdir/usr/lib/python3.9/site-packages/tests/"
}

