# This file is part of BlackArch Linux ( https://www.blackarch.org/ ).
# See COPYING for license details.

pkgbase=python-passivetotal
pkgname=('python2-passivetotal' 'python-passivetotal')
_pkgname=passivetotal
pkgver=2.5.6
pkgrel=1
pkgdesc='Client for the PassiveTotal REST API.'
arch=('any')
url='https://pypi.org/project/passivetotal/#files'
license=('GPL2')
makedepends=('python2-setuptools' 'python-setuptools')
source=("https://files.pythonhosted.org/packages/a9/a3/76b8f2702730cc6032ba56734e1fedcf0d340f9597d242a73845c6e4dfc9/$_pkgname-$pkgver.tar.gz")
sha512sums=('3114b4195a8100e06f89dd368703b26b14694ac4b2afb381a33b8250ea4d33a431fa19b66371bcdd4c8a39bdc565fd7d5afacd940da6f217fec4e82cb5414bba')

prepare() {
  cp -a "$_pkgname-$pkgver"{,-2}
}

build() {
  cd "$_pkgname-$pkgver-2"

  python2 setup.py build

  cd "$srcdir/$_pkgname-$pkgver"

  python setup.py build
}

package_python2-passivetotal() {
  depends=('python2' 'python2-dateutil' 'python2-future' 'python2-requests')

  cd "$_pkgname-$pkgver-2"

  python2 setup.py install --root="$pkgdir" --prefix=/usr -O1 --skip-build

  rm -rf "$pkgdir/usr/lib/python2.7/site-packages/tests/"

  for i in "$pkgdir/usr/bin/"*; do
    mv $i "${i}2"
  done
}

package_python-passivetotal() {
  depends=('python' 'python-dateutil' 'python-future' 'python-requests')

  cd "$_pkgname-$pkgver"

  python setup.py install --root="$pkgdir" -O1 --prefix=/usr --skip-build

  rm -rf "$pkgdir/usr/lib/python3.9/site-packages/tests/"
}

