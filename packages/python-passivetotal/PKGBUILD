# This file is part of BlackArch Linux ( https://www.blackarch.org/ ).
# See COPYING for license details.

pkgbase=python-passivetotal
pkgname=('python2-passivetotal' 'python-passivetotal')
_pkgname=passivetotal
pkgver=2.5.1
pkgrel=1
pkgdesc='Client for the PassiveTotal REST API.'
arch=('any')
url='https://pypi.org/project/passivetotal/#files'
license=('GPL2')
makedepends=('python2-setuptools' 'python-setuptools')
source=("https://files.pythonhosted.org/packages/bc/b5/e4d4a5d1120e9f4645f38f4ee324ef3336f470f1bfd907c8e3f3221ad4de/$_pkgname-$pkgver.tar.gz")
sha512sums=('eca51a778731d88bdc8ba10e0f3ecee730c764319e67b674c946c86a8761c091783592565ee5991d5f6caac2ae6f6dd8084a1689c468db8c0129e6c58ea0eff2')

prepare() {
  cp -a "$_pkgname-$pkgver"{,-2}
}

build() {
  cd "$_pkgname-$pkgver-2"

  python2 setup.py build

  cd "$srcdir/$_pkgname-$pkgver"

  python setup.py build
}

package_python2-passivetotal() {
  depends=('python2' 'python2-dateutil' 'python2-future' 'python2-requests')

  cd "$_pkgname-$pkgver-2"

  python2 setup.py install --root="$pkgdir" --prefix=/usr -O1 --skip-build

  rm -rf "$pkgdir/usr/lib/python2.7/site-packages/tests/"

  for i in "$pkgdir/usr/bin/"*; do
    mv $i "${i}2"
  done
}

package_python-passivetotal() {
  depends=('python' 'python-dateutil' 'python-future' 'python-requests')

  cd "$_pkgname-$pkgver"

  python setup.py install --root="$pkgdir" -O1 --prefix=/usr --skip-build

  rm -rf "$pkgdir/usr/lib/python3.9/site-packages/tests/"
}

