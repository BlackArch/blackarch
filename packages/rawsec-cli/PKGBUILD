# This file is part of BlackArch Linux ( https://www.blackarch.org/ ).
# See COPYING for license details.

pkgname=rawsec-cli
_pkgname=rawsec_cli
pkgver=1.1.1.r0.g692bc27
pkgrel=1
pkgdesc='Rawsec Inventory search CLI to find security tools and resources.'
arch=('any')
groups=('blackarch' 'blackarch-misc')
url='https://github.com/mBouamama/rawsec_cli'
license=('MIT')
depends=('python' 'python-requests' 'python-tabulate' 'python-click'
         'python-columnar')
makedepends=('python-setuptools' 'git')
source=("git+https://github.com/mBouamama/$_pkgname.git")
sha512sums=('SKIP')

pkgver() {
  cd $_pkgname

  git describe --long --tags | sed 's/\([^-]*-g\)/r\1/;s/-/./g'
}

build() {
  cd $_pkgname

  python setup.py build
}

package() {
  cd $_pkgname

  python setup.py install --root="$pkgdir" --prefix=/usr -O1 --skip-build

  rm -rf "$pkgdir/usr/lib/python3.9/site-packages/tests/"
}

