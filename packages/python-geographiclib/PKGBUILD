# This file is part of BlackArch Linux ( https://www.blackarch.org/ ).
# See COPYING for license details.

pkgbase=python-geographiclib
pkgname=('python2-geographiclib' 'python-geographiclib')
_pkgname=geographiclib
pkgver=1.49
pkgrel=1
pkgdesc='The geodesic routines from GeographicLib'
arch=('any')
url='https://pypi.org/project/geographiclib/#files'
license=('MIT')
makedepends=('python2-setuptools' 'python-setuptools')
source=("https://files.pythonhosted.org/packages/5b/ac/4f348828091490d77899bc74e92238e2b55c59392f21948f296e94e50e2b/$_pkgname-$pkgver.tar.gz")
sha512sums=('5cb7aa1724c51ccfc2869771aca356989db596352d2dcd82d7451bad36109d8e87118438e9cc84ceafb0dbfc0e7a742d5d5307a27658feea961bd8b3a84cb544')

prepare() {
  cp -a "$_pkgname-$pkgver"{,-2}
}

build() {
  cd "$_pkgname-$pkgver-2"

  python2 setup.py build

  cd "$srcdir/$_pkgname-$pkgver"

  python setup.py build
}

package_python2-geographiclib() {
  depends=('python2')

  cd "$_pkgname-$pkgver-2"

  python2 setup.py install --prefix=/usr --root="$pkgdir" --optimize=1 \
    --skip-build
}

package_python-geographiclib() {
  depends=('python')

  cd "$_pkgname-$pkgver"

  python setup.py install --prefix=/usr --root="$pkgdir" --optimize=1 \
    --skip-build
}

