# This file is part of BlackArch Linux ( https://www.blackarch.org/ ).
# See COPYING for license details.

pkgbase=python-liblnk-python
pkgname=('python2-liblnk-python' 'python-liblnk-python')
pkgver=20181227
pkgrel=3
pkgdesc='Library and tools to access the Windows Shortcut File (LNK) format.'
url='https://github.com/libyal/liblnk'
arch=('any')
license=('LGPLv3')
makedepends=('python2-setuptools' 'python-setuptools')
source=("https://pypi.python.org/packages/source/l/liblnk-python/liblnk-python-$pkgver.tar.gz")
sha512sums=('f3ed5c1926ac5fd93ba6cc919ea3675eda8269d5fe1d85033e789b3b376631cf5fae4fb9c0cfa1c9e221300d0aac49069119a16bea65a4f54b8139451618922e')

check(){
  cd "liblnk-$pkgver/tests"

  chmod +x ./test_runner.sh
  ./test_runner.sh
}

package_python2-liblnk-python() {
  depends=('python2')
  cd "liblnk-$pkgver"

  python2 setup.py install --root="$pkgdir" --optimize=1
}

package_python-liblnk-python() {
  depends=('python')
  cd "liblnk-$pkgver"

  python setup.py install --root="$pkgdir" --optimize=1
}
