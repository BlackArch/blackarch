# This file is part of BlackArch Linux ( http://blackarch.org ).
# See COPYING for license details.

pkgname='deen'
pkgver=241.3762fda
pkgrel=1
groups=('blackarch' 'blackarch-crypto' 'blackarch-misc')
arch=('any')
pkgdesc='Generic data encoding/decoding application built with PyQt5.'
url='https://github.com/takeshixx/deen'
license=('Apache')
depends=('python' 'python-lxml' 'python-pyqt5')
makedepends=('git' 'python-setuptools')
source=('git+https://github.com/takeshixx/deen.git')
sha1sums=('SKIP')

pkgver() {
  cd "$srcdir/deen"

  echo $(git rev-list --count HEAD).$(git rev-parse --short HEAD)
}

build() {
  cd "$srcdir/deen"

  python setup.py build
}

package() {
  cd "$srcdir/deen"

  python setup.py install --root="$pkgdir" --optimize=1 --skip-build

  install -Dm644 -t "$pkgdir/usr/share/doc/deen/" README.md
  install -Dm644 LICENSE "$pkgdir/usr/share/licenses/deen/LICENSE"
}
