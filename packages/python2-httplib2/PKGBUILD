# This file is part of BlackArch Linux ( https://www.blackarch.org/ ).
# See COPYING for license details.

pkgname=python2-httplib2
_pkgname=httplib2
pkgver=0.14.0
pkgrel=1
pkgdesc='A comprehensive HTTP client library.'
url='https://pypi.org/project/httplib2/#files'
arch=('any')
license=('MIT')
depends=('python2' 'ca-certificates')
makedepends=('python2-setuptools')
source=("https://files.pythonhosted.org/packages/ce/2e/87461bfbb7e561203b759b3f7f639e2144226604372830d00a8279960ae1/$_pkgname-$pkgver.tar.gz"
        'certs.py.patch')
sha512sums=('4038849dbb4523180e5fba29bfe17995b1da956c057a1b35c320f07543b704bfc07a45cb5bfe5d2b5abd88fcadc6693b767bbd5e97bd70a502a4fc3197b0e0e7'
            'c1d468c7109ecb1f3d7d612684394cb42349493b4b9ed97f072c5ef3e3586c1a73a79bf0a34c5bc0c823bf6260b5810338ae5949e130e1c0091a5cc15c9957d5')

prepare() {
  cd "$_pkgname-$pkgver/"

  patch -p2 < ../certs.py.patch
}

build() {
  cd "$_pkgname-$pkgver"

  python2 setup.py build
}

package() {
  cd "$_pkgname-$pkgver"

  python2 setup.py install --root="$pkgdir" --prefix=/usr -O1 --skip-build
}

