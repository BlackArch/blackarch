# This file is part of BlackArch Linux ( https://www.blackarch.org/ ).
# See COPYING for license details.

pkgbase=python-html-similarity
pkgname=('python2-html-similarity' 'python-html-similarity')
_pkgname=html-similarity
pkgver=0.3.2
pkgrel=2
pkgdesc='A set of similarity metricts to compare html files.'
arch=('any')
url='https://pypi.org/project/html-similarity/#files'
license=('BSD')
makedepends=('python2-setuptools' 'python-setuptools')
source=("https://files.pythonhosted.org/packages/01/df/24b150d3fa3bd6bdb08badff1f1cb6e61c944a2f0a2fbb845e42521e38fd/$_pkgname-$pkgver.tar.gz")
sha512sums=('ffc73cb52ef6b859f9717c6762988b1f557a777ecf04a0de2a05f12eec6ce33e5a9f2dee2fdeaa8e6e934e9b2449255b009dcd806d8e5fe64ec8a5458b851c2b')

prepare() {
  cp -a "$_pkgname-$pkgver"{,-2}
}

build() {
  cd "$_pkgname-$pkgver-2"

  python2 setup.py build

  cd "$srcdir/$_pkgname-$pkgver"

  python setup.py build
}

package_python2-html-similarity() {
  depends=('python2' 'python2-parsel')

  cd "$_pkgname-$pkgver-2"

  python2 setup.py install --prefix=/usr --root="$pkgdir" --optimize=1 \
    --skip-build
}

package_python-html-similarity() {
  depends=('python' 'python-parsel')

  cd "$_pkgname-$pkgver"

  python setup.py install --prefix=/usr --root="$pkgdir" --optimize=1 \
    --skip-build
}

