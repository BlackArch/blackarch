# This file is part of BlackArch Linux ( https://www.blackarch.org/ ).
# See COPYING for license details.

pkgname=prowler
pkgver=6065.3cab52772
pkgrel=1
epoch=1
pkgdesc='Tool for AWS security assessment, auditing and hardening.'
groups=('blackarch' 'blackarch-defensive' 'blackarch-scanner')
arch=('any')
url='https://github.com/alfresco/prowler'
license=('Apache')
depends=('bash' 'aws-cli')
makedepends=('git')
source=("git+https://github.com/alfresco/$pkgname.git")
sha512sums=('SKIP')

pkgver() {
  cd $pkgname

  echo $(git rev-list --count HEAD).$(git rev-parse --short HEAD)
}

package() {
  cd $pkgname

  install -dm 755 "$pkgdir/usr/bin"
  install -dm 755 "$pkgdir/usr/share/$pkgname"

  install -Dm 644 -t "$pkgdir/usr/share/doc/$pkgname/" README.md
  install -Dm 644 LICENSE "$pkgdir/usr/share/licenses/$pkgname/LICENSE"

  rm README.md LICENSE

  cp -a * "$pkgdir/usr/share/$pkgname/"

  cat > "$pkgdir/usr/bin/$pkgname" << EOF
#!/bin/sh
cd /usr/share/$pkgname
exec bash $pkgname "\$@"
EOF

  chmod +x "$pkgdir/usr/bin/$pkgname"
}

