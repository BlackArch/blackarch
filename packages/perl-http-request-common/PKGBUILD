# This file is part of BlackArch Linux ( https://www.blackarch.org/ ).
# See COPYING for license details.

pkgname=perl-http-request-common
_pkgname=HTTP-Message
pkgver=6.22
pkgrel=2
pkgdesc='Construct common HTTP::Request objects'
arch=('any')
license=('PerlArtistic' 'GPL')
options=('!emptydirs')
url='http://search.cpan.org/~gaas/HTTP-Message-6.06/lib/HTTP/Request/Common.pm'
source=("http://search.cpan.org/CPAN/authors/id/O/OA/OALDERS/$_pkgname-$pkgver.tar.gz")
sha512sums=('285d197f1ee28db3ebb00711264990a5527130961daecefb5bdb37feb67d13461b57d6570960025064a22099bf4f9037e146b8272be1832c433371cc0e2a712a')

prepare() {
  cd "$_pkgname-$pkgver"

  export PERL_MM_USE_DEFAULT=1 PERL5LIB="" \
    PERL_AUTOINSTALL=--skipdeps \
    PERL_MM_OPT="INSTALLDIRS=vendor DESTDIR='$pkgdir'" \
    PERL_MB_OPT="--installdirs vendor --destdir '$pkgdir'" \
    MODULEBUILDRC=/dev/null

  /usr/bin/perl Makefile.PL
}

build() {
  cd "$_pkgname-$pkgver"

  make
}

package() {
  cd "$_pkgname-$pkgver"

  make install

  find "$pkgdir" -name .packlist -o -name perllocal.pod -delete
}

