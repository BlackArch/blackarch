# $Id: PKGBUILD 88226 2013-04-14 19:02:25Z jelle $
# Maintainer:
# Contributor: Tom Newsom <Jeepster@gmx.co.uk>

pkgbase=ettercap
pkgname=('ettercap' 'ettercap-gtk')
pkgver=0.7.6
pkgrel=2
arch=('i686' 'x86_64')
url="http://ettercap.github.com/ettercap/"
license=('GPL')
depends=('openssl' 'libpcap' 'pcre' 'libltdl' 'libnet')
makedepends=('ghostscript' 'cmake' 'libpcap' 'libltdl' 'libnet' 'gtk2')
options=('!libtool' '!makeflags' '!emptydirs')
source=("https://github.com/Ettercap/ettercap/archive/v$pkgver.tar.gz" "ettercap.desktop" "org.archlinux.pkexec.ettercap.policy")
md5sums=('7c5f5e1fd4002afdfd13765ef92866e1'
         '51ae9f08e37a0621b3caeedb8e4d47d7'
         '473867a0ee6da9cfb048b3d332c3d71f')
build() {
  cd "${srcdir}"

  cp -r ${pkgbase}-${pkgver} ${pkgbase}-${pkgver}-gtk

  cd ${pkgbase}-${pkgver}
  mkdir build
  cd build
  cmake -D ENABLE_GTK=OFF -D CMAKE_INSTALL_PREFIX=/usr -D INSTALL_PREFIX=/usr -D CMAKE_BUILD_TYPE=RELEASE ../
  make

  cd "$srcdir"/${pkgbase}-${pkgver}-gtk
  mkdir build
  cd build
  cmake -D ENABLE_GTK=ON -D CMAKE_INSTALL_PREFIX=/usr -D INSTALL_PREFIX=/usr -D CMAKE_BUILD_TYPE=RELEASE ../
  make
}

package_ettercap() {
  pkgdesc="A network sniffer/interceptor/logger for ethernet LANs - console"
  backup=('etc/ettercap/etter.conf')
  conflicts=('ettercap-gtk')

  cd "${srcdir}"/${pkgbase}-${pkgver}/build
  make DESTDIR="${pkgdir}" install
}

package_ettercap-gtk() {
  pkgdesc="A network sniffer/interceptor/logger for ethernet LANs - GTK frontend"
  depends+=('gtk2' 'polkit')
  backup=('etc/ettercap/etter.conf')
  conflicts=('ettercap')
  provides=('ettercap')
  optdepends=('polkit-gnome')
  install='ettercap-gtk.install'

  install -d $pkgdir/usr/share/polkit-1/actions/
  cd "${srcdir}"/${pkgbase}-${pkgver}-gtk/build
  make DESTDIR="${pkgdir}" install

  install -Dm644 "${srcdir}/ettercap.desktop" "${pkgdir}/usr/share/applications/ettercap.desktop"
  install -Dm644 "${srcdir}/${pkgbase}-${pkgver}-gtk/desktop/ettercap.svg" "${pkgdir}/usr/share/icons/hicolor/scalable/apps/ettercap.svg"

  # Install policy file
  install -Dm644 "${srcdir}/org.archlinux.pkexec.ettercap.policy" "${pkgdir}/usr/share/polkit-1/actions/org.archlinux.pkexec.ettercap.policy"

  # We need rename the original script
  mv "${pkgdir}/usr/bin/ettercap" "${pkgdir}/usr/bin/ettercap.elf"

  # Install launcher script
  echo '#!/bin/sh' > ${pkgdir}/usr/bin/ettercap
  echo 'pkexec --disable-internal-agent "/usr/bin/ettercap.elf" "$@"' >> ${pkgdir}/usr/bin/ettercap
  chmod 755 ${pkgdir}/usr/bin/ettercap

}
