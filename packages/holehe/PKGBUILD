# This file is part of BlackArch Linux ( https://www.blackarch.org/ ).
# See COPYING for license details.

pkgname=holehe
pkgver=374.aca52fc
pkgrel=1
pkgdesc='A tool for Efficiently finding registered accounts from emails.'
arch=('any')
groups=('blackarch' 'blackarch-social' 'blackarch-recon')
url='https://github.com/megadose/holehe'
license=('GPL3')
depends=('python' 'python-termcolor' 'python-beautifulsoup4' 'python-httpx'
         'python-trio' 'python-tqdm' 'python-colorama')
makedepends=('git' 'python-setuptools')
source=("git+https://github.com/megadose/$pkgname.git")
sha512sums=('SKIP')

pkgver() {
  cd $pkgname

  echo $(git rev-list --count HEAD).$(git rev-parse --short HEAD)
}

build() {
  cd $pkgname

  python setup.py build
}

package() {
  cd $pkgname

  python setup.py install --root="$pkgdir" --prefix=/usr -O1 --skip-build

  install -Dm 644 -t "$pkgdir/usr/share/doc/$pkgname/" README.md
}

