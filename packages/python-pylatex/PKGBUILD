# This file is part of BlackArch Linux ( https://www.blackarch.org/ ).
# See COPYING for license details.

pkgbase=python-pylatex
pkgname=('python2-pylatex' 'python-pylatex')
_pkgname=PyLaTeX
pkgver=1.3.2
pkgrel=1
pkgdesc='A Python library for creating LaTeX files and snippets.'
arch=('any')
url='https://pypi.org/project/PyLaTeX/#files'
license=('MIT')
makedepends=('python2-setuptools' 'python-setuptools')
source=("https://files.pythonhosted.org/packages/a6/95/15037332a4c06237c4024a28c314ed5a46b47db1fe9f49d8a9277052ae8a/PyLaTeX-$pkgver.tar.gz")
sha512sums=('23e236a1fa922db65943bcdf3479d6e9c24ce22bf439630c4cb91dfbff82260cbc6ac0e076e6df35e864e52e13fd86e128b32ae60b00212b8ff9e5741d92ed80')

prepare() {
  cp -a "$_pkgname-$pkgver"{,-2}
}

build() {
  cd "$_pkgname-$pkgver-2"

  python2 setup.py build

  cd "$srcdir/$_pkgname-$pkgver"

  python setup.py build
}

package_python2-pylatex() {
  depends=('python2' 'python2-ordered-set')

  cd "$_pkgname-$pkgver-2"

  python2 setup.py install --prefix=/usr --root="$pkgdir" -O1 --skip-build
}

package_python-pylatex() {
  depends=('python' 'python-ordered-set')

  cd "$_pkgname-$pkgver"

  python setup.py install --prefix=/usr --root="$pkgdir" -O1 --skip-build
}

