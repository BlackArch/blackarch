# This file is part of BlackArch Linux ( https://www.blackarch.org/ ).
# See COPYING for license details.

pkgbase=python-pylatex
pkgname=('python2-pylatex' 'python-pylatex')
_pkgname=PyLaTeX
pkgver=1.3.4
pkgrel=1
pkgdesc='A Python library for creating LaTeX files and snippets.'
arch=('any')
url='https://pypi.org/project/PyLaTeX/#files'
license=('MIT')
makedepends=('python2-setuptools' 'python-setuptools')
source=("https://files.pythonhosted.org/packages/b5/e3/483de013e68502901102a3e8279e5409dc55ccf69926950ada244fbff96f/PyLaTeX-$pkgver.tar.gz")
sha512sums=('b905b38df7bd14eb649482d9f1120ba5a92f20b00d7fea38b97544890c1b23abc5c695aa64ff4f1920fee8c194458a65e8ca177b9f9b61b6b7dc93cabb0fb135')

prepare() {
  cp -a "$_pkgname-$pkgver"{,-2}
}

build() {
  cd "$_pkgname-$pkgver-2"

  python2 setup.py build

  cd "$srcdir/$_pkgname-$pkgver"

  python setup.py build
}

package_python2-pylatex() {
  depends=('python2' 'python2-ordered-set')

  cd "$_pkgname-$pkgver-2"

  python2 setup.py install --prefix=/usr --root="$pkgdir" -O1 --skip-build
}

package_python-pylatex() {
  depends=('python' 'python-ordered-set')

  cd "$_pkgname-$pkgver"

  python setup.py install --prefix=/usr --root="$pkgdir" -O1 --skip-build
}

