# This file is part of BlackArch Linux ( https://www.blackarch.org/ ).
# See COPYING for license details.

pkgbase=python-pylatex
pkgname=('python2-pylatex' 'python-pylatex')
_pkgname=PyLaTeX
pkgver=1.3.1
pkgrel=2
pkgdesc='A Python library for creating LaTeX files and snippets.'
arch=('any')
url='https://pypi.org/project/PyLaTeX/#files'
license=('MIT')
makedepends=('python2-setuptools' 'python-setuptools')
source=("https://files.pythonhosted.org/packages/e4/54/71bfdbecfcec3775668c0d790ab96e1b95c5799917aa70414b4b12644e74/PyLaTeX-$pkgver.tar.gz")
sha512sums=('6f8e96b1e80879fc2186d6fa07e22f66735fd6cf466659cedfa99dbf799a2e5fed52dc9ca0dcc16eb084aab603eef681e8ad7f54fd9f6bf2d39abd2416d1d7f6')

prepare() {
  cp -a "$_pkgname-$pkgver"{,-2}
}

build() {
  cd "$_pkgname-$pkgver-2"

  python2 setup.py build

  cd "$srcdir/$_pkgname-$pkgver"

  python setup.py build
}

package_python2-pylatex() {
  depends=('python2' 'python2-ordered-set')

  cd "$_pkgname-$pkgver-2"

  python2 setup.py install --prefix=/usr --root="$pkgdir" -O1 --skip-build
}

package_python-pylatex() {
  depends=('python' 'python-ordered-set')

  cd "$_pkgname-$pkgver"

  python setup.py install --prefix=/usr --root="$pkgdir" -O1 --skip-build
}

