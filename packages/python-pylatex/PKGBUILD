# This file is part of BlackArch Linux ( https://www.blackarch.org/ ).
# See COPYING for license details.

pkgbase=python-pylatex
pkgname=('python2-pylatex' 'python-pylatex')
_pkgname=PyLaTeX
pkgver=1.4.1
pkgrel=2
pkgdesc='A Python library for creating LaTeX files and snippets.'
arch=('any')
url='https://pypi.org/project/PyLaTeX/#files'
license=('MIT')
makedepends=('python2-setuptools' 'python-setuptools')
source=("https://files.pythonhosted.org/packages/8a/76/015a1d785221d9b0d2ad80759d892a6d9d0a8a05daffc52202311ea3d652/PyLaTeX-$pkgver.tar.gz")
sha512sums=('6c77d67d0f340c608001b7a5c69b7a5656a9dc6948a6d9bdfecae6362618d64b0e99784ed68383f48e1a9378bea81a60c31b0fb8af5552cceef4347508ff29d1')

prepare() {
  cp -a "$_pkgname-$pkgver"{,-2}
}

build() {
  cd "$_pkgname-$pkgver-2"

  python2 setup.py build

  cd "$srcdir/$_pkgname-$pkgver"

  python setup.py build
}

package_python2-pylatex() {
  depends=('python2' 'python2-ordered-set')

  cd "$_pkgname-$pkgver-2"

  python2 setup.py install --prefix=/usr --root="$pkgdir" -O1 --skip-build
}

package_python-pylatex() {
  depends=('python' 'python-ordered-set')

  cd "$_pkgname-$pkgver"

  python setup.py install --prefix=/usr --root="$pkgdir" -O1 --skip-build
}

