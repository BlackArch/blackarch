# This file is part of BlackArch Linux ( https://www.blackarch.org/ ).
# See COPYING for license details.

pkgbase=python-pylatex
pkgname=('python2-pylatex' 'python-pylatex')
_pkgname=PyLaTeX
pkgver=1.4.0
pkgrel=1
pkgdesc='A Python library for creating LaTeX files and snippets.'
arch=('any')
url='https://pypi.org/project/PyLaTeX/#files'
license=('MIT')
makedepends=('python2-setuptools' 'python-setuptools')
source=("https://files.pythonhosted.org/packages/72/ee/f99a2f3dab29e16bd893e1120a39e08a2b3fff95f897f4fe3c73c76c5844/PyLaTeX-$pkgver.tar.gz")
sha512sums=('cdde18d963a681636c1908024f7606fa09751889ff2db68e5b3ff131138b80064cfef0cdb997d96b41f61694f046ddff9a4aeb4fc40d1d8d0be414cc2e92336b')

prepare() {
  cp -a "$_pkgname-$pkgver"{,-2}
}

build() {
  cd "$_pkgname-$pkgver-2"

  python2 setup.py build

  cd "$srcdir/$_pkgname-$pkgver"

  python setup.py build
}

package_python2-pylatex() {
  depends=('python2' 'python2-ordered-set')

  cd "$_pkgname-$pkgver-2"

  python2 setup.py install --prefix=/usr --root="$pkgdir" -O1 --skip-build
}

package_python-pylatex() {
  depends=('python' 'python-ordered-set')

  cd "$_pkgname-$pkgver"

  python setup.py install --prefix=/usr --root="$pkgdir" -O1 --skip-build
}

