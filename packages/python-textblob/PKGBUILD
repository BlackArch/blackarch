# This file is part of BlackArch Linux ( https://www.blackarch.org/ ).
# See COPYING for license details.

pkgbase=python-textblob
pkgname=('python2-textblob' 'python-textblob')
_pkgname=textblob
pkgver=0.17.1
pkgrel=3
pkgdesc='Pythonic text processing. Sentiment analysis, part-of-speech tagging, and more.'
arch=('any')
url='https://pypi.org/project/textblob/#files'
license=('MIT')
makedepends=('python2-setuptools' 'python-setuptools')
options=(!emptydirs)
source=("https://files.pythonhosted.org/packages/source/${_pkgname::1}/$_pkgname/$_pkgname-$pkgver.tar.gz")
sha512sums=('a6e1b51129c42dacbdbb97a8ff469707252511122591c519500a4ec4932cf7c5da885cf622eda0e7e60a4db314fa1a8823005b817a689ea91d4ed85bdb5efdde')

prepare() {
  cp -a "$_pkgname-$pkgver"{,-2}
}

build() {
  cd "$_pkgname-$pkgver-2"

  python2 setup.py build

  cd "$srcdir/$_pkgname-$pkgver"

  python setup.py build
}

package_python2-textblob() {
  depends=('python2' 'python2-nltk' 'python2-singledispatch')

  cd "$_pkgname-$pkgver-2"

  python2 setup.py install --root="$pkgdir" --prefix=/usr -O1 --skip-build
}

package_python-textblob() {
  depends=('python' 'python-nltk')

  cd "$_pkgname-$pkgver"

  python setup.py install --root="$pkgdir" --prefix=/usr -O1 --skip-build
}

