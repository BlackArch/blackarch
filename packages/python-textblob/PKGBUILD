# This file is part of BlackArch Linux ( https://www.blackarch.org/ ).
# See COPYING for license details.

pkgbase=python-textblob
pkgname=('python2-textblob' 'python-textblob')
_pkgname=textblob
pkgver=0.15.3
pkgrel=2
pkgdesc='Pythonic text processing. Sentiment analysis, part-of-speech tagging, and more.'
arch=('any')
url='https://pypi.org/project/textblob/#files'
license=('MIT')
makedepends=('python2-setuptools' 'python-setuptools')
options=(!emptydirs)
source=("https://files.pythonhosted.org/packages/48/1b/b27c15bab38cc581dfe05504b289c5de34d68f666aedd1f565b8b5dd2de8/$_pkgname-$pkgver.tar.gz")
sha512sums=('41ee5d973c3aafc007edc5e97ba42f69248f6b827a813ae1576737f0894520dd0105089f635218cf53c7657be97ec4923ce9d2c209ef2a9b6523d4e478dd8b0c')

prepare() {
  cp -a "$_pkgname-$pkgver"{,-2}
}

build() {
  cd "$_pkgname-$pkgver-2"

  python2 setup.py build

  cd "$srcdir/$_pkgname-$pkgver"

  python setup.py build
}

package_python2-textblob() {
  depends=('python2' 'python2-nltk' 'python2-singledispatch')

  cd "$_pkgname-$pkgver-2"

  python2 setup.py install --root="$pkgdir" --prefix=/usr -O1 --skip-build
}

package_python-textblob() {
  depends=('python' 'python-nltk')

  cd "$_pkgname-$pkgver"

  python setup.py install --root="$pkgdir" --prefix=/usr -O1 --skip-build
}

