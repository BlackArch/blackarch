# This file is part of BlackArch Linux ( https://www.blackarch.org/ ).
# See COPYING for license details.

pkgbase=python-textblob
pkgname=('python2-textblob' 'python-textblob')
_pkgname=textblob
pkgver=0.18.0.post0
pkgrel=1
pkgdesc='Pythonic text processing. Sentiment analysis, part-of-speech tagging, and more.'
arch=('any')
url='https://pypi.org/project/textblob/#files'
license=('MIT')
makedepends=('python2-setuptools' 'python-setuptools')
options=(!emptydirs)
source=("https://files.pythonhosted.org/packages/source/${_pkgname::1}/$_pkgname/$_pkgname-$pkgver.tar.gz")
sha512sums=('36770ec182064723068e7e564c949f81edf6a1b58c93a1fce2e1931b69eb0e7699eacc793bc3ed6d2da7a2dc4383bf9c62765a41db90d953c7fe03aa317c5e1a')

prepare() {
  cp -a "$_pkgname-$pkgver"{,-2}
}

build() {
  cd "$_pkgname-$pkgver-2"

  python2 setup.py build

  cd "$srcdir/$_pkgname-$pkgver"

  python setup.py build
}

package_python2-textblob() {
  depends=('python2' 'python2-nltk' 'python2-singledispatch')

  cd "$_pkgname-$pkgver-2"

  python2 setup.py install --root="$pkgdir" --prefix=/usr -O1 --skip-build
}

package_python-textblob() {
  depends=('python' 'python-nltk')

  cd "$_pkgname-$pkgver"

  python setup.py install --root="$pkgdir" --prefix=/usr -O1 --skip-build
}

