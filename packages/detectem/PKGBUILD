# This file is part of BlackArch Linux ( http://blackarch.org ).
# See COPYING for license details.

pkgname=detectem
pkgver=228.c40e39a
pkgrel=1
pkgdesc='Detect software and its version on websites.'
groups=('blackarch' 'blackarch-fingerprint' 'blackarch-webapp'
        'blackarch-recon')
arch=('any')
url='https://github.com/spectresearch/detectem'
license=('MIT')
depends=('python' 'python-requests' 'python-pytest' 'python-docker'
         'python-click' 'python-yaml' 'python-zope-interface' 'python-dukpy')
makedepends=('git')
source=('git+https://github.com/spectresearch/detectem.git')
sha512sums=('SKIP')

pkgver() {
  cd "$pkgname"

  echo $(git rev-list --count HEAD).$(git rev-parse --short HEAD)
}

prepare() {
  cd "$pkgname"

  find . -type f -exec sed -i 's/==4.3.2//g;s/==2.12.4//g;s/==6.6//g' {} \;
}

build() {
  cd "$pkgname"

  python setup.py build
}

package() {
  cd "$pkgname"

  python setup.py install --root="$pkgdir" --prefix=/usr --optimize=1

  install -Dm 644 -t "$pkgdir/usr/share/doc/$pkgname/" *.md *.rst
  cp -a docs/* "$pkgdir/usr/share/doc/$pkgname/"
  install -Dm 644 LICENSE "$pkgdir/usr/share/licenses/$pkgname/LICENSE"

  mv "$pkgdir/usr/bin/det" "$pkgdir/usr/bin/detectem"
}

