# This file is part of BlackArch Linux ( https://www.blackarch.org/ ).
# See COPYING for license details.

pkgname=detectem
pkgver=269.e80471e
pkgrel=2
pkgdesc='Detect software and its version on websites.'
groups=('blackarch' 'blackarch-fingerprint' 'blackarch-webapp'
        'blackarch-recon')
arch=('any')
url='https://github.com/spectresearch/detectem'
license=('MIT')
depends=('python' 'python-requests' 'python-pytest' 'python-docker'
         'python-click' 'python-yaml' 'python-zope-interface' 'python-dukpy'
         'python-parsel' 'python-click-log')
makedepends=('git')
source=("git+https://github.com/spectresearch/$pkgname.git"
        'base.txt.patch')
sha512sums=('SKIP'
            '0c8f30e88ba62c7341eae0fb4b3b92253a094b938a8cac5ebfc6010c2ff921575ffb8e5331f86f2e272ecab03a7046b2dd145756bf4ac943963c352eed393b9c')

pkgver() {
  cd $pkgname

  echo $(git rev-list --count HEAD).$(git rev-parse --short HEAD)
}

prepare() {
  patch -p1 < base.txt.patch
}

build() {
  cd $pkgname

  python setup.py build
}

package() {
  cd $pkgname

  python setup.py install --root="$pkgdir" --prefix=/usr --optimize=1 \
    --skip-build

  install -Dm 644 -t "$pkgdir/usr/share/doc/$pkgname/" *.md *.rst
  cp -a docs/* "$pkgdir/usr/share/doc/$pkgname/"
  install -Dm 644 LICENSE "$pkgdir/usr/share/licenses/$pkgname/LICENSE"

  mv "$pkgdir/usr/bin/det" "$pkgdir/usr/bin/$pkgname"

  rm -rf "$pkgdir/usr/lib/python3.9/site-packages/tests/"
}

