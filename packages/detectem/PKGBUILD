# This file is part of BlackArch Linux ( http://blackarch.org ).
# See COPYING for license details.

pkgname=detectem
pkgver=228.c40e39a
pkgrel=2
pkgdesc='Detect software and its version on websites.'
groups=('blackarch' 'blackarch-fingerprint' 'blackarch-webapp'
        'blackarch-recon')
arch=('any')
url='https://github.com/spectresearch/detectem'
license=('MIT')
depends=('python' 'python-requests' 'python-pytest' 'python-docker'
         'python-click' 'python-yaml' 'python-zope-interface' 'python-dukpy'
         'python-parsel' 'python-click-log')
makedepends=('git')
source=('git+https://github.com/spectresearch/detectem.git'
        'requirements.txt.patch')
sha512sums=('SKIP'
            '5a5e5f029c992ff52f6e9928862016e7a75ededc7c9e95781b942998452e00bbba20516c12bc96c4f30c451c40ddd714097b93cfd2cbbc285e8aca0c17643578')

pkgver() {
  cd "$pkgname"

  echo $(git rev-list --count HEAD).$(git rev-parse --short HEAD)
}

prepare() {
  patch -p1 < requirements.txt.patch
}

build() {
  cd "$pkgname"

  python setup.py build
}

package() {
  cd "$pkgname"

  python setup.py install --root="$pkgdir" --prefix=/usr --optimize=1

  install -Dm 644 -t "$pkgdir/usr/share/doc/$pkgname/" *.md *.rst
  cp -a docs/* "$pkgdir/usr/share/doc/$pkgname/"
  install -Dm 644 LICENSE "$pkgdir/usr/share/licenses/$pkgname/LICENSE"

  mv "$pkgdir/usr/bin/det" "$pkgdir/usr/bin/detectem"
}

