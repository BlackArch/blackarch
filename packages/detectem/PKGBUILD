# This file is part of BlackArch Linux ( https://www.blackarch.org/ ).
# See COPYING for license details.

pkgname=detectem
pkgver=250.b1ecc35
pkgrel=1
pkgdesc='Detect software and its version on websites.'
groups=('blackarch' 'blackarch-fingerprint' 'blackarch-webapp'
        'blackarch-recon')
arch=('any')
url='https://github.com/spectresearch/detectem'
license=('MIT')
depends=('python' 'python-requests' 'python-pytest' 'python-docker'
         'python-click' 'python-yaml' 'python-zope-interface' 'python-dukpy'
         'python-parsel' 'python-click-log')
makedepends=('git')
source=("git+https://github.com/spectresearch/$pkgname.git"
        'requirements.txt.patch')
sha512sums=('SKIP'
            'a31b8609b6c5066e5ee911159651a0f5eead5ccb43d9766e6ee742279ddc86acbd11119f859ad96a784d5293bbaec76581d91faefd05a772642073ea83298c3a')

pkgver() {
  cd $pkgname

  echo $(git rev-list --count HEAD).$(git rev-parse --short HEAD)
}

prepare() {
  patch -p1 < requirements.txt.patch
}

build() {
  cd $pkgname

  python setup.py build
}

package() {
  cd $pkgname

  python setup.py install --root="$pkgdir" --prefix=/usr --optimize=1 \
    --skip-build

  install -Dm 644 -t "$pkgdir/usr/share/doc/$pkgname/" *.md *.rst
  cp -a docs/* "$pkgdir/usr/share/doc/$pkgname/"
  install -Dm 644 LICENSE "$pkgdir/usr/share/licenses/$pkgname/LICENSE"

  mv "$pkgdir/usr/bin/det" "$pkgdir/usr/bin/$pkgname"
}

