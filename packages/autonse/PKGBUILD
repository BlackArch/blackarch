# This file is part of BlackArch Linux ( https://www.blackarch.org/ ).
# See COPYING for license details.

pkgname=autonse
pkgver=25.7c87f4c
pkgrel=1
pkgdesc='Massive NSE (Nmap Scripting Engine) AutoSploit and AutoScanner.'
groups=('blackarch' 'blackarch-automation')
arch=('any')
url='https://github.com/m4ll0k/AutoNSE'
license=('GPL3')
depends=('bash' 'nmap' 'iputils')
makedepends=('git')
source=("$pkgname::git+https://github.com/m4ll0k/AutoNSE.git")
sha512sums=('SKIP')

pkgver() {
  cd $pkgname

  echo $(git rev-list --count HEAD).$(git rev-parse --short HEAD)
}

package() {
  cd $pkgname

  install -dm 755 "$pkgdir/usr/bin"
  install -dm 755 "$pkgdir/usr/share/$pkgname"

  install -Dm 644 -t "$pkgdir/usr/share/doc/$pkgname/" README.md
  install -Dm 644 LICENSE "$pkgdir/usr/share/licenses/$pkgname/LICENSE"

  rm README.md LICENSE

  cp -a * "$pkgdir/usr/share/$pkgname/"

  cat > "$pkgdir/usr/bin/$pkgname" << EOF
#!/bin/sh
cd /usr/share/$pkgname
exec bash $pkgname.sh "\$@"
EOF

  chmod +x "$pkgdir/usr/bin/$pkgname"
}

