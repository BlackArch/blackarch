# This file is part of BlackArch Linux ( http://blackarch.org ).
# See COPYING for license details.

pkgname=autonse
pkgver=21.73a6021
pkgrel=1
pkgdesc='Massive NSE (Nmap Scripting Engine) AutoSploit and AutoScanner.'
groups=('blackarch' 'blackarch-automation')
arch=('any')
url='https://github.com/m4ll0k/AutoNSE'
license=('GPL3')
depends=('bash' 'nmap' 'iputils')
makedepends=('git')
source=('git+https://github.com/m4ll0k/AutoNSE.git')
sha512sums=('SKIP')

pkgver() {
  cd "$srcdir/AutoNSE"

  echo $(git rev-list --count HEAD).$(git rev-parse --short HEAD)
}

package() {
  cd "$srcdir/AutoNSE"

  install -dm 755 "$pkgdir/usr/bin"
  install -dm 755 "$pkgdir/usr/share/autonse"

  install -Dm644 -t "$pkgdir/usr/share/doc/autonse/" README.md
  install -Dm644 LICENSE "$pkgdir/usr/share/licenses/autonse/LICENSE"

  rm README.md LICENSE

  cp -a * "$pkgdir/usr/share/autonse/"

  cat > "$pkgdir/usr/bin/autonse" << EOF
#!/bin/sh
cd /usr/share/autonse
exec bash autonse.sh "\${@}"
EOF

  chmod a+x "$pkgdir/usr/bin/autonse"
}
