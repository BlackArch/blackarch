# This file is part of BlackArch Linux ( https://www.blackarch.org/ ).
# See COPYING for license details.

pkgname=pass-station
pkgver=v2.0.0.r4.gbe184e9
pkgrel=2
groups=('blackarch' 'blackarch-misc' 'blackarch-recon')
pkgdesc='CLI & library to search for default credentials among thousands of Products / Vendors.'
arch=('any')
url='https://noraj.github.io/pass-station/'
license=('MIT')
depends=('ruby' 'ruby-paint' 'ruby-docopt')
makedepends=('git')
options=(!emptydirs)
source=("git+https://github.com/noraj/$pkgname")
sha512sums=('SKIP')

pkgver() {
  cd $pkgname

  git describe --long --tags | sed 's/\([^-]*-g\)/r\1/;s/-/./g'
}

build() {
  cd $pkgname

  gem build "$pkgname.gemspec"
}

package() {
  cd $pkgname

  _gemdir="$(ruby -e'puts Gem.default_dir')"
  _release=$(gem build "$pkgname.gemspec" | grep Version | cut -d' ' -f4)

  gem install --ignore-dependencies --no-user-install --no-document \
    -i "$pkgdir/$_gemdir" -n "$pkgdir/usr/bin" "$pkgname-$_release.gem"

  rm -f "$pkgdir/$_gemdir/cache/$pkgname-$_release.gem"
  find "$pkgdir/$_gemdir/extensions/" -name *.so -delete
  rm -rf "$pkgdir/$_gemdir/gems/$pkgname-$_release/{test,.github}"

  install -Dm 644 "$pkgdir/$_gemdir/gems/$pkgname-$_release/LICENSE" \
    "$pkgdir/usr/share/licenses/$pkgname/LICENSE"
}

