# This file is part of BlackArch Linux ( http://blackarch.org ).
# See COPYING for license details.

pkgname=hashdeep
pkgver=4.4
pkgrel=1
arch=('x86_64' 'armv6h' 'armv7h' 'aarch64')
groups=('blackarch' 'blackarch-forensic')
pkgdesc='Advanced checksum hashing tool.'
url='http://md5deep.sourceforge.net/'
license=('custom' 'GPL2')
source=("https://github.com/jessek/hashdeep/archive/release-${pkgver}.tar.gz")
sha512sums=('c37ec85ed04d4452f9038f43434c02e064cf9e17c4eaa233867dba0236443922a14ccb8ec76c68820087751c2ca3db014d3f17dd8fcd2c2bde84d620aae50de1')

build() {
  cd "$srcdir/hashdeep-release-$pkgver"

  sh bootstrap.sh

  if [[ "$CARCH" == "i686" || "$CARCH" == "x86_64" ]];
  then
    ./configure --prefix=/usr
  else
    ./configure --prefix=/usr --build=arm
  fi

  make
}

package() {
  cd "$srcdir/hashdeep-release-$pkgver"

  make DESTDIR="$pkgdir" install

  install -Dm644 -t "$pkgdir/usr/share/doc/hashdeep/" AUTHORS ChangeLog \
    FILEFORMAT INSTALL NEWS README README.md TODO
  install -Dm644 COPYING "$pkgdir/usr/share/licenses/hashdeep/COPYING"
}
