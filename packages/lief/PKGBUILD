# This file is part of BlackArch Linux ( https://www.blackarch.org/ ).
# See COPYING for license details.

pkgname=lief
_pkgname=LIEF
pkgver=0.12.1
pkgrel=1
pkgdesc='Library to Instrument Executable Formats.'
groups=('blackarch' 'blackarch-disassembler' 'blackarch-reversing')
arch=('x86_64' 'aarch64')
url='https://github.com/lief-project/LIEF/'
license=('Apache')
depends=('python')
makedepends=('python-setuptools' 'cmake')
source=("https://github.com/lief-project/LIEF/archive/$pkgver.tar.gz")
sha512sums=('833a04ece5ff7365717aee3408a2d522756319e43cf4ff795047021c46b48d5d525c2a56cdafd04259ddfcf6888972d19655924f999460ce05f52a32ac7e8bca')

prepare() {
  cd "$_pkgname-$pkgver"

  # tmp dirty fix! we need to change this asap
  sed -i 's|version   = "0.0.0"|version   = "0.10.1"|' setup.py
}

build() {
  cd "$_pkgname-$pkgver"

  python setup.py build
}

package() {
  cd "$_pkgname-$pkgver"

  python setup.py install --root="$pkgdir" --prefix=/usr -O1 --skip-build

  install -Dm 644 -t "$pkgdir/usr/share/doc/$pkgname/" README.md AUTHORS \
    CHANGELOG

  for i in "$pkgdir/usr/bin/"*; do
    mv $i $(echo $i | sed 's|_|-|g;s|.py||g')
  done
}

