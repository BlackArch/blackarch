# This file is part of BlackArch Linux ( https://www.blackarch.org/ ).
# See COPYING for license details.

pkgname=lief
_pkgname=LIEF
pkgver=0.10.1
pkgrel=1
pkgdesc='Library to Instrument Executable Formats.'
arch=('any')
url='https://lief.quarkslab.com/'
license=('Apache')
groups=('blackarch' 'blackarch-disassembler' 'blackarch-reversing')
depends=('python')
makedepends=('python-setuptools')
source=("https://github.com/lief-project/LIEF/archive/$pkgver.tar.gz")
sha512sums=('08cc6374fa5026099e4d43cb6bcd6401a8eadb6ea9749f5cde09ce80a555eb5ebc05ef6db8f01085ff20d56ba506fa0ab07e8d5f2e12dea723bebeb0f93410e3')

build() {
  cd "$_pkgname-$pkgver"

  python setup.py build
}

package() {
  cd "$_pkgname-$pkgver"

  python setup.py install --root="$pkgdir" --prefix=/usr -O1 --skip-build

  install -Dm 644 -t "$pkgdir/usr/share/doc/$pkgname/" README.md AUTHORS \
    CHANGELOG
  install -Dm 644 LICENSE "$pkgdir/usr/share/licenses/$pkgname/LICENSE"

  for i in "$pkgdir/usr/bin/"*; do
    mv $i $(echo $i | sed 's|_|-|g;s|.py||g')
  done
}

