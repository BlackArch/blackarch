# This file is part of BlackArch Linux ( https://www.blackarch.org/ ).
# See COPYING for license details.

pkgname=lief
_pkgname=LIEF
pkgver=0.11.5
pkgrel=2
pkgdesc='Library to Instrument Executable Formats.'
groups=('blackarch' 'blackarch-disassembler' 'blackarch-reversing')
arch=('x86_64' 'aarch64')
url='https://github.com/lief-project/LIEF/'
license=('Apache')
depends=('python')
makedepends=('python-setuptools' 'cmake')
source=("https://github.com/lief-project/LIEF/archive/$pkgver.tar.gz")
sha512sums=('fa215e6193eadd6be3f8cff92b304ca5a03db24fcb7f48b258318b8f674527fc45f6e3765ca3d6055e7a4e9f2672b2d198df02bbfcc4fa96cb36b9255df7c0c4')

prepare() {
  cd "$_pkgname-$pkgver"

  # tmp dirty fix! we need to change this asap
  sed -i 's|version   = "0.0.0"|version   = "0.10.1"|' setup.py
}

build() {
  cd "$_pkgname-$pkgver"

  python setup.py build
}

package() {
  cd "$_pkgname-$pkgver"

  python setup.py install --root="$pkgdir" --prefix=/usr -O1 --skip-build

  install -Dm 644 -t "$pkgdir/usr/share/doc/$pkgname/" README.md AUTHORS \
    CHANGELOG

  for i in "$pkgdir/usr/bin/"*; do
    mv $i $(echo $i | sed 's|_|-|g;s|.py||g')
  done
}

