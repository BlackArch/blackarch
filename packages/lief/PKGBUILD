# This file is part of BlackArch Linux ( https://www.blackarch.org/ ).
# See COPYING for license details.

pkgname=lief
_pkgname=LIEF
pkgver=0.11.0
pkgrel=1
pkgdesc='Library to Instrument Executable Formats.'
groups=('blackarch' 'blackarch-disassembler' 'blackarch-reversing')
arch=('x86_64' 'aarch64')
url='https://lief.quarkslab.com/'
license=('Apache')
depends=('python')
makedepends=('python-setuptools' 'cmake')
source=("https://github.com/lief-project/LIEF/archive/$pkgver.tar.gz")
sha512sums=('1dc6a34a2f144c794bd763c9162073045172238f931f3dc11f7491e312adfcdc92ff896e4a795de1ad1aa2d8ceb35c73893c8abb8bdbbc8179e4c2aa4100497f')

prepare() {
  cd "$_pkgname-$pkgver"

  # tmp dirty fix! we need to change this asap
  sed -i 's|version   = "0.0.0"|version   = "0.10.1"|' setup.py
}

build() {
  cd "$_pkgname-$pkgver"

  python setup.py build
}

package() {
  cd "$_pkgname-$pkgver"

  python setup.py install --root="$pkgdir" --prefix=/usr -O1 --skip-build

  install -Dm 644 -t "$pkgdir/usr/share/doc/$pkgname/" README.md AUTHORS \
    CHANGELOG
  install -Dm 644 LICENSE "$pkgdir/usr/share/licenses/$pkgname/LICENSE"

  for i in "$pkgdir/usr/bin/"*; do
    mv $i $(echo $i | sed 's|_|-|g;s|.py||g')
  done
}

