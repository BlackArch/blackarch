# This file is part of BlackArch Linux ( http://blackarch.org ).
# See COPYING for license details.

pkgname='airoscript'
pkgver=45.0a122ee
pkgrel=7
epoch=2
pkgdesc='A script to simplify the use of aircrack-ng tools.'
arch=('any')
url='http://midnightresearch.com/projects/wicrawl/'
groups=('blackarch' 'blackarch-wireless')
license=('GPL')
depends=('mdk3' 'aircrack-ng' 'dhclient' 'perl-yaml-syck')
options=(!makeflags)
makedepends=('git')
source=('git+http://github.com/patrickhno/Airoscript'
        'Makefile.patch')
sha1sums=('SKIP'
          'a25b188f590dbbe8ebf8574a6f1a04019707625f')

pkgver() {
  cd "$srcdir/Airoscript"

  echo $(git rev-list --count HEAD).$(git rev-parse --short HEAD)
}

prepare() {
  cd "$srcdir/Airoscript"

  sed -i 's|/etc/airoscript-ng.conf|/etc/airoscript-ng/airoscript-ng.conf|g' \
    src/airoscript-ng

  patch -Np1 < "$srcdir/Makefile.patch"

  find -type f -exec sed -i 's|/sbin|/bin|g' {} \;
}

package() {
  cd "$srcdir/Airoscript"

  rm -rf src/locale/*

  make prefix="$pkgdir/usr" aircrack_prefix="/usr" \
    etcdir="$pkgdir/etc/airoscript-ng" install

  # Fix conflict with extra/screen.
  #mv "$pkgdir/etc/screenrc" "$pkgdir/etc/airoscript/screenrc"

  sed -i 's|/local||' "$pkgdir"/etc/airoscript-ng/*
  sed -i 's|/minimal.theme|defaul.theme|' "$pkgdir"/etc/airoscript-ng/*
  # TODO: wordlist
}
