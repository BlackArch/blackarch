# This file is part of BlackArch Linux ( https://www.blackarch.org/ ).
# See COPYING for license details.

pkgbase=python-requesocks
pkgname=('python2-requesocks' 'python-requesocks')
_pkgname=requesocks
pkgver=0.10.8
pkgrel=5
pkgdesc='Python HTTP for Humans, with socks proxy support.'
arch=('any')
url='https://pypi.org/project/requesocks/#files'
license=('ISC')
makedepends=('python2-setuptools' 'python2-socks' 'python-setuptools'
             'python-socks')
source=("https://files.pythonhosted.org/packages/2b/33/0ecebf3903181cd74d9ff885da4029f59e5f61c99dc66f39c5a170a66d27/$_pkgname-$pkgver.tar.gz")
sha512sums=('aa81b572ba5ec36d07fe35ca74cb7684df33d22899e6abd48011f49b4897db680861ca8e81f05d4dff7e3759bfc4d72d5e4838cdab3062313abd7cffe7be80bf')

prepare() {
  cp -a "$_pkgname-$pkgver"{,-2}
}

build() {
  cd "$_pkgname-$pkgver-2"

  python2 setup.py build

  cd "$srcdir/$_pkgname-$pkgver"

  python setup.py build
}

package_python2-requesocks() {
  depends=('python2' 'python2-certifi' 'python2-chardet')

  cd "$_pkgname-$pkgver-2"

  python2 setup.py install --prefix=/usr --root="$pkgdir" --optimize=1 \
    --skip-build
}

package_python-requesocks() {
  depends=('python' 'python-certifi' 'python-chardet')

  cd "$_pkgname-$pkgver"

  python setup.py install --prefix=/usr --root="$pkgdir" --optimize=1 \
    --skip-build
}

