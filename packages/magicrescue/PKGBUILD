# This file is part of BlackArch Linux ( http://blackarch.org ).
# See COPYING for license details.

pkgname='magicrescue'
pkgver='1.1.9'
pkgrel=4
groups=('blackarch' 'blackarch-forensic')
pkgdesc='Find and recover deleted files on block devices.'
arch=('x86_64' 'armv6h' 'armv7h' 'aarch64')
url='http://freshmeat.net/projects/magicrescue/'
license=('GPL')
source=("http://www.itu.dk/people/jobr/magicrescue/release/magicrescue-${pkgver}.tar.gz")
sha1sums=('d428f7cce8bc00ea4ae3212da5d074fb5bcc1dfe')

prepare() {
  cd "$srcdir/magicrescue-$pkgver"

  sed -i 's|$(PREFIX)/man/man1|$(PREFIX)/share/man/man1|' Makefile.in
}

build() {
  cd "$srcdir/magicrescue-$pkgver"

  ./configure --prefix=/usr

  make PREFIX="$pkgdir/usr"
}

package() {
  cd "$srcdir/magicrescue-$pkgver"

  mkdir -p "$pkgdir/usr"

  make PREFIX="$pkgdir/usr" install
}
