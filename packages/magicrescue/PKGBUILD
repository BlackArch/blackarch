# This file is part of BlackArch Linux ( https://www.blackarch.org/ ).
# See COPYING for license details.

pkgname=magicrescue
pkgver=v1.1.10.r8.gd9a5793
pkgrel=1
pkgdesc='Find and recover deleted files on block devices.'
arch=('x86_64' 'aarch64')
groups=('blackarch' 'blackarch-forensic')
url='https://github.com/jbj/magicrescue'
license=('GPL-1.0-or-later')
makedepends=('git')
source=("git+https://github.com/jbj/$pkgname.git")
sha512sums=('SKIP')

pkgver() {
  cd $pkgname

  ( set -o pipefail
    git describe --long --tags --abbrev=7 2>/dev/null |
      sed 's/\([^-]*-g\)/r\1/;s/-/./g' ||
    printf "%s.%s" "$(git rev-list --count HEAD)" \
      "$(git rev-parse --short=7 HEAD)"
  )
}

prepare() {
  cd $pkgname

  sed -i 's|$(PREFIX)/man/man1|$(PREFIX)/share/man/man1|' Makefile.in
}

build() {
  cd $pkgname

  ./configure --prefix=/usr

  make PREFIX="$pkgdir/usr"
}

package() {
  cd $pkgname

  install -dm 755 "$pkgdir/usr"

  make PREFIX="$pkgdir/usr" install
}

