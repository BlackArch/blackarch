# This file is part of BlackArch Linux ( https://www.blackarch.org/ ).
# See COPYING for license details.

pkgbase=python-python-nmap
pkgname=('python2-python-nmap' 'python-python-nmap')
_pkgname=python-nmap
pkgver=0.6.4
pkgrel=1
pkgdesc='This is a python class to use nmap and access scan results from python3.'
arch=('any')
url='https://pypi.org/project/python-nmap/#files'
license=('GPL3')
makedepends=('python2-setuptools' 'python-setuptools')
source=("https://files.pythonhosted.org/packages/32/da/62e9f4e6296c29be381439683b2c00722c829cc264ef617c28f6179f8b4a/$_pkgname-$pkgver.tar.gz")
sha512sums=('9c5176087e39271015283abcd8fc4d7828c063e9209ec1e549737142864e199a78014c2ab518f9afcb75f2ea2affc94fa1f612c9d2849d25eed9589a03864b76')

prepare() {
  cp -a "$_pkgname-$pkgver"{,-2}
}

build() {
  cd "$_pkgname-$pkgver-2"

  python2 setup.py build

  cd "$srcdir/$_pkgname-$pkgver"

  python setup.py build
}

package_python2-python-nmap() {
  depends=('python2')

  cd "$_pkgname-$pkgver-2"

  python2 setup.py install --prefix=/usr --root="$pkgdir" --optimize=1 \
    --skip-build
}

package_python-python-nmap() {
  depends=('python')

  cd "$_pkgname-$pkgver"

  python setup.py install --prefix=/usr --root="$pkgdir" --optimize=1 \
    --skip-build
}

