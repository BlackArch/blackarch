# This file is part of BlackArch Linux ( https://www.blackarch.org/ ).
# See COPYING for license details.

pkgname=aquatone
pkgver=120.854a5d5
pkgrel=2
pkgdesc='A Tool for Domain Flyovers.'
groups=('blackarch' 'blackarch-recon')
arch=('x86_64' 'aarch64')
url='https://github.com/michenriksen/aquatone'
license=('MIT')
depends=('chromium')
makedepends=('git' 'go-pie')
source=("git+https://github.com/michenriksen/$pkgname.git")
sha512sums=('SKIP')

pkgver() {
  cd $pkgname

  echo $(git rev-list --count HEAD).$(git rev-parse --short HEAD)
}

prepare() {
  mkdir -p "gopath/src/github.com/michenriksen"

  ln -rTsf $pkgname "gopath/src/github.com/michenriksen/$pkgname"

  export GOPATH="$srcdir/gopath"

  cd "$GOPATH/src/github.com/michenriksen/$pkgname"

  go get -d ./...
}

build() {
  export GOPATH="$srcdir/gopath"

  cd "$GOPATH/src/github.com/michenriksen/$pkgname"

  go install -v ./...
}

package() {
  export GOPATH="$srcdir/gopath"

  cd "$GOPATH/bin"

  install -Dm 755 $pkgname "$pkgdir/usr/bin/$pkgname"

  cd "$srcdir/$pkgname"

  install -Dm 644 -t "$pkgdir/usr/share/doc/$pkgname" *.md
  install -Dm 644 LICENSE.txt "$pkgdir/usr/share/licenses/$pkgname/LICENSE"
}

