# This file is part of BlackArch Linux ( https://www.blackarch.org/ ).
# See COPYING for license details.

pkgname=hetty
pkgver=64.98dacbe
pkgrel=1
pkgdesc='HTTP toolkit for security research. It aims to become an open source alternative to commercial software like Burp Suite Pro, with powerful features tailored to the needs of the infosec and bug bounty community.'
arch=('x86_64' 'aarch64')
groups=('blackarch' 'blackarch-webapp' 'blackarch-proxy')
url='https://github.com/dstotijn/hetty'
license=('MIT')
makedepends=('git' 'go-pie' 'yarn')
source=("$pkgname.install"
        "git+https://github.com/dstotijn/$pkgname.git")
sha512sums=('ec0cd49c178b1b94084e6ae2b03d6a5825c02853fb42662f4b0150611a7f9dd0c5a13f585a51a1dbbc43c6514186a8eadb5eaa51d4b0b2397ace04a3adfb8815'
	          'SKIP')
install="$pkgname.install"
_url="github.com/dstotijn/hetty/cmd/$pkgname"

pkgver() {
  cd $pkgname

  echo $(git rev-list --count HEAD).$(git rev-parse --short HEAD)
}

build() {
  cd $pkgname

  GOPATH="$srcdir" GO111MODULE=auto go get -u -v "$_url"
  cd admin/ && yarn install && yarn export && cd ../
  PATH="$PATH:$(go env GOPATH)/bin" make build
}

package() {
  cd "$pkgname"

  install -Dm 755 $pkgname "$pkgdir/usr/bin/$pkgname"
  install -Dm 644 -t "$pkgdir/usr/share/doc/$pkgname/" README.md
  install -Dm 644 -t "$pkgdir/usr/share/licenses/$pkgname/" \
    LICENSE
}

