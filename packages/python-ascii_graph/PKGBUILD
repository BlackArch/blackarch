# This file is part of BlackArch Linux ( https://www.blackarch.org/ ).
# See COPYING for license details.

pkgbase=python-ascii_graph
pkgname=('python2-ascii_graph' 'python-ascii_graph')
_pkgname='ascii_graph'
pkgver=1.5.1
pkgrel=5
pkgdesc='A simple python lib to print data as ascii histograms.'
arch=('any')
url='https://pypi.python.org/pypi/ascii_graph/'
license=('MIT')
makedepends=('python2-setuptools' 'python-setuptools')
source=("https://files.pythonhosted.org/packages/ab/57/90a36a9377d72cfc09a433019182030daf9bdd64db97c5808867a6ddbc57/$_pkgname-$pkgver.tar.gz")
sha512sums=('3d7e37670ad20a8516f5e3a74eb62639366e4b2608409b5ad6246e7087a641cae4068ce8508804da4ea999d412f42b551afc2690f48113f46a0ae94e3da24833')

prepare() {
  cp -a "$_pkgname-$pkgver"{,-2}
}

build() {
  cd "$_pkgname-$pkgver-2"

  python2 setup.py build

  cd "$srcdir/$_pkgname-$pkgver"

  python setup.py build
}

package_python2-ascii_graph() {
  depends=('python2')

  cd "$_pkgname-$pkgver-2"

  python2 setup.py install --prefix=/usr --root="$pkgdir" --optimize=1 \
    --skip-build

  mv "$pkgdir/usr/bin/asciigraph" "$pkgdir/usr/bin/asciigraph2"
}

package_python-ascii_graph() {
  depends=('python')

  cd "$_pkgname-$pkgver"

  python setup.py install --prefix=/usr --root="$pkgdir" --optimize=1 \
    --skip-build
}

