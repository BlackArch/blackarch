# This file is part of BlackArch Linux ( https://www.blackarch.org/ ).
# See COPYING for license details.

pkgbase=python-dnsknife
pkgname=('python2-dnsknife' 'python-dnsknife')
_pkgname=dnsknife
pkgver=0.11
pkgrel=3
pkgdesc='DNS tools.'
arch=('any')
url='https://pypi.org/project/dnsknife/'
license=('GPL3')
makedepends=('python2-setuptools' 'python-setuptools')
source=("https://pypi.python.org/packages/02/db/7e13c448167e37046b785ef165a2ba022d14dffec1bf5950da80e6f0ed45/$_pkgname-$pkgver.tar.gz")
sha512sums=('88b51e535bbf61678c1c89ed652a7d7577e916209575ce99d0bdfe45b0f3cc7fb45d0d485114fe313f99bda98f630fdbf094f5a2fe06eedc6e53ed22395a712e')

prepare() {
  cp -a "$_pkgname-$pkgver" "$_pkgname-$pkgver-2"
}

build() {
  cd "$_pkgname-$pkgver-2"

  python2 setup.py build

  cd "$srcdir/$_pkgname-$pkgver"

  python setup.py build
}

package_python2-dnsknife() {
  depends=('python2' 'python2-crypto' 'python2-requests' 'python2-six')

  cd "$_pkgname-$pkgver-2"

  python2 setup.py install --prefix=/usr --root="$pkgdir" --optimize=1 \
    --skip-build
}

package_python-dnsknife() {
  depends=('python' 'python-crypto' 'python-requests' 'python-six')

  cd "$_pkgname-$pkgver"

  python setup.py install --prefix=/usr --root="$pkgdir" --optimize=1 \
    --skip-build
}

