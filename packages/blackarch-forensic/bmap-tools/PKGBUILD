# This file is part of BlackArch Linux ( https://www.blackarch.org/ ).
# See COPYING for license details.

pkgname=bmap-tools
pkgver=3.5
pkgrel=1
pkgdesc='Tool for copying largely sparse files using information from a block map file.'
groups=('blackarch' 'blackarch-forensic')
arch=('any')
url='http://git.infradead.org/users/dedekind/bmap-tools.git'
license=('GPL2')
depends=('python2')
makedepends=('git' 'python2-setuptools')
source=("git+https://github.com/01org/$pkgname.git")
sha256sums=('SKIP')

pkgver() {
  cd $pkgname

  awk '/^VERSION =/ { print $3 }' bmaptools/CLI.py | sed 's/"\(.*\)"/\1/'
}

build() {
  cd $pkgname

  python2 setup.py build
}

package() {
  cd $pkgname

  python2 setup.py install --root="$pkgdir" --optimize=1 --skip-build

  install -Dm 644 docs/man1/bmaptool.1 "$pkgdir/usr/share/man/man1/bmaptool.1"
}

