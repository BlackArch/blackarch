# This file is part of BlackArch Linux ( https://www.blackarch.org/ ).
# See COPYING for license details.

pkgname=sigma
pkgver=0.19.1
pkgrel=2
pkgdesc="Generic Signature Format for SIEM Systems"
arch=('any')
groups=('blackarch' 'blackarch-defensive')
url="https://github.com/Neo23x0/sigma"
license=('LGPL3')
depends=('python-yaml' 'python-pymisp' 'python-progressbar' 'python-urllib3'
         'python-requests')
makedepends=('python-setuptools')
source=("${pkgname}-${pkgver}.tar.gz::${url}/archive/${pkgver}.tar.gz")
sha512sums=('87817e1b228f5a62adc71c8231dd9e013b3d46a1cd29178665ada9f810b87d5c3a8004c64e758721f3ef146e64dc6bf7550bf2591508a43f41523d98db3902fd')

build() {
  cd "$pkgname-$pkgver/tools"

  python setup.py build
}

package() {
  cd "$pkgname-$pkgver/tools"

  python setup.py install --root="$pkgdir" --prefix=/usr -O1 --skip-build

  mv "$pkgdir/usr/etc/" $pkgdir
}

