# This file is part of BlackArch Linux ( https://www.blackarch.org/ ).
# See COPYING for license details.

pkgname=evil-winrm-py
_pkgname=${pkgname//-/_}
pkgver=1.5.0
pkgrel=1
pkgdesc='WinRM shell for Windows and Active Directory pentesting.'
arch=('any')
url='https://github.com/adityatelange/evil-winrm-py'
license=('MIT')
groups=('blackarch' 'blackarch-windows' 'blackarch-exploitation'
        'blackarch-backdoor')
depends=('python' 'python-pypsrp' 'python-prompt_toolkit' 'python-tqdm')
makedepends=('python-build' 'python-installer' 'python-wheel' 'python-setuptools')
optdepends=('python-kerberos: Kerberos authentication support')
source=("https://files.pythonhosted.org/packages/d8/e6/0f39fbc92ce26afd0ce79acadbacc312c81f3d438d3bf625a03f1a01c474/$_pkgname-$pkgver.tar.gz")
sha512sums=('7124b8827c71e379924787ca307cc23b5556b48f0392624db5dcfa211601e80d15bdb8f264092e3524408f1338fc9dc0108cf792c65f40c5d4129b30b8a8da33')

build() {
  cd "$_pkgname-$pkgver"

  python -m build --wheel --no-isolation
}

package() {
  cd "$_pkgname-$pkgver"

  python -m installer --destdir="$pkgdir" dist/*.whl
}

