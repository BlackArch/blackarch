# This file is part of BlackArch Linux ( http://blackarch.org ).
# See COPYING for license details.

pkgname='silk'
pkgver='3.16.0'
pkgrel=1
groups=('blackarch' 'blackarch-networking' 'blackarch-scanner')
pkgdesc='A collection of traffic analysis tools developed by the CERT NetSA to facilitate security analysis of large networks.'
arch=('x86_64' 'armv6h' 'armv7h' 'aarch64')
url='https://tools.netsa.cert.org/silk/'
license=('GPL2')
source=("https://tools.netsa.cert.org/releases/silk-${pkgver}.tar.gz")
sha1sums=('a2b8dce8ab83f8098ff21fdf441624d10bf21227')

build() {
  cd "$srcdir/silk-$pkgver"

  ./configure --prefix=/usr --enable-ipv6 --sbindir=/usr/sbin

  make
}

package() {
  cd "$srcdir/silk-$pkgver"

  mkdir -p "$pkgdir/usr/share/doc/silk"

  install -Dm644 -t "$pkgdir/usr/share/licenses/silk" doc/GPL.txt

  rm doc/{Makefile*,GPL.txt}

  install -Dm644 doc/* "$pkgdir/usr/share/doc/silk"

  make DESTDIR="$pkgdir" install

  mv "$pkgdir/usr/sbin/"* "$pkgdir/usr/bin/"
  rmdir "$pkgdir/usr/sbin"
}
