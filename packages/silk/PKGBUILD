# This file is part of BlackArch Linux ( https://www.blackarch.org/ ).
# See COPYING for license details.

pkgname=silk
pkgver=3.19.2
pkgrel=1
groups=('blackarch' 'blackarch-networking' 'blackarch-scanner')
pkgdesc='A collection of traffic analysis tools developed by the CERT NetSA to facilitate security analysis of large networks.'
arch=('x86_64' 'aarch64')
url='https://tools.netsa.cert.org/silk/download.html'
license=('GPL2')
depends=('python' 'libfixbuf' 'lzo' 'libpcap' 'gnutls' 'c-ares' 'adns' 'snappy')
source=("https://tools.netsa.cert.org/releases/$pkgname-$pkgver.tar.gz")
sha512sums=('d3e70b97f20328a8e84863f2f32028860768aca4afaf9d0cbb9a2d7ddce5c29a2209368e127f8fe0cf3c40b525bc6d0f7d2168466ccc034cd8124847af80d2b3')

build() {
  cd "$pkgname-$pkgver"

  export LIBS="-lpthread"

  ./configure --prefix=/usr --enable-ipv6 --sbindir=/usr/bin --with-gnutls \
    --with-pic --with-pcap -with-libfixbuf --enable-output-compression \
    --enable-localtime --enable-data-rootdir="/usr/share/$pkgname"

  make
}

package() {
  cd "$pkgname-$pkgver"

  install -Dm 644 -t "$pkgdir/usr/share/licenses/$pkgname" doc/GPL.txt

  rm doc/{Makefile*,GPL.txt}

  install -Dm 644 -t "$pkgdir/usr/share/doc/$pkgname/" doc/*

  make DESTDIR="$pkgdir" install
}

