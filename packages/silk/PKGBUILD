# This file is part of BlackArch Linux ( http://blackarch.org ).
# See COPYING for license details.

pkgname=silk
pkgver=3.17.2
pkgrel=1
groups=('blackarch' 'blackarch-networking' 'blackarch-scanner')
pkgdesc='A collection of traffic analysis tools developed by the CERT NetSA to facilitate security analysis of large networks.'
arch=('x86_64' 'armv6h' 'armv7h' 'aarch64')
url='https://tools.netsa.cert.org/silk/'
license=('GPL2')
source=("https://tools.netsa.cert.org/releases/silk-${pkgver}.tar.gz")
sha512sums=('2bd268545276abce2d1ca0e4a57ed2ae95c41e7200de4a93ab2def654b3b54df5d2777977667023ff02e0907cb964cfe609c3388ed30298a76725c53ee796997')

build() {
  cd "$srcdir/silk-$pkgver"

  ./configure --prefix=/usr --enable-ipv6 --sbindir=/usr/sbin

  make
}

package() {
  cd "$srcdir/silk-$pkgver"

  install -dm 755 "$pkgdir/usr/share/doc/silk"

  install -Dm644 -t "$pkgdir/usr/share/licenses/silk" doc/GPL.txt

  rm doc/{Makefile*,GPL.txt}

  install -Dm644 doc/* "$pkgdir/usr/share/doc/silk"

  make DESTDIR="$pkgdir" install

  mv "$pkgdir/usr/sbin/"* "$pkgdir/usr/bin/"
  rmdir "$pkgdir/usr/sbin"
}
