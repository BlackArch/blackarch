# This file is part of BlackArch Linux ( https://www.blackarch.org/ ).
# See COPYING for license details.

pkgname=silk
pkgver=3.23.1
pkgrel=2
groups=('blackarch' 'blackarch-networking' 'blackarch-scanner')
pkgdesc='A collection of traffic analysis tools developed by the CERT NetSA to facilitate security analysis of large networks.'
arch=('x86_64' 'aarch64')
url='https://tools.netsa.cert.org/silk/download.html'
license=('GPL2')
depends=('python' 'libfixbuf' 'lzo' 'libpcap' 'gnutls' 'c-ares' 'adns' 'snappy')
source=("https://tools.netsa.cert.org/releases/$pkgname-$pkgver.tar.gz")
sha512sums=('3833d90167437fb3f0542ebc8885d9f42c4dbf89b2cf395527009781cc75f92c9532b2c1d4a18dbcd0ef5b91a2d9bf58f986946fbc669cd44adaed0e2664fa50')

build() {
  cd "$pkgname-$pkgver"

  export LIBS="-lpthread"

  ./configure --prefix=/usr --enable-ipv6 --sbindir=/usr/bin --with-gnutls \
    --with-pic --with-pcap -with-libfixbuf --enable-output-compression \
    --enable-localtime --enable-data-rootdir="/usr/share/$pkgname"

  make
}

package() {
  cd "$pkgname-$pkgver"

  install -Dm 644 -t "$pkgdir/usr/share/licenses/$pkgname" doc/GPL.txt

  rm doc/{Makefile*,GPL.txt}

  install -Dm 644 -t "$pkgdir/usr/share/doc/$pkgname/" doc/*

  make DESTDIR="$pkgdir" install
}

