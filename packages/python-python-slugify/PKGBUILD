# This file is part of BlackArch Linux ( https://www.blackarch.org/ ).
# See COPYING for license details.

pkgbase=python-python-slugify
pkgname=('python2-python-slugify' 'python-python-slugify')
_pkgname=python-slugify
pkgver=3.0.6
pkgrel=1
pkgdesc='A Python slugify application that handles unicode.'
arch=('any')
url='https://pypi.org/project/python-slugify/#files'
license=('MIT')
makedepends=('python2-setuptools' 'python-setuptools')
source=("https://files.pythonhosted.org/packages/03/cd/f0b39ad9b5a17f93c0a006dbd49f67fc5dc8b53de58a61542bc034ca70a8/$_pkgname-$pkgver.tar.gz")
sha512sums=('f62e67f18753848155f2ddfa05e1bb869a8c28fb27fd5963fc7a67393237fe8f84ffdc447611a3e99a61c722952410ad46c10b2a70b7fe9d52c8f814cc12723e')

prepare() {
  cp -a "$_pkgname-$pkgver"{,-2}
}

build() {
  cd "$_pkgname-$pkgver-2"

  python2 setup.py build

  cd "$srcdir/$_pkgname-$pkgver"

  python setup.py build
}

package_python2-python-slugify() {
  depends=('python2' 'python2-unidecode')
  optdepends=('python2-text-unidecode: text-unidecode support')

  cd "$_pkgname-$pkgver-2"

  python2 setup.py install --prefix=/usr --root="$pkgdir" --optimize=1 \
    --skip-build

  mv "$pkgdir/usr/bin/slugify" "$pkgdir/usr/bin/slugify2"
}

package_python-python-slugify() {
  depends=('python' 'python-unidecode')
  optdepends=('python-text-unidecode: text-unidecode support')

  cd "$_pkgname-$pkgver"

  python setup.py install --prefix=/usr --root="$pkgdir" --optimize=1 \
    --skip-build
}

