# This file is part of BlackArch Linux ( https://www.blackarch.org/ ).
# See COPYING for license details.

pkgbase=python-python-slugify
pkgname=('python2-python-slugify' 'python-python-slugify')
_pkgname=python-slugify
pkgver=3.0.4
pkgrel=1
pkgdesc='A Python slugify application that handles unicode.'
arch=('any')
url='https://pypi.org/project/python-slugify/#files'
license=('MIT')
makedepends=('python2-setuptools' 'python-setuptools')
source=("https://files.pythonhosted.org/packages/f5/ef/c868a9ac657405f051a8a501ac5633e769c54228716b8db7f8d717977e57/$_pkgname-$pkgver.tar.gz")
sha512sums=('59cadfe24a9794577c4c9b7fd30ab56e129af47a8b56867bfea5c9b186f368eb3bd0b47b941c61f403af5e274654c8ee3722916b71ba5d34ed30e203ad335d12')

prepare() {
  cp -a "$_pkgname-$pkgver"{,-2}
}

build() {
  cd "$_pkgname-$pkgver-2"

  python2 setup.py build

  cd "$srcdir/$_pkgname-$pkgver"

  python setup.py build
}

package_python2-python-slugify() {
  depends=('python2' 'python2-unidecode')
  optdepends=('python2-text-unidecode: text-unidecode support')

  cd "$_pkgname-$pkgver-2"

  python2 setup.py install --prefix=/usr --root="$pkgdir" --optimize=1 \
    --skip-build

  mv "$pkgdir/usr/bin/slugify" "$pkgdir/usr/bin/slugify2"
}

package_python-python-slugify() {
  depends=('python' 'python-unidecode')
  optdepends=('python-text-unidecode: text-unidecode support')

  cd "$_pkgname-$pkgver"

  python setup.py install --prefix=/usr --root="$pkgdir" --optimize=1 \
    --skip-build
}

