# This file is part of BlackArch Linux ( https://www.blackarch.org/ ).
# See COPYING for license details.

pkgbase=python-python-slugify
pkgname=('python2-python-slugify' 'python-python-slugify')
_pkgname=python-slugify
pkgver=3.0.3
pkgrel=1
pkgdesc='A Python slugify application that handles unicode.'
arch=('any')
url='https://pypi.org/project/python-slugify/#files'
license=('MIT')
makedepends=('python2-setuptools' 'python-setuptools')
source=("https://files.pythonhosted.org/packages/a2/5d/bd30413c00bbed3945558aca07c55944073e1e30abeee1f06515281f9811/$_pkgname-$pkgver.tar.gz")
sha512sums=('65e2373c33b6b23b25d4d3b66cae3c68e9cc133dd6e26155151647bc2e1df5bbba7dfb187eafe5fa9ffb488b2b6f3415e7a5f95d2460dc3f551383d509d740fb')

prepare() {
  cp -a "$_pkgname-$pkgver"{,-2}
}

build() {
  cd "$_pkgname-$pkgver-2"

  python2 setup.py build

  cd "$srcdir/$_pkgname-$pkgver"

  python setup.py build
}

package_python2-python-slugify() {
  depends=('python2' 'python2-unidecode')
  optdepends=('python2-text-unidecode: text-unidecode support')

  cd "$_pkgname-$pkgver-2"

  python2 setup.py install --prefix=/usr --root="$pkgdir" --optimize=1 \
    --skip-build

  mv "$pkgdir/usr/bin/slugify" "$pkgdir/usr/bin/slugify2"
}

package_python-python-slugify() {
  depends=('python' 'python-unidecode')
  optdepends=('python-text-unidecode: text-unidecode support')

  cd "$_pkgname-$pkgver"

  python setup.py install --prefix=/usr --root="$pkgdir" --optimize=1 \
    --skip-build
}

