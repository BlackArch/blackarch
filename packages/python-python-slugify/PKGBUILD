# This file is part of BlackArch Linux ( https://www.blackarch.org/ ).
# See COPYING for license details.

pkgbase=python-python-slugify
pkgname=('python2-python-slugify' 'python-python-slugify')
_pkgname=python-slugify
pkgver=4.0.0
pkgrel=2
pkgdesc='A Python slugify application that handles unicode.'
arch=('any')
url='https://pypi.org/project/python-slugify/#files'
license=('MIT')
makedepends=('python2-setuptools' 'python-setuptools')
source=("https://files.pythonhosted.org/packages/92/5f/7b84a0bba8a0fdd50c046f8b57dcf179dc16237ad33446079b7c484de04c/$_pkgname-$pkgver.tar.gz")
sha512sums=('6b4c6c0891b2abb4f8ecd2523fa2211ad699bc3930422d867f91886dd3d9660959dcabacbe381590610b57d2cafff916a1c430846a7e1404b9f4a2b68851f0ce')

prepare() {
  cp -a "$_pkgname-$pkgver"{,-2}
}

build() {
  cd "$_pkgname-$pkgver-2"

  python2 setup.py build

  cd "$srcdir/$_pkgname-$pkgver"

  python setup.py build
}

package_python2-python-slugify() {
  depends=('python2' 'python2-unidecode')
  optdepends=('python2-text-unidecode: text-unidecode support')

  cd "$_pkgname-$pkgver-2"

  python2 setup.py install --prefix=/usr --root="$pkgdir" --optimize=1 \
    --skip-build

  mv "$pkgdir/usr/bin/slugify" "$pkgdir/usr/bin/slugify2"
}

package_python-python-slugify() {
  depends=('python' 'python-unidecode')
  optdepends=('python-text-unidecode: text-unidecode support')

  cd "$_pkgname-$pkgver"

  python setup.py install --prefix=/usr --root="$pkgdir" --optimize=1 \
    --skip-build
}

