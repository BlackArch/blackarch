# This file is part of BlackArch Linux ( https://www.blackarch.org/ ).
# See COPYING for license details.

pkgname=haiti-git
_pkgname=haiti
_gemname=haiti-hash
pkgver=v1.0.0.r10.g9baf8df
pkgrel=2
groups=('blackarch' 'blackarch-crypto')
pkgdesc='A CLI tool to identify the hash type of a given hash'
arch=('any')
url='https://orange-cyberdefense.github.io/haiti/'
license=('MIT')
depends=('ruby' 'ruby-paint' 'ruby-docopt')
options=(!emptydirs)
source=("git+https://github.com/Orange-Cyberdefense/$_pkgname.git")
sha512sums=('SKIP')

pkgver() {
  cd $_pkgname

  git describe --long | sed 's/\([^-]*-g\)/r\1/;s/-/./g'
}

build() {
  cd $_pkgname

  gem build $_pkgname.gemspec
}

package() {
  cd $_pkgname

  _gemdir="$(ruby -e'puts Gem.default_dir')"
  _release=$(gem build $_pkgname.gemspec | grep Version | cut -d' ' -f4)

  gem install --ignore-dependencies --no-user-install --no-document \
    -i "$pkgdir/$_gemdir" -n "$pkgdir/usr/bin" $_gemname-$_release.gem

  rm "$pkgdir/$_gemdir/cache/$_gemname-$_release.gem"
  find "$pkgdir/$_gemdir/extensions/" -name *.so -delete
  rm -r "$pkgdir/$_gemdir/gems/$_gemname-$_release/test"

  install -D -m644 "$pkgdir/$_gemdir/gems/$_gemname-$_release/LICENSE.txt" \
    "$pkgdir/usr/share/licenses/$pkgname/LICENSE"
}

