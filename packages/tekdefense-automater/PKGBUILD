# This file is part of BlackArch Linux ( http://blackarch.org ).
# See COPYING for license details.

pkgname='tekdefense-automater'
pkgver=88.42548cf
pkgrel=3
groups=('blackarch' 'blackarch-forensic')
pkgdesc='IP URL and MD5 OSINT Analysis'
arch=('any')
url='https://github.com/1aN0rmus/TekDefense-Automater'
license=('MIT')
depends=('python2')
makedepends=('git')
source=('git+https://github.com/1aN0rmus/TekDefense-Automater.git')
sha1sums=('SKIP')

pkgver() {
  cd "$srcdir/TekDefense-Automater"

  echo $(git rev-list --count HEAD).$(git rev-parse --short HEAD)
}

prepare() {
  cd "$srcdir/TekDefense-Automater"

  sed -i 's|/usr/bin/python|/usr/bin/python2|g' Automater.py
}

package() {
  cd "$srcdir/TekDefense-Automater"

  mkdir -p "$pkgdir/usr/bin"
  mkdir -p "$pkgdir/usr/share/tekdefense-automater"

  for f in *.py
  do
    install -m644 $f "$pkgdir/usr/share/tekdefense-automater/$f"
  done

  install -Dm644 -t "$pkgdir/usr/share/tekdefense-automater/" *.xml
  install -Dm644 "LICENSE" \
    "$pkgdir/usr/share/licenses/tekdefense-automater/LICENSE"
  install -Dm644 "README.md" \
    "$pkgdir/usr/share/doc/tekdefense-automater/README.md"

  cp -a docs/* "$pkgdir/usr/share/doc/tekdefense-automater"

  cat > "$pkgdir/usr/bin/automater" << EOF
#!/bin/sh
cd /usr/share/tekdefense-automater
exec python2 Automater.py "\$@"
EOF

  chmod +x "$pkgdir/usr/bin/automater"
}
