# This file is part of BlackArch Linux ( https://www.blackarch.org/ ).
# See COPYING for license details.

pkgname=rita
pkgver=752.0abef45
pkgrel=1
pkgdesc='Real Intelligence Threat Analytics.'
arch=('x86_64')
groups=('blackarch' 'blackarch-recon')
url='https://github.com/activecm/rita'
license=('GPL3')
depends=('bro' 'mongodb')
makedepends=('git' 'go-pie' 'dep')
source=("git+https://github.com/activecm/$pkgname.git")
sha512sums=('SKIP')
_url='github.com/activecm'

pkgver() {
  cd $pkgname

  echo $(git rev-list --count HEAD).$(git rev-parse --short HEAD)
}

prepare() {
  export GOPATH="$srcdir/go"

  mkdir -p "go/src/$_url"
  ln -rTsf "$pkgname" "go/src/$_url/$pkgname"
}

build() {
  export GOPATH="$srcdir/go"

  cd "go/src/$_url/$pkgname"
  make
}

package() {
  cd "go/src/$_url/$pkgname"

  install -dm 755 "$pkgdir/etc/$pkgname"
  install -dm 755 "$pkgdir/var/lib/$pkgname/logs"
  install -Dm 755 $pkgname "$pkgdir/usr/bin/$pkgname"
  install -Dm 666 "etc/$pkgname.yaml" "$pkgdir/etc/$pkgname/config.yaml"
  install -Dm 644 -t "$pkgdir/usr/share/doc/$pkgname/" docs/*
  install -Dm 644 -t "$pkgdir/usr/share/licenses/$pkgname/" LICENSE
}

