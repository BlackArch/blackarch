# This file is part of BlackArch Linux ( https://www.blackarch.org/ ).
# See COPYING for license details.

pkgname=gvm-tools
pkgver=2234.3fd4291
pkgrel=1
pkgdesc='a collection of tools that help with remote controlling a Greenbone Enterprise Appliance.'
arch=('any')
groups=('blackarch' 'blackarch-scanner')
url='https://github.com/greenbone/'
license=('GPL')
depends=('python' 'python-build' 'python-installer' 'python-wheel'
  'python-poetry')
makedepends=('git')
source=("git+https://github.com/greenbone/$pkgname.git")
sha512sums=('SKIP')

pkgver() {
  cd $pkgname

  echo "$(git rev-list --count HEAD)"."$(git rev-parse --short HEAD)"
}

build() {
  cd $pkgname

  python -m build --wheel --no-isolation
}

package() {
  cd $pkgname

  python -m installer --destdir="$pkgdir" dist/*.whl
}

