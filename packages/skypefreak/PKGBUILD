# This file is part of BlackArch Linux ( http://blackarch.org ).
# See COPYING for license details.

pkgname=skypefreak
pkgver=33.9347a65
pkgrel=2
groups=('blackarch' 'blackarch-forensic')
pkgdesc='A Cross Platform Forensic Framework for Skype.'
url='http://osandamalith.github.io/SkypeFreak/'
arch=('any')
license=('GPL3')
depends=('python2' 'python2-pysqlite')
makedepends=('git')
source=('git+https://github.com/OsandaMalith/SkypeFreak.git')
sha512sums=('SKIP')

pkgver() {
  cd SkypeFreak

  echo $(git rev-list --count HEAD).$(git rev-parse --short HEAD)
}

prepare() {
  cd SkypeFreak

  sed -i '1i\#!/usr/bin/env python2' 'source/SkypeFreak.py'
}

package() {
  cd SkypeFreak

  install -Dm 755 'source/SkypeFreak.py' "$pkgdir/usr/bin/$pkgname"
  install -Dm 644 README.md "$pkgdir/usr/share/doc/$pkgname/README.md"
}

