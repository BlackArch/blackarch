# This file is part of BlackArch Linux ( https://www.blackarch.org/ ).
# See COPYING for license details.

pkgname=skypefreak
pkgver=33.9347a65
pkgrel=2
groups=('blackarch' 'blackarch-forensic')
pkgdesc='A Cross Platform Forensic Framework for Skype.'
url='http://osandamalith.github.io/SkypeFreak/'
arch=('any')
license=('GPL3')
depends=('python2' 'python2-pysqlite')
makedepends=('git')
source=("$pkgname::git+https://github.com/OsandaMalith/SkypeFreak.git")
sha512sums=('SKIP')

pkgver() {
  cd $pkgname

  echo $(git rev-list --count HEAD).$(git rev-parse --short HEAD)
}

prepare() {
  cd $pkgname

  sed -i '1i\#!/usr/bin/env python2' 'source/SkypeFreak.py'
}

package() {
  cd $pkgname

  install -Dm 755 'source/SkypeFreak.py' "$pkgdir/usr/bin/$pkgname"
  install -Dm 644 -t "$pkgdir/usr/share/doc/$pkgname/" README.md
}

