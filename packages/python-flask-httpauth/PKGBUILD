# This file is part of BlackArch Linux ( https://blackarch.org )
# See COPYING for license details.

pkgbase=python-flask-httpauth
pkgname=('python2-flask-httpauth' 'python-flask-httpauth')
_pkgname=Flask-HTTPAuth
pkgver=4.2.0
pkgrel=2
pkgdesc='Basic and Digest HTTP authentication for Flask routes.'
arch=('any')
url='https://pypi.org/project/Flask-httpauth/#files'
makedepends=('python2-setuptools' 'python-setuptools')
license=('MIT')
source=("https://files.pythonhosted.org/packages/2d/6a/e458a74c909899d136aa76cb4d707f0f600fba6ca0d603de681e8fcac91f/Flask-HTTPAuth-$pkgver.tar.gz")
sha512sums=('29e0793dba9e8b013de14e4949316b6ae79429cbfd1cad509fd76a8f107a3d2723b565347fefc079a264cf7800b2a1685399851787ddde24e3af2e685969c47a')

prepare() {
  cp -a "$_pkgname-$pkgver"{,-2}
}

build() {
  cd "$_pkgname-$pkgver-2"

  python2 setup.py build

  cd "$srcdir/$_pkgname-$pkgver"

  python setup.py build
}

package_python2-flask-httpauth() {
  depends=('python2' 'python2-flask')

  cd "$_pkgname-$pkgver-2"

  python2 setup.py install --prefix=/usr --root="$pkgdir" --optimize=1 \
    --skip-build
}

package_python-flask-httpauth() {
  depends=('python' 'python-flask')

  cd "$_pkgname-$pkgver"

  python setup.py install --prefix=/usr --root="$pkgdir" --optimize=1 \
    --skip-build
}

