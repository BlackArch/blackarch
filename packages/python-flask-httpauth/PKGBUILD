# This file is part of BlackArch Linux ( https://blackarch.org )
# See COPYING for license details.

pkgbase=python-flask-httpauth
pkgname=('python2-flask-httpauth' 'python-flask-httpauth')
_pkgname=Flask-HTTPAuth
pkgver=4.3.0
pkgrel=1
pkgdesc='Basic and Digest HTTP authentication for Flask routes.'
arch=('any')
url='https://pypi.org/project/Flask-httpauth/#files'
makedepends=('python2-setuptools' 'python-setuptools')
license=('MIT')
source=("https://files.pythonhosted.org/packages/fe/83/7899cbf0b2eeb97f1bc7b61cb5ba255b47e831c01d191ce91145ff7b3bf4/Flask-HTTPAuth-$pkgver.tar.gz")
sha512sums=('eda90f0b468060a8156b8a09fa1c6ba5fe4361cf0e036a9473193979ef4071abf981438399d7687bfef962f68e7b0ec5aa367c187c0a6b4d2062bd7042c9a9ee')

prepare() {
  cp -a "$_pkgname-$pkgver"{,-2}
}

build() {
  cd "$_pkgname-$pkgver-2"

  python2 setup.py build

  cd "$srcdir/$_pkgname-$pkgver"

  python setup.py build
}

package_python2-flask-httpauth() {
  depends=('python2' 'python2-flask')

  cd "$_pkgname-$pkgver-2"

  python2 setup.py install --prefix=/usr --root="$pkgdir" --optimize=1 \
    --skip-build
}

package_python-flask-httpauth() {
  depends=('python' 'python-flask')

  cd "$_pkgname-$pkgver"

  python setup.py install --prefix=/usr --root="$pkgdir" --optimize=1 \
    --skip-build
}

