# This file is part of BlackArch Linux ( https://blackarch.org )
# See COPYING for license details.

pkgbase=python-flask-httpauth
pkgname=('python2-flask-httpauth' 'python-flask-httpauth')
_pkgname=Flask-HTTPAuth
pkgver=4.6.0
pkgrel=1
pkgdesc='Basic and Digest HTTP authentication for Flask routes.'
arch=('any')
url='https://pypi.org/project/Flask-httpauth/#files'
makedepends=('python2-setuptools' 'python-setuptools')
license=('MIT')
source=("https://files.pythonhosted.org/packages/0b/92/2e0ea6a6193dbd36925ee296ede67bc861f8219a722a33d8ec7227c7c419/Flask-HTTPAuth-$pkgver.tar.gz")
sha512sums=('7284f590d19f51be079800ca146fa7d444a29babfe31be8062355762b4db2238c44fc76f31bb8fae8f461130b90075216307b6a740a6d1db1b6784eb698b666a')

prepare() {
  cp -a "$_pkgname-$pkgver"{,-2}
}

build() {
  cd "$_pkgname-$pkgver-2"

  python2 setup.py build

  cd "$srcdir/$_pkgname-$pkgver"

  python setup.py build
}

package_python2-flask-httpauth() {
  depends=('python2' 'python2-flask')

  cd "$_pkgname-$pkgver-2"

  python2 setup.py install --prefix=/usr --root="$pkgdir" --optimize=1 \
    --skip-build
}

package_python-flask-httpauth() {
  depends=('python' 'python-flask')

  cd "$_pkgname-$pkgver"

  python setup.py install --prefix=/usr --root="$pkgdir" --optimize=1 \
    --skip-build
}

