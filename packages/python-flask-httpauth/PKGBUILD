# This file is part of BlackArch Linux ( https://blackarch.org )
# See COPYING for license details.

pkgbase=python-flask-httpauth
pkgname=('python2-flask-httpauth' 'python-flask-httpauth')
_pkgname=Flask-HTTPAuth
pkgver=4.4.0
pkgrel=1
pkgdesc='Basic and Digest HTTP authentication for Flask routes.'
arch=('any')
url='https://pypi.org/project/Flask-httpauth/#files'
makedepends=('python2-setuptools' 'python-setuptools')
license=('MIT')
source=("https://files.pythonhosted.org/packages/87/dc/578619e51fbcca28b3d263907558267d989a2f484bc264e2de83feca8c60/Flask-HTTPAuth-$pkgver.tar.gz")
sha512sums=('de5d13f81b8fcabafaaf62756ff03b30ed01b873d5a9fc850c42d30059aa5f2e489c4ebc46fd11e625eaf8b59a84f5a8a84156167e4ccd5f9f5f8c8281bbb4e8')

prepare() {
  cp -a "$_pkgname-$pkgver"{,-2}
}

build() {
  cd "$_pkgname-$pkgver-2"

  python2 setup.py build

  cd "$srcdir/$_pkgname-$pkgver"

  python setup.py build
}

package_python2-flask-httpauth() {
  depends=('python2' 'python2-flask')

  cd "$_pkgname-$pkgver-2"

  python2 setup.py install --prefix=/usr --root="$pkgdir" --optimize=1 \
    --skip-build
}

package_python-flask-httpauth() {
  depends=('python' 'python-flask')

  cd "$_pkgname-$pkgver"

  python setup.py install --prefix=/usr --root="$pkgdir" --optimize=1 \
    --skip-build
}

