# Maintainer: lsfxz (https://github.com/lsfxz/quarry)
# This file is part of BlackArch Linux ( http://blackarch.org ).
# See COPYING for license details.

pkgname='ruby-haml-4'
_gemname='haml'
pkgver='4.0.7'
pkgrel=1
pkgdesc="An elegant, structured (X)HTML/XML templating engine."
arch=('any')
url='http://haml.info/'
license=("MIT")
depends=('ruby' 'ruby-tilt')
options=('!emptydirs')
source=("https://rubygems.org/downloads/$_gemname-$pkgver.gem" )
noextract=("$_gemname-$pkgver.gem")
sha1sums=('8cd4dd26aabcbb769ddc53993ad87b212962c007' )


package() {
  local _gemdir="$(ruby -rubygems -e 'puts Gem.default_dir')"
  gem install --ignore-dependencies --no-document --no-user-install -i "$pkgdir/$_gemdir" -n "$pkgdir"/usr/bin $_gemname-$pkgver.gem 
  rm "$pkgdir/$_gemdir/cache/$_gemname-$pkgver.gem"
  install -D -m644 "$pkgdir/$_gemdir/gems/$_gemname-$pkgver/MIT-LICENSE" "$pkgdir/usr/share/licenses/$pkgname/MIT-LICENSE"
  # non-HEAD version should not install any files in /usr/bin
  rm -r "$pkgdir"/usr/bin/

  # Preserve library and other required directories. Remove junk that gem installs.
  local _copydir=`mktemp -d -t $_gemname.XXX`
  chmod 755 $_copydir
  (cd "$pkgdir/$_gemdir/gems/$_gemname-$pkgver"/ && cp -r --parents lib $_copydir)
  rm -rf "$pkgdir/$_gemdir/gems/$_gemname-$pkgver"
  mv $_copydir "$pkgdir/$_gemdir/gems/$_gemname-$pkgver"

}
