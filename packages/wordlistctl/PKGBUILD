# This file is part of BlackArch Linux ( https://www.blackarch.org/ ).
# See COPYING for license details.

pkgname=wordlistctl
pkgver=0.8.9
pkgrel=1
groups=('blackarch' 'blackarch-misc')
pkgdesc='Fetch, install and search wordlist archives from websites.'
arch=('any')
url='https://github.com/BlackArch/wordlistctl'
license=('GPL3')
depends=('python' 'python-requests' 'python-termcolor')
source=("https://github.com/BlackArch/$pkgname/archive/v$pkgver.tar.gz")
sha512sums=('db5acbd0f246e9c6a0eff38524f7cd7feb85ae502a10ac1eb2d974e51af9011f65496463545b2a87aa1fc7ea9f765d2fce02a9841bb89647a79145b5cd177e0b')

package() {
  cd "$pkgname-$pkgver"

  install -dm 755 "$pkgdir/usr/bin"
  install -dm 755 "$pkgdir/usr/share/$pkgname"

  install -Dm 644 -t "$pkgdir/usr/share/doc/$pkgname/" README.md
  install -Dm 644 LICENSE "$pkgdir/usr/share/licenses/$pkgname/LICENSE"
  install -Dm 644 "bash-completion/$pkgname" \
    "$pkgdir/usr/share/bash-completion/completions/$pkgname"
  install -Dm 644 "man/$pkgname.1" "$pkgdir/usr/share/man/man1/$pkgname.1"

  rm -r README.md LICENSE bash-completion

  cp -a * "$pkgdir/usr/share/$pkgname/"

  cat > "$pkgdir/usr/bin/$pkgname" << EOF
#!/bin/sh
exec /usr/share/$pkgname/$pkgname.py "\$@"
EOF

  chmod +x "$pkgdir/usr/bin/$pkgname"
}

