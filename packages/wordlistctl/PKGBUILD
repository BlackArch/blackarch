# This file is part of BlackArch Linux ( https://www.blackarch.org/ ).
# See COPYING for license details.

pkgname=wordlistctl
pkgver=0.7.1
pkgrel=1
groups=('blackarch' 'blackarch-misc')
pkgdesc='Fetch, install and search wordlist archives from websites and torrent peers.'
arch=('any')
url='https://github.com/BlackArch/wordlistctl'
license=('GPL3')
depends=('python' 'python-beautifulsoup4' 'python-rarfile' 'python-requests'
         'python-libarchive-c' 'libtorrent-rasterbar' 'python-termcolor')
source=("https://github.com/BlackArch/wordlistctl/archive/v$pkgver.tar.gz")
sha512sums=('5d8ed4121ef31de0768cc192a2b6f8aa02a9031a5884dd349c074c162d56e2ff44793c28bb2dfdaca21e32ff5dd35807ef8d6ebe17f5c98896ac3d5f843e101a')

package() {
  cd "$pkgname-$pkgver"

  install -dm 755 "$pkgdir/usr/bin"
  install -dm 755 "$pkgdir/usr/share/$pkgname"

  install -Dm 644 -t "$pkgdir/usr/share/doc/$pkgname/" README.md
  install -Dm 644 LICENSE "$pkgdir/usr/share/licenses/$pkgname/LICENSE"

  rm README.md LICENSE

  cp -a * "$pkgdir/usr/share/$pkgname/"

  cat > "$pkgdir/usr/bin/$pkgname" << EOF
#!/bin/sh
exec /usr/share/$pkgname/$pkgname.py "\$@"
EOF

  chmod +x "$pkgdir/usr/bin/$pkgname"
}

