# This file is part of BlackArch Linux ( https://www.blackarch.org/ ).
# See COPYING for license details.

pkgname=wordlistctl
pkgver=0.5
pkgrel=1
groups=('blackarch' 'blackarch-misc')
pkgdesc='Fetch, install and search wordlist archives from websites and torrent peers.'
arch=('any')
url='https://github.com/BlackArch/wordlistctl'
license=('GPL3')
depends=('python' 'python-beautifulsoup4' 'python-rarfile' 'python-requests'
         'python-libarchive-c' 'libtorrent-rasterbar')
source=("https://github.com/BlackArch/wordlistctl/archive/v$pkgver.tar.gz")
sha512sums=('806c9e06e49391baf9f3a82f11446787838762d0ed5fd059be0b15f0f688b7c349053de85356eb3b3656138ca95239b952f0aa9046d069ea9e1ec8bfc0f597b3')

package() {
  cd "$pkgname-$pkgver"

  install -dm 755 "$pkgdir/usr/bin"
  install -dm 755 "$pkgdir/usr/share/$pkgname"

  install -Dm 644 -t "$pkgdir/usr/share/doc/$pkgname/" README.md
  install -Dm 644 LICENSE "$pkgdir/usr/share/licenses/$pkgname/LICENSE"

  rm README.md LICENSE

  cp -a * "$pkgdir/usr/share/$pkgname/"

  cat > "$pkgdir/usr/bin/$pkgname" << EOF
#!/bin/sh
exec /usr/share/$pkgname/$pkgname.py "\$@"
EOF

  chmod +x "$pkgdir/usr/bin/$pkgname"
}

