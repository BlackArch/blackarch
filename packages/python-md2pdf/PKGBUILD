# This file is part of BlackArch Linux ( https://www.blackarch.org/ ).
# See COPYING for license details.

pkgname=python-md2pdf
_pkgname=${pkgname#python-}
pkgver=3.1.0
pkgrel=1
pkgdesc='Python-based Markdown to PDF converter.'
arch=('any')
url='https://pypi.org/project/md2pdf'
license=('MIT')
depends=('python' 'python-weasyprint' 'python-docopt' 'python-markdown2')
makedepends=('python-setuptools' 'python-pip')
options=(!emptydirs)
source=("https://files.pythonhosted.org/packages/source/${_pkgname::1}/$_pkgname/$_pkgname-$pkgver.tar.gz")
sha512sums=('072b22a26c17df16d1b898316cc930bfdb4290d841f22ac498bf2ccb9bafa9f30e1754a472bdb278ad1fcd313c46b7eaed583ca6294791f163d3d8fd6f1c9454')

prepare() {
  cd "$_pkgname-$pkgver"

  sed -i 's/find_packages()/find_packages(exclude=["tests*"])/g' setup.py
}

build() {
  cd "$_pkgname-$pkgver"

  python setup.py build
}

package() {
  cd "$_pkgname-$pkgver"

  python setup.py install --root="$pkgdir" --prefix=/usr -O1 --skip-build
}

