# This file is part of BlackArch Linux ( https://www.blackarch.org/ ).
# See COPYING for license details.

pkgname=dorkscout
pkgver=1.0.r13.gdd87daf
pkgrel=1
pkgdesc='Golang tool to automate google dork scan against the entire internet or specific targets.'
arch=('x86_64' 'aarch64')
groups=('blackarch' 'blackarch-automation' 'blackarch-recon')
url='https://github.com/R4yGM/dorkscout'
license=('Apache')
depends=('glibc')
makedepends=('git' 'go')
source=("git+https://github.com/R4yGM/$pkgname.git")
sha256sums=('SKIP')

pkgver() {
  cd $pkgname

  git describe --long --tags | sed 's/\([^-]*-g\)/r\1/;s/-/./g'
}

build() {
  cd $pkgname

  GOPATH="$srcdir" go mod download
  GOPATH="$srcdir" go build \
    -trimpath \
    -buildmode=pie \
    -mod=readonly \
    -modcacherw \
    -ldflags "-s -w" \
    -o $pkgname .

}

package() {
  cd $pkgname

  install -Dm 755 $pkgname "$pkgdir/usr/bin/$pkgname"
  install -Dm 644 -t "$pkgdir/usr/share/doc/$pkgname/" README.md
}

