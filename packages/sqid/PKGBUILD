pkgname='sqid'
pkgver='0.3'
pkgrel=1
pkgdesc='A SQL injection digger.'
arch=('any')
url='http://sqid.rubyforge.org/'
license=('GPL')
depends=('ruby')
groups=('blackarch' 'blackarch-webapp')
source=("http://packetstorm.igor.onlinedirect.bg/UNIX/scanners/sqid-$pkgver.tar.gz")
md5sums=('f09d8bd83176f2e4fbdd3d81bb3e712c')

prepare() {
  cd "$srcdir/sqid-$pkgver"
  
  sed -i -e 's|sqid.db|/usr/share/sqid/sqid.db|g' "sqid.rb"
}

package() {
  cd "$srcdir/sqid-$pkgver"

  install -d -m755 "$pkgdir/usr/bin"
  install -d -m755 "$pkgdir/usr/share/sqid"
  
  install -m755 "sqid.rb" "$pkgdir/usr/share/sqid"
  install -m644 "readme" "sqid.db" "$pkgdir/usr/share/sqid"

  cat > "$pkgdir/usr/bin/sqid" <<EOF
  #!/bin/sh
  cd /usr/share/sqid/
  ruby sqid.rb "\$@"
EOF
  chmod 755 "$pkgdir/usr/bin/sqid"
}
