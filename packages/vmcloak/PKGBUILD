# This file is part of BlackArch Linux ( https://www.blackarch.org/ ).
# See COPYING for license details.

pkgname=vmcloak
pkgver=890.5f2a568
pkgrel=1
epoch=2
pkgdesc='Automated Virtual Machine Generation and Cloaking for Cuckoo Sandbox.'
groups=('blackarch-malware')
url='https://github.com/jbremer/vmcloak'
arch=('any')
license=('GPL3')
depends=('cdrkit' 'virtualbox' 'dmidecode' 'lshw' 'python2-lockfile'
         'python2-sphinx')
makedepends=('python2-setuptools' 'unzip')
source=('git+https://github.com/jbremer/vmcloak.git')
sha512sums=('SKIP')

pkgver() {
  cd $pkgname

  echo $(git rev-list --count HEAD).$(git rev-parse --short HEAD)
}

build() {
  cd "$pkgname"

  python2 setup.py build
}

package() {
  cd "$pkgname"

  python2 setup.py install --root="$pkgdir" --skip-build

  install -Dm 644 docs/LICENSE.txt "$pkgdir/usr/share/licenses/$pkgname/LICENSE"
}

