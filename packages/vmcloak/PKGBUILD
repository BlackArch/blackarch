# This file is part of BlackArch Linux ( https://www.blackarch.org/ ).
# See COPYING for license details.

pkgname=vmcloak
pkgver=955.fae1f29
pkgrel=1
epoch=2
pkgdesc='Automated Virtual Machine Generation and Cloaking for Cuckoo Sandbox.'
groups=('blackarch' 'blackarch-malware')
url='https://github.com/Cryss76/vmcloak'
arch=('any')
license=('GPL3')
depends=('python' 'python-sphinx' 'python-sphinx_rtd_theme' 'python-lockfile'
         'python-requests' 'python-alembic' 'python-sqlalchemy'
         'python-pefile' 'python-jinja' 'python-psutil'
         'python-pyaml' 'python-click' 'virtualbox' 'lshw' 'dmidecode'
         'cdrtools')
makedepends=('git' 'python-setuptools')
source=("$pkgname::git+https://github.com/Cryss76/$pkgname.git"
        'setup.py.patch')
sha512sums=('SKIP'
            'c58638afaad704f7b504fa62bf9427c481f6397857e4fe87f70d11c753664dc1fe1b3f2a6056ab35f266a2b8f7f4c7afd9803e47e99effcf7875eadd3f50bfc2')

pkgver() {
  cd $_pkgbase

  ( set -o pipefail
    git describe --long --tags --abbrev=7 2>/dev/null |
      sed 's/\([^-]*-g\)/r\1/;s/-/./g' ||
    printf "%s.%s" "$(git rev-list --count HEAD)" \
      "$(git rev-parse --short=7 HEAD)"
  )
}

prepare() {
  cd $pkgname

  patch -p1 < ../setup.py.patch
}

build() {
  cd $pkgname

  python setup.py build
}

package() {
  cd $pkgname

  python setup.py install --root="$pkgdir" --prefix=/usr -O1 --skip-build

  install -Dm 644 docs/LICENSE.txt "$pkgdir/usr/share/licenses/$pkgname/LICENSE"
}
