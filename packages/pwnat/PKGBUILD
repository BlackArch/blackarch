# This file is part of BlackArch Linux ( http://blackarch.org ).
# See COPYING for license details.

pkgname='pwnat'
pkgver=13.5de412c
pkgrel=1
groups=('blackarch' 'blackarch-networking')
pkgdesc='A tool that allows any number of clients behind NATs to communicate with a server behind a separate NAT with *no* port forwarding and *no* DMZ setup on any routers in order to directly communicate with each other.'
arch=('x86_64' 'armv6h' 'armv7h' 'aarch64')
url='http://samy.pl/pwnat/'
license=('GPL')
depends=('glibc')
makedepends=('git')
source=('git+https://github.com/samyk/pwnat'
        'http://samy.pl/pwnat/pwnat.pdf')
sha1sums=('SKIP'
          '2112172d79789cbe6175f9671604d02144655a86')

pkgver() {
  cd "$srcdir/pwnat"

  echo $(git rev-list --count HEAD).$(git rev-parse --short HEAD)
}

build() {
  cd "$srcdir/pwnat"

  make
}

package() {
  cd "$srcdir/pwnat"

  install -Dm755 pwnat "$pkgdir/usr/bin/pwnat"
  install -Dm644 -t "$pkgdir/usr/share/doc/pwnat/" README-udptunnel README \
    Changes $srcdir/pwnat.pdf
}
