# This file is part of BlackArch Linux ( https://www.blackarch.org/ ).
# See COPYING for license details.

pkgname=python-libvmi
_pkgname=libvmi
pkgver=v3.7.1.r0.gc21af83
pkgrel=2
epoch=1
pkgdesc='Python interface to LibVMI.'
arch=('x86_64' 'aarch64')
url='https://github.com/libvmi/python'
license=('LGPL-3.0-only')
depends=('python' 'python-pkgconfig' 'python-cffi' 'python-future' 'libvmi')
makedepends=('git' 'python-setuptools' 'python-wheel')
source=("$pkgname::git+https://github.com/$_pkgname/python.git")
sha512sums=('SKIP')

pkgver() {
  cd $pkgname

  ( set -o pipefail
    git describe --long --tags --abbrev=7 2>/dev/null |
      sed 's/\([^-]*-g\)/r\1/;s/-/./g' ||
    printf "%s.%s" "$(git rev-list --count HEAD)" \
      "$(git rev-parse --short=7 HEAD)"
  )
}

prepare() {
  cd $pkgname

  sed -i 's|"pytest-runner"||g' setup.py
}

build() {
  cd $pkgname

  python setup.py build
}

package() {
  cd $pkgname

  python setup.py install --root="$pkgdir" --prefix=/usr -O1 --skip-build
}

