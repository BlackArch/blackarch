# This file is part of BlackArch Linux ( https://www.blackarch.org/ ).
# See COPYING for license details.

pkgname=uhoh365
_pkgname=UhOh365
pkgver=26.110277a
pkgrel=1
pkgdesc='Script to enumerate Office 365 users without performing login attempts'
groups=('blackarch' 'blackarch-recon')
arch=('any')
url='https://github.com/Raikia/UhOh365'
license=('GPL3')
depends=('python' 'python-requests' 'python-argparse')
makedepends=('git')
source=("git+https://github.com/Raikia/$_pkgname.git")
sha512sums=('SKIP')

pkgver() {
  cd $_pkgname

  echo $(git rev-list --count HEAD).$(git rev-parse --short HEAD)
}

package() {
  cd $_pkgname

  install -Dm 755 $_pkgname.py "$pkgdir/usr/bin/$pkgname"
  install -Dm 644 -t "$pkgdir/usr/share/doc/$pkgname/" README.md
  install -Dm 644 LICENSE "$pkgdir/usr/share/licenses/$pkgname/LICENSE"
}

