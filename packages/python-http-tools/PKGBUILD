# This file is part of BlackArch Linux ( https://www.blackarch.org/ ).
# See COPYING for license details.

pkgbase=python-http-tools
pkgname=('python2-http-tools' 'python-http-tools')
_pkgname=http-tools
pkgver=1.0.1
pkgrel=1
pkgdesc='httptools helps you to capture, repeat and live intercept HTTP requests. It is built on top of [mitmproxy].'
arch=('any')
url='https://pypi.org/project/http-tools/#files'
license=('GPL3')
makedepends=('python2-setuptools' 'python-setuptools')
source=("https://files.pythonhosted.org/packages/98/f1/10061b4c2e6b96f901ff3ac4ac02c42adf82504ddd1c61e93d931f3c763d/$_pkgname-$pkgver.tar.gz")
sha512sums=('73f548a6d1184646adba2d81e05d94680f1077f1b0676eb6fa8142b3ce717a0c92d2bc33931451bd86a069b953df59a9014c06bf77575fa9f7cf68ea3c0b6ce7')

prepare() {
  cp -a "$_pkgname-$pkgver"{,-2}
}

build() {
  cd "$_pkgname-$pkgver-2"

  python2 setup.py build

  cd "$srcdir/$_pkgname-$pkgver"

  python setup.py build
}

package_python2-http-tools() {
  depends=('python2')

  cd "$_pkgname-$pkgver-2"

  python2 setup.py install --prefix=/usr --root="$pkgdir" -O1 --skip-build

  mv "$pkgdir/usr/bin/httptools" "$pkgdir/usr/bin/httptools2"
}

package_python-http-tools() {
  depends=('python')

  cd "$_pkgname-$pkgver"

  python setup.py install --prefix=/usr --root="$pkgdir" -O1 --skip-build
}

