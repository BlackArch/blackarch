# This file is part of BlackArch Linux ( https://www.blackarch.org/ ).
# See COPYING for license details.

pkgbase=python-http-tools
pkgname=('python2-http-tools' 'python-http-tools')
_pkgname=http-tools
pkgver=1.0.2
pkgrel=1
pkgdesc='httptools helps you to capture, repeat and live intercept HTTP requests. It is built on top of [mitmproxy].'
arch=('any')
url='https://pypi.org/project/http-tools/#files'
license=('GPL3')
makedepends=('python2-setuptools' 'python-setuptools')
source=("https://files.pythonhosted.org/packages/06/79/3ce9a6b00c1dfd34154f131605493233b41629c9d817825e36405b495076/$_pkgname-$pkgver.tar.gz")
sha512sums=('b4e49dbdae1610457f306ff246772eba0dbb4f29b03dbf6f1d0083f73cf309f7bd75124b6fabe78fc10bc050ed83be3058c39515041e0c2d2dacd8c3f29db38c')

prepare() {
  cp -a "$_pkgname-$pkgver"{,-2}
}

build() {
  cd "$_pkgname-$pkgver-2"

  python2 setup.py build

  cd "$srcdir/$_pkgname-$pkgver"

  python setup.py build
}

package_python2-http-tools() {
  depends=('python2')

  cd "$_pkgname-$pkgver-2"

  python2 setup.py install --prefix=/usr --root="$pkgdir" -O1 --skip-build

  mv "$pkgdir/usr/bin/httptools" "$pkgdir/usr/bin/httptools2"
}

package_python-http-tools() {
  depends=('python')

  cd "$_pkgname-$pkgver"

  python setup.py install --prefix=/usr --root="$pkgdir" -O1 --skip-build
}

