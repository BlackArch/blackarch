# This file is part of BlackArch Linux ( https://www.blackarch.org/ ).
# See COPYING for license details.

pkgname=python-http-tools
_pkgname=http-tools
pkgver=6.0.0
pkgrel=2
pkgdesc='httptools helps you to capture, repeat and live intercept HTTP requests. It is built on top of [mitmproxy].'
arch=('any')
url='https://pypi.org/project/http-tools/#files'
license=('GPL3')
depends=('python' 'mitmproxy')
makedepends=('python-setuptools')
source=("https://files.pythonhosted.org/packages/source/${_pkgname::1}/$_pkgname/$_pkgname-$pkgver.tar.gz")
sha512sums=('cbad0e6ef41d23b1aef023b0022b0bb4262c63ba1f7f4fb90f8d2e1cdafde7c3038817da288f66c2a9e4e79f00da67f6912f1ec62e51bcbac8fe2a92a759f6b9')

prepare() {
  cd "$_pkgname-$pkgver"

  sed -i 's/>=6.0.0<=6.0.2//' setup.py
}

build() {
  cd "$_pkgname-$pkgver"

  python setup.py build
}

package() {
  cd "$_pkgname-$pkgver"

  python setup.py install --prefix=/usr --root="$pkgdir" -O1 --skip-build
}

