# This file is part of BlackArch Linux ( https://www.blackarch.org/ ).
# See COPYING for license details.

pkgbase=python-http-tools
pkgname=('python2-http-tools' 'python-http-tools')
_pkgname=http-tools
pkgver=1.1.1
pkgrel=2
pkgdesc='httptools helps you to capture, repeat and live intercept HTTP requests. It is built on top of [mitmproxy].'
arch=('any')
url='https://pypi.org/project/http-tools/#files'
license=('GPL3')
makedepends=('python2-setuptools' 'python-setuptools')
source=("https://files.pythonhosted.org/packages/e0/10/a005498b0113bc7071d0b9691c7bdfec74595e9b4e5358a3466977f04851/$_pkgname-$pkgver.tar.gz")
sha512sums=('fbd4fcc92e941e157d794484e982e42ec9481360490d00b4306cab6699dc1ab8ab84b7785e9b3aea0d1b77e6efce50d97ae038b4da5f064dded37ffc601f7426')

prepare() {
  cp -a "$_pkgname-$pkgver"{,-2}
}

build() {
  cd "$_pkgname-$pkgver-2"

  python2 setup.py build

  cd "$srcdir/$_pkgname-$pkgver"

  python setup.py build
}

package_python2-http-tools() {
  depends=('python2')

  cd "$_pkgname-$pkgver-2"

  python2 setup.py install --prefix=/usr --root="$pkgdir" -O1 --skip-build

  mv "$pkgdir/usr/bin/httptools" "$pkgdir/usr/bin/httptools2"
}

package_python-http-tools() {
  depends=('python')

  cd "$_pkgname-$pkgver"

  python setup.py install --prefix=/usr --root="$pkgdir" -O1 --skip-build
}

