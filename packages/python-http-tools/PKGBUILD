# This file is part of BlackArch Linux ( https://www.blackarch.org/ ).
# See COPYING for license details.

pkgname=python-http-tools
_pkgname=http-tools
pkgver=2.1.1
pkgrel=1
pkgdesc='httptools helps you to capture, repeat and live intercept HTTP requests. It is built on top of [mitmproxy].'
arch=('any')
url='https://pypi.org/project/http-tools/#files'
license=('GPL3')
depends=('python' 'mitmproxy')
makedepends=('python-setuptools')
source=("https://files.pythonhosted.org/packages/source/${_pkgname::1}/$_pkgname/$_pkgname-$pkgver.tar.gz")
sha512sums=('e8672646a3e1e6d58e4e99cbe4492728bdfed6377467ad256d636923d6e0b859e4548916960e8f340f85651332d8ae551ab1fb3ae5c47f568bc177112cb6aebf')

prepare() {
  cd "$_pkgname-$pkgver"

  sed -i 's/>=6.0.0<=6.0.2//' setup.py
}

build() {
  cd "$_pkgname-$pkgver"

  python setup.py build
}

package() {
  cd "$_pkgname-$pkgver"

  python setup.py install --prefix=/usr --root="$pkgdir" -O1 --skip-build
}

