# This file is part of BlackArch Linux ( https://www.blackarch.org/ ).
# See COPYING for license details.

pkgname=python-http-tools
_pkgname=http-tools
pkgver=2.1.0
pkgrel=1
pkgdesc='httptools helps you to capture, repeat and live intercept HTTP requests. It is built on top of [mitmproxy].'
arch=('any')
url='https://pypi.org/project/http-tools/#files'
license=('GPL3')
depends=('python' 'mitmproxy')
makedepends=('python-setuptools')
source=("https://files.pythonhosted.org/packages/8a/e6/5221300a2996adf4de3bc853eecd9eda10b98380a746c3d0631f74f53fd9/$_pkgname-$pkgver.tar.gz")
sha512sums=('ddfc2456526c5f710a025e05bca471f88adf7aaf0af43445c896bfa0e8b40a2c67f0f10ed100785164b4212bcfecea502c06d60ef841a82a108bfbc7b0fcc37e')

prepare() {
  cd "$_pkgname-$pkgver"

  sed -i 's/>=6.0.0<=6.0.2//' setup.py
}

build() {
  cd "$_pkgname-$pkgver"

  python setup.py build
}

package() {
  cd "$_pkgname-$pkgver"

  python setup.py install --prefix=/usr --root="$pkgdir" -O1 --skip-build
}

