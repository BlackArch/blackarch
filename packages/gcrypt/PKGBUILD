# This file is part of BlackArch Linux ( https://www.blackarch.org/ ).
# See COPYING for license details.

pkgname=gcrypt
_pkgname=GCrypt
pkgver=2
pkgrel=1
epoch=1
pkgdesc='Simple file encryption tool written in C++.'
groups=('blackarch' 'blackarch-crypto')
arch=('x86_64' 'armv6h' 'armv7h' 'aarch64')
url='https://github.com/GasparVardanyan/GCrypt'
license=('custom:unknown')
depends=()
source=("https://github.com/GasparVardanyan/$_pkgname/archive/v$pkgver.tar.gz")
sha512sums=('fa7b6dc7e915b1558a8760d4006d47759f000dcdbe7d20d514c495b5739b92de604621f7e734825bc9d49b27a1b0fd005ebdcb74cb5a0c128ab809d7e51c75ac')

build() {
  cd "$_pkgname-$pkgver"

  g++ -o gcrypt main.cpp gcrypt.c console.h gcrypt.h -Wall -O -pedantic \
    -fstack-protector-all -fPIE -fPIC -D_FORTIFY_SOURCE=2 -Wl,-z,now \
    -Wl,-z,-relro
}

package() {
  cd "$_pkgname-$pkgver"

  install -Dm 755 $pkgname "$pkgdir/usr/bin/$pkgname"
  install -Dm 644 -t "$pkgdir/usr/share/doc/$pkgname/" README.md
}

