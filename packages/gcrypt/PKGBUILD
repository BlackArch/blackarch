# This file is part of BlackArch Linux ( https://www.blackarch.org/ ).
# See COPYING for license details.

pkgname=gcrypt
_pkgname=GCrypt
pkgver=3.1
pkgrel=2
epoch=1
pkgdesc='Simple, secure and performance file encryption tool written in C'
groups=('blackarch' 'blackarch-crypto')
arch=('x86_64' 'armv6h' 'armv7h' 'aarch64')
url='https://github.com/GasparVardanyan/GCrypt'
license=('custom:unknown')
depends=('glibc')
makedepends=('gcc')
source=("https://github.com/GasparVardanyan/$_pkgname/archive/v$pkgver.tar.gz")
sha512sums=('95caf30fa8ee0031d4b94c17e232a4056ab5fbffcd61bc8c2c378e4fda1cc83bef0d1ab90c45d5f7bbc7c0b9f0a341396ecd59765655e30fbb06cf9ed119caa9')

build() {
  cd "$_pkgname-$pkgver"

  gcc -o $pkgname *.c -std=c18 -pedantic -pedantic-errors -Wall -O \
    -fstack-protector-all -fPIE -fPIC \
    -D_FORTIFY_SOURCE=2 -Wl,-z,now -Wl,-z,-relro
}

package() {
  cd "$_pkgname-$pkgver"

  install -Dm 755 $pkgname "$pkgdir/usr/bin/$pkgname"
  install -Dm 644 -t "$pkgdir/usr/share/doc/$pkgname/" README.md
}

