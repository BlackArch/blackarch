# This file is part of BlackArch Linux ( https://www.blackarch.org/ ).
# See COPYING for license details.

pkgname=gcrypt
_pkgname=GCrypt
pkgver=1.2
pkgrel=1
epoch=1
pkgdesc='Simple file encryption tool written in C++.'
groups=('blackarch' 'blackarch-crypto')
arch=('x86_64' 'armv6h' 'armv7h' 'aarch64')
url='https://github.com/GasparVardanyan/GCrypt'
license=('custom:unknown')
depends=()
source=("https://github.com/GasparVardanyan/$_pkgname/archive/v$pkgver.tar.gz")
sha512sums=('89d62d6d413e013ed165943a1039c31ac5119896bb88234a6129d444ab44c3d40d71ed22884880b35ef28c3eff682c922d65a66c6636c9869d7405f51eef3744')

build() {
  cd "$_pkgname-$pkgver"

  g++ -o gcrypt main.cpp console.h gcrypt.h -Wall -O -pedantic \
    -fstack-protector-all -fPIE -fPIC -D_FORTIFY_SOURCE=2 -Wl,-z,now \
    -Wl,-z,-relro
}

package() {
  cd "$_pkgname-$pkgver"

  install -Dm 755 gcrypt "$pkgdir/usr/bin/$pkgname"
  install -Dm 644 -t "$pkgdir/usr/share/doc/$pkgname/" README.md
}

