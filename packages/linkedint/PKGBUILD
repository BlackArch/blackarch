# This file is part of BlackArch Linux ( https://www.blackarch.org/ ).
# See COPYING for license details.

pkgname=linkedint
pkgver=1.1.FIX.r8.g89ab1d3
pkgrel=1
pkgdesc='LinkedIn Recon Tool.'
arch=('any')
groups=('blackarch' 'blackarch-social' 'blackarch-recon')
url='https://github.com/vysecurity/LinkedInt'
license=('MIT')
depends=('python' 'python-beautifulsoup4' 'python-certifi' 'python-chardet'
         'python-idna' 'python-requests' 'python-thready' 'python-urllib3')
makedepends=('git')
source=("$pkgname::git+https://github.com/vysecurity/LinkedInt.git")
sha512sums=('SKIP')

pkgver() {
  cd $pkgname

  ( set -o pipefail
    git describe --long --tags --abbrev=7 2>/dev/null |
      sed 's/\([^-]*-g\)/r\1/;s/-/./g' ||
    printf "%s.%s" "$(git rev-list --count HEAD)" \
      "$(git rev-parse --short=7 HEAD)"
  )
}

package() {
  cd $pkgname

  install -dm 755 "$pkgdir/usr/bin"
  install -dm 755 "$pkgdir/usr/share/$pkgname"

  install -Dm 644 -t "$pkgdir/usr/share/doc/$pkgname/" README.md
  install -Dm 644 LICENSE.md "$pkgdir/usr/share/licneses/$pkgname/LICENSE"

  rm -f README.md LICENSE.md

  cp -a * "$pkgdir/usr/share/$pkgname/"

  cat > "$pkgdir/usr/bin/$pkgname" << EOF
#!/bin/sh
cd /usr/share/$pkgname
exec python LinkedInt.py "\$@"
EOF

  chmod +x "$pkgdir/usr/bin/$pkgname"
}

