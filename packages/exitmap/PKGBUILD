# This file is part of BlackArch Linux ( http://blackarch.org ).
# See COPYING for license details.

pkgname='exitmap'
pkgver=357.0897703
pkgrel=1
groups=('blackarch' 'blackarch-recon')
arch=('any')
pkgdesc='A fast and modular scanner for Tor exit relays.'
url='https://github.com/NullHypothesis/exitmap'
license=('GPL3')
depends=('python2' 'python2-dnspython' 'python2-stem' 'tor')
makedepends=('git')
source=('git+https://github.com/NullHypothesis/exitmap.git')
sha1sums=('SKIP')

pkgver() {
  cd "$srcdir/exitmap"

  echo $(git rev-list --count HEAD).$(git rev-parse --short HEAD)
}

package() {
  cd "$srcdir/exitmap"

  mkdir -p "$pkgdir/usr/bin"
  mkdir -p "$pkgdir/usr/share/exitmap"

  install -Dm644 -t "$pkgdir/usr/share/doc/exitmap" README.md doc/*
  install -Dm644 COPYING "$pkgdir/usr/share/licenses/exitmap/COPYING"

  rm -rf README.md doc

  cp -a --no-preserve=ownership * "$pkgdir/usr/share/exitmap"

  cat > "$pkgdir/usr/bin/exitmap" << EOF
#!/bin/sh
cd /usr/share/exitmap/bin
exec python2 exitmap "\${@}"
EOF

  chmod a+x "$pkgdir/usr/bin/exitmap"
}
