# This file is part of BlackArch Linux ( https://www.blackarch.org/ ).
# See COPYING for license details.

pkgname=python-ubi-reader
_pkgname=ubi_reader
pkgver=v0.8.9.r2.gcc7225e
pkgrel=1
pkgdesc='Collection of Python scripts for reading information about and extracting data from UBI and UBIFS images.'
arch=('any')
url='https://github.com/onekey-sec/ubi_reader'
license=('GPL-3.0-or-later')
depends=('python' 'python-lzallright')
makedepends=('git' 'python-build' 'python-pip')
options=(!emptydirs)
source=("git+https://github.com/onekey-sec/$_pkgname.git")
sha512sums=('SKIP')

pkgver() {
  cd $_pkgname

  ( set -o pipefail
    git describe --long --tags --abbrev=7 2>/dev/null |
      sed 's/\([^-]*-g\)/r\1/;s/-/./g' ||
    printf "%s.%s" "$(git rev-list --count HEAD)" \
      "$(git rev-parse --short=7 HEAD)"
  )
}

build() {
  cd $_pkgname

  python -m build --wheel --outdir="$startdir/dist"
}

package() {
  cd $_pkgname

  pip install \
    --verbose \
    --disable-pip-version-check \
    --no-warn-script-location \
    --ignore-installed \
    --no-compile \
    --no-deps \
    --root="$pkgdir" \
    --prefix=/usr \
    --no-index \
    --find-links="file://$startdir/dist" \
    $_pkgname
}

