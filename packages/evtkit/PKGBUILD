# This file is part of BlackArch Linux ( https://www.blackarch.org/ ).
# See COPYING for license details.

pkgname=evtkit
pkgver=8.af06db3
pkgrel=2
pkgdesc='Fix acquired .evt - Windows Event Log files (Forensics).'
groups=('blackarch' 'blackarch-forensic' 'blackarch-windows')
arch=('any')
url='https://github.com/yarox24/evtkit'
license=('MIT')
depends=('python2')
makedepends=('git')
source=('git+https://github.com/yarox24/evtkit')
sha512sums=('SKIP')

pkgver() {
  cd $pkgname

  echo $(git rev-list --count HEAD).$(git rev-parse --short HEAD)
}

prepare() {
  cd $pkgname

  sed -i 's|bin/env python|bin/env python2|' evtkit.py
}

package() {
  cd $pkgname

  install -Dm 755 evtkit.py "$pkgdir/usr/bin/$pkgname"
  install -Dm 644 -t "$pkgdir/usr/share/doc/$pkgname/" README.md
  install -Dm 644 LICENSE "$pkgdir/usr/share/licenses/$pkgname/LICENSE"
}

