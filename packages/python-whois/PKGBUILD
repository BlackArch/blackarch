# This file is part of BlackArch Linux ( https://www.blackarch.org/ ).
# See COPYING for license details.

pkgbase=python-whois
pkgname=('python2-whois' 'python-whois')
_pkgname=whois
pkgver=0.9.3
pkgrel=1
pkgdesc='Python module/library for retrieving WHOIS information of domains.'
url='https://pypi.org/project/whois/#files'
arch=('any')
license=('MIT')
makedepends=('python2-setuptools' 'python-setuptools')
source=("https://files.pythonhosted.org/packages/e9/41/777819cb1025634b390fe1cfebb7a91e22f32abddabebee248bdda0270eb/$_pkgname-$pkgver.tar.gz")
sha512sums=('574f0aebc615921ed0a27de775815d29bc467095a6f47c6fd5f2530bea516f56323de2e17bdc9262985c547b0e0b9f1bd84d3700f8bd02e3c5155d094af72119')

prepare() {
  cp -a "$_pkgname-$pkgver"{,-2}
}

build() {
  cd "$_pkgname-$pkgver-2"

  python2 setup.py build

  cd "$srcdir/$_pkgname-$pkgver"

  python setup.py build
}

package_python2-whois() {
  depends=('python2')

  cd "$_pkgname-$pkgver-2"

  python2 setup.py install --prefix=/usr --root="$pkgdir" --optimize=1 \
    --skip-build
}

package_python-whois() {
  depends=('python')

  cd "$_pkgname-$pkgver"

  python setup.py install --prefix=/usr --root="$pkgdir" --optimize=1 \
    --skip-build
}

