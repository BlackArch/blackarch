# This file is part of BlackArch Linux ( https://www.blackarch.org/ ).
# See COPYING for license details.

pkgbase=python-whois
pkgname=('python2-whois' 'python-whois')
_pkgname=whois
pkgver=0.9.15
pkgrel=1
pkgdesc='Python module/library for retrieving WHOIS information of domains.'
url='https://pypi.org/project/whois/#files'
arch=('any')
license=('MIT')
makedepends=('python2-setuptools' 'python-setuptools')
source=("https://files.pythonhosted.org/packages/48/b5/130f79ccd552b5246eb079a769892ef4e3a20f2164d9157eccc2ea1376eb/$_pkgname-$pkgver.tar.gz")
sha512sums=('9bf66ac37bc7ba3d908f10f4f49d54172956694494a0e06dedcdd7335e9e4da4dd9974c9589f8c363743432dfe62f25a8d9328535bbdcfd39bd81dd193b64bdb')

prepare() {
  cp -a "$_pkgname-$pkgver"{,-2}
}

build() {
  cd "$_pkgname-$pkgver-2"

  python2 setup.py build

  cd "$srcdir/$_pkgname-$pkgver"

  python setup.py build
}

package_python2-whois() {
  depends=('python2')

  cd "$_pkgname-$pkgver-2"

  python2 setup.py install --root="$pkgdir" --prefix=/usr -O1 --skip-build
}

package_python-whois() {
  depends=('python')

  cd "$_pkgname-$pkgver"

  python setup.py install --root="$pkgdir" --prefix=/usr -O1 --skip-build
}

