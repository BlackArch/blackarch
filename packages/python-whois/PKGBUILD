# This file is part of BlackArch Linux ( https://www.blackarch.org/ ).
# See COPYING for license details.

pkgbase=python-whois
pkgname=('python2-whois' 'python-whois')
_pkgname=whois
pkgver=0.9.6
pkgrel=1
pkgdesc='Python module/library for retrieving WHOIS information of domains.'
url='https://pypi.org/project/whois/#files'
arch=('any')
license=('MIT')
makedepends=('python2-setuptools' 'python-setuptools')
source=("https://files.pythonhosted.org/packages/c3/7c/c67420840b1873ddae02e42e8be34a1a8d6a0973a59f65d4f46fb8aa5b64/$_pkgname-$pkgver.tar.gz")
sha512sums=('fc27afb120c3ec04697c7f210fabc9eb6d6f71402fe7fb98d2ade82afef95d80c2235437144b7e5967325c2229dd77991a9ea18c72b8f1765da88874996f6d25')

prepare() {
  cp -a "$_pkgname-$pkgver"{,-2}
}

build() {
  cd "$_pkgname-$pkgver-2"

  python2 setup.py build

  cd "$srcdir/$_pkgname-$pkgver"

  python setup.py build
}

package_python2-whois() {
  depends=('python2')

  cd "$_pkgname-$pkgver-2"

  python2 setup.py install --root="$pkgdir" --prefix=/usr -O1 --skip-build
}

package_python-whois() {
  depends=('python')

  cd "$_pkgname-$pkgver"

  python setup.py install --root="$pkgdir" --prefix=/usr -O1 --skip-build
}

