# This file is part of BlackArch Linux ( https://www.blackarch.org/ ).
# See COPYING for license details.

pkgbase=python-whois
pkgname=('python2-whois' 'python-whois')
_pkgname=whois
pkgver=0.9.8
pkgrel=1
pkgdesc='Python module/library for retrieving WHOIS information of domains.'
url='https://pypi.org/project/whois/#files'
arch=('any')
license=('MIT')
makedepends=('python2-setuptools' 'python-setuptools')
source=("https://files.pythonhosted.org/packages/4c/71/27fe2e3125acc26637d1f8063fd33e678f7dac10727afea118f52c744826/$_pkgname-$pkgver.tar.gz")
sha512sums=('554836ce0908c9bf5a5c21976bd7494c51df2915cfbe1c3b26ecf673c47fc82e9eaa9b230868ff729b4c9cef8e17477d9e7331db1a8631cf431c792cf3c3772c')

prepare() {
  cp -a "$_pkgname-$pkgver"{,-2}
}

build() {
  cd "$_pkgname-$pkgver-2"

  python2 setup.py build

  cd "$srcdir/$_pkgname-$pkgver"

  python setup.py build
}

package_python2-whois() {
  depends=('python2')

  cd "$_pkgname-$pkgver-2"

  python2 setup.py install --root="$pkgdir" --prefix=/usr -O1 --skip-build
}

package_python-whois() {
  depends=('python')

  cd "$_pkgname-$pkgver"

  python setup.py install --root="$pkgdir" --prefix=/usr -O1 --skip-build
}

