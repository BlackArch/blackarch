# This file is part of BlackArch Linux ( http://blackarch.org ).
# See COPYING for license details.

pkgname=howmanypeoplearearound
pkgver=115.e074d40
pkgrel=2
pkgdesc='Count the number of people around you by monitoring wifi signals.'
groups=('blackarch' 'blackarch-recon' 'blackarch-wireless')
arch=('any')
url='https://github.com/schollz/howmanypeoplearearound'
license=('MIT')
depends=('python' 'python-pick' 'python-netifaces' 'python-click'
         'wireshark-cli')
makedepends=('git' 'python-setuptools')
source=('git+https://github.com/schollz/howmanypeoplearearound.git')
md5sums=('SKIP')

pkgver() {
  cd "${srcdir}/howmanypeoplearearound"

  echo $(git rev-list --count HEAD).$(git rev-parse --short HEAD)
}

build() {
  cd "${srcdir}/howmanypeoplearearound"

  python setup.py build
}

package() {
  cd "${srcdir}/howmanypeoplearearound"

  install -dm 755 "${pkgdir}/usr/share/doc/howmanypeoplearearound"
  install -dm 755 "${pkgdir}/usr/share/licenses/howmanypeoplearearound"

  install -Dm644 -t "${pkgdir}/usr/share/doc/howmanypeoplearearound/" \
    README.md
  install -Dm644 -t "${pkgdir}/usr/share/licenses/howmanypeoplearearound/" \
    LICENSE

  python setup.py install --prefix=/usr --root="${pkgdir}"
}
