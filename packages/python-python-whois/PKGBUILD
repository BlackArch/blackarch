# This file is part of BlackArch Linux ( https://www.blackarch.org/ ).
# See COPYING for license details.

pkgbase=python-python-whois
pkgname=('python2-python-whois' 'python-python-whois')
_pkgname=python-whois
pkgver=0.7.2
pkgrel=3
pkgdesc='Whois querying and parsing of domain registration information.'
arch=('any')
url='https://pypi.org/project/python-whois/#files'
license=('WTFPL')
makedepends=('python2-setuptools' 'python-setuptools')
source=("https://files.pythonhosted.org/packages/f0/ab/11c2d01db2554bbaabb2c32b06b6a73f7277372533484c320c78a304dfd7/$_pkgname-$pkgver.tar.gz")
sha512sums=('9f0b0ceedac13b8c4f51389063096819b6aef2b3454c297ca7497dbe78800447e39f61e42fefec84994070ef0d10d75a4342d99a9dc5d76b711b21da0e76f1b6')

prepare() {
  cp -a "$_pkgname-$pkgver"{,-2}
}

build() {
  cd "$_pkgname-$pkgver-2"

  python2 setup.py build

  cd "$srcdir/$_pkgname-$pkgver"

  python setup.py build
}

package_python2-python-whois() {
  depends=('python2' 'python2-future')

  cd "$_pkgname-$pkgver-2"

  python2 setup.py install --root="$pkgdir" --prefix=/usr -O1 --skip-build
}

package_python-python-whois() {
  depends=('python' 'python-future')

  cd "$_pkgname-$pkgver"

  python setup.py install --root="$pkgdir" --prefix=/usr -O1 --skip-build
}

