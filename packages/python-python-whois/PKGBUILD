# This file is part of BlackArch Linux ( https://www.blackarch.org/ ).
# See COPYING for license details.

pkgbase=python-python-whois
pkgname=('python2-python-whois' 'python-python-whois')
_pkgname=python-whois
pkgver=0.9.6
pkgrel=2
pkgdesc='Whois querying and parsing of domain registration information.'
arch=('any')
url='https://pypi.org/project/python-whois/#files'
license=('WTFPL')
makedepends=('python2-setuptools' 'python-setuptools')
source=("https://files.pythonhosted.org/packages/source/${_pkgname::1}/$_pkgname/$_pkgname-$pkgver.tar.gz")
sha512sums=('1b4bfe40bc29f8903ea823b6b095787bce89f82f2cc14732c287a61b43756bb91a6a8a193beb73076d911d753214ec8d8ef96c9d3a51e275db79c0adb8813f31')

prepare() {
  cp -a "$_pkgname-$pkgver"{,-2}
}

build() {
  cd "$_pkgname-$pkgver-2"

  python2 setup.py build

  cd "$srcdir/$_pkgname-$pkgver"

  python setup.py build
}

package_python2-python-whois() {
  depends=('python2' 'python2-future')

  cd "$_pkgname-$pkgver-2"

  python2 setup.py install --root="$pkgdir" --prefix=/usr -O1 --skip-build
}

package_python-python-whois() {
  depends=('python' 'python-future')

  cd "$_pkgname-$pkgver"

  python setup.py install --root="$pkgdir" --prefix=/usr -O1 --skip-build
}

