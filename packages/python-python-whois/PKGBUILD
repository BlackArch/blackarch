# This file is part of BlackArch Linux ( https://www.blackarch.org/ ).
# See COPYING for license details.

pkgbase=python-python-whois
pkgname=('python2-python-whois' 'python-python-whois')
_pkgname=python-whois
pkgver=0.7.1
pkgrel=2
pkgdesc='Whois querying and parsing of domain registration information.'
arch=('any')
url='https://pypi.org/project/python-whois/#files'
license=('WTFPL')
makedepends=('python2-setuptools' 'python-setuptools')
source=("https://files.pythonhosted.org/packages/3e/32/3a7fa80b485f4a1c41492c062396441f2abf28865bb65d069c228af165f7/$_pkgname-$pkgver.tar.gz")
sha512sums=('b5954b92c72fa03a8dab01cddae85a7f964cca3f72820788140e75c8702ead7884ebefdaf95c50efd2992efc7966c287a5ea66220b44348827d35d62bd9b4494')

prepare() {
  cp -a "$_pkgname-$pkgver" "$_pkgname-$pkgver-2"
}

build() {
  cd "$_pkgname-$pkgver-2"

  python2 setup.py build

  cd "$srcdir/$_pkgname-$pkgver"

  python setup.py build
}

package_python2-python-whois() {
  depends=('python2' 'python2-future')

  cd "$_pkgname-$pkgver-2"

  python2 setup.py install --prefix=/usr --root="$pkgdir" --optimize=1 \
    --skip-build
}

package_python-python-whois() {
  depends=('python' 'python-future')

  cd "$_pkgname-$pkgver"

  python setup.py install --prefix=/usr --root="$pkgdir" --optimize=1 \
    --skip-build
}

