# This file is part of BlackArch Linux ( https://www.blackarch.org/ ).
# See COPYING for license details.

pkgbase=python-python-whois
pkgname=('python2-python-whois' 'python-python-whois')
_pkgname=python-whois
pkgver=0.7.3
pkgrel=2
pkgdesc='Whois querying and parsing of domain registration information.'
arch=('any')
url='https://pypi.org/project/python-whois/#files'
license=('WTFPL')
makedepends=('python2-setuptools' 'python-setuptools')
source=("https://files.pythonhosted.org/packages/4b/c6/85268f0cef6a1c7e466140437c8fe49eb12c203d9a31be6d2976a29c7266/$_pkgname-$pkgver.tar.gz")
sha512sums=('59ef578d7611f8925449dd837c8f77982eeffb2e5454ed2f893596dfc73dd984d52d1e5e557d3424bca136b40ff14e720e55feb424692c448e94fee6cd65cdc2')

prepare() {
  cp -a "$_pkgname-$pkgver"{,-2}
}

build() {
  cd "$_pkgname-$pkgver-2"

  python2 setup.py build

  cd "$srcdir/$_pkgname-$pkgver"

  python setup.py build
}

package_python2-python-whois() {
  depends=('python2' 'python2-future')

  cd "$_pkgname-$pkgver-2"

  python2 setup.py install --root="$pkgdir" --prefix=/usr -O1 --skip-build
}

package_python-python-whois() {
  depends=('python' 'python-future')

  cd "$_pkgname-$pkgver"

  python setup.py install --root="$pkgdir" --prefix=/usr -O1 --skip-build
}

