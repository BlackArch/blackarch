# This file is part of BlackArch Linux ( https://www.blackarch.org/ ).
# See COPYING for license details.

pkgbase=python-secure
pkgname=('python2-secure' 'python-secure')
_pkgname=secure
pkgver=0.2.1
pkgrel=2
pkgdesc='A lightweight package that adds optional security headers and cookie attributes for Python web frameworks.'
arch=('any')
url='https://pypi.org/project/secure/#files'
license=('MIT')
makedepends=('python2-setuptools' 'python-setuptools')
source=("https://files.pythonhosted.org/packages/8c/ce/d809b79258afb4845c2935c3ccbfe58ec9c583718bcd10e9c549d71679e9/$_pkgname-$pkgver.tar.gz")
sha512sums=('17a2480fccb6cce73e95ab31bbf42fd9a75a74916f8806572263374dd7631d13088f72b0db98f7c3fbe6a999c4a004173dd0c89c3f9bc366f5c4658bb2c66bfd')

prepare() {
  cp -a "$_pkgname-$pkgver"{,-2}
}

build() {
  cd "$_pkgname-$pkgver-2"

  python2 setup.py build

  cd "$srcdir/$_pkgname-$pkgver"

  python setup.py build
}

package_python2-secure() {
  depends=('python2')

  cd "$_pkgname-$pkgver-2"

  python2 setup.py install --prefix=/usr --root="$pkgdir" -O1 --skip-build
}

package_python-secure() {
  depends=('python')

  cd "$_pkgname-$pkgver"

  python setup.py install --prefix=/usr --root="$pkgdir" -O1 --skip-build
}

