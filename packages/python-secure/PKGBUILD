# This file is part of BlackArch Linux ( https://www.blackarch.org/ ).
# See COPYING for license details.

pkgbase=python-secure
pkgname=('python2-secure' 'python-secure')
_pkgname=secure
pkgver=0.3.0
pkgrel=1
pkgdesc='A lightweight package that adds optional security headers and cookie attributes for Python web frameworks.'
arch=('any')
url='https://pypi.org/project/secure/#files'
license=('MIT')
makedepends=('python2-setuptools' 'python-setuptools')
source=("https://files.pythonhosted.org/packages/16/09/a88439cfd51d76b5da2aff258af3af576f8902f1ca10c11a7b83b1534703/$_pkgname-$pkgver.tar.gz")
sha512sums=('633ebdd9bf92ff8a884a60b5c976c7fcfedae1ae1e62171dbee842cadd251a6cc39b7fa9925a750e1c9ead3a5c9027bd204852a03d41e92ae279dd8975bd6ca8')

prepare() {
  cp -a "$_pkgname-$pkgver"{,-2}
}

build() {
  cd "$_pkgname-$pkgver-2"

  python2 setup.py build

  cd "$srcdir/$_pkgname-$pkgver"

  python setup.py build
}

package_python2-secure() {
  depends=('python2')

  cd "$_pkgname-$pkgver-2"

  python2 setup.py install --prefix=/usr --root="$pkgdir" -O1 --skip-build
}

package_python-secure() {
  depends=('python')

  cd "$_pkgname-$pkgver"

  python setup.py install --prefix=/usr --root="$pkgdir" -O1 --skip-build
}

