# This file is part of BlackArch Linux ( https://www.blackarch.org/ ).
# See COPYING for license details.

pkgbase=python-greenery
pkgname=('python2-greenery' 'python-greenery')
_pkgname=greenery
pkgver=3.3.7
pkgrel=1
pkgdesc='Greenery allows manipulation of regular expressions as finite state machines.'
arch=('any')
url='https://pypi.org/project/greenery/#files'
license=('APACHE')
makedepends=('python2-setuptools' 'python-setuptools')
source=("https://files.pythonhosted.org/packages/72/9d/1b989222bdd19dbaedf6b25710e953d298156f0667bbcdce420405e82567/$_pkgname-$pkgver.tar.gz")
sha512sums=('ce3bcda2c6eabd04840e21c8cfaea3364a2b65425ead3242e8efe341007abcd0562638e92140c2271ae9017b05282748f7a85342525f95cef914d1815e8ca758')

prepare() {
  cp -a "$_pkgname-$pkgver"{,-2}
}

build() {
  cd "$_pkgname-$pkgver-2"

  python2 setup.py build

  cd "$srcdir/$_pkgname-$pkgver"

  python setup.py build
}

package_python2-greenery() {
  depends=('python2')

  cd "$_pkgname-$pkgver-2"

  python2 setup.py install --prefix=/usr --root="$pkgdir" -O1 --skip-build
}

package_python-greenery() {
  depends=('python')

  cd "$_pkgname-$pkgver"

  python setup.py install --prefix=/usr --root="$pkgdir" -O1 --skip-build
}

