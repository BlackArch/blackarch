# This file is part of BlackArch Linux ( https://www.blackarch.org/ ).
# See COPYING for license details.

pkgbase=python-greenery
pkgname=('python2-greenery' 'python-greenery')
_pkgname=greenery
pkgver=3.3.5
pkgrel=1
pkgdesc='Greenery allows manipulation of regular expressions as finite state machines.'
arch=('any')
url='https://pypi.org/project/greenery/#files'
license=('APACHE')
makedepends=('python2-setuptools' 'python-setuptools')
source=("https://files.pythonhosted.org/packages/ad/5f/b2afd8c34849f2643c5ddcb8ef8d1461f34d9c6cefc640dba0de6a0ed7a1/$_pkgname-$pkgver.tar.gz")
sha512sums=('523fb90dc2edeff489f185cf3d782a819a125936e40e5e189858a19e247ab88f8f50e21309e2917f4b877c1a0134bd4401b7d3f7bff2e912f5fc565a949a3f1e')

prepare() {
  cp -a "$_pkgname-$pkgver"{,-2}
}

build() {
  cd "$_pkgname-$pkgver-2"

  python2 setup.py build

  cd "$srcdir/$_pkgname-$pkgver"

  python setup.py build
}

package_python2-greenery() {
  depends=('python2')

  cd "$_pkgname-$pkgver-2"

  python2 setup.py install --prefix=/usr --root="$pkgdir" -O1 --skip-build
}

package_python-greenery() {
  depends=('python')

  cd "$_pkgname-$pkgver"

  python setup.py install --prefix=/usr --root="$pkgdir" -O1 --skip-build
}

