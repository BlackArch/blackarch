# This file is part of BlackArch Linux ( https://www.blackarch.org/ ).
# See COPYING for license details.

pkgbase=python-greenery
pkgname=('python2-greenery' 'python-greenery')
_pkgname=greenery
pkgver=3.3.6
pkgrel=1
pkgdesc='Greenery allows manipulation of regular expressions as finite state machines.'
arch=('any')
url='https://pypi.org/project/greenery/#files'
license=('APACHE')
makedepends=('python2-setuptools' 'python-setuptools')
source=("https://files.pythonhosted.org/packages/a5/63/739a6dee553f111b9f665766669d5458489a6b474574e2faab961b046c21/$_pkgname-$pkgver.tar.gz")
sha512sums=('bf334e19e8cf67b3f4ab3c812508d01a23a7fd5bdfb0e802a267253340a5988132fdbdd1c32162c7c180ff714ecd079c127078826deb0af809297714583bc8e7')

prepare() {
  cp -a "$_pkgname-$pkgver"{,-2}
}

build() {
  cd "$_pkgname-$pkgver-2"

  python2 setup.py build

  cd "$srcdir/$_pkgname-$pkgver"

  python setup.py build
}

package_python2-greenery() {
  depends=('python2')

  cd "$_pkgname-$pkgver-2"

  python2 setup.py install --prefix=/usr --root="$pkgdir" -O1 --skip-build
}

package_python-greenery() {
  depends=('python')

  cd "$_pkgname-$pkgver"

  python setup.py install --prefix=/usr --root="$pkgdir" -O1 --skip-build
}

