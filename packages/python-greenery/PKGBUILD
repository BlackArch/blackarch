# This file is part of BlackArch Linux ( https://www.blackarch.org/ ).
# See COPYING for license details.

pkgbase=python-greenery
pkgname=('python2-greenery' 'python-greenery')
_pkgname=greenery
pkgver=4.0.0
pkgrel=1
pkgdesc='Greenery allows manipulation of regular expressions as finite state machines.'
arch=('any')
url='https://pypi.org/project/greenery/#files'
license=('APACHE')
makedepends=('python2-setuptools' 'python-setuptools')
source=("https://files.pythonhosted.org/packages/source/${_pkgname::1}/$_pkgname/$_pkgname-$pkgver.tar.gz")
sha512sums=('24624e87e1ac3b75dab0a27031decf0fdafc7aa5d8336d737c0e2457ec78c1d4e286d41b1df714b2230e76af599c34e6447be305af5eba585a2ffe1f098d84fe')

prepare() {
  cp -a "$_pkgname-$pkgver"{,-2}
}

build() {
  cd "$_pkgname-$pkgver-2"

  python2 setup.py build

  cd "$srcdir/$_pkgname-$pkgver"

  python setup.py build
}

package_python2-greenery() {
  depends=('python2')

  cd "$_pkgname-$pkgver-2"

  python2 setup.py install --prefix=/usr --root="$pkgdir" -O1 --skip-build
}

package_python-greenery() {
  depends=('python')

  cd "$_pkgname-$pkgver"

  python setup.py install --prefix=/usr --root="$pkgdir" -O1 --skip-build
}

