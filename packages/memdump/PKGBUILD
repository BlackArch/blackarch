# This file is part of BlackArch Linux ( http://blackarch.org ).
# See COPYING for license details.

pkgname='memdump'
pkgver='1.01'
pkgrel=4
groups=('blackarch' 'blackarch-forensic')
pkgdesc='Dumps system memory to stdout, skipping over holes in memory maps.'
arch=('i686' 'x86_64' 'armv6h' 'armv7h')
url='http://www.porcupine.org/forensics/tct.html'
license=('custom:IPL')
options=('!makeflags' '!strip')
source=("http://www.porcupine.org/forensics/memdump-$pkgver.tar.gz"
        'makedefs.patch')
sha1sums=('bc5f0a132f41a3606697e874e942e6588398a3a8'
          '4c7be2940296bab4404a429c311e2fdaa77e6f20')

prepare() {
  cd "$srcdir/memdump-$pkgver"

  patch -p0 < "../makedefs.patch"
}

build() {
  cd "$srcdir/memdump-$pkgver"

  make
}

package() {
  cd "$srcdir/memdump-$pkgver"

  # Renamed to fix conflict with community/libmemcached
  install -Dm755 memdump "$pkgdir/usr/bin/memdump_kernel"
  install -Dm644 memdump.1 "$pkgdir/usr/share/man/man1/memdump.1"
  install -Dm644 LICENSE "$pkgdir/usr/share/licenses/memdump/LICENSE"
}
