# This file is part of BlackArch Linux ( http://blackarch.org ).
# See COPYING for license details.

pkgname=memdump
pkgver=1.01
pkgrel=6
groups=('blackarch' 'blackarch-forensic')
pkgdesc='Dumps system memory to stdout, skipping over holes in memory maps.'
arch=('x86_64' 'armv6h' 'armv7h' 'aarch64')
url='http://www.porcupine.org/forensics/tct.html'
license=('custom:IPL')
options=('!makeflags' '!strip')
source=("http://www.porcupine.org/forensics/memdump-${pkgver}.tar.gz"
        'makedefs.patch')
sha512sums=('46d013f812b0a5807c7ba38d6c3940e105057ba8e64b4f45b75a0800cab212d164caf881efbc1958d5c5c239236fdcb61f6fe093886ff3e28bc0b70791aaee3e'
            'c0616d1afc9d144d66ec8c9dd01776af5c142abdef66763a33e30ce8e77eb84bd3afa5800d4971e7c37c72aea5071ac6e8dd9a2db7a5f8c7397858691c969974')

prepare() {
  cd "$srcdir/memdump-$pkgver"

  patch -p0 < "../makedefs.patch"
}

build() {
  cd "$srcdir/memdump-$pkgver"

  make
}

package() {
  cd "$srcdir/memdump-$pkgver"

  # Renamed to fix conflict with community/libmemcached
  install -Dm755 memdump "$pkgdir/usr/bin/memdump-kernel"
  install -Dm644 memdump.1 "$pkgdir/usr/share/man/man1/memdump.1"
  install -Dm644 LICENSE "$pkgdir/usr/share/licenses/memdump/LICENSE"
}
