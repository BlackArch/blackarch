pkgname=memdump
pkgver=1.01
pkgrel=2
pkgdesc='Dumps system memory to stdout, skipping over holes in memory maps.'
arch=('i686' 'x86_64')
url='http://www.porcupine.org/forensics/tct.html'
license=('custom:IPL')
depends=('glibc')
groups=('blackarch' 'blackarch-analysis' 'blackarch-forensics')
options=('!makeflags' '!strip')
source=("http://www.porcupine.org/forensics/memdump-$pkgver.tar.gz"
        'makedefs.patch')
md5sums=('41076a611098af79f3b893f515ae22da'
         '3b162097de9a8edb1462842a769996a8')

prepare() {
  cd "$srcdir/memdump-$pkgver"

  patch -p0 < "../makedefs.patch"
}

build() {
  cd "$srcdir/memdump-$pkgver"
  
  make
}

package() {
  cd "$srcdir/memdump-$pkgver"
  
  install -Dm755 memdump "$pkgdir/usr/bin/memdump"
  install -Dm644 memdump.1 "$pkgdir/usr/share/man/man1/memdump.1"
  install -Dm644 LICENSE "$pkgdir/usr/share/licenses/memdump/LICENSE"
}
