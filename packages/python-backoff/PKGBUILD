# This file is part of BlackArch Linux ( https://www.blackarch.org/ ).
# See COPYING for license details.

pkgbase=python-backoff
pkgname=('python2-backoff' 'python-backoff')
_pkgname=backoff
pkgver=2.0.1
pkgrel=1
pkgdesc='Function decoration for backoff and retry.'
arch=('any')
url='https://pypi.org/project/backoff/#files'
license=('MIT')
makedepends=('python2-setuptools' 'python-setuptools')
source=("https://files.pythonhosted.org/packages/29/bb/e79eec932f4f56fe63e27d895579c73de8576b148b11cf22a7fb872ebd98/$_pkgname-$pkgver.tar.gz")
sha512sums=('078ec065e8ec5dd12484069736383f94f9577eb8ebbe95cab29719228cc4c4973f72c89b03831912a9973b81a4cd1cc13de68e548b7221fb28182a2bc37ae498')

prepare() {
  cp -a "$_pkgname-$pkgver"{,-2}
}

build() {
  cd "$_pkgname-$pkgver-2"

  python2 setup.py build

  cd "$srcdir/$_pkgname-$pkgver"

  python setup.py build
}

package_python2-backoff() {
  depends=('python2')

  cd "$_pkgname-$pkgver"

  python2 setup.py install --prefix=/usr --root="$pkgdir" -O1 --skip-build
}

package_python-backoff() {
  depends=('python')

  cd "$_pkgname-$pkgver"

  python setup.py install --prefix=/usr --root="$pkgdir" -O1 --skip-build
}

