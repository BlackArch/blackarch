# This file is part of BlackArch Linux ( https://www.blackarch.org/ ).
# See COPYING for license details.

pkgbase=python-sqlalchemy-mixins
pkgname=('python2-sqlalchemy-mixins' 'python-sqlalchemy-mixins')
_pkgname=sqlalchemy_mixins
pkgver=1.1
pkgrel=1
pkgdesc='Active Record, Django-like queries, nested eager load and beauty __repr__ for SQLAlchemy.'
arch=('any')
url='https://pypi.org/project/sqlalchemy-mixins/#files'
license=('MIT')
makedepends=('python2-setuptools' 'python-setuptools')
source=("https://files.pythonhosted.org/packages/db/5d/a70ff31aff91ec8d11538056ddc9a4d5cfb13f8968a685efe3943861b433/$_pkgname-$pkgver.tar.gz")
sha512sums=('a9c84247becd1955378735d2053b92d58fc353fb6bbbfe1d1289df636e8a59a3e60771346c9f6a797c1a48c5a6f8bf72bfe1a714b620e9c0a7c47004117ea38d')

prepare() {
  cp -a "$_pkgname-$pkgver"{,-2}
}

build() {
  cd "$_pkgname-$pkgver-2"

  python2 setup.py build

  cd "$srcdir/$_pkgname-$pkgver"

  python setup.py build
}

package_python2-sqlalchemy-mixins() {
  depends=('python2')

  cd "$_pkgname-$pkgver-2"

  python2 setup.py install --prefix=/usr --root="$pkgdir" -O1 --skip-build
}

package_python-sqlalchemy-mixins() {
  depends=('python')

  cd "$_pkgname-$pkgver"

  python setup.py install --prefix=/usr --root="$pkgdir" -O1 --skip-build
}

