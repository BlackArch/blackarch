# This file is part of BlackArch Linux ( https://www.blackarch.org/ ).
# See COPYING for license details.

pkgbase=python-sqlalchemy-mixins
pkgname=('python2-sqlalchemy-mixins' 'python-sqlalchemy-mixins')
_pkgname=sqlalchemy_mixins
pkgver=1.5
pkgrel=2
pkgdesc='Active Record, Django-like queries, nested eager load and beauty __repr__ for SQLAlchemy.'
arch=('any')
url='https://pypi.org/project/sqlalchemy-mixins/#files'
license=('MIT')
makedepends=('python2-setuptools' 'python-setuptools')
source=("https://files.pythonhosted.org/packages/c5/66/a130b3b69367f6fb6c9c95196bc2a1b49c50ed5a139b57973b7cd086c8f3/sqlalchemy_mixins-$pkgver.tar.gz")
sha512sums=('cda610bfcb51fe2a2c8163753c6270f5fc0c04ad2076efa6734c05ed9883ed38969678e30dcd1d9088a53bceef177c6867d4b7f32f081bb81d82015617e6e14a')

prepare() {
  cp -a "$_pkgname-$pkgver"{,-2}
}

build() {
  cd "$_pkgname-$pkgver-2"

  python2 setup.py build

  cd "$srcdir/$_pkgname-$pkgver"

  python setup.py build
}

package_python2-sqlalchemy-mixins() {
  depends=('python2')

  cd "$_pkgname-$pkgver-2"

  python2 setup.py install --prefix=/usr --root="$pkgdir" -O1 --skip-build
}

package_python-sqlalchemy-mixins() {
  depends=('python')

  cd "$_pkgname-$pkgver"

  python setup.py install --prefix=/usr --root="$pkgdir" -O1 --skip-build
}

