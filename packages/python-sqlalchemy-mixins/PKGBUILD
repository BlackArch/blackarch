# This file is part of BlackArch Linux ( https://www.blackarch.org/ ).
# See COPYING for license details.

pkgbase=python-sqlalchemy-mixins
pkgname=('python2-sqlalchemy-mixins' 'python-sqlalchemy-mixins')
_pkgname=sqlalchemy_mixins
pkgver=1.2.1
pkgrel=2
pkgdesc='Active Record, Django-like queries, nested eager load and beauty __repr__ for SQLAlchemy.'
arch=('any')
url='https://pypi.org/project/sqlalchemy-mixins/#files'
license=('MIT')
makedepends=('python2-setuptools' 'python-setuptools')
source=("https://files.pythonhosted.org/packages/e6/82/9d1a031cac151cc5841e700d210fafee08efd116161083d34091e7faec00/$_pkgname-$pkgver.tar.gz")
sha512sums=('4d35b9053d86e2b901c4a9c68d90fabc731bb09b4f0df9f2b07c4b4bf632ce19cabdfd0c12573d5820f53862525c34d31cb3c743325ffcb4d5223a9e39ffaee4')

prepare() {
  cp -a "$_pkgname-$pkgver"{,-2}
}

build() {
  cd "$_pkgname-$pkgver-2"

  python2 setup.py build

  cd "$srcdir/$_pkgname-$pkgver"

  python setup.py build
}

package_python2-sqlalchemy-mixins() {
  depends=('python2')

  cd "$_pkgname-$pkgver-2"

  python2 setup.py install --prefix=/usr --root="$pkgdir" -O1 --skip-build
}

package_python-sqlalchemy-mixins() {
  depends=('python')

  cd "$_pkgname-$pkgver"

  python setup.py install --prefix=/usr --root="$pkgdir" -O1 --skip-build
}

