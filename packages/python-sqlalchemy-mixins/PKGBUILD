# This file is part of BlackArch Linux ( https://www.blackarch.org/ ).
# See COPYING for license details.

pkgbase=python-sqlalchemy-mixins
pkgname=('python2-sqlalchemy-mixins' 'python-sqlalchemy-mixins')
_pkgname=sqlalchemy_mixins
pkgver=1.4
pkgrel=1
pkgdesc='Active Record, Django-like queries, nested eager load and beauty __repr__ for SQLAlchemy.'
arch=('any')
url='https://pypi.org/project/sqlalchemy-mixins/#files'
license=('MIT')
makedepends=('python2-setuptools' 'python-setuptools')
source=("https://files.pythonhosted.org/packages/41/35/bd81976f420560fceff7f60c051d75dbae5e8335df1d442b6ee2a85ac3d1/sqlalchemy_mixins-$pkgver.tar.gz")
sha512sums=('f7aae0a72fa6b8732108abfd0766d4c005041427e591b7fb1b42297cc5fce278ec74904a4d9b4957f7b7b70ac5e7958e96953c3b254e5438efd58d008e795e03')

prepare() {
  cp -a "$_pkgname-$pkgver"{,-2}
}

build() {
  cd "$_pkgname-$pkgver-2"

  python2 setup.py build

  cd "$srcdir/$_pkgname-$pkgver"

  python setup.py build
}

package_python2-sqlalchemy-mixins() {
  depends=('python2')

  cd "$_pkgname-$pkgver-2"

  python2 setup.py install --prefix=/usr --root="$pkgdir" -O1 --skip-build
}

package_python-sqlalchemy-mixins() {
  depends=('python')

  cd "$_pkgname-$pkgver"

  python setup.py install --prefix=/usr --root="$pkgdir" -O1 --skip-build
}

