# This file is part of BlackArch Linux ( https://www.blackarch.org/ ).
# See COPYING for license details.

pkgbase=python-pysctp
pkgname=('python2-pysctp' 'python-pysctp')
_pkgname=pysctp
pkgver=0.6.1
pkgrel=2
pkgdesc='A python module for the SCTP protocol stack and library.'
arch=('any')
url='https://pypi.org/project/pysctp/#files'
license=('LGPL')
makedepends=('python2-setuptools' 'python-setuptools' 'linux-headers'
             'lksctp-tools')
source=("https://files.pythonhosted.org/packages/b9/53/d254994ce7d145dfc92fa2abda90de3d449cc40c171ac069da82ebec8e5f/$_pkgname-$pkgver.tar.gz")
sha512sums=('ef0f9ba24e835015d7731a024e959afd7e1afca291a24f7abfa42d915a5be28b772526c472bff605d6d200ad762ccdb837663992f71d96097738837dbf59ca0b')

prepare() {
  cp -a "$_pkgname-$pkgver"{,-2}
}

build() {
  cd "$_pkgname-$pkgver-2"

  python2 setup.py build

  cd "$srcdir/$_pkgname-$pkgver"

  python setup.py build
}

package_python2-pysctp() {
  depends=('python2' 'lksctp-tools' 'linux-headers')

  cd "$_pkgname-$pkgver-2"

  python2 setup.py install --prefix=/usr --root="$pkgdir" -O1 --skip-build

  rm -rf "$pkgdir/usr/_sctp.h"
}

package_python-pysctp() {
  depends=('python' 'lksctp-tools' 'linux-headers')

  cd "$_pkgname-$pkgver"

  python setup.py install --prefix=/usr --root="$pkgdir" -O1 --skip-build

  rm -rf "$pkgdir/usr/_sctp.h"
}

