# This file is part of BlackArch Linux ( https://www.blackarch.org/ ).
# See COPYING for license details.

pkgbase=python-pysctp
pkgname=('python2-pysctp' 'python-pysctp')
_pkgname=pysctp
pkgver=0.6
pkgrel=8
pkgdesc='A python module for the SCTP protocol stack and library.'
arch=('any')
url='https://pypi.org/project/pysctp/#files'
license=('LGPL')
makedepends=('python2-setuptools' 'python-setuptools' 'linux-headers'
             'lksctp-tools')
source=("https://files.pythonhosted.org/packages/68/4f/8a11fd73939cb9a453aca3a11e88bc042656dc4b3865661da5121ba4bef0/$_pkgname-$pkgver.tar.gz")
sha512sums=('620cd4ee38b46fc59297ba0a195d38f09abee73da35c1c09a45ef78bd4447dae1c7bddc0888416d3442d54a5575e8961cb10b23f242c3c8f68fe9d80ad131222')

prepare() {
  cp -a "$_pkgname-$pkgver"{,-2}
}

build() {
  cd "$_pkgname-$pkgver-2"

  python2 setup.py build

  cd "$srcdir/$_pkgname-$pkgver"

  python setup.py build
}

package_python2-pysctp() {
  depends=('python2' 'lksctp-tools' 'linux-headers')

  cd "$_pkgname-$pkgver-2"

  python2 setup.py install --prefix=/usr --root="$pkgdir" -O1 --skip-build

  rm -rf "$pkgdir/usr/_sctp.h"
}

package_python-pysctp() {
  depends=('python' 'lksctp-tools' 'linux-headers')

  cd "$_pkgname-$pkgver"

  python setup.py install --prefix=/usr --root="$pkgdir" -O1 --skip-build

  rm -rf "$pkgdir/usr/_sctp.h"
}

