# This file is part of BlackArch Linux ( https://www.blackarch.org/ ).
# See COPYING for license details.

pkgbase=python-sphinx-rtd-theme
pkgname=('python2-sphinx-rtd-theme' 'python-sphinx-rtd-theme')
_pkgname=sphinx_rtd_theme
pkgver=0.4.3
pkgrel=1
pkgdesc='Read the Docs theme for Sphinx.'
arch=('any')
url='https://pypi.org/project/sphinx-rtd-theme/#files'
license=('MIT')
makedepends=('python2-setuptools' 'python-setuptools')
source=("https://files.pythonhosted.org/packages/ed/73/7e550d6e4cf9f78a0e0b60b9d93dba295389c3d271c034bf2ea3463a79f9/$_pkgname-$pkgver.tar.gz")
sha512sums=('44b9309ddd041181542747c70b6e180c07aaf8cd7ee9e4c7eb9e3948685b8bd578e815c022bd991cc7f4bc7d02bb3c7a6d213e5d0c2e8214aa85d50b73e53853')

prepare() {
  cp -a "$_pkgname-$pkgver"{,-2}
}

build() {
  cd "$_pkgname-$pkgver-2"

  python2 setup.py build

  cd "$srcdir/$_pkgname-$pkgver"

  python setup.py build
}

package_python2-sphinx-rtd-theme() {
  depends=('python2' 'python2-sphinx')

  cd "$_pkgname-$pkgver-2"

  python2 setup.py install --prefix=/usr --root="$pkgdir" --optimize=1 \
    --skip-build
}

package_python-sphinx-rtd-theme() {
  depends=('python' 'python-sphinx')

  cd "$_pkgname-$pkgver"

  python setup.py install --prefix=/usr --root="$pkgdir" --optimize=1 \
    --skip-build
}

