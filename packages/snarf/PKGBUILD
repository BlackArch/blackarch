# This file is part of BlackArch Linux ( http://blackarch.org ).
# See COPYING for license details.

pkgname='snarf'
pkgver=40.49cc8cb
pkgrel=1
pkgdesc='SMB Man in the Middle Attack Engine / relay suite.'
arch=('any')
groups=('blackarch' 'blackarch-networking' 'blackarch-proxy'
        'blackarch-exploitation')
url='https://github.com/purpleteam/snarf'
license=('GPL2')
depends=('nodejs')
makedepends=('git')
source=('git+https://github.com/purpleteam/snarf.git')
sha1sums=('SKIP')

pkgver() {
  cd "$srcdir/snarf"

  echo $(git rev-list --count HEAD).$(git rev-parse --short HEAD)
}

package() {
  cd "$srcdir/snarf"

  mkdir -p "$pkgdir/usr/bin"
  mkdir -p "$pkgdir/usr/share/snarf"

  install -Dm644 -t "$pkgdir/usr/share/doc/snarf" docs/* README.md CHANGELOG
  install -Dm644 LICENSE "$pkgdir/usr/share/licenses/snarf/LICENSE"

  rm -rf docs README.md CHANGELOG LICENSE

  cp -a --no-preserve=ownership * "$pkgdir/usr/share/snarf"

  cat > "$pkgdir/usr/bin/snarf" << EOF
#!/bin/sh
cd /usr/share/snarf
exec node ./snarf.js "\$@"
EOF

  chmod +x "$pkgdir/usr/bin/snarf"
}
