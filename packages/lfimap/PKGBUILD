# This file is part of BlackArch Linux ( https://www.blackarch.org/ ).
# See COPYING for license details.

pkgname=lfimap
pkgver=7.99877ec
pkgrel=1
groups=('blackarch' 'blackarch-webapp' 'blackarch-fuzzer')
pkgdesc='This script is used to take the highest beneficts of the local file include vulnerability in a webserver.'
arch=('any')
url='https://github.com/aepereyra/lfimap/'
license=('unknown')
depends=('python2')
makedepends=('git')
source=("git+https://github.com/aepereyra/$pkgname.git")
sha512sums=('SKIP')

pkgver() {
  cd $pkgname

  echo $(git rev-list --count HEAD).$(git rev-parse --short HEAD)
}

package() {
  cd $pkgname

  install -dm 755 "$pkgdir/usr/bin"
  install -dm 755 "$pkgdir/usr/share/$pkgname"

  install -Dm 644 -t "$pkgdir/usr/share/doc/$pkgname/" README

  rm README

  cp -a * "$pkgdir/usr/share/$pkgname/"

  cat > "$pkgdir/usr/bin/$pkgname" << EOF
#!/bin/sh
cd /usr/share/$pkgname
exec python2 $pkgname.py "\$@"
EOF

  chmod +x "$pkgdir/usr/bin/$pkgname"
}

