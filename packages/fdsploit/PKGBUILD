# This file is part of BlackArch Linux ( https://www.blackarch.org/ ).
# See COPYING for license details.

pkgname=fdsploit
pkgver=24.af95d1a
pkgrel=1
pkgdesc='A File Inclusion & Directory Traversal fuzzing, enumeration & exploitation tool.'
groups=('blackarch' 'blackarch-webapp' 'blackarch-fuzzer'
        'blackarch-exploitation')
arch=('any')
url='https://github.com/chrispetrou/FDsploit'
license=('GPL3')
depends=('python2' 'python2-beautifulsoup4' 'python2-requests' 'python2-colorama'
         'python2-html5lib' 'python2-validators' 'python2-fake-useragent')
makedepends=('git')
source=("$pkgname::git+https://github.com/chrispetrou/FDsploit.git")
sha512sums=('SKIP')

pkgver() {
  cd $pkgname

  echo $(git rev-list --count HEAD).$(git rev-parse --short HEAD)
}

package() {
  cd $pkgname

  install -dm 755 "$pkgdir/usr/bin"
  install -dm 755 "$pkgdir/usr/share/$pkgname"

  install -Dm 644 -t "$pkgdir/usr/share/doc/$pkgname/" README.md
  install -Dm 644 LICENSE "$pkgdir/usr/share/licenses/$pkgname/LICENSE"

  rm README.md LICENSE

  cp -a * "$pkgdir/usr/share/$pkgname/"

  cat > "$pkgdir/usr/bin/$pkgname" << EOF
#!/bin/sh
cd /usr/share/$pkgname
exec python2 $pkgname.py "\$@"
EOF

  chmod +x "$pkgdir/usr/bin/$pkgname"
}

