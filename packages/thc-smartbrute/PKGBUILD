# This file is part of BlackArch Linux ( http://blackarch.org ).
# See COPYING for license details.

pkgname='thc-smartbrute'
pkgver='1.0'
pkgrel=3
groups=('blackarch' 'blackarch-cracker' 'blackarch-scanner')
pkgdesc='This tool finds undocumented and secret commands implemented in a smartcard.'
url='https://www.thc.org/thc-smartbrute/'
license=('GPL3')
depends=('pcsclite')
arch=('x86_64' 'armv6h' 'armv7h' 'aarch64')
DLAGENTS=("https::/usr/bin/curl -k -o %o %u")
source=("https://www.thc.org/thc-smartbrute/thcsmartbrute-${pkgver}.tar.gz")
sha1sums=('fd835642320f439a93c847f72ee5c48b59f3f130')

build() {
  cd "$srcdir/thcsmartbrute-$pkgver"

  if [[ "$CARCH" == "i686" || "$CARCH" == "x86_64" ]];
  then
    ./configure --prefix=/usr
  else
    ./configure --build=arm --prefix=/usr
  fi

  make
}

package() {
  cd "$srcdir/thcsmartbrute-$pkgver"

  install -Dm644 COPYING "$pkgdir/usr/share/licenses/thc-smartbrute/COPYING"
  install -Dm644 -t "$pkgdir/usr/share/doc/thc-smartbrute/" AUTHORS README \
    INSTALL ChangeLog NEWS

  make DESTDIR="$pkgdir" install

  mv "$pkgdir/usr/bin/thcsmartbrute" "$pkgdir/usr/bin/thc-smartbrute"
}
