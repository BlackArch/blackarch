# This file is part of BlackArch Linux ( https://www.blackarch.org/ ).
# See COPYING for license details.

pkgname=rpivot
pkgver=5.4963487
pkgrel=1
pkgdesc='Socks4 reverse proxy for penetration testing.'
groups=('blackarch' 'blackarch-proxy')
arch=('any')
url='https://github.com/artkond/rpivot'
license=('custom:unknown')
depends=('python2' 'python2-ntlm-auth')
makedepends=('git')
source=("git+https://github.com/artkond/$pkgname.git")
sha512sums=('SKIP')

pkgver() {
  cd $pkgname

  echo $(git rev-list --count HEAD).$(git rev-parse --short HEAD)
}

package() {
  cd $pkgname

  install -dm 755 "$pkgdir/usr/bin"
  install -dm 755 "$pkgdir/usr/share/$pkgname"

  install -Dm 644 -t "$pkgdir/usr/share/doc/$pkgname/" README.md

  rm README.md

  cp -a * "$pkgdir/usr/share/$pkgname/"

  cat > "$pkgdir/usr/bin/$pkgname-server" << EOF
#!/bin/sh
cd /usr/share/$pkgname
exec python2 server.py "\$@"
EOF

  chmod +x "$pkgdir/usr/bin/$pkgname-server"

  cat > "$pkgdir/usr/bin/$pkgname-client" << EOF
#!/bin/sh
cd /usr/share/$pkgname
exec python2 client.py "\$@"
EOF

  chmod +x "$pkgdir/usr/bin/$pkgname-client"
}

