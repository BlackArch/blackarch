# This file is part of BlackArch Linux ( http://blackarch.org ).
# See COPYING for license details.

pkgname='rpivot'
pkgver=2.c280a92
pkgrel=1
pkgdesc='Socks4 reverse proxy for penetration testing.'
groups=('blackarch' 'blackarch-proxy')
arch=('any')
url='https://github.com/artkond/rpivot'
license=('custom:unknown')
depends=('python2' 'python2-ntlm-auth')
makedepends=('git')
source=('git+https://github.com/artkond/rpivot.git')
sha1sums=('SKIP')

pkgver() {
  cd "$srcdir/rpivot"

  echo $(git rev-list --count HEAD).$(git rev-parse --short HEAD)
}

package() {
  cd "$srcdir/rpivot"

  mkdir -p "$pkgdir/usr/bin"
  mkdir -p "$pkgdir/usr/share/rpivot"

  install -Dm644 -t "$pkgdir/usr/share/doc/rpivot/" README.md

  rm README.md

  cp -a * "$pkgdir/usr/share/rpivot/"

  cat > "$pkgdir/usr/bin/rpivot-server" << EOF
#!/bin/sh
cd /usr/share/rpivot
exec python2 server.py "\${@}"
EOF

  chmod a+x "$pkgdir/usr/bin/rpivot-server"

  cat > "$pkgdir/usr/bin/rpivot-client" << EOF
#!/bin/sh
cd /usr/share/rpivot
exec python2 client.py "\${@}"
EOF

  chmod a+x "$pkgdir/usr/bin/rpivot-client"

}
