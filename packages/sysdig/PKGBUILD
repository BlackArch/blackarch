# This file is part of BlackArch Linux ( http://blackarch.org ).
# See COPYING for license details.

pkgname='sysdig'
pkgver=2488.76ce9d0
pkgrel=2
pkgdesc='Open source system-level exploration and troubleshooting tool.'
arch=('i686' 'x86_64' 'armv6h' 'armv7h')
groups=('blackarch' 'blackarch-recon')
url='http://www.sysdig.org/'
license=('GPL2')
depends=('gcc-libs' 'dkms')
makedepends=('git' 'cmake' 'lua' 'linux-headers' 'luajit')
options=('!strip')
source=('git+https://github.com/draios/sysdig.git')
sha1sums=('SKIP')

pkgver() {
  cd "$srcdir/sysdig"

  echo $(git rev-list --count HEAD).$(git rev-parse --short HEAD)
}

build() {
  cd "$srcdir/sysdig"

  mkdir build

  cd build

  cmake -DSYSDIG_VERSION="$pkgver" -DCMAKE_INSTALL_PREFIX=/usr ..

  make
}

package() {
  cd "$srcdir/sysdig"

  cd build

  make DESTDIR="$pkgdir" install

  install -Dm755 "$srcdir/sysdig/driver/sysdig-probe.ko" \
    "$pkgdir/usr/share/sysdig/driver/sysdig-probe.ko"

  cd ..

  install -Dm644 README.md "$pkgdir/usr/share/doc/sysdig/README.md"
  install -Dm644 COPYING "$pkgdir/usr/share/licenses/sysdig/COPYING"

  rm -rf "$pkgdir/usr/src"
}
