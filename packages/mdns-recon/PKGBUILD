# This file is part of BlackArch Linux ( http://blackarch.org ).
# See COPYING for license details.

pkgname=mdns-recon
pkgver=10.81ecf94
pkgrel=2
groups=('blackarch' 'blackarch-recon')
pkgdesc='An mDNS recon tool written in Python.'
arch=('any')
url='https://github.com/chadillac/mdns_recon'
license=('MIT')
depends=('python2' 'python2-scapy')
makedepends=('git')
source=('git+https://github.com/chadillac/mdns_recon.git')
sha512sums=('SKIP')

pkgver() {
  cd "$srcdir/mdns_recon"

  echo $(git rev-list --count HEAD).$(git rev-parse --short HEAD)
}

package() {
  cd "$srcdir/mdns_recon"

  install -dm 755 "$pkgdir/usr/bin"
  install -dm 755 "$pkgdir/usr/share/mdns-recon"

  install -Dm644 README.md "$pkgdir/usr/share/doc/mdns-recon/README.md"
  install -Dm644 LICENSE "$pkgdir/usr/share/licenses/mdns-recon/LICENSE"

  cp -a * "$pkgdir/usr/share/mdns-recon"

  cat > "$pkgdir/usr/bin/mdns-recon" << EOF
#!/bin/sh
cd /usr/share/mdns-recon
exec python2 mdns_recon.py "\$@"
EOF

  chmod a+x "$pkgdir/usr/bin/mdns-recon"
}
