# This file is part of BlackArch Linux ( https://www.blackarch.org/ ).
# See COPYING for license details.

pkgbase=python-cli-helpers
pkgname=('python2-cli-helpers' 'python-cli-helpers')
_pkgname=cli_helpers
pkgver=2.2.0
pkgrel=2
pkgdesc='Helpers for building command-line apps.'
arch=('any')
url='https://pypi.org/project/cli-helpers/#files'
license=('BSD')
makedepends=('python2-setuptools' 'python-setuptools')
source=("https://files.pythonhosted.org/packages/86/ed/eb0468849a425a9ef0d15f789e2272bb5fc4081926f0261542927599177b/cli_helpers-$pkgver.tar.gz")
sha512sums=('d690e9520178be7ac3eca670651439c6d526ed29d21a2a95dbf95dcb48e366e8a9aae06bbf55121c89f16dc64abbc294014c0b945f8f80f0b1cd65bca92d3216')

prepare() {
  cp -a "$_pkgname-$pkgver"{,-2}
}

build() {
  cd "$_pkgname-$pkgver-2"

  python2 setup.py build

  cd "$srcdir/$_pkgname-$pkgver"

  python setup.py build
}

package_python2-cli-helpers() {
  depends=('python2' 'python2-tabulate' 'python2-terminaltables'
           'python2-backports-csv')

  cd "$_pkgname-$pkgver-2"

  python2 setup.py install --root="$pkgdir" --prefix=/usr -O1 --skip-build
}

package_python-cli-helpers() {
  depends=('python' 'python-tabulate' 'python-terminaltables'
           'python-backports.csv')

  cd "$_pkgname-$pkgver"

  python setup.py install --root="$pkgdir" --prefix=/usr -O1 --skip-build
}

