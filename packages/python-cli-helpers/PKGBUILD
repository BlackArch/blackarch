# This file is part of BlackArch Linux ( https://www.blackarch.org/ ).
# See COPYING for license details.

pkgbase=python-cli-helpers
pkgname=('python2-cli-helpers' 'python-cli-helpers')
_pkgname=cli_helpers
pkgver=1.2.1
pkgrel=5
pkgdesc='Helpers for building command-line apps.'
arch=('any')
url='https://pypi.org/project/cli-helpers/#files'
license=('BSD')
makedepends=('python2-setuptools' 'python-setuptools')
source=("https://files.pythonhosted.org/packages/43/36/ba00975df9d393c0ccc3b1bf1610227bc4c4d611a5c69249b57be8ba6253/$_pkgname-$pkgver.tar.gz")
sha512sums=('46f83382800da7e6ba9116df5cd77bf012e14af821a842e73a7f0ddae64e032dd2d00c124b0f0ed18a07226058dbc41185e99ccd7616e971116a57a22ceace51')

prepare() {
  cp -a "$_pkgname-$pkgver"{,-2}
}

build() {
  cd "$_pkgname-$pkgver-2"

  python2 setup.py build

  cd "$srcdir/$_pkgname-$pkgver"

  python setup.py build
}

package_python2-cli-helpers() {
  depends=('python2' 'python2-tabulate' 'python2-terminaltables'
           'python2-backports-csv')

  cd "$_pkgname-$pkgver-2"

  python2 setup.py install --root="$pkgdir" --prefix=/usr -O1 --skip-build
}

package_python-cli-helpers() {
  depends=('python' 'python-tabulate' 'python-terminaltables'
           'python-backports.csv')

  cd "$_pkgname-$pkgver"

  python setup.py install --root="$pkgdir" --prefix=/usr -O1 --skip-build
}

