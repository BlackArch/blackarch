# This file is part of BlackArch Linux ( https://www.blackarch.org/ ).
# See COPYING for license details.

pkgbase=python-cli-helpers
pkgname=('python2-cli-helpers' 'python-cli-helpers')
_pkgname=cli_helpers
pkgver=2.0.1
pkgrel=1
pkgdesc='Helpers for building command-line apps.'
arch=('any')
url='https://pypi.org/project/cli-helpers/#files'
license=('BSD')
makedepends=('python2-setuptools' 'python-setuptools')
source=("https://files.pythonhosted.org/packages/54/c5/d660b7d34ef5fe558142e397310393a39ecd6568480f12d8b0ddfad5cffc/cli_helpers-$pkgver.tar.gz")
sha512sums=('9491ca3c8798a1e1963cd0156dfc16b1c0540e6e6e3f6a58b5f82c26fc7e57778a927c81f2a0702de85676824f6faaea3c11f0a45cb3e9ecaf239c41958d48be')

prepare() {
  cp -a "$_pkgname-$pkgver"{,-2}
}

build() {
  cd "$_pkgname-$pkgver-2"

  python2 setup.py build

  cd "$srcdir/$_pkgname-$pkgver"

  python setup.py build
}

package_python2-cli-helpers() {
  depends=('python2' 'python2-tabulate' 'python2-terminaltables'
           'python2-backports-csv')

  cd "$_pkgname-$pkgver-2"

  python2 setup.py install --root="$pkgdir" --prefix=/usr -O1 --skip-build
}

package_python-cli-helpers() {
  depends=('python' 'python-tabulate' 'python-terminaltables'
           'python-backports.csv')

  cd "$_pkgname-$pkgver"

  python setup.py install --root="$pkgdir" --prefix=/usr -O1 --skip-build
}

