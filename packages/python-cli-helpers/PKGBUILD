# This file is part of BlackArch Linux ( https://www.blackarch.org/ ).
# See COPYING for license details.

pkgbase=python-cli-helpers
pkgname=('python2-cli-helpers' 'python-cli-helpers')
_pkgname=cli_helpers
pkgver=2.2.1
pkgrel=1
pkgdesc='Helpers for building command-line apps.'
arch=('any')
url='https://pypi.org/project/cli-helpers/#files'
license=('BSD')
makedepends=('python2-setuptools' 'python-setuptools')
source=("https://files.pythonhosted.org/packages/d9/5d/bd0b08f7f8f9d02f44055cf4b41aafa658c1b0731237f303b9fdb49fc8d7/cli_helpers-$pkgver.tar.gz")
sha512sums=('df0972f8ae3f2dfc8ad0541391762a8e80aa97bfd78ff14e999c386f08da2817bafaf41da74c7ca7cf24fbfc5e14e725fe18ab9865e5307fbe9fe65ff915a1c0')

prepare() {
  cp -a "$_pkgname-$pkgver"{,-2}
}

build() {
  cd "$_pkgname-$pkgver-2"

  python2 setup.py build

  cd "$srcdir/$_pkgname-$pkgver"

  python setup.py build
}

package_python2-cli-helpers() {
  depends=('python2' 'python2-tabulate' 'python2-terminaltables'
           'python2-backports-csv')

  cd "$_pkgname-$pkgver-2"

  python2 setup.py install --root="$pkgdir" --prefix=/usr -O1 --skip-build
}

package_python-cli-helpers() {
  depends=('python' 'python-tabulate' 'python-terminaltables'
           'python-backports.csv')

  cd "$_pkgname-$pkgver"

  python setup.py install --root="$pkgdir" --prefix=/usr -O1 --skip-build
}

