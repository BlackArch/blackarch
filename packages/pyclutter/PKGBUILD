# This file is part of BlackArch Linux ( http://blackarch.org ).
# See COPYING for license details.

pkgname='pyclutter'
pkgver='1.3.2'
pkgrel=4
pkgdesc="Python bindings to Clutter."
arch=('x86_64')
url="http://blogs.gnome.org/clutter/"
license=('GPL')
depends=('glib2>=2.16.0' 'gtk2>=2.10.0' 'clutter>=1.0.0' 'python2-cairo>=1.0.2' 'pygobject>=2.21.3' 'python2>=2.6.0' 'pygtk')
options=('!libtool')
source=(http://pkgs.fedoraproject.org/repo/pkgs/pyclutter/pyclutter-$pkgver.tar.bz2/36137bace35d03af4caf20747ccdc5c2/pyclutter-$pkgver.tar.bz2
        pyclutter.patch)
sha1sums=('cd78e53a673933d018d0bb9d89acc44789cd9541'
          '387e2443393b41a03ed4272b63c88ad5bb3f57d2')

prepare() {
  cd "$srcdir/pyclutter-$pkgver"

  patch -Np1 -i ../pyclutter.patch
}

build() {
  cd "$srcdir/pyclutter-$pkgver"

  find . -name '*.py' -print0 | xargs -0 \
      sed -i -e 's,^#!/usr/bin/env python$,#!/usr/bin/env python2,' \
             -e 's,^#!/usr/bin/python$,#!/usr/bin/python2,'

  export PYTHON=/usr/bin/python2

  ./configure --prefix=/usr
  make
}

package() {
  cd "$srcdir/pyclutter-$pkgver"

  make "DESTDIR=$pkgdir" install
}
