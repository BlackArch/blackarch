# This file is part of BlackArch Linux ( https://www.blackarch.org/ ).
# See COPYING for license details.

pkgbase=python-wakeonlan
pkgname=('python2-wakeonlan' 'python-wakeonlan')
_pkgname=wakeonlan
pkgver=2.0.1
pkgrel=1
pkgdesc='A small python module for wake on lan.'
arch=('any')
url='https://pypi.org/project/wakeonlan/#files'
license=('WTFPL')
makedepends=('python2-setuptools' 'python-setuptools')
source=("https://files.pythonhosted.org/packages/c9/11/71cf2944a22311cc4439011dee7fa3e92c80031a59f0ed3b61ca347ff81b/$_pkgname-$pkgver.tar.gz")
sha512sums=('409c524f7bb708059ab9608757c2f1fd5e259309c8a7bc85cfeb6b0da80cdc0e78eb70f7ce28b8a99a9c24c2d391995346a0e75700cd890ef9d5b45087d74544')

prepare() {
  cp -a "$_pkgname-$pkgver"{,-2}
}

build() {
  cd "$_pkgname-$pkgver-2"

  python2 setup.py build

  cd "$srcdir/$_pkgname-$pkgver"

  python setup.py build
}

package_python2-wakeonlan() {
  depends=('python2')

  cd "$_pkgname-$pkgver-2"

  python2 setup.py install --prefix=/usr --root="$pkgdir" --optimize=1 \
    --skip-build

  mv "$pkgdir/usr/bin/$_pkgname" "$pkgdir/usr/bin/${_pkgname}2"
}

package_python-wakeonlan() {
  depends=('python')

  cd "$_pkgname-$pkgver"

  python setup.py install --prefix=/usr --root="$pkgdir" --optimize=1 \
    --skip-build
}

