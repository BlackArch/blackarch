# This file is part of BlackArch Linux ( https://www.blackarch.org/ ).
# See COPYING for license details.

pkgbase=python-wakeonlan
pkgname=('python2-wakeonlan' 'python-wakeonlan')
_pkgname=wakeonlan
pkgver=1.1.6
pkgrel=5
pkgdesc='A small python module for wake on lan.'
arch=('any')
url='https://pypi.org/project/wakeonlan/#files'
license=('WTFPL')
makedepends=('python2-setuptools' 'python-setuptools')
source=("https://files.pythonhosted.org/packages/45/22/88c679aede4e2b1fff55b1d3c7964279d92d0e39fb499548b9508f63c831/$_pkgname-$pkgver.tar.gz")
sha512sums=('250365cffc134a9c4767ea9eacd7b9d3a6381f109ab92d39ce8a47b2d5424ff33c71e26ffa79293a43a60f6841c23ddc79bc51d04b77806c47df7cf39431b34c')

prepare() {
  cp -a "$_pkgname-$pkgver"{,-2}
}

build() {
  cd "$_pkgname-$pkgver-2"

  python2 setup.py build

  cd "$srcdir/$_pkgname-$pkgver"

  python setup.py build
}

package_python2-wakeonlan() {
  depends=('python2')

  cd "$_pkgname-$pkgver-2"

  python2 setup.py install --prefix=/usr --root="$pkgdir" --optimize=1 \
    --skip-build

  mv "$pkgdir/usr/bin/$_pkgname" "$pkgdir/usr/bin/${_pkgname}2"
}

package_python-wakeonlan() {
  depends=('python')

  cd "$_pkgname-$pkgver"

  python setup.py install --prefix=/usr --root="$pkgdir" --optimize=1 \
    --skip-build
}

