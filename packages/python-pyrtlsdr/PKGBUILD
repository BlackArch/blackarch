# This file is part of BlackArch Linux ( https://www.blackarch.org/ ).
# See COPYING for license details.

pkgbase=python-pyrtlsdr
pkgname=('python2-pyrtlsdr' 'python-pyrtlsdr')
_pkgname=pyrtlsdr
pkgver=0.2.91
pkgrel=2
epoch=1
pkgdesc="A Python wrapper for librtlsdr (a driver for Realtek RTL2832U based SDR's)."
arch=('any')
url='https://pypi.org/project/pyrtlsdr/#files'
license=('APACHE')
makedepends=('python2-setuptools' 'python-setuptools')
source=("https://files.pythonhosted.org/packages/ee/cb/70496b7aa7d8e59468de010ed0f829a2da6b90683bab0f5e6eb6e96c3058/$_pkgname-$pkgver.tar.gz")
sha512sums=('0090a083385a777f3d5a29c63d6be1045546c8f94a7af93411dedc5d5c54d5ede072d608affc515b78be0b96706d7edad344e8d78b11a1a39ca5fe82719080f7')

prepare() {
  cp -a "$_pkgname-$pkgver"{,-2}
}

build() {
  cd "$_pkgname-$pkgver-2"

  python2 setup.py build

  cd "$srcdir/$_pkgname-$pkgver"

  python setup.py build
}

package_python2-pyrtlsdr() {
  depends=('python2' 'python2-m2r')

  cd "$_pkgname-$pkgver-2"

  python2 setup.py install --prefix=/usr --root="$pkgdir" --optimize=1 \
    --skip-build
}

package_python-pyrtlsdr() {
  depends=('python')

  cd "$_pkgname-$pkgver"

  python setup.py install --prefix=/usr --root="$pkgdir" --optimize=1 \
    --skip-build
}

