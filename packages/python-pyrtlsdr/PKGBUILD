# This file is part of BlackArch Linux ( https://www.blackarch.org/ ).
# See COPYING for license details.

pkgbase=python-pyrtlsdr
pkgname=('python2-pyrtlsdr' 'python-pyrtlsdr')
_pkgname=pyrtlsdr
pkgver=0.2.93
pkgrel=2
epoch=1
pkgdesc="A Python wrapper for librtlsdr (a driver for Realtek RTL2832U based SDR's)."
arch=('any')
url='https://pypi.org/project/pyrtlsdr/#files'
license=('APACHE')
makedepends=('python2-setuptools' 'python-setuptools')
source=("https://files.pythonhosted.org/packages/source/${_pkgname::1}/$_pkgname/$_pkgname-$pkgver.tar.gz")
sha512sums=('f0a474dcd08493d52d3c27f03349b8d35b9e115401712ffb53841b81ba380b916e4482efe7523158f2f5715fbb8d252e5e951bd7a99b79bf5a353b02c658fc4f')

prepare() {
  cp -a "$_pkgname-$pkgver"{,-2}
}

build() {
  cd "$_pkgname-$pkgver-2"

  python2 setup.py build

  cd "$srcdir/$_pkgname-$pkgver"

  python setup.py build
}

package_python2-pyrtlsdr() {
  depends=('python2' 'python2-m2r')

  cd "$_pkgname-$pkgver-2"

  python2 setup.py install --prefix=/usr --root="$pkgdir" --optimize=1 \
    --skip-build
}

package_python-pyrtlsdr() {
  depends=('python')

  cd "$_pkgname-$pkgver"

  python setup.py install --prefix=/usr --root="$pkgdir" --optimize=1 \
    --skip-build
}

