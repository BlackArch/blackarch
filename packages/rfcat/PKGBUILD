# This file is part of BlackArch Linux ( https://www.blackarch.org/ ).
# See COPYING for license details.

pkgname=rfcat
pkgver=v2.0.1.r5.g07f99bb
pkgrel=2
epoch=1
pkgdesc='Swiss-army knife of ISM band radio.'
arch=('any')
groups=('blackarch' 'blackarch-radio' 'blackarch-exploitation')
url='https://code.google.com/archive/p/rfcat/'
license=('BSD')
depends=('python' 'python-pyusb' 'pyside2' 'libusb')
makedepends=('git' 'python-setuptools')
source=("git+https://github.com/atlas0fd00m/$pkgname.git")
sha512sums=('SKIP')

pkgver() {
  cd $pkgname

  ( set -o pipefail
    git describe --long --tags --abbrev=7 2>/dev/null |
      sed 's/\([^-]*-g\)/r\1/;s/-/./g' ||
    printf "%s.%s" "$(git rev-list --count HEAD)" \
      "$(git rev-parse --short=7 HEAD)"
  )
}

build() {
  cd $pkgname

  python setup.py build
}

package() {
  cd $pkgname

  python setup.py install --root="$pkgdir" --prefix=/usr -O1 --skip-build

  for i in "$pkgdir/usr/bin/"*_*
  do
    mv $i "$(echo $i | tr -s '_' '-')"
  done
}

