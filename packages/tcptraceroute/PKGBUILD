# This file is part of BlackArch Linux ( http://blackarch.org ).
# See COPYING for license details.

pkgname='tcptraceroute'
pkgver='1.5beta7'
pkgrel=3
pkgdesc='A traceroute implementation using TCP packets.'
arch=('x86_64' 'armv6h' 'armv7h' 'aarch64')
url='http://michael.toren.net/code/tcptraceroute/'
license=('GPL')
depends=('libpcap>=1.0.0' 'libnet')
groups=('blackarch' 'blackarch-networking')
source=("https://github.com/mct/tcptraceroute/archive/tcptraceroute-${pkgver}.tar.gz")
sha1sums=('36b325a73d814cd62932f0def43e7d8e952474c1')

build() {
  cd "$srcdir/tcptraceroute-tcptraceroute-$pkgver"

  if [[ "$CARCH" == "i686" || "$CARCH" == "x86_64" ]];
  then
    ./configure --prefix=/usr --sysconfdir=/etc
  else
    ./configure --build=arm --prefix=/usr --sysconfdir=/etc
  fi

  make
}

package() {
  cd "$srcdir/tcptraceroute-tcptraceroute-$pkgver"

  mkdir -p "$pkgdir/usr/share"

  make DESTDIR=$pkgdir install

  mv "$pkgdir/usr/man" "$pkgdir/usr/share/"
}
