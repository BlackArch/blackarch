# This file is part of BlackArch Linux ( http://blackarch.org ).
# See COPYING for license details.

pkgname=dradis-ce
pkgver=857.692d172
pkgrel=1
pkgdesc='An open source framework to enable effective information sharing.'
groups=('blackarch' 'blackarch-recon' 'blackarch-misc')
url='http://dradisframework.org/'
license=('GPL')
depends=('ruby2.3' 'ruby2.3-bundler' 'git')
makedepends=('git')
arch=('any')
source=("git+https://github.com/dradis/dradis-ce.git"
        "dradis-ce.patch"
        "dradis-ce.sysusers.conf")
install="dradis-ce.install"
sha512sums=('SKIP'
            'f8d219a42ffd6f4166d50204830fa6e14fce461f78372659b9ef6d6548d8ddef2b939a5147bc1f5783192386645d0cd932ba70ab4a2620bcdae0405906a4619f'
            '104c3f6d2dcdc5cd02092ad16783dcd1907ff8fba95c87db609fe5c7307ca4fd97730b5117d6db78d9fd9273c4fd5c963b7d404d08cb8615f3a9fe3864f16c95')

pkgver() {
  cd "$srcdir/dradis-ce"

  echo $(git rev-list --count HEAD).$(git rev-parse --short HEAD)
}

prepare() {
  cd "$srcdir/dradis-ce"

  patch -p1 -i ../dradis-ce.patch
}

package() {
  cd "$srcdir"

  install -dm 755 "$pkgdir/usr/lib/sysusers.d/" \
                   "$pkgdir/usr/bin" \
                   "$pkgdir/usr/share"

  install -m644 "dradis-ce.sysusers.conf"\
    "$pkgdir/usr/lib/sysusers.d/dradis-ce.conf"

  cp -ar "dradis-ce" "$pkgdir/usr/share/dradis-ce"

  cat > "$pkgdir/usr/bin/dradis-ce" << EOF
#!/bin/sh
cd /usr/share/dradis-ce
exec bundle-2.3 exec rails server \${@}
EOF

  chmod +x "$pkgdir/usr/bin/dradis-ce"
}
