# This file is part of BlackArch Linux ( http://blackarch.org ).
# See COPYING for license details.

pkgname='hash-extender'
pkgver=136.d27581e
pkgrel=1
pkgdesc='A hash length extension attack tool.'
groups=('blackarch' 'blackarch-crypto')
arch=('x86_64' 'armv6h' 'armv7h' 'aarch64')
url='https://github.com/iagox86/hash_extender'
license=('BSD')
depends=('openssl')
makedepends=('git')
source=('git+https://github.com/iagox86/hash_extender.git')
sha1sums=('SKIP')

pkgver() {
  cd "$srcdir/hash_extender"

  echo $(git rev-list --count HEAD).$(git rev-parse --short HEAD)
}

prepare() {
  cd "$srcdir/hash_extender"

  sed -i 's|-D_BSD_SOURCE|-D_DEFAULT_SOURCE|' Makefile
}

build() {
  cd "$srcdir/hash_extender"

  make
}

package() {
  cd "$srcdir/hash_extender"

  install -Dm755 hash_extender "$pkgdir/usr/bin/hash-extender"
  install -Dm755 hash_extender_test "$pkgdir/usr/bin/hash-extender-test"
  install -Dm644 -t "$pkgdir/usr/share/doc/hash-extender/" README.md
  install -Dm644 LICENSE.txt \
    "$pkgdir/usr/share/licenses/hash-extender/LICENSE.txt"
}
