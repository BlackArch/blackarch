# This file is part of BlackArch Linux ( https://www.blackarch.org/ ).
# See COPYING for license details.

pkgbase=python-oauth2
pkgname=('python2-oauth2' 'python-oauth2')
_pkgname=oauth2
pkgver=1.9.0.post1
pkgrel=8
pkgdesc='Library for OAuth.'
arch=('any')
url='https://pypi.org/project/oauth2/#files'
license=('MIT')
makedepends=('python2-setuptools' 'python-setuptools')
source=("https://files.pythonhosted.org/packages/64/19/8b9066e94088e8d06d649e10319349bfca961e87768a525aba4a2627c986/$_pkgname-$pkgver.tar.gz")
sha512sums=('a3b64eef331d4c4f59c275f14efb2dfb48ad60de47a59a88106f60d64428a09b76d8bedf08c98c113493968d83708b1478b4b61b2b5528b5b01fa22777daff4c')

prepare() {
  cp -a "$_pkgname-$pkgver"{,-2}
}

build() {
  cd "$_pkgname-$pkgver-2"

  python2 setup.py build

  cd "$srcdir/$_pkgname-$pkgver"

  python setup.py build
}

package_python2-oauth2() {
  depends=('python2' 'python2-httplib2')

  cd "$_pkgname-$pkgver-2"

  python2 setup.py install --root="$pkgdir" --prefix=/usr -O1 --skip-build

  rm -rf "$pkgdir/usr/lib/python2."*"/site-packages/tests"

  find "$pkgdir/". -type d -name '__pycache__' -exec rm -rf {} +
}

package_python-oauth2() {
  depends=('python' 'python-httplib2')

  cd "$_pkgname-$pkgver"

  python setup.py install --root="$pkgdir" --prefix=/usr -O1 --skip-build

  rm -rf "$pkgdir/usr/lib/python3."*"/site-packages/tests"

  find "$pkgdir/". -type d -name '__pycache__' -exec rm -rf {} +
}

