# This file is part of BlackArch Linux ( http://blackarch.org ).
# See COPYING for license details.

pkgname=cpptest
pkgver=1.1.2
pkgrel=2
groups=('blackarch' 'blackarch-code-audit')
pkgdesc='A portable and powerful, yet simple, unit testing framework for handling automated tests in C++.'
arch=('x86_64' 'armv6h' 'armv7h' 'aarch64')
url='http://cpptest.sourceforge.net/'
depends=('gcc-libs')
license=('GPL')
source=("http://downloads.sourceforge.net/project/cpptest/cpptest/cpptest-$pkgver/cpptest-$pkgver.tar.gz")
sha512sums=('da1030c69a5886e752b9ae08b65c501334c7d2d4abd2174190209ce11ed351239a615b9f7c5019ee601289523b2c21a1205d2042e59aea6c2882d77d636fc57a')

build() {
  cd "$srcdir/cpptest-$pkgver"

  ./configure --prefix=/usr

  make
}

package() {
  cd "$srcdir/cpptest-$pkgver"

  install -dm 755 "$pkgdir/usr/share/doc/cpptest"
  install -dm 755 "$pkgdir/usr/share/licenses/cpptest"

  cp -a NEWS INSTALL BUGS README "$pkgdir/usr/share/doc/cpptest"
  cp -a COPYING "$pkgdir/usr/share/licenses/cpptest"

  make install DESTDIR="$pkgdir"
}
