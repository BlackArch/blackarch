# This file is part of BlackArch Linux ( http://blackarch.org ).
# See COPYING for license details.

pkgname=machinae
pkgver=139.9982599
pkgrel=1
groups=('blackarch' 'blackarch-recon')
pkgdesc='A tool for collecting intelligence from public sites/feeds about various security-related pieces of data.'
arch=('any')
url='https://github.com/HurricaneLabs/machinae'
license=('MIT')
depends=('python' 'python-dateutil' 'python-requests' 'python-yaml'
         'python-magic' 'python-stopit' 'python-feedparser' 'python-ipwhois'
         'python-pytz' 'python-beautifulsoup4' 'python-filemagic'
         'python-tzlocal' 'python-relatime' 'python-html5lib' 'python-defang'
         'python-dnspython')
makedepends=('git' 'python-setuptools')
source=('git+https://github.com/HurricaneLabs/machinae.git')
sha512sums=('SKIP')

pkgver() {
  cd machinae

  echo $(git rev-list --count HEAD).$(git rev-parse --short HEAD)
}

prepare() {
  cd machinae

  # psst, tmp fix...
  sed -i 's/import machinae//g' setup.py
  sed -i 's|VERSION = machinae.__version__|VERSION = "1.4.5"|' setup.py
  sed -i 's|ipwhois<0.11|ipwhois|' setup.py
  sed -i 's|dnspython3|dnspython|' setup.py
}

package() {
  cd machinae

  python setup.py install --root="$pkgdir" --optimize=1

  install -Dm644 machinae.yml "$pkgdir/etc/machinae.yml"

  install -Dm644 -t "$pkgdir/usr/share/doc/machinae/" README.md
  install -Dm644 LICENSE.txt "$pkgdir/usr/share/licenses/machinae/LICENSE.txt"
}

