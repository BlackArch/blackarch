# This file is part of BlackArch Linux ( https://www.blackarch.org/ ).
# See COPYING for license details.

pkgname=python-pymssql
_pkgname=pymssql
pkgver=2.3.13
pkgrel=1
pkgdesc='DB-API interface to Microsoft SQL Server for Python. (new Cython-based version).'
arch=('x86_64' 'aarch64')
url='https://pypi.org/project/pymssql/#files'
license=('LGPL')
depends=('python' 'freetds')
makedepends=('python-installer' 'python-setuptools' 'python-build' 'python-wheel' 'cython'
            'python-packaging' 'python-setuptools-scm' 'python-tomli')
source=("https://files.pythonhosted.org/packages/source/${_pkgname::1}/$_pkgname/$_pkgname-$pkgver.tar.gz")
sha512sums=('53bcf3f04c00bfac60935af12331e08a5adc59e1eaab12083304f49799a925e99f944436ce4cff606c562fee842c93e4a824f46c97513f2efc6196be82184023')


prepare() {
  cd "$_pkgname-$pkgver"

  sed -i '/standard-distutils/d' pyproject.toml
}

build() {
  cd "$_pkgname-$pkgver"

  python -m build --wheel --no-isolation
}

package() {
  cd "$_pkgname-$pkgver"

  python -m installer --destdir="$pkgdir" dist/*.whl
}

