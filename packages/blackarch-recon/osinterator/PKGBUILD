# This file is part of BlackArch Linux ( https://www.blackarch.org/ ).
# See COPYING for license details.

pkgname=osinterator
pkgver=3.8447f58
pkgrel=2
pkgdesc='Open Source Toolkit for Open Source Intelligence Gathering.'
groups=('blackarch' 'blackarch-recon')
arch=('any')
url='https://github.com/guitarmanj/OSINTerator'
license=('MIT')
depends=('python2' 'python2-xgoogle')
makedepends=('git')
source=("$pkgname::git+https://github.com/guitarmanj/OSINTerator.git")
sha512sums=('SKIP')

pkgver() {
  cd $pkgname

  echo $(git rev-list --count HEAD).$(git rev-parse --short HEAD)
}

prepare() {
  cd $pkgname

  sed -i 's|bin/python|bin/python2|' dorkedIn/dorkedIn.py
}

package() {
  cd $pkgname

  install -Dm 755 dorkedIn/dorkedIn.py "$pkgdir/usr/bin/$pkgname"
  install -Dm 644 dorkedIn/README.md "$pkgdir/usr/share/doc/$pkgname/README.md"
  install -Dm 644 LICENSE "$pkgdir/usr/share/licenses/$pkgname/LICENSE"
}

