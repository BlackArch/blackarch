# This file is part of BlackArch Linux ( https://www.blackarch.org/ ).
# See COPYING for license details.

pkgname=treasure
pkgver=2.b3249be
pkgrel=1
epoch=1
groups=('blackarch' 'blackarch-recon')
pkgdesc='Hunt for sensitive information through githubs code search.'
arch=('any')
url='https://github.com/GuerrillaWarfare/Treasure'
license=('custom:unknown')
depends=('python2' 'python2-beautifulsoup4' 'python2-requests')
makedepends=('git')
#source=("$pkgname::git+https://github.com/GuerrillaWarfare/Treasure.git")
source=("git+https://github.com/BlackArch/$pkgname.git")
sha512sums=('SKIP')

pkgver() {
  cd $pkgname

  echo $(git rev-list --count HEAD).$(git rev-parse --short HEAD)
}

package() {
  cd $pkgname

  install -dm 755 "$pkgdir/usr/bin"
  install -dm 755 "$pkgdir/usr/share/$pkgname"

  install -Dm 644 -t "$pkgdir/usr/share/doc/$pkgname/" README.md

  rm README.md

  cp -a * "$pkgdir/usr/share/$pkgname/"

  cat > "$pkgdir/usr/bin/$pkgname" << EOF
#!/bin/sh
cd /usr/share/$pkgname
exec python2 $pkgname "\$@"
EOF

  chmod +x "$pkgdir/usr/bin/$pkgname"
}

