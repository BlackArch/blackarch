# This file is part of BlackArch Linux ( https://www.blackarch.org/ ).
# See COPYING for license details.

pkgname=neglected
pkgver=8.68d02b3
pkgrel=2
epoch=1
groups=('blackarch' 'blackarch-recon')
pkgdesc='Facebook CDN Photo Resolver.'
arch=('any')
url='https://github.com/GuerrillaWarfare/neglected'
license=('custom:unknown')
depends=('python2' 'python2-requests')
makedepends=('git')
source=("git+https://github.com/GuerrillaWarfare/$pkgname.git")
sha512sums=('SKIP')

pkgver() {
  cd $pkgname

  echo $(git rev-list --count HEAD).$(git rev-parse --short HEAD)
}

package() {
  cd $pkgname

  install -dm 755 "$pkgdir/usr/bin"
  install -dm 755 "$pkgdir/usr/share/$pkgname"

  install -Dm 644 -t "$pkgdir/usr/share/doc/$pkgname/" README.md

  rm README.md

  cp -a * "$pkgdir/usr/share/$pkgname/"

  cat > "$pkgdir/usr/bin/$pkgname" << EOF
#!/bin/sh
cd /usr/share/$pkgname
python2 $pkgname "\$@"
EOF

  chmod +x "$pkgdir/usr/bin/$pkgname"
}

