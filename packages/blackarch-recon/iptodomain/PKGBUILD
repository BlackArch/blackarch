# This file is part of BlackArch Linux ( https://www.blackarch.org/ ).
# See COPYING for license details.

pkgname=iptodomain
pkgver=18.f1afcd7
pkgrel=1
pkgdesc='This tool extract domains from IP address based in the information saved in virustotal.'
groups=('blackarch' 'blackarch-recon')
arch=('any')
url='https://github.com/Hackplayers/iptodomain'
license=('GPL3')
depends=('python2')
makedepends=('git')
source=("git+https://github.com/Hackplayers/$pkgname.git")
sha512sums=('SKIP')

pkgver() {
  cd $pkgname

  echo $(git rev-list --count HEAD).$(git rev-parse --short HEAD)
}

prepare() {
  cd $pkgname

  # add your fuckin shebangs...
  sed -i '1i\#!/usr/bin/env python2' "$pkgname.py"
}

package() {
  cd $pkgname

  install -Dm 755 "$pkgname.py" "$pkgdir/usr/bin/$pkgname"
  install -Dm 644 -t "$pkgdir/usr/share/doc/$pkgname/" README.md
  install -Dm 644 LICENSE.md "$pkgdir/usr/share/licenses/$pkgname/LICENSE"
}

