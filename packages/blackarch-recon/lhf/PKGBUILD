# This file is part of BlackArch Linux ( https://www.blackarch.org/ ).
# See COPYING for license details.

pkgname=lhf
pkgver=40.51568ee
pkgrel=2
pkgdesc='A modular recon tool for pentesting.'
groups=('blackarch' 'blackarch-recon')
arch=('any')
url='https://github.com/blindfuzzy/LHF'
license=('GPL3')
depends=('python2' 'arachni' 'dirb' 'nmap' 'hydra' 'sqlmap' 'enum4linux' 'nikto'
         'python2-python-libnmap' 'python2-selenium' 'python2-psutil')
makedepends=('git')
source=("$pkgname::git+https://github.com/blindfuzzy/LHF.git")
sha512sums=('SKIP')

pkgver() {
  cd $pkgname

  echo $(git rev-list --count HEAD).$(git rev-parse --short HEAD)
}

package() {
  cd $pkgname

  install -dm 755 "$pkgdir/usr/bin"
  install -dm 755 "$pkgdir/usr/share/$pkgname"

  install -Dm 644 -t "$pkgdir/usr/share/doc/$pkgname/" README.md
  install -Dm 644 LICENSE "$pkgdir/usr/share/licenses/$pkgname/LICENSE"

  rm README.md LICENSE

  cp -a * "$pkgdir/usr/share/$pkgname/"

  cat > "$pkgdir/usr/bin/$pkgname" << EOF
#!/bin/sh
cd /usr/share/$pkgname
exec python2 LHF.py "\$@"
EOF

  chmod +x "$pkgdir/usr/bin/$pkgname"
}

