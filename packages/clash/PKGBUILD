# This file is part of BlackArch Linux ( https://www.blackarch.org/ ).
# See COPYING for license details.

pkgname=clash
pkgver=v1.9.0.r1.g8c6d0c6
pkgrel=1
pkgdesc='A rule-based tunnel in Go.'
arch=('x86_64' 'aarch64')
groups=('blackarch' 'blackarch-tunnel' 'blackarch-networking')
url='https://github.com/Dreamacro/clash'
license=('GPL3')
depends=('glibc')
makedepends=('git' 'go')
source=("git+https://github.com/Dreamacro/$pkgname.git")
sha256sums=('SKIP')

pkgver() {
  cd $pkgname

  git describe --long --tags | sed 's/\([^-]*-g\)/r\1/;s/-/./g'
}

build() {
  cd $pkgname

  GOPATH="$srcdir" go mod download
  GOPATH="$srcdir" go build \
    -trimpath \
    -buildmode=pie \
    -mod=readonly \
    -modcacherw \
    -ldflags "-s -w" \
    -o $pkgname .

}

package() {
  cd $pkgname

  install -Dm 755 $pkgname "$pkgdir/usr/bin/$pkgname"
  install -Dm 644 -t "$pkgdir/usr/share/doc/$pkgname/" README.md
}

