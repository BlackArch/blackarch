# This file is part of BlackArch Linux ( https://www.blackarch.org/ ).
# See COPYING for license details.

pkgname=python-h3
_pkgname=${pkgname#python-}
pkgver=4.3.1
pkgrel=1
pkgdesc='Python bindings for H3, a hierarchical hexagonal geospatial indexing system.'
arch=('x86_64' 'aarch64')
url='https://github.com/uber/h3-py'
license=('MIT')
depends=('python')
makedepends=('git' 'python-build' 'python-pip' 'cython'
             'python-scikit-build-core')
options=(!emptydirs)
source=("https://files.pythonhosted.org/packages/source/${_pkgname::1}/$_pkgname/$_pkgname-$pkgver.tar.gz")
sha512sums=('90f2c221117e58d253f05e91404ed81fb3aaf1a00eb33dbfffdff9d082d81f0915a8f30f9824bb8ce4ba9e7598f0d68cbdde4778ed61b2601fa3d830192e3f7f')

build() {
  cd "$_pkgname-$pkgver"

  python -m build --wheel --outdir="$startdir/dist"
}

package() {
  cd "$_pkgname-$pkgver"

  pip install \
    --verbose \
    --disable-pip-version-check \
    --no-warn-script-location \
    --ignore-installed \
    --no-compile \
    --no-deps \
    --root="$pkgdir" \
    --prefix=/usr \
    --no-index \
    --find-links="file://$startdir/dist" \
    $_pkgname
}

