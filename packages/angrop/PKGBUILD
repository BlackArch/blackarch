# This file is part of BlackArch Linux ( http://blackarch.org ).
# See COPYING for license details.

pkgname='angrop'
pkgver=141.ce5d98b
pkgrel=1
pkgdesc='A rop gadget finder and chain builder.'
groups=('blackarch' 'blackarch-exploitation')
arch=('any')
url='https://github.com/salls/angrop'
license=('BSD')
depends=('python2' 'python2-progressbar' 'python2-pyvex' 'python2-claripy'
         'python2-simuvex' 'angr')
makedepends=('git' 'python2-setuptools')
source=('git+https://github.com/salls/angrop.git')
sha1sums=('SKIP')

pkgver() {
  cd "$srcdir/angrop"

  echo $(git rev-list --count HEAD).$(git rev-parse --short HEAD)
}

build() {
  cd "$srcdir/angrop"

  python2 setup.py build
}

package() {
  cd "$srcdir/angrop"

  python2 setup.py install --root="$pkgdir"

  install -Dm644 -t "$pkgdir/usr/share/angrop/tests" tests/*
  install -Dm644 -t "$pkgdir/usr/share/doc/angrop/" README.md
  install -Dm644 LICENSE "$pkgdir/usr/share/licenses/angrop/LICENSE"
}
