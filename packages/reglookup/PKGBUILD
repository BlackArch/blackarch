# This file is part of BlackArch Linux ( https://www.blackarch.org/ ).
# See COPYING for license details.

pkgname=reglookup
pkgver=1.0.1+svn296.2.r0.gcab83db
pkgrel=1
pkgdesc='Command line utility for reading and querying Windows NT registries.'
arch=('x86_64' 'aarch64')
groups=('blackarch' 'blackarch-forensic')
url='https://projects.sentinelchicken.org/reglookup'
license=('GPL-3.0-or-later')
depends=('python' 'talloc' 'bash')
makedepends=('git' 'gcc' 'scons' 'docbook2x' 'docbook-utils' 'python-build'
             'python-installer' 'python-setuptools' 'python-wheel')
source=("git+https://salsa.debian.org/pkg-security-team/$pkgname.git"
        'SConstruct.patch')
sha512sums=('SKIP'
            '84e1897dde312fbc215da788c0deb288bc36ccd318fdc5bfc6b70bb9e4ab9445b12384e4248f29b6a682a45c20d6623b05bfcf682753a49c5894fdce4974b6c6')

pkgver() {
  cd $pkgname

  git describe --long --tags --abbrev=7 2>/dev/null \
  | sed 's|^[^/]*/||; s|/|.|g; s|:||g; s|\(.*\)-\([0-9][0-9]*\)-g\([0-9a-f]\+\)$|\1.r\2.g\3|; s|-|.|g' \
  || printf '%s.%s\n' "$(git rev-list --count HEAD)" "$(git rev-parse --short=7 HEAD)"
}

prepare() {
  cd $pkgname

  patch < ../SConstruct.patch
}

build() {
  cd $pkgname

  scons
  python -m build --wheel --no-isolation
}

package() {
  cd $pkgname

  PREFIX="$pkgdir/usr" scons install
  python -m installer --destdir="$pkgdir" dist/*.whl
}

