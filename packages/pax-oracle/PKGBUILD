# This file is part of BlackArch Linux ( https://www.blackarch.org/ ).
# See COPYING for license details.

pkgname=pax-oracle
_pkgname=pax
pkgver=v0.2.2.r1.g38ac0d8
pkgrel=1
pkgdesc='CLI tool for PKCS7 padding oracle attacks.'
arch=('x86_64' 'aarch64')
groups=('blackarch' 'blackarch-crypto')
url='https://github.com/liamg/pax'
license=('MIT')
depends=()
makedepends=('git' 'go')
source=("$pkgname::git+https://github.com/liamg/$_pkgname.git")
sha512sums=('SKIP')

pkgver() {
  cd $pkgname

  git describe --long --tags | sed 's/\([^-]*-g\)/r\1/;s/-/./g'
}

build() {
  cd $pkgname

  GOPATH="$srcdir" go mod download
  GOPATH="$srcdir" go build \
    -trimpath \
    -buildmode=pie \
    -mod=readonly \
    -modcacherw \
    -ldflags "-s -w" \
    -o $pkgname ./cmd/pax
}

package() {
  cd $pkgname

  install -Dm 755 $pkgname "$pkgdir/usr/bin/$pkgname"
  install -Dm 644 -t "$pkgdir/usr/share/doc/$pkgname/" README.md
  install -Dm 644 LICENSE "$pkgdir/usr/share/licenses/$pkgname/LICENSE"
}

