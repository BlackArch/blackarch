# This file is part of BlackArch Linux ( http://blackarch.org ).
# See COPYING for license details.

pkgname='sqlninja'
pkgver='0.2.999'
pkgrel=6
groups=('blackarch' 'blackarch-exploitation' 'blackarch-fuzzer'
        'blackarch-webapp')
pkgdesc='A tool targeted to exploit SQL Injection vulnerabilities on a web application that uses Microsoft SQL Server as its back-end.'
arch=('any')
url='http://sqlninja.sourceforge.net/'
license=('GPL2')
depends=('perl' 'perl-netpacket' 'perl-net-pcap' 'perl-net-dns' 'perl-net-rawip'
         'perl-io-socket-ssl' 'perl-dbi' 'perl-dbd-sqlite')
optdepends=('metasploit: to use the metasploit attack mode'
            'tigervnc: a VNC client, vncviewer, to use the VNC payload')
conflicts=('sqlninja-svn')
source=("http://downloads.sourceforge.net/sourceforge/sqlninja/sqlninja-${pkgver}-alpha1.tgz")
sha1sums=('14d01050138c9fab77274e9823708e82742cdc29')

prepare() {
  cd "$srcdir/sqlninja-$pkgver-alpha1"

  # TODO: Is this still relevant?
  # Check if metasploit-svn or metasploit stable is installed (-svn takes precedence)
  # Apply Archlinux metasploit home directory to sqlninja.conf.
  if [[ -d /usr/src/metasploit ]] ; then
    sed -i 's|/home/user/trunk/|/usr/src/metasploit|g' sqlninja.conf
  else
    sed -i 's|/home/user/trunk/|/usr/share/metasploit|g' sqlninja.conf
  fi
}

package() {
  cd "$srcdir/sqlninja-$pkgver-alpha1"

  mkdir -p "$pkgdir/usr/bin"
  mkdir -p "$pkgdir/usr/share/sqlninja"

  install -Dm644 -t "$pkgdir/usr/share/doc/sqlninja" ChangeLog README \
    sqlninja-howto.html
  install -Dm644 LICENSE "$pkgdir/usr/share/licenses/sqlninja/LICENSE"

  rm ChangeLog README sqlninja-howto.html

  cp -a * "$pkgdir/usr/share/sqlninja"

  cat > "$pkgdir/usr/bin/sqlninja" << EOF
#!/bin/bash
cd /usr/share/sqlninja
exec ./sqlninja "\${@}"
EOF

  chmod +x "$pkgdir/usr/bin/sqlninja"
}
