# This file is part of BlackArch Linux ( http://blackarch.org ).
# See COPYING for license details.

pkgname='sqlninja'
pkgver='0.2.999'
pkgrel=7
groups=('blackarch' 'blackarch-exploitation' 'blackarch-fuzzer'
        'blackarch-webapp')
pkgdesc='A tool targeted to exploit SQL Injection vulnerabilities on a web application that uses Microsoft SQL Server as its back-end.'
arch=('any')
url='http://sqlninja.sourceforge.net/'
license=('GPL2')
depends=('perl' 'perl-netpacket' 'perl-net-pcap' 'perl-net-dns' 'perl-net-rawip'
         'perl-io-socket-ssl' 'perl-dbi' 'perl-dbd-sqlite')
optdepends=('metasploit: to use the metasploit attack mode'
            'tigervnc: a VNC client, vncviewer, to use the VNC payload')
conflicts=('sqlninja-svn')
source=("http://downloads.sourceforge.net/sourceforge/sqlninja/sqlninja-${pkgver}-alpha1.tgz"
        'sqlninja.patch')
sha1sums=('14d01050138c9fab77274e9823708e82742cdc29'
          '6da51b63397825e1366cbc6da199f9e03ccf3f2b')

prepare() {
  cd sqlninja-"${pkgver}"-alpha1

  if [[ -d /usr/src/metasploit ]] ; then
    sed -i 's|/home/user/trunk/|/opt/metasploit|g' sqlninja.conf
  else
    sed -i 's|/home/user/trunk/|/opt/metasploit|g' sqlninja.conf
  fi

  patch -p0 < $srcdir/sqlninja.patch
}


package() {
  cd sqlninja-"${pkgver}"-alpha1

  mkdir -p "$pkgdir/usr/bin"
  mkdir -p "$pkgdir/usr/share/sqlninja"

  install -Dm644 -t "$pkgdir/usr/share/doc/sqlninja" ChangeLog README \
    sqlninja-howto.html
  install -Dm644 LICENSE "$pkgdir/usr/share/licenses/sqlninja/LICENSE"

  rm ChangeLog README sqlninja-howto.html

  cp -a * "$pkgdir/usr/share/sqlninja"

  cat > "$pkgdir/usr/bin/sqlninja" << EOF
#!/bin/bash
cd /usr/share/sqlninja
exec perl ./sqlninja "\${@}"
EOF

  chmod +x "$pkgdir/usr/bin/sqlninja"
}
