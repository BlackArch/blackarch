# This file is part of BlackArch Linux ( http://blackarch.org ).
# See COPYING for license details.

pkgname='hasere'
pkgver='1.0'
pkgrel=2
groups=('blackarch' 'blackarch-recon' 'blackarch-scanner')
pkgdesc='Discover the vhosts using google and bing.'
arch=('any')
url='https://github.com/galkan/hasere'
license=('custom')
depends=('python2')
optdepends=('nmap: Adds nmap support')
source=("https://github.com/galkan/hasere/archive/v${pkgver}.tar.gz")
sha1sums=('b57ff3ced4e40ec061e9c6509e1a6dc91c172a74')

prepare() {
  cd "$srcdir/hasere-$pkgver"

  sed -i 's|python$|env python2|' *.py
}

package() {
  cd "$srcdir/hasere-$pkgver"

  mkdir -p "$pkgdir/usr/bin"
  mkdir -p "$pkgdir/usr/share/hasere/"{data,lib}

  install -Dm755 hasere.py "$pkgdir/usr/share/hasere"
  install -Dm644 LICENSE.txt "$pkgdir/usr/share/licenses/hasere/LICENSE.txt"

  cp -a --no-preserve=ownership data/* "$pkgdir/usr/share/hasere/data"
  cp -a --no-preserve=ownership lib/* "$pkgdir/usr/share/hasere/lib"

  cat > "$pkgdir/usr/bin/hasere" << EOF
#!/bin/sh
cd /usr/share/hasere
exec python2 hasere.py "\$@"
EOF

  chmod +x "$pkgdir/usr/bin/hasere"
}
