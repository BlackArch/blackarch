# This file is part of BlackArch Linux ( http://blackarch.org ).
# See COPYING for license details.

pkgname=python2-scapy-http
pkgver=1.8
pkgrel=2
pkgdesc='HTTP-layer support for Scapy.'
arch=('any')
url='https://pypi.python.org/pypi/scapy-http/'
license=('custom:unknown')
depends=('python2' 'python2-scapy')
makedepends=('python2-setuptools' 'python2-scapy')
source=("https://pypi.python.org/packages/f6/5f/d5b60f6b1a1fb2ed28faa01d5c1aaaf5d399caaa4e4fee1cc16abaad5802/scapy-http-${pkgver}.tar.gz"
        'setup.py.patch')
sha512sums=('3f5ea802d776cdb4933ad0e15fd166e25a57bec1930a010364406669d0236279a468b65397b5113d535788a1ea118a190ad04a421325490320e8f98c26786c42'
            '9cbe7f315f9dbc1502357c9bc525974316187712d3c716476c671b2a7b2a35e93d2a2811e06802272154379352e589534dbd2958d16415bb4f8bb1306a2e2fea')

prepare() {
  cd "$srcdir"

  patch -p1 < setup.py.patch
}

build() {
  cd "$srcdir/scapy-http-$pkgver"

  python2 setup.py build
}

package() {
  cd "$srcdir/scapy-http-$pkgver"

  python2 setup.py install --root="$pkgdir" --optimize=1 --prefix=/usr --force

  find "$pkgdir" -type f \( -iname '*.pyc' -or -iname '*.pyo' \) -print -exec rm -rf {} \;
}
