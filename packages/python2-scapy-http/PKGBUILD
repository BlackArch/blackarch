# This file is part of BlackArch Linux ( https://www.blackarch.org/ ).
# See COPYING for license details.

pkgname=python2-scapy-http
pkgver=1.8.2
pkgrel=1
pkgdesc='HTTP-layer support for Scapy.'
arch=('any')
url='https://pypi.python.org/pypi/scapy-http/'
license=('custom:unknown')
depends=('python2' 'python2-scapy')
makedepends=('python2-setuptools' 'python2-scapy')
source=("https://files.pythonhosted.org/packages/5b/b8/959f95e991d7cfc967d8999a73fd63f78fbf6936e17f037ac551317d85b9/scapy-http-$pkgver.tar.gz"
        'setup.py.patch')
sha512sums=('2f895db87b413066ed1094e2e57c1e93e7165871a0e3f9bd736b05c5ca775b49ab55e56bce4baa4ebfec0bd82661ceb10e4303aa8bcf93cffbfd38ddb8d1f949'
            '16d88ab6cd0115b8e13bc4ff917e2564c2a9063f1b0acf92b333d9c3ca84bf8e131621eb733ac69cf72556256b61014bc305bfdee677526f6953e2e5e6a7e816')

prepare() {
  patch -p1 < setup.py.patch
}

build() {
  cd "scapy-http-$pkgver"

  python2 setup.py build
}

package() {
  cd "scapy-http-$pkgver"

  python2 setup.py install --prefix=/usr --root="$pkgdir" --optimize=1 \
    --skip-build

  find "$pkgdir" -type f \( -iname '*.pyc' -or -iname '*.pyo' \) -print -exec rm -rf {} \;
}

