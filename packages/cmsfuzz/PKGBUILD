# This file is part of BlackArch Linux ( http://blackarch.org ).
# See COPYING for license details.

pkgname='cmsfuzz'
pkgver=5.6be5a98
pkgrel=3
groups=('blackarch' 'blackarch-webapp' 'blackarch-scanner' 'blackarch-fuzzer')
pkgdesc='Fuzzer for wordpress, cold fusion, drupal, joomla, and phpnuke.'
arch=('any')
url='https://github.com/nahamsec/CMSFuzz'
license=('GPL3')
depends=('ruby')
makedepends=('git')
source=('git+https://github.com/nahamsec/CMSFuzz.git')
sha1sums=('SKIP')

pkgver() {
  cd "$srcdir/CMSFuzz"

  echo $(git rev-list --count HEAD).$(git rev-parse --short HEAD)
}

package() {
  cd "$srcdir/CMSFuzz"

  mkdir -p "$pkgdir/usr/bin"
  mkdir -p "$pkgdir/usr/share/cmsfuzz"

  install -Dm644 README.md "$pkgdir/usr/share/doc/cmsfuzz/README.md"

  rm README.md

  cp -a * "$pkgdir/usr/share/cmsfuzz"

  cat > "$pkgdir/usr/bin/cmsfuzz" << EOF
#!/bin/sh
cd /usr/share/cmsfuzz
exec ruby dirbf.rb "\$@"
EOF

  chmod a+x "$pkgdir/usr/bin/cmsfuzz"
}
