# This file is part of BlackArch Linux ( http://blackarch.org ).
# See COPYING for license details.

pkgname='sloth-fuzzer'
pkgver=40.0f0cf2e
pkgrel=1
groups=('blackarch' 'blackarch-fuzzer')
pkgdesc='A smart file fuzzer.'
arch=('x86_64' 'armv6h' 'armv7h' 'aarch64')
url='https://github.com/mfontanini/sloth-fuzzer'
license=('custom:unknown')
depends=('crypto++' 'flex' 'bison')
makedepends=('git')
source=('git+https://github.com/mfontanini/sloth-fuzzer.git')
sha1sums=('SKIP')

pkgver() {
  cd "$srcdir/sloth-fuzzer"

  echo $(git rev-list --count HEAD).$(git rev-parse --short HEAD)
}

build() {
  cd "$srcdir/sloth-fuzzer"

  ./configure --prefix=/usr

  make
}

package() {
  cd "$srcdir/sloth-fuzzer"

  install -Dm755 sloth "$pkgdir/usr/bin/sloth"
  install -Dm644 -t "$pkgdir/usr/share/doc/sloth-fuzzer/" README.md
}
