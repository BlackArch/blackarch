# This file is part of BlackArch Linux ( https://www.blackarch.org/ ).
# See COPYING for license details.

pkgname=detect-it-easy
pkgver=2.05
pkgrel=1
epoch=1
groups=('blackarch' 'blackarch-binary' 'blackarch-reversing')
pkgdesc='A program for determining types of files.'
arch=('x86_64')
url='https://github.com/horsicq/DIE-engine/releases'
license=('MIT')
source=("https://github.com/horsicq/DIE-engine/releases/download/$pkgver/die_lin64_portable_$pkgver.tar.gz")
sha512sums=('57c8ace90585a9fcc762f011e8694ef82a2de30c6196c713fd464d26e422548b8330134f1506f229dfc602cd296f4812cd0bcab180771846a9b09ff2cd83347e')

package() {
  cd die_lin64_portable

  install -dm 755 "$pkgdir/usr/bin"
  install -dm 755 "$pkgdir/usr/share/$pkgname"

  cp -a --no-preserve=ownership * "$pkgdir/usr/share/$pkgname/"

  _tools="diec diel die"

  for tool in $_tools ; do
    cat > "$pkgdir/usr/bin/$tool" << EOF
#!/bin/sh
cd /usr/share/$pkgname/
exec ./$tool.sh "\$@"
EOF
    chmod 755 "$pkgdir/usr/bin/$tool"
  done
}

