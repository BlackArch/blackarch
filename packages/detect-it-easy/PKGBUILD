# This file is part of BlackArch Linux ( https://www.blackarch.org/ ).
# See COPYING for license details.

pkgname=detect-it-easy
pkgver=3.01
pkgrel=1
epoch=1
groups=('blackarch' 'blackarch-binary' 'blackarch-reversing')
pkgdesc='A program for determining types of files.'
arch=('x86_64')
url='https://github.com/horsicq/DIE-engine/releases'
license=('MIT')
source=("https://github.com/horsicq/DIE-engine/releases/download/$pkgver/die_lin64_portable_$pkgver.tar.gz")
sha512sums=('0ddb7e93ad7e94f41aae8d9345d06c8e212ecee9a3bdba1af731db7fb46d853082200617a0719ab3a13c4ff283d1890f8fd8d649edad2a3a57f025770d91411f')

package() {
  cd die_lin64_portable

  install -dm 755 "$pkgdir/usr/bin"
  install -dm 755 "$pkgdir/usr/share/$pkgname"

  cp -a --no-preserve=ownership * "$pkgdir/usr/share/$pkgname/"

  _tools="diec diel die"

  for tool in $_tools ; do
    cat > "$pkgdir/usr/bin/$tool" << EOF
#!/bin/sh
cd /usr/share/$pkgname/
exec ./$tool.sh "\$@"
EOF
    chmod 755 "$pkgdir/usr/bin/$tool"
  done
}

