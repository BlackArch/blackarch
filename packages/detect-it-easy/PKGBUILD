# This file is part of BlackArch Linux ( https://www.blackarch.org/ ).
# See COPYING for license details.

pkgname=detect-it-easy
pkgver=84.63ddcd1
pkgrel=1
groups=('blackarch' 'blackarch-binary' 'blackarch-reversing')
pkgdesc='A program for determining types of files.'
arch=('x86_64' 'armv6h' 'armv7h' 'aarch64')
url='https://github.com/horsicq/Detect-It-Easy'
license=('MIT')
depends=('qt4')
makedepends=('git' 'qt4')
source=('git+https://github.com/horsicq/DIE-engine.git'
        'build_console_lin.sh.patch')
sha512sums=('SKIP'
            '2f6e727e8511a2469b658947d1f93bb9a6954768037349feaa16a20fe3742b956b96829fffde42b3fc09f997ce193ca6bb1eb24fdfa13bf6223eb8b2b85b49a9')

pkgver() {
  cd DIE-engine

  echo $(git rev-list --count HEAD).$(git rev-parse --short HEAD)
}

prepare() {
  patch -p1 < build_console_lin.sh.patch
}

build() {
  cd DIE-engine

  bash build_console_lin.sh
}

package() {
  cd DIE-engine

  install -Dm 755 diec "$pkgdir/usr/bin/$pkgname"
  install -Dm 644 README.md "$pkgdir/usr/share/doc/$pkgname/README"
  install -Dm 644 LICENSE "$pkgdir/usr/share/licenses/$pkgname/LICENSE"
}

