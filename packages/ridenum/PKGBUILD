# This file is part of BlackArch Linux ( http://blackarch.org ).
# See COPYING for license details.

pkgname=ridenum
pkgver=68.490855c
pkgrel=2
groups=('blackarch' 'blackarch-cracker')
pkgdesc='A null session RID cycle attack for brute forcing domain controllers.'
url='https://github.com/trustedsec/ridenum'
arch=('any')
license=('custom')
depends=('python2' 'python2-pexpect')
makedepends=('git')
source=('git+https://github.com/trustedsec/ridenum.git')
sha512sums=('SKIP')

pkgver() {
  cd $pkgname

  echo $(git rev-list --count HEAD).$(git rev-parse --short HEAD)
}

prepare() {
  cd $pkgname

  sed -i 's|/usr/bin/python|/usr/bin/env python2|' ridenum.py
}

package() {
  cd $pkgname

  install -Dm 755 ridenum.py "$pkgdir"/usr/bin/ridenum

  install -Dm 644 README.md "$pkgdir"/usr/share/doc/$pkgname/README
  install -Dm 644 CHANGELOG.txt "$pkgdir"/usr/share/doc/$pkgname/CHANGELOG
  install -Dm 644 LICENSE.txt "$pkgdir"/usr/share/licenses/$pkgname/LICENSE
}
