# This file is part of BlackArch Linux ( https://www.blackarch.org/ ).
# See COPYING for license details.

pkgname=urlextractor
pkgver=19.739864d
pkgrel=2
pkgdesc='Information gathering & website reconnaissance.'
groups=('blackarch' 'blackarch-webapp' 'blackarch-recon')
arch=('any')
url='https://github.com/eschultze/URLextractor'
license=('MIT')
depends=('bash' 'bind-tools' 'whois' 'bc' 'python-tldextract')
makedepends=('git')
source=("$pkgname::git+https://github.com/eschultze/URLextractor.git")
sha512sums=('SKIP')

pkgver() {
  cd $pkgname

  echo $(git rev-list --count HEAD).$(git rev-parse --short HEAD)
}

package() {
  cd $pkgname

  install -dm 755 "$pkgdir/usr/bin"
  install -dm 755 "$pkgdir/usr/share/$pkgname"

  install -Dm 644 -t "$pkgdir/usr/share/doc/$pkgname/" README.md
  install -Dm 644 LICENSE "$pkgdir/usr/share/licenses/$pkgname/LICENSE"

  rm README.md LICENSE

  cp -a * "$pkgdir/usr/share/$pkgname/"

  cat > "$pkgdir/usr/bin/$pkgname" << EOF
#!/bin/sh
cd /usr/share/$pkgname
exec bash extractor.sh "\$@"
EOF

  chmod +x "$pkgdir/usr/bin/$pkgname"
}

