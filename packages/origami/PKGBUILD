# This file is part of BlackArch Linux ( http://blackarch.org ).
# See COPYING for license details.

pkgname='origami'
pkgver='1.2.7'
pkgrel=1
pkgdesc='Aims at providing a scripting tool to generate and analyze malicious PDF files.'
arch=('any')
groups=('blackarch' 'blackarch-malware' 'blackarch-binary')
url='http://code.google.com/p/origami-pdf'
license=('LGPL3')
depends=('ruby1.9')
options=(!emptydirs)
source=("https://rubygems.org/downloads/origami-${pkgver}.gem")
noextract=("origami-${pkgver}.gem")
sha1sums=('e34a4cf81dbb194a4ffe1939ff609e95479e53f1')

package() {
  cd "$srcdir"

  local _gemdir="$(ruby-1.9 -e'puts Gem.default_dir')"

  gem-1.9 install --ignore-dependencies --no-user-install \
    -i "$pkgdir/$_gemdir" -n "$pkgdir/usr/bin" origami-${pkgver}.gem

  rm "$pkgdir/$_gemdir/cache/origami-${pkgver}.gem"

  install -Dm644 "$pkgdir/$_gemdir/gems/origami-$pkgver/COPYING.LESSER" \
    "$pkgdir/usr/share/licenses/origami/COPYING.LESSER"
}
