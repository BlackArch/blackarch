# This file is part of BlackArch Linux ( http://blackarch.org ).
# See COPYING for license details.

pkgname=exabgp
pkgver=4203.214192cf
pkgrel=1
groups=('blackarch' 'blackarch-networking' 'blackarch-defensive')
pkgdesc='The BGP swiss army knife of networking.'
arch=('any')
url='https://github.com/Exa-Networks/exabgp'
license=('custom:unknown')
depends=('python2' 'python2-ipaddr' 'python2-nose' 'python2-psutil')
makedepends=('git' 'python2-setuptools')
backup=('etc/exabgp.conf')
source=('git+https://github.com/Exa-Networks/exabgp.git'
        'exabgp.service')
sha512sums=('SKIP'
            '225726cf424d9c2a23a2c0e9d3c7ed8c5cf03993f9518efd8e45079822b2d8cbc4d6fd18b49794beee522530478b57e5e3b268b3abd92442680cdd695504360d')

pkgver() {
  cd "$srcdir/exabgp"

  echo $(git rev-list --count HEAD).$(git rev-parse --short HEAD)
}

build() {
  cd "$srcdir/exabgp"

  python2 setup.py build
}

package() {
  cd "$srcdir/exabgp"

  python2 setup.py install --root="$pkgdir" --optimize=1

  #mv "$pkgdir/usr/etc" "$pkgdir/etc"

  install -Dm644 "$srcdir/exabgp.service" \
    "$pkgdir/usr/lib/systemd/system/exabgp.service"
  install -Dm644 /dev/null "$pkgdir/etc/exabgp.conf"
  install -Dm644 -t "$pkgdir/usr/share/doc/exabgp" CHANGELOG PEP8 README.md
  install -Dm644 COPYRIGHT "$pkgdir/usr/share/licenses/exabgp/COPYRIGHT"
}
