# This file is part of BlackArch Linux ( https://www.blackarch.org/ ).
# See COPYING for license details.

pkgbase=python-pylibemu
pkgname=('python2-pylibemu' 'python-pylibemu')
_pkgname=pylibemu
pkgver=0.7
pkgrel=1
pkgdesc='A Libemu Cython wrapper.'
arch=('x86_64' 'aarch64')
url='https://pypi.org/project/pylibemu/#files'
license=('GPL2')
makedepends=('python2-setuptools' 'python-setuptools' 'cython2' 'cython'
             'libemu' 'python2-pip' 'python-pip' 'python2-wheel' 'python-wheel')
source=("https://files.pythonhosted.org/packages/source/${_pkgname::1}/$_pkgname/$_pkgname-$pkgver.tar.gz")
sha512sums=('347cda9faec10983b438f5a8dcbd12a0170acb2ea1b3a407234da51a00ee5e15e8be8f1acd106034934e129aa52ccc66123463a99eb528df21c9bc82ae33fb17')

prepare() {
  cp -a "$_pkgname-$pkgver"{,-2}
}

build() {
  cd "$_pkgname-$pkgver-2"

  python2 setup.py build

  cd "$srcdir/$_pkgname-$pkgver"

  python setup.py build
}

package_python2-pylibemu() {
  depends=('python2' 'libemu')

  cd "$_pkgname-$pkgver-2"

  python2 setup.py install --root="$pkgdir" --prefix=/usr -O1 --skip-build
}

package_python-pylibemu() {
  depends=('python' 'libemu')

  cd "$_pkgname-$pkgver"

  python setup.py install --root="$pkgdir" --prefix=/usr -O1 --skip-build
}

