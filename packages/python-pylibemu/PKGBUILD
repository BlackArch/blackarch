# This file is part of BlackArch Linux ( https://www.blackarch.org/ ).
# See COPYING for license details.

pkgbase=python-pylibemu
pkgname=('python2-pylibemu' 'python-pylibemu')
_pkgname=pylibemu
pkgver=0.5.8
pkgrel=3
pkgdesc='A Libemu Cython wrapper.'
arch=('x86_64' 'armv6h' 'armv7h' 'aarch64')
url='https://pypi.org/project/pylibemu/#files'
license=('GPL2')
makedepends=('python2-setuptools' 'python-setuptools')
source=("https://files.pythonhosted.org/packages/1a/03/5862a8b8f77ab888b7ae78d979ed2f0e8f5d7dcee169932a96d22ef8a007/$_pkgname-$pkgver.tar.gz")
sha512sums=('11403e3771ee25bff967b65df2b2ab169c67486898d93138bcd046131e20a1c64fe37234e74d9c0db3c40a383a26b45f12066e398a06aa56ebaf435440fe2b4f')

prepare() {
  cp -a "$_pkgname-$pkgver"{,-2}
}

build() {
  cd "$_pkgname-$pkgver-2"

  python2 setup.py build

  cd "$srcdir/$_pkgname-$pkgver"

  python setup.py build
}

package_python2-pylibemu() {
  depends=('python2')

  cd "$_pkgname-$pkgver-2"

  python2 setup.py install --prefix=/usr --root="$pkgdir" --optimize=1 \
    --skip-build
}

package_python-pylibemu() {
  depends=('python')

  cd "$_pkgname-$pkgver"

  python setup.py install --prefix=/usr --root="$pkgdir" --optimize=1 \
    --skip-build
}

