# This file is part of BlackArch Linux ( https://www.blackarch.org/ ).
# See COPYING for license details.

pkgbase=python-pylibemu
pkgname=('python2-pylibemu' 'python-pylibemu')
_pkgname=pylibemu
pkgver=0.6.1
pkgrel=3
pkgdesc='A Libemu Cython wrapper.'
arch=('x86_64' 'aarch64')
url='https://pypi.org/project/pylibemu/#files'
license=('GPL2')
makedepends=('python2-setuptools' 'python-setuptools' 'cython2' 'cython'
             'libemu')
source=("https://files.pythonhosted.org/packages/ae/d9/7fabf36c7a9936692d766fc273d456f163320a7f3e8698875cb48ff1f840/$_pkgname-$pkgver.tar.gz")
sha512sums=('a2a99ec519920350ba6ed1f847530aa9841eeacdb47e06876ce35cafae3265caa5ccdb6889f6bfae4b972676db793592bdb05498a16304ddfca06085cac976f2')

prepare() {
  cp -a "$_pkgname-$pkgver"{,-2}
}

build() {
  cd "$_pkgname-$pkgver-2"

  python2 setup.py build

  cd "$srcdir/$_pkgname-$pkgver"

  python setup.py build
}

package_python2-pylibemu() {
  depends=('python2' 'libemu')

  cd "$_pkgname-$pkgver-2"

  python2 setup.py install --root="$pkgdir" --prefix=/usr -O1 --skip-build
}

package_python-pylibemu() {
  depends=('python' 'libemu')

  cd "$_pkgname-$pkgver"

  python setup.py install --root="$pkgdir" --prefix=/usr -O1 --skip-build
}

