# This file is part of BlackArch Linux ( http://blackarch.org ).
# See COPYING for license details.

pkgname='yasuo'
pkgver=98.2cb2b53
pkgrel=1
groups=('blackarch' 'blackarch-webapp' 'blackarch-scanner')
pkgdesc='A ruby script that scans for vulnerable & exploitable 3rd-party web applications on a network.'
arch=('any')
url='https://github.com/0xsauby/yasuo'
depends=('ruby' 'ruby-mechanize' 'ruby-colorize' 'ruby-net-http-persistent'
         'ruby-nmap' 'ruby-text-table')
makedepends=('git')
license=('GPL3')
source=('git+https://github.com/0xsauby/yasuo.git')
sha1sums=('SKIP')

pkgver() {
  cd "$srcdir/yasuo"

  echo $(git rev-list --count HEAD).$(git rev-parse --short HEAD)
}

package() {
  cd "$srcdir/yasuo"

  mkdir -p "$pkgdir/usr/bin"
  mkdir -p "$pkgdir/usr/share/yasuo"

  install -Dm644 README.md "$pkgdir/usr/share/doc/yasuo/README.md"
  install -Dm644 LICENSE "$pkgdir/usr/share/licenses/yasuo/LICENSE"

  rm README.md LICENSE

  cp -a * "$pkgdir/usr/share/yasuo"

  cat > "$pkgdir/usr/bin/yasuo" << EOF
#!/bin/sh
cd /usr/share/yasuo
ruby yasuo.rb "\$@"
EOF

  chmod a+x "$pkgdir/usr/bin/yasuo"
}
