# This file is part of BlackArch Linux ( https://www.blackarch.org/ ).
# See COPYING for license details.

pkgname=can-utils
pkgver=502.8027b49
pkgrel=1
pkgdesc='Linux-CAN / SocketCAN user space applications.'
groups=('blackarch' 'blackarch-automobile')
arch=('any')
url='https://github.com/linux-can/can-utils'
license=('GPL2')
makedepends=('autoconf' 'gcc' 'git' 'libtool')
source=("git+https://github.com/linux-can/$pkgname.git")
sha512sums=('SKIP')

pkgver() {
  cd $pkgname

  echo $(git rev-list --count HEAD).$(git rev-parse --short HEAD)
}

build() {
  cd $pkgname

  ./autogen.sh
  ./configure --prefix=/usr

  make
}

package() {
  cd $pkgname

  make DESTDIR="$pkgdir" install

  mv "$pkgdir/usr/bin/jcat" "$pkgdir/usr/bin/jcat-can-utils"
}

