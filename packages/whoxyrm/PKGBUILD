# This file is part of BlackArch Linux ( https://www.blackarch.org/ ).
# See COPYING for license details.

pkgname=whoxyrm
pkgver=1.0.0.r5.g77318a7
pkgrel=1
pkgdesc='A reverse whois tool based on Whoxy API.'
arch=('x86_64' 'aarch64')
groups=('blackarch' 'blackarch-recon')
url='https://github.com/MilindPurswani/whoxyrm'
license=('MIT')
depends=()
makedepends=('git' 'go')
source=("git+https://github.com/MilindPurswani/$pkgname.git")
sha512sums=('SKIP')

pkgver() {
  cd $pkgname

  git describe --long --tags | sed 's/\([^-]*-g\)/r\1/;s/-/./g'
}

build() {
  cd $pkgname

  GOPATH="$srcdir" go mod download
  GOPATH="$srcdir" go build \
    -trimpath \
    -buildmode=pie \
    -mod=readonly \
    -modcacherw \
    -ldflags "-s -w" \
    -o $pkgname .
}

package() {
  cd $pkgname

  install -Dm 755 $pkgname "$pkgdir/usr/bin/$pkgname"
  install -Dm 644 -t "$pkgdir/usr/share/doc/$pkgname/" README.md
  install -Dm 644 LICENSE "$pkgdir/usr/share/licenses/$pkgname/LICENSE"
}

