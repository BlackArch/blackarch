# This file is part of BlackArch Linux ( https://www.blackarch.org/ ).
# See COPYING for license details.

pkgbase=python-ordered-set
pkgname=('python2-ordered-set' 'python-ordered-set')
_pkgname=ordered-set
pkgver=3.1.1
pkgrel=2
pkgdesc='A MutableSet that remembers its order, so that every entry has an index.'
arch=('any')
url='https://pypi.org/project/ordered-set/#files'
license=('MIT')
makedepends=('python2-setuptools' 'python-setuptools')
source=("https://files.pythonhosted.org/packages/a3/b7/d4d69641cbe707a45c23b190f2d717466ba5accc4c70b5f7a8a450387895/$_pkgname-$pkgver.tar.gz")
sha512sums=('9a0d6c2fe1e678ebc5e4bb8229f2a97cfd0b579624f1f5e7153a9fe41937069a845271e431d626dff799136c3c90d8ecd7a7094de8b6f638c581ebe9c4e71ec6')

prepare() {
  cp -a "$_pkgname-$pkgver"{,-2}
}

build() {
  cd "$_pkgname-$pkgver-2"

  python2 setup.py build

  cd "$srcdir/$_pkgname-$pkgver"

  python setup.py build
}

package_python2-ordered-set() {
  depends=('python2')

  cd "$_pkgname-$pkgver-2"

  python2 setup.py install --prefix=/usr --root="$pkgdir" --optimize=1 \
    --skip-build
}

package_python-ordered-set() {
  depends=('python')

  cd "$_pkgname-$pkgver"

  python setup.py install --prefix=/usr --root="$pkgdir" --optimize=1 \
    --skip-build
}

