# This file is part of BlackArch Linux ( https://www.blackarch.org/ ).
# See COPYING for license details.

pkgbase=python-ordered-set
pkgname=('python2-ordered-set' 'python-ordered-set')
_pkgname=ordered-set
pkgver=4.0.1
pkgrel=1
pkgdesc='A MutableSet that remembers its order, so that every entry has an index.'
arch=('any')
url='https://pypi.org/project/ordered-set/#files'
license=('MIT')
makedepends=('python2-setuptools' 'python-setuptools')
source=("https://files.pythonhosted.org/packages/89/5d/60db38c980730fc91a0a9cf4c2b932037eecfbd6926aa2ebf450247c0096/$_pkgname-$pkgver.tar.gz")
sha512sums=('6ebe758efba7b847cce4a06001e84bf4a60728ae5d088cd7e3a2ebab86a6f5aa6a421bfda1cea0e8751bafd60eac1b3f2ec14e74a7b71f469dc7851aafe52235')

prepare() {
  cp -a "$_pkgname-$pkgver"{,-2}
}

build() {
  cd "$_pkgname-$pkgver-2"

  python2 setup.py build

  cd "$srcdir/$_pkgname-$pkgver"

  python setup.py build
}

package_python2-ordered-set() {
  depends=('python2')

  cd "$_pkgname-$pkgver-2"

  python2 setup.py install --prefix=/usr --root="$pkgdir" --optimize=1 \
    --skip-build
}

package_python-ordered-set() {
  depends=('python')

  cd "$_pkgname-$pkgver"

  python setup.py install --prefix=/usr --root="$pkgdir" --optimize=1 \
    --skip-build
}

