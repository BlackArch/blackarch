# This file is part of BlackArch Linux ( https://www.blackarch.org/ ).
# See COPYING for license details.

pkgname=usnjrnl2csv
pkgver=28.e99844d
pkgrel=1
groups=('blackarch' 'blackarch-forensic' 'blackarch-windows')
pkgdesc='Parser for $UsnJrnl on NTFS.'
arch=('x86_64')
url='https://github.com/jschicht/UsnJrnl2Csv'
license=('CCPL')
depends=('wine' 'wine_gecko' 'wine-mono')
makedepends=('git')
source=("$pkgname::git+https://github.com/jschicht/UsnJrnl2Csv.git")
sha512sums=('SKIP')

pkgver() {
  cd $pkgname

  echo $(git rev-list --count HEAD).$(git rev-parse --short HEAD)
}

package() {
  cd $pkgname

  install -dm 755 "$pkgdir/usr/bin"
  install -dm 755 "$pkgdir/usr/share/windows/$pkgname"

  install -Dm 644 -t "$pkgdir/usr/share/doc/$pkgname/" changelog.txt \
    readme*.txt
  install -Dm 644 LICENSE.md "$pkgdir/usr/share/licenses/$pkgname/LICENSE"

  rm -rf *.txt *.md
  cp -a --no-preserve=ownership * \
    "$pkgdir/usr/share/windows/$pkgname/"

  cat > "$pkgdir/usr/bin/$pkgname" << EOF
#!/bin/sh
cd /usr/share/windows/$pkgname
exec wine UsnJrnl2Csv64.exe
EOF

  chmod +x "$pkgdir/usr/bin/$pkgname"
}

