# This file is part of BlackArch Linux ( https://www.blackarch.org/ ).
# See COPYING for license details.

pkgbase=python-sphinxcontrib-websupport
pkgname=('python2-sphinxcontrib-websupport' 'python-sphinxcontrib-websupport')
_pkgname=sphinxcontrib-websupport
pkgver=1.2.4
pkgrel=2
pkgdesc='Sphinx API for Web Apps'
url='https://pypi.org/project/sphinxcontrib-websupport/#files'
arch=('any')
license=('BSD')
makedepends=('python2-setuptools' 'python-setuptools')
source=("https://files.pythonhosted.org/packages/da/aa/b03a3f569a52b6f21a579d168083a27036c1f606269e34abdf5b70fe3a2c/$_pkgname-$pkgver.tar.gz")
sha512sums=('d1ccfb6a9ecfa6adbdf2298f4ded345cacefbc5a450b8dafc6faadbcd0dd79a8dbefcab4d1cedf34c65fc271aab754392bea8ab8ae82852323784a9e1717914c')

prepare() {
  cp -a "$_pkgname-$pkgver"{,-2}
}

build() {
  cd "$_pkgname-$pkgver-2"

  python2 setup.py build

  cd "$srcdir/$_pkgname-$pkgver"

  python setup.py build
}

package_python2-sphinxcontrib-websupport() {
  depends=('python2')

  cd "$_pkgname-$pkgver-2"

  python2 setup.py install --root="$pkgdir" --prefix=/usr -O1 --skip-build
}

package_python-sphinxcontrib-websupport() {
  depends=('python')

  cd "$_pkgname-$pkgver"

  python setup.py install --root="$pkgdir" --prefix=/usr -O1 --skip-build
}

