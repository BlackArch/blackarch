# This file is part of BlackArch Linux ( http://blackarch.org ).
# See COPYING for license details.

pkgname=edb
pkgver=0.9.18
pkgrel=5
groups=('blackarch' 'blackarch-debugger' 'blackarch-reversing')
pkgdesc="A QT4-based binary mode debugger with the goal of having usability on par with OllyDbg."
arch=('i686' 'x86_64' 'armv6h' 'armv7h')
url='http://www.codef00.com/projects.php#Debugger'
license=('GPL2')
depends=('qt4>=4.5')
makedepends=('boost>=1.35.0')
install=edb.install
conflicts=('edb-debugger')
provides=('edb-debugger')
source=(http://www.codef00.com/projects/debugger-$pkgver.tgz
        edb.desktop)
sha1sums=('5eeaec13412c55b0415104bb9d0073a58a0b0918'
          'abf69c190bd2e89b0e202e9c17ec05967a4508f5')

build() {
  cd $srcdir/debugger

  qmake-qt4 -makefile DEFAULT_PLUGIN_PATH="/usr/lib/edb/"
  make
}

package() {
  cd $srcdir/debugger

  make INSTALL_ROOT=$pkgdir/usr install

  # /usr/lib64 -> /usr/lib on x86_64
  [[ "$CARCH" = "x86_64" ]] && (mv $pkgdir/usr/lib64 $pkgdir/usr/lib)

  # Install icons.
  install -D -m 644 src/images/edb48-logo.png $pkgdir/usr/share/pixmaps/edb.png
  install -D -m 644 src/images/edb48-logo.png $pkgdir/usr/share/icons/hicolor/48x48/apps/edb.png

  # Install desktop file.
  cd ..
  install -D -m 644 edb.desktop $pkgdir/usr/share/applications/edb.desktop
}
