# This file is part of BlackArch Linux ( http://blackarch.org ).
# See COPYING for license details.
pkgname='ruby-bettercap'
_gemname='bettercap'
pkgver=1011.e69cb78
_gemver=1.6.3b
pkgrel=2
pkgdesc="A complete, modular, portable and easily extensible MITM framework."
arch=('i686' 'x86_64' 'armv6h' 'armv7h' 'aarch64')
url='https://github.com/evilsocket/bettercap'
license=("GPL-3.0")
depends=('libpcap' 'ruby' 'ruby-colorize' 'ruby-em-proxy' 'ruby-net-dns' 'ruby-network_interface' 'ruby-packetfu-1.1.11' 'ruby-pcaprub' 'ruby-rubydns-1')
makedepends=('git')
groups=('blackarch' 'blackarch-sniffer' 'blackarch-exploitation'
        'blackarch-networking')
replaces=('bettercap')
conflicts=('bettercap')
provides=('bettercap')
options=('!emptydirs')
source=('git+https://github.com/evilsocket/bettercap.git'
        'packetfu.patch')
sha1sums=('SKIP'
          '306db877254cbe7624b67687cb6d903e34f2f447')

pkgver() {
  cd "$srcdir/$_gemname"

  echo $(git rev-list --count HEAD).$(git rev-parse --short HEAD)
}

build() {
  cd "$srcdir/$_gemname"

  gem build $_gemname.gemspec
}

package() {
  cd "$srcdir/$_gemname"
  local _gemdir="$(ruby -rubygems -e 'puts Gem.default_dir')"
  gem install --ignore-dependencies --no-document --no-user-install -i "$pkgdir/$_gemdir" -n "$pkgdir"/usr/bin $_gemname-$_gemver.gem
  rm "$pkgdir/$_gemdir/cache/$_gemname-$_gemver.gem"
  install -D -m644 "$pkgdir/$_gemdir/gems/$_gemname-$_gemver/LICENSE.md" "$pkgdir/usr/share/licenses/$pkgname/LICENSE.md"

  rm -rf "$pkgdir/$_gemdir/gems/$_gemname-$_gemver"/{LICENSE.md,README.md}
  rm -rf "$pkgdir/$_gemdir/gems/$_gemname-$_gemver"/{.gitignore,.vimrun,README.md,Rakefile,bettercap.gemspec}
}
