# This file is part of BlackArch Linux ( http://blackarch.org ).
# See COPYING for license details.

pkgname='omnihash'
pkgver=70.870e9ae
pkgrel=3
pkgdesc='Hash files, strings, input streams and network resources in various common algorithms simultaneously.'
groups=('blackarch' 'blackarch-crypto')
arch=('any')
url='https://github.com/Miserlou/omnihash'
license=('MIT')
depends=('python2' 'python2-requests' 'python2-crcmod' 'python2-click'
         'python2-colorama' 'python2-validators' 'python2-pyblake2'
         'python2-sha3')
makedepends=('git' 'python2-setuptools')
source=('git+https://github.com/Miserlou/omnihash.git')
sha1sums=('SKIP')

pkgver() {
  cd "$srcdir/omnihash"

  echo $(git rev-list --count HEAD).$(git rev-parse --short HEAD)
}

prepare() {
  cd "$srcdir/omnihash"

  sed -i 's/==.*//g' requirements.txt
}

build() {
  cd "$srcdir/omnihash"

  python2 setup.py build
}

package() {
  cd "$srcdir/omnihash"

  python2 setup.py install --root="$pkgdir"

  install -Dm644 README.md "$pkgdir/usr/share/doc/omnihash/README.md"
  install -Dm644 LICENSE "$pkgdir/usr/share/licenses/omnihash/LICENSE"

  rm "$pkgdir/usr/bin/oh"
}
