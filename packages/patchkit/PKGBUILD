# This file is part of BlackArch Linux ( http://blackarch.org ).
# See COPYING for license details.

pkgname='patchkit'
pkgver=36.5fe79b7
pkgrel=1
pkgdesc='Powerful binary patching from Python.'
groups=('blackarch' 'blackarch-binary' 'blackarch-backdoor')
arch=('any')
url='https://github.com/lunixbochs/patchkit'
license=('BSD' 'custom:unknown')
depends=('python2' 'python2-coding' 'python2-capstone' 'python2-keystone'
         'unicorn')
makedepends=('git')
source=('git+https://github.com/lunixbochs/patchkit')
sha1sums=('SKIP')

pkgver() {
  cd "$srcdir/patchkit"

  echo $(git rev-list --count HEAD).$(git rev-parse --short HEAD)
}

package() {
  cd "$srcdir/patchkit"

  mkdir -p "$pkgdir/usr/bin"
  mkdir -p "$pkgdir/usr/share/patchkit"

  install -Dm644 README.md "$pkgdir/usr/share/doc/patchkit/README.md"
  install -Dm644 LICENSE "$pkgdir/usr/share/licenses/patchkit/LICENSE"

  rm README.md LICENSE

  cp -a * "$pkgdir/usr/share/patchkit"

  cat > "$pkgdir/usr/bin/patchkit" << EOF
#!/bin/sh
cd /usr/share/patchkit
exec python2 patch "\${@}"
EOF

  chmod +x "$pkgdir/usr/bin/patchkit"
}
