# This file is part of BlackArch Linux ( http://blackarch.org ).
# See COPYING for license details.

pkgname='rr'
pkgver='5.0.0'
pkgrel=1
groups=('blackarch' 'blackarch-debugger' 'blackarch-misc')
pkgdesc='A Record and Replay Framework.'
arch=('x86_64' 'armv6h' 'armv7h' 'aarch64')
url='https://github.com/mozilla/rr'
license=('custom')
depends=('libpfm4' 'zlib' 'python2-pexpect' 'man-pages')
makedepends=('cmake' 'gdb')
source=("https://github.com/mozilla/rr/archive/${pkgver}.tar.gz")
sha1sums=('80b641b18f2cbb7b0af51e21127eb6e3a55185fa')

build(){
  cd "$srcdir/rr-$pkgver"

  export CFLAGS="$CFLAGS -fPIC"

  mkdir build

  cd build

  cmake -Ddisable32bit=ON -DCMAKE_VERBOSE_MAKEFILE=ON \
    -DCMAKE_INSTALL_PREFIX=/usr ..

  make
}


package() {
  cd "$srcdir/rr-$pkgver"

  cd build

  make DESTDIR=$pkgdir install

  install -Dm644 ../LICENSE "$pkgdir/usr/share/licenses/rr/LICENSE"
}
