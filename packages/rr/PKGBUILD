# This file is part of BlackArch Linux ( https://www.blackarch.org/ ).
# See COPYING for license details.

pkgname=rr
pkgver=5.2.0
pkgrel=2
groups=('blackarch' 'blackarch-debugger' 'blackarch-misc')
pkgdesc='A Record and Replay Framework.'
arch=('x86_64' 'aarch64')
url='https://github.com/mozilla/rr'
license=('custom')
depends=('libpfm4' 'zlib' 'python2-pexpect' 'man-pages' 'libpfm4'
         'gcc-libs' 'bash' 'lib32-zlib' 'python2' 'gcc' 'libpcap')
makedepends=('cmake' 'gdb' 'capnproto')
source=("https://github.com/mozilla/rr/archive/$pkgver.tar.gz")
sha512sums=('4465985f67e9812b62b91ac7942105a9f976faf0c0d7c352a99a638fe6bdfde024f939b3daa922d431c7fc87118ae4d9d039b00345760fb547c349e8737dc168')

build(){
  cd "$pkgname-$pkgver"

  export CFLAGS="$CFLAGS -fPIC"

  mkdir -p build

  cd build

  cmake -Ddisable32bit=ON -DCMAKE_VERBOSE_MAKEFILE=ON \
    -DCMAKE_BUILD_TYPE=Release -DCMAKE_INSTALL_PREFIX=/usr \
    -DPYTHON_EXECUTABLE=/usr/bin/python2 ..

  make
}


package() {
  cd "$pkgname-$pkgver"

  cd build

  make DESTDIR="$pkgdir" install

  install -Dm 644 ../LICENSE "$pkgdir/usr/share/licenses/$pkgname/LICENSE"
}

