# This file is part of BlackArch Linux ( https://www.blackarch.org/ ).
# See COPYING for license details.

pkgname=mallory
pkgver=134.47094fb
pkgrel=2
groups=('blackarch' 'blackarch-proxy' 'blackarch-networking')
pkgdesc='HTTP/HTTPS proxy over SSH.'
arch=('x86_64' 'aarch64')
url='https://github.com/justmao945/mallory'
license=('MIT')
makedepends=('git' 'go')
source=("git+https://github.com/justmao945/$pkgname.git")
sha512sums=('SKIP')

pkgver() {
  cd $pkgname

  echo $(git rev-list --count HEAD).$(git rev-parse --short HEAD)
}

build() {
  cd $pkgname

  GOPATH="$srcdir" go mod init go.mod
  GOPATH="$srcdir" go mod tidy
  GOPATH="$srcdir" go mod download
  GOPATH="$srcdir" go build \
    -trimpath \
    -buildmode=pie \
    -mod=readonly \
    -modcacherw \
    -ldflags "-s -w" \
    -o $pkgname ./cmd/$pkgname
}

package() {
  cd $pkgname

  install -Dm 755 $pkgname "$pkgdir/usr/bin/$pkgname"
  install -Dm 644 mallory.json mallory.service -t "$pkgdir/usr/share/$pkgname"
  install -Dm 644 README.md -t "$pkgdir/usr/share/doc/$pkgname"
}

