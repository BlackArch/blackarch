# This file is part of BlackArch Linux ( https://www.blackarch.org/ ).
# See COPYING for license details.

pkgname=bof-detector
pkgver=19.e08367d
pkgrel=2
pkgdesc='A simple detector of BOF vulnerabilities by source-code-level check.'
groups=('blackarch' 'blackarch-code-audit')
arch=('any')
url='https://github.com/st9140927/BOF_Detector'
license=('custom:unknown')
depends=('python2' 'python2-lxml')
makedepends=('git')
source=("$pkgname::git+https://github.com/st9140927/BOF_Detector.git")
sha512sums=('SKIP')

pkgver() {
  cd $pkgname

  echo $(git rev-list --count HEAD).$(git rev-parse --short HEAD)
}

package() {
  cd $pkgname

  install -dm 755 "$pkgdir/usr/bin"
  install -dm 755 "$pkgdir/usr/share/$pkgname"

  install -Dm 644 -t "$pkgdir/usr/share/doc/$pkgname/" README.md

  rm README.md

  cp -a * "$pkgdir/usr/share/$pkgname/"

  cat > "$pkgdir/usr/bin/$pkgname" << EOF
#!/bin/sh
cd /usr/share/$pkgname/execution/BOF_analyzer
exec python2 main.py "\$@"
EOF

  chmod +x "$pkgdir/usr/bin/$pkgname"
}

