# This file is part of BlackArch Linux ( https://www.blackarch.org/ ).
# See COPYING for license details.

pkgname=libosmocore
pkgver=0.12.1
pkgrel=1
pkgdesc='Collection of common code used in various sub-projects inside the Osmocom family of projects.'
arch=('x86_64' 'aarch64')
groups=('blackarch' 'blackarch-radio')
url='https://osmocom.org/projects/libosmocore/wiki/Libosmocore'
license=('GPL')
optdepends=('pcsclite')
makedepends=('git' 'talloc' 'pcsclite')
source=("git://git.osmocom.org/libosmocore.git#tag=$pkgver")
sha512sums=('SKIP')

prepare() {
  cd $pkgname

  autoreconf -i
  ./configure --prefix=/usr --exec-prefix=/usr --bindir=/usr/bin \
              --sbindir=/usr/sbin --datadir=/usr/share --libexecdir=/usr/lib \
              --localstatedir=/var --docdir=/usr/share/doc/libosmocore \
              --libdir=/usr/lib/
}

build() {
  cd $pkgname

  make
}

package() {
  cd $pkgname

  make DESTDIR="$pkgdir" install
}

