# This file is part of BlackArch Linux ( https://www.blackarch.org/ ).
# See COPYING for license details.

pkgname=dynamorio
pkgver=8.0.18377
pkgrel=1
groups=('blackarch' 'blackarch-binary' 'blackarch-reversing')
pkgdesc='A dynamic binary instrumentation framework.'
url='https://github.com/DynamoRIO/dynamorio'
arch=('x86_64')
license=('BSD')
source=("https://github.com/DynamoRIO/dynamorio/releases/download/cronbuild-$pkgver/DynamoRIO-Linux-$pkgver.tar.gz")
sha512sums=('cd37681f15e5125d6f6bb6a775bc6199d3428e0b5464ea582da081026847ced6302995d6b8a8418a2cfcdc8580dfa3ba9692989cac3ccac9922feed388b1177d')

prepare() {
  cd "$srcdir/DynamoRIO-Linux-$pkgver"

  rm -rf bin32/ lib32/ bin64/*.debug cmake/
}

package() {
  cd "$srcdir/DynamoRIO-Linux-$pkgver"

  install -dm 755 "$pkgdir/usr/bin"
  install -dm 755 "$pkgdir/usr/share/$pkgname"

  install -Dm 644 License.txt "$pkgdir/usr/share/licenses/$pkgname/LICENSE"
  cp -a --no-preserve=ownership * "$pkgdir/usr/share/$pkgname"

  _TO_LINK='drconfig drinject drloader drrun nudgeunix'

  for executable in $_TO_LINK; do
    ln -sf /usr/share/$pkgname/bin64/$executable "$pkgdir/usr/bin/$executable"
  done
}

