# This file is part of BlackArch Linux ( https://www.blackarch.org/ ).
# See COPYING for license details.

pkgname=dynamorio
_pkgver=7.1.0-1
pkgver=7.1.0
pkgrel=3
groups=('blackarch' 'blackarch-binary' 'blackarch-reversing')
pkgdesc='A dynamic binary instrumentation framework.'
url='https://github.com/DynamoRIO/dynamorio'
arch=('x86_64')
license=('BSD')
source=("https://github.com/DynamoRIO/dynamorio/releases/download/release_$pkgver/DynamoRIO-Linux-$_pkgver.tar.gz")
sha512sums=('410ee321a264e463e5863460959343a5cde90d20cb2363842cf4b90b2bd8b4cbef3d96109c54dbb0a6b2e3b5dee029e2212495764c2c38a58ba6d7f0774aaf94')

prepare() {
  cd "$srcdir/DynamoRIO-Linux-$_pkgver"

  rm -rf bin32/ lib32/ bin64/*.debug cmake/
}

package() {
  cd "$srcdir/DynamoRIO-Linux-$_pkgver"

  install -dm 755 "$pkgdir/usr/bin"
  install -dm 755 "$pkgdir/usr/share/$pkgname"

  install -Dm 644 License.txt "$pkgdir/usr/share/licenses/$pkgname/LICENSE"
  cp -a --no-preserve=ownership * "$pkgdir/usr/share/$pkgname"

  _TO_LINK='drconfig drinject drloader drrun nudgeunix'

  for executable in $_TO_LINK; do
    ln -sf /usr/share/$pkgname/bin64/$executable "$pkgdir/usr/bin/$executable"
  done
}

