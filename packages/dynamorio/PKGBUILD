# This file is part of BlackArch Linux ( https://www.blackarch.org/ ).
# See COPYING for license details.

pkgname=dynamorio
pkgver=8.0.18796
pkgrel=1
groups=('blackarch' 'blackarch-binary' 'blackarch-reversing')
pkgdesc='A dynamic binary instrumentation framework.'
url='https://github.com/DynamoRIO/dynamorio'
arch=('x86_64')
license=('BSD')
source=("https://github.com/DynamoRIO/dynamorio/releases/download/cronbuild-$pkgver/DynamoRIO-Linux-$pkgver.tar.gz")
sha512sums=('6101bf675b189cdc2a268159e1e2d62babe3385de9e87312475aef3061339e0757aaf8bd63e8a6b8e49c9d778f65e44b2acd37d3e51e30c407871f3a5b8854a3')

prepare() {
  cd "$srcdir/DynamoRIO-Linux-$pkgver"

  rm -rf bin32/ lib32/ bin64/*.debug cmake/
}

package() {
  cd "$srcdir/DynamoRIO-Linux-$pkgver"

  install -dm 755 "$pkgdir/usr/bin"
  install -dm 755 "$pkgdir/usr/share/$pkgname"

  install -Dm 644 License.txt "$pkgdir/usr/share/licenses/$pkgname/LICENSE"
  cp -a --no-preserve=ownership * "$pkgdir/usr/share/$pkgname"

  _TO_LINK='drconfig drinject drloader drrun nudgeunix'

  for executable in $_TO_LINK; do
    ln -sf /usr/share/$pkgname/bin64/$executable "$pkgdir/usr/bin/$executable"
  done
}

