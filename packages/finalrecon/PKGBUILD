# This file is part of BlackArch Linux ( https://www.blackarch.org/ ).
# See COPYING for license details.

pkgname=finalrecon
pkgver=v1.1.7.r16.gac4681c
pkgrel=1
epoch=1
pkgdesc='OSINT Tool for All-In-One Web Reconnaissance.'
arch=('any')
groups=('blackarch' 'blackarch-recon')
url='https://github.com/thewhiteh4t/FinalRecon'
license=('MIT')
depends=('python' 'python-requests' 'python-ipwhois' 'python-beautifulsoup4'
         'python-lxml' 'python-icmplib' 'python-tldextract' 'python-dnslib'
         'python-aiohttp' 'python-psycopg2' 'python-aiodns')
makedepends=('git')
source=("$pkgname::git+https://github.com/thewhiteh4t/FinalRecon.git")
sha512sums=('SKIP')

pkgver() {
  cd $pkgname

  ( set -o pipefail
    git describe --long --tags --abbrev=7 2>/dev/null |
      sed 's/\([^-]*-g\)/r\1/;s/-/./g' ||
    printf "%s.%s" "$(git rev-list --count HEAD)" \
      "$(git rev-parse --short=7 HEAD)"
  )
}

package() {
  cd $pkgname

  install -dm 755 "$pkgdir/usr/bin"
  install -dm 755 "$pkgdir/usr/share/$pkgname"

  install -Dm 644 -t "$pkgdir/usr/share/doc/$pkgname/" README.md
  install -Dm 644 -t "$pkgdir/usr/share/licenses/$pkgname/" LICENSE

  rm README.md LICENSE

  cp -a --no-preserve=ownership * "$pkgdir/usr/share/$pkgname/"

  cat > "$pkgdir/usr/bin/$pkgname" << EOF
#!/bin/sh
cd /usr/share/$pkgname
exec python $pkgname.py "\$@"
EOF

  chmod +x "$pkgdir/usr/bin/$pkgname"
}

