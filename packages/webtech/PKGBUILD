# This file is part of BlackArch Linux ( https://www.blackarch.org/ ).
# See COPYING for license details.

pkgbase=webtech
pkgname=('webtech' 'python2-webtech')
pkgver=1.2.7
pkgrel=5
pkgdesc='Identify technologies used on websites.'
arch=('any')
groups=('blackarch' 'blackarch-webapp' 'blackarch-recon' 'blackarch-scanner'
        'blackarch-fingerprint')
url='https://pypi.org/project/webtech/#files'
license=('GPL')
makedepends=('python2-setuptools' 'python-setuptools')
source=("https://files.pythonhosted.org/packages/a7/66/3bd231369ca661e76fa863546c2d7d8c73fd214fc018dcee37ff32a368d8/$pkgname-$pkgver.tar.gz")
sha512sums=('1ccd8d0e6fdb2764d8681d219f68d0d5e54ab122de0c2ee3c7523303bdabef12618b683bdf3b6117bb1c8b66dd5c05f147eaa5db5fbeaec6a5815172a2d423ea')
install="$pkgname.install"

prepare() {
  cp -a "$pkgbase-$pkgver"{,-2}
}

build() {
  cd "$pkgbase-$pkgver-2"

  python2 setup.py build

  cd "$srcdir/$pkgbase-$pkgver"

  python setup.py build
}

package_python2-webtech() {
  depends=('python2' 'python2-requests')

  cd "$pkgbase-$pkgver-2"

  python2 setup.py install --root="$pkgdir" --prefix=/usr -O1 --skip-build

  install -Dm 644 -t "$pkgdir/usr/share/doc/${pkgname[1]}/" README.md

  mv "$pkgdir/usr/bin/$pkgbase" "$pkgdir/usr/bin/${pkgbase}2"
}

package_webtech() {
  depends=('python' 'python-requests')

  cd "$pkgbase-$pkgver"

  python setup.py install --root="$pkgdir" --prefix=/usr -O1 --skip-build

  install -Dm 644 -t "$pkgdir/usr/share/doc/$pkgbase/" README.md
}

