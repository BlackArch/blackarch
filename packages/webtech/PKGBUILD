# This file is part of BlackArch Linux ( https://www.blackarch.org/ ).
# See COPYING for license details.

pkgbase=webtech
pkgname=('webtech' 'python2-webtech')
pkgver=1.2.8
pkgrel=2
pkgdesc='Identify technologies used on websites.'
arch=('any')
groups=('blackarch' 'blackarch-webapp' 'blackarch-recon' 'blackarch-scanner'
        'blackarch-fingerprint')
url='https://pypi.org/project/webtech/#files'
license=('GPL')
makedepends=('python2-setuptools' 'python-setuptools')
source=("https://files.pythonhosted.org/packages/76/eb/2066535250c39460b151865b35f62ca7d96f2adf0c99ae4a5d6e83f45f3c/$pkgbase-$pkgver.tar.gz")
sha512sums=('43ab291cb38aedf09b20882e92992388eb97bb89cbb081f59a789a0fe100476a7273445cc9bba05d11940b53e3db105021050b4c3b58301e612f21026b7533de')
install="$pkgname.install"

prepare() {
  cp -a "$pkgbase-$pkgver"{,-2}
}

build() {
  cd "$pkgbase-$pkgver-2"

  python2 setup.py build

  cd "$srcdir/$pkgbase-$pkgver"

  python setup.py build
}

package_python2-webtech() {
  depends=('python2' 'python2-requests')

  cd "$pkgbase-$pkgver-2"

  python2 setup.py install --root="$pkgdir" --prefix=/usr -O1 --skip-build

  install -Dm 644 -t "$pkgdir/usr/share/doc/${pkgname[1]}/" README.md

  mv "$pkgdir/usr/bin/$pkgbase" "$pkgdir/usr/bin/${pkgbase}2"
}

package_webtech() {
  depends=('python' 'python-requests')

  cd "$pkgbase-$pkgver"

  python setup.py install --root="$pkgdir" --prefix=/usr -O1 --skip-build

  install -Dm 644 -t "$pkgdir/usr/share/doc/$pkgbase/" README.md
}

