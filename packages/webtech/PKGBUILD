# This file is part of BlackArch Linux ( https://www.blackarch.org/ ).
# See COPYING for license details.

pkgbase=webtech
pkgname=('webtech' 'python2-webtech')
pkgver=1.2.12
pkgrel=2
pkgdesc='Identify technologies used on websites.'
arch=('any')
groups=('blackarch' 'blackarch-webapp' 'blackarch-recon' 'blackarch-scanner'
        'blackarch-fingerprint')
url='https://pypi.org/project/webtech/#files'
license=('GPL')
makedepends=('python2-setuptools' 'python-setuptools')
source=("https://files.pythonhosted.org/packages/66/64/3baa0b24c4f6a686fd74b957ca77403dd56cc62ed5dd57c7f9c1f5b2df37/$pkgbase-$pkgver.tar.gz")
sha512sums=('1eb19117148476d6146d0a16d234c15d8bbc18e0b7a8d490a8ba43cbe21f664ef47f873e760c63c254aafc3b5331c92f28225c2701f483d058cd5ee92c053835')
install="$pkgname.install"

prepare() {
  cp -a "$pkgbase-$pkgver"{,-2}
}

build() {
  cd "$pkgbase-$pkgver-2"

  python2 setup.py build

  cd "$srcdir/$pkgbase-$pkgver"

  python setup.py build
}

package_python2-webtech() {
  depends=('python2' 'python2-requests')

  cd "$pkgbase-$pkgver-2"

  python2 setup.py install --root="$pkgdir" --prefix=/usr -O1 --skip-build

  install -Dm 644 -t "$pkgdir/usr/share/doc/${pkgname[1]}/" README.md

  mv "$pkgdir/usr/bin/$pkgbase" "$pkgdir/usr/bin/${pkgbase}2"
}

package_webtech() {
  depends=('python' 'python-requests')

  cd "$pkgbase-$pkgver"

  python setup.py install --root="$pkgdir" --prefix=/usr -O1 --skip-build

  install -Dm 644 -t "$pkgdir/usr/share/doc/$pkgbase/" README.md
}

