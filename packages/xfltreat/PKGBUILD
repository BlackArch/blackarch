# This file is part of BlackArch Linux ( http://blackarch.org ).
# See COPYING for license details.

pkgname=xfltreat
pkgver=257.80c3d03
pkgrel=1
pkgdesc='Tunnelling framework.'
groups=('blackarch' 'blackarch-tunnel' 'blackarch-networking')
arch=('any')
url='https://github.com/earthquake/xfltreat/'
license=('custom:unknown')
depends=('python2' 'python2-pyroute2')
makedepends=('git')
source=('git+https://github.com/earthquake/xfltreat/')
sha512sums=('SKIP')

pkgver() {
  cd $pkgname

  echo $(git rev-list --count HEAD).$(git rev-parse --short HEAD)
}

package() {
  cd $pkgname

  install -dm 755 "$pkgdir/usr/bin"
  install -dm 755 "$pkgdir/usr/share/$pkgname"

  install -Dm 644 -t "$pkgdir/usr/share/doc/$pkgname/" README.md
  cp -r docs "$pkgdir/usr/share/doc/$pkgname/docs"

  rm -rf README.md docs

  cp -a * "$pkgdir/usr/share/$pkgname/"

  cat > "$pkgdir/usr/bin/$pkgname" << EOF
#!/bin/sh
cd /usr/share/$pkgname
exec python2 $pkgname.py "\$@"
EOF

  chmod a+x "$pkgdir/usr/bin/$pkgname"
}

