# Maintainer: Yardena Cohen <yardenack at gmail dot com>
# Contributor: Max Roder <maxroder@web.de>
# Contributor: Sebastian Jug <seb AT stianj DOT ug>
# Contributor: midgard <arch dot midgard "at symbol" janmaes "youknowwhat" com>

# To port this PKGBUILD to another language of tor-browser you
# have to change $pkgname, $_language, $pkgdesc and $url in PKGBUILD
# AND (!) the first line in the .install file!

pkgname='tor-browser-en'
pkgver='8.0.2'
_language='en-US'
pkgrel=1
pkgdesc='Tor Browser Bundle: anonymous browsing using Firefox and Tor'
url='https://www.torproject.org/projects/torbrowser.html.en'
arch=('x86_64')
groups=('blackarch' 'blackarch-defensive')
license=('GPL')
depends=('gtk2' 'mozilla-common' 'libxt' 'startup-notification' 'mime-types'
         'dbus-glib' 'alsa-lib' 'desktop-file-utils' 'hicolor-icon-theme'
         'libvpx' 'icu' 'libevent' 'nss' 'hunspell' 'sqlite')
optdepends=('zenity: simple dialog boxes'
            'kdialog: KDE dialog boxes'
            'gst-plugins-good: H.264 video'
            'gst-libav: H.264 video'
            'libpulse: PulseAudio audio driver'
            'libnotify: Gnome dialog boxes')
install="${pkgname}.install"
validpgpkeys=('EF6E286DDA85EA2A4BA7DE684E2C6E8793298290')
source=("https://dist.torproject.org/torbrowser/${pkgver}/tor-browser-linux64-${pkgver}_${_language}.tar.xz"{,.asc}
        "${pkgname}.desktop"
        "${pkgname}.png"
        "${pkgname}.sh")
md5sums=('aa40dd5b9e31be2456f785fca3346514'
         'SKIP'
         '69c75e328c117249b30a757f60b634bf'
         '7cd059a3b8194745d7d458a628ccc43a'
         '8fdd23d758c49ba83eba1b01d1934924')
sha256sums=('7f27ea36a6275d3a7ba5cc65b72f1793606bb7a450236c17b8708d422dbc52ed'
            'SKIP'
            '5a3c8dc36f46b22a2bc45de6209d5efa14898e24d968cd08a7e0e767ea3259bd'
            'bb6b0f27c33d21e0ef6df961e25418327c5e8b01c003bbe18c0a8dae3e16d77d'
            'b51ca669753409c647ba2582b3eb866b409eca61c216f879ed3c1aded8463526')
sha512sums=('6cccd91fe3137f0d5fd1c71c0857c7258415a2b078277fd90b5dcdd3cce8e8bcd536a1f3e5fb7f03119cac37d47e1e8f0e739566d5c8144306075ec46b260a18'
            'SKIP'
            '1318a652f7b65e30cdb0c607faf4391035288bcfcaffc50f7713de8eeb7d9151c115269fad1225fccd71d4d38537a804f05fba03b3df516afbce8a79d8988d7c'
            '89d971f4f0db7d1b50e090040ddce0f7191d511f4c1cae9d2c3364fda40a43b7f506d0c015ddd1485304fadbc0bf7d5d61d1dbc12bf645a7e615f51c1d1261e0'
            '88c5d3035df46aa23cd98657e19b907a68ac233f92307556ea44b6a90bac2d04dfb5b23c7efaa3addd48a5def3daacf0c8731acb59b15a7390c4c3435427674a')
noextract=("tor-browser-linux64-${pkgver}_${_language}.tar.xz")

prepare() {
   # We search and replace using sed with / as delimiter below so we can't allow slashes in these vars.
   # Makepkg already enforces that there're no slashes in $pkgname, so we don't check that again here.
   if [[ $pkgver = */* || $_language = */* || $pkgdesc = */* ]]; then
      error '$pkgver, $_language and $pkgdesc for this package are not allowed to contain /' >&2
      return 1
   fi
}

package() {
   cd "${srcdir}"

   sed -i "s/REPL_NAME/${pkgname}/g"       "${pkgname}.sh"
   sed -i "s/REPL_VERSION/${pkgver}/g"     "${pkgname}.sh"
   sed -i "s/REPL_LANGUAGE/${_language}/g" "${pkgname}.sh"

   sed -i "s/REPL_NAME/${pkgname}/g"       "${pkgname}.desktop"
   sed -i "s/REPL_LANGUAGE/${_language}/g" "${pkgname}.desktop"
   sed -i "s/REPL_COMMENT/${pkgdesc}/g"    "${pkgname}.desktop"

   install -Dm 644 "${pkgname}.desktop"    "${pkgdir}/usr/share/applications/${pkgname}.desktop"
   install -Dm 644 "${pkgname}.png"        "${pkgdir}/usr/share/pixmaps/${pkgname}.png"
   install -Dm 755 "${pkgname}.sh"         "${pkgdir}/usr/bin/${pkgname}"

   install -Dm 644 "tor-browser-linux64-${pkgver}_${_language}.tar.xz" "${pkgdir}/opt/${pkgname}/tor-browser-linux64-${pkgver}_${_language}.tar.xz"
}

# vim: set et ts=3 sw=3 :
