# This file is part of BlackArch Linux ( https://www.blackarch.org/ ).
# See COPYING for license details.

pkgname=tor-browser-en
pkgver=10.0.7
pkgrel=1
pkgdesc='Tor Browser Bundle: anonymous browsing using Firefox and Tor.'
url='https://www.torproject.org/projects/torbrowser.html.en'
arch=('x86_64')
groups=('blackarch' 'blackarch-defensive')
license=('GPL')
depends=('mozilla-common' 'libxt' 'startup-notification' 'mime-types'
         'dbus-glib' 'alsa-lib' 'desktop-file-utils' 'hicolor-icon-theme'
         'libvpx' 'icu' 'libevent' 'nss' 'hunspell' 'sqlite')
optdepends=('kdialog: KDE dialog boxes'
            'gst-plugins-good: H.264 video'
            'gst-libav: H.264 video'
            'libpulse: PulseAudio audio driver'
            'libnotify: Gnome dialog boxes')
source=("https://dist.torproject.org/torbrowser/$pkgver/tor-browser-linux64-${pkgver}_en-US.tar.xz"{,.asc}
        "$pkgname.desktop"
        "$pkgname.png"
        "$pkgname.sh")
sha512sums=('fb51e096dff18b5ace323d58ae4f18586de9a7e2caaa7819f0e6fc399815d12d92752b001273df5f6a05664a67832adc2f7131e67ad7d7371c0fc0f3c4c2d5c6'
            'SKIP'
            '1318a652f7b65e30cdb0c607faf4391035288bcfcaffc50f7713de8eeb7d9151c115269fad1225fccd71d4d38537a804f05fba03b3df516afbce8a79d8988d7c'
            '89d971f4f0db7d1b50e090040ddce0f7191d511f4c1cae9d2c3364fda40a43b7f506d0c015ddd1485304fadbc0bf7d5d61d1dbc12bf645a7e615f51c1d1261e0'
            '88c5d3035df46aa23cd98657e19b907a68ac233f92307556ea44b6a90bac2d04dfb5b23c7efaa3addd48a5def3daacf0c8731acb59b15a7390c4c3435427674a')
noextract=("tor-browser-linux64-${pkgver}_en-US.tar.xz")
install="$pkgname.install"
validpgpkeys=('EF6E286DDA85EA2A4BA7DE684E2C6E8793298290')

prepare() {
  sed -i "s/REPL_NAME/$pkgname/g" "$pkgname.sh"
  sed -i "s/REPL_VERSION/$pkgver/g" "$pkgname.sh"
  sed -i "s/REPL_LANGUAGE/en-US/g" "$pkgname.sh"
  sed -i "s/REPL_NAME/$pkgname/g" "$pkgname.desktop"
  sed -i "s/REPL_LANGUAGE/en-US/g" "$pkgname.desktop"
  sed -i "s/REPL_COMMENT/$pkgdesc/g" "$pkgname.desktop"
}

package() {
  install -Dm 755 "$pkgname.sh" "$pkgdir/usr/bin/$pkgname"
  install -Dm 644 "tor-browser-linux64-${pkgver}_en-US.tar.xz" \
    "$pkgdir/opt/$pkgname/tor-browser-linux64-${pkgver}_en-US.tar.xz"
  install -Dm 644 "$pkgname.desktop" \
    "$pkgdir/usr/share/applications/$pkgname.desktop"
  install -Dm 644 "$pkgname.png" "$pkgdir/usr/share/pixmaps/$pkgname.png"
}

