# This file is part of BlackArch Linux ( http://blackarch.org ).
# See COPYING for license details.

pkgname='tor-browser-en'
pkgver='4.0.4'
_realpkgver="$pkgver"
_realpkgver_i686="$pkgver"
_language='en-US'
pkgrel=1
pkgdesc='Tor Browser Bundle: Anonymous browsing using firefox and tor.'
url='https://www.torproject.org/projects/torbrowser.html.en'
arch=('x86_64' 'i686')
license=('GPL')
depends=('gtk2' 'mozilla-common' 'libxt' 'startup-notification' 'mime-types'
         'dbus-glib' 'alsa-lib' 'desktop-file-utils' 'hicolor-icon-theme'
         'libvpx' 'icu' 'libevent' 'nss' 'hunspell' 'sqlite')
optdepends=('zenity: simple dialog boxes'
			      'kdebase-kdialog: KDE dialog boxes'
            'gst-plugins-good: h.264 video'
            'gst-libav: h.264 video'
            'libpulse: PulseAudio audio driver'
			      'libnotify: Gnome dialog boxes')
install='tor-browser-en.install'

if [[ "$CARCH" == 'i686' ]]; then
	_realpkgver=${_realpkgver_i686}
	_pkgarch='32'
else
	_pkgarch='64'
fi

validpgpkeys=('8738A680B84B3031A630F2DB416F061063FEE659'
              'EF6E286DDA85EA2A4BA7DE684E2C6E8793298290')
source=("https://www.torproject.org/dist/torbrowser/${pkgver}/tor-browser-linux${_pkgarch}-${_realpkgver}_${_language}.tar.xz"
		    "https://www.torproject.org/dist/torbrowser/${pkgver}/tor-browser-linux${_pkgarch}-${_realpkgver}_${_language}.tar.xz.asc"
		    'tor-browser-en.desktop'
		    'tor-browser-en.png'
        'tor-browser-en.sh')

if [[ "$CARCH" == 'x86_64' ]]; then
  sha256sums=('0f0a07905daaf714322bc54cf25ed0e3b8ef91aeb937ab0df2d39010c9ee7b82'
              'bb578c05520ed25e0c3223ec409ccf59cb979ceccf26b3ae3ef53ea966f19212')
else
  sha256sums=('f527b85a057ae402fa1dc84e6c175071e6448300cd2ebdd0d50494e1dd605185'
              'e6743ba76720a360e7ff79e73fc8eef4b510b33f530662b082c6c20d4b932e29')
fi
sha256sums+=('3d5fc01f2cfbae0a00b7117b0b0a24028d1686e6f81a347809f74de2d8522ff7'
			       '17fc2f5784d080233aca16e788d62ab6fe3e57cf781b123cfe32767de97d6d3b'
			       '1c1fbfa21973a2991db8a58f718656fc835dda03257c951432500bbd5dd8d3f5')

noextract=("tor-browser-linux${_pkgarch}-${_realpkgver}_${_language}.tar.xz")

package() {
	cd "$srcdir"

	sed -i "s/REPL_NAME/tor-browser-en/g" tor-browser-en.sh
	sed -i "s/REPL_VERSION/${_realpkgver}/g" tor-browser-en.sh
	sed -i "s/REPL_LANGUAGE/${_language}/g" tor-browser-en.sh

	sed -i "s/REPL_NAME/tor-browser-en/g" tor-browser-en.desktop
	sed -i "s/REPL_LANGUAGE/${_language}/g" tor-browser-en.desktop
	sed -i "s/REPL_COMMENT/${pkgdesc}/g" tor-browser-en.desktop

	install -Dm644 tor-browser-en.desktop	\
    "${pkgdir}/usr/share/applications/tor-browser-en.desktop"
	install -Dm644 tor-browser-en.png \
    "${pkgdir}/usr/share/pixmaps/tor-browser-en.png"
	install -Dm755 tor-browser-en.sh "${pkgdir}/usr/bin/tor-browser-en"
	install -Dm644 \
    "tor-browser-linux${_pkgarch}-${_realpkgver}_${_language}.tar.xz" \
    "${pkgdir}/opt/tor-browser-en/tor-browser-linux${_pkgarch}-${_realpkgver}_${_language}.tar.xz"
}
