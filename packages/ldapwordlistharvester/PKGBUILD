# This file is part of BlackArch Linux ( https://www.blackarch.org/ ).
# See COPYING for license details.

pkgname=ldapwordlistharvester
pkgver=1.2.r17.ge5f1862
pkgrel=2
pkgdesc='Tool to generate wordlist from information present in LDAP, in order to crack passwords of domain accounts.'
arch=('any')
groups=('blackarch' 'blackarch-wordlist' 'blackarch-wordlist')
url='https://github.com/p0dalirius/pyLDAPWordlistHarvester'
license=('GPL-2.0-only')
depends=('python' 'python-pycryptodome' 'python-sectools>=1.5.0' 'python-xlsxwriter')
makedepends=('git')
source=("$pkgname::git+$url.git")
sha512sums=('SKIP')

pkgver() {
  cd $pkgname

  ( set -o pipefail
    git describe --long --tags --abbrev=7 2>/dev/null |
      sed 's/\([^-]*-g\)/r\1/;s/-/./g' ||
    printf "%s.%s" "$(git rev-list --count HEAD)" \
      "$(git rev-parse --short=7 HEAD)"
  )
}

package() {
  cd $pkgname

  install -dm 755 "$pkgdir/usr/bin"
  install -dm 755 "$pkgdir/usr/share/$pkgname"

  install -Dm 644 -t "$pkgdir/usr/share/doc/$pkgname/" README.md

  cp -a LDAPWordlistHarvester.py "$pkgdir/usr/share/$pkgname/$pkgname.py"

  cat > "$pkgdir/usr/bin/$pkgname" << EOF
#!/bin/sh
exec python /usr/share/$pkgname/$pkgname.py "\$@"
EOF

  chmod +x "$pkgdir/usr/bin/$pkgname"
}

