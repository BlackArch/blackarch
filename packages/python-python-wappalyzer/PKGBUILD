# This file is part of BlackArch Linux ( https://www.blackarch.org/ ).
# See COPYING for license details.

pkgbase=python-python-wappalyzer
pkgname=('python2-python-wappalyzer' 'python-python-wappalyzer')
_pkgname=python-Wappalyzer
pkgver=0.3.0
pkgrel=1
pkgdesc='Python implementation of the Wappalyzer web application detection utility.'
arch=('any')
url='https://pypi.org/project/python-wappalyzer/#files'
license=('GPL3')
makedepends=('python2-setuptools' 'python-setuptools')
source=("https://files.pythonhosted.org/packages/1c/0e/e99cc5041e48fd5dc25ee566ac6c01c563b0497040d49c2ba2c0c2d4c9ad/python-Wappalyzer-$pkgver.tar.gz")
sha512sums=('b0dc217dc99389392d73dfe7b0087fc9f718569444bf5834402cdd779d7798b51c3446219d5bcc97a7c576cf1a9c9d2cb53c4293e4d108603e14f6810e33999e')

prepare() {
  cp -a "$_pkgname-$pkgver"{,-2}
}

build() {
  cd "$_pkgname-$pkgver-2"

  python2 setup.py build

  cd "$srcdir/$_pkgname-$pkgver"

  python setup.py build
}

package_python2-python-wappalyzer() {
  depends=('python2' 'python2-beautifulsoup3' 'python2-requests')

  cd "$_pkgname-$pkgver-2"

  python2 setup.py install --prefix=/usr --root="$pkgdir" --optimize=1 \
    --skip-build
}

package_python-python-wappalyzer() {
  depends=('python' 'python-beautifulsoup4' 'python-requests')

  cd "$_pkgname-$pkgver"

  python setup.py install --prefix=/usr --root="$pkgdir" --optimize=1 \
    --skip-build
}

