# This file is part of BlackArch Linux ( https://www.blackarch.org/ ).
# See COPYING for license details.

pkgbase=python-python-wappalyzer
pkgname=('python2-python-wappalyzer' 'python-python-wappalyzer')
_pkgname=python-Wappalyzer
pkgver=0.2.2
pkgrel=4
pkgdesc='Python implementation of the Wappalyzer web application detection utility.'
arch=('any')
url='https://pypi.org/project/python-wappalyzer/#files'
license=('GPL3')
makedepends=('python2-setuptools' 'python-setuptools')
source=("https://files.pythonhosted.org/packages/34/30/1d686c4de1f3b67286d187488c404afa77fdb14e26f60a9ca5c890c74f05/$_pkgname-$pkgver.tar.gz")
sha512sums=('f62f3e5ba3daa39c7d62c63aacd17635903b4ea86bf1a32c8a12ca5316fc9b6f339803a37de1e67d455527929865723282ffce0efd36ce51d68925c53281ae20')

prepare() {
  cp -a "$_pkgname-$pkgver"{,-2}
}

build() {
  cd "$_pkgname-$pkgver-2"

  python2 setup.py build

  cd "$srcdir/$_pkgname-$pkgver"

  python setup.py build
}

package_python2-python-wappalyzer() {
  depends=('python2' 'python2-beautifulsoup3' 'python2-requests')

  cd "$_pkgname-$pkgver-2"

  python2 setup.py install --prefix=/usr --root="$pkgdir" --optimize=1 \
    --skip-build
}

package_python-python-wappalyzer() {
  depends=('python' 'python-beautifulsoup4' 'python-requests')

  cd "$_pkgname-$pkgver"

  python setup.py install --prefix=/usr --root="$pkgdir" --optimize=1 \
    --skip-build
}

