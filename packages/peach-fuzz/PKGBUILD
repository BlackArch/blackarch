# This file is part of BlackArch Linux ( https://www.blackarch.org/ ).
# See COPYING for license details.

pkgname=peach-fuzz
pkgver=55.404e8ee
pkgrel=3
pkgdesc='Simple vulnerability scanning framework.'
groups=('blackarch' 'blackarch-fuzzer' 'blackarch-code-audit')
arch=('any')
url='https://github.com/Caleb1994/peach'
license=('custom:unknown')
depends=('python2' 'python2-colorama' 'pwntools')
makedepends=('git')
source=("$pkgname::git+https://github.com/Caleb1994/peach.git")
sha512sums=('SKIP')

pkgver() {
  cd $pkgname

  echo $(git rev-list --count HEAD).$(git rev-parse --short HEAD)
}

package() {
  cd $pkgname

  install -dm 755 "$pkgdir/usr/bin"
  install -dm 755 "$pkgdir/usr/share/$pkgname"

  install -Dm 644 -t "$pkgdir/usr/share/doc/$pkgname/" README.md

  rm *.md

  cp -a * "$pkgdir/usr/share/$pkgname"

  cat > "$pkgdir/usr/bin/$pkgname" << EOF
#!/bin/sh
cd /usr/share/$pkgname
exec python2 peach.py "\$@"
EOF

  chmod +x "$pkgdir/usr/bin/$pkgname"
}

