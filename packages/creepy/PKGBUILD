pkgname=creepy
pkgver=20121023
pkgrel=4
groups=('blackarch' 'blackarch-exploitation' 'blackarch-intel')
pkgdesc="A geolocation information gatherer. Offers geolocation information gathering through social networking platforms."
arch=('i686' 'x86_64')
url='http://github.com/ilektrojohn/creepy'
license=('GPL3')
provides=('creepy')
makedepends=('git')
depends=('python2' 'python2-tweepy' 'python2-osmgpsmap' 'python2-flickrapi'
         'python2-beautifulsoup3' 'python2-pyexiv2' 'python2-configobj'
         'python2-simplejson')
source=('git+https://github.com/ilektrojohn/creepy.git')
md5sums=('SKIP')

prepare() {
  # Fix a spacing error. This should be fixed by the upstream in the near future.
  sed -i '127s/         /        /' "$srcdir/creepy/creepy/creepymap.py"
}

package() {
  cd "$srcdir/creepy"

  # Make base directories.
  install -dm755 "$pkgdir/usr/share/creepy"
  install -dm755 "$pkgdir/usr/bin"
  install -dm755 "$pkgdir/usr/share/creepy/doc"

  python2 setup.py install "--root=$pkgdir"

  # Make joint script.
  cat > "$pkgdir/usr/bin/creepy" <<EOF
#!/bin/sh
python2 /usr/lib/python2.7/site-packages/creepy/creepymap.py "\$@"
EOF
  chmod +x "$pkgdir/usr/bin/creepy"
}
