# This file is part of BlackArch Linux ( http://blackarch.org ).
# See COPYING for license details.

pkgname='creepy'
pkgver=137.9f60449
pkgrel=3
epoch=1
groups=('blackarch' 'blackarch-scanner' 'blackarch-social' 'blackarch-recon')
pkgdesc='A geolocation information gatherer. Offers geolocation information gathering through social networking platforms.'
arch=('any')
url='http://github.com/ilektrojohn/creepy.git'
license=('GPL3')
provides=('creepy')
makedepends=('git')
depends=('python2' 'python2-tweepy' 'python2-osmgpsmap' 'python2-flickrapi'
         'python2-beautifulsoup3' 'python2-exiv2' 'python2-configobj'
         'python2-simplejson' 'python2-yapsy' 'python2-pyqt4')
source=('git+https://github.com/ilektrojohn/creepy.git')
sha1sums=('SKIP')

pkgver() {
  cd "$srcdir/creepy"

  echo $(git rev-list --count HEAD).$(git rev-parse --short HEAD)
}

package() {
  cd "$srcdir/creepy"

  mkdir -p "$pkgdir/usr/bin"
  mkdir -p "$pkgdir/usr/share/creepy"

  install -Dm644 README.md "$pkgdir/usr/share/creepy/doc/README.md"
  install -Dm644 LICENSE "$pkgdir/usr/share/licenses/creepy/LICENSE"

  rm README.md LICENSE

  cp -a creepy/* "$pkgdir/usr/share/creepy/"

  cat > "$pkgdir/usr/bin/creepy" << EOF
#!/bin/sh
exec python2 "/usr/share/creepy/CreepyMain.py" "\$@"
EOF

  chmod +x "$pkgdir/usr/bin/creepy"
}
