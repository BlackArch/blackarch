# Maintainer: lsfxz (https://github.com/lsfxz/quarry)
# Many thanks to anatol for quarry and gem2arch!
# This file is part of BlackArch Linux ( http://blackarch.org ).
# See COPYING for license details.
pkgname='ruby-rdoc-4'
_gemname='rdoc'
pkgver='4.3.0'
pkgrel=2
pkgdesc="RDoc produces HTML and command-line documentation for Ruby projects"
arch=('any')
url='http://docs.seattlerb.org/rdoc'
license=("Ruby")
depends=('ruby')
options=('!emptydirs')
source=("https://rubygems.org/downloads/$_gemname-$pkgver.gem" )
noextract=("$_gemname-$pkgver.gem")
sha1sums=('2a5d0a235c5f17b3bd686e4e1609a50ddb910897' )


package() {
  local _gemdir="$(ruby -rubygems -e 'puts Gem.default_dir')"
  gem install --ignore-dependencies --no-document --no-user-install -i "$pkgdir/$_gemdir" -n "$pkgdir"/usr/bin $_gemname-$pkgver.gem 
  rm "$pkgdir/$_gemdir/cache/$_gemname-$pkgver.gem"
  install -D -m644 "$pkgdir/$_gemdir/gems/$_gemname-$pkgver/LICENSE.rdoc" "$pkgdir/usr/share/licenses/$pkgname/LICENSE.rdoc"
  # non-HEAD version should not install any files in /usr/bin
  rm -r "$pkgdir"/usr/bin/

  rm -rf "$pkgdir/$_gemdir/gems/$_gemname-$pkgver"/{.autotest,.document,CONTRIBUTING.rdoc,CVE-2013-0256.rdoc,ExampleMarkdown.md,ExampleRDoc.rdoc,History.rdoc,LEGAL.rdoc,LICENSE.rdoc,Manifest.txt,README.rdoc,RI.rdoc,Rakefile,TODO.rdoc,test,bin}
}
