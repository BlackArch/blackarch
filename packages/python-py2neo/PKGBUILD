# This file is part of BlackArch Linux ( https://www.blackarch.org/ ).
# See COPYING for license details.

pkgbase=python-py2neo
pkgname=('python2-py2neo' 'python-py2neo')
_pkgname=py2neo
pkgver=2020.1.0
epoch=1
pkgrel=1
pkgdesc='Python client library and toolkit for Neo4j.'
arch=('x86_64' 'aarch64')
url='https://pypi.python.org/pypi/py2neo/'
license=('Apache')
makedepends=('python2-setuptools' 'python-setuptools')
source=("https://files.pythonhosted.org/packages/87/55/d178ab1901fc75d5fc76dda3820baed36e61dc7cb928ef4050242bc4fae5/$_pkgname-$pkgver.tar.gz")
sha512sums=('03eaae80b571c8bf674ee33a6f33d74f097c0a6972b1b49d24463be84bd157d053082855d0f27f1c8f8e869b1acc706cb0b4759b3922b934640404d994af6746')

prepare() {
  cp -a "$_pkgname-$pkgver"{,-2}
}

build() {
  cd "$_pkgname-$pkgver-2"

  python2 setup.py build

  cd "$srcdir/$_pkgname-$pkgver"

  python setup.py build
}

package_python2-py2neo() {
  depends=('python2' 'python2-certifi' 'python2-click' 'python2-colorama'
           'python2-pygments' 'python2-pytz' 'python2-urllib3' 'python2-neobolt'
           'python2-neotime' 'python2-prompt_toolkit')

  cd "$_pkgname-$pkgver-2"

  python2 setup.py install --prefix=/usr --root="$pkgdir" --optimize=1 \
    --skip-build

  mv "$pkgdir/usr/bin/$_pkgname" "$pkgdir/usr/bin/${_pkgname}2"
}

package_python-py2neo() {
  depends=('python' 'python-certifi' 'python-click' 'python-colorama'
           'python-pygments' 'python-pytz' 'python-urllib3' 'python-neobolt'
           'python-neotime' 'python-prompt_toolkit')

  cd "$_pkgname-$pkgver"

  python setup.py install --prefix=/usr --root="$pkgdir" --optimize=1 \
    --skip-build
}

