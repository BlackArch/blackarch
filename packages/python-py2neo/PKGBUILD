# This file is part of BlackArch Linux ( https://www.blackarch.org/ ).
# See COPYING for license details.

pkgbase=python-py2neo
pkgname=('python2-py2neo' 'python-py2neo')
_pkgname=py2neo
pkgver=2020.1.1
epoch=1
pkgrel=2
pkgdesc='Python client library and toolkit for Neo4j.'
arch=('x86_64' 'aarch64')
url='https://pypi.python.org/pypi/py2neo/'
license=('Apache')
makedepends=('python2-setuptools' 'python-setuptools')
source=("https://files.pythonhosted.org/packages/2e/a8/1b08a0c357ec27ca111976b00ccc95d0f075d103b08f0068b5ebed606a3c/$_pkgname-$pkgver.tar.gz")
sha512sums=('a828563baf5be5670d0a220e25f555dda978dc513c3f10f5a2490899ff08377a148bed639b98814f0c6847ae659aacd5d07c365381fd0bbdcac4f44673d6bff5')

prepare() {
  cp -a "$_pkgname-$pkgver"{,-2}
}

build() {
  cd "$_pkgname-$pkgver-2"

  python2 setup.py build

  cd "$srcdir/$_pkgname-$pkgver"

  python setup.py build
}

package_python2-py2neo() {
  depends=('python2' 'python2-certifi' 'python2-click' 'python2-colorama'
           'python2-pygments' 'python2-pytz' 'python2-urllib3' 'python2-neobolt'
           'python2-neotime' 'python2-prompt_toolkit')

  cd "$_pkgname-$pkgver-2"

  python2 setup.py install --prefix=/usr --root="$pkgdir" --optimize=1 \
    --skip-build

  mv "$pkgdir/usr/bin/$_pkgname" "$pkgdir/usr/bin/${_pkgname}2"
}

package_python-py2neo() {
  depends=('python' 'python-certifi' 'python-click' 'python-colorama'
           'python-pygments' 'python-pytz' 'python-urllib3' 'python-neobolt'
           'python-neotime' 'python-prompt_toolkit')

  cd "$_pkgname-$pkgver"

  python setup.py install --prefix=/usr --root="$pkgdir" --optimize=1 \
    --skip-build
}

