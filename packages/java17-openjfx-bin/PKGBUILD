# This file is part of BlackArch Linux ( https://www.blackarch.org/ ).
# See COPYING for license details.
#
# Initial author: Mohammadreza Abdollahzadeh <morealaz at gmail dot com>
# Modified: noptrix at blackarch d0t org
# Modified: noraj at blackarch d0t org

pkgname=java17-openjfx-bin
_ver=17
pkgver=17.0.16
pkgrel=1
pkgdesc="Java OpenJFX ${_ver} client application platform (Gluon build)."
arch=('x86_64')
url='https://openjfx.io/'
license=('GPL2')
depends=('ffmpeg' 'gstreamer' "java-environment>=$_ver" 'libxtst' 'qt5-base'
         'webkit2gtk')
provides=("java-openjfx=$_ver")
conflicts=("java-openjfx=$_ver" "java${_ver}-openjfx")
source=("https://download2.gluonhq.com/openjfx/$pkgver/openjfx-${pkgver}_linux-x64_bin-sdk.zip"
        "https://download2.gluonhq.com/openjfx/$pkgver/openjfx-${pkgver}_linux-x64_bin-jmods.zip")
sha512sums=('b11a52a6e86cf634d3acff9d4de6e3a239fe90184a0fb56637c7c48906f7d1938e123482773acffb53b48469b792829f3e8a970de0838b1d0c91dc6b57f6052f'
            '2ea9695f991879cbe2d8278d8f07ef06ff76476956001a667e184918f1d902138baf5c93cddee646d9d6f50088fcbd64b5d34b4fa1b521c7cbcdba1f5f9c8165')

package() {
  _jvmdir="usr/lib/jvm/java-${_ver}-openjdk"

  install -dm 755 "$pkgdir/$_jvmdir"
  install -dm 755 "$pkgdir/$_jvmdir/jmods"
  install -dm 750 "$pkgdir/usr/share/licenses/java$_ver-openjdk"

  cp -a "javafx-sdk-$pkgver/lib" "$pkgdir/$_jvmdir/"
  cp -a "javafx-sdk-$pkgver/src.zip" "$pkgdir/$_jvmdir/"
  mv "$pkgdir/$_jvmdir/src.zip" "$pkgdir/$_jvmdir/lib/javafx-src.zip"
  cp -a "javafx-sdk-$pkgver/legal" \
    "$pkgdir/usr/share/licenses/java$_ver-openjdk/"
  cp -a "javafx-jmods-$pkgver/"* "$pkgdir/$_jvmdir/jmods/"
}

