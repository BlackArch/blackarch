# This file is part of BlackArch Linux ( http://blackarch.org ).
# See COPYING for license details.

pkgname='binwalk'
pkgver='2.0.1'
pkgrel=3
groups=('blackarch' 'blackarch-firmware' 'blackarch-binary'
        'blackarch-reversing' 'blackarch-disassembler')
pkgdesc='A tool for searching a given binary image for embedded files.'
arch=('i686' 'x86_64' 'armv6h' 'armv7h')
url="http://binwalk.org"
license=('MIT')
depends=('python2' 'python2-magic' 'python2-pyqtgraph' 'python2-opengl'
         'python2-pydeep')
makedepends=('python2' 'python2-setuptools')
optdepends=('firmware-mod-kit: Firmware extract rules helper')
source=("https://github.com/devttys0/binwalk/archive/v${pkgver}.tar.gz"
        'binwalk.patch' 'binwalk.install')
sha1sums=('b0ec783cbf72db358c5118a3052fc1ccf446d8f3'
          '64bcb12a601314471f878453375c5eadbeccab4f'
          '369025df2bef867e2d8ffb81aa018d6f5ecd33ef')

build() {
  cd "$srcdir/binwalk-$pkgver"

  python2 setup.py build

  ./configure --prefix=/usr

  patch -i "$srcdir/binwalk.patch"

  make
}

package() {
  cd "$srcdir/binwalk-$pkgver"

  mkdir -p "$pkgdir/opt"

  make DESTDIR="$pkgdir" install

  install -Dm644 -t "$pkgdir/usr/share/doc/binwalk" README.md INSTALL.md API.md
  install -Dm644 LICENSE "$pkgdir/usr/share/licenses/binwalk/LICENSE"

  ln -sf /usr/share/firmware-mod-kit "$pkgdir/opt/firmware-mod-kit"
}
