# This file is part of BlackArch Linux ( http://blackarch.org ).
# See COPYING for license details.

pkgname='binwalk'
pkgver='2.1.1'
pkgrel=1
groups=('blackarch' 'blackarch-firmware' 'blackarch-binary'
        'blackarch-reversing' 'blackarch-disassembler')
pkgdesc='A tool for searching a given binary image for embedded files.'
arch=('x86_64' 'armv6h' 'armv7h' 'aarch64')
url="http://binwalk.org"
license=('MIT')
depends=('python2' 'python2-magic' 'python2-pyqtgraph' 'python2-opengl'
         'python2-pydeep' 'python2-pylzma')
makedepends=('python2' 'python2-setuptools')
optdepends=('firmware-mod-kit: Firmware extract rules helper')
source=("https://github.com/devttys0/binwalk/archive/v${pkgver}.tar.gz"
        'binwalk.install')
install='binwalk.install'
sha1sums=('7a958497f7eccd951379d2e358a4ca94317cd6bc'
          '369025df2bef867e2d8ffb81aa018d6f5ecd33ef')

build() {
  cd "$srcdir/binwalk-$pkgver"

  python2 setup.py build
}

package() {
  cd "$srcdir/binwalk-$pkgver"

  mkdir -p "$pkgdir/opt"

  python2 setup.py install --root="$pkgdir" --optimize=1

  install -Dm644 -t "$pkgdir/usr/share/doc/binwalk" README.md INSTALL.md API.md
  install -Dm644 LICENSE "$pkgdir/usr/share/licenses/binwalk/LICENSE"

  ln -sf /usr/share/firmware-mod-kit "$pkgdir/opt/firmware-mod-kit"
}
