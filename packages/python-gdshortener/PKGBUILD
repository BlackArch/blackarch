# This file is part of BlackArch Linux ( https://www.blackarch.org/ ).
# See COPYING for license details.

pkgbase=python-gdshortener
pkgname=('python2-gdshortener' 'python-gdshortener')
_pkgname=gdshortener
pkgver=0.0.4
pkgrel=2
pkgdesc='A module that provides access to .gd URL Shortener.'
arch=('any')
url='https://pypi.python.org/pypi/gdshortener'
license=('LGPL')
makedepends=('python2-setuptools' 'python-setuptools')
source=("https://files.pythonhosted.org/packages/a8/9c/9d7f8103b0eed64e895dfa670cfcba7152527e7dd166e22ca542a1af18cd/$_pkgname-$pkgver.tar.gz")
sha512sums=('4912ae4f1e1533202ad4034531b1844118cf7c369b8b084e730ee3064b15d45b3a443b59eb8bae4439ef68cb3b00fca951a56e46cf5c960175c974c6c024c793')

prepare() {
  cp -a "$_pkgname-$pkgver" "$_pkgname-$pkgver-2"
}

build() {
  cd "$_pkgname-$pkgver-2"

  python2 setup.py build

  cd "$srcdir/$_pkgname-$pkgver"

  python setup.py build
}

package_python2-gdshortener() {
  depends=('python2' 'python2-requests')

  cd "$_pkgname-$pkgver-2"

  python2 setup.py install --prefix=/usr --root="$pkgdir" --optimize=1 \
    --skip-build
}

package_python-gdshortener() {
  depends=('python' 'python-requests')

  cd "$_pkgname-$pkgver"

  python setup.py install --prefix=/usr --root="$pkgdir" --optimize=1 \
    --skip-build
}

