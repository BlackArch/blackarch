# This file is part of BlackArch Linux ( https://www.blackarch.org/ ).
# See COPYING for license details.

pkgname=python-forkedsimhash-py
_pkgname=forkedsimhash-py
pkgver=0.4.2
_pyver=3.12
pkgrel=3
pkgdesc='Near-Duplicate Detection with Simhash.'
url='https://pypi.org/project/forkedsimhash-py/#files'
arch=('x86_64' 'aarch64')
license=('custom:unknown')
depends=('python')
makedepends=('python-pip' 'python-build' 'python-wheel')
source=("https://files.pythonhosted.org/packages/source/${_pkgname::1}/$_pkgname/$_pkgname-$pkgver.tar.gz"
        'simhash.cpp.patch')
sha512sums=('7a82a9e29cc245927d48c53455a10d22f1f9fe4f90309cb9524e8c727263e3c0412ae5b6f09fa572d458f44382de07a445ac82d119179c59266ab44113d89d3f'
            '5f04437aacedf5a24a6523dd58300c02bc1b41e8d63a252b08412187db81af4de9271fe71fe589e7a8348ca7a85c1c2a397748748a7d6ac8cad7cde62f2df3bb')

prepare() {
  cd "$_pkgname-$pkgver"

  sed -i 's|ext_files.append("simhash/simhash.pyx")||g' setup.py

  patch -p2 < ../simhash.cpp.patch
}

build() {
  cd "$_pkgname-$pkgver"

  python -m build --wheel --outdir="$startdir/dist"
}

package() {
  cd "$_pkgname-$pkgver"

  pip install \
    --verbose \
    --disable-pip-version-check \
    --no-warn-script-location \
    --ignore-installed \
    --no-compile \
    --no-deps \
    --root="$pkgdir" \
    --prefix=/usr \
    --no-index \
    --find-links="file://$startdir/dist" \
    $_pkgname

  rm -rf "$pkgdir/usr/lib/python$_pyver/site-packages/simhash/"{__init__.py,__pycache__}
}

