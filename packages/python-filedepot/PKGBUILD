# This file is part of BlackArch Linux ( https://www.blackarch.org/ ).
# See COPYING for license details.

pkgbase=python-filedepot
pkgname=('python2-filedepot' 'python-filedepot')
_pkgname=filedepot
pkgver=0.10.0
pkgrel=1
pkgdesc='Toolkit for storing files and attachments in web applications.'
arch=('any')
url='https://pypi.org/project/filedepot/#files'
license=('MIT')
makedepends=('python2-setuptools' 'python-setuptools')
source=("https://files.pythonhosted.org/packages/source/${_pkgname::1}/$_pkgname/$_pkgname-$pkgver.tar.gz")
sha512sums=('f5246a3f6804a6658260bdabd2c802a73582e7897f4692049587727a9786b5f5c536ba0f46d44787bee446022d75cbceb9238261a63aa17e5b2ca65c12d911df')

prepare() {
  cp -a "$_pkgname-$pkgver"{,-2}
}

build() {
  cd "$_pkgname-$pkgver-2"

  python2 setup.py build

  cd "$srcdir/$_pkgname-$pkgver"

  python2 setup.py build
}

package_python2-filedepot() {
  depends=('python2' 'python2-unidecode')

  cd "$_pkgname-$pkgver-2"

  python2 setup.py install --prefix=/usr --root="$pkgdir" --optimize=1 \
    --skip-build
}

package_python-filedepot() {
  depends=('python' 'python-unidecode')

  cd "$_pkgname-$pkgver"

  python setup.py install --prefix=/usr --root="$pkgdir" --optimize=1 \
    --skip-build
}

