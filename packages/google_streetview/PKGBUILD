# This file is part of BlackArch Linux ( https://www.blackarch.org/ ).
# See COPYING for license details.

pkgbase=google_streetview
pkgname=('google_streetview' 'python2-google_streetview')
pkgver=1.2.9
pkgrel=1
pkgdesc='A command line tool and module for Google Street View Image API.'
arch=('any')
groups=('blackarch' 'blackarch-misc')
url='https://pypi.org/project/google-streetview/#files'
license=('MIT')
makedepends=('python2-setuptools' 'python-setuptools')
source=("https://files.pythonhosted.org/packages/04/33/be168a62a973e8dd3f00a83b2481f4c5602bb8339cbe81cf70a437b04ade/$pkgname-$pkgver.tar.gz")
sha512sums=('f0ce6c282377081fd24878d04f0b6d4dcc5a596d47ab19c1a00aa05647b5d06f3689941e50e49ba9b066942890d130acefaac044c58e879df96f414fb5ca601f')

prepare() {
  cp -a "$pkgbase-$pkgver" "$pkgbase-$pkgver-2"
}

build() {
  cd "$pkgbase-$pkgver-2"

  python2 setup.py build

  cd "$srcdir/$pkgbase-$pkgver"

  python setup.py build
}

package_google_streetview() {
  depends=('python' 'python-requests')

  cd "$pkgbase-$pkgver"

  python setup.py install --prefix=/usr --root="$pkgdir" --optimize=1 \
	--skip-build
}

package_python2-google_streetview() {
  depends=('python2' 'python2-requests')

  cd "$pkgbase-$pkgver-2"

  python2 setup.py install --prefix=/usr --root="$pkgdir" --optimize=1 \
	--skip-build

  mv "$pkgdir/usr/bin/$pkgbase" "$pkgdir/usr/bin/${pkgbase}2"
}

