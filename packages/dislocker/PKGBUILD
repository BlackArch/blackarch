# This file is part of BlackArch Linux ( http://blackarch.org ).
# See COPYING for license details.
# PKGBUILD retrieved from AUR. Credits to: mrxx <mrxx at cyberhome dot at>
# changes: psf

pkgname=dislocker
pkgver=0.7.1
pkgrel=1
pkgdesc="Read BitLocker encrypted volumes under Linux"
arch=('x86_64')
url='http://www.hsc.fr/ressources/outils/dislocker'
license=('GPL2')
depends=('fuse' 'mbedtls' 'ruby')
makedepends=('make' 'cmake')
conflicts=('dislocker-git')
optdepends=('ntfs-3g: NTFS file system support')
source=("https://github.com/Aorimn/dislocker/archive/v${pkgver}.tar.gz")
sha512sums=('8852ba3e363fdea992eebecfe1e4dad2b85404f57c57ce6b2937a9859d03cfa88d969926c9e11e6d22596c6d96805b7d1737893ae2c5a957448ab26708bce226')

build() {
  cd "${srcdir}/dislocker-${pkgver}"

  # Fix v0.7 upstream path error
  sed -i 's/DIS_MAN \${PROJECT_SOURCE_DIR}/DIS_MAN ../' src/CMakeLists.txt

  cmake -DCMAKE_INSTALL_PREFIX=/usr \
        -Dlibdir=/usr/lib \
        -D WARN_FLAGS:STRING="-Wall -Wextra" \
        .
  make
}

package() {
  cd "${srcdir}/dislocker-${pkgver}"
  make DESTDIR="${pkgdir}/" install

  install -Dm644 README.md ${pkgdir}/usr/share/doc/dislocker/README
  install -Dm644 CHANGELOG.md ${pkgdir}/usr/share/doc/dislocker/CHANGELOG
  install -Dm644 LICENSE.txt ${pkgdir}/usr/share/licenses/dislocker/LICENSE
}
