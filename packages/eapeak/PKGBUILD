# This file is part of BlackArch Linux ( http://blackarch.org ).
# See COPYING for license details.

pkgname='eapeak'
pkgver=116.32eba5b
pkgrel=1
groups=('blackarch' 'blackarch-wireless' 'blackarch-recon')
pkgdesc='Analysis Suite For EAP Enabled Wireless Networks.'
arch=('any')
url='https://github.com/securestate/eapeak'
license=('GPL2' 'GPL3')
depends=('python2' 'scapy' 'python2-m2crypto')
makedepends=('git' 'python2-setuptools')
source=('git+https://github.com/securestate/eapeak.git')
sha1sums=('SKIP')

pkgver() {
  cd "$srcdir/eapeak"

  echo $(git rev-list --count HEAD).$(git rev-parse --short HEAD)
}

build() {
  cd "$srcdir/eapeak"

  python2 setup.py build
}

package() {
  cd "$srcdir/eapeak"

  python2 setup.py install --root="$pkgdir" --prefix=/usr --optimize=1

  install -Dm644 -t "$pkgdir/usr/share/doc/eapeak/" README INSTALL
}
