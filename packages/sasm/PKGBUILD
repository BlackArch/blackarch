# This file is part of BlackArch Linux ( http://blackarch.org ).
# See COPYING for license details.

pkgname=sasm
pkgver=3.9.0
pkgrel=1
pkgdesc='A simple crossplatform IDE for NASM, MASM, GAS and FASM assembly languages.'
arch=('x86_64' 'armv6h' 'armv7h' 'aarch64')
groups=('blackarch' 'blackarch-misc')
url='https://github.com/Dman95/SASM'
license=('custom')
depends=('nasm' 'libxcb' 'gdb' 'qt5-base' 'mesa')
makedepends=('qt4>=4.8')
sha512sums=('b6b0ed5d68455fa7ca03514c20db27b2c5b0c519e0284c34e4b25910e168450f61c72eccfe080667d2b6876491d3e3ddf5823b75b1c7edcb6be7e190774630a9')
source=("https://github.com/Dman95/SASM/archive/v${pkgver}.tar.gz")

build() {
  cd "$srcdir/SASM-$pkgver"

  qmake QMAKE_CXXFLAGS="$CFLAGS -Wa,--noexecstack -Wl,-z,noexecstack -W \
    -D_REENTRANT -fPIE -fPIC" QMAKE_LFLAGS="-Wl,-z,noexecstack,-fPIC"

  make CXXFLAGS="$CXXFLAGS -Wa,--noexecstack -W -D_REENTRANT -fPIE -fPIC" \
    LDFLAGS="-Wl,-z,noexecstack"
}

package() {
  cd "$srcdir/SASM-$pkgver"

  install -dm 755 "$pkgdir/usr/bin/"
  install -dm 755 "$pkgdir/usr/share/sasm"
  install -dm 755 "$pkgdir/usr/share/doc/sasm"
  install -dm 755 "$pkgdir/usr/share/sasm/NASM"
  install -dm 755 "$pkgdir/usr/share/sasm/Projects"
  install -dm 755 "$pkgdir/usr/share/sasm/include"

  # Bins
  install -Dm755 Linux/bin/fasm "$pkgdir/usr/bin/fasm"
  install -Dm755 Linux/bin/listing "$pkgdir/usr/bin/listing"
  install -Dm755 sasm "$pkgdir/usr/bin/sasm"

  # Docs & License
  install -Dm644 Linux/share/doc/sasm/* "$pkgdir/usr/share/doc/sasm/"
  install -Dm644 Linux/share/doc/sasm/copyright \
    "$pkgdir/usr/share/licenses/sasm/copyright"

  # Share dir
  install -Dm644 Linux/share/sasm/keys.ini "$pkgdir/usr/share/sasm/"
  install -Dm644 Linux/share/sasm/sasm.png "$pkgdir/usr/share/sasm/"
  install -Dm644 Linux/share/sasm/NASM/* "$pkgdir/usr/share/sasm/NASM/"
  install -Dm644 Linux/share/sasm/Projects/* "$pkgdir/usr/share/sasm/Projects/"
  install -Dm644 Linux/share/sasm/include/* "$pkgdir/usr/share/sasm/include/"

}
