# This file is part of BlackArch Linux ( https://www.blackarch.org/ ).
# See COPYING for license details.

pkgbase=python-censys
pkgname=('python2-censys' 'python-censys')
_pkgname=censys
pkgver=2.1.5
pkgrel=1
epoch=1
pkgdesc='Python library for interacting with Censys Search Engine (censys.io).'
arch=('any')
url='https://pypi.org/project/censys/#files'
license=('Apache')
makedepends=('python2-setuptools' 'python-setuptools')
source=("https://files.pythonhosted.org/packages/7c/aa/c1c6daf3c637fcd7af50a41ec9e4a4b40803e840844b01e5312c8c635874/$_pkgname-$pkgver.tar.gz")
sha512sums=('cc29f708038c7dcc70861d2dece5fba3668561dd6f976e830ff9a0559be01a38707eafa97a1993fafa0f2bd69ba8000d5878574b23ebfe3f503120e101eb9582')

prepare() {
  cp -a "$_pkgname-$pkgver"{,-2}
}

build() {
  cd "$_pkgname-$pkgver-2"

  python2 setup.py build

  cd "$srcdir/$_pkgname-$pkgver"

  python setup.py build
}

package_python2-censys() {
  depends=('python2' 'python2-requests' 'python2-netaddr' 'python2-backoff')

  cd "$_pkgname-$pkgver-2"

  python2 setup.py install --root="$pkgdir" --prefix=/usr -O1 --skip-build

  mv "$pkgdir/usr/bin/$_pkgname" "$pkgdir/usr/bin/${_pkgname}2"
}

package_python-censys() {
  depends=('python' 'python-requests' 'python-netaddr' 'python-backoff'
           'python-rich')

  cd "$_pkgname-$pkgver"

  python setup.py install --root="$pkgdir" --prefix=/usr -O1 --skip-build
}

