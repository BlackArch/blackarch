# This file is part of BlackArch Linux ( https://www.blackarch.org/ ).
# See COPYING for license details.

pkgbase=python-censys
pkgname=('python2-censys' 'python-censys')
_pkgname=censys
pkgver=2.1.3
pkgrel=1
epoch=1
pkgdesc='Python library for interacting with Censys Search Engine (censys.io).'
arch=('any')
url='https://pypi.org/project/censys/#files'
license=('Apache')
makedepends=('python2-setuptools' 'python-setuptools')
source=("https://files.pythonhosted.org/packages/75/e3/4db8e47fd59fa163372494ddaa4bbf3a6fede27d0e03a7b2f165f02f0c79/$_pkgname-$pkgver.tar.gz")
sha512sums=('24d45f737093b85b8335f2623d567134c5996519005489c1c15fe8a04a64d34d213d41edd178634c0078256e4026e5c9493f83ff12a0d5f401575abf158e2ac1')

prepare() {
  cp -a "$_pkgname-$pkgver"{,-2}
}

build() {
  cd "$_pkgname-$pkgver-2"

  python2 setup.py build

  cd "$srcdir/$_pkgname-$pkgver"

  python setup.py build
}

package_python2-censys() {
  depends=('python2' 'python2-requests' 'python2-netaddr' 'python2-backoff')

  cd "$_pkgname-$pkgver-2"

  python2 setup.py install --root="$pkgdir" --prefix=/usr -O1 --skip-build

  mv "$pkgdir/usr/bin/$_pkgname" "$pkgdir/usr/bin/${_pkgname}2"
}

package_python-censys() {
  depends=('python' 'python-requests' 'python-netaddr' 'python-backoff'
           'python-rich')

  cd "$_pkgname-$pkgver"

  python setup.py install --root="$pkgdir" --prefix=/usr -O1 --skip-build
}

