# This file is part of BlackArch Linux ( https://www.blackarch.org/ ).
# See COPYING for license details.

pkgbase=python-censys
pkgname=('python2-censys' 'python-censys')
_pkgname=censys
pkgver=1.1.0
pkgrel=2
pkgdesc='Python library for interacting with Censys Search Engine (censys.io).'
arch=('any')
url='https://pypi.org/project/censys/#files'
license=('Apache')
makedepends=('python2-setuptools' 'python-setuptools')
source=("https://files.pythonhosted.org/packages/3e/fb/d15206c8f59c755127c28699a9ecf48c0384d5f721bbe9037f96b5e07606/$_pkgname-$pkgver.tar.gz")
sha512sums=('e9680f4c26fd59260e4c8637c8e28067e756d42af2e116ad61565cafe1bdcc91277e830a4fc77bbc50cb4c96d9d48a48983b405ddf0cd43453dcc39ced9fef81')

prepare() {
  cp -a "$_pkgname-$pkgver"{,-2}
}

build() {
  cd "$_pkgname-$pkgver-2"

  python2 setup.py build

  cd "$srcdir/$_pkgname-$pkgver"

  python setup.py build
}

package_python2-censys() {
  depends=('python2' 'python2-requests' 'python2-netaddr')

  cd "$_pkgname-$pkgver-2"

  python2 setup.py install --prefix=/usr --root="$pkgdir" --optimize=1 \
    --skip-build

  mv "$pkgdir/usr/bin/$_pkgname" "$pkgdir/usr/bin/${_pkgname}2"
}

package_python-censys() {
  depends=('python' 'python-requests' 'python-netaddr')

  cd "$_pkgname-$pkgver"

  python setup.py install --prefix=/usr --root="$pkgdir" --optimize=1 \
    --skip-build
}

