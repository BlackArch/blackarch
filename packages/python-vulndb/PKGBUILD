# This file is part of BlackArch Linux ( https://www.blackarch.org/ ).
# See COPYING for license details.

pkgbase=python-vulndb
pkgname=('python2-vulndb' 'python-vulndb')
_pkgname=vulndb
pkgver=0.1.1
pkgrel=2
pkgdesc='Provides access to the vulndb information.'
arch=('any')
url='https://pypi.org/project/vulndb/#files'
license=('BSD')
makedepends=('python2-setuptools' 'python-setuptools')
source=("https://files.pythonhosted.org/packages/d9/85/7f8c036837ea921e6c2d090a0a4465dc8c4a3c5240d74df9df87f0b7e83e/$_pkgname-$pkgver.tar.gz")
sha512sums=('58509f0ed3e413513f85e936388fcfb269143e7eb60e01302b25e8c3e259731a58f2d64bda94506ba6d1f3bdadf72e5feba67fa293137ecec3f0cb156b4ef4a2')

prepare() {
  cp -a "$_pkgname-$pkgver" "$_pkgname-$pkgver-2"
}

build() {
  cd "$_pkgname-$pkgver-2"

  python2 setup.py build

  cd "$srcdir/$_pkgname-$pkgver"

  python setup.py build
}

package_python2-vulndb() {
  depends=('python2')

  cd "$_pkgname-$pkgver-2"

  python2 setup.py install --prefix=/usr --root="$pkgdir" --optimize=1 \
    --skip-build
}

package_python-vulndb() {
  depends=('python')

  cd "$_pkgname-$pkgver"

  python setup.py install --prefix=/usr --root="$pkgdir" --optimize=1 \
    --skip-build
}

