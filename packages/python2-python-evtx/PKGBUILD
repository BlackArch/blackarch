# This file is part of BlackArch Linux ( http://blackarch.org ).
# See COPYING for license details.

pkgname='python2-python-evtx'
pkgver='0.5.1'
pkgrel=2
pkgdesc='Pure Python parser for recent Windows event log files (.evtx).'
arch=('any')
url='https://pypi.python.org/pypi/evtx/'
license=('Apache2')
depends=('python2')
makedepends=('python2-setuptools')
source=("https://github.com/williballenthin/python-evtx/archive/v${pkgver}.tar.gz")
sha1sums=('a1c13710a9b50e25164bc7e45055d28daf2d72ad')

build() {
  cd "$srcdir/python-evtx-$pkgver"

  python2 setup.py build
}

package() {
  cd "$srcdir/python-evtx-$pkgver"

  python2 setup.py install --prefix=/usr --root="$pkgdir" --optimize=1

  install -Dm755 -t "$pkgdir/usr/share/python2-python-evtx/scripts/" scripts/*
  install -Dm644 -t "$pkgdir/usr/share/doc/python2-python-evtx/" \
    CHANGELOG.TXT  CONTRIBUTORS.TXT documentation/*
}
