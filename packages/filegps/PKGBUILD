# This file is part of BlackArch Linux ( https://www.blackarch.org/ ).
# See COPYING for license details.

pkgname=filegps
pkgver=90.03cbc75
pkgrel=1
pkgdesc='A tool that help you to guess how your shell was renamed after the server-side script of the file uploader saved it.'
groups=('blackarch' 'blackarch-webapp' 'blackarch-misc')
arch=('any')
url='https://github.com/0blio/fileGPS'
license=('GPL3')
depends=('python2' 'python2-argparse' 'python2-requests')
makedepends=('git')
source=("$pkgname::git+https://github.com/0blio/fileGPS.git")
sha512sums=('SKIP')

pkgver() {
  cd $pkgname

  echo $(git rev-list --count HEAD).$(git rev-parse --short HEAD)
}

package() {
  cd $pkgname

  install -dm 755 "$pkgdir/usr/bin"
  install -dm 755 "$pkgdir/usr/share/$pkgname"

  install -Dm 644 -t "$pkgdir/usr/share/doc/$pkgname/" README.md
  install -Dm 644 LICENSE "$pkgdir/usr/share/licenses/$pkgname/LICENSE"

  rm README.md LICENSE

  cp -a * "$pkgdir/usr/share/$pkgname/"

  cat > "$pkgdir/usr/bin/$pkgname" << EOF
#!/bin/sh
cd /usr/share/$pkgname
exec python2 fileGPS.py "\$@"
EOF

  chmod +x "$pkgdir/usr/bin/$pkgname"
}

