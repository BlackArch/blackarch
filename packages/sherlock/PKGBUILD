# This file is part of BlackArch Linux ( https://www.blackarch.org/ ).
# See COPYING for license details.

pkgname=sherlock
_pypi_name=sherlock-project
_pypi_archive=sherlock_project
pkgver=0.15.0
pkgrel=2
epoch=1
pkgdesc='Find usernames across social networks.'
groups=('blackarch' 'blackarch-social' 'blackarch-recon')
arch=('any')
url='https://github.com/sherlock-project/sherlock'
license=('MIT')
depends=('python' 'python-certifi' 'python-colorama' 'python-pysocks'
         'python-requests' 'python-requests-futures' 'python-stem'
          'python-torrequest' 'python-pandas' 'python-openpyxl')
makedepends=('python-build' 'python-pip')
source=("https://files.pythonhosted.org/packages/source/${pkgname::1}/$_pypi_name/$_pypi_archive-$pkgver.tar.gz")
sha512sums=('b5f7f403cf403bc6b3af90fa34fe7cd4558f45800de84071370dd9e62e779e51a318a91943e894d92d3227a104c348beac9b7e3d2e1bb6ac07f063687ef4756a')

build() {
  cd "$_pypi_archive-$pkgver"

  python -m build --wheel --outdir="$startdir/dist"
}

package() {
  cd "$_pypi_archive-$pkgver"

  pip install \
    --verbose \
    --disable-pip-version-check \
    --no-warn-script-location \
    --ignore-installed \
    --no-compile \
    --no-deps \
    --root="$pkgdir" \
    --prefix=/usr \
    --no-index \
    --find-links="file://$startdir/dist" \
    $_pypi_name
}

