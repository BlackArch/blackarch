pkgname=upnpscan
pkgver=0.4
pkgrel=2
groups=('blackarch' 'blackarch-scanner')
pkgdesc='Scans the LAN or a given address range for UPnP capable devices.'
arch=('i686' 'x86_64' 'armv6h' 'armv7h')
url='http://www.cqure.net/wp/upnpscan/'
source=("http://www.cqure.net/tools/upnpscan-v$pkgver-src.tgz")
license=('GPL3')
depends=('libpcap')
makedepends=('libtool' 'autoconf')
md5sums=('23db71f206cefd67a5d4c1397577d77f')

build() {
	cd "$srcdir/upnpscan"

	libtoolize --copy --force -i
	aclocal
	autoconf
	automake --add-missing

	./configure --prefix=/usr
	make
}

package() {
	cd "$srcdir/upnpscan"

	install -Dm755 "src/upnpscan" "$pkgdir/usr/bin/upnpscan"
	install -Dm644 "COPYING" "$pkgdir/usr/share/licenses/upnpscan/COPYING"
}
