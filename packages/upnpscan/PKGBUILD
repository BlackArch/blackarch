# This file is part of BlackArch Linux ( http://blackarch.org ).
# See COPYING for license details.

pkgname='upnpscan'
pkgver='0.4'
pkgrel=4
groups=('blackarch' 'blackarch-scanner')
pkgdesc='Scans the LAN or a given address range for UPnP capable devices.'
arch=('x86_64' 'armv6h' 'armv7h' 'aarch64')
url='http://www.cqure.net/wp/upnpscan/'
source=("http://www.cqure.net/tools/upnpscan-v${pkgver}-src.tgz")
license=('GPL3')
depends=('libpcap')
makedepends=('libtool' 'autoconf')
sha1sums=('d15125d147ef962107fc3f01b52160d50f0d8710')

build() {
  cd "$srcdir/upnpscan"

  libtoolize --copy --force -i
  aclocal
  autoconf
  automake --add-missing

  ./configure --prefix=/usr

  make
}

package() {
  cd "$srcdir/upnpscan"

  install -Dm755 src/upnpscan "$pkgdir/usr/bin/upnpscan"
  install -Dm644 COPYING "$pkgdir/usr/share/licenses/upnpscan/COPYING"
}
