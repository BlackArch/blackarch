# This file is part of BlackArch Linux ( https://www.blackarch.org/ ).
# See COPYING for license details.

pkgbase=python-ipparser
pkgname=('python2-ipparser' 'python-ipparser')
_pkgname=ipparser
pkgver=0.3.5
pkgrel=3
pkgdesc='Parse IP address information and return a list for iteration.'
arch=('any')
url='https://pypi.org/project/ipparser/#files'
license=('BSD')
makedepends=('python2-setuptools' 'python-setuptools')
source=("https://files.pythonhosted.org/packages/d3/9d/aeaa797f7278c6dca39b6878a252b03e70cd09a2b956f56bfdcc186cc9ba/$_pkgname-$pkgver.tar.gz")
sha512sums=('83e28daff05e52e175358357d105e3d4b209451e63c65443e3859885ad763c8310c7b3c4dbf89249e5e6779b9cbbd1fb3f0825b30685a2c18d8ead764d672b2b')

prepare() {
  cp -a "$_pkgname-$pkgver"{,-2}
}

build() {
  cd "$_pkgname-$pkgver-2"

  python2 setup.py build

  cd "$srcdir/$_pkgname-$pkgver"

  python setup.py build
}

package_python2-ipparser() {
  depends=('python2' 'python2-dnspython')

  cd "$_pkgname-$pkgver-2"

  python2 setup.py install --prefix=/usr --root="$pkgdir" -O1 --skip-build

  chmod 644 \
    "$pkgdir/usr/lib/python2.7/site-packages/$_pkgname-$pkgver-py2.7.egg-info/"*
}

package_python-ipparser() {
  depends=('python' 'python-dnspython')

  cd "$_pkgname-$pkgver"

  python setup.py install --prefix=/usr --root="$pkgdir" -O1 --skip-build

  chmod 644 \
    "$pkgdir/usr/lib/python3.7/site-packages/$_pkgname-$pkgver-py3.7.egg-info/"*
}

