# This file is part of BlackArch Linux ( https://www.blackarch.org/ ).
# See COPYING for license details.

pkgbase=python-ipparser
pkgname=('python2-ipparser' 'python-ipparser')
_pkgname=ipparser
pkgver=0.3.6
pkgrel=2
pkgdesc='Parse IP address information and return a list for iteration.'
arch=('any')
url='https://pypi.org/project/ipparser/#files'
license=('BSD')
makedepends=('python2-setuptools' 'python-setuptools')
source=("https://files.pythonhosted.org/packages/5f/56/35ef5488d047a621c3e2805ec17a56f1f95bb58d08d0f5e540a8b7d47631/$_pkgname-$pkgver.tar.gz")
sha512sums=('f77ee98de87a1342ce0d10809ff2a376ebe82d6b990456e6632aefccf344cada6917fdb877aa0a32aa980a86cccc5ca8729569d1e3deaae64e31e8df8261cedb')

prepare() {
  cp -a "$_pkgname-$pkgver"{,-2}
}

build() {
  cd "$_pkgname-$pkgver-2"

  python2 setup.py build

  cd "$srcdir/$_pkgname-$pkgver"

  python setup.py build
}

package_python2-ipparser() {
  depends=('python2' 'python2-dnspython')

  cd "$_pkgname-$pkgver-2"

  python2 setup.py install --prefix=/usr --root="$pkgdir" -O1 --skip-build

  chmod 644 \
    "$pkgdir/usr/lib/python2.7/site-packages/$_pkgname-$pkgver-py2.7.egg-info/"*
}

package_python-ipparser() {
  depends=('python' 'python-dnspython')

  cd "$_pkgname-$pkgver"

  python setup.py install --prefix=/usr --root="$pkgdir" -O1 --skip-build

  chmod 644 \
    "$pkgdir/usr/lib/python3.9/site-packages/$_pkgname-$pkgver-py3.9.egg-info/"*
}

