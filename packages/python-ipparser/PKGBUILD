# This file is part of BlackArch Linux ( https://www.blackarch.org/ ).
# See COPYING for license details.

pkgbase=python-ipparser
pkgname=('python2-ipparser' 'python-ipparser')
_pkgname=ipparser
pkgver=0.3.8
pkgrel=1
pkgdesc='Parse IP address information and return a list for iteration.'
arch=('any')
url='https://pypi.org/project/ipparser/#files'
license=('BSD')
makedepends=('python2-setuptools' 'python-setuptools')
source=("https://files.pythonhosted.org/packages/a0/14/7b6124565ac0fc52f77c286d315aca0791a10a752d8b353560fcc8cb3379/$_pkgname-$pkgver.tar.gz")
sha512sums=('07389fc37ae8d81638346334370209b972624e2d08bf225b3165c2d196ebf967ae0ad382f065735a7ff98655d1fec2f12476dbe53ea044c98b2d19394915f358')

prepare() {
  cp -a "$_pkgname-$pkgver"{,-2}
}

build() {
  cd "$_pkgname-$pkgver-2"

  python2 setup.py build

  cd "$srcdir/$_pkgname-$pkgver"

  python setup.py build
}

package_python2-ipparser() {
  depends=('python2' 'python2-dnspython')

  cd "$_pkgname-$pkgver-2"

  python2 setup.py install --prefix=/usr --root="$pkgdir" -O1 --skip-build

  chmod 644 \
    "$pkgdir/usr/lib/python2.7/site-packages/$_pkgname-$pkgver-py2.7.egg-info/"*
}

package_python-ipparser() {
  depends=('python' 'python-dnspython')

  cd "$_pkgname-$pkgver"

  python setup.py install --prefix=/usr --root="$pkgdir" -O1 --skip-build

  chmod 644 \
    "$pkgdir/usr/lib/python3.9/site-packages/$_pkgname-$pkgver-py3.9.egg-info/"*
}

