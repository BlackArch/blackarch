# This file is part of BlackArch Linux ( https://www.blackarch.org/ ).
# See COPYING for license details.

pkgname=cent
pkgver=v1.3.4.r7.g33e50b9
pkgrel=1
pkgdesc='Community edition nuclei templates.'
arch=('x86_64')
groups=('blackarch' 'blackarch-webapp' 'blackarch-scanner')
url='https://github.com/xm1k3/cent'
license=('Apache')
makedepends=('git' 'go')
source=("git+https://github.com/xm1k3/$pkgname.git")
sha512sums=('SKIP')

pkgver() {
  cd $pkgname

  git describe --long --tags | sed 's/\([^-]*-g\)/r\1/;s/-/./g'
}

build() {
  cd $pkgname

  GOPATH="$srcdir" go mod download
  GOPATH="$srcdir" go build \
    -trimpath \
    -buildmode=pie \
    -mod=readonly \
    -modcacherw \
    -ldflags "-s -w" \
    -o $pkgname .
}

package() {
  cd $pkgname

  install -Dm 755 $pkgname "$pkgdir/usr/bin/$pkgname"
  install -Dm 644 -t "$pkgdir/usr/share/doc/$pkgname/" README.md
}

