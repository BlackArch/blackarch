# This file is part of BlackArch Linux ( https://www.blackarch.org/ ).
# See COPYING for license details.

pkgbase=python-winsspi
pkgname=('python2-winsspi' 'python-winsspi')
_pkgname=winsspi
pkgver=0.0.5
pkgrel=1
pkgdesc='Windows SSPI library in pure Python.'
arch=('any')
url='https://pypi.org/project/winsspi/#files'
license=('MIT')
makedepends=('python2-setuptools' 'python-setuptools')
source=("https://files.pythonhosted.org/packages/ef/04/1e6567a27d7e85bf7844636d553b21d3e1b9666779efd6b68317da09fc71/$_pkgname-$pkgver.tar.gz")
sha512sums=('12596c5f38a147d121bd57083ef94a6ee64f1c409d81ece77d88a72125611ca9b17373675dbccf0a83ba349881f99b450538f6bb1d13b89c16f8ffd56fe84b85')

prepare() {
  cp -a "$_pkgname-$pkgver"{,-2}
}

build() {
  cd "$_pkgname-$pkgver-2"

  python2 setup.py build

  cd "$srcdir/$_pkgname-$pkgver"

  python setup.py build
}

package_python2-winsspi() {
  depends=('python2' 'python2-minikerberos')

  cd "$_pkgname-$pkgver-2"

  python2 setup.py install --prefix=/usr --root="$pkgdir" -O1 --skip-build
}

package_python-winsspi() {
  depends=('python' 'python-minikerberos')

  cd "$_pkgname-$pkgver"

  python setup.py install --prefix=/usr --root="$pkgdir" -O1 --skip-build
}

