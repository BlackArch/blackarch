# This file is part of BlackArch Linux ( https://www.blackarch.org/ ).
# See COPYING for license details.

pkgbase=python-winsspi
pkgname=('python2-winsspi' 'python-winsspi')
_pkgname=winsspi
pkgver=0.0.9
pkgrel=4
pkgdesc='Windows SSPI library in pure Python.'
arch=('any')
url='https://pypi.org/project/winsspi/#files' #https://github.com/skelsec/winsspi
license=('MIT')
makedepends=('python2-setuptools' 'python-setuptools')
source=("https://files.pythonhosted.org/packages/bc/dc/ec5d16880452aca0c86b92aa7828255f7808d3aaf92b69b34d8182659d2a/$_pkgname-$pkgver.tar.gz")
sha512sums=('badbd1be38b21cbaca15f6632c50e5d869819c0f41a21bcadc574cb91b5e17d707c1bb23fbcf467418d180d3e3bbbf1daf3e2d53e4df85b668f28710ee87e26a')

prepare() {
  cp -a "$_pkgname-$pkgver"{,-2}
}

build() {
  cd "$_pkgname-$pkgver-2"

  python2 setup.py build

  cd "$srcdir/$_pkgname-$pkgver"

  python setup.py build
}

package_python2-winsspi() {
  depends=('python2' 'python2-minikerberos')

  cd "$_pkgname-$pkgver-2"

  python2 setup.py install --prefix=/usr --root="$pkgdir" -O1 --skip-build
}

package_python-winsspi() {
  depends=('python' 'python-minikerberos')

  cd "$_pkgname-$pkgver"

  python setup.py install --prefix=/usr --root="$pkgdir" -O1 --skip-build
}

