# This file is part of BlackArch Linux ( https://www.blackarch.org/ ).
# See COPYING for license details.

pkgbase=python-winsspi
pkgname=('python2-winsspi' 'python-winsspi')
_pkgname=winsspi
pkgver=0.0.3
pkgrel=1
pkgdesc='Windows SSPI library in pure Python.'
arch=('any')
url='https://pypi.org/project/winsspi/#files'
license=('MIT')
makedepends=('python2-setuptools' 'python-setuptools')
source=("https://files.pythonhosted.org/packages/ce/5b/9878e7728e9de36b8b5cb7f95140cab6510d3d205ca25b52c9a8d5cf18f9/$_pkgname-$pkgver.tar.gz")
sha512sums=('0c7ba3fe20ce2ba0917f6281d558e0be93a6c14482d7681bba65b0b62b0a2f6becb4f5b8fb3973a196865a20baf0656139ec66f19e94b94b1e733358a035efc1')

prepare() {
  cp -a "$_pkgname-$pkgver"{,-2}
}

build() {
  cd "$_pkgname-$pkgver-2"

  python2 setup.py build

  cd "$srcdir/$_pkgname-$pkgver"

  python setup.py build
}

package_python2-winsspi() {
  depends=('python2' 'python2-minikerberos')

  cd "$_pkgname-$pkgver-2"

  python2 setup.py install --prefix=/usr --root="$pkgdir" -O1 --skip-build
}

package_python-winsspi() {
  depends=('python' 'python-minikerberos')

  cd "$_pkgname-$pkgver"

  python setup.py install --prefix=/usr --root="$pkgdir" -O1 --skip-build
}

