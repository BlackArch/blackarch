# This file is part of BlackArch Linux ( https://www.blackarch.org/ ).
# See COPYING for license details.

pkgbase=python-winsspi
pkgname=('python2-winsspi' 'python-winsspi')
_pkgname=winsspi
pkgver=0.0.7
pkgrel=1
pkgdesc='Windows SSPI library in pure Python.'
arch=('any')
url='https://pypi.org/project/winsspi/#files'
license=('MIT')
makedepends=('python2-setuptools' 'python-setuptools')
source=("https://files.pythonhosted.org/packages/3a/2f/82e959eebc13ddc4f9e43a734f1551fdeee0d4d7304b3fc14eb4c332e7ab/$_pkgname-$pkgver.tar.gz")
sha512sums=('439d678512bae292c43f182e425c7f22f20de1315ea9ad68b6c06e72e1f038f7508b9bc9047d827f51e75119f96e967b4d21ea437b97ebdb49788577f9cf4bc6')

prepare() {
  cp -a "$_pkgname-$pkgver"{,-2}
}

build() {
  cd "$_pkgname-$pkgver-2"

  python2 setup.py build

  cd "$srcdir/$_pkgname-$pkgver"

  python setup.py build
}

package_python2-winsspi() {
  depends=('python2' 'python2-minikerberos')

  cd "$_pkgname-$pkgver-2"

  python2 setup.py install --prefix=/usr --root="$pkgdir" -O1 --skip-build
}

package_python-winsspi() {
  depends=('python' 'python-minikerberos')

  cd "$_pkgname-$pkgver"

  python setup.py install --prefix=/usr --root="$pkgdir" -O1 --skip-build
}

