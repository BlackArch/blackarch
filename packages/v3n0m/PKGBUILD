# This file is part of BlackArch Linux ( https://www.blackarch.org/ ).
# See COPYING for license details.

pkgname=v3n0m
pkgver=312.ebe7fe3
pkgrel=1
groups=('blackarch' 'blackarch-scanner' 'blackarch-webapp' 'blackarch-recon')
pkgdesc='A tool to automate mass SQLi d0rk scans and Metasploit Vulns.'
arch=('any')
url='https://github.com/v3n0m-Scanner/V3n0M-Scanner'
license=('GPL2')
depends=('python' 'python-httplib2' 'python-aiohttp' 'python-asyncio'
         'python-pysocks' 'python-dnspython' 'python-multidict'
         'python-requests' 'python-pysocks' 'python-tqdm' 'python-yarl'
         'python-argparse')
makedepends=('git')
source=('git+https://github.com/v3n0m-Scanner/V3n0M-Scanner.git')
sha512sums=('SKIP')

pkgver() {
  cd V3n0M-Scanner

  echo $(git rev-list --count HEAD).$(git rev-parse --short HEAD)
}

package() {
  cd V3n0M-Scanner

  install -dm 755 "$pkgdir/usr/bin"
  install -dm 755 "$pkgdir/usr/share/$pkgname"

  python setup.py install --root="$pkgdir" --prefix=/usr --optimize=1

  cp -a src/* "$pkgdir/usr/share/$pkgname/"

  install -Dm 644 -t "$pkgdir/usr/share/doc/$pkgname/" README.md
  install -Dm 644 LICENSE "$pkgdir/usr/share/licenses/$pkgname/LICENSE"
  install -Dm 644 "src/desktop-menu/v3n0m.ico" \
    "$pkgdir/usr/share/icons/v3n0m.ico"
  install -Dm 644 "src/desktop-menu/v3n0m.desktop" \
    "$pkgdir/usr/share/applications/v3n0m.desktop"

  cat > "$pkgdir/usr/bin/$pkgname" << EOF
#!/bin/sh
cd /usr/share/$pkgname
exec python3 v3n0m.py "\$@"
EOF

  chmod +x "$pkgdir/usr/bin/$pkgname"
}

