# This file is part of BlackArch Linux ( https://www.blackarch.org/ ).
# See COPYING for license details.

pkgname=kautilya
pkgver=0.5.5
pkgrel=5
groups=('blackarch' 'blackarch-hardware')
pkgdesc='Pwnage with Human Interface Devices using Teensy++2.0 and Teensy 3.0 devices.'
arch=('any')
url='https://github.com/samratashok/Kautilya/releases'
license=('GPL3')
makedepends=('git')
depends=('ruby' 'ruby-bundler' 'libpcap')
source=("https://github.com/samratashok/Kautilya/archive/v$pkgver.tar.gz")
install='kautilya.install'
sha512sums=('090c9635f392596f00a7833a56f05ff399230bedebbaf8daca3586f49b6da483ca0ed7ffbfa49556aecc518eb0650ae0740b187556cc7f89517a87168a4a440b')

package() {
  cd "Kautilya-$pkgver"

  install -dm 755 "$pkgdir/usr/bin"
  install -dm 755 "$pkgdir/usr/share/$pkgname/output"

  install -Dm 644 -t "$pkgdir/usr/share/doc/$pkgname/" CHANGELOG README.md
  install -Dm 644 LICENSE "$pkgdir/usr/share/licenses/$pkgname/LICENSE"

  rm CHANGELOG README.md LICENSE

  cp -a * "$pkgdir/usr/share/$pkgname/"

  cat > "$pkgdir/usr/bin/$pkgname" << EOF
#!/bin/sh
cd /usr/share/$pkgname
exec ruby /usr/share/$pkgname/kautilya.rb "\$@"
EOF

  chmod a+x "$pkgdir/usr/bin/$pkgname"
}

