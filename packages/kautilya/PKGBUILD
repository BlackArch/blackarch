# This file is part of BlackArch Linux ( http://blackarch.org ).
# See COPYING for license details.

pkgname=kautilya
pkgver=0.5.5
pkgrel=5
groups=('blackarch' 'blackarch-hardware')
pkgdesc='Pwnage with Human Interface Devices using Teensy++2.0 and Teensy 3.0 devices.'
arch=('any')
url='https://github.com/samratashok/Kautilya/releases'
license=('GPL3')
makedepends=('git')
depends=('ruby' 'ruby-bundler' 'libpcap')
source=("https://github.com/samratashok/Kautilya/archive/v${pkgver}.tar.gz")
install='kautilya.install'
sha512sums=('090c9635f392596f00a7833a56f05ff399230bedebbaf8daca3586f49b6da483ca0ed7ffbfa49556aecc518eb0650ae0740b187556cc7f89517a87168a4a440b')

package() {
  cd "$srcdir/Kautilya-$pkgver"

  install -dm 755 "$pkgdir/usr/bin"
  install -dm 755 "$pkgdir/usr/share/kautilya/output"

  install -Dm644 -t "$pkgdir/usr/share/doc/kautilya" CHANGELOG README.md
  install -Dm644 LICENSE "$pkgdir/usr/share/licenses/kautilya/LICENSE"

  rm CHANGELOG README.md LICENSE

  cp -a * "$pkgdir/usr/share/kautilya"

  cat > "$pkgdir/usr/bin/kautilya" << EOF
#!/bin/sh
cd /usr/share/kautilya
exec ruby /usr/share/kautilya/kautilya.rb "\$@"
EOF

  chmod a+x "$pkgdir/usr/bin/kautilya"
}
