# This file is part of BlackArch Linux ( http://blackarch.org ).
# See COPYING for license details.

pkgname='kautilya'
pkgver='0.5.5'
pkgrel=5
groups=('blackarch' 'blackarch-hardware')
pkgdesc='Pwnage with Human Interface Devices using Teensy++2.0 and Teensy 3.0 devices.'
arch=('any')
url='https://github.com/samratashok/Kautilya/releases'
license=('GPL3')
makedepends=('git')
depends=('ruby' 'ruby-colored' 'ruby-highline' 'ruby-artii' 'libpcap')
source=("https://github.com/samratashok/Kautilya/archive/v${pkgver}.tar.gz")
sha1sums=('866305978d1fc3ae1c3e5b031180d89ed26e1856')

package() {
  cd "$srcdir/Kautilya-$pkgver"

  mkdir -p "$pkgdir/usr/bin"
  mkdir -p "$pkgdir/usr/share/kautilya/output"

  install -Dm644 -t "$pkgdir/usr/share/doc/kautilya" CHANGELOG README.md
  install -Dm644 LICENSE "$pkgdir/usr/share/licenses/kautilya/LICENSE"

  rm CHANGELOG README.md LICENSE

  cp -a * "$pkgdir/usr/share/kautilya"

  cat > "$pkgdir/usr/bin/kautilya" << EOF
#!/bin/sh
cd /usr/share/kautilya
exec ruby /usr/share/kautilya/kautilya.rb "\$@"
EOF

  chmod a+x "$pkgdir/usr/bin/kautilya"
}
