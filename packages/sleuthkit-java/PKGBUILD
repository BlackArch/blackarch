# This file is part of BlackArch Linux ( https://www.blackarch.org/ ).
# See COPYING for license details.

pkgname=sleuthkit-java
_pkgname=sleuthkit
pkgver=4.11.0
pkgrel=1
pkgdesc='Java dependencies for The Sleuth Kit.'
arch=('x86_64')
url='http://www.sleuthkit.org/sleuthkit'
license=('CPL, custom:"IBM Public Licence", GPL2')
provides=('sleuthkit')
depends=('jre8-openjdk' 'sleuthkit')
makedepends=('ant' 'jdk8-openjdk')
source=("https://github.com/sleuthkit/sleuthkit/releases/download/sleuthkit-$pkgver/sleuthkit-$pkgver.tar.gz")
sha512sums=('fabaf0e7815145f82cf7d8c342d403939511afa36f29318a89212a8aa1a3465e2050629ff9e308b34cfeb3c2c98efa94f9546737cba749c075c4e583b8808580')

prepare() {
  cd "$_pkgname-$pkgver"

  sed -i 's|value="${user.home}/.ant"/>|value="/tmp/ant"/>|g' \
    bindings/java/build.xml
}

build() {
  cd "$_pkgname-$pkgver"

  ./configure
  make

  cd bindings/java
  ant
}

package() {
  cd "$_pkgname-$pkgver"

  install -Dm 644 "bindings/java/dist/$_pkgname-$pkgver.jar" \
    "$pkgdir/usr/share/java/$_pkgname-$pkgver.jar"
}

