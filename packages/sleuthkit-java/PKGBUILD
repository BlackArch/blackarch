# This file is part of BlackArch Linux ( https://www.blackarch.org/ ).
# See COPYING for license details.

pkgname=sleuthkit-java
_pkgname=sleuthkit
pkgver=4.6.7
pkgrel=1
pkgdesc='Java dependencies for The Sleuth Kit.'
arch=('x86_64')
url='http://www.sleuthkit.org/sleuthkit'
license=('CPL, custom:"IBM Public Licence", GPL2')
provides=('sleuthkit')
depends=('java-runtime' 'sleuthkit')
makedepends=('ant')
source=("https://github.com/sleuthkit/sleuthkit/releases/download/sleuthkit-$pkgver/sleuthkit-$pkgver.tar.gz")
sha512sums=('5b2000960a054fd947acb214532076a369d788914a8ff141c9a2d1b8a69e001c891b1e4b8dd40f94d96810a436e240e5a76e9f90ccab462888fad789c1181cc6')

prepare() {
  cd "$_pkgname-$pkgver"

  sed -i 's|value="${user.home}/.ant"/>|value="/tmp/ant"/>|g' \
    bindings/java/build.xml
}

build() {
  cd "$_pkgname-$pkgver"

  ./configure
  make

  cd bindings/java
  ant
}

package() {
  cd "$_pkgname-$pkgver"

  install -Dm 644 "bindings/java/dist/$_pkgname-$pkgver.jar" \
    "$pkgdir/usr/share/java/$pkgname-$pkgver.jar"
}

