# This file is part of BlackArch Linux ( https://www.blackarch.org/ ).
# See COPYING for license details.

pkgname=sleuthkit-java
_pkgname=sleuthkit
pkgver=4.12.0
pkgrel=1
pkgdesc='Java dependencies for The Sleuth Kit.'
arch=('x86_64')
url='https://github.com/sleuthkit/sleuthkit/releases/'
license=('CPL, custom:"IBM Public Licence", GPL2')
provides=('sleuthkit')
depends=('jre8-openjdk' 'sleuthkit')
makedepends=('ant' 'jdk-openjdk')
source=("https://github.com/sleuthkit/sleuthkit/releases/download/sleuthkit-$pkgver/sleuthkit-$pkgver.tar.gz")
sha512sums=('9f20eb42d1dd7d0e15d49a4b6c18441cd31d2343fe34bc7fad1a6b6fe344b414efe959a4f7e34f5368a6efafeecbf39655648a9a3045b437a747c726134c77dc')

prepare() {
  cd "$_pkgname-$pkgver"

  sed -i 's|value="${user.home}/.ant"/>|value="/tmp/ant"/>|g' \
    bindings/java/build.xml
}

build() {
  cd "$_pkgname-$pkgver"

  ./configure
  make

  cd bindings/java
  ant
}

package() {
  cd "$_pkgname-$pkgver"

  install -Dm 644 "bindings/java/dist/$_pkgname-$pkgver.jar" \
    "$pkgdir/usr/share/java/$_pkgname-$pkgver.jar"
}

