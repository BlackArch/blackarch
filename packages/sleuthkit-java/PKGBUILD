# This file is part of BlackArch Linux ( https://www.blackarch.org/ ).
# See COPYING for license details.

pkgname=sleuthkit-java
_pkgname=sleuthkit
pkgver=4.9.0
pkgrel=1
pkgdesc='Java dependencies for The Sleuth Kit.'
arch=('x86_64')
url='http://www.sleuthkit.org/sleuthkit'
license=('CPL, custom:"IBM Public Licence", GPL2')
provides=('sleuthkit')
depends=('jre8-openjdk' 'sleuthkit')
makedepends=('ant' 'jdk8-openjdk')
source=("https://github.com/sleuthkit/sleuthkit/releases/download/sleuthkit-$pkgver/sleuthkit-$pkgver.tar.gz")
sha512sums=('1db8b9f825aa551fe5bdf01285832b903fb4f2d6a8fd9a57a5cd5c9005dfdbec22d3dd49d394616d838e2a2111110018e787f2ed467a1c27a500fc75bed0dd0b')

prepare() {
  cd "$_pkgname-$pkgver"

  sed -i 's|value="${user.home}/.ant"/>|value="/tmp/ant"/>|g' \
    bindings/java/build.xml
}

build() {
  cd "$_pkgname-$pkgver"

  ./configure
  make

  cd bindings/java
  ant
}

package() {
  cd "$_pkgname-$pkgver"

  install -Dm 644 "bindings/java/dist/$_pkgname-$pkgver.jar" \
    "$pkgdir/usr/share/java/$_pkgname-$pkgver.jar"
}

