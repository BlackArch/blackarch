# This file is part of BlackArch Linux ( https://www.blackarch.org/ ).
# See COPYING for license details.

pkgname=artlas
pkgver=152.bc26f20
pkgrel=1
pkgdesc='Apache Real Time Logs Analyzer System.'
groups=('blackarch' 'blackarch-defensive')
arch=('any')
url='https://github.com/mthbernardes/ARTLAS'
license=('MIT')
depends=('python2' 'python2-requests' 'python2-six' 'python2-urllib3'
         'python2-user-agents' 'python2-ua-parser' 'python2-pygtail'
         'python2-apache-log-parser' 'python2-py-zabbix' 'python2-telepot')
makedepends=('git')
source=("$pkgname::git+https://github.com/mthbernardes/ARTLAS.git")
sha512sums=('SKIP')

pkgver() {
  cd $pkgname

  echo $(git rev-list --count HEAD).$(git rev-parse --short HEAD)
}

package() {
  cd $pkgname

  install -dm 755 "$pkgdir/usr/bin"
  install -dm 755 "$pkgdir/usr/share/$pkgname"

  install -Dm 644 -t "$pkgdir/usr/share/doc/$pkgname/" README.md
  install -Dm 644 LICENSE "$pkgdir/usr/share/licenses/$pkgname/LICENSE"

  rm README.md LICENSE

  cp -a * "$pkgdir/usr/share/$pkgname/"

  cat > "$pkgdir/usr/bin/$pkgname" << EOF
#!/bin/sh
cd /usr/share/$pkgname
exec python2 $pkgname.py "\$@"
EOF

  chmod +x "$pkgdir/usr/bin/$pkgname"
}

