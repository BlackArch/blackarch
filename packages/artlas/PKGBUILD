# This file is part of BlackArch Linux ( http://blackarch.org ).
# See COPYING for license details.

pkgname='artlas'
pkgver=140.728aea5
pkgrel=1
pkgdesc='Apache Real Time Logs Analyzer System.'
groups=('blackarch' 'blackarch-defensive')
arch=('any')
url='https://github.com/mthbernardes/ARTLAS'
license=('MIT')
depends=('python2' 'python2-requests' 'python2-six' 'python2-urllib3'
         'python2-user-agents' 'python2-ua-parser' 'python2-pygtail'
         'python2-apache-log-parser' 'python2-py-zabbix' 'python2-telepot')
makedepends=('git')
source=('git+https://github.com/mthbernardes/ARTLAS.git')
sha1sums=('SKIP')

pkgver() {
  cd "$srcdir/ARTLAS"

  echo $(git rev-list --count HEAD).$(git rev-parse --short HEAD)
}

package() {
  cd "$srcdir/ARTLAS"

  mkdir -p "$pkgdir/usr/bin"
  mkdir -p "$pkgdir/usr/share/artlas"

  install -Dm644 -t "$pkgdir/usr/share/doc/artlas/" README.md
  install -Dm644 LICENSE "$pkgdir/usr/share/licenses/artlas/LICENSE"

  rm README.md LICENSE

  cp -a * "$pkgdir/usr/share/artlas/"

  cat > "$pkgdir/usr/bin/artlas" << EOF
#!/bin/sh
cd /usr/share/artlas
exec python2 artlas.py "\${@}"
EOF

  chmod a+x "$pkgdir/usr/bin/artlas"
}
