# This file is part of BlackArch Linux ( https://www.blackarch.org/ ).
# See COPYING for license details.

pkgname=maltrail
pkgver=1.1.r54.g788c7a9
pkgrel=1
epoch=2
pkgdesc='Malicious traffic detection system.'
groups=('blackarch' 'blackarch-defensive' 'blackarch-networking'
        'blackarch-sniffer')
arch=('any')
url='https://github.com/stamparm/maltrail'
license=('MIT')
depends=('python' 'python-pcapy' 'python-pysqlite3')
makedepends=('git')
source=("git+https://github.com/stamparm/$pkgname.git")
sha512sums=('SKIP')

pkgver() {
  cd $pkgname

  ( set -o pipefail
    git describe --long --tags --abbrev=7 2>/dev/null |
      sed 's/\([^-]*-g\)/r\1/;s/-/./g' ||
    printf "%s.%s" "$(git rev-list --count HEAD)" \
      "$(git rev-parse --short=7 HEAD)"
  )
}

package() {
  cd $pkgname

  install -dm 755 "$pkgdir/usr/bin"
  install -dm 755 "$pkgdir/usr/share/$pkgname"

  install -Dm 644 -t "$pkgdir/usr/share/doc/$pkgname/" README.md
  install -Dm 644 LICENSE "$pkgdir/usr/share/licenses/$pkgname/LICENSE"

  rm README.md LICENSE

  cp -a * "$pkgdir/usr/share/$pkgname/"

  cat > "$pkgdir/usr/bin/maltrail-sensor" << EOF
#!/bin/sh
cd /usr/share/$pkgname
exec python sensor.py "\$@"
EOF

  chmod +x "$pkgdir/usr/bin/maltrail-sensor"

  cat > "$pkgdir/usr/bin/maltrail-server" << EOF
#!/bin/sh
cd /usr/share/$pkgname
exec python server.py "\$@"
EOF

  chmod +x "$pkgdir/usr/bin/maltrail-server"
}

