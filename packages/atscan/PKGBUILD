# This file is part of BlackArch Linux ( http://blackarch.org ).
# See COPYING for license details.

pkgname='atscan'
pkgver=1830.e2dfd48
pkgrel=1
groups=('blackarch' 'blackarch-scanner' 'blackarch-webapp' 'blackarch-fuzzer'
        'blackarch-exploitation' 'blackarch-automation')
pkgdesc='Server, Site and Dork Scanner.'
arch=('any')
url='https://github.com/AlisamTechnology/ATSCAN-V3.1'
license=('MIT')
depends=('perl' 'perl-io-socket' 'perl-uri' 'perl-term-ansicolor'
         'perl-html-parser' 'perl-term-readkey' 'perl-http-cookies'
         'perl-getopt-long' 'perl-http-message' 'perl-encode-locale'
         'perl-file-copy-recursive')
makedepends=('git' 'dos2unix')
source=('git+https://github.com/AlisamTechnology/ATSCAN-V3.1.git')
sha1sums=('SKIP')

pkgver() {
  cd "$srcdir/ATSCAN-V3.1"

  echo $(git rev-list --count HEAD).$(git rev-parse --short HEAD)
}

prepare() {
  cd "$srcdir/ATSCAN-V3.1"

  dos2unix atscan.pl

  sed -i 's|defined @ARGV|@ARGV|' atscan.pl
}

package() {
  cd "$srcdir/ATSCAN-V3.1"

  mkdir -p "$pkgdir/usr/bin"
  mkdir -p "$pkgdir/usr/share/atscan"

  install -Dm644 -t "$pkgdir/usr/share/doc/atscan/" README.md

  rm README.md

  cp -a * "$pkgdir/usr/share/atscan/"

  cat > "$pkgdir/usr/bin/atscan" << EOF
#!/bin/sh
cd /usr/share/atscan
exec perl atscan.pl "\${@}"
EOF

  chmod a+x "$pkgdir/usr/bin/atscan"
}
