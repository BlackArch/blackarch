# This file is part of BlackArch Linux ( https://www.blackarch.org/ ).
# See COPYING for license details.

pkgname=atscan
pkgver=2138.7638c3c
pkgrel=1
groups=('blackarch' 'blackarch-scanner' 'blackarch-webapp' 'blackarch-fuzzer'
        'blackarch-exploitation' 'blackarch-automation')
pkgdesc='Server, Site and Dork Scanner.'
arch=('any')
url='https://github.com/AlisamTechnology/ATSCAN-V3.1'
license=('MIT')
depends=('perl' 'perl-io-socket' 'perl-uri' 'perl-term-ansicolor'
         'perl-html-parser' 'perl-term-readkey' 'perl-http-cookies'
         'perl-getopt-long' 'perl-http-message' 'perl-encode-locale'
         'perl-file-copy-recursive')
makedepends=('git' 'dos2unix')
source=('git+https://github.com/AlisamTechnology/ATSCAN-V3.1.git')
sha512sums=('SKIP')

pkgver() {
  cd ATSCAN-V3.1

  echo $(git rev-list --count HEAD).$(git rev-parse --short HEAD)
}

prepare() {
  cd ATSCAN-V3.1

  dos2unix atscan.pl

  sed -i 's|defined @ARGV|@ARGV|' atscan.pl
}

package() {
  cd ATSCAN-V3.1

  install -dm 755 "$pkgdir/usr/bin"
  install -dm 755 "$pkgdir/usr/share/$pkgname"

  install -Dm 644 -t "$pkgdir/usr/share/doc/$pkgname/" README.md

  rm README.md

  cp -a * "$pkgdir/usr/share/$pkgname/"

  cat > "$pkgdir/usr/bin/$pkgname" << EOF
#!/bin/sh
cd /usr/share/atscan
exec perl atscan.pl "\$@"
EOF

  chmod +x "$pkgdir/usr/bin/$pkgname"
}

