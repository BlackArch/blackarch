# This file is part of BlackArch Linux ( http://blackarch.org ).
# See COPYING for license details.

pkgname='atscan'
pkgver=292.6ddcf62
pkgrel=1
groups=('blackarch' 'blackarch-scanner' 'blackarch-webapp' 'blackarch-fuzzer'
        'blackarch-exploitation' 'blackarch-automation')
pkgdesc='Server, Site and Dork Scanner.'
arch=('any')
url='https://github.com/AlisamTechnology/ATSCAN-V3.1'
license=('custom:unknown')
depends=('perl' 'perl-io-socket')
makedepends=('git')
source=('git+https://github.com/AlisamTechnology/ATSCAN-V3.1.git')
sha1sums=('SKIP')

pkgver() {
  cd "$srcdir/ATSCAN-V3.1"

  echo $(git rev-list --count HEAD).$(git rev-parse --short HEAD)
}

prepare() {
  cd "$srcdir/ATSCAN-V3.1"

  sed -i 's|defined @ARGV|@ARGV|' atscan.pl
}

package() {
  cd "$srcdir/ATSCAN-V3.1"

  install -Dm755 atscan.pl "$pkgdir/usr/bin/atscan"
  install -Dm644 -t "$pkgdir/usr/share/doc/atscan/" README.md
}
