# This file is part of BlackArch Linux ( http://blackarch.org ).
# See COPYING for license details.

pkgname=('python-coveralls' 'python2-coveralls')
pkgver=1.5.1
pkgrel=1
pkgdesc='Show coverage stats online via coveralls.io.'
arch=('any')
url='https://github.com/coveralls-clients/coveralls-python'
license=('MIT')
makedepends=('python-setuptools' 'python2-setuptools')
source=("https://github.com/coveralls-clients/coveralls-python/archive/$pkgver.tar.gz")
sha512sums=('826c26de03abb1675f83a9a55979ede96e0f6368006d0e80a804cb921da542e99aff094c2ab3bfbf38186e6f073b366550353362f076c55ef1f746d288a77831')

package_python-coveralls() {
  depends=('python' 'python-coverage' 'python-docopt' 'python-requests')
  makedepends=('python-setuptools')

  cd "coveralls-python-$pkgver"

  python setup.py install --root="$pkgdir" --prefix=/usr --optimize=1
}

package_python2-coveralls() {
  depends=('python2' 'python2-coverage' 'python2-docopt' 'python2-requests')
  makedepends=('python2-setuptools')

  cd "coveralls-python-$pkgver"

  python2 setup.py install --root="$pkgdir" --prefix=/usr --optimize=1

  mv "$pkgdir/usr/bin/coveralls" "$pkgdir/usr/bin/coveralls2"
}

