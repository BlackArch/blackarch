# This file is part of BlackArch Linux ( https://www.blackarch.org/ ).
# See COPYING for license details.

pkgname=('python-coveralls' 'python2-coveralls')
_pkgname=coveralls
pkgver=1.9.2
pkgrel=1
pkgdesc='Show coverage stats online via coveralls.io.'
arch=('any')
url='https://github.com/coveralls-clients/coveralls-python'
license=('MIT')
makedepends=('python-setuptools' 'python2-setuptools')
source=("https://files.pythonhosted.org/packages/c8/d0/e4336140e37a5fe8b8372a29939d6328d20289e343bf885777fc1b075258/$_pkgname-$pkgver.tar.gz")
sha512sums=('a7bdad18672be7bef6353358722475c36dc0ad186f5a0a1fa41bdcb40ff042942743027aa42780b976c02d683564c687df4f3f354bd142f50c5654f76b7d9fc9')

package_python-coveralls() {
  depends=('python' 'python-coverage' 'python-docopt' 'python-requests')
  makedepends=('python-setuptools')

  cd "$_pkgname-$pkgver"

  python setup.py install --root="$pkgdir" --prefix=/usr --optimize=1
}

package_python2-coveralls() {
  depends=('python2' 'python2-coverage' 'python2-docopt' 'python2-requests')
  makedepends=('python2-setuptools')

  cd "$_pkgname-$pkgver"

  python2 setup.py install --root="$pkgdir" --prefix=/usr --optimize=1

  mv "$pkgdir/usr/bin/coveralls" "$pkgdir/usr/bin/coveralls2"
}

