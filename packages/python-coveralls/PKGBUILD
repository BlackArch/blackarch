# This file is part of BlackArch Linux ( https://www.blackarch.org/ ).
# See COPYING for license details.

pkgname=('python-coveralls' 'python2-coveralls')
pkgver=1.8.1
pkgrel=1
pkgdesc='Show coverage stats online via coveralls.io.'
arch=('any')
url='https://github.com/coveralls-clients/coveralls-python'
license=('MIT')
makedepends=('python-setuptools' 'python2-setuptools')
source=("https://files.pythonhosted.org/packages/4a/b1/cffad71ecfe92c1ea4b5ff9a958a234cf6d12868b9711f7027d127554995/coveralls-1.8.1.tar.gz")
sha512sums=('eb7f1914909c821e87fe988e2afcf793874171da92d7f766a74c3529ae1b2dc388bbc71cc6e6f1bd0accacdadbd270af91ec5e48a30458cc58e85044bf6c4d8c')

package_python-coveralls() {
  depends=('python' 'python-coverage' 'python-docopt' 'python-requests')
  makedepends=('python-setuptools')

  cd "coveralls-$pkgver"

  python setup.py install --root="$pkgdir" --prefix=/usr --optimize=1
}

package_python2-coveralls() {
  depends=('python2' 'python2-coverage' 'python2-docopt' 'python2-requests')
  makedepends=('python2-setuptools')

  cd "coveralls-$pkgver"

  python2 setup.py install --root="$pkgdir" --prefix=/usr --optimize=1

  mv "$pkgdir/usr/bin/coveralls" "$pkgdir/usr/bin/coveralls2"
}

