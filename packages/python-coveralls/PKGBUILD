# This file is part of BlackArch Linux ( https://www.blackarch.org/ ).
# See COPYING for license details.

pkgname=('python-coveralls' 'python2-coveralls')
pkgver=1.7.0
pkgrel=1
pkgdesc='Show coverage stats online via coveralls.io.'
arch=('any')
url='https://github.com/coveralls-clients/coveralls-python'
license=('MIT')
makedepends=('python-setuptools' 'python2-setuptools')
source=("https://files.pythonhosted.org/packages/af/76/5592d2757487de97d64865d9eb586046820ac90681a63137a40bfe20d650/coveralls-1.7.0.tar.gz")
sha512sums=('b77eada5029225e2c6c361c33736962069429f01b4ebe006afaae07a62f675b6144bc140157e3d6126309a62ca9837cec56ebcd02ad7c913bf050f5fdc38c30a')

package_python-coveralls() {
  depends=('python' 'python-coverage' 'python-docopt' 'python-requests')
  makedepends=('python-setuptools')

  cd "coveralls-$pkgver"

  python setup.py install --root="$pkgdir" --prefix=/usr --optimize=1
}

package_python2-coveralls() {
  depends=('python2' 'python2-coverage' 'python2-docopt' 'python2-requests')
  makedepends=('python2-setuptools')

  cd "coveralls-$pkgver"

  python2 setup.py install --root="$pkgdir" --prefix=/usr --optimize=1

  mv "$pkgdir/usr/bin/coveralls" "$pkgdir/usr/bin/coveralls2"
}

