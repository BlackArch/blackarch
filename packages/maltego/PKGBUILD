# This file is part of BlackArch Linux ( https://www.blackarch.org/ ).
# See COPYING for license details.

pkgname=maltego
pkgver=4.2.18.13878
_pkgname=Maltego
_pkgver=4.2.18
pkgrel=2
groups=('blackarch' 'blackarch-forensic' 'blackarch-recon' 'blackarch-scanner')
pkgdesc='An open source intelligence and forensics application, enabling to easily gather information about DNS, domains, IP addresses, websites, persons, etc.'
arch=('any')
url='http://www.paterva.com/web5'
license=('GPL')
depends=('java-environment=11' 'giflib')
source=("https://maltego-downloads.s3.us-east-2.amazonaws.com/linux/$_pkgname.v$pkgver.linux.zip")
sha512sums=('890495bf0fb6bcb9d603117e4c08919aac0822f322c3d65c5faa1086268b16bf322db70963e9f909aa48786b4476f744756296bdbc48eb32623d7c15892f276e')

package() {
  cd "${pkgname}_${_pkgver}"

  install -dm 755 "$pkgdir/usr/bin"
  install -dm 755 "$pkgdir/usr/share/$pkgname"

  cp -aR * "$pkgdir/usr/share/$pkgname/"

  cat > "$pkgdir/usr/bin/$pkgname" << EOF
#!/bin/sh
cd /usr/share/$pkgname/bin
exec ./$pkgname "\$@"
EOF

  chmod +x "$pkgdir/usr/bin/$pkgname"
}

