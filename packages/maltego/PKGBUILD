# This file is part of BlackArch Linux ( https://www.blackarch.org/ ).
# See COPYING for license details.

pkgname=maltego
pkgver=4.2.9.12898
_pkgname=Maltego
_pkgver=4.2.9
pkgrel=1
groups=('blackarch' 'blackarch-forensic' 'blackarch-recon' 'blackarch-scanner')
pkgdesc='An open source intelligence and forensics application, enabling to easily gather information about DNS, domains, IP addresses, websites, persons, etc.'
arch=('any')
url='http://www.paterva.com/web5'
license=('GPL')
depends=('java-environment' 'giflib')
source=("https://maltego-downloads.s3.us-east-2.amazonaws.com/linux/Maltego.v$pkgver.linux.zip")
sha512sums=('1c11a4726e3e0c6f08755ed3b9d1936aca34639688c0ae880128dc9dab1f8abb020df8ab7dce8d0119d722d782709fe825c78633ec72132bf6e8d82e822781a8')

package() {
  cd "${pkgname}_${_pkgver}"

  install -dm 755 "$pkgdir/usr/bin"
  install -dm 755 "$pkgdir/usr/share/$pkgname"

  cp -aR * "$pkgdir/usr/share/$pkgname/"

  cat > "$pkgdir/usr/bin/$pkgname" << EOF
#!/bin/sh
cd /usr/share/$pkgname/bin
exec ./$pkgname "\$@"
EOF

  chmod +x "$pkgdir/usr/bin/$pkgname"
}

