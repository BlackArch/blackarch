# This file is part of BlackArch Linux ( https://www.blackarch.org/ ).
# See COPYING for license details.

pkgname=maltego
pkgver=4.3.1
_pkgname=Maltego
_pkgver=4.3.1
pkgrel=1
groups=('blackarch' 'blackarch-forensic' 'blackarch-recon' 'blackarch-scanner')
pkgdesc='An open source intelligence and forensics application, enabling to easily gather information about DNS, domains, IP addresses, websites, persons, etc.'
arch=('any')
url='https://www.maltego.com/downloads/'
license=('GPL')
depends=('java-environment=11' 'giflib')
source=("https://maltego-downloads.s3.us-east-2.amazonaws.com/linux/$_pkgname.v$pkgver.linux.zip")
sha512sums=('dfd83f7e2f9cabe24a1fc8bee3902a479b38212eb82ff914b65e69ae9b00fd4cfe86d622054ff51edba714411f0bea2294d3e81e7fa4fe6e36a4c8d79490698b')

package() {
  cd "${pkgname}_${_pkgver}"

  install -dm 755 "$pkgdir/usr/bin"
  install -dm 755 "$pkgdir/usr/share/$pkgname"

  cp -aR * "$pkgdir/usr/share/$pkgname/"

  cat > "$pkgdir/usr/bin/$pkgname" << EOF
#!/bin/sh
cd /usr/share/$pkgname/bin
exec ./$pkgname "\$@"
EOF

  chmod +x "$pkgdir/usr/bin/$pkgname"
}

