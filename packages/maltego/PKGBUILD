# This file is part of BlackArch Linux ( https://www.blackarch.org/ ).
# See COPYING for license details.

pkgname=maltego
pkgver=4.5.0
_pkgname=Maltego
_pkgver=4.5.0
pkgrel=1
groups=('blackarch' 'blackarch-forensic' 'blackarch-recon' 'blackarch-scanner')
pkgdesc='An open source intelligence and forensics application, enabling to easily gather information about DNS, domains, IP addresses, websites, persons, etc.'
arch=('any')
url='https://www.maltego.com/downloads/'
license=('GPL')
depends=('java-environment=11' 'giflib')
source=("https://downloads.maltego.com/maltego-v4/linux/Maltego.v$pkgver.linux.zip")
sha512sums=('86ec433d424ec368395285693e460b8d842622df9a55c94ec90a94d04a33d33897dcb1b5ab741dfb29db92f5ff5d3cf712ca2a53106395696c38576fca4aeb39')

package() {
  cd "${pkgname}_${_pkgver}"

  install -dm 755 "$pkgdir/usr/bin"
  install -dm 755 "$pkgdir/usr/share/$pkgname"

  cp -aR * "$pkgdir/usr/share/$pkgname/"

  cat > "$pkgdir/usr/bin/$pkgname" << EOF
#!/bin/sh
cd /usr/share/$pkgname/bin
exec ./$pkgname "\$@"
EOF

  chmod +x "$pkgdir/usr/bin/$pkgname"
}

