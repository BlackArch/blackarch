# This file is part of BlackArch Linux ( https://www.blackarch.org/ ).
# See COPYING for license details.

pkgname=maltego
pkgver=4.2.6.12502
_pkgname=Maltego
_pkgver=4.2.6
pkgrel=1
groups=('blackarch' 'blackarch-forensic' 'blackarch-recon' 'blackarch-scanner')
pkgdesc='An open source intelligence and forensics application, enabling to easily gather information about DNS, domains, IP addresses, websites, persons, etc.'
arch=('any')
url='http://www.paterva.com/web5'
license=('GPL')
depends=('java-environment' 'giflib' 'python2')
source=("https://www.paterva.com/malv${_pkgver//./}/${_pkgname}.v${pkgver}.linux.zip")
sha512sums=('da41130fec221265e00fe2565df2b2395971eee76bb8568257c516b534e6fe38e4df78d1640031a1eab3776eeeee13c942e2318e0ff340f093bd155a04cfb561')

package() {
  cd "${pkgname}_${_pkgver}"

  install -dm 755 "$pkgdir/usr/bin"
  install -dm 755 "$pkgdir/usr/share/$pkgname"

  cp -aR * "$pkgdir/usr/share/$pkgname/"

  cat > "$pkgdir/usr/bin/$pkgname" << EOF
#!/bin/sh
cd /usr/share/$pkgname/bin
exec ./$pkgname "\$@"
EOF

  chmod +x "$pkgdir/usr/bin/$pkgname"
}

