# This file is part of BlackArch Linux ( https://www.blackarch.org/ ).
# See COPYING for license details.

pkgname=kismon
pkgver=1.0.1
pkgrel=1
pkgdesc='GUI client for kismet (wireless scanner/sniffer/monitor).'
groups=('blackarch' 'blackarch-wireless')
arch=('any')
url='https://www.salecker.org/software/kismon.html'
license=('BSD')
depends=('python' 'gtk3' 'gobject-introspection-runtime' 'python-simplejson'
         'python-cairo' 'osm-gps-map' 'python-gobject' 'python-requests'
         'python-lxml' 'kismet' 'python-kismet-rest')
makedepends=('python-setuptools')
source=("http://files.salecker.org/kismon/$pkgname-$pkgver.tar.gz")
sha512sums=('060c31149f558691395f43ddd506bf6e0c48cb370f5175c2b91a02dfed03cb95af21e51ea96147945b81716bd33e6367d89ad9dbec3c2c66143eec9ff10feba6')

build() {
  cd "$pkgname-$pkgver"

  python setup.py build
}

package() {
  cd "$pkgname-$pkgver"

  python setup.py install --root="$pkgdir" --prefix=/usr -O1 --skip-build

  install -Dm 644 COPYING "$pkgdir/usr/share/licenses/$pkgname/COPYING"
}

