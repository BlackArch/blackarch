# This file is part of BlackArch Linux ( https://www.blackarch.org/ ).
# See COPYING for license details.

pkgname=limesuite
pkgver=23.11.0.r19.g524cd2e
pkgrel=1
pkgdesc='Driver and GUI for LMS7002M-based SDR platforms.'
arch=('x86_64' 'aarch64')
groups=('blackarch' 'blackarch-radio')
url='https://github.com/myriadrf/LimeSuite'
license=('Apache-2.0')
depends=('glu' 'i2c-tools' 'libusb' 'soapysdr' 'sqlite'
         'wxwidgets-gtk3')
makedepends=('git' 'cmake')
source=("git+https://github.com/myriadrf/$pkgname.git")
sha512sums=('SKIP')

pkgver() {
  cd $pkgname

  ( set -o pipefail
    git describe --long --tags --abbrev=7 2>/dev/null |
      sed 's/\([^-]*-g\)/r\1/;s/-/./g' |
      sed 's/^v//' ||
    printf "%s.%s" "$(git rev-list --count HEAD)" \
      "$(git rev-parse --short=7 HEAD)"
  )
}

build() {
  cd $pkgname

  cmake -B build -S . \
    -DCMAKE_BUILD_TYPE=Release \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DCMAKE_INSTALL_LIBDIR=lib
  make -C build
}

package() {
  cd $pkgname

  make -C build DESTDIR="$pkgdir" install

  install -Dm 644 -t "$pkgdir/usr/share/doc/$pkgname/" docs/*.pdf *.md
}

