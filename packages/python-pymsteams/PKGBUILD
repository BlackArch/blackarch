# This file is part of BlackArch Linux ( https://www.blackarch.org/ ).
# See COPYING for license details.

pkgname=python-pymsteams
_pkgname=${pkgname#python-}
pkgver=0.2.5
pkgrel=1
pkgdesc='Format messages and post to Microsoft Teams.'
arch=('any')
url='https://pypi.org/project/pymsteams/#files'
license=('Apache-2.0')
depends=('python' 'python-requests')
makedepends=('python-build' 'python-installer' 'python-setuptools'
             'python-wheel' 'python-setuptools-scm')
options=(!emptydirs)
source=("https://files.pythonhosted.org/packages/source/${_pkgname::1}/$_pkgname/$_pkgname-$pkgver.tar.gz")
sha512sums=('33f496f1307671d3dfeaa46623bc3614aa207690b89976ff884bd49343bcd0fbb82e81bf57fe15afa92f723aba34355af95b752cd2ec9e60e5bfc8a416df302d')

build() {
  cd "$_pkgname-$pkgver"

  python -m build --wheel --no-isolation
}

package() {
  cd "$_pkgname-$pkgver"

  python -m installer --destdir="$pkgdir" dist/*.whl
}

