# This file is part of BlackArch Linux ( http://blackarch.org ).
# See COPYING for license details.

pkgname=amass
pkgver=2.8.0
pkgrel=1
epoch=2
pkgdesc="In-depth subdomain enumeration written in Go."
arch=('any')
groups=('blackarch' 'blackarch-scanner' 'blackarch-recon')
url='https://github.com/OWASP/Amass'
license=('Apache2')
makedepends=('go' 'git')
source=("https://github.com/OWASP/Amass/archive/$pkgver.tar.gz")
sha512sums=('c62abe61f21e3fd6506042e3774a960716de677be0f9f2e57c61c8c0075cb4bff8535948f1795dfedab5a4c0d6a229a20f8a17aca579bbf1471639e12ab58a3f')

prepare() {
  mkdir -p "gopath/src/github.com/OWASP"
  ln -rTsf "Amass-$pkgver" "gopath/src/github.com/OWASP/Amass"

  export GOPATH="$srcdir/gopath"
  cd "$GOPATH/src/github.com/OWASP/Amass"
  go get -d ./...
}

build() {
  export GOPATH="$srcdir/gopath"
  cd "$GOPATH/src/github.com/OWASP/Amass"

  go install -v ./...
}

package() {
  export GOPATH="$srcdir/gopath"
  cd "$GOPATH/bin"
	
	install -dm 755 "$pkgdir/usr/share/$pkgname"

  install -Dm 755 "$pkgname" "$pkgdir/usr/bin/$pkgname"
  install -Dm 755 "$pkgname.netdomains" "$pkgdir/usr/bin/$pkgname.netdomains"
  install -Dm 755 "$pkgname.viz" "$pkgdir/usr/bin/$pkgname.viz"
  install -Dm 755 "$pkgname.db" "$pkgdir/usr/bin/$pkgname.db"
	cp -a --no-preserve=ownership "$GOPATH/src/github.com/OWASP/Amass/"{wordlists/,examples/,snapcraft.yaml} "$pkgdir/usr/share/$pkgname"

	chmod 644 "$pkgdir/usr/share/$pkgname/"{wordlists/*,examples/*}
}

