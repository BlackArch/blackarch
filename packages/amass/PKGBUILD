# This file is part of BlackArch Linux ( https://www.blackarch.org/ ).
# See COPYING for license details.

pkgname=amass
pkgver=2.8.7
pkgrel=1
epoch=2
pkgdesc='In-depth subdomain enumeration written in Go.'
arch=('any')
groups=('blackarch' 'blackarch-scanner' 'blackarch-recon')
url='https://github.com/OWASP/Amass'
license=('Apache2')
makedepends=('go' 'git')
source=("https://github.com/OWASP/Amass/archive/$pkgver.tar.gz")
sha512sums=('2569319e54062ade745696bcf48c157613bf3aeb0f2e69800c2a42c16a1bbc2548407294d5860b95d6699edf1545eb1d942cfd1070b8b1d2cf62f6561496fb0a')

prepare() {
  mkdir -p "gopath/src/github.com/OWASP"
  ln -rTsf "Amass-$pkgver" "gopath/src/github.com/OWASP/Amass"

  export GOPATH="$srcdir/gopath"
  cd "$GOPATH/src/github.com/OWASP/Amass"

  go get -d ./...
}

build() {
  export GOPATH="$srcdir/gopath"
  cd "$GOPATH/src/github.com/OWASP/Amass"

  go install -v ./...
}

package() {
  export GOPATH="$srcdir/gopath"
  cd "$GOPATH/bin"

  install -dm 755 "$pkgdir/usr/share/$pkgname"

  install -Dm 755 $pkgname "$pkgdir/usr/bin/$pkgname"
  install -Dm 755 "$pkgname.netdomains" "$pkgdir/usr/bin/$pkgname.netdomains"
  install -Dm 755 "$pkgname.viz" "$pkgdir/usr/bin/$pkgname.viz"
  install -Dm 755 "$pkgname.db" "$pkgdir/usr/bin/$pkgname.db"
  cp -a --no-preserve=ownership "$GOPATH/src/github.com/OWASP/Amass/"{wordlists/,examples/,snapcraft.yaml} "$pkgdir/usr/share/$pkgname"

  chmod 644 "$pkgdir/usr/share/$pkgname/"{wordlists/*,examples/*}
}

