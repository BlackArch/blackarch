# This file is part of BlackArch Linux ( https://www.blackarch.org/ ).
# See COPYING for license details.

pkgname=amass
pkgver=2.8.5
pkgrel=1
epoch=2
pkgdesc='In-depth subdomain enumeration written in Go.'
arch=('any')
groups=('blackarch' 'blackarch-scanner' 'blackarch-recon')
url='https://github.com/OWASP/Amass'
license=('Apache2')
makedepends=('go' 'git')
source=("https://github.com/OWASP/Amass/archive/$pkgver.tar.gz")
sha512sums=('c5976b02e2b962f589a23542846eaff1e3508e32ee97fa2b1e295422691515711eb5ad2c78b70c9f3b5b5d6f041a25610ad015288f31d73469f3d7f6f6e53927')

prepare() {
  mkdir -p "gopath/src/github.com/OWASP"
  ln -rTsf "Amass-$pkgver" "gopath/src/github.com/OWASP/Amass"

  export GOPATH="$srcdir/gopath"
  cd "$GOPATH/src/github.com/OWASP/Amass"

  go get -d ./...
}

build() {
  export GOPATH="$srcdir/gopath"
  cd "$GOPATH/src/github.com/OWASP/Amass"

  go install -v ./...
}

package() {
  export GOPATH="$srcdir/gopath"
  cd "$GOPATH/bin"

  install -dm 755 "$pkgdir/usr/share/$pkgname"

  install -Dm 755 $pkgname "$pkgdir/usr/bin/$pkgname"
  install -Dm 755 "$pkgname.netdomains" "$pkgdir/usr/bin/$pkgname.netdomains"
  install -Dm 755 "$pkgname.viz" "$pkgdir/usr/bin/$pkgname.viz"
  install -Dm 755 "$pkgname.db" "$pkgdir/usr/bin/$pkgname.db"
  cp -a --no-preserve=ownership "$GOPATH/src/github.com/OWASP/Amass/"{wordlists/,examples/,snapcraft.yaml} "$pkgdir/usr/share/$pkgname"

  chmod 644 "$pkgdir/usr/share/$pkgname/"{wordlists/*,examples/*}
}

