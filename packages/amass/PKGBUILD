# This file is part of BlackArch Linux ( https://www.blackarch.org/ ).
# See COPYING for license details.

pkgname=amass
_pkgname=Amass
pkgver=1099.22dd146
pkgrel=1
epoch=2
pkgdesc='In-depth subdomain enumeration written in Go.'
arch=('x86_64' 'aarch64')
groups=('blackarch' 'blackarch-scanner' 'blackarch-recon')
url='https://github.com/OWASP/Amass'
license=('Apache')
makedepends=('git' 'go-pie')
source=('git+https://github.com/OWASP/Amass.git')
sha512sums=('SKIP')

pkgver() {
  cd $_pkgname

  echo $(git rev-list --count HEAD).$(git rev-parse --short HEAD)
}

prepare() {
  mkdir -p "gopath/src/github.com/OWASP"
  ln -rTsf $_pkgname "gopath/src/github.com/OWASP/$_pkgname"

  export GOPATH="$srcdir/gopath"
  cd "$GOPATH/src/github.com/OWASP/$_pkgname"

  go get -d ./...
}

build() {
  export GOPATH="$srcdir/gopath"
  cd "$GOPATH/src/github.com/OWASP/$_pkgname"

  go install -v ./...
}

package() {
  export GOPATH="$srcdir/gopath"

  install -dm 755 "$pkgdir/usr/share/$pkgname"

  install -Dm 755 "$GOPATH/bin/$pkgname" "$pkgdir/usr/bin/$pkgname"

  cp -a --no-preserve=ownership \
    "$GOPATH/src/github.com/OWASP/$_pkgname/examples" \
    "$pkgdir/usr/share/$pkgname"

  mv "$pkgdir/usr/share/$pkgname/examples/wordlists" "$pkgdir/usr/share/$pkgname"

  chmod 644 "$pkgdir/usr/share/$pkgname/"{wordlists/*,examples/*}
}

