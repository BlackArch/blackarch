# This file is part of BlackArch Linux ( http://blackarch.org ).
# See COPYING for license details.

pkgname=amass
pkgver=196.d159de0
pkgrel=1
groups=('blackarch' 'blackarch-scanner' 'blackarch-recon')
pkgdesc='In-depth subdomain enumeration written in Go.'
arch=('x86_64' 'armv6h' 'armv7h' 'aarch64')
url='https://github.com/caffix/amass'
license=('Apache2')
depends=('go')
makedepends=('git' 'go-pie')
source=('git+https://github.com/caffix/amass.git')
sha512sums=('SKIP')
_url='github.com/caffix/amass'

pkgver() {
  cd amass

  echo $(git rev-list --count HEAD).$(git rev-parse --short HEAD)
}

build() {
  cd amass/amass

  GOPATH="$srcdir" go get -d $_url
}

package() {
  cd amass/amass

  install -dm 755 "$pkgdir/usr/share/${pkgname}"

  GOPATH="$srcdir" go build -v $_url

  cd ..

  install -Dm 755 amass/amass "$pkgdir/usr/bin/amass"
  cp -a wordlists examples snapcraft.yaml "$pkgdir/usr/share/${pkgname}"
  install -Dm 644 -t "$pkgdir/usr/share/doc/${pkgname}/" README.md
  install -Dm 644 LICENSE "$pkgdir/usr/share/licenses/${pkgname}/LICENSE"

  chmod 644 "$pkgdir/usr/share/${pkgname}/"{wordlists/*,examples/*}
}

