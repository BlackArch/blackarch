pkgname=xssless
pkgver=r24.59732fb
pkgrel=1
pkgdesc="An automated XSS payload generator written in python."
url="https://github.com/mandatoryprogrammer/xssless"
arch=('any')
license=('GPLv2')
depends=('python2' 'python2-beautifulsoup4')
source=("git+https://github.com/mandatoryprogrammer/xssless.git")
md5sums=('SKIP')

pkgver() {
  cd "$srcdir/xssless"
  printf "r%s.%s" "$(git rev-list --count HEAD)" "$(git rev-parse --short HEAD)"
}

prepare(){
  grep -iRl 'python' "$srcdir/xssless" | xargs sed -i 's|#!.*/usr/bin/python|#!/usr/bin/python2|;s|#!.*/usr/bin/env python$|#!/usr/bin/env python2|'
}

package() {
  cd "$srcdir/xssless"
  
  # Base directories.
  install -dm755 "$pkgdir/usr/share/$pkgname"
  install -dm755 "$pkgdir/usr/bin/"

  cp -a --no-preserve=ownership * "$pkgdir/usr/share/$pkgname"

cat > "$pkgdir/usr/bin/xssless" << EOF
#!/bin/sh
python2 /usr/share/xssless/xssless.py "\$@"
EOF
 chmod +x "$pkgdir/usr/bin/xssless"
}

