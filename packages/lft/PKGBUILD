# This file is part of BlackArch Linux ( http://blackarch.org ).
# See COPYING for license details.

pkgname=lft
pkgver=3.8
pkgrel=1
epoch=1
groups=('blackarch' 'blackarch-recon' 'blackarch-networking')
pkgdesc='A layer four traceroute implementing numerous other features.'
arch=('x86_64' 'armv6h' 'armv7h' 'aarch64')
license=('custom')
url='http://pwhois.org/lft/'
depends=('libpcap')
source=("http://pwhois.org/get/lft-${pkgver}.tar.gz")
sha512sums=('13ea176362096cc8c20500d61969483642f5d003263b529ec8d601786f621341107859510f9cafa5d365417beafce897bcc9cd631171e291ca3dc07fec1bd483')

build() {
  cd "$srcdir/lft-$pkgver"

  if [[ "$CARCH" == "x86_64" ]];
  then
    ./configure --prefix=/usr
  else
    ./configure --prefix=/usr --build=arm
  fi

  make
}

package() {
  cd "$srcdir/lft-$pkgver"

  make prefix="$pkgdir/usr" install

  install -Dm644 COPYING "$pkgdir/usr/share/licenses/lft/COPYING"
}
