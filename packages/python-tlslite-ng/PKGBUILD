# This file is part of BlackArch Linux ( https://www.blackarch.org/ ).
# See COPYING for license details.

pkgbase=python-tlslite-ng
pkgname=('python2-tlslite-ng' 'python-tlslite-ng')
_pkgname=tlslite-ng
pkgver=0.7.6
pkgrel=1
epoch=1
pkgdesc='Pure python implementation of SSL and TLS.'
arch=('any')
url='https://pypi.org/project/tlslite-ng/#files'
license=('APACHE')
makedepends=('python2-setuptools' 'python-setuptools')
source=("https://files.pythonhosted.org/packages/cd/95/4311e6b70ded82035b7f3a92bfe5ea350e6d9effe925493ac31ccaf924cc/$_pkgname-$pkgver.tar.gz")
sha512sums=('cc9d3119b9f781742b328bf1c3287fc01b96f9b9e0f77d5a168b070a84d301f9a8d84b203461e13c3799b20d255af3d02c7e5cf8cffca844da598b04dfed9999')

prepare() {
  cp -a "$_pkgname-$pkgver"{,-2}
}

build() {
  cd "$_pkgname-$pkgver-2"

  python2 setup.py build

  cd "$srcdir/$_pkgname-$pkgver"

  python setup.py build
}

package_python2-tlslite-ng() {
  depends=('python2' 'python2-ecdsa')

  cd "$_pkgname-$pkgver-2"

  python2 setup.py install --prefix=/usr --root="$pkgdir" --optimize=1 \
    --skip-build

  for i in "$pkgdir/usr/bin/"* ; do
    mv $i "${i}2"
  done
}

package_python-tlslite-ng() {
  depends=('python' 'python-ecdsa')

  cd "$_pkgname-$pkgver"

  python setup.py install --prefix=/usr --root="$pkgdir" --optimize=1 \
    --skip-build
}

