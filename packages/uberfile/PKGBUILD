# This file is part of BlackArch Linux ( https://www.blackarch.org/ ).
# See COPYING for license details.

pkgname=uberfile
pkgver=14.4414c2a
pkgrel=9
pkgdesc='CLI tool for the generation of downloader oneliners for UNIX-like or Windows systems.'
arch=('any')
groups=('blackarch' 'blackarch-misc')
url='https://github.com/ShutdownRepo/uberfile'
license=('GPL-3.0-or-later')
depends=('python' 'python-psutil' 'python-console-menu' 'python-colorama'
         'python-simple-term-menu')
makedepends=('git' 'python-setuptools')
source=("git+https://github.com/ShutdownRepo/$pkgname.git")
sha512sums=('SKIP')

pkgver() {
  cd $pkgname

  ( set -o pipefail
    git describe --long --tags --abbrev=7 2>/dev/null |
      sed 's/\([^-]*-g\)/r\1/;s/-/./g' ||
    printf "%s.%s" "$(git rev-list --count HEAD)" \
      "$(git rev-parse --short=7 HEAD)"
  )
}

build() {
  cd $pkgname

  python setup.py build
}

package() {
  cd $pkgname

  python setup.py install --root="$pkgdir" --prefix=/usr -O1 --skip-build
}

