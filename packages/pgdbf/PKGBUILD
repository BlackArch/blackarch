# This file is part of BlackArch Linux ( http://blackarch.org ).
# See COPYING for license details.

pkgname='pgdbf'
pkgver=113.4e84775
pkgrel=1
groups=('blackarch' 'blackarch-database')
pkgdesc='Convert XBase / FoxPro databases to PostgreSQL'
url='https://github.com/kstrauser/pgdbf'
arch=('x86_64' 'armv6h' 'armv7h' 'aarch64')
license=('GPL3')
makedepends=('git')
source=('git+https://github.com/kstrauser/pgdbf')
sha1sums=('SKIP')

pkgver() {
  cd "$srcdir/pgdbf"

  echo $(git rev-list --count HEAD).$(git rev-parse --short HEAD)
}

build() {
  cd "$srcdir/pgdbf"

  aclocal
  autoconf
  automake --add-missing

  if [[ "$CARCH" == "i686" || "$CARCH" == "x86_64" ]];
  then
    ./configure --prefix=/usr
  else
    ./configure --build=arm --prefix=/usr
  fi

	make
}

package() {
  cd "$srcdir/pgdbf/src"

  install -Dm755 pgdbf "$pkgdir/usr/bin/pgdbf"
}
