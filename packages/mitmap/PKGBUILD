# This file is part of BlackArch Linux ( https://www.blackarch.org/ ).
# See COPYING for license details.

pkgname=mitmap
pkgver=89.b590f9a
pkgrel=2
pkgdesc='A python program to create a fake AP and sniff data.'
groups=('blackarch' 'blackarch-wireless' 'blackarch-sniffer')
arch=('any')
url='https://github.com/xdavidhu/mitmAP'
license=('MIT')
depends=('python' 'python-dnspython' 'python-pcapy' 'python-twisted' 'sslstrip')
makedepends=('git')
source=("$pkgname::git+https://github.com/xdavidhu/mitmAP.git")
sha512sums=('SKIP')

pkgver() {
  cd $pkgname

  echo $(git rev-list --count HEAD).$(git rev-parse --short HEAD)
}

package() {
  cd $pkgname

  install -dm 755 "$pkgdir/usr/bin"
  install -dm 755 "$pkgdir/usr/share/$pkgname"

  install -Dm 644 -t "$pkgdir/usr/share/doc/$pkgname/" README.md
  install -Dm 644 LICENSE "$pkgdir/usr/share/licenses/$pkgname/LICENSE"

  rm README.md LICENSE

  cp -a * "$pkgdir/usr/share/$pkgname/"

  cat > "$pkgdir/usr/bin/$pkgname" << EOF
#!/bin/sh
cd /usr/share/$pkgname
exec python mitmAP.py "\$@"
EOF

  chmod +x "$pkgdir/usr/bin/$pkgname"
}

