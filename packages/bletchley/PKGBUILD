# This file is part of BlackArch Linux ( http://blackarch.org ).
# See COPYING for license details.

pkgname='bletchley'
pkgver='0.0.1'
pkgrel=8
groups=('blackarch' 'blackarch-crypto')
pkgdesc='A collection of practical application cryptanalysis tools.'
arch=('x86_64' 'armv6h' 'armv7h' 'aarch64')
url='https://code.google.com/p/bletchley/'
license=('GPL')
depends=('python3' 'python-requests')
makedepends=('subversion' 'scons')
source=('svn+https://code.blindspotsecurity.com/dav/bletchley/trunk/')
#source=("https://googledrive.com/host/0B6HCWPzk_jKoVHI5WV9EMURfbms/bletchley-src-${pkgver}.tar.gz")
sha1sums=('SKIP')

build() {
  cd "$srcdir/trunk"

  scons
}

package() {
  cd "$srcdir/trunk"

  tools="bletchley-analyze bletchley-clonecertchain bletchley-decode"
  tools="${tools} bletchley-encode bletchley-http2py bletchley-nextrand"

  mkdir -p "$pkgdir/usr/bin"
  mkdir -p "$pkgdir/usr/share/bletchley"

  PREFIX="$pkgdir" scons install

  install -Dm644 INSTALL "$pkgdir/usr/share/doc/bletchley/INSTALL"
  install -m644 doc/TODO "$pkgdir/usr/share/doc/bletchley/TODO"
  install -Dm644 LICENSE "$pkgdir/usr/share/licenses/bletchley/LICENSE"

  rm -rf build doc src bin INSTALL LICENSE SConstruct

  cp -a * "$pkgdir/usr/share/bletchley/"

  mv "$pkgdir/bin/"* "$pkgdir/usr/share/bletchley/lib/"
  rmdir "$pkgdir/bin"

  for t in ${tools}
  do
    cat > "$pkgdir/usr/bin/${t}" << EOF
#!/bin/sh
cd /usr/share/bletchley/lib
exec python3 ${t} "\${@}"
EOF
    chmod a+x "$pkgdir/usr/bin/${t}"
  done
}
