# This file is part of BlackArch Linux ( https://www.blackarch.org/ ).
# See COPYING for license details.

pkgname=httpscreenshot
pkgver=70.f081c69
pkgrel=6
pkgdesc='A tool for grabbing screenshots and HTML of large numbers of websites.'
arch=('any')
groups=('blackarch' 'blackarch-misc')
url='https://github.com/breenmachine/httpscreenshot'
license=('custom:unknown')
depends=('python' 'python-selenium' 'python-pillow' 'python-python-libnmap'
         'python-m2crypto' 'python-requests' 'python-beautifulsoup4'
         'python-pyvirtualdisplay' 'python-lxml' 'python-webdriver-manager')
makedepends=('git')
source=("git+https://github.com/breenmachine/$pkgname.git")
sha512sums=('SKIP')

pkgver() {
  cd $pkgname

  ( set -o pipefail
    git describe --long --tags --abbrev=7 2>/dev/null |
      sed 's/\([^-]*-g\)/r\1/;s/-/./g' ||
    printf "%s.%s" "$(git rev-list --count HEAD)" \
      "$(git rev-parse --short=7 HEAD)"
  )
}

package() {
  cd $pkgname

  install -dm 755 "$pkgdir/usr/bin"
  install -dm 755 "$pkgdir/usr/share/$pkgname"

  install -Dm 644 -t "$pkgdir/usr/share/doc/$pkgname/" README.md

  rm README.md

  cp -a * "$pkgdir/usr/share/$pkgname/"

  cat > "$pkgdir/usr/bin/$pkgname" << EOF
#!/bin/sh
cd /usr/share/$pkgname
exec python $pkgname.py "\$@"
EOF

  chmod +x "$pkgdir/usr/bin/$pkgname"
}

