# This file is part of BlackArch Linux ( http://blackarch.org ).
# See COPYING for license details.

pkgname='httpscreenshot'
pkgver=53.888faaf
pkgrel=1
pkgdesc='A tool for grabbing screenshots and HTML of large numbers of websites.'
groups=('blackarch' 'blackarch-misc')
arch=('any')
url='https://github.com/breenmachine/httpscreenshot'
license=('custom:unknown')
depends=('python2' 'python2-selenium' 'python2-pillow' 'python2-python-libnmap'
         'python2-m2crypto' 'python2-requests' 'python2-beautifulsoup4'
         'python2-requesocks' 'python2-pyvirtualdisplay')
makedepends=('git')
source=('git+https://github.com/breenmachine/httpscreenshot.git')
sha1sums=('SKIP')

pkgver() {
  cd "$srcdir/httpscreenshot"

  echo $(git rev-list --count HEAD).$(git rev-parse --short HEAD)
}

package() {
  cd "$srcdir/httpscreenshot"

  mkdir -p "$pkgdir/usr/bin"
  mkdir -p "$pkgdir/usr/share/httpscreenshot"

  install -Dm644 -t "$pkgdir/usr/share/doc/httpscreenshot/" README.md

  rm README.md

  cp -a * "$pkgdir/usr/share/httpscreenshot/"

  cat > "$pkgdir/usr/bin/httpscreenshot" << EOF
#!/bin/sh
cd /usr/share/httpscreenshot
exec python2 httpscreenshot.py "\${@}"
EOF

  chmod a+x "$pkgdir/usr/bin/httpscreenshot"
}
