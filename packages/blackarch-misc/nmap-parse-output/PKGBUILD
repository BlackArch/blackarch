# This file is part of BlackArch Linux ( https://www.blackarch.org/ ).
# See COPYING for license details.

pkgname=nmap-parse-output
pkgver=23.6405abf
pkgrel=1
pkgdesc='Converts/manipulates/extracts data from a nmap scan output.'
groups=('blackarch' 'blackarch-misc')
arch=('any')
url='https://github.com/hahwul/nmap-parse-output'
license=('BSD')
depends=('bash' 'libxslt')
makedepends=('git')
source=("git+https://github.com/ernw/$pkgname.git")
sha512sums=('SKIP')

pkgver() {
  cd $pkgname

  echo $(git rev-list --count HEAD).$(git rev-parse --short HEAD)
}

package() {
  cd $pkgname

  install -dm 755 "$pkgdir/usr/bin"
  install -dm 755 "$pkgdir/usr/share/$pkgname"

  install -Dm 644 -t "$pkgdir/usr/share/doc/$pkgname/" README.md
  install -Dm 644 LICENSE "$pkgdir/usr/share/licenses/$pkgname/LICENSE"

  rm README.md LICENSE

  cp -a * "$pkgdir/usr/share/$pkgname/"

  ln -sf "/usr/share/$pkgname/$pkgname" "$pkgdir/usr/bin/$pkgname"
}

