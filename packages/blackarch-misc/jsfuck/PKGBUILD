# This file is part of BlackArch Linux ( https://www.blackarch.org/ ).
# See COPYING for license details.

pkgname=jsfuck
pkgver=222.94c3835
pkgrel=1
pkgdesc='Write any JavaScript with 6 Characters: []()!+.'
arch=('any')
groups=('blackarch' 'blackarch-misc')
url='https://github.com/aemkei/jsfuck'
license=('custom:WTFPL')
depends=('php')
makedepends=('git')
source=("git+https://github.com/aemkei/$pkgname.git")
sha512sums=('SKIP')

pkgver() {
  cd $pkgname

  echo $(git rev-list --count HEAD).$(git rev-parse --short HEAD)
}

package() {
  cd $pkgname

  install -dm 755 "$pkgdir/usr/bin"
  install -dm 755 "$pkgdir/usr/share/$pkgname"

  install -Dm 644 -t "$pkgdir/usr/share/doc/$pkgname/" README.md
  install -Dm 644 LICENSE.txt "$pkgdir/usr/share/licenses/$pkgname/LICENSE"

  rm README.md LICENSE.txt

  cp -a --no-preserve=ownership * "$pkgdir/usr/share/$pkgname/"

  cat > "$pkgdir/usr/bin/$pkgname" << EOF
#!/bin/sh
if [ -z "\$@" ]; then
    echo "Usage: $pkgname <PORT>"
    exit 1
fi

cd /usr/share/$pkgname
exec php -S 127.0.0.1:\$@
EOF

  chmod +x "$pkgdir/usr/bin/$pkgname"
}

