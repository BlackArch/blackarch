# This file is part of BlackArch Linux ( https://www.blackarch.org/ ).
# See COPYING for license details.

pkgname=trid
pkgver=2.47
pkgrel=2
pkgdesc='An utility designed to identify file types from their binary signatures.'
arch=('any')
groups=('blackarch' 'blackarch-forensic' 'blackarch-binary')
url='http://mark0.net/soft-trid-e.html'
license=('custom:unknown')
depends=('python' 'trid-defs')
source=("https://mark0.net/download/trid.zip")
sha512sums=('e542f137ebe02ef4642a46e2138ae345ed3308656130b79d12e8d6d62253802191df78de704b441946c7d9ffb171199d9d4c3c6f85f955bcea43f0602b9c6a07')

package() {
  install -dm 755 "$pkgdir/usr/bin"

  install -Dm 755 "$pkgname.py" "$pkgdir/usr/share/$pkgname/$pkgname.py"
  install -Dm 644 -t "$pkgdir/usr/share/doc/$pkgname/" readme.txt

  cat > "$pkgdir/usr/bin/$pkgname" << EOF
#!/bin/sh
exec /usr/share/$pkgname/$pkgname.py -d:/usr/share/$pkgname/triddefs.trd "\$@"
EOF

  chmod +x "$pkgdir/usr/bin/$pkgname"
}

