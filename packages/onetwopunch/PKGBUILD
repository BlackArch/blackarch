# This file is part of BlackArch Linux ( http://blackarch.org ).
# See COPYING for license details.

pkgname=onetwopunch
pkgver=v1.0.0.r2.gd4ab4e8
pkgrel=1
pkgdesc='Use unicornscan to quickly scan all open ports, and then pass the open ports to nmap for detailed scans.'
groups=('blackarch' 'blackarch-scanner')
arch=('any')
url='https://github.com/superkojiman/onetwopunch'
license=('MIT')
depends=('nmap' 'unicornscan')
makedepends=('git')
source=('git+https://github.com/superkojiman/onetwopunch.git')
sha512sums=('SKIP')

pkgver() {
  cd "$srcdir/onetwopunch"

  git describe --tags --long | sed 's/\([^-]*-g\)/r\1/;s/-/./g'
}

package() {
  cd "$srcdir/onetwopunch"

  install -Dm755 onetwopunch.sh "$pkgdir/usr/bin/onetwopunch"
  install -Dm644 -t "$pkgdir/usr/share/doc/onetwopunch/" README.md
  install -Dm644 LICENSE.md "$pkgdir/usr/share/licenses/onetwopunch/LICENSE"
}
