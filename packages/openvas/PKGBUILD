# This file is part of BlackArch Linux ( https://www.blackarch.org/ ).
# See COPYING for license details.

# Old Maintainer  : Christian Rebischke <chris.rebischke@archlinux.org>
# Old Maintainer  : Levente Polyak <anthraxx[at]archlinux[dot]org>
# Old Contributor : Daniel Micay <danielmicay@gmail.com>

pkgname=openvas
pkgver=20.8.0
pkgrel=1
pkgdesc='The OpenVAS scanning Daemon.'
groups=('blackarch' 'blackarch-scanner' 'blackarch-recon' 'blackarch-fuzzer'
        'blackarch-exploitation')
arch=('x86_64')
url='https://github.com/greenbone/openvas/'
license=('GPL')
depends=('gvm-libs' 'redis')
makedepends=('cmake' 'doxygen')
source=("$pkgname-$pkgver.tar.gz::https://github.com/greenbone/$pkgname/archive/v$pkgver.tar.gz"
        "https://github.com/greenbone/$pkgname-scanner/releases/download/v$pkgver/$pkgname-$pkgver.tar.gz.sig"
        "$pkgname.service")
sha512sums=('179a38e96768a4e922fd978d6d47c9a2f6902fd4bb2b25f03b999001eb00ba06cacfb5be098b9909f67135c879a210d2b900bbb28b3aea467e379f2d1aec69f3'
            'SKIP'
            '574a9125928e672a9a22a00cab9c635897eedad0eaae550295c21d45cabd52d1d1cd376f441665d37fd1bb9fa48360039f553c300eb15dd9b83cf14df7f37de5')
validpgpkeys=(
              '8AE4BE429B60A59B311C2E739823FAA60ED1E580' # GVM Transfer Integrity
)
changelog=CHANGELOG.md

build() {
  cd "$pkgname-$pkgver"

  cmake -DCMAKE_BUILD_TYPE=Release -DSBINDIR=/usr/bin -DLIBDIR=/usr/lib \
    -DCMAKE_INSTALL_PREFIX=/usr -DSYSCONFDIR=/etc -DLOCALSTATEDIR=/var .

  make
}

package() {
  cd "$pkgname-$pkgver"

  make DESTDIR="$pkgdir" install

  install -Dm 644 "$srcdir/$pkgname.service" \
    "$pkgdir/usr/lib/systemd/system/$pkgname.service"

  #mv "$pkgdir/usr/lib64/"* "$pkgdir/usr/lib"
  #rmdir "$pkgdir/usr/lib64"
  #ln -s /usr/lib "$pkgdir/usr/lib64"
}

