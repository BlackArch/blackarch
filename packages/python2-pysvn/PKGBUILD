# This file is part of BlackArch Linux ( http://blackarch.org ).
# See COPYING for license details.

pkgname='python2-pysvn'
pkgver='1.7.10'
pkgrel=2
pkgdesc='Python2 SVN Extension.'
arch=('any')
url='http://pysvn.tigris.org/'
license=('APACHE')
depends=('python2>=2.7' 'subversion' 'apr' 'expat' 'neon' 'openssl')
conflicts=('pysvn<=1.7.4-3' 'pysvn-py2')
replaces=('pysvn-py2')
provides=('pysvn-py2')
source=("http://pysvn.barrys-emacs.org/source_kits/pysvn-${pkgver}.tar.gz")
sha1sums=('92775270760ff9b355c0d708c4266ec6d48749c8')

build() {
  cd "$srcdir/pysvn-$pkgver"

  cd Source

  python2 setup.py configure

  make
}

package() {
  cd "$srcdir/pysvn-$pkgver"

  _pyver=`python2 -V 2>&1|sed -e 's/.* 2\.\([0-9]\).*/\1/'`

  cd Source

  install -Dm644 pysvn/__init__.py \
    $pkgdir/usr/lib/python2.${_pyver}/site-packages/pysvn/__init__.py
  install -Dm755 pysvn/_pysvn_2_${_pyver}.so \
    $pkgdir/usr/lib/python2.${_pyver}/site-packages/pysvn/_pysvn_2_${_pyver}.so
}
