# This file is part of BlackArch Linux ( https://www.blackarch.org/ ).
# See COPYING for license details.

pkgname=chisel
pkgver=160.92d90be
pkgrel=2
pkgdesc='A fast TCP tunnel over HTTP.'
groups=('blackarch' 'blackarch-tunnel' 'blackarch-proxy')
arch=('x86_64' 'aarch64')
url='https://github.com/jpillora/chisel'
license=('MIT')
makedepends=('git' 'go')
source=("git+https://github.com/jpillora/$pkgname.git")
sha512sums=('SKIP')

pkgver() {
  cd $pkgname

  echo $(git rev-list --count HEAD).$(git rev-parse --short HEAD)
}

build() {
  cd $pkgname

  GOPATH="$srcdir" go mod download
  GOPATH="$srcdir" go build \
    -trimpath \
    -buildmode=pie \
    -mod=readonly \
    -modcacherw \
    -ldflags "-s -w -X github.com/jpillora/chisel/share.BuildVersion=$(git describe --abbrev=0 --tags)" \
    -o $pkgname .
}

package() {
  cd $pkgname

  install -Dm 755 $pkgname "$pkgdir/usr/bin/$pkgname"
  install -Dm 644 example/users.json \
  "$pkgdir/usr/share/doc/$pkgname/examples/users.json"
}

