# This file is part of BlackArch Linux ( https://www.blackarch.org/ ).
# See COPYING for license details.

pkgbase=python-pgcli
pkgname=('python2-pgcli' 'python-pgcli')
_pkgname=pgcli
pkgver=3.1.0
pkgrel=1
pkgdesc='CLI for Postgres Database. With auto-completion and syntax highlighting.'
arch=('any')
url='https://pypi.org/project/pgcli/#files'
license=('BSD')
makedepends=('python2-setuptools' 'python-setuptools')
source=("https://files.pythonhosted.org/packages/40/87/be9244b89f82959ee2703499fa8b22e1be339a2bdd394c967c23aa19bf55/$_pkgname-$pkgver.tar.gz")
sha512sums=('7f14a331f6aa2d8d97e8cb14fb53a174ffcf4d93bf4ad067953d296d8c3a12c4514a45a7ff0ce249ac8c67a2859859fc25b76593ad4f563c67a3525fcecae8bc')

prepare() {
  cp -a "$_pkgname-$pkgver"{,-2}
}

build() {
  cd "$_pkgname-$pkgver-2"

  python2 setup.py build

  cd "$srcdir/$_pkgname-$pkgver"

  python setup.py build
}

package_python2-pgcli() {
  depends=('python2' 'python2-click' 'python2-pygments' 'python2-psycopg2'
           'python2-sqlparse' 'python2-configobj' 'python2-pgspecial'
           'python2-setproctitle' 'python2-cli-helpers' 'python2-humanize')

  cd "$_pkgname-$pkgver-2"

  python2 setup.py install --prefix=/usr --root="$pkgdir" --optimize=1 \
    --skip-build

  mv "$pkgdir/usr/bin/$_pkgname" "$pkgdir/usr/bin/${_pkgname}-2"
}

package_python-pgcli() {
  depends=('python' 'python-click' 'python-pygments' 'python-psycopg2'
           'python-sqlparse' 'python-configobj' 'python-pgspecial'
           'python-setproctitle' 'python-cli-helpers' 'python-humanize')

  cd "$_pkgname-$pkgver"

  python setup.py install --prefix=/usr --root="$pkgdir" --optimize=1 \
    --skip-build
}

