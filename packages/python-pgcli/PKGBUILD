# This file is part of BlackArch Linux ( https://www.blackarch.org/ ).
# See COPYING for license details.

pkgname=python-pgcli
_pkgname=${pkgname#python-}
pkgver=4.4.0
pkgrel=1
pkgdesc='CLI for Postgres Database. With auto-completion and syntax highlighting.'
arch=('any')
url='https://pypi.org/project/pgcli/#files'
license=('BSD-3-Clause')
depends=('python' 'python-click' 'python-pygments' 'python-psycopg2'
         'python-sqlparse' 'python-configobj' 'python-pgspecial'
         'python-setproctitle' 'python-cli-helpers' 'python-humanize')
makedepends=('python-build' 'python-installer' 'python-setuptools'
             'python-wheel' 'python-setuptools-scm')
options=(!emptydirs)
source=("https://files.pythonhosted.org/packages/source/${_pkgname::1}/$_pkgname/$_pkgname-$pkgver.tar.gz")
sha512sums=('92020513cc2b90c24cd68312fbc45689b463caf5ab27eec1ce368ab152dfcb45ad108e26f5e2a09123d9af6695e0893cd278db5e4caa00f328575dc771ca2b7d')

build() {
  cd "$_pkgname-$pkgver"

  python -m build --wheel --no-isolation
}

package() {
  cd "$_pkgname-$pkgver"

  python -m installer --destdir="$pkgdir" dist/*.whl
}

