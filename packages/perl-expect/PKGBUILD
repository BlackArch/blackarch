# This file is part of BlackArch Linux ( http://blackarch.org ).
# See COPYING for license details.

pkgname='perl-expect'
pkgver='1.33'
pkgrel=1
pkgdesc='This module is a successor of Comm.pl and a descendent of Chat.pl. It more closely ressembles the Tcl Expect language than its predecessors.'
arch=('any')
license=('PerlArtistic' 'GPL')
options=('!emptydirs')
depends=('perl-io-tty>=1.03')
url='http://search.cpan.org/dist/Expect'
source=("http://search.cpan.org/CPAN/authors/id/J/JA/JACOBY/Expect-${pkgver}.tar.gz")
sha1sums=('1fda0abadfd88e702ffd6e50f960673d05b146af')

build() {
  cd "$srcdir"

  ( export PERL_MM_USE_DEFAULT=1 PERL5LIB=""                 \
      PERL_AUTOINSTALL=--skipdeps                            \
      PERL_MM_OPT="INSTALLDIRS=vendor DESTDIR='$pkgdir'"     \
      PERL_MB_OPT="--installdirs vendor --destdir '$pkgdir'" \
      MODULEBUILDRC=/dev/null

    cd "$srcdir/Expect-$pkgver"

    /usr/bin/perl Makefile.PL

    make
  )
}

package() {
  cd "$srcdir/Expect-$pkgver"

  make install

  find "$pkgdir" -name .packlist -o -name perllocal.pod -delete
}
