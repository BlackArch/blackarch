# This file is part of BlackArch Linux ( http://blackarch.org ).
# See COPYING for license details.

pkgname=dawnscanner
pkgver=1.6.8
pkgrel=3
groups=('blackarch' 'blackarch-webapp' 'blackarch-scanner')
pkgdesc='A static analysis security scanner for ruby written web applications.'
arch=('x86_64' 'armv6h' 'armv7h' 'aarch64')
url='https://rubygems.org/gems/dawnscanner'
license=('MIT')
depends=('ruby')
makedepends=('ruby')
options=('!strip')
source=("https://rubygems.org/downloads/dawnscanner-${pkgver}.gem")
sha512sums=('0e1b2599922ea395c1a41a00fdbc1c315f3dfa0ea1775945770e49fd5487c7c8f7693a86d25c0ee2227ebefdcb4da883acc06936c8578ad1e2f64272f4bdb47d')

package() {
  install -dm 755 "$pkgdir"/usr/bin

  local _gemdir="$(ruby -e'puts Gem.default_dir')"

  gem install --ignore-dependencies --no-user-install -n "$pkgdir"/usr/bin \
    -i "$pkgdir/$_gemdir" dawnscanner*.gem

  rm "$pkgdir"/usr/bin/{cvss,haml,ruby_parse,ruby_parse_extract_error,tilt}
}
