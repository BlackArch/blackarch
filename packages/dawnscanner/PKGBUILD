# This file is part of BlackArch Linux ( https://www.blackarch.org/ ).
# See COPYING for license details.

pkgname=dawnscanner
pkgver=1.6.9
pkgrel=2
groups=('blackarch' 'blackarch-webapp' 'blackarch-scanner')
pkgdesc='A static analysis security scanner for ruby written web applications.'
arch=('x86_64' 'armv6h' 'armv7h' 'aarch64')
url='https://rubygems.org/gems/dawnscanner'
license=('MIT')
depends=('ruby')
makedepends=('ruby' 'ruby-rdoc')
options=('!strip')
source=("https://rubygems.org/downloads/$pkgname-$pkgver.gem")
sha512sums=('2bb908145237d51c662872f64558a542badb9e4c795d119c99c2859a859b27e36daf84c2721c4013111cadb733b4594be9c1691e2693751a6801602b07b1fbee')

package() {
  install -dm 755 "$pkgdir/usr/bin"

  local _gemdir="$(ruby -e'puts Gem.default_dir')"

  gem install --ignore-dependencies --no-user-install -n "$pkgdir"/usr/bin \
    -i "$pkgdir/$_gemdir" "$pkgname-$pkgver.gem"

  rm -f "$pkgdir"/usr/bin/{cvss,haml,ruby_parse,ruby_parse_extract_error,tilt}
}

