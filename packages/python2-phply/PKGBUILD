# This file is part of BlackArch Linux ( https://www.blackarch.org/ ).
# See COPYING for license details.

pkgname=python2-phply
pkgver=1.2.5
pkgrel=7
pkgdesc='PHP parser written in Python using PLY.'
arch=('any')
url='https://pypi.org/project/phply/#files'
license=('BSD')
depends=('python2')
makedepends=('python2-setuptools')
source=("https://files.pythonhosted.org/packages/e2/cd/8dd43fc77b3390e58237e5b3b000efcfbc4fc460941aee36ec0e2a7be4b3/phply-$pkgver.tar.gz")
sha512sums=('24e12bad4d7e44b3354717ff59ca751c337a1083f6d576af9c334fc53afd3921a3714f594cabcf107526b423ce9039fc5ed5d5713a6be208477e7ff41da114da')

build() {
  cd "phply-$pkgver"

  python2 setup.py build
}

package() {
  cd "phply-$pkgver"

  python2 setup.py install --prefix=/usr --root="$pkgdir" --optimize=1 \
    --skip-build

  # Fix a conflict with community/python2-poster.
  # This probably shouldn't be here anyways.
  rm -rf "$pkgdir/usr/lib/python2.7/site-packages/tests"
}

