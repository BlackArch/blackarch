# This file is part of BlackArch Linux ( https://www.blackarch.org/ ).
# See COPYING for license details.

pkgbase=python2-webargs
pkgname=('python2-webargs' 'python-webargs')
_pkgname=webargs
pkgver=8.0.0
pkgrel=1
pkgdesc='A friendly library for parsing and validating HTTP request arguments, with built-in support for popular web frameworks, including Flask, Django, Bottle, Tornado, Pyramid, webapp2, Falcon, and aiohttp.'
arch=('any')
url='https://pypi.org/project/webargs/#files'
license=('MIT')
makedepends=('python2-setuptools' 'python-setuptools')
source=("https://files.pythonhosted.org/packages/16/64/8add6d00de43337a3c52d065fca21689f0387d635544dbabeda45b0adf7c/$_pkgname-$pkgver.tar.gz")
sha512sums=('cd1caae32b23dc89b56ec9caeec908c2384d44b1766d17c467ef1813f6d84a41c34245651f78840b2ce7be31ff22d501da53a55cb30b7494ccdb7aa6ff807b41')

prepare() {
  cp -a "$_pkgname-$pkgver"{,-2}
}

build() {
  cd "$_pkgname-$pkgver-2"

  python2 setup.py build

  cd "$srcdir/$_pkgname-$pkgver"

  python setup.py build
}

package_python2-webargs() {
  depends=('python2' 'python2-marshmallow' 'python2-simplejson')

  cd "$_pkgname-$pkgver-2"

  python2 setup.py install --prefix=/usr --root="$pkgdir" --optimize=1 \
    --skip-build
}

package_python-webargs() {
  depends=('python' 'python-marshmallow')

  cd "$_pkgname-$pkgver"

  python setup.py install --prefix=/usr --root="$pkgdir" --optimize=1 \
    --skip-build
}

