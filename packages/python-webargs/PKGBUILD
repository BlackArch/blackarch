# This file is part of BlackArch Linux ( https://www.blackarch.org/ ).
# See COPYING for license details.

pkgbase=python2-webargs
pkgname=('python2-webargs' 'python-webargs')
_pkgname=webargs
pkgver=5.3.2
pkgrel=2
pkgdesc='A friendly library for parsing and validating HTTP request arguments, with built-in support for popular web frameworks, including Flask, Django, Bottle, Tornado, Pyramid, webapp2, Falcon, and aiohttp.'
arch=('any')
url='https://pypi.org/project/webargs/#files'
license=('MIT')
makedepends=('python2-setuptools' 'python-setuptools')
source=("https://files.pythonhosted.org/packages/56/29/9f669d309500255c9fa4c3e021e83e3c992af40d44ce5ca22e2dec60823a/$_pkgname-$pkgver.tar.gz")
sha512sums=('c55dc9eb4589716a2f022f56b5354d82a7d7284126e94975617b160a5cac8f790eabfa3c73404c280704259005b885d50a8cb509edf16acce309c87e5228474e')

prepare() {
  cp -a "$_pkgname-$pkgver" "$_pkgname-$pkgver-2"
}

build() {
  cd "$_pkgname-$pkgver-2"

  python2 setup.py build

  cd "$srcdir/$_pkgname-$pkgver"

  python setup.py build
}

package_python2-webargs() {
  depends=('python2' 'python2-marshmallow' 'python2-simplejson')

  cd "$_pkgname-$pkgver-2"

  python2 setup.py install --prefix=/usr --root="$pkgdir" --optimize=1 \
    --skip-build
}

package_python-webargs() {
  depends=('python' 'python-marshmallow')

  cd "$_pkgname-$pkgver"

  python setup.py install --prefix=/usr --root="$pkgdir" --optimize=1 \
    --skip-build
}

