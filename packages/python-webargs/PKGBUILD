# This file is part of BlackArch Linux ( https://www.blackarch.org/ ).
# See COPYING for license details.

pkgbase=python2-webargs
pkgname=('python2-webargs' 'python-webargs')
_pkgname=webargs
pkgver=8.0.1
pkgrel=2
pkgdesc='A friendly library for parsing and validating HTTP request arguments, with built-in support for popular web frameworks, including Flask, Django, Bottle, Tornado, Pyramid, webapp2, Falcon, and aiohttp.'
arch=('any')
url='https://pypi.org/project/webargs/#files'
license=('MIT')
makedepends=('python2-setuptools' 'python-setuptools')
source=("https://files.pythonhosted.org/packages/d9/58/24538fd2bf7164abea0d9e53b3dddb49fda3ff7008e2bfd1309aaf8244f5/$_pkgname-$pkgver.tar.gz")
sha512sums=('7ca079b2d80d6115f98fbdd55c92bb88e523b5676011ffe7f4c7b2e4f7067852613b4b115d92ceba1fcc8677fa5fb12648d686d0f55911f41696a14733b40375')

prepare() {
  cp -a "$_pkgname-$pkgver"{,-2}
}

build() {
  cd "$_pkgname-$pkgver-2"

  python2 setup.py build

  cd "$srcdir/$_pkgname-$pkgver"

  python setup.py build
}

package_python2-webargs() {
  depends=('python2' 'python2-marshmallow' 'python2-simplejson')

  cd "$_pkgname-$pkgver-2"

  python2 setup.py install --prefix=/usr --root="$pkgdir" --optimize=1 \
    --skip-build
}

package_python-webargs() {
  depends=('python' 'python-marshmallow')

  cd "$_pkgname-$pkgver"

  python setup.py install --prefix=/usr --root="$pkgdir" --optimize=1 \
    --skip-build
}

