# This file is part of BlackArch Linux ( http://blackarch.org ).
# See COPYING for license details.

pkgname=dorkbot
pkgver=49.870eb65
pkgrel=1
pkgdesc='Command-line tool to scan Google search results for vulnerabilities.'
groups=('blackarch' 'blackarch-scanner')
arch=('any')
url='https://github.com/utiso/dorkbot'
license=('custom:unknown')
depends=('python2' 'python2-argparse')
makedepends=('git')
source=('git+https://github.com/utiso/dorkbot.git')
sha512sums=('SKIP')

pkgver() {
  cd "$srcdir/dorkbot"

  echo $(git rev-list --count HEAD).$(git rev-parse --short HEAD)
}

package() {
  cd "$srcdir/dorkbot"

  install -dm 755 "$pkgdir/usr/bin"
  install -dm 755 "$pkgdir/usr/share/dorkbot"

  install -Dm644 -t "$pkgdir/usr/share/doc/dorkbot/" README.md
  install -Dm644 LICENSE "$pkgdir/usr/share/licenses/dorkbot/LICENSE"

  rm README.md LICENSE

  cp -a * "$pkgdir/usr/share/dorkbot/"

  cat > "$pkgdir/usr/bin/dorkbot" << EOF
#!/bin/sh
cd /usr/share/dorkbot
exec python2 dorkbot.py "\${@}"
EOF

  chmod a+x "$pkgdir/usr/bin/dorkbot"
}
