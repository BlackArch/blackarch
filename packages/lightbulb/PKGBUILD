# This file is part of BlackArch Linux ( http://blackarch.org ).
# See COPYING for license details.

pkgname='lightbulb'
pkgver=65.ab49c32
pkgrel=1
pkgdesc='Python framework for auditing web applications firewalls.'
groups=('blackarch' 'blackarch-webapp' 'blackarch-scanner')
arch=('any')
url='https://github.com/lightbulb-framework/lightbulb-framework'
license=('MIT')
depends=('python2' 'python2-cliff' 'python2-sfalearn' 'python2-symautomata')
makedepends=('git' 'python2-setuptools')
source=('git+https://github.com/lightbulb-framework/lightbulb-framework.git')
sha1sums=('SKIP')

pkgver() {
  cd "$srcdir/lightbulb-framework"

  echo $(git rev-list --count HEAD).$(git rev-parse --short HEAD)
}

prepare() {
  cd "$srcdir/lightbulb-framework"

  sed -i "s|'multiprocessing',||" setup.py
  sed -i "s|'multiprocessing',||" lightbulb/cli/use.py
}

package() {
  cd "$srcdir/lightbulb-framework"

  python2 setup.py install --root="$pkgdir"

  install -Dm644 README.md "$pkgdir/usr/share/doc/lightbulb/README.md"
  install -Dm644 LICENSE "$pkgdir/usr/share/licenses/lightbulb/LICENSE"
}
