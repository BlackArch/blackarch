# This file is part of BlackArch Linux ( http://blackarch.org ).
# See COPYING for license details.

pkgname='snuck'
pkgver=6.76196b6
pkgrel=1
pkgdesc='Automatic XSS filter bypass.'
groups=('blackarch' 'blackarch-webapp')
arch=('any')
url='https://github.com/mauro-g/snuck'
license=('custom:unknown')
depends=('java-environment')
makedepends=('git' 'apache-ant')
source=('git+https://github.com/mauro-g/snuck.git')
sha1sums=('SKIP')

pkgver() {
  cd "$srcdir/snuck"

  echo $(git rev-list --count HEAD).$(git rev-parse --short HEAD)
}

build() {
  cd "$srcdir/snuck"

  ant build
  ant jar
}

package() {
  cd "$srcdir/snuck"

  mkdir -p "$pkgdir/usr/bin"
  mkdir -p "$pkgdir/usr/share/snuck"

  install -Dm644 -t "$pkgdir/usr/share/doc/snuck/" README.md

  rm README.md

  cp -a * "$pkgdir/usr/share/snuck"

  cat > "$pkgdir/usr/bin/snuck" << EOF
#!/bin/sh
exec java -jar /usr/share/snuck/snuck.jar "\${@}"
EOF

  chmod a+x "$pkgdir/usr/bin/snuck"
}
