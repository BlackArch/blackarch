# This file is part of BlackArch Linux ( https://www.blackarch.org/ ).
# See COPYING for license details.

pkgbase=python-pysmt
pkgname=('python2-pysmt' 'python-pysmt')
_pkgname=PySMT
pkgver=0.8.0
pkgrel=1
pkgdesc='A solver-agnostic library for SMT Formulae manipulation and solving.'
arch=('any')
url='https://pypi.org/project/pysmt/#files'
license=('APACHE')
makedepends=('python2-setuptools' 'python-setuptools')
source=("https://files.pythonhosted.org/packages/dd/93/a3e2e53c71c8a5713ba505d47e45c99e614f026fc26257ba28dc5130a3b2/$_pkgname-$pkgver.tar.gz")
sha512sums=('11f0e4deedf85ebbc96db5d3a057fd17d8323f5f659716f2a2cd9c021c395037989f68c2993ddc9dcc8f080fbb9a5d8d595367bba750959eb35ff150f2c84055')

prepare() {
  cp -a "$_pkgname-$pkgver"{,-2}
}

build() {
  cd "$_pkgname-$pkgver-2"

  python2 setup.py build

  cd "$srcdir/$_pkgname-$pkgver"

  python setup.py build
}

package_python2-pysmt() {
  depends=('python2')

  cd "$_pkgname-$pkgver-2"

  python2 setup.py install --prefix=/usr --root="$pkgdir" -O1 --skip-build

  for i in "$pkgdir/usr/bin/"*; do
    mv $i "${i}2"
  done
}

package_python-pysmt() {
  depends=('python')

  cd "$_pkgname-$pkgver"

  python setup.py install --prefix=/usr --root="$pkgdir" -O1 --skip-build
}

