# This file is part of BlackArch Linux ( https://www.blackarch.org/ ).
# See COPYING for license details.

pkgname=python2-pytest
_pkgname=pytest
pkgver=4.6.6
pkgrel=1
pkgdesc='Simple powerful testing with Python.'
url='https://pypi.org/project/pytest/#files'
arch=('any')
license=('MIT')
depends=('python2' 'python2-importlib-metadata' 'python2-atomicwrites'
         'python2-py' 'python2-attrs' 'python2-wcwidth'
         'python2-more-itertools')
makedepends=('python2-setuptools')
source=("https://files.pythonhosted.org/packages/a4/03/249d02980265d099cffc41029f9ebb5c218110d19424032458d0f5127321/$_pkgname-$pkgver.tar.gz")
sha512sums=('c5b6c21c352787eadce06be7529ecb6f6fd5f5eb193ec29e5a4cfff3d7e305f50c6f07aeac15bddce007d88077a35a8f59633111815321792ed3d9fa1011b684')

build() {
  cd "$_pkgname-$pkgver"

  python2 setup.py build
}

package() {
  cd "$_pkgname-$pkgver"

  python2 setup.py install --root="$pkgdir" --prefix=/usr -O1 --skip-build

  for i in "$pkgdir/usr/bin/"*; do
    mv $i "${i}2"
  done
}

