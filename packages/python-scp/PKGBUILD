# This file is part of BlackArch Linux ( https://www.blackarch.org/ ).
# See COPYING for license details.

pkgbase=python-scp
pkgname=('python2-scp' 'python-scp')
_pkgname=scp
pkgver=0.13.5
pkgrel=1
pkgdesc='SCP module for paramiko.'
arch=('any')
url='https://pypi.org/project/scp/#files'
license=('GPL')
makedepends=('python2-setuptools' 'python-setuptools')
source=("https://files.pythonhosted.org/packages/d2/35/392dde4dc4a1cb48f90fc7137104ac3246654c73aafd81efde5bd0078b12/$_pkgname-$pkgver.tar.gz")
sha512sums=('69b3b727d84b862eaebd0543e320975ae9acddbe577accef0c27d741df5b0dd42a53cf26900769fdfc134866a00083ebc953d0e666adee81b8e903acc991c6d2')

prepare() {
  cp -a "$_pkgname-$pkgver"{,-2}
}

build() {
  cd "$_pkgname-$pkgver-2"

  python2 setup.py build

  cd "$srcdir/$_pkgname-$pkgver"

  python setup.py build
}

package_python2-scp() {
  depends=('python2')

  cd "$_pkgname-$pkgver-2"

  python2 setup.py install --prefix=/usr --root="$pkgdir" --optimize=1 \
    --skip-build
}

package_python-scp() {
  depends=('python')

  cd "$_pkgname-$pkgver"

  python setup.py install --prefix=/usr --root="$pkgdir" --optimize=1 \
    --skip-build
}

