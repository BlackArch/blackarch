# This file is part of BlackArch Linux ( https://www.blackarch.org/ ).
# See COPYING for license details.

pkgbase=python-scp
pkgname=('python2-scp' 'python-scp')
_pkgname=scp
pkgver=0.14.1
pkgrel=2
pkgdesc='SCP module for paramiko.'
arch=('any')
url='https://pypi.org/project/scp/#files'
license=('GPL')
makedepends=('python2-setuptools' 'python-setuptools')
source=("https://files.pythonhosted.org/packages/1d/b4/1d471ed28e5c044c0e9349ff205740606b5ccdce3371e1fc5697d1e55acb/$_pkgname-$pkgver.tar.gz")
sha512sums=('4ec74662e28fb1442590ceaea59479d42d9ce27862eb4bec0ce3882c3b4ab79b1aaeae0bc23c9afd3dd7996a4cddf2e608c87d9c5133bbc7ca103f4f5bf8f204')

prepare() {
  cp -a "$_pkgname-$pkgver"{,-2}
}

build() {
  cd "$_pkgname-$pkgver-2"

  python2 setup.py build

  cd "$srcdir/$_pkgname-$pkgver"

  python setup.py build
}

package_python2-scp() {
  depends=('python2')

  cd "$_pkgname-$pkgver-2"

  python2 setup.py install --prefix=/usr --root="$pkgdir" --optimize=1 \
    --skip-build
}

package_python-scp() {
  depends=('python')

  cd "$_pkgname-$pkgver"

  python setup.py install --prefix=/usr --root="$pkgdir" --optimize=1 \
    --skip-build
}

