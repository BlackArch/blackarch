# This file is part of BlackArch Linux ( https://www.blackarch.org/ ).
# See COPYING for license details.

pkgbase=python-scp
pkgname=('python2-scp' 'python-scp')
_pkgname=scp
pkgver=0.13.4
pkgrel=1
pkgdesc='SCP module for paramiko.'
arch=('any')
url='https://pypi.org/project/scp/#files'
license=('GPL')
makedepends=('python2-setuptools' 'python-setuptools')
source=("https://files.pythonhosted.org/packages/78/ab/3c8a6160786eff8032deb55b72fa4e74484e9a5723510475395baeaa64cc/$_pkgname-$pkgver.tar.gz")
sha512sums=('7151fff83f053440db77302fa5a1a1df157445854347ac7da3573425fdd32ede63678caa34a0c8e5a0e2e5e336f07ed8d91f1109ac9d3228e3d286753a53a326')

prepare() {
  cp -a "$_pkgname-$pkgver"{,-2}
}

build() {
  cd "$_pkgname-$pkgver-2"

  python2 setup.py build

  cd "$srcdir/$_pkgname-$pkgver"

  python setup.py build
}

package_python2-scp() {
  depends=('python2')

  cd "$_pkgname-$pkgver-2"

  python2 setup.py install --prefix=/usr --root="$pkgdir" --optimize=1 \
    --skip-build
}

package_python-scp() {
  depends=('python')

  cd "$_pkgname-$pkgver"

  python setup.py install --prefix=/usr --root="$pkgdir" --optimize=1 \
    --skip-build
}

