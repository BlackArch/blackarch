pkgname=cdpsnarf
pkgver=0.1.6
pkgrel=2
groups=('blackarchlinux' 'blackarchlinux-sniffer')
pkgdesc="Cisco discovery protocol sniffer"
url="https://github.com/Zapotek/cdpsnarf"
license=('GPL2')
arch=('i686' 'x86_64' 'armv6h' 'armv7h')
depends=('git' 'libpcap')
source=("git+https://github.com/Zapotek/cdpsnarf")
md5sums=('SKIP')

pkgver() {
  cd "$srcdir/cdpsnarf"

  # We have to get the version number from the README file. It's better than
  #  nothing.
  grep -m1 'v[0-9].*[0-9].*[0-9]' README | cut -d'v' -f2 | awk '{ print $1 }'
}

build() {
  cd "$srcdir/cdpsnarf"
  make all
}

package() {
  cd "$srcdir/cdpsnarf"

  # Base directories.
  install -dm755 "$pkgdir/usr/bin"
  install -dm755 "$pkgdir/usr/share/doc/cdpsnarf"

  install -m755 cdpsnarf "$pkgdir/usr/bin"
  install -m644 README "$pkgdir/usr/share/doc/cdpsnarf"
}
