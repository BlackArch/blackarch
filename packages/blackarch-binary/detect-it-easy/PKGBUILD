# This file is part of BlackArch Linux ( https://www.blackarch.org/ ).
# See COPYING for license details.

pkgname=detect-it-easy
pkgver=3.00
pkgrel=1
epoch=1
groups=('blackarch' 'blackarch-binary' 'blackarch-reversing')
pkgdesc='A program for determining types of files.'
arch=('x86_64')
url='https://github.com/horsicq/DIE-engine/releases'
license=('MIT')
source=("https://github.com/horsicq/DIE-engine/releases/download/$pkgver/die_lin64_portable_$pkgver.tar.gz")
sha512sums=('053666af975206caa3c24574bbc968f6b58b974a4532fe655763ded9b2bd462c3c6177afe647bcbddbcf396b0a4a5d9d029334cc28985b28df04fa1ca3e8b8ac')

package() {
  cd die_lin64_portable

  install -dm 755 "$pkgdir/usr/bin"
  install -dm 755 "$pkgdir/usr/share/$pkgname"

  cp -a --no-preserve=ownership * "$pkgdir/usr/share/$pkgname/"

  _tools="diec diel die"

  for tool in $_tools ; do
    cat > "$pkgdir/usr/bin/$tool" << EOF
#!/bin/sh
cd /usr/share/$pkgname/
exec ./$tool.sh "\$@"
EOF
    chmod 755 "$pkgdir/usr/bin/$tool"
  done
}

