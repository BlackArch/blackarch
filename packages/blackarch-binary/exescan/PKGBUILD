# This file is part of BlackArch Linux ( https://www.blackarch.org/ ).
# See COPYING for license details.

pkgname=exescan
pkgver=1.ad993e3
pkgrel=3
pkgdesc='A tool to detect anomalies in PE (Portable Executable) files.'
groups=('blackarch' 'blackarch-binary')
arch=('any')
url='https://github.com/cysinfo/Exescan'
license=('custom:unknown')
depends=('python2' 'python2-pefile')
makedepends=('git')
source=("$pkgname::git+https://github.com/cysinfo/Exescan.git")
sha512sums=('SKIP')

pkgver() {
  cd $pkgname

  echo $(git rev-list --count HEAD).$(git rev-parse --short HEAD)
}

package() {
  cd $pkgname

  install -dm 755 "$pkgdir/usr/bin"
  install -dm 755 "$pkgdir/usr/share/$pkgname"

  install -Dm 644 ReadMe.txt "$pkgdir/usr/share/doc/$pkgname/ReadMe.txt"

  rm ReadMe.txt

  cp -a * "$pkgdir/usr/share/$pkgname/"

  cat > "$pkgdir/usr/bin/$pkgname" << EOF
#!/bin/sh
cd /usr/share/$pkgname
exec python2 $pkgname.py "\$@"
EOF

  chmod +x "$pkgdir/usr/bin/$pkgname"
}

