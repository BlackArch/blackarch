# This file is part of BlackArch Linux ( https://www.blackarch.org/ ).
# See COPYING for license details.

pkgname=patchkit
pkgver=37.95dc699
pkgrel=1
pkgdesc='Powerful binary patching from Python.'
groups=('blackarch' 'blackarch-binary' 'blackarch-backdoor')
arch=('any')
url='https://github.com/lunixbochs/patchkit'
license=('BSD' 'custom:unknown')
depends=('python2' 'python2-coding' 'python2-capstone' 'python2-keystone'
         'unicorn')
makedepends=('git')
source=("git+https://github.com/lunixbochs/$pkgname.git")
sha512sums=('SKIP')

pkgver() {
  cd $pkgname

  echo $(git rev-list --count HEAD).$(git rev-parse --short HEAD)
}

package() {
  cd $pkgname

  install -dm 755 "$pkgdir/usr/bin"
  install -dm 755 "$pkgdir/usr/share/$pkgname"

  install -Dm 644 -t "$pkgdir/usr/share/doc/$pkgname/" README.md
  install -Dm 644 LICENSE "$pkgdir/usr/share/licenses/$pkgname/LICENSE"

  rm README.md LICENSE

  cp -a * "$pkgdir/usr/share/$pkgname/"

  cat > "$pkgdir/usr/bin/$pkgname" << EOF
#!/bin/sh
cd /usr/share/$pkgname
exec python2 patch "\$@"
EOF

  chmod +x "$pkgdir/usr/bin/$pkgname"
}

