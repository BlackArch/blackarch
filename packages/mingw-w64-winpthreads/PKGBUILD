# This file is part of BlackArch Linux ( https://www.blackarch.org/ ).
# See COPYING for license details.

pkgname=mingw-w64-winpthreads
pkgver=7.0.0
pkgrel=1
pkgdesc='MinGW-w64 winpthreads library.'
arch=('any')
url='http://mingw-w64.sourceforge.net/'
license=('custom:unknown')
makedepends=('mingw-w64-gcc-base' 'mingw-w64-binutils' 'mingw-w64-crt')
provides=('mingw-w64-headers-bootstrap')
conflicts=('mingw-w64-headers-bootstrap')
replaces=('mingw-w64-headers-bootstrap')
options=('!strip' '!buildflags' 'staticlibs' '!emptydirs')
source=("https://sourceforge.net/projects/mingw-w64/files/mingw-w64/mingw-w64-release/mingw-w64-v$pkgver.tar.bz2"{,.sig})
sha512sums=('30e5b2824a24eeb99ab519e3fc134cc9a7f04ee8b853bc8d66a13d1ab74144bf78b93e162bfe6de2a5e61f63c0e620a933b260b02d019cd68cc4b78bd36c67fd'
            'SKIP')
validpgpkeys=('CAF5641F74F7DFBA88AE205693BDB53CD4EBC740')
_targets='i686-w64-mingw32 x86_64-w64-mingw32'

build() {
  for t in $_targets ; do
    mkdir -p "$srcdir/winpthreads-build-$t" && cd "$srcdir/winpthreads-build-$t"

    "$srcdir/mingw-w64-v$pkgver/mingw-w64-libraries/winpthreads/"configure \
      --prefix=/usr/$t --host=$t --enable-static --enable-shared

    make
  done
}

package() {
  for t in $_targets ; do
    cd "$srcdir/winpthreads-build-$t"

    make DESTDIR="$pkgdir" install

    "$t-strip" --strip-unneeded "$pkgdir"/usr/$t/bin/*.dll
  done
}

