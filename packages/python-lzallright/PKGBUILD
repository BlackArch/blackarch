# This file is part of BlackArch Linux ( https://www.blackarch.org/ ).
# See COPYING for license details.

pkgname=python-lzallright
_pkgname=${pkgname#python-}
pkgver=v0.2.5.r0.gcfa42fa
pkgrel=1
pkgdesc='Python binding for LZ library.'
arch=('x86_64' 'aarch64')
url='https://github.com/vlaci/lzallright'
license=('MIT')
depends=('python')
makedepends=('git' 'python-build' 'python-pip' 'rust')
options=(!emptydirs)
source=("git+https://github.com/vlaci/$_pkgname.git")
sha512sums=('SKIP')

pkgver() {
  cd $_pkgname

  ( set -o pipefail
    git describe --long --tags --abbrev=7 2>/dev/null |
      sed 's/\([^-]*-g\)/r\1/;s/-/./g' ||
    printf "%s.%s" "$(git rev-list --count HEAD)" \
      "$(git rev-parse --short=7 HEAD)"
  )
}

build() {
  cd $_pkgname

  export CXXFLAGS="$CXXFLAGS -ffat-lto-objects"
  python -m build --wheel --outdir="$startdir/dist"
}

package() {
  cd $_pkgname

  pip install \
    --verbose \
    --disable-pip-version-check \
    --no-warn-script-location \
    --ignore-installed \
    --no-compile \
    --no-deps \
    --root="$pkgdir" \
    --prefix=/usr \
    --no-index \
    --find-links="file://$startdir/dist" \
    $_pkgname
}

