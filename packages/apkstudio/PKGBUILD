# Maintainer: William Luc Ritchie <luc dot ritchie at gmail dot com>
# Contributor: Borja Ruiz <borja [at] libcrack [dot] so>

pkgname=apkstudio
pkgver=v6.3.0.r0.gc6e2724
pkgrel=1
epoch=1
pkgdesc='An IDE for decompiling/editing & then recompiling of android application binaries.'
groups=('blackarch' 'blackarch-reversing' 'blackarch-mobile')
arch=('x86_64' 'aarch64')
url='http://www.vaibhavpandey.com/apkstudio/'
license=('GPL-2.0-or-later')
depends=('qt6-base' 'android-tools' 'android-apktool')
makedepends=('git' 'qt5-tools' 'unzip' 'vulkan-headers')
source=("git+https://github.com/vaibhavpandeyvpz/$pkgname.git"
        'APKTOOL_VERSION'
        "$pkgname-setup.sh")
sha512sums=('SKIP'
            '07ab697a07e8dfa7da53eb48ea43898cf9e2187b33cb31fbd33df1225a76703684be4f8d0eac660d6cfc60746ff09d7acfd554249545f84338642f357f5aed3c'
            '4a24a5ada6b7c426faa91b90bd314b16dace01463406be6dc6246a78afa3bce2c5474b670bf708e891de7907eae45ade28d71d8bd1db152fc4e45d08ee13f3b4')

pkgver() {
  cd $pkgname

  ( set -o pipefail
    git describe --long --tags --abbrev=7 2>/dev/null |
      sed 's/\([^-]*-g\)/r\1/;s/-/./g' ||
    printf "%s.%s" "$(git rev-list --count HEAD)" \
      "$(git rev-parse --short=7 HEAD)"
  )
}

build() {
  cd $pkgname

  cmake -B build -S . \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DCMAKE_BUILD_TYPE=Release

  cmake --build build \
    --config Release \
    -DCMAKE_INSTALL_PREFIX=/usr
}

package() {
  install -Dm 755 "$pkgname/$pkgname" "$pkgdir/usr/bin/$pkgname"
  install -Dm 744 'APKTOOL_VERSION' "$pkgdir/usr/share/$pkgname/APKTOOL_VERSION"
  install -Dm 755 "$pkgname-setup.sh" "$pkgdir/usr/bin/$pkgname-setup.sh"
}

