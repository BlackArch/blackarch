# This file is part of BlackArch Linux ( http://blackarch.org ).
# See COPYING for license details.

pkgname='rebind'
pkgver='0.3.4'
pkgrel=6
groups=('blackarch' 'blackarch-exploitation')
pkgdesc='DNS Rebinding Tool'
arch=('x86_64' 'armv6h' 'armv7h' 'aarch64')
url='http://code.google.com/p/rebind/'
license=('MIT')
depends=('python2' 'sqlite3' 'readline')
makedepends=('subversion')
source=("https://rebind.googlecode.com/files/rebind_${pkgver//./-}.tar.gz")
sha1sums=('e6e97cc05cd313255ee1f3f2b742ea2401843144')

prepare(){
  cd "$srcdir/rebind/dev"

  sed -i 's/env python/env python2/' *.py

  chmod +x *.py
}

build(){
  cd "$srcdir/rebind/src"
  make
}

package(){
  cd "$srcdir/rebind"

  mkdir -p "$pkgdir/usr/bin"
  mkdir -p "$pkgdir/usr/share/rebind"
  mkdir -p "$pkgdir/usr/share/doc/rebind"

  rm -rf bin/rebind_{i686,x86-64}

  cp -a --no-preserve=ownership src/www "$pkgdir/usr/share/doc/rebind"
  cp -a --no-preserve=ownership docs/* "$pkgdir/usr/share/doc/rebind"

  # Renamed to fix conflict with community/elfkickers.
  install -m755 src/rebind "$pkgdir/usr/bin/rebind_dns"
}
