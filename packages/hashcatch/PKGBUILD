# This file is part of BlackArch Linux ( https://www.blackarch.org/ ).
# See COPYING for license details.

pkgname=hashcatch
pkgver=26.ef629de
pkgrel=1
pkgdesc='Capture handshakes of nearby WiFi networks automatically.'
groups=('blackarch' 'blackarch-wireless' 'blackarch-cracker')
arch=('any')
url='https://github.com/staz0t/hashcatch'
license=('custom:unknown')
depends=('bash' 'aircrack-ng' 'hashcat-utils' 'hcxtools' 'jq')
makedepends=('git')
source=("git+https://github.com/staz0t/$pkgname.git")
sha512sums=('SKIP')
install="$pkgname.install"

pkgver() {
  cd $pkgname

  echo $(git rev-list --count HEAD).$(git rev-parse --short HEAD)
}

package() {
  cd $pkgname

  install -dm 755 "$pkgdir/usr/bin"
  install -dm 755 "$pkgdir/usr/share/$pkgname/handshakes"

  install -Dm 644 -t "$pkgdir/usr/share/doc/$pkgname/" README.md

  rm README.md

  cp -a * "$pkgdir/usr/share/$pkgname/"

  touch "$pkgdir/usr/share/$pkgname/"{config,db}

  cat > "$pkgdir/usr/bin/$pkgname" << EOF
#!/bin/sh
cd /usr/share/$pkgname
exec bash $pkgname.sh
EOF

  chmod +x "$pkgdir/usr/bin/$pkgname"
}

