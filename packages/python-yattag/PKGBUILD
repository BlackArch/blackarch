# This file is part of BlackArch Linux ( https://www.blackarch.org/ ).
# See COPYING for license details.

pkgbase=python-yattag
pkgname=('python2-yattag' 'python-yattag')
_pkgname=yattag
pkgver=1.15.0
pkgrel=1
pkgdesc='Generate HTML or XML in a pythonic way.'
arch=('any')
url='https://pypi.org/project/yattag/#files'
license=('LGPL')
makedepends=('python2-setuptools' 'python-setuptools')
source=("https://files.pythonhosted.org/packages/source/${_pkgname::1}/$_pkgname/$_pkgname-$pkgver.tar.gz")
sha512sums=('f7fb6900cc01582e8c11302c2cea0e3c5724eabb846d8f148c75dff7c72a1d1fe3242d937c3a320ee28cb696cf0d548135a68a58d9047c8ab42f74cec672a129')

prepare() {
  cp -a "$_pkgname-$pkgver"{,-2}
}

build() {
  cd "$_pkgname-$pkgver-2"

  python2 setup.py build

  cd "$srcdir/$_pkgname-$pkgver"

  python setup.py build
}

package_python2-yattag() {
  depends=('python2')

  cd "$_pkgname-$pkgver-2"

  python2 setup.py install --prefix=/usr --root="$pkgdir" --optimize=1 \
    --skip-build
}

package_python-yattag() {
  depends=('python')

  cd "$_pkgname-$pkgver"

  python setup.py install --prefix=/usr --root="$pkgdir" --optimize=1 \
    --skip-build
}

