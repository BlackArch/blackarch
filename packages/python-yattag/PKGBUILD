# This file is part of BlackArch Linux ( https://www.blackarch.org/ ).
# See COPYING for license details.

pkgbase=python-yattag
pkgname=('python2-yattag' 'python-yattag')
_pkgname=yattag
pkgver=1.12.0
pkgrel=1
pkgdesc='Generate HTML or XML in a pythonic way.'
arch=('any')
url='https://pypi.org/project/yattag/#files'
license=('LGPL')
makedepends=('python2-setuptools' 'python-setuptools')
source=("https://files.pythonhosted.org/packages/f3/38/7a715e5defed2341fa90773c59c1353d789dbc620d22b5c7259039a7b698/yattag-1.12.0.tar.gz")
sha512sums=('1dd8e5119ef9cc54eeae8d66da077366c881cd58abdf79ef4e908b3ef6b762375a305d930d257e45887f65868b7aab05585e261a01657fb514291b273b67020f')

prepare() {
  cp -a "$_pkgname-$pkgver"{,-2}
}

build() {
  cd "$_pkgname-$pkgver-2"

  python2 setup.py build

  cd "$srcdir/$_pkgname-$pkgver"

  python setup.py build
}

package_python2-yattag() {
  depends=('python2')

  cd "$_pkgname-$pkgver-2"

  python2 setup.py install --prefix=/usr --root="$pkgdir" --optimize=1 \
    --skip-build
}

package_python-yattag() {
  depends=('python')

  cd "$_pkgname-$pkgver"

  python setup.py install --prefix=/usr --root="$pkgdir" --optimize=1 \
    --skip-build
}

