# This file is part of BlackArch Linux ( https://www.blackarch.org/ ).
# See COPYING for license details.

pkgbase=python-smoke-zephyr
pkgname=('python2-smoke-zephyr' 'python-smoke-zephyr')
_pkgname=smoke-zephyr
pkgver=2.0.0
pkgrel=2
pkgdesc='This project provides a collection of miscellaneous Python utilities.'
arch=('any')
url='https://pypi.org/project/smoke-zephyr/#files'
license=('BSD')
makedepends=('python2-setuptools' 'python-setuptools')
source=("https://files.pythonhosted.org/packages/7b/00/8c320183af8c0f0e79a87f86805a0aafeac0314ef124806956ccdc34097b/$_pkgname-$pkgver.tar.gz")
sha512sums=('e1ddfaffadb5bf95fa7d8a57d3d59e5bab697860c57aec78827b4d6ce10b78bf7601c6568822650830dea1003f2949402aad3ecf68bc63d873a4dfdb2bc98c45')

prepare() {
  cp -a "$_pkgname-$pkgver"{,-2}
}

build() {
  cd "$_pkgname-$pkgver-2"

  python2 setup.py build

  cd "$srcdir/$_pkgname-$pkgver"

  python setup.py build
}

package_python2-smoke-zephyr() {
  depends=('python2')

  cd "$_pkgname-$pkgver-2"

  python2 setup.py install --prefix=/usr --root="$pkgdir" -O1 --skip-build
}

package_python-smoke-zephyr() {
  depends=('python')

  cd "$_pkgname-$pkgver"

  python setup.py install --prefix=/usr --root="$pkgdir" -O1 --skip-build
}

