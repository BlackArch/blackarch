# This file is part of BlackArch Linux ( https://www.blackarch.org/ ).
# See COPYING for license details.

pkgbase=python-smoke-zephyr
pkgname=('python2-smoke-zephyr' 'python-smoke-zephyr')
_pkgname=smoke-zephyr
pkgver=1.4.1
pkgrel=1
pkgdesc='This project provides a collection of miscellaneous Python utilities.'
arch=('any')
url='https://pypi.org/project/smoke-zephyr/#files'
license=('BSD')
makedepends=('python2-setuptools' 'python-setuptools')
source=("https://files.pythonhosted.org/packages/f9/87/d473b58bcdf9c3de17b607e2b173287391d543e83c16bb2744b0f4870030/$_pkgname-$pkgver.tar.gz")
sha512sums=('dd6d6c8e955dc4ffe7591c348ef286246865967d24835232173e97306e531e761afe89255754cafd08e01b91a67420de8a0e8d4d0d440cb604c5d711b20fe670')

prepare() {
  cp -a "$_pkgname-$pkgver"{,-2}
}

build() {
  cd "$_pkgname-$pkgver-2"

  python2 setup.py build

  cd "$srcdir/$_pkgname-$pkgver"

  python setup.py build
}

package_python2-smoke-zephyr() {
  depends=('python2')

  cd "$_pkgname-$pkgver-2"

  python2 setup.py install --prefix=/usr --root="$pkgdir" -O1 --skip-build
}

package_python-smoke-zephyr() {
  depends=('python')

  cd "$_pkgname-$pkgver"

  python setup.py install --prefix=/usr --root="$pkgdir" -O1 --skip-build
}

