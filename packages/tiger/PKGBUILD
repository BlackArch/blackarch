# This file is part of BlackArch Linux ( http://blackarch.org ).
# See COPYING for license details.

pkgname='tiger'
pkgver='3.2.3'
pkgrel=7
groups=('blackarch' 'blackarch-automation' 'blackarch-scanner')
pkgdesc='A security scanner, that checks computer for known problems. Can also use tripwire, aide and chkrootkit.'
arch=('x86_64' 'armv6h' 'armv7h' 'aarch64')
url='http://www.nongnu.org/tiger/'
license=('GPL')
depends=('perl')
makedepends=('patch')
backup=('etc/tiger/tigerrc' 'etc/tiger/cronrc')
source=("http://download.savannah.gnu.org/releases/tiger/tiger-${pkgver}.tar.gz"
        'tiger-makefile.diff'
        'build-fix.diff')
sha1sums=('1751f9a2d73dc36ac89cce5bd224685fdd5e313c'
          '2580679a39e2101390babcd5047c9b896ca9b806'
          '2dc4c994f8df94bdeba02c0e7e826573db35ca16')

prepare() {
  cd "$srcdir/tiger-$pkgver"

  patch -Np0 -i "$srcdir/tiger-makefile.diff"
  patch -Np0 -i "$srcdir/build-fix.diff"
}

build() {
  cd "$srcdir/tiger-$pkgver"

  ./configure --prefix=/usr \
    --mandir=/usr/share/man \
    --with-tigerhome=/usr/share/tiger \
    --with-tigerconfig=/etc/tiger \
    --with-tigerwork=/var/run/tiger \
    --with-tigerlog=/var/log/tiger \
    --with-tigerbin=/usr/bin

  make
}

package() {
  cd "$srcdir/tiger-$pkgver"

  mkdir -p "$pkgdir/usr/bin"
  mkdir -p "$pkgdir/usr/share/tiger"

  cp -aR tiger "$pkgdir/usr/share/tiger/"

  ln -s /usr/share/tiger/tiger "$pkgdir/usr/bin/"
}
