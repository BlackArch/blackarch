# This file is part of BlackArch Linux ( https://www.blackarch.org/ ).
# See COPYING for license details.

pkgbase=python-vcrpy
pkgname=('python2-vcrpy' 'python-vcrpy')
_pkgname=vcrpy
pkgver=2.0.1
pkgrel=4
pkgdesc='Automatically mock your HTTP interactions to simplify and speed up testing.'
arch=('any')
url='https://pypi.org/project/vcrpy/#files'
license=('MIT')
makedepends=('python2-setuptools' 'python-setuptools')
source=("https://files.pythonhosted.org/packages/ce/ad/cd36d304ecaa5c627b10e8f77ff4002a1866d06c9c3aa325d1cac3cc6169/$_pkgname-$pkgver.tar.gz")
sha512sums=('8b3bd52dac0addf535a9c40b71756f6e32c38afb02d00bd3b3425ec7f0da0105bcde60ede7f1bada62ee949fe5c058a422cbcbcc4c1a60785ff3a627caf334e2')

prepare() {
  cp -a "$_pkgname-$pkgver"{,-2}
}

build() {
  cd "$_pkgname-$pkgver-2"

  python2 setup.py build

  cd "$srcdir/$_pkgname-$pkgver"

  python setup.py build
}

package_python2-vcrpy() {
  depends=('python2' 'python2-yaml' 'python2-six' 'python2-mock' 'python2-wrapt'
           'python2-contextlib2')

  cd "$_pkgname-$pkgver-2"

  python2 setup.py install --prefix=/usr --root="$pkgdir" --optimize=1 \
    --skip-build
}

package_python-vcrpy() {
  depends=('python' 'python-yaml' 'python-six' 'python-mock' 'python-yarl'
           'python-wrapt' 'python-contextlib2')

  cd "$_pkgname-$pkgver"

  python setup.py install --prefix=/usr --root="$pkgdir" --optimize=1 \
    --skip-build
}

