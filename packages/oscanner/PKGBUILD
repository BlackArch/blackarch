pkgname=oscanner
pkgver=1.0.6
pkgrel=2
pkgdesc="An Oracle assessment framework developed in Java."
arch=('any')
url='http://www.cqure.net/wp/oscanner/'
groups=(blackarch blackarch-analysis)
license=('GPL')
depends=('java-runtime')
source=(http://www.cqure.net/tools/${pkgname}_bin_${pkgver//./_}.zip)
md5sums=('e85138c3cb60b3c6edc997a2160e0576')

prepare() {
  cd "$srcdir/${pkgname}_bin"
  sed -i "4i\\cd /usr/share/$pkgname" *.sh
}

package() {
  cd "$srcdir/${pkgname}_bin"

  rm COPYING

  # This isn't Windows.
  rm *.exe

  # This isn't a FAT filesystem.
  chmod -R 644 .
  chmod -R +X .
  chmod +x *.sh

  # This isn't love. 
  install -d "$pkgdir/usr/share/$pkgname"
  install -d "$pkgdir/usr/bin"

  cp --no-preserve=ownership -r * "$pkgdir/usr/share/$pkgname"

  # This isn't Java.
  mv "$pkgdir/usr/share/$pkgname/$pkgname.sh" "$pkgdir/usr/bin/$pkgname"
  mv "$pkgdir/usr/share/$pkgname/reportviewer.sh" "$pkgdir/usr/bin/reportviewer"
}
