# This file is part of BlackArch Linux ( http://blackarch.org ).
# See COPYING for license details.

pkgname=xray
pkgver=91.ca50a32
pkgrel=1
groups=('blackarch' 'blackarch-recon')
pkgdesc='A tool for recon, mapping and OSINT gathering from public networks.'
arch=('x86_64' 'armv6h' 'armv7h' 'aarch64')
url='https://github.com/evilsocket/xray'
license=('GPL3')
depends=('glibc' 'go')
makedepends=('git' 'go-pie' 'go-bindata' 'glide')
_url='github.com/evilsocket/xray'
source=('git+https://github.com/evilsocket/xray.git')
sha512sums=('SKIP')

pkgver() {
  cd "$pkgname"

  echo $(git rev-list --count HEAD).$(git rev-parse --short HEAD)
}

build() {
  mkdir -p src/$_url && rm -rf src/$_url && mv xray src/$_url && cd src/$_url

  GOPATH="$srcdir" go get -d -t $_url
  GOPATH="$srcdir" go get -u github.com/jteeuwen/go-bindata/...
  GOPATH="$srcdir" make get_glide
  GOPATH="$srcdir" make install_dependencies
  GOPATH="$srcdir" make build
}

package() {
  cd "src/$_url"

  install -dm755 "$pkgdir/usr/bin"

  install -Dm755 build/xray "$pkgdir/usr/bin/xray"
  install -Dm644 -t "$pkgdir/usr/share/doc/$pkgname/" README.md
  install -Dm644 LICENSE "$pkgdir/usr/share/licenses/$pkgname/LICENSE"
}

