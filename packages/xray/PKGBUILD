# This file is part of BlackArch Linux ( https://www.blackarch.org/ ).
# See COPYING for license details.

pkgname=xray
pkgver=93.73f8221
pkgrel=2
groups=('blackarch' 'blackarch-recon')
pkgdesc='A tool for recon, mapping and OSINT gathering from public networks.'
arch=('x86_64' 'aarch64')
url='https://github.com/evilsocket/xray'
license=('GPL3')
depends=('glibc')
makedepends=('git' 'go-pie' 'go-bindata' 'glide')
source=('git+https://github.com/evilsocket/xray.git')
sha512sums=('SKIP')
_url='github.com/evilsocket/xray'

pkgver() {
  cd $pkgname

  echo $(git rev-list --count HEAD).$(git rev-parse --short HEAD)
}

build() {
  mkdir -p "src/$_url" && rm -rf "src/$_url" && mv $pkgname "src/$_url" &&
    cd "src/$_url"

  GOPATH="$srcdir" go get -d -t $_url
  GOPATH="$srcdir" go get -u github.com/jteeuwen/go-bindata/...
  GOPATH="$srcdir" make get_glide
  GOPATH="$srcdir" make install_dependencies
  GOPATH="$srcdir" make build
}

package() {
  cd "src/$_url"

  install -dm 755 "$pkgdir/usr/bin"

  install -Dm 755 "build/$pkgname" "$pkgdir/usr/bin/$pkgname"
  install -Dm 644 -t "$pkgdir/usr/share/doc/$pkgname/" README.md
  install -Dm 644 LICENSE "$pkgdir/usr/share/licenses/$pkgname/LICENSE"
}

