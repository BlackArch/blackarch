# This file is part of BlackArch Linux ( http://blackarch.org ).
# See COPYING for license details.

pkgname='reaver-wps-fork-t6x'
pkgver='1.5.2'
pkgrel=1
pkgdesc='Brute force attack against Wifi Protected Setup mod with Pixie Dust Attack.'
arch=('i686' 'x86_64' 'armv6h' 'armv7h')
url='https://github.com/t6x/reaver-wps-fork-t6x'
license=('GPL2')
depends=('libpcap' 'sqlite3' 'pixiewps')
backup=('etc/reaver/reaver.db')
replaces=('reaver' 'reaver-svn' 'reaver-git')
conflicts=('reaver' 'reaver-svn' 'reaver-git')
provides=('reaver')
source=("https://github.com/t6x/reaver-wps-fork-t6x/archive/v${pkgver}.tar.gz")
sha1sums=('ceefd6070cd83d55e13514beceeaf8b22a5b9c24')

build() {
  cd "$srcdir/reaver-wps-fork-t6x-$pkgver/src"

  ./configure --prefix=/usr --sysconfdir=/etc

  make
}

package() {
  cd "$srcdir/reaver-wps-fork-t6x-$pkgver"

  install -Dm755 src/reaver "$pkgdir/usr/bin/reaver"
  install -Dm755 src/wash "$pkgdir/usr/bin/wash"
  install -Dm644 src/reaver.db "$pkgdir/etc/reaver/reaver.db"
  install -Dm644 docs/README "$pkgdir/usr/share/doc/reaver/README"
  install -Dm644 docs/reaver.1.gz "$pkgdir/usr/share/man/man1/reaver.1.gz"
}
