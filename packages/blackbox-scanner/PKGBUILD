# This file is part of BlackArch Linux ( https://www.blackarch.org/ ).
# See COPYING for license details.

pkgname=blackbox-scanner
pkgver=1.7a25220
pkgrel=1
epoch=4
pkgdesc='Dork scanner & bruteforcing & hash cracker with blackbox framework.'
groups=('blackarch' 'blackarch-scanner' 'blackarch-recon' 'blackarch-cracker')
arch=('any')
url='https://github.com/sepehrdaddev/blackbox'
license=('custom:unknown')
depends=('python2' 'python2-requests' 'python2-pexpect' 'python2-passlib')
makedepends=('git')
source=("$pkgname::git+https://github.com/sepehrdaddev/blackbox.git")
sha512sums=('SKIP')

pkgver() {
  cd $pkgname

  echo $(git rev-list --count HEAD).$(git rev-parse --short HEAD)
}

package() {
  cd $pkgname

  install -dm 755 "$pkgdir/usr/bin"
  install -dm 755 "$pkgdir/usr/share/$pkgname"

  install -Dm 644 -t "$pkgdir/usr/share/doc/$pkgname/" README.md
  install -Dm 644 COPYING "$pkgdir/usr/share/licenses/$pkgname/COPYING"

  rm README.md COPYING

  cp -a * "$pkgdir/usr/share/$pkgname/"

  cat > "$pkgdir/usr/bin/$pkgname" << EOF
#!/bin/sh
exec /usr/share/$pkgname/blackbox.py "\$@"
EOF

  chmod +x "$pkgdir/usr/bin/$pkgname"
}

