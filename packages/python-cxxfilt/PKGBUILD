# This file is part of BlackArch Linux ( https://www.blackarch.org/ ).
# See COPYING for license details.

pkgbase=python-cxxfilt
pkgname=('python2-cxxfilt' 'python-cxxfilt')
_pkgname=cxxfilt
pkgver=0.3.0
pkgrel=2
pkgdesc='Python interface to c++filt / abi::__cxa_demangle.'
arch=('any')
url='https://pypi.org/project/cxxfilt/#files'
license=('BSD')
makedepends=('python2-setuptools' 'python-setuptools')
source=("https://files.pythonhosted.org/packages/d7/dc/71ac606f7dfa71d49e3dc126b49b18daefaf6bd953078858af30fde40702/$_pkgname-$pkgver.tar.gz")
sha512sums=('87cf42c8ae7a2eeb74712b2d952d011ca1e2ac90e3d9c7d254eeef1dd88ceba1f25aff377852d384f73c38cd0183898f3ba9ccde0d9f09d03025c0262290704f')

prepare() {
  cp -a "$_pkgname-$pkgver"{,-2}
}

build() {
  cd "$_pkgname-$pkgver-2"

  python2 setup.py build

  cd "$srcdir/$_pkgname-$pkgver"

  python setup.py build
}

package_python2-cxxfilt() {
  depends=('python2')

  cd "$_pkgname-$pkgver-2"

  python2 setup.py install --prefix=/usr --root="$pkgdir" -O1 --skip-build
}

package_python-cxxfilt() {
  depends=('python')

  cd "$_pkgname-$pkgver"

  python setup.py install --prefix=/usr --root="$pkgdir" -O1 --skip-build
}

