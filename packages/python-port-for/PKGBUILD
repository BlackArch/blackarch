# This file is part of BlackArch Linux ( https://www.blackarch.org/ ).
# See COPYING for license details.

_pkgname=port-for
pkgbase="python-$_pkgname"
pkgname=("python-$_pkgname" "python2-$_pkgname")
pkgver=0.6.1
pkgrel=2
pkgdesc='Utility that helps with local TCP ports managment.'
arch=('any')
url='https://github.com/kmike/port-for'
license=('MIT')
makedepends=('python-setuptools' 'python2-setuptools')
checkdepends=('python-tox' 'python2-tox')
source=("https://github.com/kmike/port-for/archive/refs/tags/v$pkgver.tar.gz")
sha512sums=('a5ed2444ff5922087ff3c91d26af588b03cfd7a984065fd98249c57b6dfb5a7fbd7af239685e716db7062b8bc3c44b4eb22f4589d2f701d7991f7ecbd26a66ea')

build() {
  cd "$_pkgname-$pkgver"

  python setup.py build
  python2 setup.py build
}

package_python2-port-for() {
  depends=('python2')

  cd "$_pkgname-$pkgver"

  python2 setup.py install --root="$pkgdir" --prefix=/usr -O1 --skip-build

  mv "$pkgdir/usr/bin/port-for" "$pkgdir/usr/bin/port-for2"
}

package_python-port-for() {
  depends=('python')

  cd "$_pkgname-$pkgver"

  python setup.py install --root="$pkgdir" --prefix=/usr -O1 --skip-build
}

