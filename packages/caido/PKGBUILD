# This file is part of BlackArch Linux ( https://www.blackarch.org/ ).
# See COPYING for license details.

pkgbase=caido
pkgname=('caido-cli' 'caido-desktop')
pkgver=0.52.0
pkgrel=1
pkgdesc='Intercepting proxy to replay, inject, scan and fuzz HTTP requests.'
arch=('x86_64' 'aarch64')
groups=('blackarch' 'blackarch-webapp' 'blackarch-proxy' 'blackarch-scanner'
        'blackarch-fuzzer')
url='https://caido.io/'
license=('custom:private')
options=(!strip)
_appimage="caido-desktop-v$pkgver-linux-$CARCH.AppImage"
noextract=($_appimage)
source=(
  "https://caido.download/releases/v$pkgver/caido-desktop-v$pkgver-linux-$CARCH.AppImage"
  "https://caido.download/releases/v$pkgver/caido-cli-v$pkgver-linux-$CARCH.tar.gz"
  'LICENSE'
)
sha512sums=('88ed092074b0a933744412c187bed07e76ca3c4a6a8e327302a732baf8a87d5af8fa7e7a1e7e9f09459ce934fde455002b19419b9a2dc2aed9dc37ee5e8b4713'
            '6dd7c244d40658c3fa0ff89a89f36fb3ede3db4ead9e8a24e5067b9c90835c923a255ad05836a4feace90a3e18735efa743faa68f9398b30e8c093d99e937b9e'
            '83a63c23f42f381a1f312387896598409dbd7b6dad8c9d89b7b747ff09f143c173272c724f0f6755ddd0bb26662866a8f7a8992a521f659be57ef0fb82f0d917')

prepare() {
  chmod +x $_appimage

  ./$_appimage --appimage-extract
}

build() {
  sed -i -E \
    "s|Exec=AppRun|Exec=env DESKTOPINTEGRATION=false /usr/bin/$pkgbase|" \
    "squashfs-root/$pkgbase.desktop"

    chmod -R a-x+rX squashfs-root/usr
}

package_caido-desktop() {
  depends=('glibc' 'gcc-libs')
  provides=('caido')

  install -Dm 755 "$srcdir/$_appimage" "$pkgdir/opt/$pkgbase/$pkgbase.AppImage"
  install -Dm 644 "$srcdir/LICENSE" "$pkgdir/opt/$pkgbase/LICENSE"

  install -Dm 644 "$srcdir/squashfs-root/$pkgbase.desktop" \
    "$pkgdir/usr/share/applications/$pkgbase.desktop"

  install -dm 755 "$pkgdir/usr/share/applications/"
  cp -a "$srcdir/squashfs-root/usr/share/icons" "$pkgdir/usr/share/"
  cp -a "$srcdir/squashfs-root/$pkgbase.desktop" \
    "$pkgdir/usr/share/applications/"

  install -dm 755 "$pkgdir/usr/bin"
  ln -s "/opt/$pkgbase/$pkgbase.AppImage" "$pkgdir/usr/bin/$pkgbase"

  install -dm 755 "$pkgdir/usr/share/licenses/$pkgname/"
  install -m 644 LICENSE "$pkgdir/usr/share/licenses/$pkgname/"
}

package_caido-cli() {
  depends=('glibc' 'gcc-libs')

  install -dm 755 "$pkgdir/usr/share/licenses/$pkgname/"
  install -m 644 LICENSE "$pkgdir/usr/share/licenses/$pkgname/"
  install -Dm 755 caido-cli "$pkgdir/usr/bin/caido-cli"
}

