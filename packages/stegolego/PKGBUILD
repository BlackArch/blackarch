# This file is part of BlackArch Linux ( https://www.blackarch.org/ ).
# See COPYING for license details.

pkgname=stegolego
pkgver=8.85354f6
pkgrel=3
groups=('blackarch' 'blackarch-stego')
pkgdesc='Simple program for using stegonography to hide data within BMP images.'
arch=('x86_64' 'aarch64')
url='https://github.com/razc411/StegoLeggo'
license=('custom:unknown')
depends=('')
makedepends=('git')
source=("$pkgname::git+https://github.com/razc411/StegoLeggo.git")
sha512sums=('SKIP')

pkgver() {
  cd $pkgname

  echo $(git rev-list --count HEAD).$(git rev-parse --short HEAD)
}

build() {
  cd $pkgname

  gcc *.c -o stegolego
}

package() {
  cd $pkgname

  install -Dm 755 $pkgname "$pkgdir/usr/bin/$pkgname"
  install -Dm 644 -t "$pkgdir/usr/share/doc/$pkgname/" README
}

