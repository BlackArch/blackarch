# This file is part of BlackArch Linux ( http://blackarch.org ).
# See COPYING for license details.

pkgname='gmock'
pkgver='1.7.0'
_ubuntu_ver='0ubuntu1'
pkgrel=1
pkgdesc='A library for writing and using C++ mock classes.'
arch=('x86_64' 'armv6h' 'armv7h' 'aarch64')
url='http://code.google.com/p/googlemock/'
license=('BSD')
depends=('python2' 'gtest')
options=(!libtool)
source=("http://googlemock.googlecode.com/files/gmock-${pkgver}.zip"
        'import_private_module.patch'
        'reenable_make_install.patch')
sha1sums=('f9d9dd882a25f4069ed9ee48e70aff1b53e3c5a5'
          '930b402fef3fbc2b13b4e3e7edc77a6e5249e2ca'
          '2b9d278bf585d61b3333fce36e26d5310e52e5a6')

prepare() {
  cd "$srcdir/gmock-$pkgver"

  patch -Np1 -i "$srcdir/import_private_module.patch"
  patch -Np1 -i "$srcdir/reenable_make_install.patch"

  find -name \*.py -exec sed -i \
    's/#!\/usr\/bin\/env python/#!\/usr\/bin\/env python2/' {} \;
}

build() {
  cd "$srcdir/gmock-$pkgver"

  ./configure --prefix=/usr --enable-external-gtest

  make
}

package() {
  cd "$srcdir/gmock-$pkgver"

  mkdir -p "$pkgdir/usr/share/gmock"
  mkdir -p "$pkgdir/usr/share/doc/gmock"

  make DESTDIR="$pkgdir" install

  install -Dm755 "scripts/generator/gmock_gen.py" \
    "${pkgdir}/usr/bin/gmock_gen.py"
  install -Dm644 LICENSE "${pkgdir}/usr/share/licenses/gmock/LICENSE"

  cp -a --preserve=mode,links "scripts/generator/cpp" \
    "${pkgdir}/usr/share/gmock/"

  for i in README CONTRIBUTORS
  do
    install -m644 ${i} "${pkgdir}/usr/share/doc/gmock/"
  done

  for f in $(ls "${pkgdir}"/usr/bin/*.py)
  do
    mv ${f} $(echo ${f} | sed s/\.py//)
  done

  rm -f "${pkgdir}"/usr/lib/libgtest*
  rm -rf "${pkgdir}"/usr/include/gtest*
}
