# This file is part of BlackArch Linux ( https://www.blackarch.org/ ).
# See COPYING for license details.

pkgbase=python-javalang
pkgname=('python2-javalang' 'python-javalang')
_pkgname=javalang
pkgver=0.12.0
pkgrel=2
pkgdesc='Pure Python Java parser and tools.'
arch=('any')
url='https://pypi.org/project/javalang/#files'
license=('MIT')
makedepends=('python2-setuptools' 'python-setuptools')
source=("https://files.pythonhosted.org/packages/a5/b8/a0133911319cb349e6709e2ced39a34433490b393766c32088003c414a00/$_pkgname-$pkgver.tar.gz")
sha512sums=('ed516ad64e4a73e3f4981c4794b04485c93c4dfead1a03c0f3b5470748cad5c47cf5265df1fc220b12aff8aece4c0261befa24830e3f7038b0b4d560253e4c65')

prepare() {
  cp -a "$_pkgname-$pkgver" "$_pkgname-$pkgver-2"
}

build() {
  cd "$_pkgname-$pkgver-2"

  python2 setup.py build

  cd "$srcdir/$_pkgname-$pkgver"

  python setup.py build
}

package_python2-javalang() {
  depends=('python2' 'python2-six' 'java-runtime')

  cd "$_pkgname-$pkgver-2"

  python2 setup.py install --prefix=/usr --root="$pkgdir" --optimize=1 \
    --skip-build
}

package_python-javalang() {
  depends=('python' 'python-six' 'java-runtime')

  cd "$_pkgname-$pkgver"

  python setup.py install --prefix=/usr --root="$pkgdir" --optimize=1 \
    --skip-build
}

