# This file is part of BlackArch Linux ( https://www.blackarch.org/ ).
# See COPYING for license details.

pkgname=trinity
pkgver=5199.ba2360ed
pkgrel=1
pkgdesc='A Linux System call fuzzer.'
groups=('blackarch' 'blackarch-fuzzer')
arch=('x86_64' 'aarch64')
url='http://codemonkey.org.uk/projects/trinity/'
license=('GPL')
makedepends=('git')
source=("git+https://github.com/kernelslacker/$pkgname.git")
sha512sums=('SKIP')

pkgver() {
  cd $pkgname

  echo $(git rev-list --count HEAD).$(git rev-parse --short HEAD)
}

build() {
  cd $pkgname

  ./configure

  make
}

package() {
  cd $pkgname

  install -Dm 644 README "$pkgdir/usr/share/doc/$pkgname/README"
  install -Dm 644 -t "$pkgdir/usr/share/doc/$pkgname" Documentation/*.txt
  install -Dm 644 COPYING "$pkgdir/usr/share/licenses/$pkgname/COPYING"

  make DESTDIR="$pkgdir/usr" install
}

