# This file is part of BlackArch Linux ( http://blackarch.org ).
# See COPYING for license details.

pkgname='trinity'
pkgver=4475.0064f75
pkgrel=1
pkgdesc='A Linux System call fuzzer.'
groups=('blackarch' 'blackarch-fuzzer')
arch=('i686' 'x86_64' 'armv6h' 'armv7h')
url='http://codemonkey.org.uk/projects/trinity/'
license=('GPL')
makedepends=('git')
source=('git+https://github.com/kernelslacker/trinity.git')
sha1sums=('SKIP')

pkgver() {
  cd "$srcdir/trinity"

  echo $(git rev-list --count HEAD).$(git rev-parse --short HEAD)
}

build() {
  cd "$srcdir/trinity"

  ./configure.sh

  make
}

package() {
  cd "$srcdir/trinity"

  install -Dm644 README "$pkgdir/usr/share/doc/trinity/README"

  cd Documentation

  install -Dm644 -t "$pkgdir/usr/share/doc/trinity" HACKING.txt Known-bugs.txt \
    TODO pro-tips.txt

  cd ..

  install -Dm644 COPYING "$pkgdir/usr/share/licenses/trinity/COPYING"

  make DESTDIR="$pkgdir/usr" install
}
