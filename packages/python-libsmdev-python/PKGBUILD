# This file is part of BlackArch Linux ( https://www.blackarch.org/ ).
# See COPYING for license details.

pkgbase=python-libsmdev-python
pkgname=('python2-libsmdev-python' 'python-libsmdev-python')
_pkgname=libsmdev
pkgver=20191221
pkgrel=1
pkgdesc='Python bindings module for libsmdev.'
arch=('x86_64' 'aarch64')
url='https://pypi.python.org/pypi/libsmdev-python/'
license=('LGPL3')
makedepends=('python2-setuptools' 'python-setuptools')
source=("https://files.pythonhosted.org/packages/c0/88/0d88984259d405a5e65472972b6fbb4e318e08bb207ed2384d8fedf3e159/$_pkgname-python-$pkgver.tar.gz")
sha512sums=('fc0044465e0feecc882ea9a2d819bacc8cb9c22eb28c19e189e25d80f6849deface9780b3323223377d5961345fa183c750f30ae3a234a0dc7294499516ab11b')

prepare() {
  cp -a "$_pkgname-$pkgver"{,-2}
}

build() {
  cd "$_pkgname-$pkgver-2"

  python2 setup.py build

  cd "$srcdir/$_pkgname-$pkgver"

  python setup.py build
}

package_python2-libsmdev-python() {
  depends=('python2')

  cd "$_pkgname-$pkgver-2"

  python2 setup.py install --root="$pkgdir" --prefix=/usr -O1 --skip-build
}

package_python-libsmdev-python() {
  depends=('python')

  cd "$_pkgname-$pkgver"

  python setup.py install --root="$pkgdir" --prefix=/usr -O1 --skip-build
}

