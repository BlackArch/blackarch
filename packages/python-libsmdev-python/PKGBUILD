# This file is part of BlackArch Linux ( https://www.blackarch.org/ ).
# See COPYING for license details.

pkgbase=python-libsmdev-python
pkgname=('python2-libsmdev-python' 'python-libsmdev-python')
_pkgname=libsmdev
pkgver=20210418
pkgrel=1
pkgdesc='Python bindings module for libsmdev.'
arch=('x86_64' 'aarch64')
url='https://pypi.python.org/pypi/libsmdev-python/'
license=('LGPL3')
makedepends=('python2-setuptools' 'python-setuptools')
source=("https://files.pythonhosted.org/packages/a8/e3/c0835d76bfd26d7d7e75db9b700bd65a0019e948a959874845e57ca382c5/$_pkgname-$pkgver.tar.gz")
sha512sums=('446e54d65238a66e55671db14ac0d15578f46bec70809570caa4c1f46d5fa01421704713e072297df230452c8c7592aa4d27409455c9839916b7de80399b6581')

prepare() {
  cp -a "$_pkgname-$pkgver"{,-2}
}

build() {
  cd "$_pkgname-$pkgver-2"

  python2 setup.py build

  cd "$srcdir/$_pkgname-$pkgver"

  python setup.py build
}

package_python2-libsmdev-python() {
  depends=('python2')

  cd "$_pkgname-$pkgver-2"

  python2 setup.py install --root="$pkgdir" --prefix=/usr -O1 --skip-build
}

package_python-libsmdev-python() {
  depends=('python')

  cd "$_pkgname-$pkgver"

  python setup.py install --root="$pkgdir" --prefix=/usr -O1 --skip-build
}

