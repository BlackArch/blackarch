# This file is part of BlackArch Linux ( https://www.blackarch.org/ ).
# See COPYING for license details.

pkgbase=python-libsmdev-python
pkgname=('python2-libsmdev-python' 'python-libsmdev-python')
_pkgname=libsmdev
pkgver=20200210
pkgrel=2
pkgdesc='Python bindings module for libsmdev.'
arch=('x86_64' 'aarch64')
url='https://pypi.python.org/pypi/libsmdev-python/'
license=('LGPL3')
makedepends=('python2-setuptools' 'python-setuptools')
source=("https://files.pythonhosted.org/packages/94/a6/b541eac0dd4f0f5e52caf5317ee559d5a6b523088061be9c88c16cd3f4da/libsmdev-python-$pkgver.tar.gz")
sha512sums=('e90699655210f7a3569286aee658e54b602a237156631d8825de46629fc4ae648dd31ef1ca3130075c0ce9ab43f0f48016f1098bf28c1547b0be318c6d762360')

prepare() {
  cp -a "$_pkgname-$pkgver"{,-2}
}

build() {
  cd "$_pkgname-$pkgver-2"

  python2 setup.py build

  cd "$srcdir/$_pkgname-$pkgver"

  python setup.py build
}

package_python2-libsmdev-python() {
  depends=('python2')

  cd "$_pkgname-$pkgver-2"

  python2 setup.py install --root="$pkgdir" --prefix=/usr -O1 --skip-build
}

package_python-libsmdev-python() {
  depends=('python')

  cd "$_pkgname-$pkgver"

  python setup.py install --root="$pkgdir" --prefix=/usr -O1 --skip-build
}

