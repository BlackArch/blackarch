# This file is part of BlackArch Linux ( http://blackarch.org ).
# See COPYING for license details.

pkgname='tinfoleak2'
pkgver=41.c45c33e
pkgrel=1
groups=('blackarch' 'blackarch-recon' 'blackarch-social' 'blackarch-webapp')
pkgdesc='The most complete open-source tool for Twitter intelligence analysis.'
arch=('any')
url='http://www.vicenteaguileradiaz.com/tools/'
license=('custom:unknown')
depends=('python2' 'python2-tweepy' 'python2-jinja' 'python2-pyopenssl'
         'python2-exiv2' 'python2-exifread' 'python2-oauth2' 'python2-pillow')
makedepends=('git')
source=('git+https://github.com/vaguileradiaz/tinfoleak.git')
sha1sums=('SKIP')

pkgver() {
  cd "$srcdir/tinfoleak"

  echo $(git rev-list --count HEAD).$(git rev-parse --short HEAD)
}

package() {
  cd "$srcdir/tinfoleak"

  mkdir -p "$pkgdir/usr/bin"
  mkdir -p "$pkgdir/usr/share/tinfoleak2"

  install -Dm644 -t "$pkgdir/usr/share/doc/tinfoleak2" README.md INSTALL.txt
  install -Dm644 LICENSE.txt "$pkgdir/usr/share/licenses/tinfoleak2/LICENSE.txt"

  rm *.txt

  cp -a * "$pkgdir/usr/share/tinfoleak2/"

  cat > "$pkgdir/usr/bin/tinfoleak2" << EOF
#!/bin/sh
cd /usr/share/tinfoleak2
exec python2 tinfoleak.py "\${@}"
EOF

  chmod a+x "$pkgdir/usr/bin/tinfoleak2"
}
