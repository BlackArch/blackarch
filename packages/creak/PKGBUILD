# This file is part of BlackArch Linux ( https://www.blackarch.org/ ).
# See COPYING for license details.

pkgname=creak
pkgver=40.52b0d74
pkgrel=3
groups=('blackarch' 'blackarch-networking' 'blackarch-exploitation'
        'blackarch-sniffer')
pkgdesc='Poison, reset, spoof, redirect MITM script.'
arch=('any')
url='https://github.com/codepr/creak'
license=('GPL3')
makedepends=('git')
depends=('python2' 'python2-pypcap' 'python2-dpkt' 'python2-scapy'
         'python2-pydnet')
source=("git+https://github.com/codepr/$pkgname.git")
sha512sums=('SKIP')

pkgver() {
  cd $pkgname

  echo $(git rev-list --count HEAD).$(git rev-parse --short HEAD)
}

package() {
  cd $pkgname

  install -dm 755 "$pkgdir/usr/bin"
  install -dm 755 "$pkgdir/usr/share/$pkgname"

  install -Dm 644 -t "$pkgdir/usr/share/doc/$pkgname/" README.md
  install -Dm 644 LICENSE "$pkgdir/usr/share/licenses/$pkgname/LICENSE"

  rm README.md LICENSE

  cp -a * "$pkgdir/usr/share/$pkgname"

  cat > "$pkgdir/usr/bin/$pkgname" << EOF
#!/bin/sh
cd /usr/share/$pkgname
exec python2 $pkgname.py "\$@"
EOF

  chmod +x "$pkgdir/usr/bin/$pkgname"
}

