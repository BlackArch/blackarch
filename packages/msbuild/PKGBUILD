# This file is part of BlackArch Linux ( https://www.blackarch.org/ ).
# See COPYING for license details.

# This file is based on the AUR PKGBUILD file created by:
# Maintainer: Florian Maunier <fmauneko@dissidence.ovh>
# Contributor: jtmb <packaging at technologicalwizardry dot com>
# Thanks to them & their work.

pkgbase=msbuild
pkgname=('msbuild' 'msbuild-sdkresolver')
pkgver=16.4+xamarinxplat.2019.11.27.22.07
pkgrel=1
arch=('x86_64')
url='https://github.com/mono/msbuild'
license=('MIT')
depends=('mono>=6.4.0')
makedepends=('unzip' 'dotnet-host>=3.1.0' 'dotnet-sdk>=3.1.0')
source=("https://download.mono-project.com/sources/msbuild/msbuild-${pkgver}.tar.xz"
        'copy_hostfxr.patch'
        'fix_bashisms.patch'
        'license_check_is_case_sensitive.diff'
        'case_sensitive_dotnetbits.patch')
sha512sums=('9d99ab648db85c5d61c8dd8f8a2e5e0e0b323b3a9ef093a2749fc960238ea6c56b5070a91616598e50de53985aec953c42b441e86d72305ec56d26fcc19cf5a3'
            '26859f2a9e727c473e5b8aebd7ccd0fa92c6b4b0d51d0737719ca03ea9cbb41d89839ebe0494ea1bf8d3ee63f27c253052b7643c9aa8c40a49ca3f37e90e2963'
            '6d90545d53c1f5660364d13fd42ce98ab42a75cbf38581e80e690a695a019620b7f12317d48a768024ad2e028976761607b4065a026c90dc008fccdecf35fef5'
            '009601659b9f35cd383be165350bf106ae7e2ae09e16d17cb2882484ee95ba74a1a2a07e18e19e39c076a0fabd253f85a8655276d97c3d294d5e864330d12ec7'
            '82b52fbb5031e86cf5d5eef431e99e31f857329e000f4a6f3d428b9926de5a2515357bb0060f1b914d411f3041bfeb988b12bf1e3ba6c81a1abcbe504e1115e6')

prepare() {
  cd "$pkgname-${pkgver%+*}"

  patch --forward --strip=1 --input="$srcdir/fix_bashisms.patch"
  patch --forward --strip=1 --input="$srcdir/copy_hostfxr.patch"
  patch --forward --strip=1 --input="$srcdir/license_check_is_case_sensitive.diff"
  patch --forward --strip=1 --input="$srcdir/case_sensitive_dotnetbits.patch"
}

build() {
  cd "$pkgname-${pkgver%+*}"

  DOTNET_MSBUILD_SDK_RESOLVER_CLI_DIR=/opt/dotnet \
    ./eng/cibuild_bootstrapped_msbuild.sh --host_type mono \
    --configuration Release \
    --skip_tests /p:DisableNerdbankVersioning=true

  ./stage1/mono-msbuild/msbuild mono/build/install.proj \
    /p:MonoInstallPrefix="$srcdir/target/usr" /p:Configuration=Release-MONO \
    /p:IgnoreDiffFailure=true

  sed -i "s@${srcdir}/target@@g" "$srcdir/target/usr/bin/msbuild"

  find "$srcdir/target/usr/lib/mono/" -name Microsoft.DiaSymReader.Native.*dll \
    -delete
  find "$srcdir/target/usr/lib/mono/" -name *.dylib -delete
  find "$srcdir/target/usr/lib/mono/" -name *.so -delete
}

package_msbuild() {
  pkgdesc="Xamarin implementation of the Microsoft build system"

  cp -dr --no-preserve='ownership' "$srcdir/target/usr" "$pkgdir"
  rm -rf "$pkgdir/usr/lib/mono/msbuild/Current/bin/SdkResolvers/Microsoft.DotNet.MSBuildSdkResolver"
}

package_msbuild-sdkresolver() {
  pkgdesc="Xamarin implementation of the Microsoft build system (SDK resolver)"
  depends=('msbuild' 'dotnet-host>=3.1.0')

  install -dm 755 "$pkgdir/usr/lib/mono/msbuild/Current/bin/SdkResolvers/"

  cp -dr --no-preserve='ownership' \
    "$srcdir/target/usr/lib/mono/msbuild/Current/bin/SdkResolvers/Microsoft.DotNet.MSBuildSdkResolver" \
    "$pkgdir/usr/lib/mono/msbuild/Current/bin/SdkResolvers/"
}

