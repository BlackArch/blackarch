# This file is part of BlackArch Linux ( https://www.blackarch.org/ ).
# See COPYING for license details.

pkgname=python-semgrep
_pkgname=semgrep
pkgver=0.18.0
pkgrel=2
pkgdesc='Lightweight static analysis for many languages.'
arch=('any')
url='https://pypi.org/project/semgrep/#files'
license=('LGPL')
depends=('python' 'python-colorama' 'python-requests' 'python-attrs'
         'python-tqdm' 'python-packaging' 'python-ruamel-yaml' 'python-wheel')
makedepends=('python-setuptools')
source=("https://files.pythonhosted.org/packages/cb/e4/6c731a2797e6e2e0c94ea2b7060d460b623f0b65d6bee16f0afaae973037/$_pkgname-$pkgver.tar.gz"
        'setup.py.patch')
sha512sums=('4dc496d808f8b6c834fa5c3f918516ecb068fe16fe3bf7a522e0009d32190167e1c08c4b9f0566e36d07ea8cc2c77811f084db628c6adec47cf161fff1e8bba4'
            '7513f01d7d0ac30ac8c06ef17c72643f63f0cf5867d132c99bd2a8370e63619b0f680bb7fed3f6689ea1a2ece91483b40ef28acc09641d6280e113a394786a03')

prepare() {
  # dirty fix
  patch -p1 < setup.py.patch
}

build() {
  cd "$_pkgname-$pkgver"

  python setup.py build
}

package() {
  cd "$_pkgname-$pkgver"

  python setup.py install --root="$pkgdir" --prefix=/usr -O1 --skip-build

  rm -rf "$pkgdir/usr/lib/python3.8/site-packages/tests"
}

