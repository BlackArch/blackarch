# This file is part of BlackArch Linux ( https://www.blackarch.org/ ).
# See COPYING for license details.

pkgname=python-semgrep
_pkgname=semgrep
pkgver=0.67.0
pkgrel=1
pkgdesc='Lightweight static analysis for many languages.'
arch=('x86_64')
url='https://github.com/returntocorp/semgrep'
license=('LGPL')
depends=('python' 'python-colorama' 'python-requests' 'python-attrs'
         'python-tqdm' 'python-packaging' 'python-ruamel-yaml' 'python-wheel'
         'python-jsonschema' 'python-junit-xml' 'python-peewee'
         'python-wcmatch')
makedepends=('python-setuptools')
source=("https://files.pythonhosted.org/packages/95/7e/1292dcb0f8adc8075c3999719a4d162784d8d47ed38a8e85bd10d0ff4f24/$_pkgname-$pkgver.tar.gz"
        "https://github.com/returntocorp/$_pkgname/releases/download/v$pkgver/$_pkgname-v$pkgver-ubuntu-16.04.tgz")
sha512sums=('7d6a1cb0fb2f2ec5ef9db1c00a920e64affdb71556c585611edfc9c65a36c67e7b066aac7b1a17b3688d8fcb76aa86e0f06fbbe3117559e01305ba4047230ef5'
            '536dbaced1890f97e636233da6181bcf4bce767f8980d76b922a816594c47475690c352e0b9030114d645bbaefe54fb3af103176d0d2dd92f887832c05538d65')

prepare() {
  cd "$_pkgname-$pkgver"

  sed -i 's/ruamel.yaml==0.16.10/ruamel.yaml>=0.16.10/' setup.py
}

build() {
  cd "$_pkgname-$pkgver"

  export SEMGREP_CORE_BIN="$srcdir/semgrep-files/semgrep-core"
  export SPACEGREP_BIN="$srcdir/semgrep-files/spacegrep"

  python setup.py build
}

package() {
  cd "$_pkgname-$pkgver"

  python setup.py install --root="$pkgdir" --prefix=/usr -O1 --skip-build

  rm -rf "$pkgdir/usr/lib/python3.9/site-packages/tests"

  # default is enough for now
  strip "$pkgdir/usr/lib/python3.9/site-packages/$_pkgname/bin/spacegrep"
  strip "$pkgdir/usr/lib/python3.9/site-packages/$_pkgname/bin/semgrep-core"
}

