# This file is part of BlackArch Linux ( https://www.blackarch.org/ ).
# See COPYING for license details.

pkgname=python-semgrep
_pkgname=semgrep
pkgver=0.57.0
pkgrel=1
pkgdesc='Lightweight static analysis for many languages.'
arch=('x86_64')
url='https://github.com/returntocorp/semgrep'
license=('LGPL')
depends=('python' 'python-colorama' 'python-requests' 'python-attrs'
         'python-tqdm' 'python-packaging' 'python-ruamel-yaml' 'python-wheel'
         'python-jsonschema' 'python-junit-xml')
makedepends=('python-setuptools')
source=("https://files.pythonhosted.org/packages/74/5b/14268269db2659c0ea737c81b224ced4a261da2e0e7680d88557dd2f58cc/$_pkgname-$pkgver.tar.gz"
        "https://github.com/returntocorp/$_pkgname/releases/download/v$pkgver/$_pkgname-v$pkgver-ubuntu-16.04.tgz")
sha512sums=('40735c9ed1525464b3b4822ebf920eba2976237faf034e8a14a10e0cdff11674e68d8094b5b2d0ad758943152184505fde8a554ed9f197138fbccef9304ac0b9'
            '5166ddffa762ffe52d29f45fc4c981d3e47cd0af24760e0b4f283fff83e5897e91025c78c889ab46d99585562d77f8506cf2c141245bb9342e7b9cf24ae60292')

prepare() {
  cd "$_pkgname-$pkgver"

  sed -i 's/ruamel.yaml==0.16.10/ruamel.yaml>=0.16.10/' setup.py
}

build() {
  cd "$_pkgname-$pkgver"

  export SEMGREP_CORE_BIN="$srcdir/semgrep-files/semgrep-core"
  export SPACEGREP_BIN="$srcdir/semgrep-files/spacegrep"

  python setup.py build
}

package() {
  cd "$_pkgname-$pkgver"

  python setup.py install --root="$pkgdir" --prefix=/usr -O1 --skip-build

  rm -rf "$pkgdir/usr/lib/python3.9/site-packages/tests"

  # default is enough for now
  strip "$pkgdir/usr/lib/python3.9/site-packages/$_pkgname/bin/spacegrep"
  strip "$pkgdir/usr/lib/python3.9/site-packages/$_pkgname/bin/semgrep-core"
}

