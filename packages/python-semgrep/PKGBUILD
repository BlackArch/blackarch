# This file is part of BlackArch Linux ( https://www.blackarch.org/ ).
# See COPYING for license details.

pkgname=python-semgrep
_pkgname=semgrep
pkgver=0.84.0
_pyver=3.10
pkgrel=1
pkgdesc='Lightweight static analysis for many languages.'
arch=('x86_64')
url='https://pypi.org/project/semgrep/#files'
license=('LGPL')
depends=('python' 'python-colorama' 'python-requests' 'python-attrs'
         'python-tqdm' 'python-packaging' 'python-ruamel-yaml' 'python-wheel'
         'python-jsonschema' 'python-junit-xml' 'python-peewee' 'python-click'
         'python-click-option-group' 'python-wcmatch')
makedepends=('python-setuptools')
source=("https://files.pythonhosted.org/packages/14/cc/a8aa6b3de7d54e087d6a54fca17a4f842e62fe2dabd734ff323b7a55855b/$_pkgname-$pkgver.tar.gz"
        "https://github.com/returntocorp/$_pkgname/releases/download/v$pkgver/$_pkgname-v$pkgver-ubuntu-16.04.tgz")
sha512sums=('5dd65c23bad99b65a2cb6bd50f70700e1863d00a8ad1fe8495cf9007dfe64bd3d006521879b10944770ea5777c3eae9215821f2ec3bdf43a1c6af5844df22a08'
            '62e03f45f5e9ae64f7abaff9a8ec7965ebe99f12f643a5384baf53af7c9fa9f1fe5c79dd378b7e8491d13ecc79d39a95661db60603a81b9e66e249625214d5c3')

prepare() {
  cd "$_pkgname-$pkgver"

  sed -i 's/ruamel.yaml==0.16.10/ruamel.yaml>=0.16.10/' setup.py
}

build() {
  cd "$_pkgname-$pkgver"

  export SEMGREP_CORE_BIN="$srcdir/semgrep-files/semgrep-core"
  export SPACEGREP_BIN="$srcdir/semgrep-files/spacegrep"

  python setup.py build
}

package() {
  cd "$_pkgname-$pkgver"

  python setup.py install --root="$pkgdir" --prefix=/usr -O1 --skip-build

  rm -rf "$pkgdir/usr/lib/python$_pyver/site-packages/tests"

  # default is enough for now
  #strip "$pkgdir/usr/lib/python$_pyver/site-packages/$_pkgname/bin/spacegrep"
  strip "$pkgdir/usr/lib/python$_pyver/site-packages/$_pkgname/bin/semgrep-core"
}

