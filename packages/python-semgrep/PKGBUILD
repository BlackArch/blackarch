# This file is part of BlackArch Linux ( https://www.blackarch.org/ ).
# See COPYING for license details.

pkgname=python-semgrep
_pkgname=semgrep
pkgver=0.74.0
pkgrel=1
pkgdesc='Lightweight static analysis for many languages.'
arch=('x86_64')
url='https://pypi.org/project/semgrep/#files'
license=('LGPL')
depends=('python' 'python-colorama' 'python-requests' 'python-attrs'
         'python-tqdm' 'python-packaging' 'python-ruamel-yaml' 'python-wheel'
         'python-jsonschema' 'python-junit-xml' 'python-peewee' 'python-click'
         'python-click-option-group' 'python-wcmatch')
makedepends=('python-setuptools')
source=("https://files.pythonhosted.org/packages/cb/4a/c2b8ad822ce7fb2f9a6d7a966343c86fa88be6f6cecb96ad2995cca86fdf/$_pkgname-$pkgver.tar.gz"
        "https://github.com/returntocorp/$_pkgname/releases/download/v$pkgver/$_pkgname-v$pkgver-ubuntu-16.04.tgz")
sha512sums=('985e509f8831933f34f45a4d1d818a071c1d670279d6c50e4ddcc13fb36df761bac1278d666b817d033a2e70f37e022343d7014bada953e229e33398e4d48e7f'
            '553fec6055c77cb53297c859f844bef9fba8778d4b16ab2594cf4b03e635bbac59dae7f62f04a7cb1f07df7a5d128db64f2f4caf84758b9529014368eac266aa')

prepare() {
  cd "$_pkgname-$pkgver"

  sed -i 's/ruamel.yaml==0.16.10/ruamel.yaml>=0.16.10/' setup.py
}

build() {
  cd "$_pkgname-$pkgver"

  export SEMGREP_CORE_BIN="$srcdir/semgrep-files/semgrep-core"
  export SPACEGREP_BIN="$srcdir/semgrep-files/spacegrep"

  python setup.py build
}

package() {
  cd "$_pkgname-$pkgver"

  python setup.py install --root="$pkgdir" --prefix=/usr -O1 --skip-build

  rm -rf "$pkgdir/usr/lib/python3.9/site-packages/tests"

  # default is enough for now
  strip "$pkgdir/usr/lib/python3.9/site-packages/$_pkgname/bin/spacegrep"
  strip "$pkgdir/usr/lib/python3.9/site-packages/$_pkgname/bin/semgrep-core"
}

