# This file is part of BlackArch Linux ( https://www.blackarch.org/ ).
# See COPYING for license details.

pkgname=python-semgrep
_pkgname=semgrep
pkgver=0.30.0
pkgrel=1
pkgdesc='Lightweight static analysis for many languages.'
arch=('any')
url='https://pypi.org/project/semgrep/#files'
license=('LGPL')
depends=('python' 'python-colorama' 'python-requests' 'python-attrs'
         'python-tqdm' 'python-packaging' 'python-ruamel-yaml' 'python-wheel')
makedepends=('python-setuptools')
source=("https://files.pythonhosted.org/packages/16/30/48a6edb229c1e23e96844a2b74e4266e36c69aa095ddc7ab63eea4d73da4/$_pkgname-$pkgver.tar.gz"
        'setup.py.patch')
sha512sums=('82cf2bcf99ad68553ec8cfe9f5e8067a006bba9202dcddc216cfd4d0878fe8a6f6d435a7828f7d11921d78120247f883287c9a52fbbaf8c0bef0b0222dde2fde'
            '0f51707a607ee52ac1e106a3c498615b054cd1e1095294123734115fa073c26c28a569dee483b220b8e57f35feeb4374a9a1dd481a70404289b57b2ea450c0c9')

prepare() {
  # dirty fix
  patch -p1 < setup.py.patch
}

build() {
  cd "$_pkgname-$pkgver"

  python setup.py build
}

package() {
  cd "$_pkgname-$pkgver"

  python setup.py install --root="$pkgdir" --prefix=/usr -O1 --skip-build

  rm -rf "$pkgdir/usr/lib/python3.8/site-packages/tests"
}

