# This file is part of BlackArch Linux ( https://www.blackarch.org/ ).
# See COPYING for license details.

pkgname=python-semgrep
_pkgname=semgrep
pkgver=0.49.0
pkgrel=1
pkgdesc='Lightweight static analysis for many languages.'
arch=('x86_64')
url='https://github.com/returntocorp/semgrep'
license=('LGPL')
depends=('python' 'python-colorama' 'python-requests' 'python-attrs'
         'python-tqdm' 'python-packaging' 'python-ruamel-yaml' 'python-wheel'
         'python-jsonschema' 'python-junit-xml')
makedepends=('python-setuptools')
source=("https://files.pythonhosted.org/packages/24/eb/5797c1a1b7382a4f905f3409083a52b86fe4dee38516cab43bd92b17ea83/$_pkgname-$pkgver.tar.gz"
        "https://github.com/returntocorp/$_pkgname/releases/download/v$pkgver/$_pkgname-v$pkgver-ubuntu-16.04.tgz")
sha512sums=('0da1062531ca1bb279cb5da5da6b887af6d70ccd153556b0c15678b2d2fdbb720f5e265c5a684d7d027113d73cf530804299630f38f6083fe35a92e0815d35dc'
            '31fe509d9db22b7d05f83ccbbf2b3ac372a7d2171e3e31d62add9fccf9ccdbedae96e962076b737945bf94a30e821daa7221f2bed524c979158f296a178ae3be')

prepare() {
  cd "$_pkgname-$pkgver"

  sed -i 's/ruamel.yaml==0.16.10/ruamel.yaml>=0.16.10/' setup.py
}

build() {
  cd "$_pkgname-$pkgver"

  export SEMGREP_CORE_BIN="$srcdir/semgrep-files/semgrep-core"
  export SPACEGREP_BIN="$srcdir/semgrep-files/spacegrep"

  python setup.py build
}

package() {
  cd "$_pkgname-$pkgver"

  python setup.py install --root="$pkgdir" --prefix=/usr -O1 --skip-build

  rm -rf "$pkgdir/usr/lib/python3.9/site-packages/tests"

  # default is enough for now
  strip "$pkgdir/usr/lib/python3.9/site-packages/$_pkgname/bin/spacegrep"
  strip "$pkgdir/usr/lib/python3.9/site-packages/$_pkgname/bin/semgrep-core"
}

