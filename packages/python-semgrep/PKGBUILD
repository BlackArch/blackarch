# This file is part of BlackArch Linux ( https://www.blackarch.org/ ).
# See COPYING for license details.

pkgname=python-semgrep
_pkgname=semgrep
pkgver=0.41.1
pkgrel=1
pkgdesc='Lightweight static analysis for many languages.'
arch=('x86_64')
url='https://github.com/returntocorp/semgrep'
license=('LGPL')
depends=('python' 'python-colorama' 'python-requests' 'python-attrs'
         'python-tqdm' 'python-packaging' 'python-ruamel-yaml' 'python-wheel'
         'python-jsonschema' 'python-junit-xml')
makedepends=('python-setuptools')
source=("https://files.pythonhosted.org/packages/aa/07/cc42218fd634118a6d5742468c8c72d625db4a18b467cd56c916caa7a55f/$_pkgname-$pkgver.tar.gz"
        "https://github.com/returntocorp/$_pkgname/releases/download/v$pkgver/semgrep-v$pkgver-ubuntu-16.04.tgz")
sha512sums=('4795be117f640d56a405e8ec86864e33bf13fbfb937c63c15d3d8731c42794f99b8e36aa5a9c235503b700ec2a3cd5e5c427dbb270fde1173284eb2b4fab7266'
            '2299f02d34ceb58c472bfe15329c6139b91fd9d9d4b01a9b3eeefbdd911e6e825983dc9e3f26769a82431a70fec4642d9b178a5d31355bf59b04354b9e0599e4')

prepare() {
  cd "$_pkgname-$pkgver"

  sed -i 's/ruamel.yaml==0.16.10/ruamel.yaml>=0.16.10/' setup.py
}

build() {
  cd "$_pkgname-$pkgver"

  export SEMGREP_CORE_BIN="$srcdir/semgrep-files/semgrep-core"
  export SPACEGREP_BIN="$srcdir/semgrep-files/spacegrep"

  python setup.py build
}

package() {
  cd "$_pkgname-$pkgver"

  python setup.py install --root="$pkgdir" --prefix=/usr -O1 --skip-build

  rm -rf "$pkgdir/usr/lib/python3.9/site-packages/tests"

  # default is enough for now
  strip "$pkgdir/usr/lib/python3.9/site-packages/$_pkgname/bin/"{spacegrep,semgrep-core}
}

