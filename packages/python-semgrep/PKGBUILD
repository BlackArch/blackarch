# This file is part of BlackArch Linux ( https://www.blackarch.org/ ).
# See COPYING for license details.

pkgname=python-semgrep
_pkgname=semgrep
pkgver=0.80.0
_pyver=3.10
pkgrel=1
pkgdesc='Lightweight static analysis for many languages.'
arch=('x86_64')
url='https://pypi.org/project/semgrep/#files'
license=('LGPL')
depends=('python' 'python-colorama' 'python-requests' 'python-attrs'
         'python-tqdm' 'python-packaging' 'python-ruamel-yaml' 'python-wheel'
         'python-jsonschema' 'python-junit-xml' 'python-peewee' 'python-click'
         'python-click-option-group' 'python-wcmatch')
makedepends=('python-setuptools')
source=("https://files.pythonhosted.org/packages/46/eb/418e2fff99a2a3690352309d449d6d3a7c6f9b53e52ac177146aa6f971de/$_pkgname-$pkgver.tar.gz"
        "https://github.com/returntocorp/$_pkgname/releases/download/v$pkgver/$_pkgname-v$pkgver-ubuntu-16.04.tgz")
sha512sums=('84929019be4b416cad19c7399d0ad64b2f19575b8665e25b9be74ad38d18e671051fe44724231e5e0dd4be6dcf288b6421ddcd63af2be640d6fd1e2d7c1e1e5f'
            'd7bf5ca78c5d28583c3b9783bccd31459796a87c63a534a1051730cc5a1e8ba79e470ce01d8e45db90ed2413f2fa620349bcb9ffa2369e6e4a61148b1b4ecd06')

prepare() {
  cd "$_pkgname-$pkgver"

  sed -i 's/ruamel.yaml==0.16.10/ruamel.yaml>=0.16.10/' setup.py
}

build() {
  cd "$_pkgname-$pkgver"

  export SEMGREP_CORE_BIN="$srcdir/semgrep-files/semgrep-core"
  export SPACEGREP_BIN="$srcdir/semgrep-files/spacegrep"

  python setup.py build
}

package() {
  cd "$_pkgname-$pkgver"

  python setup.py install --root="$pkgdir" --prefix=/usr -O1 --skip-build

  rm -rf "$pkgdir/usr/lib/python$_pyver/site-packages/tests"

  # default is enough for now
  strip "$pkgdir/usr/lib/python$_pyver/site-packages/$_pkgname/bin/spacegrep"
  strip "$pkgdir/usr/lib/python$_pyver/site-packages/$_pkgname/bin/semgrep-core"
}

