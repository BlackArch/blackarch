# This file is part of BlackArch Linux ( https://www.blackarch.org/ ).
# See COPYING for license details.

pkgname=python-semgrep
_pkgname=semgrep
pkgver=0.77.0
_pyver=3.10
pkgrel=1
pkgdesc='Lightweight static analysis for many languages.'
arch=('x86_64')
url='https://pypi.org/project/semgrep/#files'
license=('LGPL')
depends=('python' 'python-colorama' 'python-requests' 'python-attrs'
         'python-tqdm' 'python-packaging' 'python-ruamel-yaml' 'python-wheel'
         'python-jsonschema' 'python-junit-xml' 'python-peewee' 'python-click'
         'python-click-option-group' 'python-wcmatch')
makedepends=('python-setuptools')
source=("https://files.pythonhosted.org/packages/95/b6/d35f7f4a61dd7020987bec046fea4bfefc202e2483118107074dfed237b7/$_pkgname-$pkgver.tar.gz"
        "https://github.com/returntocorp/$_pkgname/releases/download/v$pkgver/$_pkgname-v$pkgver-ubuntu-16.04.tgz")
sha512sums=('14d9b1ab76e9ff00c8229ab54085714f3057e0405b31d3a8f0aeb38e3c3e25add701d4438f31d7614af4e7dc973b120be5edecfa9330aa988faafe58b3067cf5'
            '1c58dd2970603cf00422b66c0b521bcaededca7cf0b12f422fe2c1e11d1a22496c156974bc269caaac4b1942e30dedc56d8a11e71df98c49eb3879a32906bf98')

prepare() {
  cd "$_pkgname-$pkgver"

  sed -i 's/ruamel.yaml==0.16.10/ruamel.yaml>=0.16.10/' setup.py
}

build() {
  cd "$_pkgname-$pkgver"

  export SEMGREP_CORE_BIN="$srcdir/semgrep-files/semgrep-core"
  export SPACEGREP_BIN="$srcdir/semgrep-files/spacegrep"

  python setup.py build
}

package() {
  cd "$_pkgname-$pkgver"

  python setup.py install --root="$pkgdir" --prefix=/usr -O1 --skip-build

  rm -rf "$pkgdir/usr/lib/python$_pyver/site-packages/tests"

  # default is enough for now
  strip "$pkgdir/usr/lib/python$_pyver/site-packages/$_pkgname/bin/spacegrep"
  strip "$pkgdir/usr/lib/python$_pyver/site-packages/$_pkgname/bin/semgrep-core"
}

