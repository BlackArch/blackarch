# This file is part of BlackArch Linux ( https://www.blackarch.org/ ).
# See COPYING for license details.

pkgname=python-semgrep
_pkgname=semgrep
pkgver=0.39.1
pkgrel=1
pkgdesc='Lightweight static analysis for many languages.'
arch=('x86_64')
url='https://github.com/returntocorp/semgrep'
license=('LGPL')
depends=('python' 'python-colorama' 'python-requests' 'python-attrs'
         'python-tqdm' 'python-packaging' 'python-ruamel-yaml' 'python-wheel'
         'python-jsonschema' 'python-junit-xml')
makedepends=('python-setuptools')
source=("https://files.pythonhosted.org/packages/55/f9/df3a2e79e9353ff8023f918f0f749c2fbf75864e7868dc87b6c1c9403d73/$_pkgname-$pkgver.tar.gz"
        "https://github.com/returntocorp/$_pkgname/releases/download/v$pkgver/semgrep-v$pkgver-ubuntu-16.04.tgz")
sha512sums=('731728735a7ab3f84d2cd3e79338fb194c2c3d1805a7aeb53856eaec78c90f0bf1ac50955e51a705132716b36ed64c532e1a1463b269bae6284279b5d895cc22'
            '5763a6f14e160ca5691513ef049b555df833ac1424083a6a02c2a8c76c04f50246d6f4fd0f481fe40c94b5ea0c6205beb4ce552d13ec7198fc35bcf4cb15b558')

build() {
  cd "$_pkgname-$pkgver"

  python setup.py build
}

package() {
  cd "$_pkgname-$pkgver"

  python setup.py install --root="$pkgdir" --prefix=/usr -O1 --skip-build

  rm -rf "$pkgdir/usr/lib/python3.9/site-packages/tests"

  # default is enough for now
  strip "$pkgdir/usr/bin/"{spacegrep,semgrep-core}
}

