# This file is part of BlackArch Linux ( https://www.blackarch.org/ ).
# See COPYING for license details.

pkgname=python-semgrep
_pkgname=semgrep
pkgver=0.85.0
_pyver=3.10
pkgrel=4
pkgdesc='Lightweight static analysis for many languages.'
arch=('x86_64')
url='https://pypi.org/project/semgrep/#files'
license=('LGPL')
depends=('python' 'python-colorama' 'python-requests' 'python-attrs'
         'python-tqdm' 'python-packaging' 'python-ruamel-yaml' 'python-wheel'
         'python-jsonschema' 'python-junit-xml' 'python-peewee' 'python-click'
         'python-click-option-group' 'python-wcmatch' 'python-defusedxml')
makedepends=('python-setuptools')
source=("https://files.pythonhosted.org/packages/c7/bf/c25d3932651963a0724f36fc16732eecf2122ab530d14454385eca14f3ff/$_pkgname-$pkgver.tar.gz"
        "https://github.com/returntocorp/$_pkgname/releases/download/v$pkgver/$_pkgname-v$pkgver-ubuntu-16.04.tgz"
        'setup.py.patch')
sha512sums=('e2de9688aa40c2682797404833625dc0e74bb1af370cb062ce279955d038c8a9a2ebd14274d5eb71de8586e8099eee2111254c740a09f3ad861aa0b308e6ffa7'
            'fcd32462cff16172615ebff0150e96544e7076b93bde016b09be04f32de7cf084b3a0f46b441c415bd11158c8ffc2424c6cd97a502578dde08ce4f94ed703516'
            'af7b5226aa0a92837b5f664cf436a7eb28ac14a43c9b0d75218c04606d5b7a772e901d33d745f6b1199a0ce3be37040232f78a47a394e19f0b316703e2812ead')

prepare() {
  cd "$_pkgname-$pkgver"

  patch -p0 < ../setup.py.patch
}

build() {
  cd "$_pkgname-$pkgver"

  export SEMGREP_CORE_BIN="$srcdir/semgrep-files/semgrep-core"
  export SPACEGREP_BIN="$srcdir/semgrep-files/spacegrep"

  python setup.py build
}

package() {
  cd "$_pkgname-$pkgver"

  python setup.py install --root="$pkgdir" --prefix=/usr -O1 --skip-build

  rm -rf "$pkgdir/usr/lib/python$_pyver/site-packages/tests"

  # default is enough for now
  #strip "$pkgdir/usr/lib/python$_pyver/site-packages/$_pkgname/bin/spacegrep"
  strip "$pkgdir/usr/lib/python$_pyver/site-packages/$_pkgname/bin/semgrep-core"
  chmod +x "$pkgdir/usr/lib/python$_pyver/site-packages/$_pkgname/bin/semgrep-core"
}

