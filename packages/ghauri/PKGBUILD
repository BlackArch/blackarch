# This file is part of BlackArch Linux ( https://www.blackarch.org/ ).
# See COPYING for license details.

pkgname=ghauri
pkgver=1.1.2.r1.g3a6fb68
pkgrel=1
pkgdesc='An advanced cross-platform tool that automates the process of detecting and exploiting SQL injection security flaws.'
arch=('any')
groups=('blackarch' 'blackarch-webapp' 'blackarch-exploitation')
url='https://github.com/r0oth3x49/ghauri'
license=('MIT')
depends=('python' 'python-chardet' 'python-tldextract' 'python-colorama'
         'python-requests')
makedepends=('git' 'python-setuptools')
source=("git+https://github.com/r0oth3x49/$pkgname.git")
sha512sums=('SKIP')

pkgver() {
  cd $pkgname

  git describe --long --tags | sed 's/\([^-]*-g\)/r\1/;s/-/./g' | sed 's/#/\./g'
}

build() {
  cd $pkgname

  python setup.py build
}

package() {
  cd $pkgname

  python setup.py install --root="$pkgdir" --prefix=/usr -O1 --skip-build
}

