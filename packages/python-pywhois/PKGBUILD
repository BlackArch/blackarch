# This file is part of BlackArch Linux ( https://www.blackarch.org/ ).
# See COPYING for license details.

pkgbase=python-pywhois
pkgname=('python2-pywhois' 'python-pywhois')
_pkgname=pywhois
pkgver=0.1
pkgrel=6
pkgdesc='A whois program with truncated output.'
arch=('any')
url='https://pypi.org/project/pywhois/#files'
license=('MIT')
makedepends=('python2-setuptools' 'python-setuptools')
source=("https://files.pythonhosted.org/packages/43/7a/2f1a8b11c58b0b783afea6a42d4d08894dfafa074fcf5448e72f3ed3ec55/$_pkgname-$pkgver.tar.gz")
sha512sums=('2a6f8f3d27feede88f6a5503aad5a8fe34c87d6ad6762c72e71e69beff4d1d3f2f1327bbe4a4782034438e865c63db9b9b79a91ca23302486a7dfee8768f212c')

prepare() {
  cp -a "$_pkgname-$pkgver"{,-2}
}

build() {
  cd "$_pkgname-$pkgver-2"

  python2 setup.py build

  cd "$srcdir/$_pkgname-$pkgver"

  python setup.py build
}

package_python2-pywhois() {
  depends=('python2')

  cd "$_pkgname-$pkgver-2"

  python2 setup.py install --prefix=/usr --root="$pkgdir" --optimize=1 \
    --skip-build

  mv "$pkgdir/usr/bin/$_pkgname" "$pkgdir/usr/bin/${_pkgname}2"
}

package_python-pywhois() {
  depends=('python')

  cd "$_pkgname-$pkgver"

  python setup.py install --prefix=/usr --root="$pkgdir" --optimize=1 \
    --skip-build
}

