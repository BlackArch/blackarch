# This file is part of BlackArch Linux ( https://www.blackarch.org/ ).
# See COPYING for license details.

pkgname=hubbit-sniffer
pkgver=74.460ecf8
pkgrel=4
groups=('blackarch' 'blackarch-sniffer' 'blackarch-wireless')
pkgdesc='Simple application that listens for WIFI-frames and records the mac-address of the sender and posts them to a REST-api.'
arch=('any')
url='https://github.com/cthit/hubbIT-sniffer'
license=('GPL3')
depends=('python' 'python-requests')
makedepends=('git')
source=("$pkgname::git+https://github.com/cthit/hubbIT-sniffer.git")
sha512sums=('SKIP')

pkgver() {
  cd $pkgname

  echo $(git rev-list --count HEAD).$(git rev-parse --short HEAD)
}

package() {
  cd $pkgname

  install -dm 755 "$pkgdir/usr/bin"
  install -dm 755 "$pkgdir/usr/share/$pkgname"

  install -Dm 644 -t "$pkgdir/usr/share/doc/$pkgname/" README.md
  install -Dm 644 COPYING "$pkgdir/usr/share/licenses/$pkgname/COPYING"

  rm README.md COPYING

  cp -a * "$pkgdir/usr/share/$pkgname/"

  cat > "$pkgdir/usr/bin/$pkgname" << EOF
#!/bin/sh
cd /usr/share/$pkgname/sniffer
exec python sniffer.py "\$@"
EOF

  chmod +x "$pkgdir/usr/bin/$pkgname"
}

