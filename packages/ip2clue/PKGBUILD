# This file is part of BlackArch Linux ( http://blackarch.org ).
# See COPYING for license details.

pkgname=ip2clue
pkgver=0.0.94
pkgrel=1
pkgdesc='A small memory/CPU footprint daemon to lookup country (and other info) based on IP (v4 and v6).'
groups=('blackarch' 'blackarch-recon')
url='http://kernel.embedromix.ro/us/'
license=('GPL3')
arch=('x86_64' 'armv6h' 'armv7h' 'aarch64')
depends=('bash' 'conn')
options=('!emptydirs')
source=("http://kernel.embedromix.ro/us/ip2clue/ip2clue-${pkgver}.tar.gz"
        'ip2clued.service')
sha512sums=('d33f9fab32d7539ccf180c5be9320c03ced76db1aac76899f0f29fe78655ca225699f9da78c30b0b678261b95c2a3bedfd72a621aaea8428699ba6103b48c766'
            '90e43e1a1482c2ab2234a63156a03bc728132abeccb7330d6326dbd93140b8a9bd109e92006c6fb9f9d4de553b4663452173953ae334ca06bd7864a75e4dc947')

build() {
  cd "ip2clue-$pkgver"

  ./configure --prefix=/usr

  make
}

package() {
  cd "ip2clue-$pkgver"

  make DESTDIR="$pkgdir" install

  mv "$pkgdir/usr/sbin/"* "$pkgdir/usr/bin"

  rm -rf "$pkgdir/etc/rc.d"

  install -Dm644 "$srcdir/ip2clued.service" \
    "$pkgdir/usr/lib/systemd/system/ip2clued.service"
  install -Dm644 -t "$pkgdir/usr/share/doc/ip2clue" Changelog README TODO
  install -Dm644 LICENSE "$pkgdir/usr/share/licenses/ip2clue/LICENSE"
}
