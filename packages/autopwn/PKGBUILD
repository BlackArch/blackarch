# This file is part of BlackArch Linux ( http://blackarch.org ).
# See COPYING for license details.

pkgname='autopwn'
pkgver=177.2f3f605
pkgrel=4
groups=('blackarch' 'blackarch-automation')
pkgdesc='Specify targets and run sets of tools against them.'
arch=('any')
url='https://github.com/nccgroup/autopwn'
license=('GAGPL')
depends=('python3' 'python-readchar' 'python-inquirer' 'python-screenutils'
         'python-yaml' 'python-blessings' 'nmap' 'skipfish' 'patator'
         'hydra' 'cmsmap' 'lynis' 'openssh' 'arachni' 'sslscan' 'metasploit'
         'nbtscan' 'enum4linux' 'nikto' 'sslyze' 'dirb' 'linenum' 'httrack'
         'testssl' 'linux-exploit-suggester' 'nfs-utils')
makedepends=('git' 'python-setuptools')
source=('git+https://github.com/nccgroup/autopwn.git')
sha1sums=('SKIP')

pkgver() {
  cd "$srcdir/autopwn"

  echo $(git rev-list --count HEAD).$(git rev-parse --short HEAD)
}

package() {
  cd "$srcdir/autopwn"

  mkdir -p "$pkgdir/usr/bin"
  mkdir -p "$pkgdir/usr/share/autopwn"

  python3 setup.py install --root="$pkgdir" --optimize=1

  install -Dm644 -t "$pkgdir/usr/share/doc/autopwn/" README.md docs/*
  install -Dm644 LICENSE.md "$pkgdir/usr/share/licenses/autopwn/LICENSE.md"
}
