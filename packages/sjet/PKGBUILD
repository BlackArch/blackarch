# This file is part of BlackArch Linux ( http://blackarch.org ).
# See COPYING for license details.

pkgname=sjet
pkgver=27.2d52f0c
pkgrel=1
pkgdesc='Siberas JMX exploitation toolkit.'
groups=('blackarch' 'blackarch-exploitation' 'blackarch-webapp')
arch=('any')
url='https://github.com/h0ng10/sjet'
license=('MIT')
depends=('python2' 'python2-argparse' 'jython')
makedepends=('git')
source=("git+https://github.com/h0ng10/$pkgname.git")
sha1sums=('SKIP')

pkgver() {
  cd $pkgname

  echo $(git rev-list --count HEAD).$(git rev-parse --short HEAD)
}

package() {
  cd $pkgname

  install -dm 755 "$pkgdir/usr/bin"
  install -dm 755 "$pkgdir/usr/share/$pkgname"

  install -Dm 644 -t "$pkgdir/usr/share/doc/$pkgname/" README.md
  install -Dm 644 LICENSE "$pkgdir/usr/share/licenses/$pkgname/LICENSE"

  rm README.md LICENSE

  cp -a * "$pkgdir/usr/share/$pkgname/"

  cat > "$pkgdir/usr/bin/$pkgname" << EOF
#!/bin/sh
cd /usr/share/$pkgname
exec /opt/jython/bin/jython $pkgname.py "\$@"
EOF

  chmod a+x "$pkgdir/usr/bin/$pkgname"
}

