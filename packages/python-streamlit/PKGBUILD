# This file is part of BlackArch Linux ( https://www.blackarch.org/ ).
# See COPYING for license details.

pkgname=python-streamlit
_pkgname=streamlit
pkgver=1.35.0
pkgrel=1
pkgdesc='A faster way to build and share data apps.'
arch=('any')
url='https://pypi.org/project/streamlit/#files'
license=('MIT')
depends=('python' 'python-altair' 'python-blinker' 'python-cachetools'
         'python-click' 'python-numpy' 'python-packaging' 'python-pandas'
         'python-pillow' 'python-protobuf' 'python-pyarrow' 'python-requests'
         'python-rich' 'python-tenacity' 'python-toml'
         'python-typing_extensions' 'python-watchdog')
makedepends=('python-build' 'python-pip' 'python-wheel')
source=("https://files.pythonhosted.org/packages/source/${_pkgname::1}/$_pkgname/$_pkgname-$pkgver.tar.gz")
sha512sums=('859daba233dea1178ecff5612a43699170d27759f8f1a0e322a68c4fadbf7682048d3d99946e74835e2725a7ea00f41d1a194cf3fd452d6e37b732fcdcd89a0e')

build() {
  cd "$_pkgname-$pkgver"

  python -m build --wheel --outdir="$startdir/dist"
}

package() {
  cd "$_pkgname-$pkgver"

  pip install \
    --verbose \
    --disable-pip-version-check \
    --no-warn-script-location \
    --ignore-installed \
    --no-compile \
    --no-deps \
    --root="$pkgdir" \
    --prefix=/usr \
    --no-index \
    --find-links="file://$startdir/dist" \
    $_pkgname
}

