# This file is part of BlackArch Linux ( http://blackarch.org ).
# See COPYING for license details.

pkgname='whitewidow'
pkgver=605.4f27bfe
pkgrel=2
pkgdesc='SQL Vulnerability Scanner.'
groups=('blackarch' 'blackarch-scanner' 'blackarch-webapp')
arch=('any')
url='https://github.com/Ekultek/whitewidow'
license=('GPL3' 'custom:unknown')
depends=('ruby' 'ruby-bundler')
makedepends=('git')
source=('git+https://github.com/Ekultek/whitewidow.git')
install='whitewidow.install'
sha1sums=('SKIP')

pkgver() {
  cd "$srcdir/whitewidow"

  echo $(git rev-list --count HEAD).$(git rev-parse --short HEAD)
}

prepare() {
  cd "$srcdir/whitewidow"

  echo -e "\ngem 'test-unit'" >> Gemfile
}

package() {
  cd "$srcdir/whitewidow"

  mkdir -p "$pkgdir/usr/bin"
  mkdir -p "$pkgdir/usr/share/whitewidow"

  install -Dm644 -t "$pkgdir/usr/share/licenses/whitewidow/" docs/legal/*
  install -Dm644 -t "$pkgdir/usr/share/doc/whitewidow/" README.md \
    docs/CHANGELOG.md docs/{misc/*,self_help/*}

  cp -a * "$pkgdir/usr/share/whitewidow/"

  cat > "$pkgdir/usr/bin/whitewidow" << EOF
#!/bin/sh
cd /usr/share/whitewidow
exec ruby whitewidow.rb "\${@}"
EOF

  chmod a+x "$pkgdir/usr/bin/whitewidow"
}
