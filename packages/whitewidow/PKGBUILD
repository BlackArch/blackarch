# This file is part of BlackArch Linux ( https://www.blackarch.org/ ).
# See COPYING for license details.

pkgname=whitewidow
pkgver=605.4f27bfe
pkgrel=5
pkgdesc='SQL Vulnerability Scanner.'
groups=('blackarch' 'blackarch-scanner' 'blackarch-webapp')
arch=('any')
url='https://github.com/Ekultek/whitewidow'
license=('GPL3' 'custom:unknown')
depends=('ruby' 'ruby-bundler')
makedepends=('git')
source=('git+https://github.com/Ekultek/whitewidow.git')
install="$pkgname.install"
sha512sums=('SKIP')

pkgver() {
  cd $pkgname

  echo $(git rev-list --count HEAD).$(git rev-parse --short HEAD)
}

prepare() {
  cd $pkgname

  echo -e "\ngem 'test-unit'" >> Gemfile
}

package() {
  cd $pkgname

  install -dm 755 "$pkgdir/usr/bin"
  install -dm 755 "$pkgdir/usr/share/$pkgname"

  install -Dm 644 -t "$pkgdir/usr/share/doc/$pkgname/" README.md \
    docs/CHANGELOG.md docs/{misc/*,self_help/*}
  install -Dm 644 -t "$pkgdir/usr/share/licenses/$pkgname/" docs/legal/*

  cp -a * "$pkgdir/usr/share/$pkgname/"

  cat > "$pkgdir/usr/bin/$pkgname" << EOF
#!/bin/sh
cd /usr/share/$pkgname
exec ruby $pkgname.rb "\$@"
EOF

  chmod +x "$pkgdir/usr/bin/$pkgname"
}

