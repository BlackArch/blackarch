# This file is part of BlackArch Linux ( https://www.blackarch.org/ ).
# See COPYING for license details.

pkgname=raccoon
pkgver=185.533c773
pkgrel=1
pkgdesc='A high performance offensive security tool for reconnaissance and vulnerability scanning.'
groups=('blackarch' 'blackarch-recon' 'blackarch-scanner')
arch=('any')
url='https://github.com/evyatarmeged/Raccoon'
license=('MIT')
depends=('python' 'python-xmltodict' 'python-dnspython' 'python-requests'
         'python-lxml' 'python-beautifulsoup4' 'python-click' 'python-pysocks'
         'python-fake-useragent')
makedepends=('git' 'python-setuptools')
source=("$pkgname::git+https://github.com/evyatarmeged/Raccoon.git")
sha512sums=('SKIP')

pkgver() {
  cd $pkgname

  echo $(git rev-list --count HEAD).$(git rev-parse --short HEAD)
}

build() {
  cd $pkgname

  python setup.py build
}

package() {
  cd $pkgname

  python setup.py install --root="$pkgdir" --prefix=/usr -O1 --skip-build

  rm -rf "$pkgdir/usr/lib/python3.9/site-packages/tests"
}

