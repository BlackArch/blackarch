# This file is part of BlackArch Linux ( https://www.blackarch.org/ ).
# See COPYING for license details.

pkgname=raccoon
pkgver=183.985797f
pkgrel=1
pkgdesc='A high performance offensive security tool for reconnaissance and vulnerability scanning.'
arch=('any')
url='https://github.com/evyatarmeged/Raccoon'
license=('MIT')
depends=('python' 'python-xmltodict' 'python-dnspython'
         'python-requests' 'python-lxml' 'python-beautifulsoup4'
         'python-click' 'python-fake-useragent' 'python-pysocks')
makedepends=('git' 'python-setuptools')
source=('raccoon::git+https://github.com/evyatarmeged/Raccoon.git')
sha512sums=('SKIP')

pkgver() {
  cd $pkgname

  echo $(git rev-list --count HEAD).$(git rev-parse --short HEAD)
}

build() {
  cd $pkgname

  python setup.py build
}

package() {
  cd $pkgname

  python setup.py install --root="$pkgdir" --prefix=/usr --optimize=1 \
    --skip-build
}

