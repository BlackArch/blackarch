# This file is part of BlackArch Linux ( http://blackarch.org ).
# See COPYING for license details.

pkgname='pkt2flow'
pkgver='1.3'
pkgrel=1
groups=('blackarch' 'blackarch-networking')
pkgdesc='A simple utility to classify packets into flows.'
arch=('x86_64' 'armv6h' 'armv7h' 'aarch64')
url='https://github.com/caesar0301/pkt2flow'
license=('MIT')
depends=('libpcap')
makedepends=('scons')
source=("https://github.com/caesar0301/pkt2flow/archive/v${pkgver}.tar.gz")
sha1sums=('a448753dc14347e678da8272e19ea172714d80cf')

build() {
  cd "$srcdir/pkt2flow-$pkgver"

  scons -Q PREFIX=/usr
}

package() {
  cd "$srcdir/pkt2flow-$pkgver"

  install -Dm755 pkt2flow "$pkgdir/usr/bin/pkt2flow"
  install -Dm644 -t "$pkgdir/usr/share/doc/pkt2flow" README.md
  install -Dm644 LICENSE "$pkgdir/usr/share/licenses/pkt2flow/LICENSE"
}
