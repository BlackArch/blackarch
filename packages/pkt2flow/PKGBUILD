# This file is part of BlackArch Linux ( https://www.blackarch.org/ ).
# See COPYING for license details.

pkgname=pkt2flow
pkgver=68.6e74185
pkgrel=1
groups=('blackarch' 'blackarch-networking')
pkgdesc='A simple utility to classify packets into flows.'
arch=('x86_64' 'armv6h' 'armv7h' 'aarch64')
url='https://github.com/caesar0301/pkt2flow'
license=('MIT')
depends=('libpcap')
makedepends=('scons')
source=('git+https://github.com/caesar0301/pkt2flow.git')
sha512sums=('SKIP')

pkgver() {
  cd $pkgname

  echo $(git rev-list --count HEAD).$(git rev-parse --short HEAD)
}

build() {
  cd "$pkgname"

  scons -Q PREFIX=/usr
}

package() {
  cd "$pkgname"

  install -Dm 755 pkt2flow "$pkgdir/usr/bin/$pkgname"
  install -Dm 644 -t "$pkgdir/usr/share/doc/$pkgname" README.md
  install -Dm 644 LICENSE "$pkgdir/usr/share/licenses/$pkgname/LICENSE"
}

