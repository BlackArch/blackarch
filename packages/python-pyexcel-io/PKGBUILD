# This file is part of BlackArch Linux ( https://www.blackarch.org/ ).
# See COPYING for license details.

pkgname=python-pyexcel-io
_pkgname=pyexcel-io
pkgver=0.5.20
pkgrel=1
pkgdesc='A python library to read and write structured data in csv, zipped csvformat and to/from databases.'
arch=('any')
url='https://pypi.org/project/pyexcel-io/#files'
license=('BSD')
depends=('python' 'python-lml')
makedepends=('python-setuptools')
source=("https://files.pythonhosted.org/packages/65/be/2a3956415d52c5302c6a6aacc08d17f21b653bcdb1f93a6973a393a09c2d/$_pkgname-$pkgver.tar.gz")
sha512sums=('ea2f8624b493325b0e94ed768ea4e18ff1bffb5708c41a1a044308a28712a6ab3fa26cd6e24030065ffc8941be79b6a4e4fa2cc1eb91a26b4e017e22a5af4a7f')

prepare() {
  cd "$_pkgname-$pkgver"

  sed -i 's|lml==0.0.1|lml|g' setup.py
}

build() {
  cd "$_pkgname-$pkgver"

  python setup.py build
}

package() {
  cd "$_pkgname-$pkgver"

  python setup.py install --root="$pkgdir" --prefix=/usr -O1 --skip-build
}

