# This file is part of BlackArch Linux ( https://www.blackarch.org/ ).
# See COPYING for license details.

pkgname=python-altair
_pkgname=altair
pkgver=5.3.0
pkgrel=1
pkgdesc='A declarative statistical visualization library for Python.'
arch=('any')
url='https://pypi.org/project/altair/#files'
license=('BSD')
depends=('python' 'python-typing_extensions' 'python-numpy' 'python-jinja'
         'python-pandas' 'python-jsonschema' 'python-packaging' 'python-toolz')
makedepends=('python-build' 'python-pip')
source=("https://files.pythonhosted.org/packages/source/${_pkgname::1}/$_pkgname/$_pkgname-$pkgver.tar.gz")
sha512sums=('1ee6e4c801db3123163449239f4163161bef878efdf87504aec9493676e57d54caee0756371a1904db746964c1d9213e2134fa49bde52b14ad7aae331177f78f')

build() {
  cd "$_pkgname-$pkgver"

  python -m build --wheel --outdir="$startdir/dist"
}

package() {
  cd "$_pkgname-$pkgver"

  pip install \
    --verbose \
    --disable-pip-version-check \
    --no-warn-script-location \
    --ignore-installed \
    --no-compile \
    --no-deps \
    --root="$pkgdir" \
    --prefix=/usr \
    --no-index \
    --find-links="file://$startdir/dist" \
    $_pkgname
}

