# This file is part of BlackArch Linux ( https://www.blackarch.org/ ).
# See COPYING for license details.

pkgname=python-altair
_pkgname=altair
pkgver=5.4.1
pkgrel=1
pkgdesc='A declarative statistical visualization library for Python.'
arch=('any')
url='https://pypi.org/project/altair/#files'
license=('BSD')
depends=('python' 'python-typing_extensions' 'python-numpy' 'python-jinja'
         'python-pandas' 'python-jsonschema' 'python-packaging' 'python-toolz')
makedepends=('python-build' 'python-pip')
source=("https://files.pythonhosted.org/packages/source/${_pkgname::1}/$_pkgname/$_pkgname-$pkgver.tar.gz")
sha512sums=('7e2711ba073688ad462c52ec64b335bc848e01831258ffd7606de5ec27978f611e0e34aba23c91a8a69e11c559b0604cc6e8437a15490d546ff202619ac625cf')

build() {
  cd "$_pkgname-$pkgver"

  python -m build --wheel --outdir="$startdir/dist"
}

package() {
  cd "$_pkgname-$pkgver"

  pip install \
    --verbose \
    --disable-pip-version-check \
    --no-warn-script-location \
    --ignore-installed \
    --no-compile \
    --no-deps \
    --root="$pkgdir" \
    --prefix=/usr \
    --no-index \
    --find-links="file://$startdir/dist" \
    $_pkgname
}

