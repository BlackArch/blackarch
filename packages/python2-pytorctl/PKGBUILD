# This file is part of BlackArch Linux ( http://blackarch.org ).
# See COPYING for license details.

pkgname=python2-pytorctl
_pkgname=pytorctl
pkgver=2013.10.16.4.gc8fcb25
pkgrel=3
pkgdesc="A Python Tor controller with extensions to support path building and various constraints on node and path selection, as well as statistics gathering."
arch=('any')
license=('custom')
url='http://fscked.org/projects/torctl'
depends=('python2' 'python2-geoip')
makedepends=('git')
optdepends=('python2-elixir: SQL Support')
conflicts=('pytorctl')
provides=('pytorctl')
source=("git+http://git.torproject.org/${_pkgname}.git"
        'setup.py')
sha512sums=('SKIP'
            'bfa1696c63935fa49d8f5aad9d8d2735e882cddb9565e2d9f59d063aab55bbf2112df46a99aaefd9c9a22432fbfff4b9f2376f5394d348c6b18015a53126f10e')

pkgver() {
  cd ${srcdir}/${_pkgname}
  git describe --always | sed 's|-|.|g'
}

package() {
  install -dm 755 ${pkgdir}/usr/share/{licenses,doc}/${_pkgname}
  install -Dm644 ${srcdir}/${_pkgname}/LICENSE ${pkgdir}/usr/share/licenses/${_pkgname}/LICENSE
  install -Dm644 ${srcdir}/${_pkgname}/README ${pkgdir}/usr/share/doc/${_pkgname}

  python2 setup.py install --root=${pkgdir} --optimize=1
}
