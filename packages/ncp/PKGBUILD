# This file is part of BlackArch Linux ( http://blackarch.org ).
# See COPYING for license details.

pkgname='ncp'
pkgver='1.2.4'
pkgrel=4
pkgdesc='A fast file copy tool for LANs.'
arch=('x86_64' 'armv6h' 'armv7h' 'aarch64')
url='http://www.fefe.de/ncp/'
license=('GPL')
depends=('libowfat')
source=("http://dl.fefe.de/ncp-$pkgver.tar.bz2"
        "ncp-${pkgver}_version-string.patch")
sha1sums=('bd33e5311c249948559d17bfd59df93ae72e1f17'
          '2ce3495b3d82f3954a8ca464b295ccf484721b09')

prepare() {
  cd "$srcdir/ncp-$pkgver"

  # Update the internal version string.
  patch -Np1 -i ../ncp-${pkgver}_version-string.patch
}

build() {
  cd "$srcdir/ncp-$pkgver"

  make
}

package() {
  cd "$srcdir/ncp-$pkgver"

  make "PREFIX=$pkgdir/usr" install
}
