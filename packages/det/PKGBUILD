# This file is part of BlackArch Linux ( http://blackarch.org ).
# See COPYING for license details.

pkgname='det'
pkgver=31.417cbce
pkgrel=1
pkgdesc='(extensible) Data Exfiltration Toolkit.'
groups=('blackarch' 'blackarch-networking' 'blackarch-misc')
arch=('any')
url='https://github.com/sensepost/det'
license=('custom:unknown')
depends=('python2' 'python2-tweepy' 'python2-pysocks' 'python2-dnslib'
         'python2-crypto' 'scapy')
makedepends=('git')
source=('git+https://github.com/sensepost/det.git')
sha1sums=('SKIP')

pkgver() {
  cd "$srcdir/det"

  echo $(git rev-list --count HEAD).$(git rev-parse --short HEAD)
}

package() {
  cd "$srcdir/det"

  mkdir -p "$pkgdir/usr/bin"
  mkdir -p "$pkgdir/usr/share/det"

  install -Dm644 README.md "$pkgdir/usr/share/doc/det/README.md"

  rm README.md

  cp -a * "$pkgdir/usr/share/det"

  cat > "$pkgdir/usr/bin/det" << EOF
#!/bin/sh
cd /usr/share/det
exec python2 det.py "\${@}"
EOF

  chmod a+x "$pkgdir/usr/bin/det"
}
