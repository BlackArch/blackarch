# This file is part of BlackArch Linux ( https://www.blackarch.org/ ).
# See COPYING for license details.

pkgbase=python-polyswarm-artifact
pkgname=('python2-polyswarm-artifact' 'python-polyswarm-artifact')
_pkgname=polyswarm-artifact
pkgver=1.3.2
pkgrel=1
pkgdesc='Library containing artifact type enums and functions'
arch=('any')
url='https://pypi.org/project/polyswarm-artifact/#files'
license=('MIT')
makedepends=('python2-setuptools' 'python-setuptools')
source=("https://files.pythonhosted.org/packages/6b/1a/87c60ec8873f967380bcb24983699f46c51e46f9de2d942d0f64ec5c09af/$_pkgname-$pkgver.tar.gz")
sha512sums=('0f31ba35083603c4e1993fed190769bfd8c7e4e28162c70047e36cc7f1db4c6a51da684ced763b64d992477e86ecc9db31d997dfafb01bbdccdb473a52d1659d')

prepare() {
  cp -a "$_pkgname-$pkgver"{,-2}
}

build() {
  cd "$_pkgname-$pkgver-2"

  python2 setup.py build

  cd "$srcdir/$_pkgname-$pkgver"

  python setup.py build
}

package_python2-polyswarm-artifact() {
  depends=('python2' 'python2-jsonschema')

  cd "$_pkgname-$pkgver-2"

  python2 setup.py install --prefix=/usr --root="$pkgdir" --optimize=1 \
    --skip-build
}

package_python-polyswarm-artifact() {
  depends=('python' 'python-jsonschema')

  cd "$_pkgname-$pkgver"

  python setup.py install --prefix=/usr --root="$pkgdir" --optimize=1 \
    --skip-build
}

