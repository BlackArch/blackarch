# This file is part of BlackArch Linux ( https://www.blackarch.org/ ).
# See COPYING for license details.

pkgbase=python-polyswarm-artifact
pkgname=('python2-polyswarm-artifact' 'python-polyswarm-artifact')
_pkgname=polyswarm-artifact
pkgver=1.4.1
pkgrel=2
pkgdesc='Library containing artifact type enums and functions'
arch=('any')
url='https://pypi.org/project/polyswarm-artifact/#files'
license=('MIT')
makedepends=('python2-setuptools' 'python-setuptools')
source=("https://files.pythonhosted.org/packages/08/a6/a1064a3461ef2582a37f6a53e416aef40559ad5347e2bd7efb15acd0af44/$_pkgname-$pkgver.tar.gz")
sha512sums=('983d303f36e0c50a05c4056ae3a43794d69c90419b95be1af5be8eaf7a9177a478b0f2764031e6832a0004347ad4645395b45bf5d03636a49338a047b215f847')

prepare() {
  cp -a "$_pkgname-$pkgver"{,-2}
}

build() {
  cd "$_pkgname-$pkgver-2"

  python2 setup.py build

  cd "$srcdir/$_pkgname-$pkgver"

  python setup.py build
}

package_python2-polyswarm-artifact() {
  depends=('python2' 'python2-jsonschema')

  cd "$_pkgname-$pkgver-2"

  python2 setup.py install --prefix=/usr --root="$pkgdir" --optimize=1 \
    --skip-build
}

package_python-polyswarm-artifact() {
  depends=('python' 'python-jsonschema')

  cd "$_pkgname-$pkgver"

  python setup.py install --prefix=/usr --root="$pkgdir" --optimize=1 \
    --skip-build
}

