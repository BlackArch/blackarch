# This file is part of BlackArch Linux ( https://www.blackarch.org/ ).
# See COPYING for license details.

pkgbase=python-polyswarm-artifact
pkgname=('python2-polyswarm-artifact' 'python-polyswarm-artifact')
_pkgname=polyswarm-artifact
pkgver=1.4.2
pkgrel=1
pkgdesc='Library containing artifact type enums and functions'
arch=('any')
url='https://pypi.org/project/polyswarm-artifact/#files'
license=('MIT')
makedepends=('python2-setuptools' 'python-setuptools')
source=("https://files.pythonhosted.org/packages/ae/72/6f9a85db28672c15503a6b3abc4144beae7ee90bd264593c7600aed85679/$_pkgname-$pkgver.tar.gz")
sha512sums=('455eb087883615d522ee2deeecfbd679f8856f3776854670a7c4a13469d935b4c8bfd0dc14b7333a77277f430fa17b8de3c63529459a74eb830aa1fe4d7bd558')

prepare() {
  cp -a "$_pkgname-$pkgver"{,-2}
}

build() {
  cd "$_pkgname-$pkgver-2"

  python2 setup.py build

  cd "$srcdir/$_pkgname-$pkgver"

  python setup.py build
}

package_python2-polyswarm-artifact() {
  depends=('python2' 'python2-jsonschema')

  cd "$_pkgname-$pkgver-2"

  python2 setup.py install --prefix=/usr --root="$pkgdir" --optimize=1 \
    --skip-build
}

package_python-polyswarm-artifact() {
  depends=('python' 'python-jsonschema')

  cd "$_pkgname-$pkgver"

  python setup.py install --prefix=/usr --root="$pkgdir" --optimize=1 \
    --skip-build
}

