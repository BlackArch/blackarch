# This file is part of BlackArch Linux ( https://www.blackarch.org/ ).
# See COPYING for license details.

pkgbase=python-polyswarm-artifact
pkgname=('python2-polyswarm-artifact' 'python-polyswarm-artifact')
_pkgname=polyswarm-artifact
pkgver=1.3.0
pkgrel=1
pkgdesc='Library containing artifact type enums and functions'
arch=('any')
url='https://pypi.org/project/polyswarm-artifact/#files'
license=('MIT')
makedepends=('python2-setuptools' 'python-setuptools')
source=("https://files.pythonhosted.org/packages/64/5e/23ba893d469ba60d0a8a940b29d2e65c5fdea194280f69341944330547b5/$_pkgname-$pkgver.tar.gz")
sha512sums=('d72b894ec5364edc7e602948ae511bc61d9c1e2dae819e06722282c61b46205fee2a47d3db1bdd614bcb45fd0974217602e807c577e74bf266fc38d1df5ce054')

prepare() {
  cp -a "$_pkgname-$pkgver"{,-2}
}

build() {
  cd "$_pkgname-$pkgver-2"

  python2 setup.py build

  cd "$srcdir/$_pkgname-$pkgver"

  python setup.py build
}

package_python2-polyswarm-artifact() {
  depends=('python2' 'python2-jsonschema')

  cd "$_pkgname-$pkgver-2"

  python2 setup.py install --prefix=/usr --root="$pkgdir" --optimize=1 \
    --skip-build
}

package_python-polyswarm-artifact() {
  depends=('python' 'python-jsonschema')

  cd "$_pkgname-$pkgver"

  python setup.py install --prefix=/usr --root="$pkgdir" --optimize=1 \
    --skip-build
}

