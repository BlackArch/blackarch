# This file is part of BlackArch Linux ( http://blackarch.org ).
# See COPYING for license details.

pkgname='indxparse'
pkgver=169.1b32736
pkgrel=1
groups=('blackarch' 'blackarch-forensic')
pkgdesc='A Tool suite for inspecting NTFS artifacts.'
arch=('any')
url='http://www.williballenthin.com/forensics/mft/indxparse/'
license=('APACHE')
depends=('python2' 'python2-progressbar')
optdepends=('wxpython' 'python2-fusepy')
makedepends=('git')
source=('git+https://github.com/williballenthin/INDXParse.git')
sha1sums=('SKIP')

pkgver() {
  cd "$srcdir/INDXParse"

  echo $(git rev-list --count HEAD).$(git rev-parse --short HEAD)
}

package() {
  cd "$srcdir/INDXParse"

  python2 setup.py install --root="${pkgdir}/" --optimize=1
}
