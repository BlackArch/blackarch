# This file is part of BlackArch Linux ( https://www.blackarch.org/ ).
# See COPYING for license details.

pkgname=plumber
pkgver=18.3f1be68
pkgrel=3
pkgdesc='A python implementation of a grep friendly ftrace wrapper.'
groups=('blackarch' 'blackarch-misc')
arch=('any')
url='https://github.com/cybereason/linux_plumber'
license=('AGPL3')
depends=('python' 'python-termcolor')
makedepends=('git')
source=("$pkgname::git+https://github.com/cybereason/linux_plumber.git")
sha512sums=('SKIP')

pkgver() {
  cd $pkgname

  echo $(git rev-list --count HEAD).$(git rev-parse --short HEAD)
}

prepare() {
  cd $pkgname

  sed -i '1i\#!/usr/bin/python' "$pkgname.py"
}

package() {
  cd $pkgname

  install -Dm 755 "$pkgname.py" "$pkgdir/usr/bin/$pkgname"
  install -Dm 644 -t "$pkgdir/usr/share/doc/$pkgname/" README.md
  install -Dm 644 LICENSE.md "$pkgdir/usr/share/licenses/$pkgname/LICENSE"
}

