# This file is part of BlackArch Linux ( http://blackarch.org ).
# See COPYING for license details.

pkgname='plumber'
pkgver=12.842aa48
pkgrel=1
pkgdesc='A python implementation of a grep friendly ftrace wrapper.'
groups=('blackarch' 'blackarch-misc')
arch=('any')
url='https://github.com/cybereason/linux_plumber'
license=('GAGPL3')
depends=('python2' 'python2-termcolor')
makedepends=('git')
source=('git+https://github.com/cybereason/linux_plumber.git')
sha1sums=('SKIP')

pkgver() {
  cd "$srcdir/linux_plumber"

  echo $(git rev-list --count HEAD).$(git rev-parse --short HEAD)
}

prepare() {
  cd "$srcdir/linux_plumber"

  sed -i '1i\#!/usr/bin/python2' plumber.py
}

package() {
  cd "$srcdir/linux_plumber"

  install -Dm755 plumber.py "$pkgdir/usr/bin/plumber"
  install -Dm644 -t "$pkgdir/usr/share/doc/plumber/" README.md
  install -Dm644 LICENSE.md "$pkgdir/usr/share/licenses/plumber/LICENSE.md"
}
