# This file is part of BlackArch Linux ( https://www.blackarch.org/ ).
# See COPYING for license details.

pkgname=plumber
pkgver=12.842aa48
pkgrel=1
pkgdesc='A python implementation of a grep friendly ftrace wrapper.'
groups=('blackarch' 'blackarch-misc')
arch=('any')
url='https://github.com/cybereason/linux_plumber'
license=('GAGPL3')
depends=('python2' 'python2-termcolor')
makedepends=('git')
source=('git+https://github.com/cybereason/linux_plumber.git')
sha512sums=('SKIP')

pkgver() {
  cd linux_plumber

  echo $(git rev-list --count HEAD).$(git rev-parse --short HEAD)
}

prepare() {
  cd linux_plumber

  sed -i '1i\#!/usr/bin/python2' plumber.py
}

package() {
  cd linux_plumber

  install -Dm 755 plumber.py "$pkgdir/usr/bin/$pkgname"
  install -Dm 644 -t "$pkgdir/usr/share/doc/$pkgname/" README.md
  install -Dm 644 LICENSE.md "$pkgdir/usr/share/licenses/$pkgname/LICENSE.md"
}

