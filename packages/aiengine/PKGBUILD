# This file is part of BlackArch Linux ( https://www.blackarch.org/ ).
# See COPYING for license details.

pkgname=aiengine
pkgver=2.2.0
pkgrel=1
epoch=1
groups=('blackarch' 'blackarch-networking' 'blackarch-defensive')
pkgdesc='A packet inspection engine with capabilities of learning without any human intervention.'
arch=('x86_64' 'aarch64')
url='https://bitbucket.org/camp0/aiengine/downloads/'
license=('GPL3')
depends=('boost' 'boost-libs' 'python' 'libpcap')
makedepends=('make')
source=("https://bitbucket.org/camp0/aiengine/downloads/$pkgname-$pkgver.tar.gz")
sha512sums=('ba29a2097c525e161f013286ad9c4f0772a1ad4cb074967f332aea5e43053a2ea3e499bf5e956effa41a423f5ba8b95a06909669ed5c35cb7a577c44890fffbe')

build() {
  cd "$pkgname-$pkgver"

  ./autogen.sh

  export PYTHON_VERSION=3.10

  ./configure --prefix=/usr --enable-tcpqos --enable-reject --enable-pythongil

  make
}

package() {
  cd "$pkgname-$pkgver"

  make DESTDIR="$pkgdir" install
}

