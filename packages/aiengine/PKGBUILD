# This file is part of BlackArch Linux ( http://blackarch.org ).
# See COPYING for license details.

pkgname='aiengine'
pkgver=730.d36dd564
pkgrel=1
groups=('blackarch' 'blackarch-networking' 'blackarch-defensive')
pkgdesc='A packet inspection engine with capabilities of learning without any human intervention.'
arch=('x86_64' 'armv6h' 'armv7h' 'aarch64')
url='https://bitbucket.org/camp0/aiengine/'
license=('GPL3')
depends=('boost' 'boost-libs' 'python2' 'libpcap')
makedepends=('git')
source=('git+https://bitbucket.com/camp0/aiengine')
sha1sums=('SKIP')

pkgver() {
  cd "$srcdir/aiengine"

  echo $(git rev-list --count HEAD).$(git rev-parse --short HEAD)
}

build() {
  cd "$srcdir/aiengine"

  ./autogen.sh

  ./configure --prefix=/usr

  make
}

package() {
  cd "$srcdir/aiengine"

  make DESTDIR="$pkgdir" install
}
