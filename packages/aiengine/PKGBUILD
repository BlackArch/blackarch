# This file is part of BlackArch Linux ( https://www.blackarch.org/ ).
# See COPYING for license details.

pkgname=aiengine
pkgver=2.0.1
pkgrel=1
epoch=1
groups=('blackarch' 'blackarch-networking' 'blackarch-defensive')
pkgdesc='A packet inspection engine with capabilities of learning without any human intervention.'
arch=('x86_64' 'aarch64')
url='https://bitbucket.org/camp0/aiengine/'
license=('GPL3')
depends=('boost' 'boost-libs' 'python' 'libpcap')
makedepends=('make')
source=("https://bitbucket.org/camp0/aiengine/downloads/$pkgname-$pkgver.tar.gz")
sha512sums=('3623e2cdffbeba9ef8a2f92e85f5c1312115f054e57df3b133e59d9502ada2db3f4897324dc25df20b3789a4d1113ae6e4d45ab6dd63e905de7084308f4c5ca8')

build() {
  cd "$pkgname-$pkgver"

  ./autogen.sh

  export PYTHON_VERSION=3.9

  ./configure --prefix=/usr --enable-tcpqos --enable-reject --enable-pythongil

  make
}

package() {
  cd "$pkgname-$pkgver"

  make DESTDIR="$pkgdir" install
}

