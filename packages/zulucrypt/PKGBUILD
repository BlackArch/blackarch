# This file is part of BlackArch Linux ( https://www.blackarch.org/ ).
# See COPYING for license details.

pkgname=zulucrypt
_pkgname=zuluCrypt
pkgver=7.0.0
pkgrel=1
pkgdesc='Front end to cryptsetup and tcplay and it allows easy management of encrypted block devices.'
groups=('blackarch' 'blackarch-crypto')
arch=('x86_64')
url="https://mhogomchungu.github.io/${_pkgname}"
license=('GPL')
depends=('cryptsetup' 'qt5-base' 'libpwquality' 'libsecret' 'libxkbcommon-x11' 'util-linux')
optdepends=('kwalletmanager: retrieve volume keys from kde kwallet')
conflicts=('zulucrypt-git')
makedepends=('cmake')
source=("https://github.com/mhogomchungu/zuluCrypt/releases/download/${pkgver}/${_pkgname}-${pkgver}.tar.xz")
md5sums=('457f6a06e166a29f6b8f806508d3aed0')
sha256sums=('55cedd886f32d4c660d8b076e5a8f285bad7f585eec30a5e681ff31645065df6')

build() {
  cd "${srcdir}/${_pkgname}-${pkgver}"
  mkdir -p build
  cd build
  cmake -DCMAKE_INSTALL_PREFIX=/usr/ -DLIB_SUFFIX=lib -DNOGUI=false -DQT5=true -DHOMEMOUNTPREFIX=false -DCMAKE_BUILD_TYPE=release -DWITH_UUID=e2fs . ..
  make
}

package() {
  echo "changelog updated"
  cp "${srcdir}/${_pkgname}-${pkgver}"/changelog ../${pkgname}.changelog
  cd "${srcdir}/${_pkgname}-${pkgver}/build"
  make DESTDIR="$pkgdir" install
  mkdir -p ${pkgdir}/etc/modules-load.d
  echo 'loop' > "${pkgdir}/etc/modules-load.d/${pkgname}.conf"
}
