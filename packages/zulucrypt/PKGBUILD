# This file is part of BlackArch Linux ( https://www.blackarch.org/ ).
# See COPYING for license details.

pkgname=zulucrypt
_name=zuluCrypt
pkgver=2852.a54ff817
pkgrel=1
pkgdesc='Front end to cryptsetup and tcplay and it allows easy management of encrypted block devices.'
groups=('blackarch' 'blackarch-crypto')
arch=('x86_64')
url='https://github.com/mhogomchungu/zuluCrypt'
license=('GPL')
depends=('cryptsetup' 'libpwquality' 'libsecret' 'libxkbcommon-x11' 'qt5-base')
makedepends=('cmake')
source=('git+https://github.com/mhogomchungu/zuluCrypt.git')
sha512sums=('SKIP')

pkgver() {
  cd $pkgname

  echo $(git rev-list --count HEAD).$(git rev-parse --short HEAD)
}

build() {
  cd "$_name"

  mkdir -p build
  cd build

  cmake -DCMAKE_INSTALL_PREFIX=/usr/ -DLIB_SUFFIX=lib -DNOGUI=false -DQT5=true \
    -DHOMEMOUNTPREFIX=false -DCMAKE_BUILD_TYPE=release . ..

  make
}

package() {
  cd "$_name"

  install -Dm 644 -t "$pkgdir/usr/share/doc/$pkgname/" changelog README.md
  install -Dm 644 LICENSE "$pkgdir/usr/share/licenses/$pkgname/LICENSE"

  cd build
  make DESTDIR="$pkgdir" install
}

