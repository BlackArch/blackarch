# This file is part of BlackArch Linux ( https://www.blackarch.org/ ).
# See COPYING for license details.

pkgname=zulucrypt
_pkgname=zuluCrypt
pkgver=7.0.0
pkgrel=1
pkgdesc='Front end to cryptsetup and tcplay and it allows easy management of encrypted block devices.'
groups=('blackarch' 'blackarch-crypto')
arch=('x86_64' 'aarch64')
url='https://mhogomchungu.github.io/$_pkgname'
license=('GPL')
depends=('cryptsetup' 'qt5-base' 'libpwquality' 'libsecret' 'libxkbcommon-x11'
         'util-linux')
optdepends=('kwalletmanager: retrieve volume keys from kde kwallet')
conflicts=('zulucrypt-git')
makedepends=('cmake')
source=("https://github.com/mhogomchungu/zuluCrypt/releases/download/$pkgver/$_pkgname-$pkgver.tar.xz")
sha512sums=('f131f423da2796d760e4eaa50a7e56e0283bdf6d5e46ff32bd9bf0edc127a087e947e0f7d39fd99fd28cc4566c89cde9e1a85dd67e50e1cb7d6c9de31d7918bf')

build() {
  cd "$_pkgname-$pkgver"

  mkdir -p build
  cd build
  cmake -DCMAKE_INSTALL_PREFIX=/usr/ -DLIB_SUFFIX=lib -DNOGUI=false -DQT5=true \
    -DHOMEMOUNTPREFIX=false -DCMAKE_BUILD_TYPE=release -DWITH_UUID=e2fs . ..
  make
}

package() {
  cp "$_pkgname-$pkgver"/changelog ../${pkgname}.changelog

  cd "$_pkgname-$pkgver/build"
  make DESTDIR="$pkgdir" install

  mkdir -p "$pkgdir/etc/modules-load.d"
  echo 'loop' > "$pkgdir/etc/modules-load.d/$pkgname.conf"
}

