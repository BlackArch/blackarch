# This file is part of BlackArch Linux ( https://www.blackarch.org/ ).
# See COPYING for license details.

pkgname=zulucrypt
_pkgname=zuluCrypt
pkgver=6.2.0
pkgrel=1
pkgdesc='Front end to cryptsetup and tcplay and it allows easy management of encrypted block devices.'
groups=('blackarch' 'blackarch-crypto')
arch=('x86_64')
url='https://github.com/mhogomchungu/zuluCrypt'
license=('GPL')
depends=('cryptsetup' 'libpwquality' 'libsecret' 'libxkbcommon-x11' 'qt5-base')
makedepends=('cmake')
source=("https://github.com/mhogomchungu/zuluCrypt/releases/download/$pkgver/$_pkgname-$pkgver.tar.xz")
sha512sums=('8ec56a35b57834dcd8d97e3ef738ff98841490ee50bfa3e7baf78a0977e3a1f1fa76bd5dc780d45184cdb8356bade75ae11fff17b3b4aa897bebd3620da043f3')

build() {
  cd "$_pkgname-$pkgver"

  mkdir -p build
  cd build

  cmake -DCMAKE_INSTALL_PREFIX=/usr/ -DLIB_SUFFIX=lib -DNOGUI=false -DQT5=true \
    -DHOMEMOUNTPREFIX=false -DCMAKE_BUILD_TYPE=release . ..

  make
}

package() {
  cd "$_pkgname-$pkgver"

  install -Dm 644 -t "$pkgdir/usr/share/doc/$pkgname/" changelog README.md
  install -Dm 644 LICENSE "$pkgdir/usr/share/licenses/$pkgname/LICENSE"

  cd build
  make DESTDIR="$pkgdir" install
}

