# This file is part of BlackArch Linux ( https://www.blackarch.org/ ).
# See COPYING for license details.

pkgname=datajackproxy
_pkgname=DatajackProxy
pkgver=42.f75f3a3
pkgrel=2
pkgdesc='A proxy which allows you to intercept TLS traffic in native x86 applications across platform.'
groups=('blackarch' 'blackarch-proxy' 'blackarch-networking')
arch=('any')
url='https://github.com/nccgroup/DatajackProxy'
license=('MIT')
depends=('python' 'python-frida')
makedepends=('git')
source=("git+https://github.com/nccgroup/$_pkgname.git")
sha512sums=('SKIP')

pkgver() {
  cd $_pkgname

  echo $(git rev-list --count HEAD).$(git rev-parse --short HEAD)
}

package() {
  cd $_pkgname

  install -dm 755 "$pkgdir/usr/bin"
  install -dm 755 "$pkgdir/usr/share/$pkgname"

  install -Dm 644 -t "$pkgdir/usr/share/doc/$pkgname/" README.md
  install -Dm 644 LICENSE "$pkgdir/usr/share/licenses/$pkgname/LICENSE"

  rm README.md LICENSE

  cp -a * "$pkgdir/usr/share/$pkgname/"

  cat > "$pkgdir/usr/bin/$pkgname" << EOF
#!/bin/sh
cd /usr/share/$pkgname
exec python $_pkgname.py "\$@"
EOF

  chmod +x "$pkgdir/usr/bin/$pkgname"
}

