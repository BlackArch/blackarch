# This file is part of BlackArch Linux ( https://www.blackarch.org/ ).
# See COPYING for license details.

pkgbase=python-commandparse
pkgname=('python2-commandparse' 'python-commandparse')
_pkgname=commandparse
pkgver=1.0.9
pkgrel=1
pkgdesc='CLI application commands parser.'
arch=('any')
url='https://pypi.org/project/commandparse/#files'
license=('MIT')
makedepends=('python2-setuptools' 'python-setuptools')
source=("https://files.pythonhosted.org/packages/e8/4b/111a2b1f0e5935a33fb502fc1497b8a8acbf6076581a4d6fd638448584c6/$_pkgname-$pkgver.tar.gz")
sha512sums=('dc46edd9d65e409047625688a918a61f133678e291ce70e626d8f8514759f72229e87c5469088e6cc73af0c9a59ef284c53d21e718a958c06c3fd142ba848552')

prepare() {
  cp -a "$_pkgname-$pkgver"{,-2}
}

build() {
  cd "$_pkgname-$pkgver-2"

  python2 setup.py build

  cd "$srcdir/$_pkgname-$pkgver"

  python setup.py build
}

package_python2-commandparse() {
  depends=('python2')

  cd "$_pkgname-$pkgver-2"

  python2 setup.py install --prefix=/usr --root="$pkgdir" -O1 --skip-build

  chmod 644 "$pkgdir"/usr/lib/python2.7/site-packages/commandparse-1.0.9-py2.7.egg-info/*
}

package_python-commandparse() {
  depends=('python')

  cd "$_pkgname-$pkgver"

  python setup.py install --prefix=/usr --root="$pkgdir" -O1 --skip-build

  chmod 644 \
    "$pkgdir"/usr/lib/python3.9/site-packages/commandparse-1.0.9-py3.9.egg-info/*
}

