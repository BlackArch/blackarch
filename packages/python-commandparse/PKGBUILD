# This file is part of BlackArch Linux ( https://www.blackarch.org/ ).
# See COPYING for license details.

pkgbase=python-commandparse
pkgname=('python2-commandparse' 'python-commandparse')
_pkgname=commandparse
pkgver=1.1.1
pkgrel=1
pkgdesc='CLI application commands parser.'
arch=('any')
url='https://pypi.org/project/commandparse/#files'
license=('MIT')
makedepends=('python2-setuptools' 'python-setuptools')
source=("https://files.pythonhosted.org/packages/99/48/5cc2e8f050b234366b9632dc798b1a8f6b4dd2b25727a95770f11f5a9f2f/$_pkgname-$pkgver.tar.gz")
sha512sums=('78dfa34adc6e5843f36385975ceedc23760c4c3a0d02504946a50212a0fc9bf9d7477613dc249f98feae6af0fc2a15da1c06a60c5cb6b7baf48646ef6869f85c')

prepare() {
  cp -a "$_pkgname-$pkgver"{,-2}
}

build() {
  cd "$_pkgname-$pkgver-2"

  python2 setup.py build

  cd "$srcdir/$_pkgname-$pkgver"

  python setup.py build
}

package_python2-commandparse() {
  depends=('python2')

  cd "$_pkgname-$pkgver-2"

  python2 setup.py install --prefix=/usr --root="$pkgdir" -O1 --skip-build

  chmod 644 "$pkgdir"/usr/lib/python2.7/site-packages/commandparse-1.1.1-py2.7.egg-info/*
}

package_python-commandparse() {
  depends=('python')

  cd "$_pkgname-$pkgver"

  python setup.py install --prefix=/usr --root="$pkgdir" -O1 --skip-build

  chmod 644 \
    "$pkgdir"/usr/lib/python3.9/site-packages/commandparse-1.1.1-py3.9.egg-info/*
}

