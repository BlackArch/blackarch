# This file is part of BlackArch Linux ( https://www.blackarch.org/ ).
# See COPYING for license details.

pkgbase=python-commandparse
pkgname=('python2-commandparse' 'python-commandparse')
_pkgname=commandparse
pkgver=1.0.8
pkgrel=5
pkgdesc='CLI application commands parser.'
arch=('any')
url='https://pypi.org/project/commandparse/#files'
license=('MIT')
makedepends=('python2-setuptools' 'python-setuptools')
source=("https://files.pythonhosted.org/packages/e5/38/c2a10b4afb81787588dc9991c253f546093f7e0d92fcda36daaa76580d2c/$_pkgname-$pkgver.tar.gz")
sha512sums=('d4f0ae47bf272c7e4f2a1e6d110eade2184afd45b0ab1da3f370cf608af9f778226859d87bb6e2d2bd78f9d2e5152c3b08c99adf838cea0bd47b12b6ecb6b13a')

prepare() {
  cp -a "$_pkgname-$pkgver"{,-2}
}

build() {
  cd "$_pkgname-$pkgver-2"

  python2 setup.py build

  cd "$srcdir/$_pkgname-$pkgver"

  python setup.py build
}

package_python2-commandparse() {
  depends=('python2')

  cd "$_pkgname-$pkgver-2"

  python2 setup.py install --prefix=/usr --root="$pkgdir" -O1 --skip-build

  chmod 644 "$pkgdir"/usr/lib/python2.7/site-packages/commandparse-1.0.8-py2.7.egg-info/*
}

package_python-commandparse() {
  depends=('python')

  cd "$_pkgname-$pkgver"

  python setup.py install --prefix=/usr --root="$pkgdir" -O1 --skip-build

  chmod 644 \
    "$pkgdir"/usr/lib/python3.9/site-packages/commandparse-1.0.8-py3.9.egg-info/*
}

