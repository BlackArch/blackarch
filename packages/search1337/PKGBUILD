# This file is part of BlackArch Linux ( http://blackarch.org ).
# See COPYING for license details.

pkgname='search1337'
pkgver=16.ee49db9
pkgrel=2
groups=('blackarch' 'blackarch-automation' 'blackarch-exploitation')
pkgdesc='1337Day Online Exploit Scanner.'
arch=('any')
url='https://github.com/b3mb4m/Search1337'
license=('MIT')
depends=('python2' 'python2-mechanize')
makedepends=('git' 'python2-setuptools')
source=('git+https://github.com/b3mb4m/Search1337.git')
sha1sums=('SKIP')

pkgver() {
  cd "$srcdir/Search1337"

  echo $(git rev-list --count HEAD).$(git rev-parse --short HEAD)
}

package() {
  cd "$srcdir/Search1337"

  python2 setup.py install --root="$pkgdir" --optimize=1

  install -Dm644 README.md "$pkgdir/usr/share/doc/search1337/README.md"
  install -Dm644 LICENSE "$pkgdir/usr/share/licenses/search1337/LICENSE"
}
