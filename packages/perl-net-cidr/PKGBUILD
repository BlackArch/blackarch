# This file is part of BlackArch Linux ( http://blackarch.org ).
# See COPYING for license details.

pkgname='perl-net-cidr'
pkgver='0.18'
pkgrel=5
pkgdesc='Manipulate IPv4/IPv6 netblocks in CIDR notation.'
arch=('any')
license=('PerlArtistic' 'GPL')
options=('!emptydirs')
depends=('perl')
url='http://search.cpan.org/dist/Net-CIDR'
source=("http://search.cpan.org/CPAN/authors/id/M/MR/MRSAM/Net-CIDR-${pkgver}.tar.gz")
sha1sums=('d2bed482e19a1031fca57ab82ead4db638edf9bd')

# Function to change to the working directory and set
# environment variables to override undesired options.
_enviro() {
  cd "$srcdir/Net-CIDR-$pkgver"

  export \
    PERL_MM_USE_DEFAULT=1 \
    PERL_AUTOINSTALL=--skipdeps \
    PERL_MM_OPT="INSTALLDIRS=vendor DESTDIR='$pkgdir'" \
    PERL_MB_OPT="--installdirs vendor --destdir '$pkgdir'" \
    MODULEBUILDRC=/dev/null
}

build() {
  cd "$srcdir"

  _enviro

  /usr/bin/perl Makefile.PL

  make
}

package() {
  cd "$srcdir"

  _enviro

  make install

  # Remove "perllocal.pod" and ".packlist".
  find "$pkgdir" -name .packlist -o -name perllocal.pod -delete
}
