# This file is part of BlackArch Linux ( https://www.blackarch.org/ ).
# See COPYING for license details.

pkgname=perl-net-cidr
_pkgname=Net-CIDR
pkgver=0.20
pkgrel=2
pkgdesc='Manipulate IPv4/IPv6 netblocks in CIDR notation.'
arch=('any')
license=('PerlArtistic' 'GPL')
options=('!emptydirs')
depends=('perl')
url='http://search.cpan.org/dist/Net-CIDR'
source=("http://search.cpan.org/CPAN/authors/id/M/MR/MRSAM/$_pkgname-$pkgver.tar.gz")
sha512sums=('e00fd79bc21e9e56e74cb8de90372daf12e23a2be9a3cbfe328c2d72b7a280faba8368d89f8df11c79e2be47874b24f225504747c468498950ff33c9996cc314')

# Function to change to the working directory and set
# environment variables to override undesired options.
_enviro() {
  cd "$_pkgname-$pkgver"

  export \
    PERL_MM_USE_DEFAULT=1 \
    PERL_AUTOINSTALL=--skipdeps \
    PERL_MM_OPT="INSTALLDIRS=vendor DESTDIR='$pkgdir'" \
    PERL_MB_OPT="--installdirs vendor --destdir '$pkgdir'" \
    MODULEBUILDRC=/dev/null
}

build() {
  _enviro

  /usr/bin/perl Makefile.PL

  make
}

package() {
  _enviro

  make install

  find "$pkgdir" -name .packlist -o -name perllocal.pod -delete
}

