# This file is part of BlackArch Linux ( http://blackarch.org ).
# See COPYING for license details.

_gemname=ssrf_proxy
_pkgver=0.0.4.pre
pkgver=224.2e4c4a7
pkgname=ruby-$_gemname
pkgrel=1
pkgdesc='Facilitates tunneling HTTP communications through servers vulnerable to Server-Side Request Forgery.'
groups=('blackarch' 'blackarch-proxy' 'blackarch-tunnel')
arch=('any')
url='https://github.com/bcoles/ssrf_proxy'
license=('MIT')
depends=(ruby ruby-base32 ruby-celluloid ruby-celluloid-io ruby-colorize ruby-htmlentities ruby-ipaddress ruby-logger ruby-socksify ruby-webrick)
makedepends=('git')
source=('git+https://github.com/bcoles/ssrf_proxy.git')
options=(!emptydirs)
sha1sums=('SKIP')
replaces=(ssrf-proxy)
provides=(ssrf-proxy)
conflicts=(ssrf-proxy)

pkgver() {
	cd "$srcdir/ssrf_proxy"

  echo $(git rev-list --count HEAD).$(git rev-parse --short HEAD)
}

build() {
  cd "$srcdir/ssrf_proxy"

  gem build ssrf_proxy.gemspec
}

package() {
  cd "$srcdir/ssrf_proxy"

  local _gemdir="/usr/lib/ruby/gems/2.3.0"

  gem install --ignore-dependencies --no-document --no-user-install -i "$pkgdir/$_gemdir" -n "$pkgdir"/usr/bin $_gemname-$_pkgver.gem
  rm "$pkgdir/$_gemdir/cache/$_gemname-$_pkgver.gem"

  install -D -m644 "$pkgdir/$_gemdir/gems/$_gemname-$_pkgver/LICENSE.md" "$pkgdir/usr/share/licenses/$pkgname/LICENSE.md"
  install -D -m644 -t "$pkgdir/usr/share/doc/$pkgname" README.md

  local _extdir="$pkgdir//usr/lib/ruby/gems/2.3.0/extensions/$CARCH-linux/2.3.0/$_gemname-$_pkgver"
  if [ -d "$_extdir" ]; then
    rm -rf "$_extdir"/*
    touch "$_extdir/gem.build_complete"
  fi
  rm -rf "$pkgdir/$_gemdir/gems/$_gemname-$_pkgver"/{LICENSE.md,README.md}
}
