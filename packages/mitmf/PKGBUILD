# This file is part of BlackArch Linux ( http://blackarch.org ).
# See COPYING for license details.

pkgname='mitmf'
pkgver=169.83b4a93
pkgrel=1
groups=('blackarch' 'blackarch-exploitation' 'blackarch-proxy'
        'blackarch-networking' 'blackarch-spoof')
pkgdesc='A Framework for Man-In-The-Middle attacks written in Python.'
arch=('any')
url='https://github.com/byt3bl33d3r/MITMf'
license=('GPL3')
depends=('python2' 'python2-twisted' 'python2-msgpack' 'python2-requests'
         'scapy' 'capstone' 'python2-pefile' 'python2-capstone')
makedepends=('git')
source=('git+https://github.com/byt3bl33d3r/MITMf.git')
sha1sums=('SKIP')

pkgver() {
  cd "$srcdir/MITMf"

  echo $(git rev-list --count HEAD).$(git rev-parse --short HEAD)
}

prepare() {
  cd "$srcdir/MITMf"

  git submodule init
  git submodule update
}

package() {
  cd "$srcdir/MITMf"

  mkdir -p "$pkgdir/usr/bin"
  mkdir -p "$pkgdir/usr/share/mitmf"

  install -Dm644 README.md "$pkgdir/usr/share/doc/mitmf/README.md"
  install -Dm644 LICENSE "$pkgdir/usr/share/licenses/mitmf/LICENSE"

  rm README.md LICENSE

  cp -a * "$pkgdir/usr/share/mitmf"

  cat > "$pkgdir/usr/bin/mitmf" << EOF
#!/bin/sh
cd /usr/share/mitmf
exec python2 mitmf.py "\$@"
EOF

  chmod +x "$pkgdir/usr/bin/mitmf"
}
