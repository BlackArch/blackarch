# This file is part of BlackArch Linux ( https://www.blackarch.org/ ).
# See COPYING for license details.

pkgname=python-litecli
_pkgname=litecli
pkgver=1.12.3
pkgrel=3
pkgdesc='CLI for SQLite Databases with auto-completion and syntax highlighting.'
arch=('any')
url='https://pypi.org/project/litecli/#files'
license=('BSD')
depends=('python' 'python-click' 'python-pygments' 'python-prompt_toolkit'
         'python-sqlparse' 'python-configobj' 'python-cli-helpers')
makedepends=('python-pip' 'python-build' 'python-wheel')
source=("https://files.pythonhosted.org/packages/source/${_pkgname::1}/$_pkgname/$_pkgname-$pkgver.tar.gz")
sha512sums=('624750c6e7c48a9aab21de34d2c13e3cbbf44c34e7ee93c95bb7af1cbf0cd26d3cf978b3180cc69d7cd5b7365e72e1515fbe826dd269faf6fbe7af050de0ecc8')

build() {
  cd "$_pkgname-$pkgver"

  python -m build --wheel --outdir="$startdir/dist"
}

package() {
  cd "$_pkgname-$pkgver"

  pip install \
    --verbose \
    --disable-pip-version-check \
    --no-warn-script-location \
    --ignore-installed \
    --no-compile \
    --no-deps \
    --root="$pkgdir" \
    --prefix=/usr \
    --no-index \
    --find-links="file://$startdir/dist" \
    $_pkgname
}

