# This file is part of BlackArch Linux ( https://www.blackarch.org/ ).
# See COPYING for license details.

pkgbase=python-litecli
pkgname=('python2-litecli' 'python-litecli')
_pkgname=litecli
pkgver=1.3.2
pkgrel=1
pkgdesc='CLI for SQLite Databases with auto-completion and syntax highlighting.'
arch=('any')
url='https://pypi.org/project/litecli/#files'
license=('BSD')
makedepends=('python2-setuptools' 'python-setuptools')
source=("https://files.pythonhosted.org/packages/01/39/a973a8111ff73275f47b1ef287ae0562d737d530ac87b702d30e1decdee0/$_pkgname-$pkgver.tar.gz"
        'setup.py.patch')
sha512sums=('b5c10689322188f10f4f35baf72a9f9c61a9126008cddbdeccc7c010b38f69a5076d1122e262fe658291ee29320bdb296c3aee14231d4ee25b85fb0856cb9116'
            'fe6de29e5c81b9ace7aed537fb642b24b526970bda439d93aae7d3e90e02aef53ce6c73d784f2429a26151cdbe37160ad3675b5b031995a80bc234731f393c9c')

prepare() {
  patch -p1 < setup.py.patch

  cp -a "$_pkgname-$pkgver"{,-2}
}

build() {
  cd "$_pkgname-$pkgver-2"

  python2 setup.py build

  cd "$srcdir/$_pkgname-$pkgver"

  python setup.py build
}

package_python2-litecli() {
  depends=('python2' 'python2-click' 'python2-pygments'
           'python2-prompt_toolkit' 'python2-sqlparse' 'python2-configobj'
           'python2-cli-helpers')

  cd "$_pkgname-$pkgver-2"

  python2 setup.py install --root="$pkgdir" --prefix=/usr -O1 --skip-build

  mv "$pkgdir/usr/bin/$_pkgname" "$pkgdir/usr/bin/${_pkgname}2"
}

package_python-litecli() {
  depends=('python' 'python-click' 'python-pygments' 'python-prompt_toolkit'
           'python-sqlparse' 'python-configobj' 'python-cli-helpers')

  cd "$_pkgname-$pkgver"

  python setup.py install --root="$pkgdir" --prefix=/usr -O1 --skip-build
}

