# This file is part of BlackArch Linux ( https://www.blackarch.org/ ).
# See COPYING for license details.

pkgname=python-litecli
_pkgname=litecli
pkgver=1.17.1
pkgrel=1
pkgdesc='CLI for SQLite Databases with auto-completion and syntax highlighting.'
arch=('any')
url='https://pypi.org/project/litecli/#files'
license=('BSD')
depends=('python' 'python-click' 'python-pygments' 'python-prompt_toolkit'
         'python-sqlparse' 'python-configobj' 'python-cli-helpers')
makedepends=('python-pip' 'python-build' 'python-wheel')
source=("https://files.pythonhosted.org/packages/source/${_pkgname::1}/$_pkgname/$_pkgname-$pkgver.tar.gz")
sha512sums=('67945258564e650d395c5d86bde92cc1f92132763557d63937de7b53cd447018d80f606f9aa8f581a02f5f46da6fd3491f532c26418973acbe507ef8db6dd69f')

build() {
  cd "$_pkgname-$pkgver"

  python -m build --wheel --outdir="$startdir/dist"
}

package() {
  cd "$_pkgname-$pkgver"

  pip install \
    --verbose \
    --disable-pip-version-check \
    --no-warn-script-location \
    --ignore-installed \
    --no-compile \
    --no-deps \
    --root="$pkgdir" \
    --prefix=/usr \
    --no-index \
    --find-links="file://$startdir/dist" \
    $_pkgname
}

