# This file is part of BlackArch Linux ( http://blackarch.org ).
# See COPYING for license details.

pkgname=pngcheck
pkgver=2.3.0
pkgrel=1
groups=('blackarch' 'blackarch-stego' 'blackarch-defensive' 'blackarch-forensic')
pkgdesc='Verifies the integrity of PNG, JNG and MNG files by checking the CRCs and decompressing the image data.'
arch=('x86_64' 'armv6h' 'armv7h' 'aarch64')
url='http://www.libpng.org/pub/png/apps/pngcheck.html'
license=('GPL')
depends=('libpng')
source=("http://sourceforge.net/projects/png-mng/files/pngcheck/$pkgver/pngcheck-${pkgver}.tar.gz"
        'makefile.patch')
sha512sums=('d099a360d41fc553e9ea02e56ae9812a4a72f3b7f1cd73c021fdb0b9233bb247e100b95fe8e297a2dcdd1bb996128a365794cb8f5702e5c93618af03f18140ad'
            '2915fab1ccd358a32acf0151759ef4e6d7b433ed37531a4ecaab6a70128a091de31ea41e1ef5d7b738a57bfe01d7331b61b15632e5b6856cc782704b516a4221')

prepare() {
	cd "$srcdir/pngcheck-$pkgver"

  cp Makefile.unx Makefile

	patch -p0 < $srcdir/makefile.patch
}

build() {
	cd "$srcdir/pngcheck-$pkgver"

  make
}

package() {
	cd "$srcdir/pngcheck-$pkgver"

  install -Dm755 pngcheck "$pkgdir/usr/bin/pngcheck"
	install -Dm755 pngsplit "$pkgdir/usr/bin/pngsplit"
	install -Dm755 png-fix-IDAT-windowsize \
    "$pkgdir/usr/bin/png-fix-IDAT-windowsize"
}
