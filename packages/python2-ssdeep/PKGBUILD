# This file is part of BlackArch Linux ( https://www.blackarch.org/ ).
# See COPYING for license details.

pkgname=python2-ssdeep
_pkgname=ssdeep
pkgver=3.4
pkgrel=10
pkgdesc='Python wrapper for ssdeep fuzzy hashing library.'
arch=('x86_64' 'aarch64')
license=('LGPL-3.0-or-later')
url='https://pypi.org/project/ssdeep/#files'
depends=('python2' 'python2-six' 'python2-cffi' 'ssdeep')
makedepends=('python2-setuptools' 'python2-six' 'python2-cffi' 'ssdeep'
             'python2-pip' 'python2-wheel')
source=("https://files.pythonhosted.org/packages/source/${_pkgname::1}/$_pkgname/$_pkgname-$pkgver.tar.gz")
sha512sums=('bcfbc145fbb467567f617883a8e0e4ce58a33db67d5c0a5193637e2506d076a3d70d371a84250290ad04844173516a6e6158624b43cd227e98edbfca330975a9')

build() {
  cd "$_pkgname-$pkgver"

  python2 setup.py build
}

package() {
  cd "$_pkgname-$pkgver"

  python2 setup.py install --root="$pkgdir" --prefix=/usr -O1 --skip-build
}

