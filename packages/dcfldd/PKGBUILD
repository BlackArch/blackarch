# This file is part of BlackArch Linux ( https://www.blackarch.org/ ).
# See COPYING for license details.

# Old Maintainer: Alexander F RÃ¸dseth <xyproto@archlinux.org>
# Old Contributor: Paul Mattal <pjmattal@elys.com>

pkgname=dcfldd
pkgver=1.7
pkgrel=2
pkgdesc='DCFL (DoD Computer Forensics Lab) dd replacement with hashing.'
groups=('blackarch' 'blackarch-forensic')
arch=('x86_64' 'aarch64')
url='https://dcfldd.sourceforge.net/'
license=('GPL')
depends=('glibc')
source=("https://github.com/resurrecting-open-source-projects/dcfldd/archive/v$pkgver.tar.gz")
sha512sums=('e1dc4773438914a8102a692c7b7cbf486a9b03111820facb0134b4b9d058751695d1a7c64481852026412aacbb0fa04a18f833ff77ca30f154d5067a07057f31')

prepare() {
  cd "$pkgname-$pkgver"

  ./autogen.sh
}

build() {
  cd "$pkgname-$pkgver"

  ./configure --prefix=/usr --mandir=/usr/share/man

  make
}

package() {
  cd "$pkgname-$pkgver"

  make DESTDIR="$pkgdir" install
}

