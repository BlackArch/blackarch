# This file is part of BlackArch Linux ( https://www.blackarch.org/ ).
# See COPYING for license details.

# Old Maintainer: Alexander F RÃ¸dseth <xyproto@archlinux.org>
# Old Contributor: Paul Mattal <pjmattal@elys.com>

pkgname=dcfldd
pkgver=1.7.1
pkgrel=1
pkgdesc='DCFL (DoD Computer Forensics Lab) dd replacement with hashing.'
groups=('blackarch' 'blackarch-forensic')
arch=('x86_64' 'aarch64')
url='https://github.com/resurrecting-open-source-projects/dcfldd/'
license=('GPL')
depends=('glibc')
source=("https://github.com/resurrecting-open-source-projects/dcfldd/archive/v$pkgver.tar.gz")
sha512sums=('d1120ef9b50e379015d50d00b6c8b24978ec58c05434580d67a1707f5e1d41d1e294ca5a7f9d1e7bfeabcb9fd7976b384db93a78f70421870d26391404ee4ae6')

prepare() {
  cd "$pkgname-$pkgver"

  ./autogen.sh
}

build() {
  cd "$pkgname-$pkgver"

  ./configure --prefix=/usr --mandir=/usr/share/man

  make
}

package() {
  cd "$pkgname-$pkgver"

  make DESTDIR="$pkgdir" install
}

