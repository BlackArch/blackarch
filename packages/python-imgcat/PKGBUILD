# This file is part of BlackArch Linux ( https://www.blackarch.org/ ).
# See COPYING for license details.

pkgbase=python-imgcat
pkgname=('python2-imgcat' 'python-imgcat')
_pkgname=imgcat
pkgver=0.4.0
pkgrel=1
pkgdesc='imgcat as Python API and CLI.'
arch=('any')
url='https://pypi.org/project/imgcat/#files'
license=('MIT')
makedepends=('python2-setuptools' 'python-setuptools' 'python2-pytest-runner'
             'python-pytest-runner')
options=(!emptydirs)
source=("https://files.pythonhosted.org/packages/a1/ca/af20d06dba98d2a2b4bf2dc84fa28bc657f96f9690dd7d746315865c13ce/$_pkgname-$pkgver.tar.gz")
sha512sums=('64aeb65cf31b04ccf4e7e53463aa6d019c7ff309d1b6f9f0ec157009d36ee77f197ecf40c057f164e2515d0457d1fe6405c73f644993fa3ca4af6f2aa12f5d33')

prepare() {
  cp -a "$_pkgname-$pkgver"{,-2}
}

build() {
  cd "$_pkgname-$pkgver-2"

  python2 setup.py build

  cd "$srcdir/$_pkgname-$pkgver"

  python setup.py build
}

package_python2-imgcat() {
  depends=('python2')

  cd "$_pkgname-$pkgver-2"

  python2 setup.py install --root="$pkgdir" --prefix=/usr -O1 --skip-build

  mv "$pkgdir/usr/bin/$_pkgname" "$pkgdir/usr/bin/${_pkgname}2"
}

package_python-imgcat() {
  depends=('python')

  cd "$_pkgname-$pkgver"

  python setup.py install --root="$pkgdir" --prefix=/usr -O1 --skip-build
}

