# This file is part of BlackArch Linux ( https://www.blackarch.org/ ).
# See COPYING for license details.

pkgbase=python-imgcat
pkgname=('python2-imgcat' 'python-imgcat')
_pkgname=imgcat
pkgver=0.5.0
pkgrel=2
pkgdesc='imgcat as Python API and CLI.'
arch=('any')
url='https://pypi.org/project/imgcat/#files'
license=('MIT')
makedepends=('python2-setuptools' 'python-setuptools' 'python2-pytest-runner'
             'python-pytest-runner')
options=(!emptydirs)
source=("https://files.pythonhosted.org/packages/d4/56/1c3277dd00e8014e9499d8f654f7179d84edf4a07bf4b7932410625057aa/$_pkgname-$pkgver.tar.gz")
sha512sums=('660b2ffcbce5908c7f811e7bfabcfa0816c8b82c0720da29694db56acb8d19bd65fdfa68f5b5f1df32a6682a23583dc912248e014c28c10bc3fcaab8ce74d3f2')

prepare() {
  cp -a "$_pkgname-$pkgver"{,-2}
}

build() {
  cd "$_pkgname-$pkgver-2"

  python2 setup.py build

  cd "$srcdir/$_pkgname-$pkgver"

  python setup.py build
}

package_python2-imgcat() {
  depends=('python2')

  cd "$_pkgname-$pkgver-2"

  python2 setup.py install --root="$pkgdir" --prefix=/usr -O1 --skip-build

  mv "$pkgdir/usr/bin/$_pkgname" "$pkgdir/usr/bin/${_pkgname}2"
}

package_python-imgcat() {
  depends=('python')

  cd "$_pkgname-$pkgver"

  python setup.py install --root="$pkgdir" --prefix=/usr -O1 --skip-build
}

