# This file is part of BlackArch Linux ( https://www.blackarch.org/ ).
# See COPYING for license details.

# This PKGBUILD was extracted from AUR and modified by us.
# Creator: Charlie Wolf <charlie@wolf.is>
# Changes by: petruknisme

pkgname=ruby-foreman
_pkgname=foreman
pkgver=0.88.1
pkgrel=4
pkgdesc='Process manager for Procfile based applications.'
arch=('any')
depends=('ruby2.6' 'ruby-thor')
url="https://rubygems.org/gems/foreman"
noextract=("$_pkgname-$pkgver.gem")
license=('MIT')
source=("https://rubygems.org/gems/$_pkgname-$pkgver.gem")
sha512sums=('1aff7b6f4846fb2e87a90bcb3aea695953ac399bc226cbd70dc4a952fe3de2ed510dd263ce71fbfa4f756592548a57ade29ab6ce1dde43e610e9ae82026f224b')

package() {
  local _gemdir="$(ruby -e'puts Gem.default_dir')"

  gem install --ignore-dependencies --no-user-install --no-document \
    -i "$pkgdir/$_gemdir" -n "$pkgdir/usr/bin" "$_pkgname-$pkgver.gem"

  rm "$pkgdir/$_gemdir/cache/$_pkgname-$pkgver.gem"
  find "$pkgdir/$_gemdir/extensions/" -name *.so -delete
  rm -r "$pkgdir/$_gemdir/gems/$_pkgname-$pkgver/spec"

  install -Dm 644 "$pkgdir/$_gemdir/gems/$_pkgname-$pkgver/README.md" \
    "$pkgdir/usr/share/licenses/$pkgname/README.md"
}

