# This file is part of BlackArch Linux ( http://blackarch.org ).
# See COPYING for license details.

pkgname=mtscan
pkgver=73.19ddb81
pkgrel=1
pkgdesc='Mikrotik RouterOS wireless scanner.'
arch=('x86_64' 'armv6h' 'armv7h' 'aarch64')
groups=('blackarch' 'blackarch-wireless')
url='https://github.com/kkonradpl/mtscan'
license=('GPL2')
depends=('libpcap' 'openssl' 'gtk2' 'zlib' 'yajl' 'libssh')
makedepends=('git' 'cmake')
source=('git+https://github.com/kkonradpl/mtscan.git')
sha512sums=('SKIP')

pkgver() {
  cd mtscan

  echo $(git rev-list --count HEAD).$(git rev-parse --short HEAD)
}

build() {
  cd mtscan

  install -dm 755 build
  cd build

  cmake -DCMAKE_INSTALL_PREFIX=/usr ..

  make
}

package() {
  cd mtscan

  install -Dm 755 build/mtscan "$pkgdir/usr/bin/mtscan"
  install -Dm 644 -t "$pkgdir/usr/share/doc/${pkgname}/" *.md
  install -Dm 644 LICENSE "$pkgdir/usr/share/licenses/${pkgname}/LICENSE"
}

