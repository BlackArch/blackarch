# This file is part of BlackArch Linux ( https://www.blackarch.org/ ).
# See COPYING for license details.

pkgname=trevorspray
pkgver=2.2.1
pkgrel=1
epoch=1
pkgdesc='A modular password sprayer with threading, clever proxying, loot modules, and more!'
arch=('any')
groups=('blackarch' 'blackarch-cracker')
url='https://github.com/blacklanternsecurity/TREVORspray'
license=('GPL3')
depends=('trevorproxy' 'python-pysocks' 'python-tldextract'
         'python-beautifulsoup4' 'python-exchangelib' )
makedepends=('python-build' 'python-pip')
source=("https://files.pythonhosted.org/packages/source/${pkgname::1}/$pkgname/$pkgname-$pkgver.tar.gz")
sha512sums=('92888a116555cbaeff5158a1b941956a1a6b19b6dbae78fe5162003d6333b9cba01738efd113e7d7ed21a89ec64e40576381baa0c91483785df9da6d037b6e9d')

build() {
  cd "$pkgname-$pkgver"

  python -m build --wheel --outdir="$startdir/dist"
}

package() {
  cd "$pkgname-$pkgver"

  pip install \
    --verbose \
    --disable-pip-version-check \
    --no-warn-script-location \
    --ignore-installed \
    --no-compile \
    --no-deps \
    --root="$pkgdir" \
    --prefix=/usr \
    --no-index \
    --find-links="file://$startdir/dist" \
    "$pkgname"
}

