# This file is part of BlackArch Linux ( https://www.blackarch.org/ ).
# See COPYING for license details.

pkgname=ruby-nokogiri
_gemname=nokogiri
pkgver=1.10.7
pkgrel=3
pkgdesc='An HTML, XML, SAX, and Reader parser.'
arch=('any')
license=('MIT')
depends=('ruby-mini_portile2')
makedepends=('ruby-rdoc')
options=('!distcc')
url='http://nokogiri.org'
source=("https://rubygems.org/gems/$_gemname-$pkgver.gem")
sha512sums=('2290626ebb70e47b467d04844722db8137937387dff029e2c214f49c6ee0fbf160d8534023823591f279d768c57152197ba4141a3e33c1216b53d48a4aef62b5')
noextract=("$_gemname-$pkgver.gem")

package() {
  _gemdir=$(ruby -e 'puts Gem.default_dir')

  if [[ $CARCH == arm* ]] ; then
    gem install --no-user-install --ignore-dependencies --no-rdoc --no-ri \
      -i "$pkgdir$_gemdir" "$_gemname-$pkgver.gem"
  else
    gem install --no-user-install --ignore-dependencies -i "$pkgdir$_gemdir" \
      "$_gemname-$pkgver.gem"
  fi
}

