# This file is part of BlackArch Linux ( https://www.blackarch.org/ ).
# See COPYING for license details.

pkgname=libfvde
pkgver=149.86e0c5b
pkgrel=1
pkgdesc='Library and tools to access FileVault Drive Encryption (FVDE) encrypted volumes.'
groups=('blackarch' 'blackarch-forensic')
arch=('x86_64' 'aarch64')
url='https://github.com/libyal/libfvde'
license=('LGPL3')
depends=('libxml2' 'openssl')
makedepends=('autoconf' 'automake' 'binutils' 'gcc' 'git' 'libtool' 'make')
source=("$pkgname::git+https://github.com/libyal/$pkgname.git")
sha512sums=('SKIP')

pkgver() {
  cd $pkgname

  echo $(git rev-list --count HEAD).$(git rev-parse --short HEAD)
}

prepare() {
  cd $pkgname

  ./synclibs.sh
}

build() {
  cd $pkgname

  ./autogen.sh
  ./configure --prefix=/usr
  make
}

package() {
  cd $pkgname

  install -Dm 644 -t "$pkgdir/usr/share/doc/$pkgname/" \
    AUTHORS ChangeLog NEWS README
  install -Dm 644 COPYING "$pkgdir/usr/share/licenses/$pkgname/LICENSE"

  make DESTDIR="$pkgdir" install
}

