# This file is part of BlackArch Linux ( http://blackarch.org ).
# See COPYING for license details.

pkgname='ftpmap'
pkgver=52.cbeabbe
pkgrel=1
groups=('blackarch' 'blackarch-fingerprint' 'blackarch-scanner')
pkgdesc='Scans remote FTP servers to identify what software and what versions they are running.'
arch=('x86_64' 'armv6h' 'armv7h' 'aarch64')
url='http://wcoserver.googlecode.com/files/'
license=('GPL2')
makedepends=('git')
source=('git+https://github.com/Hypsurus/ftpmap.git')
sha1sums=('SKIP')

pkgver() {
  cd "$srcdir/ftpmap"

  echo $(git rev-list --count HEAD).$(git rev-parse --short HEAD)
}

prepare() {
  cd "$srcdir/ftpmap"

  sed -ig 's|"../db/|"/usr/share/ftpmap/db/|' src/ftpmap.c
}

build() {
  cd "$srcdir/ftpmap"

  if [[ "$CARCH" == "i686" || "$CARCH" == "x86_64" ]];
  then
    ./configure --prefix=/usr
  else
    ./configure --prefix=/usr --build=arm
  fi

  make
}

package() {
  cd "$srcdir/ftpmap"

  mkdir -p "$pkgdir/usr/share/ftpmap"

  make DESTDIR="$pkgdir" install

  install -Dm644 -t "$pkgdir/usr/share/doc/ftpmap/" AUTHORS THANKS NEWS \
    README.md
  install -Dm644 "COPYING" "$pkgdir/usr/share/licenses/ftpmap/COPYING"
}
