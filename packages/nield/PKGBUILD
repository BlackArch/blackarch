# This file is part of BlackArch Linux ( http://blackarch.org ).
# See COPYING for license details.

pkgname=nield
pkgver=0.6.1
pkgrel=1
groups=('blackarch' 'blackarch-networking')
pkgdesc='A tool to receive notifications from kernel through netlink socket, and generate logs related to interfaces, neighbor cache(ARP,NDP), IP address(IPv4,IPv6), routing, FIB rules, traffic control.'
arch=('x86_64' 'armv6h' 'armv7h' 'aarch64')
url='http://nield.sourceforge.net/'
license=('GPL2')
source=("https://github.com/t2mune/nield/archive/v${pkgver}.tar.gz")
sha512sums=('ad026a6d0c47b7eed858a9b2ce71e7280272068aedb6aaeb1305b13a80de3011d199169a7ff479b76ce8f58b1f4b70daa3e933533eb71a536e14281aadc87387')

build() {
  cd "$srcdir/nield-$pkgver"

  if [[ "$CARCH" == "x86_64" ]];
  then
    ./configure --prefix=/usr --sbindir=/usr/bin
  else
    ./configure --build=arm --prefix=/usr --sbindir=/usr/bin
  fi

  make
}

package() {
  cd "$srcdir/nield-$pkgver"

  install -Dm644 INSTALL "$pkgdir/usr/share/doc/nield/INSTALL"
  install -m644 ChangeLog "$pkgdir/usr/share/doc/nield/ChangeLog"
  install -Dm644 COPYING "$pkgdir/usr/share/licenses/nield/COPYING"

  make DESTDIR="$pkgdir" install
}
