# This file is part of BlackArch Linux ( https://www.blackarch.org/ ).
# See COPYING for license details.

pkgname=easy-creds
pkgver=3.9
pkgrel=2
groups=('blackarch' 'blackarch-automation' 'blackarch-networking')
pkgdesc='A bash script that leverages ettercap and other tools to obtain credentials.'
arch=('any')
url='https://github.com/brav0hax/easy-creds'
license=('GPL2')
depends=('bash' 'ettercap' 'dsniff' 'dhcp' 'hostapd' 'ipcalc' 'screen'
         'freeradius' 'hamster' 'sslstrip' 'metasploit' 'mdk3' 'aircrack-ng'
         'asleap')
source=("https://github.com/brav0hax/easy-creds/archive/v$pkgver.tar.gz")
sha512sums=('9e5f99f0bd227fa45235b0da9bba4985a6cc15667b7fe64b4aebe52515381aa4f40a18346cd8814ff37801f795619970b6965f78c5d586c6901bb367de9a6cb3')

package() {
  cd "$pkgname-$pkgver"

  install -dm 755 "$pkgdir/usr/bin"

  install -Dm 755 easy-creds.sh "$pkgdir/usr/share/$pkgname/easy-creds.sh"
  install -m 644 definitions.sslstrip \
    "$pkgdir/usr/share/$pkgname/definitions.sslstrip"
  install -Dm 644 README "$pkgdir/usr/share/$pkgname/"

  cat > "$pkgdir/usr/bin/$pkgname" << EOF
#!/bin/sh
cd /usr/share/$pkgname
exec ./easy-creds.sh "\$@"
EOF

  chmod +x "$pkgdir/usr/bin/$pkgname"
}

