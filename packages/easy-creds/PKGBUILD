# This file is part of BlackArch Linux ( http://blackarch.org ).
# See COPYING for license details.

pkgname=easy-creds
pkgver=3.9
pkgrel=2
groups=('blackarch' 'blackarch-automation' 'blackarch-networking')
pkgdesc='A bash script that leverages ettercap and other tools to obtain credentials.'
arch=('any')
url='https://github.com/brav0hax/easy-creds'
license=('GPL2')
depends=('bash' 'ettercap' 'dsniff' 'dhcp' 'hostapd' 'ipcalc' 'screen'
         'freeradius' 'hamster' 'sslstrip' 'metasploit' 'mdk3' 'aircrack-ng'
         'asleap')
source=("https://github.com/brav0hax/easy-creds/archive/v${pkgver}.tar.gz")
sha512sums=('9e5f99f0bd227fa45235b0da9bba4985a6cc15667b7fe64b4aebe52515381aa4f40a18346cd8814ff37801f795619970b6965f78c5d586c6901bb367de9a6cb3')

package() {
  cd "$srcdir/easy-creds-$pkgver"

  install -dm 755 "$pkgdir/usr/bin"

  install -Dm755 easy-creds.sh "$pkgdir/usr/share/easy-creds/easy-creds.sh"
  install -m644 definitions.sslstrip \
    "$pkgdir/usr/share/easy-creds/definitions.sslstrip"
  install -Dm644 README "$pkgdir/usr/share/easy-creds/"

  cat > "$pkgdir/usr/bin/easy-creds" << EOF
#!/bin/sh
cd /usr/share/easy-creds
exec ./easy-creds.sh
EOF

  chmod a+x "$pkgdir/usr/bin/easy-creds"
}
