# This file is part of BlackArch Linux ( http://blackarch.org ).
# See COPYING for license details.

pkgname=keimpx
pkgver=166.a10a0c7
pkgrel=3
epoch=3
groups=('blackarch' 'blackarch-cracker')
pkgdesc='Tool to verify the usefulness of credentials across a network over SMB.'
url='http://code.google.com/p/keimpx/'
license=('Apache')
depends=('python2' 'impacket' 'python2-pycryptodome')
makedepends=('git' 'python2-distribute')
arch=('any')
source=('git+https://github.com/inquisb/keimpx.git'
        'setup.py-remove_py2exe.patch')
sha512sums=('SKIP'
            'aec4d1650df3dbf7442c803c7cb7f08739c0a71c7c504cfce09781621cf84a5c8447b2126bde895d8de909dbed3cb9c30c1266bc7c4a647d8a95afbc2e9c5b36')

pkgver() {
  cd "$srcdir/keimpx"

  echo $(git rev-list --count HEAD).$(git rev-parse --short HEAD)
}

prepare() {
  cd "$srcdir/keimpx"

  sed -i 's/\/usr\/bin\/env python/\/usr\/bin\/env python2/' keimpx.py
  sed -i 's/keimpx_path =/keimpx_path = \"\/usr\/share\/keimpx\" #/' keimpx.py
}

package() {
  cd "$srcdir/keimpx"

  install -dm 755 "$pkgdir/usr/bin"
  install -dm 755 "$pkgdir/usr/share/keimpx"

  install -Dm644 README.md "$pkgdir/usr/share/doc/keimpx/README.md"

  rm README.md setup.py

  cp -a * "$pkgdir/usr/share/keimpx"

  cat > "$pkgdir/usr/bin/keimpx" << EOF
#!/bin/sh
cd /usr/share/keimpx
exec python2 keimpx.py "\$@"
EOF

  chmod a+x "$pkgdir/usr/bin/keimpx"
}
