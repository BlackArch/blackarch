# This file is part of BlackArch Linux ( http://blackarch.org ).
# See COPYING for license details.

pkgname='keimpx'
pkgver=166.a10a0c7
pkgrel=2
epoch=3
groups=('blackarch' 'blackarch-cracker')
pkgdesc='Tool to verify the usefulness of credentials across a network over SMB.'
url='http://code.google.com/p/keimpx/'
license=('Apache')
depends=('python2' 'impacket' 'python2-crypto')
makedepends=('git' 'python2-distribute')
arch=('any')
source=('git+https://github.com/inquisb/keimpx.git'
        'setup.py-remove_py2exe.patch')
sha1sums=('SKIP'
          'd54425f9d34d17870d13eade888207252233cfb5')

pkgver() {
  cd "$srcdir/keimpx"

  echo $(git rev-list --count HEAD).$(git rev-parse --short HEAD)
}

prepare() {
  cd "$srcdir/keimpx"

  sed -i 's/\/usr\/bin\/env python/\/usr\/bin\/env python2/' keimpx.py
  sed -i 's/keimpx_path =/keimpx_path = \"\/usr\/share\/keimpx\" #/' keimpx.py
}

package() {
  cd "$srcdir/keimpx"

  mkdir -p "$pkgdir/usr/bin"
  mkdir -p "$pkgdir/usr/share/keimpx"

  install -Dm644 README.md "$pkgdir/usr/share/doc/keimpx/README.md"

  rm README.md setup.py

  cp -a * "$pkgdir/usr/share/keimpx"

  cat > "$pkgdir/usr/bin/keimpx" << EOF
#!/bin/sh
cd /usr/share/keimpx
exec python2 keimpx.py "\$@"
EOF

  chmod a+x "$pkgdir/usr/bin/keimpx"
}
