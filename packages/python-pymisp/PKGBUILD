# This file is part of BlackArch Linux ( https://www.blackarch.org/ ).
# See COPYING for license details.

pkgname=python-pymisp
_pkgname=pymisp
pkgver=2.4.175
pkgrel=1
pkgdesc='Python API for MISP.'
arch=('any')
url='https://pypi.org/project/pymisp/#files'
license=('BSD')
depends=('python' 'python-six' 'python-dateutil' 'python-jsonschema'
         'python-requests' 'python-deprecated')
makedepends=('python-pip' 'python-build' 'python-wheel')
optdepends=('python-virustotal-api: VirusTotal support')
source=("https://files.pythonhosted.org/packages/source/${_pkgname::1}/$_pkgname/$_pkgname-$pkgver.tar.gz")
sha512sums=('7439ce4a5b5ecb4186c733dacc428a6521a790803eed2e3329d9ca6695e6f5105a55eb19141f6f5af5a191fe30e765d78d43a867fedd64bb1eefc69db9731ab5')

build() {
  cd "$_pkgname-$pkgver"

  python -m build --wheel --outdir="$startdir/dist"
}

package() {
  cd "$_pkgname-$pkgver"

  pip install \
    --verbose \
    --disable-pip-version-check \
    --no-warn-script-location \
    --ignore-installed \
    --no-compile \
    --no-deps \
    --root="$pkgdir" \
    --prefix=/usr \
    --no-index \
    --find-links="file://$startdir/dist" \
    $_pkgname
}

