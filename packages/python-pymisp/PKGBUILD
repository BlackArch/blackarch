# This file is part of BlackArch Linux ( https://www.blackarch.org/ ).
# See COPYING for license details.

pkgbase=python-pymisp
pkgname=('python-pymisp')
_pkgname=pymisp
pkgver=2.4.138
pkgrel=1
pkgdesc='Python API for MISP.'
arch=('any')
url='https://pypi.org/project/pymisp/#files'
license=('BSD')
makedepends=('python-setuptools')
source=("https://files.pythonhosted.org/packages/f4/a4/7bbfd212d546f7d2affd7eb7705f06ef4a0a255972a26625e6ad57679729/$_pkgname-$pkgver.tar.gz")
sha512sums=('34d5c29643f499ac293fcb0baeb50604426a163f56469976bdc05d289d0838b45154f9e27b03620fcab6ef7d1dd570a4d9fdfaf522f1dd8c4649bb11ea30ee84')

build() {
  cd "$srcdir/$_pkgname-$pkgver"

  python setup.py build
}

package_python-pymisp() {
  depends=('python' 'python-six' 'python-dateutil' 'python-jsonschema'
           'python-requests' 'python-deprecated')
  optdepends=('python-virustotal-api: VirusTotal support')

  cd "$_pkgname-$pkgver"

  python setup.py install --prefix=/usr --root="$pkgdir" --optimize=1 \
    --skip-build
}

