# This file is part of BlackArch Linux ( https://www.blackarch.org/ ).
# See COPYING for license details.

pkgbase=python-pymisp
pkgname=('python-pymisp')
_pkgname=pymisp
pkgver=2.4.169.2
pkgrel=1
pkgdesc='Python API for MISP.'
arch=('any')
url='https://pypi.org/project/pymisp/#files'
license=('BSD')
makedepends=('python-setuptools')
source=("https://files.pythonhosted.org/packages/source/${_pkgname::1}/$_pkgname/$_pkgname-$pkgver.tar.gz")
sha512sums=('21676891eb80db7ffc21b5cd7eb2acfaf95229640271bea13d7ed080688e10193efc878a4551a3aa517e6e6c280f1efe7847ad0769fd76e8a60db2579dcd4bf0')

build() {
  cd "$srcdir/$_pkgname-$pkgver"

  python setup.py build
}

package_python-pymisp() {
  depends=('python' 'python-six' 'python-dateutil' 'python-jsonschema'
           'python-requests' 'python-deprecated')
  optdepends=('python-virustotal-api: VirusTotal support')

  cd "$_pkgname-$pkgver"

  python setup.py install --prefix=/usr --root="$pkgdir" --optimize=1 \
    --skip-build
}

