# This file is part of BlackArch Linux ( https://www.blackarch.org/ ).
# See COPYING for license details.

pkgbase=python-pymisp
pkgname=('python2-pymisp' 'python-pymisp')
_pkgname=pymisp
pkgver=2.4.119.1
pkgrel=1
pkgdesc='Python API for MISP.'
arch=('any')
url='https://pypi.org/project/pymisp/#files'
license=('BSD')
makedepends=('python2-setuptools' 'python-setuptools')
source=("https://files.pythonhosted.org/packages/3b/ca/e612691c33957b8439da6b97047c63535946f1c8e8b4611b77ae9358272a/$_pkgname-$pkgver.tar.gz")
sha512sums=('65d6b84b9bdf05d4ac4a214ab27b98cc59687a66ee84cb7fdc66022a2951eae03f1fdfaf8be97576699510254edc642e0797b25cd890757f70e269cd776ff7b1')

prepare() {
  cp -a "$_pkgname-$pkgver"{,-2}
}

build() {
  cd "$_pkgname-$pkgver-2"

  python2 setup.py build

  cd "$srcdir/$_pkgname-$pkgver"

  python setup.py build
}

package_python2-pymisp() {
  depends=('python2' 'python2-six' 'python2-dateutil' 'python2-enum34'
           'python2-functools32' 'python2-jsonschema')

  cd "$_pkgname-$pkgver-2"

  python2 setup.py install --prefix=/usr --root="$pkgdir" --optimize=1 \
    --skip-build
}

package_python-pymisp() {
  depends=('python' 'python-six' 'python-dateutil' 'python-jsonschema')

  cd "$_pkgname-$pkgver"

  python setup.py install --prefix=/usr --root="$pkgdir" --optimize=1 \
    --skip-build
}

