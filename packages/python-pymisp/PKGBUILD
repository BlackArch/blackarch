# This file is part of BlackArch Linux ( https://www.blackarch.org/ ).
# See COPYING for license details.

pkgbase=python-pymisp
pkgname=('python2-pymisp' 'python-pymisp')
_pkgname=pymisp
pkgver=2.4.114
pkgrel=1
pkgdesc='Python API for MISP.'
arch=('any')
url='https://pypi.org/project/pymisp/#files'
license=('BSD')
makedepends=('python2-setuptools' 'python-setuptools')
source=("https://files.pythonhosted.org/packages/ac/b2/30bc613151f43a1572e1cca67e8981b9990d138d4cc28554501c2fc4fbb4/$_pkgname-$pkgver.tar.gz")
sha512sums=('af69310173e90ed1eccd1043a18fbacc6f7c14a28bebc7dd812fbdc3caec4bbafb476bdef7755e0adb8e7e8e836b268f033fb569d8b6fd856d6335089431bf2d')

prepare() {
  cp -a "$_pkgname-$pkgver"{,-2}
}

build() {
  cd "$_pkgname-$pkgver-2"

  python2 setup.py build

  cd "$srcdir/$_pkgname-$pkgver"

  python setup.py build
}

package_python2-pymisp() {
  depends=('python2' 'python2-six' 'python2-dateutil' 'python2-enum34'
           'python2-functools32' 'python2-jsonschema')

  cd "$_pkgname-$pkgver-2"

  python2 setup.py install --prefix=/usr --root="$pkgdir" --optimize=1 \
    --skip-build
}

package_python-pymisp() {
  depends=('python' 'python-six' 'python-dateutil' 'python-jsonschema')

  cd "$_pkgname-$pkgver"

  python setup.py install --prefix=/usr --root="$pkgdir" --optimize=1 \
    --skip-build
}

