# This file is part of BlackArch Linux ( https://www.blackarch.org/ ).
# See COPYING for license details.

pkgname=python-pymisp
_pkgname=pymisp
pkgver=2.4.176
_pyver=3.12
pkgrel=2
pkgdesc='Python API for MISP.'
arch=('any')
url='https://pypi.org/project/pymisp/#files'
license=('BSD')
depends=('python' 'python-six' 'python-dateutil' 'python-jsonschema'
         'python-requests' 'python-deprecated')
makedepends=('python-pip' 'python-build' 'python-wheel')
optdepends=('python-virustotal-api: VirusTotal support')
source=("https://files.pythonhosted.org/packages/source/${_pkgname::1}/$_pkgname/$_pkgname-$pkgver.tar.gz")
sha512sums=('095e10c466e44e01ecced8fbf08396969ce894b5c7a60c39dde8143163d7b9006f7b691ceaa758fc6667ee3f3cf44af95e4d585f741cb0600c8d5a94c6862396')

build() {
  cd "$_pkgname-$pkgver"

  python -m build --wheel --outdir="$startdir/dist"
}

package() {
  cd "$_pkgname-$pkgver"

  pip install \
    --verbose \
    --disable-pip-version-check \
    --no-warn-script-location \
    --ignore-installed \
    --no-compile \
    --no-deps \
    --root="$pkgdir" \
    --prefix=/usr \
    --no-index \
    --find-links="file://$startdir/dist" \
    $_pkgname

  rm -rf "$pkgdir/usr/lib/python$_pyver/site-packages/"{README.md,CHANGELOG.txt}
}

