# This file is part of BlackArch Linux ( https://www.blackarch.org/ ).
# See COPYING for license details.

pkgbase=python-pymisp
pkgname=('python2-pymisp' 'python-pymisp')
_pkgname=pymisp
pkgver=2.4.117.3
pkgrel=1
pkgdesc='Python API for MISP.'
arch=('any')
url='https://pypi.org/project/pymisp/#files'
license=('BSD')
makedepends=('python2-setuptools' 'python-setuptools')
source=("https://files.pythonhosted.org/packages/63/d2/34e0afe6f979906c72ae45317f075c58a48fa658138909b1a843006d20ac/$_pkgname-$pkgver.tar.gz")
sha512sums=('5013ee58211e767f9178dcce9143abda80e718c46f063695f274f6f1c1b0b5ab225c673322ad487806e4244d7c31c192dbfb5576589d396e4bab89a261781ed2')

prepare() {
  cp -a "$_pkgname-$pkgver"{,-2}
}

build() {
  cd "$_pkgname-$pkgver-2"

  python2 setup.py build

  cd "$srcdir/$_pkgname-$pkgver"

  python setup.py build
}

package_python2-pymisp() {
  depends=('python2' 'python2-six' 'python2-dateutil' 'python2-enum34'
           'python2-functools32' 'python2-jsonschema')

  cd "$_pkgname-$pkgver-2"

  python2 setup.py install --prefix=/usr --root="$pkgdir" --optimize=1 \
    --skip-build
}

package_python-pymisp() {
  depends=('python' 'python-six' 'python-dateutil' 'python-jsonschema')

  cd "$_pkgname-$pkgver"

  python setup.py install --prefix=/usr --root="$pkgdir" --optimize=1 \
    --skip-build
}

