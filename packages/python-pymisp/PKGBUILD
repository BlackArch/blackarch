# This file is part of BlackArch Linux ( https://www.blackarch.org/ ).
# See COPYING for license details.

pkgbase=python-pymisp
pkgname=('python2-pymisp' 'python-pymisp')
_pkgname=pymisp
pkgver=2.4.124
pkgrel=1
pkgdesc='Python API for MISP.'
arch=('any')
url='https://pypi.org/project/pymisp/#files'
license=('BSD')
makedepends=('python2-setuptools' 'python-setuptools')
source=("https://files.pythonhosted.org/packages/36/e6/bbd0947a75336d244a814b8097b098b2766b0f3a0e7a4a461972d07cb862/$_pkgname-$pkgver.tar.gz")
sha512sums=('8895941883077fd2490e3a1749e977cb309d8a2e6d9899df5fac05b116e9fdb1003d3648fed3e15efca63626cd42e5aa8530698a2722ce2b85825459331b1946')

prepare() {
  cp -a "$_pkgname-$pkgver"{,-2}
}

build() {
  cd "$_pkgname-$pkgver-2"

  python2 setup.py build

  cd "$srcdir/$_pkgname-$pkgver"

  python setup.py build
}

package_python2-pymisp() {
  depends=('python2' 'python2-six' 'python2-dateutil' 'python2-enum34'
           'python2-functools32' 'python2-jsonschema')

  cd "$_pkgname-$pkgver-2"

  python2 setup.py install --prefix=/usr --root="$pkgdir" --optimize=1 \
    --skip-build
}

package_python-pymisp() {
  depends=('python' 'python-six' 'python-dateutil' 'python-jsonschema')

  cd "$_pkgname-$pkgver"

  python setup.py install --prefix=/usr --root="$pkgdir" --optimize=1 \
    --skip-build
}

