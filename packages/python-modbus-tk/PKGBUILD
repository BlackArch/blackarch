# This file is part of BlackArch Linux ( https://www.blackarch.org/ ).
# See COPYING for license details.

pkgbase=python-modbus-tk
pkgname=('python2-modbus-tk' 'python-modbus-tk')
_pkgname=modbus_tk
pkgver=1.1.4
pkgrel=1
epoch=1
pkgdesc='Implementation of modbus protocol in Python.'
url='https://pypi.org/project/modbus_tk/#files'
license=('LGPL')
arch=('any')
provides=('modbus-tk' 'python2-modbus-tk' 'python-modbus-tk')
makedepends=('python2-setuptools' 'python-setuptools')
source=("https://files.pythonhosted.org/packages/source/${_pkgname::1}/$_pkgname/modbus_tk-$pkgver.tar.gz")
sha512sums=('3a339f8a0d63fcd8da92431d921117458654713703e0115d627492e4c614d3532ef891c05be6fb47421b34b033bcc93882e1194fc4d2a4dbb82050ace17654bd')

prepare() {
  cp -a "$_pkgname-$pkgver"{,-2}
}

build() {
  cd "$_pkgname-$pkgver-2"

  python2 setup.py build

  cd "$srcdir/$_pkgname-$pkgver"

  python setup.py build
}

package_python2-modbus-tk() {
  depends=('python2' 'python2-pyserial')

  cd "$_pkgname-$pkgver-2"

  python2 setup.py install --root="$pkgdir" --prefix=/usr -O1 --skip-build
}

package_python-modbus-tk() {
  depends=('python' 'python-pyserial')

  cd "$_pkgname-$pkgver"

  python setup.py install --root="$pkgdir" --prefix=/usr -O1 --skip-build
}

