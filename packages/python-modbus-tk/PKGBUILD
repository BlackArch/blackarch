# This file is part of BlackArch Linux ( https://www.blackarch.org/ ).
# See COPYING for license details.

pkgbase=python-modbus-tk
pkgname=('python2-modbus-tk' 'python-modbus-tk')
_pkgname=modbus_tk
pkgver=1.0.0
pkgrel=1
epoch=1
pkgdesc='Implementation of modbus protocol in Python.'
url='https://pypi.org/project/modbus_tk/#files'
license=('LGPL')
arch=('any')
provides=('modbus-tk' 'python2-modbus-tk' 'python-modbus-tk')
makedepends=('python2-setuptools' 'python-setuptools')
source=("https://files.pythonhosted.org/packages/8b/03/50193a66aac4eeccbaecea22044de6f07f4e8bfcb6ec37d724a0bba50f46/modbus_tk-1.0.0.tar.gz")
sha512sums=('5818829b6bc788a6bb34401811cedfe8422c0436cda0011f2ce076a65bb33cbb74b3442b34f7738eb10cd4e407a5c76a2e441f574cd50d2237b56c0d130bc999')

prepare() {
  cp -a "$_pkgname-$pkgver"{,-2}
}

build() {
  cd "$_pkgname-$pkgver-2"

  python2 setup.py build

  cd "$srcdir/$_pkgname-$pkgver"

  python setup.py build
}

package_python2-modbus-tk() {
  depends=('python2' 'python2-pyserial')

  cd "$_pkgname-$pkgver-2"

  python2 setup.py install --prefix=/usr --root="$pkgdir" --optimize=1 \
    --skip-build
}

package_python-modbus-tk() {
  depends=('python' 'python-serial')

  cd "$_pkgname-$pkgver"

  python setup.py install --prefix=/usr --root="$pkgdir" --optimize=1 \
    --skip-build
}

