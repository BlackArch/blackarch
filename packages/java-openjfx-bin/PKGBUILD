# This file is part of BlackArch Linux ( https://www.blackarch.org/ ).
# See COPYING for license details.
#
# Initial author: Mohammadreza Abdollahzadeh <morealaz at gmail dot com>
# Modified: noptrix at blackarch d0t org

pkgname=java-openjfx-bin
_ver=17
pkgver=17
pkgrel=1
pkgdesc="Java OpenJFX ${_ver} client application platform (Gluon build)."
arch=('x86_64')
url='https://openjfx.io/'
license=('GPL2')
depends=('ffmpeg' 'gstreamer' "java-environment>=$_ver" 'libxtst' 'qt5-base'
         'webkit2gtk')
provides=("java-openjfx=$_ver")
conflicts=("java-openjfx=$_ver" "java${_ver}-openjfx")
source=("https://download2.gluonhq.com/openjfx/$pkgver/openjfx-${pkgver}_linux-x64_bin-sdk.zip"
        "https://download2.gluonhq.com/openjfx/$pkgver/openjfx-${pkgver}_linux-x64_bin-jmods.zip")
sha512sums=('6f2c0284cc65e0417c82df6f812d23a033390c95683a7435625ee77ea6fac9262c7566725a34343f08fdec4ee9d829fa36de90169303cf8d09d47d27345bed5d'
            '313ceb3b5e87fbf9a904b0f825b4390ab07c072a5bedeb9795406fae5ff36c6abb2a41b0ee6aea62a6bd1c417f872ab430393fd9e5314ab15717d841db3f83cc')

package() {
  _jvmdir="usr/lib/jvm/java-${_ver}-openjdk"

  install -dm 750 "$pkgdir/$_jvmdir"
  install -dm 750 "$pkgdir/$_jvmdir/jmods"
  install -dm 750 "$pkgdir/usr/share/licenses/java$_ver-openjdk"

  cp -a "javafx-sdk-$pkgver/lib" "$pkgdir/$_jvmdir/"
  mv "$pkgdir/$_jvmdir/lib/src.zip" "$pkgdir/$_jvmdir/lib/javafx-src.zip"
  cp -a "javafx-sdk-$pkgver/legal" "$pkgdir/usr/share/licenses/java$_ver-openjdk/"
  cp -a "javafx-jmods-$pkgver/"* "$pkgdir/$_jvmdir/jmods/"
}

