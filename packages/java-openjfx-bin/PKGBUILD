# This file is part of BlackArch Linux ( https://www.blackarch.org/ ).
# See COPYING for license details.
#
# Initial author: Mohammadreza Abdollahzadeh <morealaz at gmail dot com>
# Modified: noptrix at blackarch d0t org

pkgname=java-openjfx-bin
_ver=16
pkgver=16
pkgrel=1
pkgdesc="Java OpenJFX ${_ver} client application platform (Gluon build)."
arch=('x86_64')
url='https://openjfx.io/'
license=('GPL2')
depends=('ffmpeg' 'gstreamer' "java-environment>=$_ver" 'libxtst' 'qt5-base'
         'webkit2gtk')
provides=("java-openjfx=$_ver")
conflicts=("java-openjfx=$_ver" "java${_ver}-openjfx")
source=("https://download2.gluonhq.com/openjfx/$pkgver/openjfx-${pkgver}_linux-x64_bin-sdk.zip"
        "https://download2.gluonhq.com/openjfx/$pkgver/openjfx-${pkgver}_linux-x64_bin-jmods.zip")
sha512sums=('e366fdc3464f5c735a060af9f5ae199b0d78b7788bc39359586a45065a774a72a6d98724ce00a5ca5f1ece9e28c594c89300bc780bb577578f635188b61e6348'
            '690fa2edb690dcc496b80bc45f74641a04f02aaec36fde8bbdc4474d1e22b2f2d1d8dd9f7d77f257b8e04a9dd2b3a07aabea4bb7d99dd70f6da24826f02bd0a3')

package() {
  _jvmdir="usr/lib/jvm/java-${_ver}-openjdk"

  install -dm 750 "$pkgdir/$_jvmdir"
  install -dm 750 "$pkgdir/$_jvmdir/jmods"
  install -dm 750 "$pkgdir/usr/share/licenses/java$_ver-openjdk"

  cp -a "javafx-sdk-$pkgver/lib" "$pkgdir/$_jvmdir/"
  mv "$pkgdir/$_jvmdir/lib/src.zip" "$pkgdir/$_jvmdir/lib/javafx-src.zip"
  cp -a "javafx-sdk-$pkgver/legal" "$pkgdir/usr/share/licenses/java$_ver-openjdk/"
  cp -a "javafx-jmods-$pkgver/"* "$pkgdir/$_jvmdir/jmods/"
}

