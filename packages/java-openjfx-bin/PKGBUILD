# This file is part of BlackArch Linux ( https://www.blackarch.org/ ).
# See COPYING for license details.
# Initial author: Mohammadreza Abdollahzadeh <morealaz at gmail dot com>
# Modified: noptrix at blackarch d0t org

pkgname=java-openjfx-bin
_ver=13
pkgver=13
pkgrel=3
pkgdesc="Java OpenJFX ${_ver} client application platform (Gluon build)."
arch=('x86_64')
url='https://openjfx.io/'
license=('GPL2')
depends=('ffmpeg' 'gstreamer' "java-environment>=$_ver" 'libxtst' 'qt5-base'
         'webkit2gtk')
provides=("java-openjfx=$_ver")
conflicts=("java-openjfx=$_ver" "java${_ver}-openjfx")
source=("https://download2.gluonhq.com/openjfx/$pkgver/openjfx-${pkgver}_linux-x64_bin-sdk.zip"
        "https://download2.gluonhq.com/openjfx/$pkgver/openjfx-${pkgver}_linux-x64_bin-jmods.zip")
sha512sums=('3a781d39b9f01a291707bf0cd1dfe09f765b6765d53e8563d0663e6ced0e6d8cb189ac8475b381a53200480ce798f174e875710d1d158eb8b00a57d211e34b99'
            '10d00c96db49feb6df174824eb7e41da36096ae83423ca3ef035b75d1d6a1a749c106391231446d1a649e58a69a0301d3431a633d37fcf4845dc8e41afd8395b')

package() {
  _jvmdir="usr/lib/jvm/java-${_ver}-openjdk"

  install -dm 750 "$pkgdir/$_jvmdir"
  install -dm 750 "$pkgdir/$_jvmdir/jmods"
  install -dm 750 "$pkgdir/usr/share/licenses/java$_ver-openjdk"

  cp -a "javafx-sdk-$pkgver/lib" "$pkgdir/$_jvmdir/"
  mv "$pkgdir/$_jvmdir/lib/src.zip" "$pkgdir/$_jvmdir/lib/javafx-src.zip"
  cp -a "javafx-sdk-$pkgver/legal" "$pkgdir/usr/share/licenses/java$_ver-openjdk/"
  cp -a "javafx-jmods-$pkgver/"* "$pkgdir/$_jvmdir/jmods/"
}

