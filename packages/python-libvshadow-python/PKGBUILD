# This file is part of BlackArch Linux ( https://www.blackarch.org/ ).
# See COPYING for license details.

pkgbase=python-libvshadow-python
pkgname=('python2-libvshadow-python' 'python-libvshadow-python')
_pkgname=libvshadow
pkgver=20210425
pkgrel=1
pkgdesc='Library and tools to access the Volume Shadow Snapshot (VSS) format.'
arch=('x86_64' 'aarch64')
url='https://pypi.org/project/libvshadow-python/#files'
license=('LGPL3')
makedepends=('python2-setuptools' 'python-setuptools')
source=("https://files.pythonhosted.org/packages/2a/18/b3d80d7f10369205caf0eb746495ebaaec6f4c7f92f4d62aa4e0b3638d25/$_pkgname-$pkgver.tar.gz")
sha512sums=('8493d4aa60b2134f355a8bfba84cd0420b33b2e56b39cfd1fea67888a9470f3984f713ddf48091633c41a5c5171a912a9b940774a02a3d9103f25b839c73a1ea')

prepare() {
  cp -a "$_pkgname-$pkgver"{,-2}
}

build() {
  cd "$_pkgname-$pkgver-2"

  python2 setup.py build

  cd "$srcdir/$_pkgname-$pkgver"

  python setup.py build
}

package_python2-libvshadow-python() {
  depends=('python2')
  cd "$_pkgname-$pkgver"

  python2 setup.py install --root="$pkgdir" --prefix=/usr -O1 --skip-build
}

package_python-libvshadow-python() {
  depends=('python')

  cd "$_pkgname-$pkgver"

  python setup.py install --root="$pkgdir" --prefix=/usr -O1 --skip-build
}

