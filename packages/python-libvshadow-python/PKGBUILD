# This file is part of BlackArch Linux ( https://www.blackarch.org/ ).
# See COPYING for license details.

pkgbase=python-libvshadow-python
pkgname=('python2-libvshadow-python' 'python-libvshadow-python')
_pkgname=libvshadow
pkgver=20211114
pkgrel=1
pkgdesc='Library and tools to access the Volume Shadow Snapshot (VSS) format.'
arch=('x86_64' 'aarch64')
url='https://pypi.org/project/libvshadow-python/#files'
license=('LGPL3')
makedepends=('python2-setuptools' 'python-setuptools')
source=("https://files.pythonhosted.org/packages/bb/62/4d6713f9c6ea755a4a75ca0e00784e874e88802d69cd923fec17a01c06f0/$_pkgname-$pkgver.tar.gz")
sha512sums=('79062923c0bbdfa1327dd21d5ee6fc314d5f37c1f7a1831c6bf0d872322aba436958503e0dd6df65dfb1be48322103790de6e17713cb43a6c2939c49a064dd82')

prepare() {
  cp -a "$_pkgname-$pkgver"{,-2}
}

build() {
  cd "$_pkgname-$pkgver-2"

  python2 setup.py build

  cd "$srcdir/$_pkgname-$pkgver"

  python setup.py build
}

package_python2-libvshadow-python() {
  depends=('python2')
  cd "$_pkgname-$pkgver"

  python2 setup.py install --root="$pkgdir" --prefix=/usr -O1 --skip-build
}

package_python-libvshadow-python() {
  depends=('python')

  cd "$_pkgname-$pkgver"

  python setup.py install --root="$pkgdir" --prefix=/usr -O1 --skip-build
}

