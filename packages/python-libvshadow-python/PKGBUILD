# This file is part of BlackArch Linux ( https://www.blackarch.org/ ).
# See COPYING for license details.

pkgbase=python-libvshadow-python
pkgname=('python2-libvshadow-python' 'python-libvshadow-python')
_pkgname=libvshadow
pkgver=20191221
pkgrel=2
pkgdesc='Library and tools to access the Volume Shadow Snapshot (VSS) format.'
arch=('x86_64' 'aarch64')
url='https://pypi.org/project/libvshadow-python/#files'
license=('LGPL3')
makedepends=('python2-setuptools' 'python-setuptools')
source=("https://files.pythonhosted.org/packages/42/11/365087ea337f541835c8673ef29072e97dd1c2419c1178e9c97a195fce49/$_pkgname-python-$pkgver.tar.gz")
sha512sums=('7e9157f95449a3de26ec4cfbbe9574d9855b01aa5cd110b424bb7d8ad3e010ebb28dbeee3337e1ae52afecf603d0194a06ca2438fe4461d1f314541701756d22')

prepare() {
  cp -a "$_pkgname-$pkgver"{,-2}
}

build() {
  cd "$_pkgname-$pkgver-2"

  python2 setup.py build

  cd "$srcdir/$_pkgname-$pkgver"

  python setup.py build
}

package_python2-libvshadow-python() {
  depends=('python2')
  cd "$_pkgname-$pkgver"

  python2 setup.py install --root="$pkgdir" --prefix=/usr -O1 --skip-build
}

package_python-libvshadow-python() {
  depends=('python')

  cd "$_pkgname-$pkgver"

  python setup.py install --root="$pkgdir" --prefix=/usr -O1 --skip-build
}

