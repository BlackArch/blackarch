# This file is part of BlackArch Linux ( http://blackarch.org ).
# See COPYING for license details.

pkgname='sagan'
pkgver='1.0.0'
pkgrel=1
pkgdesc='A snort-like log analysis engine.'
arch=('i686' 'x86_64' 'armv6h' 'armv7h')
groups=('blackarch' 'blackarch-ids' 'blackarch-defensive')
url='https://quadrantsec.com/sagan_log_analysis_engine/'
license=('GPL')
depends=('sagan-rules' 'pcre' 'libdnet' 'libesmtp' 'mysql-clients'
         'postgresql-libs' 'liblognorm' 'geoip' 'json-c')
makedepends=('git')
options=('!makeflags' '!emptydirs')
conflicts=('sagan' 'sagan-svn')
replaces=('sagan-svn')
source=("https://github.com/beave/sagan/archive/sagan-${pkgver}.tar.gz")
sha1sums=('05525e3167eb10234ea37f0e5f7feb851e6ecb3c')

build() {
  cd "$srcdir/sagan-sagan-$pkgver"

  ./configure --enable-esmtp --enable-geoip --prefix=/usr --sysconfdir=/etc \
    --sbindir=/usr/bin

  make

  sed "s:^var RULE_PATH.*$:var RULE_PATH /usr/share/sagan-git/rules/:" \
    etc/sagan.conf > etc/sagan.conf.new

  mv etc/sagan.conf.new etc/sagan.conf
}

package() {
  cd "$srcdir/sagan-sagan-$pkgver"

  make DESTDIR="$pkgdir" install
}
