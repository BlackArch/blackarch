# This file is part of BlackArch Linux ( https://www.blackarch.org/ ).
# See COPYING for license details.

pkgname=sagan
pkgver=1.2.1
pkgrel=1
pkgdesc='A snort-like log analysis engine.'
arch=('x86_64' 'armv6h' 'armv7h' 'aarch64')
groups=('blackarch' 'blackarch-ids' 'blackarch-defensive')
url='https://quadrantsec.com/sagan_log_analysis_engine/'
license=('GPL')
depends=('sagan-rules' 'pcre' 'libdnet' 'libesmtp' 'mysql-clients'
         'postgresql-libs' 'liblognorm' 'geoip' 'json-c' 'libfastjson'
         'libyaml')
makedepends=('git')
options=('!makeflags' '!emptydirs')
conflicts=('sagan' 'sagan-svn')
replaces=('sagan-svn')
source=("https://quadrantsec.com/download/$pkgname-$pkgver.tar.gz")
sha512sums=('20bc16c9a6d02df85321b94d2b65877d02778e23db4c36883a0236dad57cb6fc331e8901870e662fa545605d4f1550115243414e275225aa6f956c6b55fd71ec')

build() {
  cd "$pkgname-$pkgver"

  ./autogen.sh

  ./configure --enable-esmtp --enable-geoip --prefix=/usr --sysconfdir=/etc \
    --sbindir=/usr/bin --enable-system-strstr

  make

  sed "s:^var RULE_PATH.*$:var RULE_PATH /usr/share/sagan/rules/:" \
    etc/sagan.yaml > etc/sagan.yaml.new

  mv etc/sagan.yaml.new etc/sagan.yaml
}

package() {
  cd "$pkgname-$pkgver"

  make DESTDIR="$pkgdir" install
}

