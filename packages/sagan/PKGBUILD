# This file is part of BlackArch Linux ( http://blackarch.org ).
# See COPYING for license details.

pkgname='sagan'
pkgver='1.1.0'
pkgrel=2
pkgdesc='A snort-like log analysis engine.'
arch=('x86_64' 'armv6h' 'armv7h' 'aarch64')
groups=('blackarch' 'blackarch-ids' 'blackarch-defensive')
url='https://quadrantsec.com/sagan_log_analysis_engine/'
license=('GPL')
depends=('sagan-rules' 'pcre' 'libdnet' 'libesmtp' 'mysql-clients'
         'postgresql-libs' 'liblognorm' 'geoip' 'json-c' 'libfastjson')
makedepends=('git')
options=('!makeflags' '!emptydirs')
conflicts=('sagan' 'sagan-svn')
replaces=('sagan-svn')
source=("https://github.com/beave/sagan/archive/sagan-${pkgver}.tar.gz")
sha1sums=('a2ac6cc588ce8eecf18fd5f674d20938fb6ec405')

build() {
  cd "$srcdir/sagan-sagan-$pkgver"

  ./autogen.sh

  ./configure --enable-esmtp --enable-geoip --prefix=/usr --sysconfdir=/etc \
    --sbindir=/usr/bin

  make

  sed "s:^var RULE_PATH.*$:var RULE_PATH /usr/share/sagan-git/rules/:" \
    etc/sagan.conf > etc/sagan.conf.new

  mv etc/sagan.conf.new etc/sagan.conf
}

package() {
  cd "$srcdir/sagan-sagan-$pkgver"

  make DESTDIR="$pkgdir" install
}
