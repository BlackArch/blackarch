pkgname=bmap-tools
pkgver=3.1
pkgrel=1
pkgdesc="Tool for copying largely sparse files using information from a block map file"
arch=('any')
url="http://git.infradead.org/users/dedekind/bmap-tools.git"
license=('GPL2')
depends=('python2')
makedepends=('git' 'python2-setuptools')
source=(git://git.infradead.org/users/dedekind/bmap-tools.git)
sha256sums=('SKIP')

pkgver() {
  cd "$srcdir/bmap-tools"

  awk '/^VERSION =/ { print $3 }' bmaptool | sed 's/"\(.*\)"/\1/'
}

build() {
  cd "$srcdir/bmap-tools"

  python2 setup.py build
}

package() {
  cd "$srcdir/bmap-tools"

  python2 setup.py install --root=$pkgdir/ --optimize=1
  install -Dm644 docs/man1/bmaptool.1 "$pkgdir"/usr/share/man/man1/bmaptool.1
}
