# This file is part of BlackArch Linux ( http://blackarch.org ).
# See COPYING for license details.

pkgname=wifiscanmap
pkgver=135.9adcd08
pkgrel=2
pkgdesc='Another wifi mapping tool.'
groups=('blackarch' 'blackarch-wireless' 'blackarch-scanner')
arch=('any')
url='https://github.com/mehdilauters/wifiScanMap'
license=('GPL3')
depends=('python2' 'python2-pysqlite' 'python2-rpi-gpio' 'python2-scapy' 'aircrack-ng'
         'gpsd' 'bluez' 'bower')
makedepends=('git')
source=('git+https://github.com/mehdilauters/wifiScanMap.git')
sha512sums=('SKIP')

pkgver() {
  cd "$srcdir/wifiScanMap"

  echo $(git rev-list --count HEAD).$(git rev-parse --short HEAD)
}

package() {
  cd "$srcdir/wifiScanMap"

  install -dm 755 "$pkgdir/usr/bin"
  install -dm 755 "$pkgdir/usr/share/wifiscanmap"

  install -Dm644 README.md "$pkgdir/usr/share/doc/wifiscanmap/README.md"

  rm README.md

  cp -a * "$pkgdir/usr/share/wifiscanmap/"

  cat > "$pkgdir/usr/bin/wifiscanmap" << EOF
#!/bin/sh
cd /usr/share/wifiscanmap
exec python2 scanmap.py "\$@"
EOF

  chmod a+x "$pkgdir/usr/bin/wifiscanmap"
}
