# This file is part of BlackArch Linux ( https://www.blackarch.org/ ).
# See COPYING for license details.

pkgname=dsjs
pkgver=23.450a7f4
pkgrel=1
groups=('blackarch' 'blackarch-webapp' 'blackarch-scanner')
pkgdesc='A fully functional JavaScript library vulnerability scanner written in under 100 lines of code.'
arch=('any')
url='https://github.com/stamparm/DSJS'
license=('custom:unknown')
depends=('python2' 'python2-simplejson')
makedepends=('git')
source=('git+https://github.com/stamparm/DSJS.git')
sha512sums=('SKIP')

pkgver() {
  cd DSJS

  echo $(git rev-list --count HEAD).$(git rev-parse --short HEAD)
}

prepare() {
  cd DSJS

  sed -i 's|bin/env python|bin/env python2|' dsjs.py
}

package() {
  cd DSJS

  install -Dm 755 dsjs.py "$pkgdir/usr/bin/$pkgname"
  install -Dm 644 -t "$pkgdir/usr/share/doc/$pkgname/" README.md
}

