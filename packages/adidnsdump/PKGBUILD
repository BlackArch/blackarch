# This file is part of BlackArch Linux ( https://www.blackarch.org/ ).
# See COPYING for license details.

pkgname=adidnsdump
pkgver=v1.4.0.r1.g95adaf8
pkgrel=1
epoch=1
pkgdesc='Active Directory Integrated DNS dumping by any authenticated user.'
groups=('blackarch' 'blackarch-recon')
arch=('any')
url='https://github.com/dirkjanm/adidnsdump'
license=('custom:unknown')
depends=('python' 'python-ldap3' 'python-dnspython' 'python-future' 'impacket')
makedepends=('git' 'python-setuptools')
source=("$pkgname::git+https://github.com/dirkjanm/$pkgname.git")
sha512sums=('SKIP')

pkgver() {
  cd $pkgname

  ( set -o pipefail
    git describe --long --tags --abbrev=7 2>/dev/null |
      sed 's/\([^-]*-g\)/r\1/;s/-/./g' ||
    printf "%s.%s" "$(git rev-list --count HEAD)" \
      "$(git rev-parse --short=7 HEAD)"
  )
}

build() {
  cd $pkgname

  python setup.py build
}

package() {
  cd $pkgname

  python setup.py install --root="$pkgdir" --prefix=/usr -O1 --skip-build

  install -Dm 644 -t "$pkgdir/usr/share/doc/$pkgname/" README.md
}

