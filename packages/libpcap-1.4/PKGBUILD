# This file is part of BlackArch Linux ( https://www.blackarch.org/ ).
# See COPYING for license details.

pkgname=libpcap-1.4
_pkgname=libpcap
pkgver=1.4.0
pkgrel=4
pkgdesc="A system-independent interface for user-level packet capture"
arch=('x86_64' 'aarch64')
url="http://www.tcpdump.org/"
license=('BSD')
depends=('glibc' 'libnl' 'sh' 'libusb')
makedepends=('flex')
source=(http://www.tcpdump.org/release/libpcap-$pkgver.tar.gz)
sha512sums=('b7662867c6449c603a65c37e63ce881260dee80989ccb5b71b1e55fa437fb18ac9bb123a7b8ed68302baf4511b97bca40d9f08dea7b0dd0eb6a2ac4a7b73cf15')

build() {
  cd "$_pkgname-$pkgver"

  ./configure --prefix=/usr --enable-ipv6 --with-libnl

  sed -i 's/libpcap\.so/libpcap-1.4.so/g' Makefile
  sed -i 's/^shared: libpcap/shared: libpcap-1.4/' Makefile

  make
}

package() {
  cd "$_pkgname-$pkgver"

  install -dm 755 "$pkgdir/usr/bin"

  make DESTDIR="$pkgdir" install

  # backwards compatibility, programs often look for net/bpf.h
  install -dm 755 "$pkgdir/usr/include/net"

  cd "$pkgdir/usr/include/net"

  ln -s ../pcap-bpf.h bpf.h

  # install the license
  #install -D -m644 ${srcdir}/libpcap-$pkgver/LICENSE ${pkgdir}/usr/share/licenses/libpcap/LICENSE

  # Avoid conflicting with normal libpcap
  rm -rf "$pkgdir/usr/share"
  rm -rf "$pkgdir/usr/bin"
  rm -rf "$pkgdir/usr/include"
}

