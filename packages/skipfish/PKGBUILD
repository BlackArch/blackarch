pkgname=skipfish
pkgver=2.10b
groups=('blackarch' 'blackarch-web')
pkgrel=4
pkgdesc="A fully automated, active web application security reconnaissance tool"
arch=('any')
license=('Apache')
url='http://code.google.com/p/skipfish/'
depends=('openssl' 'libidn')
source=("http://skipfish.googlecode.com/files/skipfish-$pkgver.tgz"
        "skipfish.patch")
md5sums=('8edf6092f3d1835cb4a381b7a723db8a'
         '640b24c2989f2316ac5e0926bba3b226')

build() {
  cd "$srcdir/skipfish-$pkgver"

  # Patch fixes application run directory system error
  patch -Np0 -i ../skipfish.patch
  make
}

package() {
  cd "$srcdir/skipfish-$pkgver"

  install -dm 0755 "$pkgdir/usr/share/skipfish"
  install -dm 0755 "$pkgdir/usr/bin"

  install -dm 0755 "$pkgdir/usr/share/skipfish/dictionaries"
  install -Dm 0644 dictionaries/* "$pkgdir/usr/share/skipfish/dictionaries"

  install -dm 0755 assets "$pkgdir/usr/share/skipfish/assets"
  install -Dm 0644 assets/* "$pkgdir/usr/share/skipfish/assets/"

  install -dm 0755 "$pkgdir/usr/share/skipfish/signatures"
  install -Dm 0644 signatures/* "$pkgdir/usr/share/skipfish/signatures/"

  install -Dm 0755 skipfish tools/* "$pkgdir/usr/bin/"
  install -Dm 0644 doc/skipfish.1 "$pkgdir/usr/share/man/man1/skipfish.1"
}
