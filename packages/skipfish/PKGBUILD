# This file is part of BlackArch Linux ( http://blackarch.org ).
# See COPYING for license details.

pkgname='skipfish'
pkgver='2.10b'
pkgrel=7
groups=('blackarch' 'blackarch-fuzzer' 'blackarch-scanner' 'blackarch-webapp')
pkgdesc='A fully automated, active web application security reconnaissance tool.'
arch=('i686' 'x86_64' 'armv6h' 'armv7h')
license=('Apache')
url='http://code.google.com/p/skipfish/'
depends=('openssl' 'libidn')
source=("http://skipfish.googlecode.com/files/skipfish-${pkgver}.tgz"
        "skipfish.patch")
sha1sums=('2564162a13d02f8310eef5edcbaf74ed6043be99'
          '36b5b90f9569d364687d618c45d9ffab8b6db566')

build() {
  cd "$srcdir/skipfish-$pkgver"

  # Patch fixes application run directory system error
  patch -Np0 -i ../skipfish.patch
  make
}

package() {
  cd "$srcdir/skipfish-$pkgver"

  mkdir -p "$pkgdir/usr/share/skipfish"
  mkdir -p "$pkgdir/usr/bin"

  mkdir -p "$pkgdir/usr/share/skipfish/dictionaries"
  install -Dm 0644 dictionaries/* "$pkgdir/usr/share/skipfish/dictionaries"

  mkdir -p assets "$pkgdir/usr/share/skipfish/assets"
  install -Dm 0644 assets/* "$pkgdir/usr/share/skipfish/assets/"

  mkdir -p "$pkgdir/usr/share/skipfish/signatures"
  install -Dm 0644 signatures/* "$pkgdir/usr/share/skipfish/signatures/"

  install -Dm 0755 skipfish tools/* "$pkgdir/usr/bin/"
  install -Dm 0644 doc/skipfish.1 "$pkgdir/usr/share/man/man1/skipfish.1"
}
