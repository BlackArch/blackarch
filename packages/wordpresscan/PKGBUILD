# This file is part of BlackArch Linux ( https://www.blackarch.org/ ).
# See COPYING for license details.

pkgname=wordpresscan
pkgver=62.dd794cf
pkgrel=2
pkgdesc='WPScan rewritten in Python + some WPSeku ideas.'
groups=('blackarch' 'blackarch-scanner' 'blackarch-webapp')
arch=('any')
url='https://github.com/swisskyrepo/Wordpresscan'
license=('custom:unknown')
depends=('python2' 'python2-argparse' 'python2-requests' 'python2-lxml'
         'python2-tornado')
makedepends=('git')
source=('git+https://github.com/swisskyrepo/Wordpresscan.git')
sha512sums=('SKIP')

pkgver() {
  cd Wordpresscan

  echo $(git rev-list --count HEAD).$(git rev-parse --short HEAD)
}

package() {
  cd Wordpresscan

  install -dm 755 "$pkgdir/usr/bin"
  install -dm 755 "$pkgdir/usr/share/$pkgname"

  install -Dm 644 -t "$pkgdir/usr/share/doc/$pkgname/" README.md

  rm README.md

  cp -a * "$pkgdir/usr/share/$pkgname/"

  cat > "$pkgdir/usr/bin/$pkgname" << EOF
#!/bin/sh
cd /usr/share/$pkgname
exec python2 wordpresscan.py "\$@"
EOF

  chmod +x "$pkgdir/usr/bin/$pkgname"
}

