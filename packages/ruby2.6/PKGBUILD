# Contributor: Thomas Dziedzic <gostrc@gmail.com>
# Contributor: Allan McRae <allan@archlinux.org>
# Contributor: John Proctor <jproctor@prium.net>
# Contributor: Jeramy Rutley <jrutley@gmail.com>

pkgname=ruby2.6
_pkgname=ruby
pkgver=2.6.9
pkgrel=1
arch=('x86_64' 'aarch64')
pkgdesc='An object-oriented language for quick and easy programming, version 2.6.'
url='https://www.ruby-lang.org/en/'
license=('BSD' 'custom')
depends=('libxcrypt' 'gdbm' 'openssl' 'libffi' 'libyaml' 'gmp' 'zlib')
optdepends=('tk: for Ruby/TK')
makedepends=('tk')
options=(!emptydirs)
source=("https://cache.ruby-lang.org/pub/ruby/${pkgver:0:3}/$_pkgname-${pkgver}.tar.xz")
sha512sums=('f60aa89e685cea324185eb0d13e6b44caef4e4f761cbf9ea1386ae70e39faf3866ac01e4bb5354574f2583e74290b8c80eaf63d126040d52368be6c771476451')

build() {
  cd "$_pkgname-$pkgver"

  ./configure \
    --prefix=/usr \
    --program-suffix=-2.6 \
    --with-soname=ruby-2.6 \
    --sysconfdir=/etc \
    --localstatedir=/var \
    --sharedstatedir=/var/lib \
    --libexecdir=/usr/lib/ruby \
    --enable-shared \
    --disable-rpath \
    --with-dbm-type=gdbm_compat \
    --disable-install-doc

  make
}

package() {
  cd "$_pkgname-$pkgver"

  make DESTDIR="$pkgdir" install-nodoc

  install -Dm 644 COPYING "${pkgdir}/usr/share/licenses/ruby2.6/LICENSE"
  install -Dm 644 BSDL "${pkgdir}/usr/share/licenses/ruby2.6/BSDL"

  # remove files conflicting with 'ruby' package
  rm -r "$pkgdir"/usr/share/man/
}

