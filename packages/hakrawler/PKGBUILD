# This file is part of BlackArch Linux ( https://www.blackarch.org/ ).
# See COPYING for license details.

pkgname=hakrawler
pkgver=134.bd3d30a
pkgrel=2
groups=('blackarch' 'blackarch-webapp' 'blackarch-scanner')
pkgdesc='Simple, fast web crawler designed for easy, quick discovery of endpoints and assets within a web application.'
arch=('x86_64' 'aarch64')
url='https://github.com/hakluke/hakrawler'
license=('MIT')
makedepends=('git' 'go')
source=("git+https://github.com/hakluke/$pkgname.git")
sha512sums=('SKIP')

pkgver() {
  cd $pkgname

  echo $(git rev-list --count HEAD).$(git rev-parse --short HEAD)
}

build() {
  cd $pkgname

  GOPATH="$srcdir" go mod download
  GOPATH="$srcdir" go build \
    -trimpath \
    -buildmode=pie \
    -mod=readonly \
    -modcacherw \
    -ldflags "-s -w" \
    -o $pkgname .
}

package() {
  cd $pkgname

  install -Dm 755 $pkgname "$pkgdir/usr/bin/$pkgname"
  install -Dm 644 -t "$pkgdir/usr/share/doc/$pkgname/README.md" README.md
}

