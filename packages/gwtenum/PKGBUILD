# This file is part of BlackArch Linux ( http://blackarch.org ).
# See COPYING for license details.

pkgname='gwtenum'
pkgver=7.f27a5aa
pkgrel=5
epoch=1
pkgdesc='Enumeration of GWT-RCP method calls.'
url='http://www.gdssecurity.com/l/t/d.php?k=GwtEnum'
groups=('blackarch' 'blackarch-recon' 'blackarch-webapp')
license=('CCPL')
arch=('any')
depends=('python2')
makedepends=('git')
source=("git+https://github.com/GDSSecurity/GWT-Penetration-Testing-Toolset.git")
sha1sums=('SKIP')

pkgver() {
  cd "$srcdir/GWT-Penetration-Testing-Toolset"

  echo $(git rev-list --count HEAD).$(git rev-parse --short HEAD)
}

package() {
  cd "$srcdir/GWT-Penetration-Testing-Toolset"

  _tools="gwtenum gwtfuzzer gwtparse"

  mkdir -p "$pkgdir/usr/share/gwtenum"
  mkdir -p "$pkgdir/usr/bin"

  cp -r * "$pkgdir/usr/share/gwtenum"

  for tool in $_tools
  do
    cat > "$pkgdir/usr/bin/$tool" << EOF
#!/bin/sh
cd /usr/share/gwtenum/$tool
exec python2 $tool.py "\$@"
EOF
    chmod 755 "$pkgdir/usr/bin/$tool"
  done
}
