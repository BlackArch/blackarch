# This file is part of BlackArch Linux ( https://www.blackarch.org/ ).
# See COPYING for license details.

pkgname=yawast
pkgver=0.11.0
pkgrel=2
epoch=1
pkgdesc='The YAWAST Antecedent Web Application Security Toolkit.'
groups=('blackarch' 'blackarch-webapp' 'blackarch-scanner' 'blackarch-fuzzer')
arch=('any')
url='https://github.com/adamcaudill/yawast'
license=('BSD')
depends=('python' 'python-asn1crypto' 'python-attrs' 'python-beautifulsoup4'
         'python-certifi' 'python-cffi' 'python-chardet' 'python-colorama'
         'python-cryptography' 'python-dnspython' 'python-idna'
         'python-jsonschema' 'python-nassl' 'python-packaging' 'python-psutil'
         'python-publicsuffixlist' 'python-pycparser' 'python-pyparsing'
         'python-requests-mock' 'python-requests' 'python-selenium' 'python-six'
         'python-soupsieve' 'sslyze' 'python-tls-parser' 'python-urllib3'
         'python-validator-collection' 'python-wheel' 'python-vistir'
         'python-toml' 'python-dateutil' 'python-appdirs' 'python-distlib'
         'python-cached-property' 'python-cerberus' 'python-first'
         'python-importlib-metadata' 'python-more-itertools' 'python-pep517'
         'python-ordered-set' 'python-orderedmultidict' 'python-pip-shims'
         'python-plette' 'python-tomlkit' 'python-zipp'
         'python-requirementslib')
makedepends=('python-setuptools' 'python-requirementslib')
#source=("git+https://github.com/adamcaudill/$pkgname.git"
source=("https://files.pythonhosted.org/packages/source/${pkgname::1}/$pkgname/$pkgname-$pkgver.tar.gz"
        'setup.py.patch')
sha512sums=('a03c8621cef543cd2c6635f595cae9eed8e458eb25b941ddc4c506143f32cf19f7ae73da926fa59f832c63bcf5c0d53b9363b213d1a40abcae29fa14f678939e'
            'acabc5def734bab6bfe60834f1c762c26c1496f3f20dfd822bba31c26d56917353004a27a6779c6bf55f2ce44e4cc9e4680210d65a49a956f67b9e7c88fe8ea6')

prepare() {
  cd "$pkgname-$pkgver"

  patch -p0 < ../setup.py.patch

  touch Pipfile
}

build() {
  cd "$pkgname-$pkgver"

  python setup.py build
}

package() {
  cd "$pkgname-$pkgver"

  python setup.py install --root="$pkgdir" --prefix=/usr -O1 --skip-build

  install -Dm 644 -t "$pkgdir/usr/share/doc/$pkgname/" *.md
  install -Dm 644 LICENSE "$pkgdir/usr/share/licenses/$pkgname/LICENSE"
}

