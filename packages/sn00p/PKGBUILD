# This file is part of BlackArch Linux ( https://www.blackarch.org/ ).
# See COPYING for license details.

pkgname=sn00p
pkgver=0.8
pkgrel=9
groups=('blackarch' 'blackarch-automation' 'blackarch-scanner' 'blackarch-recon'
        'blackarch-fingerprint' 'blackarch-networking' 'blackarch-fuzzer'
        'blackarch-exploitation')
pkgdesc='A modular tool written in bourne shell and designed to chain and automate security tools and tests.'
url='http://www.nullsecurity.net/tools/automation.html'
license=('GPL3')
arch=('any')
source=("https://github.com/nullsecuritynet/tools/raw/master/automation/$pkgname/release/$pkgname-$pkgver.tar.gz")
sha512sums=('0e80a9c08af126c7357ca24bd1f4de073b7b7bc09fb2bb9d061383f4eedfd5c8d4ecb4fb4f25316391d77b55b978c81c1e75baeb553b6158f8ad94f36857403e')

prepare() {
  cd "$pkgname-$pkgver"

  sed -i 's|/home/noptrix/haxx/own/dev/|/usr/share/|' "src/$pkgname"
}

package() {
  cd "$pkgname-$pkgver"

  install -dm 755 "$pkgdir/usr/share/$pkgname"

  install -Dm 755 "src/$pkgname" "$pkgdir/usr/bin/$pkgname"
  install -Dm 644 "docs/$pkgname.1.gz" \
    "$pkgdir/usr/share/man/man1/$pkgname.1.gz"
  install -Dm 644 docs/COPYING "$pkgdir/usr/share/licenses/$pkgname/COPYING"

  rm -rf README "src/$pkgname" docs/COPYING "docs/$pkgname.1.gz"

  install -Dm 644 docs/* "$pkgdir/usr/share/doc/$pkgname/"

  rm -rf docs

  cp -a * "$pkgdir/usr/share/$pkgname"

  find "$pkgdir/usr/share/$pkgname/" -type f -exec chmod 644 {} \;
}

