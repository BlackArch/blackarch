# This file is part of BlackArch Linux ( http://blackarch.org ).
# See COPYING for license details.

pkgname=sn00p
pkgver=0.8
pkgrel=9
groups=('blackarch' 'blackarch-automation' 'blackarch-scanner' 'blackarch-recon'
        'blackarch-fingerprint' 'blackarch-networking' 'blackarch-fuzzer'
        'blackarch-exploitation')
pkgdesc='A modular tool written in bourne shell and designed to chain and automate security tools and tests.'
url='http://www.nullsecurity.net/tools/automation.html'
license=('GPL3')
arch=('any')
source=("https://github.com/nullsecuritynet/tools/raw/master/automation/sn00p/release/sn00p-${pkgver}.tar.gz")
sha512sums=('0e80a9c08af126c7357ca24bd1f4de073b7b7bc09fb2bb9d061383f4eedfd5c8d4ecb4fb4f25316391d77b55b978c81c1e75baeb553b6158f8ad94f36857403e')

prepare() {
  cd "$srcdir/sn00p-$pkgver"

  sed -i 's|/home/noptrix/haxx/own/dev/|/usr/share/|' src/sn00p
}

package() {
  cd "$srcdir/sn00p-$pkgver"

  install -dm 755 "$pkgdir/usr/share/sn00p"
  install -dm 755 "$pkgdir/usr/share/doc/sn00p"

  install -Dm755 src/sn00p "$pkgdir/usr/bin/sn00p"
  install -Dm644 docs/sn00p.1.gz "$pkgdir/usr/share/man/man1/sn00p.1.gz"
  install -Dm644 docs/COPYING "$pkgdir/usr/share/licenses/sn00p/COPYING"

  rm -rf README src/sn00p docs/COPYING docs/sn00p.1.gz

  install -Dm644 docs/* "$pkgdir/usr/share/doc/sn00p/"

  rm -rf docs

  cp -a * "$pkgdir/usr/share/sn00p"

  find "$pkgdir"/usr/share/sn00p/ -type f -exec chmod 644 {} \;
}
