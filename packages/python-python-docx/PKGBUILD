# This file is part of BlackArch Linux ( https://www.blackarch.org/ ).
# See COPYING for license details.

pkgbase=python-python-docx
pkgname=('python2-python-docx' 'python-python-docx')
_pkgname=python-docx
pkgver=0.8.10
pkgrel=4
pkgdesc='Create and update Microsoft Word .docx files.'
arch=('any')
url='https://pypi.org/project/python-docx/#files'
license=('MIT')
makedepends=('python2-setuptools' 'python-setuptools')
source=("https://files.pythonhosted.org/packages/e4/83/c66a1934ed5ed8ab1dbb9931f1779079f8bca0f6bbc5793c06c4b5e7d671/$_pkgname-$pkgver.tar.gz")
sha512sums=('96c9212e2b2d6a81832e4026490f97d545fbd2572a6f7e9f201c76a83cd5f89d0284193c8f71daa61b748e3da889ec4afc7459f2c237acfb5bc47475f8abe4ae')

prepare() {
  cp -a "$_pkgname-$pkgver"{,-2}
}

build() {
  cd "$_pkgname-$pkgver-2"

  python2 setup.py build

  cd "$srcdir/$_pkgname-$pkgver"

  python setup.py build
}

package_python2-python-docx() {
  depends=('python2' 'python2-lxml')

  cd "$_pkgname-$pkgver-2"

  python2 setup.py install --prefix=/usr --root="$pkgdir" --optimize=1 \
    --skip-build
}

package_python-python-docx() {
  depends=('python' 'python-lxml')

  cd "$_pkgname-$pkgver"

  python setup.py install --prefix=/usr --root="$pkgdir" --optimize=1 \
    --skip-build
}

