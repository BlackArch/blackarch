# This file is part of BlackArch Linux ( https://www.blackarch.org/ ).
# See COPYING for license details.

pkgbase=python-python-docx
pkgname=('python2-python-docx' 'python-python-docx')
_pkgname=python-docx
pkgver=1.1.2
pkgrel=1
pkgdesc='Create and update Microsoft Word .docx files.'
arch=('any')
url='https://pypi.org/project/python-docx/#files'
license=('MIT')
makedepends=('python2-setuptools' 'python-setuptools')
source=("https://files.pythonhosted.org/packages/source/${_pkgname::1}/$_pkgname/$_pkgname-$pkgver.tar.gz")
sha512sums=('3085a5c82920482519aa87ce8280766440063b1713c3bc2e0591ea02b3613480d590e12b0720a710e368d82a6cd0b430ff786970ac6282aa9b39116463c148dc')

prepare() {
  cp -a "$_pkgname-$pkgver"{,-2}
}

build() {
  cd "$_pkgname-$pkgver-2"

  python2 setup.py build

  cd "$srcdir/$_pkgname-$pkgver"

  python setup.py build
}

package_python2-python-docx() {
  depends=('python2' 'python2-lxml')

  cd "$_pkgname-$pkgver-2"

  python2 setup.py install --root="$pkgdir" --prefix=/usr -O1 --skip-build
}

package_python-python-docx() {
  depends=('python' 'python-lxml')

  cd "$_pkgname-$pkgver"

  python setup.py install --root="$pkgdir" --prefix=/usr -O1 --skip-build
}

