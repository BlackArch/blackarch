# This file is part of BlackArch Linux ( https://www.blackarch.org/ ).
# See COPYING for license details.

pkgbase=python-python-docx
pkgname=('python2-python-docx' 'python-python-docx')
_pkgname=python-docx
pkgver=0.8.11
pkgrel=2
pkgdesc='Create and update Microsoft Word .docx files.'
arch=('any')
url='https://pypi.org/project/python-docx/#files'
license=('MIT')
makedepends=('python2-setuptools' 'python-setuptools')
source=("https://files.pythonhosted.org/packages/8b/a0/52729ce4aa026f31b74cc877be1d11e4ddeaa361dc7aebec148171644b33/$_pkgname-$pkgver.tar.gz")
sha512sums=('6e32c75aa28da779f548dfe6902f95299aaee1936d7ce463742b2d1c4374148e947a4fcc7efd785696b2bcf4d7faaa4f769e88c91e8ec7c6e048d6b1fe67d61e')

prepare() {
  cp -a "$_pkgname-$pkgver"{,-2}
}

build() {
  cd "$_pkgname-$pkgver-2"

  python2 setup.py build

  cd "$srcdir/$_pkgname-$pkgver"

  python setup.py build
}

package_python2-python-docx() {
  depends=('python2' 'python2-lxml')

  cd "$_pkgname-$pkgver-2"

  python2 setup.py install --prefix=/usr --root="$pkgdir" --optimize=1 \
    --skip-build
}

package_python-python-docx() {
  depends=('python' 'python-lxml')

  cd "$_pkgname-$pkgver"

  python setup.py install --prefix=/usr --root="$pkgdir" --optimize=1 \
    --skip-build
}

