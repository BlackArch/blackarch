# This file is part of BlackArch Linux ( https://www.blackarch.org/ ).
# See COPYING for license details.

pkgname=python2-pexpect
_pkgname=pexpect
pkgver=4.7.0
pkgrel=1
pkgdesc='Allows easy control of interactive console applications.'
url='https://pypi.org/project/pexpect/#files'
arch=('any')
license=('MIT')
depends=('python2' 'python2-ptyprocess')
makedepends=('python2-setuptools')
source=("https://files.pythonhosted.org/packages/1c/b1/362a0d4235496cb42c33d1d8732b5e2c607b0129ad5fdd76f5a583b9fcb3/$_pkgname-$pkgver.tar.gz")
sha512sums=('4cea4229332c1f3de26dfcad596877665b3c02e91d51ca3c45c1f9b44462adb7c82abc7b76eb09a73822c2d1ccc9d812574cf79bf6bc8fb0b7d2f1093962cc3f')

prepare() {
  cd "$_pkgname-$pkgver"

  sed -i 's|env python|env python2|' "$_pkgname/FSM.py"
}

build() {
  cd "$_pkgname-$pkgver"

  python2 setup.py build
}

package() {
  cd "$_pkgname-$pkgver"

  python2 setup.py install --root="$pkgdir" --prefix=/usr -O1 --skip-build
}

