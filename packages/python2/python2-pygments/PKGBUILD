# This file is part of BlackArch Linux ( https://www.blackarch.org/ ).
# See COPYING for license details.

pkgname=python2-pygments
_pkgname=Pygments
pkgver=2.5.1
pkgrel=1
pkgdesc='A syntax highlighting package written in Python.'
url='https://pypi.org/project/pygments/#files'
arch=('any')
license=('BSD')
depends=('python2')
makedepends=('python2-setuptools')
source=("https://files.pythonhosted.org/packages/10/a3/0ccb2cdffd92e596db885d838f8659258f07084d70374438142334440948/$_pkgname-$pkgver.tar.gz")
sha512sums=('370b1a17bfee310c3aa3190f71e1062f510f66f8ca47bc9b5c65982020ce12e7637a213af703684f0e3026ed417aefe2919bae6735ff6454c54670ea56a4d92c')

build() {
  cd "$_pkgname-$pkgver"

  python2 setup.py build
}

package() {
  cd "$_pkgname-$pkgver"

  python2 setup.py install --root="$pkgdir" --prefix=/usr -O1 --skip-build

  mv "$pkgdir/usr/bin/pygmentize" "$pkgdir/usr/bin/pygmentize2"
}

