# This file is part of BlackArch Linux ( https://www.blackarch.org/ ).
# See COPYING for license details.

pkgname=python2-commonmark
_pkgname=commonmark
pkgver=0.9.1
pkgrel=1
pkgdesc='Python parser for the CommonMark Markdown spec.'
url='https://pypi.org/project/commonmark/#files'
arch=('any')
license=('BSD')
depends=('python2' 'python2-future')
makedepends=('python2-setuptools')
source=("https://files.pythonhosted.org/packages/60/48/a60f593447e8f0894ebb7f6e6c1f25dafc5e89c5879fdc9360ae93ff83f0/$_pkgname-$pkgver.tar.gz")
sha512sums=('bacf5ef387a0c330a2128f42c42c29c1a48ae623683e6e56ad28a1621aabd72e29cd3e2d661e8207b8cc0c347d267f262a55f2145fc58f8c8aeff91e7a283324')

build() {
  cd "$_pkgname-$pkgver"

  python2 setup.py build
}

package() {
  cd "$_pkgname-$pkgver"

  python2 setup.py install --root="$pkgdir" --prefix=/usr -O1 --skip-build

  mv "$pkgdir/usr/bin/cmark" "$pkgdir/usr/bin/cmark2"
}

