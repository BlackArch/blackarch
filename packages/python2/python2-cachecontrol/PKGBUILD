# This file is part of BlackArch Linux ( https://www.blackarch.org/ ).
# See COPYING for license details.

pkgname=python2-cachecontrol
_pkgname=CacheControl
pkgver=0.12.5
pkgrel=1
pkgdesc='httplib2 caching for requests.'
url='https://pypi.org/project/CacheControl/#files'
arch=('any')
license=('MIT')
depends=('python2' 'python2-requests' 'python2-msgpack')
makedepends=('python2-setuptools')
source=("https://files.pythonhosted.org/packages/5e/f0/2c193ed1f17c97ae539da7e1c2d48b80d8cccb1917163b26a91ca4355aa6/$_pkgname-$pkgver.tar.gz")
sha512sums=('b7be614c84405ad4bc1125d3d4d149131ffd56455925c7eea4706c4a2d3841e1edfca58f9612cd85dc2a2838313b4c0e56df4ba93a5297b12ebafbb900ea20f6')

build() {
  cd "$_pkgname-$pkgver"

  python2 setup.py build
}

package() {
  cd "$_pkgname-$pkgver"

  python2 setup.py install --root="$pkgdir" --prefix=/usr -O1 --skip-build

  mv "$pkgdir/usr/bin/doesitcache" "$pkgdir/usr/bin/doesitcache2"
}

