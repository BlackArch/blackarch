# This file is part of BlackArch Linux ( https://www.blackarch.org/ ).
# See COPYING for license details.

pkgname=ruby-rubyntlm
_gemname=rubyntlm
pkgver=0.6.3
pkgrel=3
pkgdesc='Ruby/NTLM library.'
arch=('any')
url='https://github.com/winrb/rubyntlm'
license=('MIT')
depends=('ruby')
options=(!emptydirs)
source=("https://rubygems.org/downloads/$_gemname-$pkgver.gem")
noextract=("$_gemname-$pkgver.gem")
sha512sums=('7e50c1bf63477fead9189a5ade06db65a3fad04d82bccfbffb513517ab97766053c800af9551e027dedc3aee6fb35288c234d98b1a02815299bcbc463e0edd87')

package() {
  _gemdir="$(ruby -e'puts Gem.default_dir')"

	if [[ $CARCH == arm* ]] ; then
		gem install --no-user-install --ignore-dependencies --no-rdoc --no-ri \
			-n "$pkgdir/usr/bin" -i "$pkgdir$_gemdir" "$_gemname-$pkgver.gem"
	else
		gem install --no-user-install --ignore-dependencies -i "$pkgdir$_gemdir" \
			-n "$pkgdir/usr/bin" "$_gemname-$pkgver.gem"
	fi

  rm "$pkgdir/$_gemdir/cache/$_gemname-$pkgver.gem"

  install -Dm 644 "$pkgdir/$_gemdir/gems/$_gemname-$pkgver/LICENSE" \
    "$pkgdir/usr/share/licenses/$pkgname/LICENSE"
}

