# This file is part of BlackArch Linux ( http://blackarch.org ).
# See COPYING for license details.

pkgname=ilo4-toolbox
pkgver=14.086e8d2
pkgrel=1
pkgdesc='Toolbox for HPE iLO4 analysis.'
groups=('blackarch' 'blackarch-scanner' 'blackarch-automation'
        'blackarch-backdoor')
arch=('x86_64' 'armv6h' 'armv7h' 'aarch64')
url='https://github.com/airbus-seclab/ilo4_toolbox'
license=('GPL2')
depends=('python2' 'python2-requests' 'python2-keystone' 'python2-capstone'
         'ruby' 'go')
makedepends=('git')
source=('git+https://github.com/airbus-seclab/ilo4_toolbox.git')
sha512sums=('SKIP')

pkgver() {
  cd "$srcdir/ilo4_toolbox"

  echo $(git rev-list --count HEAD).$(git rev-parse --short HEAD)
}

package() {
  cd "$srcdir/ilo4_toolbox"

  install -dm 755 "$pkgdir/usr/bin"
  install -dm 755 "$pkgdir/usr/share/ilo4-toolbox"

  install -Dm644 -t "$pkgdir/usr/share/doc/ilo4-toolbox/" README.rst
  install -Dm644 COPYING "$pkgdir/usr/share/licenses/ilo4-toolbox/COPYING"

  rm README.rst COPYING

  cp -a * "$pkgdir/usr/share/ilo4-toolbox/"
}
