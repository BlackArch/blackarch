# This file is part of BlackArch Linux ( https://www.blackarch.org/ ).
# See COPYING for license details.

pkgname=httpx
pkgver=v1.7.4.r56.g1a11e4c
pkgrel=1
epoch=1
groups=('blackarch' 'blackarch-webapp' 'blackarch-scanner')
pkgdesc='A fast and multi-purpose HTTP toolkit allow to run multiple probers using retryablehttp library.'
arch=('x86_64' 'aarch64')
url='https://github.com/projectdiscovery/httpx'
license=('MIT')
makedepends=('git' 'go')
source=("git+https://github.com/projectdiscovery/$pkgname.git")
sha512sums=('SKIP')
install="$pkgname.install"

pkgver() {
  cd $pkgname

  ( set -o pipefail
    git describe --long --tags --abbrev=7 2>/dev/null |
      sed 's/\([^-]*-g\)/r\1/;s/-/./g' ||
    printf "%s.%s" "$(git rev-list --count HEAD)" \
      "$(git rev-parse --short=7 HEAD)"
  )
}

build() {
  cd $pkgname

  GOPATH="$srcdir" go mod download
  GOPATH="$srcdir" go build \
    -trimpath \
    -buildmode=pie \
    -mod=readonly \
    -modcacherw \
    -ldflags "-s -w" \
    -o $pkgname "./cmd/$pkgname"
}

package() {
  cd $pkgname

  install -Dm 755 $pkgname "$pkgdir/usr/bin/$pkgname-pd"
  install -Dm 644 -t "$pkgdir/usr/share/doc/$pkgname/" README.md
}

