# This file is part of BlackArch Linux ( https://www.blackarch.org/ ).
# See COPYING for license details.

pkgname=operative
pkgver=147.4448b19
pkgrel=1
epoch=1
pkgdesc='Framework based on fingerprint action, this tool is used for get information on a website or a enterprise target with multiple modules (Viadeo search,Linkedin search, Reverse email whois, Reverse ip whois, SQL file forensics ...).'
groups=('blackarch' 'blackarch-fingerprint')
arch=('any')
url='https://github.com/graniet/operative-framework'
license=('Apache')
depends=('glibc')
makedepends=('git' 'go-pie')
source=("$pkgname::git+https://github.com/graniet/operative-framework.git")
sha512sums=('SKIP')

pkgver() {
  cd $pkgname

  echo $(git rev-list --count HEAD).$(git rev-parse --short HEAD)
}

build() {
  cd $pkgname

  export GOOS=linux
  export GOPATH="$srcdir"

  if [ "$CARCH" == "x86_64" ]
  then
    export GOARCH=amd64
  elif [ "$CARCH" == "aarch64" ]
  then
    export GOARCH=arm
  fi

  go get -d -v ./...
  go build -v -o $pkgname

}

package() {
  cd $pkgname

  install -Dm 755 $pkgname "$pkgdir/usr/bin/$pkgname"
  install -Dm 644 -t "$pkgdir/usr/share/doc/$pkgname/" README.md
  install -Dm 644 LICENSE "$pkgdir/usr/share/licenses/$pkgname/LICENSE"
}

