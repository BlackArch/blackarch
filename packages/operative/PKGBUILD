# This file is part of BlackArch Linux ( http://blackarch.org ).
# See COPYING for license details.

pkgname='operative'
pkgver=105.5af1120
pkgrel=1
pkgdesc='Framework based on fingerprint action, this tool is used for get information on a website or a enterprise target with multiple modules (Viadeo search,Linkedin search, Reverse email whois, Reverse ip whois, SQL file forensics ...).'
groups=('blackarch' 'blackarch-fingerprint')
arch=('any')
url='https://github.com/graniet/operative-framework'
license=('Apache2')
depends=('python2' 'python2-colorama' 'python2-requests' 'python2-pythonwhois'
         'python2-beautifulsoup4' 'sslyze' 'waffit')
makedepends=('git')
source=('git+https://github.com/graniet/operative-framework.git')
sha1sums=('SKIP')

pkgver() {
  cd "$srcdir/operative-framework"

  echo $(git rev-list --count HEAD).$(git rev-parse --short HEAD)
}

package() {
  cd "$srcdir/operative-framework"

  mkdir -p "$pkgdir/usr/bin"
  mkdir -p "$pkgdir/usr/share/operative"

  install -Dm644 -t "$pkgdir/usr/share/doc/operative/" README.md
  install -Dm644 LICENSE "$pkgdir/usr/share/licenses/operative/LICENSE"

  rm README.md LICENSE

  cp -a * "$pkgdir/usr/share/operative"

  cat > "$pkgdir/usr/bin/operative" << EOF
#!/bin/sh
cd /usr/share/operative
exec python2 operative.py "\${@}"
EOF

  chmod a+x "$pkgdir/usr/bin/operative"
}
