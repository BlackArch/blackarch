# This file is part of BlackArch Linux ( https://www.blackarch.org/ ).
# See COPYING for license details.

pkgbase=python-apscheduler
pkgname=('python2-apscheduler' 'python-apscheduler')
_pkgname=APScheduler
pkgver=3.6.3
pkgrel=3
pkgdesc='In-process task scheduler with Cron-like capabilities.'
arch=('any')
url='https://pypi.org/project/apscheduler/#files'
license=('MIT')
makedepends=('python2-setuptools' 'python-setuptools')
source=("https://files.pythonhosted.org/packages/89/3d/f65972547c5aa533276ada2bea3c2ef51bb4c4de55b67a66129c111b89ad/$_pkgname-$pkgver.tar.gz")
sha512sums=('4c348a4f37a7973633554a69635f334b3c293c8942fe853b309b4b65335f5d4d553d7d42fd4661b0e6a36f5e97446d296a2c8971bdb51dad3bc8ee6b6d3b20ba')

prepare() {
  cp -a "$_pkgname-$pkgver"{,-2}
}

build() {
  cd "$_pkgname-$pkgver-2"

  python2 setup.py build

  cd "$srcdir/$_pkgname-$pkgver"

  python setup.py build
}

package_python2-apscheduler() {
  depends=('python2' 'python2-six' 'python2-tzlocal' 'python2-pytz')

  cd "$_pkgname-$pkgver-2"

  python2 setup.py install --prefix=/usr --root="$pkgdir" -O1 --skip-build
}

package_python-apscheduler() {
  depends=('python' 'python-six' 'python-tzlocal' 'python-pytz')

  cd "$_pkgname-$pkgver"

  python setup.py install --prefix=/usr --root="$pkgdir" -O1 --skip-build
}

