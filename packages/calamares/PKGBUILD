# This file is part of BlackArch Linux ( https://www.blackarch.org/ ).
#
# This PKGBUILD was extracted from Manjaro Linux and adapted to our style
#
# Old Maintainer: Philip MÃ¼ller <philm[at]manjaro[dot]org>

pkgname=calamares
pkgver=3.2.61
pkgrel=1
epoch=3
pkgdesc='Distribution-independent installer framework.'
url='https://github.com/calamares/calamares'
arch=('x86_64' 'aarch64')
license=('LGPL')
depends=('kconfig' 'kcoreaddons' 'kiconthemes' 'ki18n' 'kio' 'solid' 'yaml-cpp'
         'kpmcore' 'mkinitcpio-openswap' 'boost-libs' 'ckbcomp' 'hwinfo'
         'qt5-svg' 'polkit-qt5' 'gtk-update-icon-cache' 'plasma-framework'
         'qt5-xmlpatterns' 'squashfs-tools' 'libpwquality' 'appstream-qt' 'icu'
         'efibootmgr')
makedepends=('extra-cmake-modules' 'qt5-tools' 'qt5-translations' 'git' 'boost')
backup=('usr/share/calamares/modules/bootloader.conf'
        'usr/share/calamares/modules/displaymanager.conf'
        'usr/share/calamares/modules/initcpio.conf'
        'usr/share/calamares/modules/unpackfs.conf')
source=("https://github.com/calamares/$pkgname/archive/v$pkgver.tar.gz")
sha512sums=('54ff30fc04c4b1f4ae6ddb0df5dd8255e08e1e24a69754361d2adc7207de2fafcce7f7d3988d15588e9a006b7afe48a5ba74bf1cc5cc19fc31ac17fa1a4e17f1')

prepare() {
  cd "$pkgname-$pkgver"

  sed -i -e \
    's/"Install configuration files" OFF/"Install configuration files" ON/' \
    CMakeLists.txt
  sed -i -e \
    "s|\${CALAMARES_VERSION_MAJOR}.\${CALAMARES_VERSION_MINOR}.\${CALAMARES_VERSION_PATCH}|${_ver}-${pkgrel}|g" \
    CMakeLists.txt
  sed -i -e "s|CALAMARES_VERSION_RC 1|CALAMARES_VERSION_RC 0|g" CMakeLists.txt
}

build() {
  cd "$pkgname-$pkgver"

  mkdir -p build && cd build

  cmake .. \
  -DCMAKE_BUILD_TYPE=Release \
  -DCMAKE_INSTALL_PREFIX=/usr \
  -DCMAKE_INSTALL_LIBDIR=lib \
  -DWITH_PYTHONQT=OFF \
  -DWITH_KF5DBus=OFF \
  -DBoost_NO_BOOST_CMAKE=ON \
  -DWEBVIEW_FORCE_WEBKIT=OFF \
  -DSKIP_MODULES="webview tracking interactiveterminal initramfs \
  initramfscfg dracut dracutlukscfg \
  dummyprocess dummypython dummycpp \
  dummypythonqt services-openrc \
  keyboardq localeq welcomeq"

  make
}

package() {
  cd "$pkgname-$pkgver/build"

  make DESTDIR="$pkgdir" install
}

