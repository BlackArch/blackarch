# This file is part of BlackArch Linux ( https://www.blackarch.org/ ).
# See COPYING for license details.

pkgbase=python-neobolt
pkgname=('python2-neobolt' 'python-neobolt')
_pkgname=neobolt
pkgver=1.7.15
pkgrel=1
pkgdesc='Neo4j Bolt connector for Python.'
arch=('x86_64' 'aarch64')
url='https://pypi.org/project/neobolt/#files'
license=('APACHE')
makedepends=('python2-setuptools' 'python-setuptools')
source=("https://files.pythonhosted.org/packages/9f/63/bb8b2ba18b4929c8bd17299d499530d6779635439e33bcc95043dc597223/$_pkgname-$pkgver.tar.gz")
sha512sums=('5a58b34da11f70cd9ef1dcebf6dd4e96f62e8fce1943f1b926d57bd24938dd6351dbf008df1b883480e960b37ac1da4ece2ab18481412c5e787d34eb7879b1ff')

prepare() {
  cp -a "$_pkgname-$pkgver"{,-2}
}

build() {
  cd "$_pkgname-$pkgver-2"

  python2 setup.py build

  cd "$srcdir/$_pkgname-$pkgver"

  python setup.py build
}

package_python2-neobolt() {
  depends=('python2')

  cd "$_pkgname-$pkgver-2"

  python2 setup.py install --prefix=/usr --root="$pkgdir" --optimize=1 \
    --skip-build
}

package_python-neobolt() {
  depends=('python')

  cd "$_pkgname-$pkgver"

  python setup.py install --prefix=/usr --root="$pkgdir" --optimize=1 \
    --skip-build
}

