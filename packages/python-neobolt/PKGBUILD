# This file is part of BlackArch Linux ( https://www.blackarch.org/ ).
# See COPYING for license details.

pkgbase=python-neobolt
pkgname=('python2-neobolt' 'python-neobolt')
_pkgname=neobolt
pkgver=1.7.16
pkgrel=1
pkgdesc='Neo4j Bolt connector for Python.'
arch=('x86_64' 'aarch64')
url='https://pypi.org/project/neobolt/#files'
license=('APACHE')
makedepends=('python2-setuptools' 'python-setuptools')
source=("https://files.pythonhosted.org/packages/0d/b2/eb7c33e1f02cd7bbaf4c237619dd8dc43fa6081f331c2d35b28deb78e259/$_pkgname-$pkgver.tar.gz")
sha512sums=('4edd8518d061326401fb27fcb2dd00b94aab7b515f17068956f5de722bd9f3e479ffaf78dae7da5ac94ed32bcd51108b46e562f0672cd8c8db8408fcc9a45943')

prepare() {
  cp -a "$_pkgname-$pkgver"{,-2}
}

build() {
  cd "$_pkgname-$pkgver-2"

  python2 setup.py build

  cd "$srcdir/$_pkgname-$pkgver"

  python setup.py build
}

package_python2-neobolt() {
  depends=('python2')

  cd "$_pkgname-$pkgver-2"

  python2 setup.py install --prefix=/usr --root="$pkgdir" --optimize=1 \
    --skip-build
}

package_python-neobolt() {
  depends=('python')

  cd "$_pkgname-$pkgver"

  python setup.py install --prefix=/usr --root="$pkgdir" --optimize=1 \
    --skip-build
}

