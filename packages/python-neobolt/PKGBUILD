# This file is part of BlackArch Linux ( https://www.blackarch.org/ ).
# See COPYING for license details.

pkgbase=python-neobolt
pkgname=('python2-neobolt' 'python-neobolt')
_pkgname=neobolt
pkgver=1.7.13
pkgrel=2
pkgdesc='Neo4j Bolt connector for Python.'
arch=('x86_64' 'aarch64')
url='https://pypi.org/project/neobolt/#files'
license=('APACHE')
makedepends=('python2-setuptools' 'python-setuptools')
source=("https://files.pythonhosted.org/packages/60/60/80ff9b440ecfc649689d5ff7836f608b675711842a07c5ad17eff48ae4e7/$_pkgname-$pkgver.tar.gz")
sha512sums=('4a0de468e09f6f01a5cba024d303eaae046b204453934dd3abcbdcfe7aa827168400754b22899e67ec8543e9b3cbaf553d3450a6473667dbd69a8263bf27dc1e')

prepare() {
  cp -a "$_pkgname-$pkgver"{,-2}
}

build() {
  cd "$_pkgname-$pkgver-2"

  python2 setup.py build

  cd "$srcdir/$_pkgname-$pkgver"

  python setup.py build
}

package_python2-neobolt() {
  depends=('python2')

  cd "$_pkgname-$pkgver-2"

  python2 setup.py install --prefix=/usr --root="$pkgdir" --optimize=1 \
    --skip-build
}

package_python-neobolt() {
  depends=('python')

  cd "$_pkgname-$pkgver"

  python setup.py install --prefix=/usr --root="$pkgdir" --optimize=1 \
    --skip-build
}

