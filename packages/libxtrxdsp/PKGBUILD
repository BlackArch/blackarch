# This file is part of BlackArch Linux ( https://www.blackarch.org/ ).
# See COPYING for license details.

pkgname=libxtrxdsp
pkgver=12.271f5e6
pkgrel=1
pkgdesc='DSP specific function for SDR in general and XTRX in specific.'
arch=('x86_64' 'aarch64' 'i686')
url='https://github.com/xtrx-sdr/libxtrxdsp'
license=('Apache-2.0')
makedepends=('git' 'cmake')
source=("git+https://github.com/xtrx-sdr/$pkgname.git")
sha512sums=('SKIP')

pkgver() {
  cd $pkgname

  ( set -o pipefail
    git describe --long --tags --abbrev=7 2>/dev/null |
      sed 's/\([^-]*-g\)/r\1/;s/-/./g' ||
    printf "%s.%s" "$(git rev-list --count HEAD)" \
      "$(git rev-parse --short=7 HEAD)"
  )
}

prepare() {
  cd $pkgname

  sed -i 's/^cmake_minimum_required *(VERSION \([0-9]\+\(\.[0-9]\+\)\{0,2\}\)).*/cmake_minimum_required(VERSION 3.5)/' CMakeLists.txt
}

build() {
  cd $pkgname

  mkdir -p build
  cd build

  if [ "$CARCH" = 'i686' ]; then
    FORCE_ARCH="-DFORCE_ARCH=x86"
  fi

  cmake .. \
    ${FORCE_ARCH} \
    -DBUILD_SHARED_LIBS=ON \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DCMAKE_BUILD_TYPE=Release

  make
}

package() {
  cd $pkgname

  make -C build DESTDIR="$pkgdir" install
}

