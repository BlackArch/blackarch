# This file is part of BlackArch Linux ( http://blackarch.org ).
# See COPYING for license details.

pkgname='phishingkithunter'
pkgver=15.55f54c2
pkgrel=1
pkgdesc="Find phishing kits which use your brand/organization's files and image'."
groups=('blackarch' 'blackarch-social' 'blackarch-defensive')
arch=('any')
url='https://github.com/t4d/PhishingKitHunter'
license=('GAGPL3')
depends=('python' 'python-tqdm' 'python-whois' 'python-socksipy-branch')
makedepends=('git')
source=('git+https://github.com/t4d/PhishingKitHunter')
sha1sums=('SKIP')

pkgver() {
  cd "$srcdir/PhishingKitHunter"

  echo $(git rev-list --count HEAD).$(git rev-parse --short HEAD)
}

package() {
  cd "$srcdir/PhishingKitHunter"

  mkdir -p "$pkgdir/usr/bin"
  mkdir -p "$pkgdir/usr/share/phishingkithunter"

  install -Dm644 -t "$pkgdir/usr/share/doc/phishingkithunter/" README.md
  install -Dm644 LICENSE "$pkgdir/usr/share/licenses/phishingkithunter/LICENSE"

  rm README.md LICENSE

  cp -a * "$pkgdir/usr/share/phishingkithunter/"

  cat > "$pkgdir/usr/bin/phishingkithunter" << EOF
#!/bin/sh
cd /usr/share/phishingkithunter
exec python PhishingKitHunter.py "\${@}"
EOF

  chmod a+x "$pkgdir/usr/bin/phishingkithunter"
}
