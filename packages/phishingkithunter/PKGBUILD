# This file is part of BlackArch Linux ( https://www.blackarch.org/ ).
# See COPYING for license details.

pkgname=phishingkithunter
pkgver=20.ac9bd1e
pkgrel=2
pkgdesc="Find phishing kits which use your brand/organization's files and image'."
groups=('blackarch' 'blackarch-social' 'blackarch-defensive')
arch=('any')
url='https://github.com/t4d/PhishingKitHunter'
license=('AGPL3')
depends=('python' 'python-tqdm' 'python-python-whois' 'python-pysocks')
makedepends=('git')
source=("$pkgname::git+https://github.com/t4d/PhishingKitHunter.git")
sha512sums=('SKIP')

pkgver() {
  cd $pkgname

  echo $(git rev-list --count HEAD).$(git rev-parse --short HEAD)
}

package() {
  cd $pkgname

  install -dm 755 "$pkgdir/usr/bin"
  install -dm 755 "$pkgdir/usr/share/$pkgname"

  install -Dm 644 -t "$pkgdir/usr/share/doc/$pkgname/" README.md
  install -Dm 644 LICENSE "$pkgdir/usr/share/licenses/$pkgname/LICENSE"

  rm README.md LICENSE

  cp -a * "$pkgdir/usr/share/$pkgname/"

  cat > "$pkgdir/usr/bin/$pkgname" << EOF
#!/bin/sh
cd /usr/share/$pkgname
exec python PhishingKitHunter.py "\$@"
EOF

  chmod +x "$pkgdir/usr/bin/$pkgname"
}

