# This file is part of BlackArch Linux ( http://blackarch.org ).
# See COPYING for license details.

pkgname=phishingkithunter
pkgver=17.1db0258
pkgrel=3
pkgdesc="Find phishing kits which use your brand/organization's files and image'."
groups=('blackarch' 'blackarch-social' 'blackarch-defensive')
arch=('any')
url='https://github.com/t4d/PhishingKitHunter'
license=('GAGPL3')
depends=('python' 'python-tqdm' 'python-whois' 'python-pysocks')
makedepends=('git')
source=('git+https://github.com/t4d/PhishingKitHunter')
sha512sums=('SKIP')

pkgver() {
  cd PhishingKitHunter

  echo $(git rev-list --count HEAD).$(git rev-parse --short HEAD)
}

package() {
  cd PhishingKitHunter

  install -dm 755 "$pkgdir/usr/bin"
  install -dm 755 "$pkgdir/usr/share/phishingkithunter"

  install -Dm644 -t "$pkgdir/usr/share/doc/phishingkithunter/" README.md
  install -Dm644 LICENSE "$pkgdir/usr/share/licenses/phishingkithunter/LICENSE"

  rm README.md LICENSE

  cp -a * "$pkgdir/usr/share/phishingkithunter/"

  cat > "$pkgdir/usr/bin/phishingkithunter" << EOF
#!/bin/sh
cd /usr/share/phishingkithunter
exec python PhishingKitHunter.py "\${@}"
EOF

  chmod a+x "$pkgdir/usr/bin/phishingkithunter"
}

