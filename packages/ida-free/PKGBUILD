# This file is part of BlackArch Linux ( https://www.blackarch.org/ ).
# See COPYING for license details.
#
# This file is based on the AUR package by fatalis <fatalis@fatalis.pw>
# (https://aur.archlinux.org/packages/ida-free)
pkgname=ida-free
pkgver=8.2
pkgrel=1
pkgdesc="Freeware version of the world's smartest and most feature-full disassembler."
groups=('blackarch' 'blackarch-reversing' 'blackarch-disassembler'
        'blackarch-decompiler' 'blackarch-binary')
arch=('x86_64')
url='https://www.hex-rays.com/products/ida/'
license=('custom:unknown')
makedepends=('fakechroot')
options=('!strip')
_originalname="idafree${pkgver//./}_linux.run"
_installer="$_originalname-$pkgver-$pkgrel"
source=("$_installer::https://out7.hex-rays.com/files/${_originalname}")
sha512sums=('82e04514b7fab749a774a31c3cfe2132101ec5f8ea1943f4a6b92b0dd3fa2bbe3b5db9c79974b841368e066980beb5c9380d5e7bf0973962e507078556305bce')

package() {
  install -d "$pkgdir/opt/$pkgname"
  install -d "$pkgdir/usr/bin"
  install -d "$pkgdir"/usr/share/{icons,applications,licenses/$pkgname}
  install -d "$pkgdir/tmp"

  # chroot is needed to prevent the installer from creating a single file
  # outside of prefix have to copy the installer due to chroot
  cp $_installer $pkgdir
  chmod +x "$pkgdir"/$_installer
  fakechroot chroot "${pkgdir}" /${_installer} --mode unattended \
  --prefix /opt/${pkgname} --installpassword ""
  rm "$pkgdir"/${_installer}
  rm "$pkgdir"/tmp/installbuilder_installer.log
  rmdir "$pkgdir"/tmp

  # the installer needlessly makes a lot of files executable
  find "$pkgdir/opt/$pkgname" -type f -exec chmod -x {} \;
  # make dir permissions consistent with the 7.0 installer
  find "$pkgdir/opt/$pkgname" -type d -exec chmod g-w {} \;
  chmod +x "$pkgdir/opt/$pkgname/"{ida64,assistant}

  rm "$pkgdir"/opt/$pkgname/{uninstall*,Uninstall*}

  ln -s "/opt/$pkgname/appico64.png" "$pkgdir/usr/share/icons/ida-free.png"
  ln -s "/opt/$pkgname/license.txt" \
    "$pkgdir/usr/share/licenses/$pkgname/LICENSE"
  ln -s "/opt/$pkgname/ida64" "$pkgdir/usr/bin/ida64"
}

