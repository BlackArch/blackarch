# This file is part of BlackArch Linux ( https://www.blackarch.org/ ).
# See COPYING for license details.

pkgname=lrod
pkgver=Version.r32.g57d9cab
pkgrel=1
pkgdesc='Perform subdomain enumeration, endpoint recognition, and more.'
arch=('any')
groups=('blackarch' 'blackarch-recon')
url='https://gitlab.com/Edu0x01/Lrod'
license=('MIT')
depends=('python' 'python-virtualenv')
makedepends=('git' 'python-setuptools' 'python-pip')
source=("git+https://gitlab.com/Edu0x01/$pkgname.git")
sha512sums=('SKIP')
install="$pkgname.install"

pkgver() {
  cd $pkgname

  ( set -o pipefail
    git describe --long --tags --abbrev=7 2>/dev/null |
      sed 's/\([^-]*-g\)/r\1/;s/-/./g' ||
    printf "%s.%s" "$(git rev-list --count HEAD)" \
      "$(git rev-parse --short=7 HEAD)"
  )
}

package() {
  cd $pkgname

  install -dm 755 "$pkgdir/usr/bin"
  install -Dm 644 requirements.txt "$pkgdir/usr/share/$pkgname/requirements.txt"
  install -Dm 644 -t "$pkgdir/usr/share/doc/$pkgname/" *.md

  rm -rf LICENSE *.md .gitignore

  cp -a * "$pkgdir/usr/share/$pkgname/"

  cat > "$pkgdir/usr/bin/$pkgname" << EOF
#!/bin/sh
source /usr/share/$pkgname/venv/bin/activate
exec python /usr/share/$pkgname/Lrod.py "\$@"
EOF

  chmod a+x "$pkgdir/usr/bin/$pkgname"
}

