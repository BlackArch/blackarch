# This file is part of BlackArch Linux ( https://www.blackarch.org/ ).
# See COPYING for license details.

pkgbase=python-msoffcrypto-tool
pkgname=('python2-msoffcrypto-tool' 'python-msoffcrypto-tool')
_pkgname=msoffcrypto-tool
pkgver=4.12.0
pkgrel=2
pkgdesc='A Python tool and library for decrypting MS Office files with passwords or other keys.'
arch=('any')
url='https://pypi.org/project/msoffcrypto-tool/#files'
license=('MIT')
makedepends=('python2-setuptools' 'python-setuptools')
source=("https://files.pythonhosted.org/packages/b3/62/1367929fc910abc684dae3a9d054a2ebbe37b5161613db1f856cb30a263c/$_pkgname-$pkgver.tar.gz")
sha512sums=('1e45906d7daa99e93af866a5e47899bcb162fe8b386945a629af3aec09260f4f747df0e2407ed8bb4d3e153c100c7898fbe9d4f5c36dad6c8bf0974a7310a12e')

prepare() {
  cp -a "$_pkgname-$pkgver"{,-2}
}

build() {
  cd "$_pkgname-$pkgver-2"

  python2 setup.py build

  cd "$srcdir/$_pkgname-$pkgver"

  python setup.py build
}

package_python2-msoffcrypto-tool() {
  depends=('python2' 'python2-olefile' 'python2-cryptography')

  cd "$_pkgname-$pkgver-2"

  python2 setup.py install --prefix=/usr --root="$pkgdir" -O1 --skip-build

  mv "$pkgdir/usr/bin/$_pkgname" "$pkgdir/usr/bin/${_pkgname}-2"
}

package_python-msoffcrypto-tool() {
  depends=('python' 'python-olefile' 'python-cryptography')

  cd "$_pkgname-$pkgver"

  python setup.py install --prefix=/usr --root="$pkgdir" -O1 --skip-build
}

