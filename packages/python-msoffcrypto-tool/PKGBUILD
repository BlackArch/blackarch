# This file is part of BlackArch Linux ( https://www.blackarch.org/ ).
# See COPYING for license details.

pkgbase=python-msoffcrypto-tool
pkgname=('python2-msoffcrypto-tool' 'python-msoffcrypto-tool')
_pkgname=msoffcrypto-tool
pkgver=4.10.1
pkgrel=1
pkgdesc='A Python tool and library for decrypting MS Office files with passwords or other keys.'
arch=('any')
url='https://pypi.org/project/msoffcrypto-tool/#files'
license=('MIT')
makedepends=('python2-setuptools' 'python-setuptools')
source=("https://files.pythonhosted.org/packages/c5/68/58628967bc5328b22706c74081df4564e853ee68f4dafd6d77e329ae56e8/$_pkgname-$pkgver.tar.gz")
sha512sums=('64e39a291908e88ee0dba2698e1dd852b00896cd417df0ed77c53bd12d9c93be3171689242b050f26a5ac86cbe1b34f59efa5b6958785a4d4d704b5b3d4cb5b9')

prepare() {
  cp -a "$_pkgname-$pkgver"{,-2}
}

build() {
  cd "$_pkgname-$pkgver-2"

  python2 setup.py build

  cd "$srcdir/$_pkgname-$pkgver"

  python setup.py build
}

package_python2-msoffcrypto-tool() {
  depends=('python2' 'python2-olefile' 'python2-cryptography')

  cd "$_pkgname-$pkgver-2"

  python2 setup.py install --prefix=/usr --root="$pkgdir" -O1 --skip-build

  mv "$pkgdir/usr/bin/$_pkgname" "$pkgdir/usr/bin/${_pkgname}-2"
}

package_python-msoffcrypto-tool() {
  depends=('python' 'python-olefile' 'python-cryptography')

  cd "$_pkgname-$pkgver"

  python setup.py install --prefix=/usr --root="$pkgdir" -O1 --skip-build
}

