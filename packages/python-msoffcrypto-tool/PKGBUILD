# This file is part of BlackArch Linux ( https://www.blackarch.org/ ).
# See COPYING for license details.

pkgbase=python-msoffcrypto-tool
pkgname=('python2-msoffcrypto-tool' 'python-msoffcrypto-tool')
_pkgname=msoffcrypto-tool
pkgver=5.0.0
pkgrel=1
pkgdesc='A Python tool and library for decrypting MS Office files with passwords or other keys.'
arch=('any')
url='https://pypi.org/project/msoffcrypto-tool/#files'
license=('MIT')
makedepends=('python2-setuptools' 'python-setuptools')
source=("https://files.pythonhosted.org/packages/da/b5/392d8cefae11aacc788a40539579e32c8051fb53fbeea19c0e725901d3f7/$_pkgname-$pkgver.tar.gz")
sha512sums=('6e9cceb2c49fb6a26427fb9668b6493861a002c2972d8ee902978a86a3d99e942145eefe0575a3827f9527d9db0b209a137223841d1ab6c86cb058ba141e32bc')

prepare() {
  cp -a "$_pkgname-$pkgver"{,-2}
}

build() {
  cd "$_pkgname-$pkgver-2"

  python2 setup.py build

  cd "$srcdir/$_pkgname-$pkgver"

  python setup.py build
}

package_python2-msoffcrypto-tool() {
  depends=('python2' 'python2-olefile' 'python2-cryptography')

  cd "$_pkgname-$pkgver-2"

  python2 setup.py install --prefix=/usr --root="$pkgdir" -O1 --skip-build

  mv "$pkgdir/usr/bin/$_pkgname" "$pkgdir/usr/bin/${_pkgname}-2"
}

package_python-msoffcrypto-tool() {
  depends=('python' 'python-olefile' 'python-cryptography')

  cd "$_pkgname-$pkgver"

  python setup.py install --prefix=/usr --root="$pkgdir" -O1 --skip-build
}

