# This file is part of BlackArch Linux ( http://blackarch.org ).
# See COPYING for license details.

pkgname='python2-exscript'
pkgver=1756.d75e2a9
pkgrel=1
pkgdesc='A Python module making Telnet and SSH easy.'
arch=('any')
url='https://github.com/knipknap/exscript'
license=('GPL2')
depends=('python2' 'python2-crypto' 'python2-paramiko')
makedepends=('git' 'python2-setuptools')
source=('git+https://github.com/knipknap/exscript.git')
sha1sums=('SKIP')

pkgver() {
  cd "$srcdir/exscript"

  echo $(git rev-list --count HEAD).$(git rev-parse --short HEAD)
}

build() {
  cd "$srcdir/exscript"

  python2 setup.py build
}

package() {
  cd "$srcdir/exscript"

  python2 setup.py install --prefix=/usr --root="$pkgdir" --optimize=1
}
