# This file is part of BlackArch Linux ( https://www.blackarch.org/ ).
# See COPYING for license details.

pkgname=uppwn
_pkgname=UpPwn
pkgver=9.f69dec4
pkgrel=4
pkgdesc="A script that automates detection of security flaws on websites' file upload systems'."
groups=('blackarch' 'blackarch-webapp' 'blackarch-fuzzer')
arch=('any')
url='https://github.com/ferrery1/UpPwn'
license=('custom:unknown')
depends=('python2' 'python2-selenium' 'python2-xvfbwrapper' 'nodejs' 'phantomjs'
         'python2-xvfbwrapper' 'xorg-server-xvfb' 'python2-pymiproxy')
makedepends=('git')
source=('git+https://github.com/ferrery1/UpPwn.git')
sha512sums=('SKIP')

pkgver() {
  cd $_pkgname

  echo $(git rev-list --count HEAD).$(git rev-parse --short HEAD)
}

package() {
  cd $_pkgname

  install -dm 755 "$pkgdir/usr/bin"
  install -dm 755 "$pkgdir/usr/share/$pkgname"

  install -Dm 644 -t "$pkgdir/usr/share/doc/$pkgname/" README.md

  rm README.md

  cp -a * "$pkgdir/usr/share/$pkgname/"

  cat > "$pkgdir/usr/bin/$pkgname" << EOF
#!/bin/sh
cd /usr/share/$pkgname
exec python2 $_pkgname.py "\$@"
EOF

  chmod +x "$pkgdir/usr/bin/$pkgname"
}

