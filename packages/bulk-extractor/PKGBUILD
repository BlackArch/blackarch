# This file is part of BlackArch Linux ( http://blackarch.org ).
# See COPYING for license details.

pkgname=bulk-extractor
pkgver=1.3.1
pkgrel=8
groups=('blackarch' 'blackarch-forensic' 'blackarch-misc')
pkgdesc="Bulk Email and URL extraction tool"
arch=('i686' 'x86_64' 'armv6h' 'armv7h')
url='https://github.com/simsong/bulk_extractor'
license=('GPL')
depends=('tre' 'java-environment' 'openssl' 'exiv2' 'afflib' 'libewf')
source=(http://digitalcorpora.org/downloads/bulk_extractor/bulk_extractor-${pkgver}.tar.gz)
md5sums=('811ec55285d74d41837f3ffe66de57ba')

prepare() {
  cd "$srcdir/bulk_extractor-$pkgver"

  sed -i 's|`dirname $0`|/usr/share/bulk-extractor|' java_gui/BEViewer
}

build() {
  cd "$srcdir/bulk_extractor-$pkgver"

  ./configure --prefix=/usr --sysconfdir=/etc
  make
  make -C java_gui
}

package() {
  cd "$srcdir/bulk_extractor-$pkgver"

  install -dm755 "$pkgdir/usr/share/bulk-extractor"

  make "DESTDIR=$pkgdir" install

  install -m644 java_gui/BEViewer.jar "$pkgdir/usr/share/bulk-extractor"
  install -m755 java_gui/BEViewer "$pkgdir/usr/bin"
}
