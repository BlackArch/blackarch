# This file is part of BlackArch Linux ( https://www.blackarch.org/ ).
# See COPYING for license details.

pkgbase=python-coloredlogs
pkgname=('python2-coloredlogs' 'python-coloredlogs')
_pkgname=coloredlogs
pkgver=10.0
pkgrel=3
pkgdesc='Colored stream handler for the logging module.'
arch=('any')
url='https://pypi.org/project/coloredlogs/#files'
makedepends=('python2-setuptools' 'python-setuptools')
license=('MIT')
options=(!emptydirs)
source=("https://files.pythonhosted.org/packages/63/09/1da37a51b232eaf9707919123b2413662e95edd50bace5353a548910eb9d/$_pkgname-$pkgver.tar.gz")
sha512sums=('f4a51fd8fa92ea4e2ef0b58305dcc4bbe12851e722fc85cfd4f48a9388002efced52bcf027f6dc4c525b095f3f953970e73928fdce3245ab4f9d13ae36b9d498')

prepare() {
  cp -a "$_pkgname-$pkgver"{,-2}
}

build() {
  cd "$_pkgname-$pkgver-2"

  python2 setup.py build

  cd "$srcdir/$_pkgname-$pkgver"

  python setup.py build
}

package_python2-coloredlogs() {
  depends=('python2' 'python2-humanfriendly')

  cd "$_pkgname-$pkgver-2"

  python2 setup.py install --prefix=/usr --root="$pkgdir" --optimize=1 \
    --skip-build

  mv "$pkgdir/usr/bin/coloredlogs" "$pkgdir/usr/bin/coloredlogs2"
}

package_python-coloredlogs() {
  depends=('python' 'python-humanfriendly')

  cd "$_pkgname-$pkgver"

  python setup.py install --prefix=/usr --root="$pkgdir" --optimize=1 \
    --skip-build
}

