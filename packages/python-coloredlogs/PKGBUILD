# This file is part of BlackArch Linux ( https://www.blackarch.org/ ).
# See COPYING for license details.

pkgbase=python-coloredlogs
pkgname=('python2-coloredlogs' 'python-coloredlogs')
_pkgname=coloredlogs
pkgver=15.0
pkgrel=1
pkgdesc='Colored stream handler for the logging module.'
arch=('any')
url='https://pypi.org/project/coloredlogs/#files'
makedepends=('python2-setuptools' 'python-setuptools')
license=('MIT')
options=(!emptydirs)
source=("https://files.pythonhosted.org/packages/ce/ef/bfca8e38c1802896f67045a0c9ea0e44fc308b182dbec214b9c2dd54429a/$_pkgname-$pkgver.tar.gz")
sha512sums=('0b5eb03441ec8a6f593da35fbc46d0c9ca45a32a1e5ed0881f584e31d107f30c79d2ad84c3602d2046047beb04910d33283d455eb6b24c1ff407071e085ceee7')

prepare() {
  cp -a "$_pkgname-$pkgver"{,-2}
}

build() {
  cd "$_pkgname-$pkgver-2"

  python2 setup.py build

  cd "$srcdir/$_pkgname-$pkgver"

  python setup.py build
}

package_python2-coloredlogs() {
  depends=('python2' 'python2-humanfriendly')

  cd "$_pkgname-$pkgver-2"

  python2 setup.py install --root="$pkgdir" --prefix=/usr -O1 --skip-build

  mv "$pkgdir/usr/bin/$_pkgname" "$pkgdir/usr/bin/${_pkgname}2"
}

package_python-coloredlogs() {
  depends=('python' 'python-humanfriendly')

  cd "$_pkgname-$pkgver"

  python setup.py install --root="$pkgdir" --prefix=/usr -O1 --skip-build
}

