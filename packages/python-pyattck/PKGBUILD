# This file is part of BlackArch Linux ( https://www.blackarch.org/ ).
# See COPYING for license details.

pkgname=python-pyattck
_pkgname=pyattck
pkgver=7.1.1
pkgrel=2
pkgdesc='A Python Module to interact with the Mitre ATT&CK Framework.'
arch=('any')
url='https://pypi.org/project/pyattck/#files'
license=('MIT')
depends=('python' 'python-requests' 'python-pendulum' 'python-pyfiglet'
         'python-pyaml' 'python-pillow' 'python-fire')
makedepends=('python-setuptools')
options=(!emptydirs)
source=("https://files.pythonhosted.org/packages/source/${_pkgname::1}/$_pkgname/$_pkgname-$pkgver.tar.gz")
sha512sums=('ce32b1d4fa64e39f35ec6acb71525558f27059d656d24532ec05214038ba48f13f0d41d65bf6fb7c83d170e4f0e6a5ec09985c4589bc668a029b8c0d4896013e')

build() {
  cd "$_pkgname-$pkgver"

  touch requirements.txt
  python setup.py build
}

package() {
  cd "$_pkgname-$pkgver"

  python setup.py install --root="$pkgdir" --prefix=/usr -O1 --skip-build
}

