# This file is part of BlackArch Linux ( https://www.blackarch.org/ ).
# See COPYING for license details.

pkgbase=python-bintrees
pkgname=('python2-bintrees' 'python-bintrees')
_pkgname=bintrees
pkgver=2.2.0
pkgrel=1
pkgdesc='Package provides Binary-, RedBlack- and AVL-Trees in Python and Cython.'
arch=('any')
url='https://pypi.org/project/bintrees/#files'
license=('MIT')
makedepends=('python2-setuptools' 'python-setuptools')
source=("https://files.pythonhosted.org/packages/a1/1f/e76488f335e532dc0bbc90336fe35f9a99a030964b05a9f2106ae03006db/$_pkgname-$pkgver.zip")
sha512sums=('897109b658354c91c0a04cb79d928048c787a4f63b70b557390ea7898fdb63bf6742c52891f1b2c2d6cc170f0e89e0269c8904cc958532e7295aba9d42cbf764')

prepare() {
  cp -a "$_pkgname-$pkgver"{,-2}
}

build() {
  cd "$_pkgname-$pkgver-2"

  python2 setup.py build

  cd "$srcdir/$_pkgname-$pkgver"

  python setup.py build
}

package_python2-bintrees() {
  depends=('python2')

  cd "$_pkgname-$pkgver-2"

  python2 setup.py install --root="$pkgdir" --prefix=/usr -O1 --skip-build
}

package_python-bintrees() {
  depends=('python')

  cd "$_pkgname-$pkgver"

  python setup.py install --root="$pkgdir" --prefix=/usr -O1 --skip-build
}

