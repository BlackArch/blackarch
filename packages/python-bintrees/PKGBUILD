# This file is part of BlackArch Linux ( https://www.blackarch.org/ ).
# See COPYING for license details.

pkgbase=python-bintrees
pkgname=('python2-bintrees' 'python-bintrees')
_pkgname=bintrees
pkgver=2.1.0
pkgrel=2
pkgdesc='Package provides Binary-, RedBlack- and AVL-Trees in Python and Cython.'
arch=('any')
url='https://pypi.org/project/bintrees/#files'
license=('MIT')
makedepends=('python2-setuptools' 'python-setuptools')
source=("https://files.pythonhosted.org/packages/ba/a2/91500400b145c8ee668992654ff43afec3ebbb8ed80bc5f0717dd07a7349/$_pkgname-$pkgver.zip")
sha512sums=('56d83e826992915c4085d76daa767e033a221ff346cac307c0a820d692d85e503517f1c339deccf315d0985ff6b4a8ebd139c173d054802586a9b68a7783bc01')

prepare() {
  cp -a "$_pkgname-$pkgver"{,-2}
}

build() {
  cd "$_pkgname-$pkgver-2"

  python2 setup.py build

  cd "$srcdir/$_pkgname-$pkgver"

  python setup.py build
}

package_python2-bintrees() {
  depends=('python2')

  cd "$_pkgname-$pkgver-2"

  python2 setup.py install --prefix=/usr --root="$pkgdir" --optimize=1 \
    --skip-build
}

package_python-bintrees() {
  depends=('python')

  cd "$_pkgname-$pkgver"

  python setup.py install --prefix=/usr --root="$pkgdir" --optimize=1 \
    --skip-build
}

