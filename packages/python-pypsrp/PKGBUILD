# This file is part of BlackArch Linux ( https://www.blackarch.org/ ).
# See COPYING for license details.

pkgname=python-pypsrp
_pkgname=pypsrp
pkgver=v0.5.0.r5.gc704874
pkgrel=2
pkgdesc='PowerShell Remoting Protocol for Python.'
arch=('x86_64')
url='https://github.com/jborean93/pypsrp'
license=('MIT')
depends=('python' 'krb5' 'python-cryptography' 'python-ntlm-auth'
         'python-six' 'python-requests')
makedepends=('python-setuptools' 'git' 'python-pip' 'python-wheel')
optdepends=('python-requests-credssp: credssp')
options=(!emptydirs)
source=("git+https://github.com/jborean93/$_pkgname#commit=c70487480cbb651a89ee7d3efe5dfd21014ecc92")
sha512sums=('SKIP')

pkgver() {
  cd $_pkgname

  git describe --long --tags | sed 's/\([^-]*-g\)/r\1/;s/-/./g'
}

build() {
  cd $_pkgname

  python setup.py build
}

package() {
  cd "$_pkgname"

  python setup.py install --root="$pkgdir" --prefix=/usr -O1 --skip-build
}
