# This file is part of BlackArch Linux ( http://blackarch.org ).
# See COPYING for license details.

pkgname=hate-crack
pkgver=51.47ba42c
pkgrel=1
pkgdesc='A tool for automating cracking methodologies through Hashcat.'
groups=('blackarch' 'blackarch-automation' 'blackarch-cracker')
arch=('any')
url='https://github.com/trustedsec/hate_crack'
license=('custom:unknown')
depends=('python2' 'pygtk' 'python2-pyenchant')
makedepends=('git')
source=('git+https://github.com/trustedsec/hate_crack.git')
sha512sums=('SKIP')

pkgver() {
  cd "$srcdir/hate_crack"

  echo $(git rev-list --count HEAD).$(git rev-parse --short HEAD)
}

prepare() {
  cd "$srcdir/hate_crack"

  sed -i 's|/Passwords/hashcat|/usr/bin|' config.json
}

package() {
  cd "$srcdir/hate_crack"

  install -dm 755 "$pkgdir/usr/bin"
  install -dm 755 "$pkgdir/usr/share/hate-crack"

  install -Dm644 -t "$pkgdir/usr/share/doc/hate-crack/" readme.md

  rm readme.md

  cp -a * "$pkgdir/usr/share/hate-crack/"

  cat > "$pkgdir/usr/bin/hate-crack" << EOF
#!/bin/sh
cd /usr/share/hate-crack
exec python2 hate_crack.py "\${@}"
EOF

  chmod a+x "$pkgdir/usr/bin/hate-crack"
}
