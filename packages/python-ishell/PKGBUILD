# This file is part of BlackArch Linux ( https://www.blackarch.org/ ).
# See COPYING for license details.

pkgbase=python-ishell
pkgname=('python2-ishell' 'python-ishell')
_pkgname=ishell
pkgver=0.1.7
pkgrel=5
pkgdesc='Build Interactive Shells with Python.'
arch=('any')
url='https://pypi.python.org/pypi/ishell/'
license=('MIT')
makedepends=('python2-setuptools' 'python-setuptools')
source=("https://files.pythonhosted.org/packages/5e/e8/a06a152bb609ffe4d17ba1615ac311aa8984785c324b026c109fe2360f9e/$_pkgname-$pkgver.tar.gz")
sha512sums=('f5e34e9e3c6f05d629ff494196e3f0cab082d1a6deba2d6af890f537e8079e0c150ae4db70f1b3380322aefe61c6cf936b5a2943a08e61e5ac1ea29b952c4a13')

prepare() {
  cp -a "$_pkgname-$pkgver"{,-2}
}

build() {
  cd "$_pkgname-$pkgver-2"

  python2 setup.py build

  cd "$srcdir/$_pkgname-$pkgver"

  python setup.py build
}

package_python2-ishell() {
  depends=('python2' 'python2-future')

  cd "$_pkgname-$pkgver-2"

  python2 setup.py install --prefix=/usr --root="$pkgdir" --optimize=1 \
    --skip-build
}

package_python-ishell() {
  depends=('python' 'python-future')

  cd "$_pkgname-$pkgver"

  python setup.py install --prefix=/usr --root="$pkgdir" --optimize=1 \
    --skip-build
}

