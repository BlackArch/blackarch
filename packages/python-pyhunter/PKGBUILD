# This file is part of BlackArch Linux ( https://www.blackarch.org/ ).
# See COPYING for license details.

pkgbase=python-pyhunter
pkgname=('python2-pyhunter' 'python-pyhunter')
_pkgname=pyhunter
pkgver=1.3
pkgrel=1
pkgdesc='An (unofficial) Python wrapper for the Hunter.io API.'
arch=('any')
url='https://pypi.org/project/pyhunter/#files'
license=('MIT')
makedepends=('python2-setuptools' 'python-setuptools')
source=("https://files.pythonhosted.org/packages/34/79/23e3cb5e23ff4d4a610ea6b4434eb3c2338888307ced958aefbe45ebcff2/$_pkgname-$pkgver.tar.gz")
sha512sums=('2d4d8d97bb7f5305d099478df74729236cb31df2df695dc93d6399f512748609799198de5abfc22d23da5afdd2426603b14e91b1c20f7fcaacbef22455979b54')

prepare() {
  cp -a "$_pkgname-$pkgver"{,-2}
}

build() {
  cd "$_pkgname-$pkgver-2"

  python2 setup.py build

  cd "$srcdir/$_pkgname-$pkgver"

  python setup.py build
}

package_python2-pyhunter() {
  depends=('python2' 'python2-requests')

  cd "$_pkgname-$pkgver-2"

  python2 setup.py install --prefix=/usr --root="$pkgdir" --optimize=1 \
    --skip-build
}

package_python-pyhunter() {
  depends=('python' 'python-requests')

  cd "$_pkgname-$pkgver"

  python setup.py install --prefix=/usr --root="$pkgdir" --optimize=1 \
    --skip-build
}

