# This file is part of BlackArch Linux ( https://www.blackarch.org/ ).
# See COPYING for license details.

pkgbase=python-pyhunter
pkgname=('python2-pyhunter' 'python-pyhunter')
_pkgname=pyhunter
pkgver=0.3
pkgrel=1
pkgdesc='An (unofficial) Python wrapper for the Hunter.io API.'
arch=('any')
url='https://pypi.org/project/pyhunter/#files'
license=('MIT')
makedepends=('python2-setuptools' 'python-setuptools')
source=("https://files.pythonhosted.org/packages/7e/ac/82cb7bc8ecdf6c1966d87e89c3c90a4c86c3ba42042e0454db7854ae3689/$_pkgname-$pkgver.tar.gz")
sha512sums=('c59221a34c9b417a63b44670cd34f2e4655062aae759c4bac4463e6cd186f8df44d516e5d63e66de6e18074b56a13c700889e074cdb3887b31abc7a7566633a3')

prepare() {
  cp -a "$_pkgname-$pkgver"{,-2}
}

build() {
  cd "$_pkgname-$pkgver-2"

  python2 setup.py build

  cd "$srcdir/$_pkgname-$pkgver"

  python setup.py build
}

package_python2-pyhunter() {
  depends=('python2' 'python2-requests')

  cd "$_pkgname-$pkgver-2"

  python2 setup.py install --prefix=/usr --root="$pkgdir" --optimize=1 \
    --skip-build
}

package_python-pyhunter() {
  depends=('python' 'python-requests')

  cd "$_pkgname-$pkgver"

  python setup.py install --prefix=/usr --root="$pkgdir" --optimize=1 \
    --skip-build
}

