# This file is part of BlackArch Linux ( https://www.blackarch.org/ ).
# See COPYING for license details.

pkgbase=python-pyhunter
pkgname=('python2-pyhunter' 'python-pyhunter')
_pkgname=pyhunter
pkgver=1.2
pkgrel=1
pkgdesc='An (unofficial) Python wrapper for the Hunter.io API.'
arch=('any')
url='https://pypi.org/project/pyhunter/#files'
license=('MIT')
makedepends=('python2-setuptools' 'python-setuptools')
source=("https://files.pythonhosted.org/packages/cf/44/a3d6e1968ac5be330c73ee18a6aae6c49caa5eef4683a2e7740263e95515/$_pkgname-$pkgver.tar.gz")
sha512sums=('48342106a09cb442723cd5fea1e11357047ef8609bd78ea002453688c7257aea8b98b2bbe769b9152e4468c2514ba8bcf741810dac7c990d33848aa35e48da77')

prepare() {
  cp -a "$_pkgname-$pkgver"{,-2}
}

build() {
  cd "$_pkgname-$pkgver-2"

  python2 setup.py build

  cd "$srcdir/$_pkgname-$pkgver"

  python setup.py build
}

package_python2-pyhunter() {
  depends=('python2' 'python2-requests')

  cd "$_pkgname-$pkgver-2"

  python2 setup.py install --prefix=/usr --root="$pkgdir" --optimize=1 \
    --skip-build
}

package_python-pyhunter() {
  depends=('python' 'python-requests')

  cd "$_pkgname-$pkgver"

  python setup.py install --prefix=/usr --root="$pkgdir" --optimize=1 \
    --skip-build
}

