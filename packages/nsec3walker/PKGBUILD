# This file is part of BlackArch Linux ( http://blackarch.org ).
# See COPYING for license details.

pkgname='nsec3walker'
pkgver='20101223'
pkgrel=5
groups=('blackarch' 'blackarch-recon')
pkgdesc="Enumerates domain names using DNSSEC"
url='http://dnscurve.org/nsec3walker.html'
arch=('x86_64' 'armv6h' 'armv7h' 'aarch64')
license=('custom')
source=("http://dnscurve.org/nsec3walker-${pkgver}.tar.gz")
sha1sums=('b59c8ee5f9ec94628e40e5f94896b593de914fd3')

prepare() {
  cd "$srcdir/nsec3walker-$pkgver"

  sed -i 's|env python|env python2|' *
  sed -i 's|collect.todo|/tmp/collect.todo|' collect
}

build() {
  cd "$srcdir/nsec3walker-$pkgver"

  make
}

package() {
  cd "$srcdir/nsec3walker-$pkgver"

  mkdir -p "$pkgdir/usr/bin"
  mkdir -p "$pkgdir/usr/share/nsec3walker"

  for bin in collect dicthashes query randomhashes unhash ; do
    install -m755 $bin "$pkgdir/usr/share/nsec3walker"
    cat > "$pkgdir/usr/bin/nsec3walker-$bin" << EOF
#!/bin/sh
cd /usr/share/nsec3walker
exec ./$bin "\$@"
EOF
    chmod +x "$pkgdir/usr/bin/nsec3walker-$bin"
  done
}
