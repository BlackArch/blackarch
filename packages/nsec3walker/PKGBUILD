# This file is part of BlackArch Linux ( http://blackarch.org ).
# See COPYING for license details.

pkgname=nsec3walker
pkgver=20101223
pkgrel=5
groups=('blackarch' 'blackarch-recon')
pkgdesc="Enumerates domain names using DNSSEC"
url='http://dnscurve.org/nsec3walker.html'
arch=('x86_64' 'armv6h' 'armv7h' 'aarch64')
license=('custom')
source=("http://dnscurve.org/nsec3walker-${pkgver}.tar.gz")
sha512sums=('19378a2d37b9f796ee5834d2765e968e2bbf98e110495dda809aa6d7ea610d674332af340b084c947070d969a63ed53ece61825143ecd4571cb7c8f701b79d92')

prepare() {
  cd "$srcdir/nsec3walker-$pkgver"

  sed -i 's|env python|env python2|' *
  sed -i 's|collect.todo|/tmp/collect.todo|' collect
}

build() {
  cd "$srcdir/nsec3walker-$pkgver"

  make
}

package() {
  cd "$srcdir/nsec3walker-$pkgver"

  install -dm 755 "$pkgdir/usr/bin"
  install -dm 755 "$pkgdir/usr/share/nsec3walker"

  for bin in collect dicthashes query randomhashes unhash ; do
    install -m755 $bin "$pkgdir/usr/share/nsec3walker"
    cat > "$pkgdir/usr/bin/nsec3walker-$bin" << EOF
#!/bin/sh
cd /usr/share/nsec3walker
exec ./$bin "\$@"
EOF
    chmod +x "$pkgdir/usr/bin/nsec3walker-$bin"
  done
}
