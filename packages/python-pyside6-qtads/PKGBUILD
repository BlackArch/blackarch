# This file is part of BlackArch Linux ( https://www.blackarch.org/ ).
# See COPYING for license details.

pkgname=python-pyside6-qtads
_pkgname=PySide6-QtAds
pkgver=4.0.1
pkgrel=1
pkgdesc='PySide6 bindings to Qt Advanced Docking System.'
arch=('any')
url='https://github.com/mborgerson/pyside6_qtads/'
license=('LGPL')
depends=('python' 'pyside6')
makedepends=('python-build' 'python-pip' 'python-setuptools' 'python-wheel'
             'python-setuptools-scm' 'python-cmake-build-extension'
             'shiboken6' 'shiboken6-generator')
options=(!emptydirs)
source=("https://files.pythonhosted.org/packages/source/${_pkgname::1}/$_pkgname/$_pkgname-$pkgver.tar.gz")
sha512sums=('837791209351fd3d1db0f319036f3465a27e26290bc989d2df95d1c7f58988a73dde685a5d2b2afcf5b7681bec0ba3f523265d2996976dfa63ec611c6d6c3231')

build() {
  cd "$_pkgname-$pkgver"

  python -m build --wheel --outdir="$startdir/dist"
}

package() {
  cd "$_pkgname-$pkgver"

  pip install \
    --verbose \
    --disable-pip-version-check \
    --no-warn-script-location \
    --ignore-installed \
    --no-compile \
    --no-deps \
    --root="$pkgdir" \
    --prefix=/usr \
    --no-index \
    --find-links="file://$startdir/dist" \
    "$_pkgname"
}

