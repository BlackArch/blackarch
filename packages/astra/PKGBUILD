# This file is part of BlackArch Linux ( http://blackarch.org ).
# See COPYING for license details.

pkgname=astra
pkgver=226.3fa84ca
pkgrel=1
pkgdesc="Automated Security Testing For REST API's."
groups=('blackarch' 'blackarch-webapp' 'blackarch-fuzzer')
arch=('any')
url='https://github.com/flipkart-incubator/astra'
license=('Apache2')
depends=('python2' 'python2-requests' 'python2-pymongo' 'python2-pyjwt'
         'python2-flask' 'python2-argparse' 'sqlmap' 'mongodb')
makedepends=('git')
source=('git+https://github.com/flipkart-incubator/astra.git')
sha512sums=('SKIP')

pkgver() {
  cd "$srcdir/astra"

  echo $(git rev-list --count HEAD).$(git rev-parse --short HEAD)
}

package() {
  cd "$srcdir/astra"

  install -dm 755 "$pkgdir/usr/bin"
  install -dm 755 "$pkgdir/usr/share/astra"

  install -Dm644 -t "$pkgdir/usr/share/doc/astra/" README.md docs/*
  install -Dm644 LICENSE "$pkgdir/usr/share/licenses/astra/LICENSE"

  rm -rf README.md LICENSE docs

  cp -a * "$pkgdir/usr/share/astra/"

  cat > "$pkgdir/usr/bin/astra" << EOF
#!/bin/sh
cd /usr/share/astra
exec python2 astra.py "\${@}"
EOF

  chmod a+x "$pkgdir/usr/bin/astra"
}
