# This file is part of BlackArch Linux ( https://www.blackarch.org/ ).
# See COPYING for license details.

pkgname=ruler
pkgver=301.1e5ee2d
pkgrel=2
groups=('blackarch' 'blackarch-webapp' 'blackarch-exploitation')
pkgdesc='A tool to abuse Exchange services.'
arch=('x86_64' 'aarch64')
url='https://github.com/sensepost/ruler'
license=('custom:unknown')
makedepends=('git' 'go')
source=("git+https://github.com/sensepost/$pkgname.git")
sha512sums=('SKIP')

pkgver() {
  cd $pkgname

  echo $(git rev-list --count HEAD).$(git rev-parse --short HEAD)
}

build() {
  cd $pkgname

  GOPATH="$srcdir" go mod download
  GOPATH="$srcdir" go build \
    -trimpath \
    -buildmode=pie \
    -mod=readonly \
    -modcacherw \
    -ldflags "-s -w" \
    -o $pkgname .
}

package() {
  cd $pkgname

  install -dm 755 "$pkgdir/usr/share/$pkgname"

  install -Dm 755 $pkgname "$pkgdir/usr/bin/$pkgname"
  install -Dm 644 -t "$pkgdir/usr/share/doc/$pkgname/" README.md
  install -Dm 644 LICENSE "$pkgdir/usr/share/licenses/$pkgname/LICENSE"

  cp -a templates utils "$pkgdir/usr/share/$pkgname/"
}

