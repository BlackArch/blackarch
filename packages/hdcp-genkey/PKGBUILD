# This file is part of BlackArch Linux ( https://www.blackarch.org/ ).
# See COPYING for license details.

pkgname=hdcp-genkey
pkgver=18.e8d342d
pkgrel=5
groups=('blackarch' 'blackarch-crypto' 'blackarch-cracker')
pkgdesc='Generate HDCP source and sink keys from the leaked master key.'
arch=('any')
url='https://github.com/rjw57/hdcp-genkey'
license=('custom:unknown')
depends=('python2' 'python2-simplejson')
makedepends=('git')
source=("git+https://github.com/rjw57/$pkgname.git")
sha512sums=('SKIP')

pkgver() {
  cd $pkgname

  echo $(git rev-list --count HEAD).$(git rev-parse --short HEAD)
}

prepare() {
  cd $pkgname

  sed -i 's|/usr/bin/env python|/usr/bin/env python2|' generate_key.py
}

package() {
  cd $pkgname

  install -dm 755 "$pkgdir/usr/bin"
  install -dm 755 "$pkgdir/usr/share/$pkgname"

  install -Dm 644 -t "$pkgdir/usr/share/doc/$pkgname/" README.md

  rm README.md

  cp -a * "$pkgdir/usr/share/$pkgname/"

  cat > "$pkgdir/usr/bin/$pkgname" << EOF
#!/bin/sh
cd /usr/share/$pkgname
exec python2 generate_key.py "\$@"
EOF

  chmod +x "$pkgdir/usr/bin/$pkgname"
}

