# This file is part of BlackArch Linux ( https://www.blackarch.org/ ).
# See COPYING for license details.

pkgname=donpapi
_pkgname=DonPAPI
pkgver=v1.0.0.r45.gf724f82
pkgrel=1
pkgdesc='Dumping revelant information on compromised targets without AV detection with DPAPI.'
arch=('any')
groups=('blackarch' 'blackarch-windows' 'blackarch-exploitation')
url='https://github.com/login-securite/DonPAPI'
license=('custom:unknown')
depends=('python' 'impacket' 'python-pyasn' 'python-lnkparse3' 'python-wheel'
         'python-m2crypto' 'python-pycryptodome')
makedepends=('python-build' 'python-pip' 'git')
source=("git+https://github.com/login-securite/DonPAPI.git")
sha512sums=('SKIP')

pkgver() {
  cd $_pkgname

  git describe --long --tags | sed 's/\([^-]*-g\)/r\1/;s/-/./g'
}

build() {
  cd $_pkgname

  python -m build --wheel --outdir="$startdir/dist"
}

package() {
  cd $_pkgname

  pip install --verbose \
              --disable-pip-version-check \
              --no-warn-script-location \
              --ignore-installed \
              --no-compile \
              --no-deps \
              --root="$pkgdir" \
              --prefix=/usr \
              --no-index \
              --find-links="file://$startdir/dist" \
              "$pkgname"
}

