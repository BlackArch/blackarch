# This file is part of BlackArch Linux ( https://www.blackarch.org/ ).
# See COPYING for license details.

pkgname=eos
pkgver=8.7b85ee1
pkgrel=1
pkgdesc='Enemies Of Symfony - Debug mode Symfony looter.'
arch=('any')
groups=('blackarch' 'blackarch-webapp' 'blackarch-scanner')
url='https://github.com/synacktiv/eos'
license=('custom:THE BEER-WARE')
depends=('python' 'python-requests' 'python-beautifulsoup4' 'python-defusedxml')
makedepends=('python-setuptools' 'git')
source=("git+https://github.com/synacktiv/$pkgname")
sha512sums=('SKIP')

pkgver() {
  cd $pkgname

  echo $(git rev-list --count HEAD).$(git rev-parse --short HEAD)
}

build() {
  cd $pkgname

  python setup.py build
}

package() {
  cd $pkgname

  python setup.py install --root="$pkgdir" --prefix=/usr -O1 --skip-build
}

