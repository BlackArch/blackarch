# This file is part of BlackArch Linux ( http://blackarch.org ).
# See COPYING for license details.

pkgname=amap
pkgver=5.4
pkgrel=5
epoch=1
pkgdesc='Next-generation tool for assisting network penetration testing.'
arch=('x86_64' 'armv6h' 'armv7h' 'aarch64')
license=('GPL2' 'custom')
depends=('pcre' 'openssl' 'ca-certificates')
url='http://freeworld.thc.org/releases.php'
makedepends=('git')
source=("git+https://github.com/BlackArch/amap.git"
        'pcre.patch')
sha512sums=('SKIP'
            'd54886d9e612673e23edb5d9a82b264f68c3a77ff728ca3c1faff8a7f6ff1d17a1c9afb4e5b178783858896398987a9a8ab57b2fded6bc975bc51db0d6d59ee0')

prepare() {
  cd "$srcdir/amap"

  patch -p1 < "$srcdir/pcre.patch"
}

build() {
  cd "$srcdir/amap"

  ./configure --prefix=/usr/share/amap/

  make
}

package() {
  cd "$srcdir/amap"

  install -dm 755 "$pkgdir/usr/share/amap/etc"

  install -Dm755 amap "$pkgdir/usr/bin/amap"
  ln "$pkgdir/usr/bin/amap" "$pkgdir/usr/bin/amap6"
  install -Dm755 amapcrap "$pkgdir/usr/bin/amapcrap"

  install -Dm644 appdefs.* "$pkgdir/usr/share/amap/etc"

  install -Dm644 amap.1 "$pkgdir/usr/share/man/man1/amap.1"

  install -Dm644 LICENCE.AMAP "$pkgdir/usr/share/licenses/amap/LICENCE"
}
