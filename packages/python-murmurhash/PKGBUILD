# This file is part of BlackArch Linux ( https://www.blackarch.org/ ).
# See COPYING for license details.

pkgbase=python-murmurhash
pkgname=('python-murmurhash' 'python2-murmurhash')
_pkgname=murmurhash
pkgver=1.0.5
pkgrel=1
pkgdesc='Cython bindings for MurmurHash2.'
arch=('x86_64')
url='https://pypi.org/project/murmurhash/'
license=('MIT')
makedepends=('python-setuptools' 'python2-setuptools')
source=("https://files.pythonhosted.org/packages/source/${_pkgname::1}/$_pkgname/$_pkgname-$pkgver.tar.gz")
sha512sums=('fb59d94856514d53482bc2b147a202eeab345461b7fd7ddd694eef8043bc68c6b9235f25c04a461a60881fa9b1303ab9a32b894dbb3c78496f627a8934b6272e')

prepare() {
  cp -a "$_pkgname-$pkgver"{,-2}
}

build() {
  cd "$_pkgname-$pkgver-2"

  python2 setup.py build

  cd "$srcdir/$_pkgname-$pkgver"

  python setup.py build
}

package_python2-murmurhash() {
  depends=('python2' 'python2-pytest' 'cython2')

  cd "$_pkgname-$pkgver-2"

  python2 setup.py install --root="$pkgdir" --prefix=/usr -O1 --skip-build

  install -Dm 644 LICENSE "$pkgdir/usr/share/licenses/$pkgname/LICENSE"
}

package_python-murmurhash() {
  depends=('python' 'python-pytest' 'cython')

  cd "$_pkgname-$pkgver"

  python setup.py install --root="$pkgdir" --prefix=/usr -O1 --skip-build

  install -Dm 644 LICENSE "$pkgdir/usr/share/licenses/$pkgname/LICENSE"
}

