# This file is part of BlackArch Linux ( http://blackarch.org ).
# See COPYING for license details.

pkgname='sickle'
pkgver=27.603457a
pkgrel=1
pkgdesc='A shellcode development tool, created to speed up the various steps needed to create functioning shellcode.'
groups=('blackarch' 'blackarch-exploitation')
arch=('any')
url='https://github.com/wetw0rk/Sickle'
license=('MIT')
depends=('python2')
makedepends=('git')
source=('git+https://github.com/wetw0rk/Sickle.git')
sha1sums=('SKIP')

pkgver() {
  cd "$srcdir/Sickle"

  echo $(git rev-list --count HEAD).$(git rev-parse --short HEAD)
}

package() {
  cd "$srcdir/Sickle"

  mkdir -p "$pkgdir/usr/share/sickle"

  install -Dm755 sickle.py "$pkgdir/usr/bin/sickle"
  install -Dm644 -t "$pkgdir/usr/share/doc/sickle/" README.md
  install -Dm644 LICENSE "$pkgdir/usr/share/licenses/sickle/LICENSE"

  rm sickle.py LICENSE README.md

  cp -a * "$pkgdir/usr/share/sickle"
}
