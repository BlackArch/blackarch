# This file is part of BlackArch Linux ( http://blackarch.org ).
# See COPYING for license details.

pkgname=dpeparser
pkgver=beta002
pkgrel=3
epoch=1
pkgdesc="Default password enumeration project"
arch=('any')
url="http://www.toolswatch.org/dpe/"
license=('GPL')
groups=('blackarch' 'blackarch-cracker')
depends=('python2')
source=('http://www.toolswatch.org/dpe/dpeparser.py')
sha512sums=('f941a536b7eb05f589dbbc1533d07dbb10a50776f197aca5b5158b263e216603e833ce315f73b1f3d0fe59ea10578dcd1059b998ca1db27c0bd4c8c4239b9f27')

prepare() {
  cd "$srcdir"

  sed -i 's#MydpeDB ="dpe_db.xml"#MydpeDB = "/usr/share/dpeparser/dpe_db.xml"#' dpeparser.py
}

build() {
  cd "$srcdir"

  python2 dpeparser.py -u
}

package() {
  cd "$srcdir"

  install -dm 755 "$pkgdir/usr/bin"
  install -dm 755 "$pkgdir/usr/share/dpeparser"

  install -m755 dpeparser.py "$pkgdir/usr/share/dpeparser"
  install -m644 dpe_db.xml "$pkgdir/usr/share/dpeparser"

  cat > "$pkgdir/usr/bin/dpeparser" <<EOF
#!/bin/bash
exec python2 /usr/share/dpeparser/dpeparser.py "\$@"
EOF
  chmod 755 "$pkgdir/usr/bin/dpeparser"
}
