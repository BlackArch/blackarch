# This file is part of BlackArch Linux ( https://www.blackarch.org/ ).
# See COPYING for license details.

pkgbase=python-statsd
pkgname=('python2-statsd' 'python-statsd')
_pkgname=statsd
pkgver=3.3.0
pkgrel=2
pkgdesc='A Python client for statsd.'
arch=('any')
url='https://pypi.org/project/statsd/#files'
license=('MIT')
makedepends=('python2-setuptools' 'python-setuptools')
options=(!emptydirs)
source=("https://files.pythonhosted.org/packages/2d/f2/48ffc8d0051849e4417e809dc9420e76084c8a62749b3442915402127caa/$_pkgname-$pkgver.tar.gz")
sha512sums=('e2693bc7f179e275c53044d13a4685dd72ebd47adafcab5064e803fbf9e8df4a0f20f61c3695846d3c33178be17dd7286f487213fa2bd8545ed1612e200c8f36')

prepare() {
  cp -a "$_pkgname-$pkgver"{,-2}
}

build() {
  cd "$_pkgname-$pkgver-2"

  python2 setup.py build

  cd "$srcdir/$_pkgname-$pkgver"

  python setup.py build
}

package_python2-statsd() {
  depends=('python2' 'python2-mock' 'python2-nose' 'python2-flake8')

  cd "$_pkgname-$pkgver-2"

  python2 setup.py install --root="$pkgdir" --prefix=/usr -O1 --skip-build
}

package_python-statsd() {
  depends=('python' 'python-mock' 'python-nose' 'flake8')

  cd "$_pkgname-$pkgver"

  python setup.py install --root="$pkgdir" --prefix=/usr -O1 --skip-build
}

