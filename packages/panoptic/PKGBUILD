pkgname=panoptic
pkgver=0.r167.9e81e72
pkgrel=2
pkgdesc="A tool that automates the process of search and retrieval of content for common log and config files through LFI vulnerability"
url="https://github.com/lightos/Panoptic"
arch=('any')
license=('MIT')
depends=('python2')
source=("git+https://github.com/lightos/Panoptic.git")
md5sums=('SKIP')

pkgver() {
  cd "$srcdir/Panoptic"
  printf "0.r%s.%s" "$(git rev-list --count HEAD)" "$(git rev-parse --short HEAD)"
}

prepare(){
  grep -iRl 'python' "$srcdir/Panoptic" | xargs sed -i 's|#!.*/usr/bin/python|#!/usr/bin/python2|;s|#!.*/usr/bin/env python$|#!/usr/bin/env python2|'
}

package() {
  cd "$srcdir/Panoptic"

  # Base directories.
  install -dm755 "$pkgdir/usr/share/panoptic"
  install -dm755 "$pkgdir/usr/bin/"

  cp -a --no-preserve=ownership * "$pkgdir/usr/share/panoptic"

cat > "$pkgdir/usr/bin/panoptic" << EOF
#!/bin/sh
cd /usr/share/panoptic/
python2 panoptic.py "\$@"
EOF
  chmod +x "$pkgdir/usr/bin/panoptic"
}
