# This file is part of BlackArch Linux ( http://blackarch.org ).
# See COPYING for license details.

pkgname='panoptic'
pkgver=185.df35a6c
pkgrel=1
groups=('blackarch' 'blackarch-automation')
pkgdesc='A tool that automates the process of search and retrieval of content for common log and config files through LFI vulnerability.'
url='https://github.com/lightos/Panoptic'
arch=('any')
license=('MIT')
depends=('python2')
makedepends=('git')
source=('git+https://github.com/lightos/Panoptic.git')
sha1sums=('SKIP')

pkgver() {
  cd "$srcdir/Panoptic"

  echo $(git rev-list --count HEAD).$(git rev-parse --short HEAD)
}

prepare() {
  cd "$srcdir"

  grep -iRl 'python' "$srcdir/Panoptic" |
  xargs sed -i 's|#!.*/usr/bin/python|#!/usr/bin/python2|;s|#!.*/usr/bin/env python$|#!/usr/bin/env python2|'
}

package() {
  cd "$srcdir/Panoptic"

  mkdir -p "$pkgdir/usr/share/panoptic"
  mkdir -p "$pkgdir/usr/bin/"

  cp -a --no-preserve=ownership * "$pkgdir/usr/share/panoptic"

cat > "$pkgdir/usr/bin/panoptic" << EOF
#!/bin/sh
cd /usr/share/panoptic/
exec python2 panoptic.py "\$@"
EOF

  chmod +x "$pkgdir/usr/bin/panoptic"
}
