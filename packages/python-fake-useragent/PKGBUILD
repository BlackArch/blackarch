# This file is part of BlackArch Linux ( https://www.blackarch.org/ ).
# See COPYING for license details.

pkgbase=python-fake-useragent
pkgname=('python2-fake-useragent' 'python-fake-useragent')
_pkgname=fake-useragent
pkgver=1.5.1
pkgrel=1
pkgdesc='Up to date simple useragent faker with real world database.'
arch=('any')
url='https://pypi.org/project/fake-useragent/#files'
license=('Apache')
makedepends=('python2-setuptools' 'python-setuptools')
source=("https://files.pythonhosted.org/packages/source/${_pkgname::1}/$_pkgname/$_pkgname-$pkgver.tar.gz")
sha512sums=('304615863dda069297ae06c199639a86dfecd8355240a6338e7935aab2bdb6bc5bac49e8462be27f2ebcd23b34cab1c126cd3e4acab5c917c0a3f50ebfa6fbf2')

prepare() {
  cp -a "$_pkgname-$pkgver"{,-2}
}

build() {
  cd "$_pkgname-$pkgver-2"

  python2 setup.py build

  cd "$srcdir/$_pkgname-$pkgver"

  python setup.py build
}

package_python2-fake-useragent() {
  depends=('python2')

  cd "$_pkgname-$pkgver-2"

  python2 setup.py install --root="$pkgdir" --prefix=/usr -O1 --skip-build
}

package_python-fake-useragent() {
  depends=('python')

  cd "$_pkgname-$pkgver"

  python setup.py install --root="$pkgdir" --prefix=/usr -O1 --skip-build
}

