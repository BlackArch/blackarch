# This file is part of BlackArch Linux ( https://www.blackarch.org/ ).
# See COPYING for license details.

pkgbase=python-fake-useragent
pkgname=('python2-fake-useragent' 'python-fake-useragent')
_pkgname=fake-useragent
pkgver=1.2.1
pkgrel=1
pkgdesc='Up to date simple useragent faker with real world database.'
arch=('any')
url='https://pypi.org/project/fake-useragent/#files'
license=('Apache')
makedepends=('python2-setuptools' 'python-setuptools')
source=("https://files.pythonhosted.org/packages/source/${_pkgname::1}/$_pkgname/$_pkgname-$pkgver.tar.gz")
sha512sums=('877cc7b7bf9fbc0028fa8117af8e420af65315fdfff26d5cb0999bb2de44ea7a347f758ab614b6f20f374940f09040c6edb0754125ac04dce006f8aa49f526ae')

prepare() {
  cp -a "$_pkgname-$pkgver"{,-2}
}

build() {
  cd "$_pkgname-$pkgver-2"

  python2 setup.py build

  cd "$srcdir/$_pkgname-$pkgver"

  python setup.py build
}

package_python2-fake-useragent() {
  depends=('python2')

  cd "$_pkgname-$pkgver-2"

  python2 setup.py install --prefix=/usr --root="$pkgdir" --optimize=1 \
    --skip-build
}

package_python-fake-useragent() {
  depends=('python')

  cd "$_pkgname-$pkgver"

  python setup.py install --prefix=/usr --root="$pkgdir" --optimize=1 \
    --skip-build
}

