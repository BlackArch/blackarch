# This file is part of BlackArch Linux ( https://www.blackarch.org/ ).
# See COPYING for license details.

pkgbase=python-fake-useragent
pkgname=('python2-fake-useragent' 'python-fake-useragent')
_pkgname=fake-useragent
pkgver=1.3.0
pkgrel=1
pkgdesc='Up to date simple useragent faker with real world database.'
arch=('any')
url='https://pypi.org/project/fake-useragent/#files'
license=('Apache')
makedepends=('python2-setuptools' 'python-setuptools')
source=("https://files.pythonhosted.org/packages/source/${_pkgname::1}/$_pkgname/$_pkgname-$pkgver.tar.gz")
sha512sums=('c399495a4e5b4e9d82538bd420001bf5f9df28abb35263f0766b675f2f00d21bd4317812a63417cba2fd9e1c10aa830b57b5100ef162a3e7258b89f45084d1ec')

prepare() {
  cp -a "$_pkgname-$pkgver"{,-2}
}

build() {
  cd "$_pkgname-$pkgver-2"

  python2 setup.py build

  cd "$srcdir/$_pkgname-$pkgver"

  python setup.py build
}

package_python2-fake-useragent() {
  depends=('python2')

  cd "$_pkgname-$pkgver-2"

  python2 setup.py install --root="$pkgdir" --prefix=/usr -O1 --skip-build
}

package_python-fake-useragent() {
  depends=('python')

  cd "$_pkgname-$pkgver"

  python setup.py install --root="$pkgdir" --prefix=/usr -O1 --skip-build
}

