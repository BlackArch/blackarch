# This file is part of BlackArch Linux ( http://blackarch.org ).
# See COPYING for license details.

pkgname='maskprocessor'
pkgver='0.71'
pkgrel=1
groups=('blackarch' 'blackarch-automation' 'blackarch-misc')
pkgdesc='A High-Performance word generator with a per-position configurable charset.'
arch=('i686' 'x86_64' 'armv6h' 'armv7h')
url='http://hashcat.net/wiki/doku.php?id=maskprocessor'
license=('custom')
makedepends=('p7zip')
source=("http://hashcat.net/tools/maskprocessor/maskprocessor-${pkgver}.7z")
sha1sums=('7f483d1b3eb96ef8326fd64a34338c0925ea1e57')

prepare() {
  cd "$srcdir/maskprocessor-$pkgver"

  sed -i 's|dicts/|"$1"/|' generate.sh
  sed -i 's|./mp64.bin|/usr/bin/maskprocessor|' generate.sh
}

package() {
  cd "$srcdir/maskprocessor-$pkgver"

  mkdir -p "$pkgdir/usr/bin"
  mkdir -p "$pkgdir/usr/share/maskprocessor"

  if [[ "$CARCH" == 'x86_64' ]]; then
    install -m755 mp64.bin "$pkgdir/usr/bin/mp64"
    ln -s mp64 "$pkgdir/usr/bin/maskprocessor"
  elif [[ "$CARCH" == 'i686' ]]; then
    install -m755 mp32.bin "$pkgdir/usr/bin/mp32"
    ln -s mp32 "$pkgdir/usr/bin/maskprocessor"
  fi

  install -m755 generate.sh "$pkgdir/usr/share/maskprocessor"
}
