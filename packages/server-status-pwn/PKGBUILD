# This file is part of BlackArch Linux ( http://blackarch.org ).
# See COPYING for license details.

pkgname='server-status-pwn'
pkgver=5.1067aac
pkgrel=2
pkgdesc='A script that monitors and extracts requested URLs and clients connected to the service by exploiting publicly accessible Apache server-status instances.'
groups=('blackarch' 'blackarch-recon')
arch=('any')
url='https://github.com/mazen160/server-status_PWN'
license=('MIT')
depends=('python' 'python-argparse' 'python-beautifulsoup4' 'python-requests')
makedepends=('git')
source=('git+https://github.com/mazen160/server-status_PWN.git')
sha1sums=('SKIP')

pkgver() {
  cd "$srcdir/server-status_PWN"

  echo $(git rev-list --count HEAD).$(git rev-parse --short HEAD)
}

package() {
  cd "$srcdir/server-status_PWN"

  mkdir -p "$pkgdir/usr/bin"
  mkdir -p "$pkgdir/usr/share/server-status-pwn"

  install -Dm644 -t "$pkgdir/usr/share/doc/server-status-pwn/" README.md
  install -Dm644 LICENSE.txt \
    "$pkgdir/usr/share/licenses/server-status-pwn/LICENSE.txt"

  rm README.md LICENSE.txt

  cp -a * "$pkgdir/usr/share/server-status-pwn/"

  cat > "$pkgdir/usr/bin/server-status-pwn" << EOF
#!/bin/sh
cd /usr/share/server-status-pwn
exec python server-status_PWN.py "\${@}"
EOF

  chmod a+x "$pkgdir/usr/bin/server-status-pwn"
}
