# This file is part of BlackArch Linux ( http://blackarch.org ).
# See COPYING for license details.

pkgname='princeprocessor'
pkgver=130.f0c101d
pkgrel=2
epoch=1
groups=('blackarch' 'blackarch-misc')
pkgdesc='Standalone password candidate generator using the PRINCE algorithm.'
arch=('x86_64' 'armv6h' 'armv7h' 'aarch64')
url='https://github.com/jsteube/princeprocessor/'
license=('MIT')
depends=('lib32-glibc')
makedepends=('git' 'gcc')
source=('git+https://github.com/jsteube/princeprocessor.git')
sha1sums=('SKIP')

pkgver() {
  cd "$srcdir/princeprocessor"

  echo $(git rev-list --count HEAD).$(git rev-parse --short HEAD)
}

build() {
  cd "$srcdir/princeprocessor/src"

  make pp64.bin
}

package() {
  cd "$srcdir/princeprocessor"

  cd src

  install -Dm755 pp64.bin "$pkgdir/usr/bin/princeprocessor"

  cd ..

  install -Dm644 -t "$pkgdir/usr/share/princeprocessor" rules/*
  install -Dm644 -t "$pkgdir/usr/share/doc/princeprocessor/" README.md CHANGES
  install -Dm644 LICENSE "$pkgdir/usr/share/licenses/princeprocessor/LICENSE"
}
