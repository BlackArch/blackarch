# This file is part of BlackArch Linux ( https://www.blackarch.org/ ).
# See COPYING for license details.

pkgname=sfuzz
pkgver=198.3bf135b
pkgrel=1
groups=('blackarch' 'blackarch-fuzzer')
pkgdesc='A simple fuzzer.'
url='http://aconole.brad-x.com/programs/sfuzz.html'
arch=('x86_64' 'armv6h' 'armv7h' 'aarch64')
license=('BSD')
makedepends=('git')
source=('git+https://github.com/orgcandman/Simple-Fuzzer.git')
sha512sums=('SKIP')

pkgver() {
  cd Simple-Fuzzer

  echo $(git rev-list --count HEAD).$(git rev-parse --short HEAD)
}

build() {
  cd Simple-Fuzzer

  ./configure --prefix=/usr

  make
}

package() {
  cd Simple-Fuzzer

  install -dm 755 "$pkgdir/usr/bin"
  install -dm 755 "$pkgdir/usr/share"

  make BIN_DIR="$pkgdir/usr/bin/" SHARE_DIR="$pkgdir/usr/share/" \
    MAN_DIR="$pkgdir/usr/man/man1/" install
}

