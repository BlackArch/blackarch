# This file is part of BlackArch Linux ( http://blackarch.org ).
# See COPYING for license details.

pkgname='bluto'
pkgver=114.3a0dc02
pkgrel=1
epoch=1
groups=('blackarch' 'blackarch-scanner' 'blackarch-recon')
pkgdesc='Recon, Subdomain Bruting, Zone Transfers.'
arch=('any')
url='https://github.com/RandomStorm/Bluto'
license=('GPL3')
depends=('python2' 'python2-dnspython' 'python2-beautifulsoup4'
         'python2-termcolor' 'python2-pythonwhois')
makedepends=('git' 'python2-setuptools')
source=('git+https://github.com/darryllane/Bluto.git')
sha1sums=('SKIP')

pkgver() {
  cd "$srcdir/Bluto"

  echo $(git rev-list --count HEAD).$(git rev-parse --short HEAD)
}

build() {
  cd "$srcdir/Bluto"

  python2 setup.py build
}

package() {
  cd "$srcdir/Bluto"

  python2 setup.py install --root="$pkgdir" --optimize=1

  install -Dm644 README.md "$pkgdir/usr/share/doc/bluto/README.md"
  install -Dm644 LICENSE.txt "$pkgdir/usr/share/licenses/bluto/LICENSE.txt"
}
