# This file is part of BlackArch Linux ( https://www.blackarch.org/ ).
# See COPYING for license details.

pkgname=bluto
pkgver=142.25cad7a
pkgrel=1
epoch=1
groups=('blackarch' 'blackarch-scanner' 'blackarch-recon')
pkgdesc='Recon, Subdomain Bruting, Zone Transfers.'
arch=('any')
url='https://github.com/darryllane/Bluto'
license=('GPL3')
depends=('python2' 'python2-dnspython2' 'python2-beautifulsoup4' 'python2-lxml'
         'python2-termcolor' 'python2-python2whois' 'python2-docopt'
         'python2-requests' 'python2-pdfminer' 'python2-oletools')
makedepends=('git' 'python2-setuptools')
source=("$pkgname::git+https://github.com/darryllane/Bluto")
sha512sums=('SKIP')

pkgver() {
  cd $pkgname

  echo $(git rev-list --count HEAD).$(git rev-parse --short HEAD)
}

build() {
  cd $pkgname

  python2 setup.py build
}

package() {
  cd $pkgname

  python2 setup.py install --root="$pkgdir" --prefix=/usr -O1 --skip-build

  install -Dm 644 -t "$pkgdir/usr/share/doc/$pkgname/" README.md
  install -Dm 644 LICENSE.txt "$pkgdir/usr/share/licenses/$pkgname/LICENSE"
}

