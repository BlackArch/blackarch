# This file is part of BlackArch Linux ( https://www.blackarch.org/ ).
# See COPYING for license details.

pkgbase=python-pathmatch
pkgname=('python2-pathmatch' 'python-pathmatch')
_pkgname=pathmatch
pkgver=0.2.1
pkgrel=2
pkgdesc='Path matching utilities.'
arch=('any')
url='https://pypi.org/project/pathmatch/#files'
license=('MIT')
makedepends=('python2-setuptools' 'python-setuptools')
source=("https://files.pythonhosted.org/packages/10/f5/b9823d0042bbff98bd5cf3f5a19ccadd6d0c41034f9bfb6639baed18595c/$_pkgname-$pkgver.zip")
sha512sums=('de95a10b8b6477d5ebb7a72bb318d836db4f6dc629fb85beaa3deea21c858e47c09e2c310f8144eae7ca34a4375401037d0ae271ccb711b03cf0883eb8777eb2')

prepare() {
  cp -a "$_pkgname-$pkgver"{,-2}
}

build() {
  cd "$_pkgname-$pkgver-2"

  python2 setup.py build

  cd "$srcdir/$_pkgname-$pkgver"

  python setup.py build
}

package_python2-pathmatch() {
  depends=('python2' 'python2-six' 'python2-typing')

  cd "$_pkgname-$pkgver-2"

  python2 setup.py install --prefix=/usr --root="$pkgdir" --optimize=1 \
    --skip-build
}

package_python-pathmatch() {
  depends=('python' 'python-six' 'python-typing')

  cd "$_pkgname-$pkgver"

  python setup.py install --prefix=/usr --root="$pkgdir" --optimize=1 \
    --skip-build
}

