# This file is part of BlackArch Linux ( http://blackarch.org ).
# See COPYING for license details.

pkgname='gtk'
pkgver='1.2.10'
pkgrel=1
pkgdesc='A multi-platform toolkit.'
arch=('i686' 'x86_64' 'armv6h' 'armv7h')
url='http://www.gtk.org/'
license=('LGPL')
depends=('libxi' 'glib')
makedepends=('libxt')
install='gtk.install'
source=("ftp://ftp.gnome.org/pub/gnome/sources/gtk+/1.2/gtk+-${pkgver}.tar.gz"
        'aclocal-fixes.patch')
sha1sums=('a5adcb909257da01ae4d4761e1d41081d06e4d7c'
          'b034e33efb85d27f3f3fb082c404e3b6ea79259f')

prepare() {
  cd "$srcdir/gtk+-$pkgver"

  cp /usr/share/libtool/config/config.guess .
  cp /usr/share/libtool/config/config.sub .

  patch -p0 -i ../aclocal-fixes.patch

  sed -i "/ac_cpp=/s/\$CPPFLAGS/\$CPPFLAGS -O2/" configure
}

build() {
  cd "$srcdir/gtk+-$pkgver"

  ./configure --prefix=/usr --sysconfdir=/etc --mandir=/usr/share/man \
    --infodir=/usr/share/info --with-xinput=xfree

  make
}

package() {
  cd "$srcdir/gtk+-$pkgver"

  make DESTDIR="$pkgdir" install
}
