# This file is part of BlackArch Linux ( https://www.blackarch.org/ ).
# See COPYING for license details.

pkgbase=python-pdfkit
pkgname=('python2-pdfkit' 'python-pdfkit')
_pkgname=pdfkit
pkgver=0.6.1
pkgrel=6
pkgdesc='Wkhtmltopdf python wrapper to convert html to pdf using the webkit rendering engine and qt.'
arch=('any')
url='https://pypi.python.org/pypi/pdfkit/'
license=('MIT')
makedepends=('python2-setuptools' 'python-setuptools')
source=("https://files.pythonhosted.org/packages/a1/98/6988328f72fe3be4cbfcb6cbfc3066a00bf111ca7821a83dd0ce56e2cf57/$_pkgname-$pkgver.tar.gz")
sha512sums=('b3ac1016d1c01a2a196f567b9b672caca10f564cc6a62122691d34c3cbbf143f6a846bfba26c3474e9c0296977f0d30c0b5af13b3321ab207b787df3cba12e5d')

prepare() {
  cp -a "$_pkgname-$pkgver"{,-2}
}

build() {
  cd "$_pkgname-$pkgver-2"

  python2 setup.py build

  cd "$srcdir/$_pkgname-$pkgver"

  python setup.py build
}

package_python2-pdfkit() {
  depends=('python2' 'wkhtmltopdf')

  cd "$_pkgname-$pkgver-2"

  python2 setup.py install --prefix=/usr --root="$pkgdir" --optimize=1 \
    --skip-build
}

package_python-pdfkit() {
  depends=('python' 'wkhtmltopdf')

  cd "$_pkgname-$pkgver"

  python setup.py install --prefix=/usr --root="$pkgdir" --optimize=1 \
    --skip-build
}

