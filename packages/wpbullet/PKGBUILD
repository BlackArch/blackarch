# This file is part of BlackArch Linux ( https://www.blackarch.org/ ).
# See COPYING for license details.

pkgname=wpbullet
pkgver=34.6185112
pkgrel=10
pkgdesc='A static code analysis for WordPress (and PHP).'
groups=('blackarch' 'blackarch-code-audit' 'blackarch-webapp')
arch=('any')
url='https://github.com/webarx-security/wpbullet'
license=('GPL-2.0-or-later')
depends=('python' 'python-certifi' 'python-chardet' 'python-colorama'
         'python-idna' 'python-requests' 'python-tabulate' 'python-urllib3'
         'python-terminaltables')
makedepends=('git')
source=("git+https://github.com/webarx-security/$pkgname.git")
sha512sums=('SKIP')

pkgver() {
  cd $pkgname

  ( set -o pipefail
    git describe --long --tags --abbrev=7 2>/dev/null |
      sed 's/\([^-]*-g\)/r\1/;s/-/./g' ||
    printf "%s.%s" "$(git rev-list --count HEAD)" \
      "$(git rev-parse --short=7 HEAD)"
  )
}

package() {
  cd $pkgname

  install -dm 755 "$pkgdir/usr/bin"
  install -dm 755 "$pkgdir/usr/share/$pkgname"

  install -Dm 644 -t "$pkgdir/usr/share/doc/$pkgname/" README.md
  install -Dm 644 LICENSE "$pkgdir/usr/share/licenses/$pkgname/LICENSE"

  rm README.md LICENSE

  cp -a * "$pkgdir/usr/share/$pkgname/"

  cat > "$pkgdir/usr/bin/$pkgname" << EOF
#!/bin/sh
cd /usr/share/$pkgname
exec python $pkgname.py "\$@"
EOF

  chmod +x "$pkgdir/usr/bin/$pkgname"
}

