# This file is part of BlackArch Linux ( https://www.blackarch.org/ ).
#
# This PKGBUILD was extracted from AUR.
# Creator: Anton Leontiev <scileont /at/ gmail.com>
# Changes by: petruknisme

pkgname=perl-future
_pkgname=Future
pkgver=0.45
pkgrel=2
pkgdesc='Perl module to deal with operation awaiting completion.'
arch=('any')
url='https://metacpan.org/release/Future'
license=('GPL' 'PerlArtistic')
makedepends=('perl-module-build>=0.4004')
checkdepends=('perl-test-fatal' 'perl-test-identity' 'perl-test-refcount')
depends=('perl>=5.15.8' 'perl-test-fatal')
options=('!emptydirs')
source=("https://cpan.metacpan.org/authors/id/P/PE/PEVANS/$_pkgname-$pkgver.tar.gz")
sha512sums=('18609d053461fe1ad68fe43ea1961543a690aef4524d5f90a191de977bbe925b38892f71f0ac7099f0c5d934a7cb64d637acf71303cc8f4d7fd74d1586997ff7')

build() {
  cd "$_pkgname-$pkgver"

  ( export PERL_MM_USE_DEFAULT=1 PERL5LIB="" PERL_AUTOINSTALL=--skipdeps \
    PERL_MM_OPT="INSTALLDIRS=vendor DESTDIR='$pkgdir'" \
    PERL_MB_OPT="--installdirs vendor --destdir '$pkgdir'" \
    MODULEBUILDRC=/dev/null

    /usr/bin/perl Build.PL INSTALLDIRS=vendor && ./Build
  )
}

package() {
  cd "$_pkgname-$pkgver"

  ./Build install

  find "$pkgdir" -type f -name 'perllocal.pod' -delete;
  find "$pkgdir" -name .packlist -o -name perllocal.pod -delete
}

