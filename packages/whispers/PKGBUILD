# This file is part of BlackArch Linux ( https://www.blackarch.org/ ).
# See COPYING for license details.

pkgname=whispers
pkgver=2.1.5.r45.g3f5282e
pkgrel=1
pkgdesc='Identify hardcoded secrets in static structured text.'
arch=('any')
groups=('blackarch' 'blackarch-code-audit')
url='https://github.com/adeptex/whispers/'
license=('GPL3')
depends=('python' 'python-astroid' 'python-beautifulsoup4' 'python-jproperties'
         'python-jellyfish' 'python-luhn' 'python-lxml' 'python-yaml'
         'python-crossplane')
makedepends=('git' 'python-setuptools' 'python-pip')
source=("git+https://github.com/adeptex/$pkgname.git")
sha512sums=('SKIP')

pkgver() {
  cd $pkgname

  git describe --long --tags | sed 's/\([^-]*-g\)/r\1/;s/-/./g'
}

build() {
  cd $pkgname

  python setup.py build
}

package() {
  cd $pkgname

  python setup.py install --root="$pkgdir" --prefix=/usr -O1 --skip-build
}

