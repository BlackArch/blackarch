pkgname=patator
pkgver=0.4
pkgrel=5
groups=('blackarch' 'blackarch-cracker')
pkgdesc="A multi-purpose bruteforcer"
arch=('any')
url='http://code.google.com/p/patator/'
license=('GPL2')
depends=('python2')
optdepends=('python2-paramiko: SSH'
            'python2-pycurl: HTTP'
            'openldap: LDAP'
            'impacket: SMB'
            'python2-cx_oracle: Oracle'
            'mysql-python: MySQL'
            'python2-psycopg2: PostgreSQL'
            'python2-crypto: VNC'
            'python2-pydns: DNS'
            'python2-pysnmp: SNMP'
            'python2-ipy: NETx keywords'
            'java-environment: keystore files'
            'unzip: zip archives')
source=("http://patator.googlecode.com/files/patator_v$pkgver.py")
sha256sums=('af7aed50ba0db1d2e932d9edecb1b2945e675ea8e1fcb27da48273b00db8c1f0')

build() {
  sed -e 's|#!/usr/bin/env python|#!/usr/bin/env python2|g' -i "$srcdir/patator_v$pkgver.py"
}

package() {
  install -D -m 0755 "$srcdir/patator_v$pkgver.py" "$pkgdir/usr/bin/patator"

  _modules=(
    'ftp_login'
    'ssh_login'
    'telnet_login'
    'smtp_login'
    'smtp_vrfy'
    'smtp_rcpt'
    'finger_lookup'
    'http_fuzz'
    'pop_login'
    'pop_passd'
    'imap_login'
    'ldap_login'
    'smb_login'
    'smb_lookupsid'
    'vmauthd_login'
    'mssql_login'
    'oracle_login'
    'mysql_login'
    'pgsql_login'
    'vnc_login'
    'dns_forward'
    'dns_reverse'
    'snmp_login'
    'unzip_pass'
    'keystore_pass'
  )
  for module in ${_modules[@]}; do
    ln -s /usr/bin/patator "$pkgdir/usr/bin/$module"
  done
}

