# This file is part of BlackArch Linux ( https://www.blackarch.org/ ).
# See COPYING for license details.

pkgname=patator
pkgver=1.1.0.r4.g964e87c
pkgrel=1
epoch=2
groups=('blackarch' 'blackarch-cracker')
pkgdesc='A multi-purpose bruteforcer.'
arch=('any')
url='https://github.com/lanjelot/patator'
license=('GPL-2.0-or-later')
depends=('python' 'python-paramiko' 'python-pycurl' 'python-dnspython'
         'mysql-python' 'python-psycopg2' 'python-pycryptodome' 'python-pysnmp'
         'python-ipy' 'python-pyopenssl' 'python-ajpy' 'python-cx_oracle'
         'python-pyasn1' 'impacket' 'java-environment' 'unzip' 'ike-scan'
         'openldap')
makedepends=('git' 'python-build' 'python-pip')
source=("git+https://github.com/lanjelot/$pkgname.git")
sha512sums=('SKIP')

pkgver() {
  cd $pkgname

  ( set -o pipefail
    git describe --long --tags --abbrev=7 2>/dev/null |
      sed 's/\([^-]*-g\)/r\1/;s/-/./g' ||
    printf "%s.%s" "$(git rev-list --count HEAD)" \
      "$(git rev-parse --short=7 HEAD)"
  )
}

build() {
  cd $pkgname

  python -m build --wheel --outdir="$startdir/dist"
}

package() {
  cd $pkgname

  pip install \
    --verbose \
    --disable-pip-version-check \
    --no-warn-script-location \
    --ignore-installed \
    --no-compile \
    --no-deps \
    --root="$pkgdir" \
    --prefix=/usr \
    --no-index \
    --find-links="file://$startdir/dist" \
    $pkgname

  install -Dm 644 -t "$pkgdir/usr/share/doc/$pkgname/" README.md
}

