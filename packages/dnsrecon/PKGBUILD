# This file is part of BlackArch Linux ( https://www.blackarch.org/ ).
# See COPYING for license details.

pkgname=dnsrecon
pkgver=0.10.1
pkgrel=1
epoch=2
groups=('blackarch' 'blackarch-recon')
pkgdesc='Python script for enumeration of hosts, subdomains and emails from a given domain using google.'
url='https://github.com/darkoperator/dnsrecon'
license=('custom')
arch=('any')
depends=('python' 'python-netaddr' 'python-dnspython' 'python-lxml')
makedepends=('git')
source=("git+https://github.com/darkoperator/$pkgname.git")
sha512sums=('SKIP')

pkgver() {
  cd $pkgname

  grep -m1 __version "$pkgname.py" | cut -d '=' -f 2 | sed "s/'//g" | tr -d ' '
}

package() {
  cd $pkgname

  install -dm 755 "$pkgdir/usr/bin"
  install -dm 755 "$pkgdir/usr/share/$pkgname"

  install -Dm 644 -t "$pkgdir/usr/share/doc/$pkgname/" README.md

  rm README.md

  cp -a * "$pkgdir/usr/share/$pkgname/"

  cat > "$pkgdir/usr/bin/$pkgname" << EOF
#!/bin/sh
cd /usr/share/$pkgname
exec python3 $pkgname.py "\$@"
EOF

  chmod +x "$pkgdir/usr/bin/$pkgname"
}

