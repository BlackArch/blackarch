# This file is part of BlackArch Linux ( https://www.blackarch.org/ ).
# See COPYING for license details.

pkgname=aws_iam_privesc
pkgver=33.f5c92f9
pkgrel=2
pkgdesc='AWS IAM policy scanner that helps determine where privilege escalation can be achieved'
arch=('any')
groups=('blackarch' 'blackarch-scanner' 'blackarch-recon' 'blackarch-exploitation' 'blackarch-automation')
url='https://github.com/cyberqueenmeg/aws_iam_privesc'
license=('MIT')
depends=('python' 'python-requests' 'python-pycryptodome' 'python-termcolor' 'python-boto3' 'python-botocore' 'aws' )
makedepends=('git')
source=("git+https://github.com/cyberqueenmeg/$pkgname.git")
sha512sums=('SKIP')

pkgver() {
  cd $pkgname

  echo $(git rev-list --count HEAD).$(git rev-parse --short HEAD)
}

package() {
  cd $pkgname

  install -dm 755 "$pkgdir/usr/bin"
  install -dm 755 "$pkgdir/usr/share/$pkgname/"

  install -Dm 644 -t "$pkgdir/usr/share/doc/$pkgname/" README.md
  install -Dm 644 LICENSE "$pkgdir/usr/share/licenses/$pkgname/LICENSE"

  rm -rf LICENSE README.d

  cp -a * "$pkgdir/usr/share/$pkgname/"

  cat > "$pkgdir/usr/bin/$pkgname" << EOF
#!/bin/sh
cd /usr/share/$pkgname
exec python3 aws_escalate.py "\$@"
EOF
  chmod +x "$pkgdir/usr/bin/$pkgname"
}
