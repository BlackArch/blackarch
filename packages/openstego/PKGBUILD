# This file is part of BlackArch Linux ( https://www.blackarch.org/ ).
# See COPYING for license details.

pkgname=openstego
pkgver=0.8.0
pkgrel=1
groups=('blackarch' 'blackarch-crypto' 'blackarch-stego')
pkgdesc='A tool implemented in Java for generic steganography, with support for password-based encryption of the data.'
arch=('any')
url='https://www.openstego.com/'
license=('GPL2')
depends=('java-environment')
source=("https://github.com/syvaidya/$pkgname/releases/download/$pkgname-$pkgver/$pkgname-$pkgver.zip")
sha512sums=('73f5dd915e77e5fbd573ed0dd89291326000e81c9b3310fec75de91532bebdc7e70589804622ae0c1d43cd9fb859e6d7d8ce5970b0f033f37eabc02fb356648e')

package() {
  cd "$pkgname-$pkgver"

  install -dm 755 "$pkgdir/usr/bin"
  install -dm 755 "$pkgdir/usr/share/$pkgname"

  install -Dm 644 -t "$pkgdir/usr/share/doc/$pkgname/" README
  install -Dm 644 LICENSE "$pkgdir/usr/share/licenses/$pkgname/LICENSE"

  rm -rf README

  cp -a * "$pkgdir/usr/share/$pkgname/"

  cat > "$pkgdir/usr/bin/$pkgname" << EOF
#!/bin/sh
exec java -Xmx512m -jar /usr/share/$pkgname/lib/$pkgname.jar "\$@"
EOF

  chmod +x "$pkgdir/usr/bin/$pkgname"
}

