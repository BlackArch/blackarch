# This file is part of BlackArch Linux ( https://www.blackarch.org/ ).
# See COPYING for license details.

pkgname=openstego
pkgver=0.7.3
pkgrel=1
groups=('blackarch' 'blackarch-crypto' 'blackarch-stego')
pkgdesc='A tool implemented in Java for generic steganography, with support for password-based encryption of the data.'
arch=('any')
url='https://www.openstego.com/'
license=('GPL2')
depends=('java-environment')
source=("https://github.com/syvaidya/$pkgname/releases/download/$pkgname-$pkgver/$pkgname-$pkgver.zip")
sha512sums=('cd8d7b1c650829656e6b7a47d69ab5f07bb38244ca0d4ba0d94fc9e6c59075531bc188b8953d1320266230afe6f4a5bcdbe0b33e84d1f9b3db0667f8ca2bab5c')

package() {
  cd "$pkgname-$pkgver"

  install -dm 755 "$pkgdir/usr/bin"
  install -dm 755 "$pkgdir/usr/share/$pkgname"

  install -Dm 644 -t "$pkgdir/usr/share/doc/$pkgname/" README
  install -Dm 644 LICENSE "$pkgdir/usr/share/licenses/$pkgname/LICENSE"

  rm -rf README

  cp -a * "$pkgdir/usr/share/$pkgname/"

  cat > "$pkgdir/usr/bin/$pkgname" << EOF
#!/bin/sh
exec java -Xmx512m -jar /usr/share/$pkgname/lib/$pkgname.jar "\$@"
EOF

  chmod +x "$pkgdir/usr/bin/$pkgname"
}

