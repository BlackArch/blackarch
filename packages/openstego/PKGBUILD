# This file is part of BlackArch Linux ( https://www.blackarch.org/ ).
# See COPYING for license details.

pkgname=openstego
pkgver=122.af68e61
pkgrel=1
groups=('blackarch' 'blackarch-crypto' 'blackarch-stego')
pkgdesc='A tool implemented in Java for generic steganography, with support for password-based encryption of the data.'
arch=('any')
url='https://www.openstego.com/'
license=('GPL2')
depends=('java-environment')
source=('git+https://github.com/syvaidya/openstego.git')
sha512sums=('SKIP')

pkgver() {
  cd $pkgname

  echo $(git rev-list --count HEAD).$(git rev-parse --short HEAD)
}

package() {
  cd "$pkgname"

  install -dm 755 "$pkgdir/usr/bin"
  install -dm 755 "$pkgdir/usr/share/$pkgname"

  install -Dm 644 README "$pkgdir/usr/share/doc/$pkgname/README"
  install -Dm 644 LICENSE "$pkgdir/usr/share/licenses/$pkgname/LICENSE"

  rm -rf README

  cp -a * "$pkgdir/usr/share/$pkgname/"

  cat > "$pkgdir/usr/bin/$pkgname" << EOF
#!/bin/sh
exec java -Xmx512m -jar /usr/share/$pkgname/lib/openstego.jar "\$@"
EOF

  chmod +x "$pkgdir/usr/bin/$pkgname"
}

