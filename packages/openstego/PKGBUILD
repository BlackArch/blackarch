# This file is part of BlackArch Linux ( https://www.blackarch.org/ ).
# See COPYING for license details.

pkgname=openstego
pkgver=0.8.4
pkgrel=1
groups=('blackarch' 'blackarch-crypto' 'blackarch-stego')
pkgdesc='A tool implemented in Java for generic steganography, with support for password-based encryption of the data.'
arch=('any')
url='https://github.com/syvaidya/openstego/releases'
license=('GPL2')
depends=('java-environment')
source=("https://github.com/syvaidya/$pkgname/releases/download/$pkgname-$pkgver/$pkgname-$pkgver.zip")
sha512sums=('e6ab6d6a1ddd74b7f9ead38254dce5a7b3810dc1d0538596c78d2d58719e65dd3c84acd03aca8f4e441b0c4d01fcf691ebc67dda61d5ed8fbec50fee8468e5d0')

package() {
  cd "$pkgname-$pkgver"

  install -dm 755 "$pkgdir/usr/bin"
  install -dm 755 "$pkgdir/usr/share/$pkgname"

  install -Dm 644 -t "$pkgdir/usr/share/doc/$pkgname/" README
  install -Dm 644 LICENSE "$pkgdir/usr/share/licenses/$pkgname/LICENSE"

  rm -rf README

  cp -a * "$pkgdir/usr/share/$pkgname/"

  cat > "$pkgdir/usr/bin/$pkgname" << EOF
#!/bin/sh
exec java -Xmx512m -jar /usr/share/$pkgname/lib/$pkgname.jar "\$@"
EOF

  chmod +x "$pkgdir/usr/bin/$pkgname"
}

