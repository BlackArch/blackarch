# This file is part of BlackArch Linux ( http://blackarch.org ).
# See COPYING for license details.

pkgname=flyr
pkgver=60.1b5b6e0
pkgrel=1
pkgdesc='Block-based software vulnerability fuzzing framework.'
groups=('blackarch' 'blackarch-fuzzer')
arch=('x86_64' 'armv6h' 'armv7h' 'aarch64')
url='https://github.com/zznop/flyr'
license=('custom:unknown')
makedepends=('git' 'scons')
source=('git+https://github.com/zznop/flyr.git')
sha512sums=('SKIP')

pkgver() {
  cd "$pkgname"

  echo $(git rev-list --count HEAD).$(git rev-parse --short HEAD)
}

prepare() {
  cd "$pkgname"

  git submodule update --init
}

build() {
  cd "$pkgname"

  scons -j 1
}

package() {
  cd "$pkgname"

  install -Dm 755 flyr "$pkgdir/usr/bin/$pkgname"
  install -Dm 644 -t "$pkgdir/usr/share/doc/$pkgname/" README.md
  install -Dm 644 LICENSE "$pkgdir/usr/share/licenses/$pkgname/LICENSE"
}

