# This file is part of BlackArch Linux ( https://www.blackarch.org/ ).
# See COPYING for license details.

pkgname=python-folium
_pkgname=folium
pkgver=0.12.1
pkgrel=2
pkgdesc='Make beautiful maps with Leaflet.js & Python.'
arch=('any')
url='https://pypi.org/project/folium/#files'
license=('MIT')
depends=('python' 'python-branca' 'python-jinja' 'python-numpy'
         'python-requests')
makedepends=('python-setuptools')
source=("https://github.com/python-visualization/folium/archive/v$pkgver.tar.gz")
sha512sums=('1e0b39226dd625d4026ba077c9ca8f93c9a29984475cac33ad06d70bae2c6ad0582413c8a6117ad8f08c61380483caab9834199a279bb4f0d532b93c0b5fc9f7')

build() {
  cd "$_pkgname-$pkgver"

  python setup.py build
}

package() {
  cd "$_pkgname-$pkgver"

  python setup.py install --root="$pkgdir" --prefix=/usr -O1 --skip-build
}

