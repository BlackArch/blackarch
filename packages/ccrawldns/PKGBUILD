# This file is part of BlackArch Linux ( https://www.blackarch.org/ ).
# See COPYING for license details.

pkgname=ccrawldns
pkgver=v1.0.r2.gdfd5062
pkgrel=1
epoch=1
pkgdesc='Retrieves from the CommonCrawl data set unique subdomains for a given domain name.'
arch=('any')
groups=('blackarch' 'blackarch-recon')
url='https://github.com/lgandx/CCrawlDNS'
license=('GPL-1.0-or-later')
depends=('python' 'python-requests')
makedepends=('git')
source=("$pkgname::git+https://github.com/lgandx/CCrawlDNS.git")
sha512sums=('SKIP')

pkgver() {
  cd $pkgname

  ( set -o pipefail
    git describe --long --tags --abbrev=7 2>/dev/null |
      sed 's/\([^-]*-g\)/r\1/;s/-/./g' ||
    printf "%s.%s" "$(git rev-list --count HEAD)" \
      "$(git rev-parse --short=7 HEAD)"
  )
}

package() {
  cd $pkgname

  install -Dm 755 CCrawlDNS.py "$pkgdir/usr/bin/$pkgname"
  install -Dm 644 -t "$pkgdir/usr/share/doc/$pkgname/" README.md
}

