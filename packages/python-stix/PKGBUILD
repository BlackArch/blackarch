# This file is part of BlackArch Linux ( https://www.blackarch.org/ ).
# See COPYING for license details.

pkgbase=python-stix
pkgname=('python2-stix' 'python-stix')
_pkgname=python-stix
pkgver=1.2.0.6
pkgrel=4
pkgdesc='An API for parsing and generating STIX content.'
url='https://pypi.org/project/stix/#files'
license=('custom:unknown')
arch=('any')
makedepends=('python2-setuptools' 'python-setuptools')
source=("https://github.com/STIXProject/python-stix/archive/v$pkgver.tar.gz")
sha512sums=('c7a656e12fb3c8e6223904d5c66b8c4916197497f70635eb7375ba8f73a3b6787559a895a1cad932950691d73e04e92afed850495f38bd0ec5d1ba6f31b10ef0')

prepare() {
  cp -a "$_pkgname-$pkgver"{,-2}
}

build() {
  cd "$_pkgname-$pkgver-2"

  python2 setup.py build

  cd "$srcdir/$_pkgname-$pkgver"

  python setup.py build
}

package_python2-stix() {
  depends=('python2' 'python2-lxml' 'python2-dateutil' 'python2-cybox'
           'python2-mixbox')

  cd "$_pkgname-$pkgver-2"

  python2 setup.py install --prefix=/usr --root="$pkgdir" --optimize=1 \
    --skip-build

  install -Dm 644 LICENSE.txt \
    "$pkgdir/usr/share/licenses/${pkgname[0]}/LICENSE"
}

package_python-stix() {
  depends=('python' 'python-lxml' 'python-dateutil' 'python-cybox'
           'python-mixbox')

  cd "$_pkgname-$pkgver"

  python setup.py install --prefix=/usr --root="$pkgdir" --optimize=1 \
    --skip-build

  install -Dm 644 LICENSE.txt \
    "$pkgdir/usr/share/licenses/${pkgname[1]}/LICENSE"
}

