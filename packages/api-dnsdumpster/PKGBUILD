# This file is part of BlackArch Linux ( https://www.blackarch.org/ ).
# See COPYING for license details.

pkgname=api-dnsdumpster
pkgver=59.eda15d6
pkgrel=1
groups=('blackarch' 'blackarch-recon' 'blackarch-scanner')
pkgdesc='Unofficial Python API for http://dnsdumpster.com/.'
arch=('any')
url='https://github.com/PaulSec/API-dnsdumpster.com'
license=('custom:unknown')
depends=('python2-beautifulsoup4')
makedepends=('git')
source=('git+https://github.com/PaulSec/API-dnsdumpster.com.git')
sha512sums=('SKIP')

pkgver() {
  cd API-dnsdumpster.com

  echo $(git rev-list --count HEAD).$(git rev-parse --short HEAD)
}

build() {
  cd API-dnsdumpster.com

  python2 setup.py build
}

package() {
  cd API-dnsdumpster.com

  install -dm 755 "$pkgdir/usr/bin"
  install -dm 755 "$pkgdir/usr/share/$pkgname"

  python2 setup.py install --root="$pkgdir" --prefix=/usr --optimize=1

  install -Dm644 -t "$pkgdir/usr/share/$pkgname/" README.md

  rm README.md

  cp -a * "$pkgdir/usr/share/$pkgname"

  cat > "$pkgdir/usr/bin/$pkgname" << EOF
#!/bin/sh
exec python2 /usr/share/api-dnsdumpster/API_example.py "\$@"
EOF

  chmod +x "$pkgdir/usr/bin/$pkgname"

  find "$pkgdir" -type f -iname '*.pyc' -exec rm {} \;
}

