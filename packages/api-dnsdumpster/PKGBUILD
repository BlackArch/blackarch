# This file is part of BlackArch Linux ( http://blackarch.org ).
# See COPYING for license details.

pkgname='api-dnsdumpster'
pkgver=55.9b59694
pkgrel=1
groups=('blackarch' 'blackarch-recon' 'blackarch-scanner')
pkgdesc='Unofficial Python API for http://dnsdumpster.com/.'
arch=('any')
url='https://github.com/PaulSec/API-dnsdumpster.com'
license=('custom:unknown')
depends=('python2-beautifulsoup4')
makedepends=('git')
source=('git+https://github.com/PaulSec/API-dnsdumpster.com.git')
sha1sums=('SKIP')

pkgver() {
  cd "$srcdir/API-dnsdumpster.com"

  echo $(git rev-list --count HEAD).$(git rev-parse --short HEAD)
}

build() {
  cd "$srcdir/API-dnsdumpster.com"

  python2 setup.py build
}

package() {
  cd "$srcdir/API-dnsdumpster.com"

  mkdir -p "$pkgdir/usr/bin"
  mkdir -p "$pkgdir/usr/share/api-dnsdumpster"

  python2 setup.py install --root="$pkgdir" --prefix=/usr --optimize=1

  install -Dm644 -t "$pkgdir/usr/share/api-dnsdumpster/" README.md

  rm README.md

  cp -a * "$pkgdir/usr/share/api-dnsdumpster"

  cat > "$pkgdir/usr/bin/api-dnsdumpster" << EOF
#!/bin/sh
exec python2 /usr/share/api-dnsdumpster/API_example.py "\$@"
EOF

  chmod a+x "$pkgdir/usr/bin/api-dnsdumpster"

  find "$pkgdir" -type f -iname '*.pyc' -exec rm {} \;
}
