# This file is part of BlackArch Linux ( https://www.blackarch.org/ ).
# See COPYING for license details.

pkgname=mingw-w64-gcc
pkgver=9.2.0
_islver=0.21
pkgrel=1
pkgdesc='Cross GCC for the MinGW-w64 cross-compiler.'
arch=('x86_64')
url='http://gcc.gnu.org/'
license=('GPL' 'LGPL' 'FDL' 'custom:unknown')
depends=('zlib' 'libmpc' 'mingw-w64-crt' 'mingw-w64-binutils'
         'mingw-w64-winpthreads' 'mingw-w64-headers')
makedepends=('gcc-ada')
provides=('mingw-w64-gcc-base')
options=('!strip' 'staticlibs' '!emptydirs' '!buildflags')
source=("https://ftp.gnu.org/gnu/gcc/gcc-$pkgver/gcc-$pkgver.tar.xz"{,.sig}
        "http://isl.gforge.inria.fr/isl-$_islver.tar.bz2")
sha512sums=('a12dff52af876aee0fd89a8d09cdc455f35ec46845e154023202392adc164848faf8ee881b59b681b696e27c69fd143a214014db4214db62f9891a1c8365c040'
            'SKIP'
            '48f3b8d90550e8ab28837b5757f87bf99cddec67769877e04942abef69bbe526ef4c863951d55dd89a6027dc09df48988c8df6029782f990aa4d5b67e65f6d53')
validpgpkeys=('F3691687D867B81B51CE07D9BBE43771487328A9'
              '13975A70E63C361C73AE69EF6EEB81F8981C74C7'
              '33C235A34C46AA3FFB293709A328C3A2C3C45C06')
_targets='i686-w64-mingw32 x86_64-w64-mingw32'

prepare() {
  ln -sf gcc-${pkgver/+/-} gcc

  cd gcc

  # link isl for in-tree builds
  ln -sf "../isl-$_islver" isl
}

build() {
  for t in $_targets ; do
    mkdir -p "$srcdir/gcc-build-$t" && cd "$srcdir/gcc-build-$t"

    "$srcdir/gcc/"configure --prefix=/usr --libexecdir=/usr/lib \
      --target=$t --enable-languages=c,lto,c++,objc,obj-c++,fortran \
      --enable-shared --enable-static --enable-threads=posix \
      --enable-fully-dynamic-string --enable-libstdcxx-time=yes \
      --with-system-zlib --enable-cloog-backend=isl --enable-lto \
      --disable-dw2-exceptions --enable-libgomp --disable-multilib \
      --enable-checking=release
    make
  done
}

package() {
  for t in $_targets ; do
    cd "$srcdir/gcc-build-$t"

    make DESTDIR="$pkgdir" install

    "$t-strip" "$pkgdir"/usr/$t/lib/*.dll

    strip "$pkgdir/usr/bin/"$t-*
    strip "$pkgdir/usr/lib/gcc/$t/${pkgver:0:5}/"{cc1*,collect2,f951,lto*}

    ln -s "$t-gcc" "$pkgdir/usr/bin/"$t-cc

    mkdir -p "$pkgdir/usr/$t/bin/"
    mv "$pkgdir/usr/"$t/lib/*.dll "$pkgdir/usr/$t/bin/"

    strip "$pkgdir/usr/bin/"*

    rm -rf "$pkgdir/usr/share" "$pkgdir/usr/lib/libcc1.so" \
      "$pkgdir/usr/lib/libcc1.a" "$pkgdir/usr/lib/libcc1.so.0" \
      "$pkgdir/usr/lib/libcc1.so.0.0.0"
  done
}

