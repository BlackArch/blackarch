# This file is part of BlackArch Linux ( https://www.blackarch.org/ ).
# See COPYING for license details.

pkgname=mingw-w64-gcc
pkgver=9.1.0
_islver=0.19
pkgrel=1
pkgdesc='Cross GCC for the MinGW-w64 cross-compiler.'
arch=('x86_64')
url='http://gcc.gnu.org/'
license=('GPL' 'LGPL' 'FDL' 'custom:unknown')
depends=('zlib' 'libmpc' 'mingw-w64-crt' 'mingw-w64-binutils'
         'mingw-w64-winpthreads' 'mingw-w64-headers')
makedepends=('gcc-ada')
provides=('mingw-w64-gcc-base')
options=('!strip' 'staticlibs' '!emptydirs' '!buildflags')
source=("https://ftp.gnu.org/gnu/gcc/gcc-$pkgver/gcc-$pkgver.tar.xz"{,.sig}
        "http://isl.gforge.inria.fr/isl-$_islver.tar.bz2")
sha512sums=('b6134df027e734cee5395afd739fcfa4ea319a6017d662e54e89df927dea19d3fff7a6e35d676685383034e3db01c9d0b653f63574c274eeb15a2cb0bc7a1f28'
            'SKIP'
            '08f4db964d9e02ec8aa9779378ed76e0ddf1d56f81f87664dbf787555ce496cdc87e836f8a51ae137f45e648c604870cce07ee45919eafb549e404afb8f27083')
validpgpkeys=('F3691687D867B81B51CE07D9BBE43771487328A9'
              '13975A70E63C361C73AE69EF6EEB81F8981C74C7'
              '33C235A34C46AA3FFB293709A328C3A2C3C45C06')
_targets='i686-w64-mingw32 x86_64-w64-mingw32'

prepare() {
  ln -sf gcc-${pkgver/+/-} gcc

  cd gcc

  # link isl for in-tree builds
  ln -sf "../isl-$_islver" isl
}

build() {
  for t in $_targets ; do
    install -dm 755 "$srcdir/gcc-build-$t"

    cd "$srcdir/gcc-build-$t"

    "$srcdir/gcc/"configure --prefix=/usr --libexecdir=/usr/lib \
      --target=${t} --enable-languages=c,lto,c++,objc,obj-c++,fortran,ada \
      --enable-shared --enable-static --enable-threads=posix \
      --enable-fully-dynamic-string --enable-libstdcxx-time=yes \
      --with-system-zlib --enable-cloog-backend=isl --enable-lto \
      --disable-dw2-exceptions --enable-libgomp --disable-multilib \
      --enable-checking=release

    make all
  done
}

package() {
  for t in $_targets ; do
    cd "$srcdir/gcc-build-$t"

    make DESTDIR="$pkgdir" install

    $t-strip "$pkgdir/usr/$t/lib/*.dll"

    strip "$pkgdir/usr/bin/$t-*"
    strip "$pkgdir/usr/lib/gcc/$t/${pkgver:0:5}/"{cc1*,collect2,gnat1,f951,lto*}

    ln -s "$t-gcc" "$pkgdir/usr/bin/$t-cc"

    # mv dlls
    install -dm 755 "$pkgdir/usr/$t/bin/"

    mv "$pkgdir/usr/$t/lib/*.dll" "$pkgdir/usr/$t/bin/"
  done

  strip "$pkgdir/usr/bin/"*

  # remove unnecessary files
  rm -r "$pkgdir/usr/share"
  rm -f "$pkgdir/usr/lib/libcc1.so"*
  rm -f "$pkgdir/usr/lib/libcc1.a"
}

