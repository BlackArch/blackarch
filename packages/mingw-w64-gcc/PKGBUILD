# This file is part of BlackArch Linux ( https://www.blackarch.org/ ).
# See COPYING for license details.

pkgname=mingw-w64-gcc
pkgver=9.1.0
_islver=0.21
pkgrel=1
pkgdesc='Cross GCC for the MinGW-w64 cross-compiler.'
arch=('x86_64')
url='http://gcc.gnu.org/'
license=('GPL' 'LGPL' 'FDL' 'custom:unknown')
depends=('zlib' 'libmpc' 'mingw-w64-crt' 'mingw-w64-binutils'
         'mingw-w64-winpthreads' 'mingw-w64-headers')
makedepends=('gcc-ada')
provides=('mingw-w64-gcc-base')
options=('!strip' 'staticlibs' '!emptydirs' '!buildflags')
source=("https://ftp.gnu.org/gnu/gcc/gcc-$pkgver/gcc-$pkgver.tar.xz"{,.sig}
        "http://isl.gforge.inria.fr/isl-$_islver.tar.bz2")
sha512sums=('b6134df027e734cee5395afd739fcfa4ea319a6017d662e54e89df927dea19d3fff7a6e35d676685383034e3db01c9d0b653f63574c274eeb15a2cb0bc7a1f28'
            'SKIP'
            '48f3b8d90550e8ab28837b5757f87bf99cddec67769877e04942abef69bbe526ef4c863951d55dd89a6027dc09df48988c8df6029782f990aa4d5b67e65f6d53')
validpgpkeys=('F3691687D867B81B51CE07D9BBE43771487328A9'
              '13975A70E63C361C73AE69EF6EEB81F8981C74C7'
              '33C235A34C46AA3FFB293709A328C3A2C3C45C06')
_target='x86_64-w64-mingw32'

prepare() {
  ln -sf gcc-${pkgver/+/-} gcc

  cd gcc

  # link isl for in-tree builds
  ln -sf "../isl-$_islver" isl
}

build() {
  mkdir -p "$srcdir/gcc-build-$_target"

  cd "$srcdir/gcc-build-$_target"

  "$srcdir/gcc/"configure --prefix=/usr --libexecdir=/usr/lib \
      --target=$_target --enable-languages=c,lto,c++,objc,obj-c++,fortran,ada \
      --enable-shared --enable-static --enable-threads=posix \
      --enable-fully-dynamic-string --enable-libstdcxx-time=yes \
      --with-system-zlib --enable-cloog-backend=isl --enable-lto \
      --disable-dw2-exceptions --enable-libgomp --disable-multilib \
      --enable-checking=release

  make
}

package() {
  cd "$srcdir/gcc-build-$_target"

  make DESTDIR="$pkgdir" install

  "$_target-strip" "$pkgdir/usr/$_target/lib/*.dll"

  strip "$pkgdir/usr/bin/$_target-*"
  strip "$pkgdir/usr/lib/gcc/$_target/${pkgver:0:5}/"{cc1*,collect2,gnat1,f951,lto*}
  ln -s "$_target-gcc" "$pkgdir/usr/bin/$_target-cc"
  mkdir -p "$pkgdir/usr/$_target/bin/"
  mv "$pkgdir/usr/$_target/lib/*.dll" "$pkgdir/usr/$_target/bin/"
  strip "$pkgdir/usr/bin/"*
  rm -r "$pkgdir/usr/share" "$pkgdir/usr/lib/libcc1.so" \
    "$pkgdir/usr/lib/libcc1.a"
}

