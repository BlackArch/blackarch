# This file is part of BlackArch Linux ( http://blackarch.org ).
# See COPYING for license details.

pkgname='vhostscan'
pkgver=279.c8f9397
pkgrel=1
pkgdesc='A virtual host scanner that can be used with pivot tools, detect catch-all scenarios, aliases and dynamic default pages.'
groups=('blackarch' 'blackarch-scanner')
arch=('any')
url='https://github.com/codingo/VHostScan'
license=('GPL3')
depends=('python2' 'python2-requests' 'python2-numpy' 'python2-argparse'
         'python2-pandas')
makedepends=('git')
source=('git+https://github.com/codingo/VHostScan.git')
sha1sums=('SKIP')

pkgver() {
  cd "$srcdir/VHostScan"

  echo $(git rev-list --count HEAD).$(git rev-parse --short HEAD)
}

package() {
  cd "$srcdir/VHostScan"

  mkdir -p "$pkgdir/usr/bin"
  mkdir -p "$pkgdir/usr/share/vhostscan"

  install -Dm644 -t "$pkgdir/usr/share/doc/vhostscan/" *.md
  install -Dm644 LICENSE "$pkgdir/usr/share/licenses/vhostscan/LICENSE"

  rm *.md LICENSE

  cp -a * "$pkgdir/usr/share/vhostscan/"

  cat > "$pkgdir/usr/bin/vhostscan" << EOF
#!/bin/sh
cd /usr/share/vhostscan
exec python2 VHostScan.py "\${@}"
EOF

  chmod a+x "$pkgdir/usr/bin/vhostscan"
}
