# This file is part of BlackArch Linux ( http://blackarch.org ).
# See COPYING for license details.

pkgname='vhostscan'
pkgver=302.7d290b9
pkgrel=2
pkgdesc='A virtual host scanner that can be used with pivot tools, detect catch-all scenarios, aliases and dynamic default pages.'
groups=('blackarch' 'blackarch-scanner')
arch=('any')
url='https://github.com/codingo/VHostScan'
license=('GPL3')
depends=('python' 'python-requests' 'python-numpy' 'python-argparse'
         'python-pandas' 'python-dnspython' 'python-fuzzywuzzy'
         'python-argparse' 'python-urllib3' 'python-simplejson')
makedepends=('git' 'python-setuptools')
source=('git+https://github.com/codingo/VHostScan.git')
sha1sums=('SKIP')

pkgver() {
  cd "$srcdir/VHostScan"

  echo $(git rev-list --count HEAD).$(git rev-parse --short HEAD)
}

prepare() {
  cd "$srcdir/VHostScan"

  sed -i 's/==.*//g' requirements.txt
}

build() {
  cd "$srcdir/VHostScan"

  python3 setup.py build
}

package() {
  cd "$srcdir/VHostScan"

  python3 setup.py install --root="$pkgdir" --prefix="/usr"

  install -Dm644 -t "$pkgdir/usr/share/doc/vhostscan/" *.md
  install -Dm644 LICENSE "$pkgdir/usr/share/licenses/vhostscan/LICENSE"

  mv "$pkgdir/usr/bin/VHostScan" "$pkgdir/usr/bin/vhostscan"

  rm "$pkgdir/usr/lib/python3.6/site-packages/tests/__init__.py" \
    "$pkgdir/usr/lib/python3.6/site-packages/tests/__pycache__/__init__.cpython-36.pyc"

  find "$pkgdir/" -type d -name '__pycache__' -exec rm -rf {} +
}
