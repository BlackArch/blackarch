# This file is part of BlackArch Linux ( http://blackarch.org ).
# See COPYING for license details.

pkgname=python2-cement
pkgver=2.10.12
pkgrel=4
pkgdesc='CLI Application Framework for Python.'
arch=('any')
url='https://pypi.org/project/cement/#files'
license=('BSD')
depends=('python2')
makedepends=('python2-setuptools')
checkdepends=('python2-nose-cover3')
source=("https://files.pythonhosted.org/packages/e3/f8/fbab593659a793e53fa2a04d2d8349a555ba1e760e2d553b2748920be1e9/cement-${pkgver}.tar.gz")
sha512sums=('0c9384c2c9c5ece7e3ea3617a798a158e0e616431d1389f928242dd86c1e99e1897a15484b493ef29496c2417212d4e32273639624914ffd089e29578625f545')

build() {
  cd cement-$pkgver

  python2 setup.py build
}

check() {
  cd cement-$pkgver

  nosetests2
}

package() {
  cd cement-$pkgver

  python2 setup.py install --root="$pkgdir" -O1

  rm -rf "$pkgdir/usr/lib/python2.7/site-packages/examples"
}

