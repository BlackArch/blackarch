# This file is part of BlackArch Linux ( http://blackarch.org ).
# See COPYING for license details.

pkgname=python2-cement
pkgver=3.0.0
pkgrel=4
pkgdesc='CLI Application Framework for Python.'
arch=('any')
url='https://pypi.org/project/cement/#files'
license=('BSD')
depends=('python2')
makedepends=('python2-setuptools')
checkdepends=('python2-nose-cover3')
source=("https://files.pythonhosted.org/packages/fe/07/ab14a1a8a8e326b479ec76006e5d48f58cb058a2a9a6d70a373e463de1f4/cement-$pkgver.tar.gz")
sha512sums=('39c847198f7ec58205db572b2bce24459f051b3619a770a9294c353e735287ec9f19aad4f1e511959b225aa19aed00031f754e9a2acd9a8a9b16b4039160910e')

build() {
  cd cement-$pkgver

  python2 setup.py build
}

check() {
  cd cement-$pkgver

  nosetests2
}

package() {
  cd cement-$pkgver

  python2 setup.py install --root="$pkgdir" -O1

  rm -rf "$pkgdir/usr/lib/python2.7/site-packages/examples"
}

