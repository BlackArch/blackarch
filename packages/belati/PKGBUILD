# This file is part of BlackArch Linux ( http://blackarch.org ).
# See COPYING for license details.

pkgname='belati'
pkgver=57.c571e67
pkgrel=1
pkgdesc='The Traditional Swiss Army Knife for OSINT.'
groups=('blackarch' 'blackarch-scanner' 'blackarch-recon' 'blackarch-webapp')
arch=('any')
url='https://github.com/aancw/Belati'
license=('GPL2')
depends=('python2' 'python2-requests' 'python2-dnspython' 'python2-argparse'
         'python2-texttable' 'python2-python-geoip'
         'python2-python-geoip-geolite2' 'python2-colorama' 'python2-dnsknife'
         'python2-termcolor' 'python2-validators' 'python2-tqdm'
         'python2-tldextract' 'python2-fake-useragent' 'python2-python-wappalyzer'
         'python2-futures' 'python2-future' 'python2-beautifulsoup4'
         'python2-python-whois' 'python2-django')
makedepends=('git')
source=('git+https://github.com/aancw/Belati.git')
sha1sums=('SKIP')

pkgver() {
  cd "$srcdir/Belati"

  echo $(git rev-list --count HEAD).$(git rev-parse --short HEAD)
}

prepare() {
  cd "$srcdir/Belati"

  git submodule update --init --recursive --remote
}

package() {
  cd "$srcdir/Belati"

  mkdir -p "$pkgdir/usr/bin"
  mkdir -p "$pkgdir/usr/share/belati"

  install -Dm644 -t "$pkgdir/usr/share/doc/belati/" README.md
  install -Dm644 LICENSE "$pkgdir/usr/share/licenses/belati/LICENSE"

  rm README.md LICENSE

  cp -a * "$pkgdir/usr/share/belati/"

  cat > "$pkgdir/usr/bin/belati" << EOF
#!/bin/sh
cd /usr/share/belati
exec python2 Belati.py "\${@}"
EOF

  chmod a+x "$pkgdir/usr/bin/belati"
}
