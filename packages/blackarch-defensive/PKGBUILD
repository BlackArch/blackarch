# This file is part of BlackArch Linux ( https://www.blackarch.org/ ).
# See COPYING for license details.

pkgname=arpon
_pkgname=ArpON
pkgver=3.0
pkgrel=1
pkgdesc='A portable handler daemon that make ARP protocol secure in order to avoid the Man In The Middle (MITM) attack through ARP Spoofing, ARP Cache Poisoning or ARP Poison Routing (APR) attacks.'
arch=('x86_64' 'aarch64')
url='http://arpon.sourceforge.net/'
license=('custom')
depends=('libdnet' 'libnet' 'libpcap')
makedepends=('cmake' 'libdnet')
backup=('etc/conf.d/arpon')
groups=('blackarch' 'blackarch-defensive')
source=("http://downloads.sourceforge.net/sourceforge/arpon/$_pkgname-$pkgver-ng.tar.gz"
        "arpon"
        "arpon.conf.d")
sha512sums=('e6338018d65f3f8300958e168a9eb6f6be85cba21ae0aee4b03e9838a29a06afbf2448c2f104367aa18389cc549e4489bcf8dad384ad46eadf2884a0908238af'
            '0cd0114efd2f35fcf06ec91a56c5aca66adf13a7b25fbf82b2483489d040447d1a3a609f223f911ad1c407b321c6fbc22d32c9200f08fef2bbbf9ec391b7bb90'
            '75526a64be9ab7e6cc787dbff5cdb1e3d7c3b708fc3f9cc1712ea7b16181767aca4747a00c99f4bec6b0de0f86f131c76ec20e2ef7d24c2237c314df95dfc39d')

build() {
  cd "$_pkgname-$pkgver-ng"

  [ -d build ] || mkdir build

  cd build

  cmake -DPCAP_LIB_DIR="/usr/lib" -DPCAP_LIBRARY="/usr/lib/libpcap.so" ..

  make
}

package() {
  cd "$_pkgname-$pkgver-ng/build"

  make DESTDIR="$pkgdir" install

  install -Dm 755 "$srcdir/$pkgname" "$pkgdir/etc/rc.d/$pkgname"
  install -Dm 644 "$srcdir/$pkgname.conf.d" "$pkgdir/etc/conf.d/$pkgname"
  install -Dm 644 ../LICENSE "$pkgdir/usr/share/licenses/$pkgname/LICENSE"

  mv "$pkgdir/sbin" "$pkgdir/usr/bin"
}

