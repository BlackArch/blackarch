# This file is part of BlackArch Linux ( https://www.blackarch.org/ ).
# See COPYING for license details.

pkgname=lorg
pkgver=98.aa4f1a3
pkgrel=1
pkgdesc='Apache Logfile Security Analyzer.'
groups=('blackarch' 'blackarch-defensive')
arch=('any')
url='https://github.com/jensvoid/lorg'
license=('GPL2')
depends=('php')
makedepends=('git')
source=("git+https://github.com/jensvoid/$pkgname.git")
sha512sums=('SKIP')

pkgver() {
  cd $pkgname

  echo $(git rev-list --count HEAD).$(git rev-parse --short HEAD)
}

package() {
  cd $pkgname

  install -dm 755 "$pkgdir/usr/bin"
  install -dm 755 "$pkgdir/usr/share/$pkgname"

  install -Dm 644 -t "$pkgdir/usr/share/doc/$pkgname/" README.md
  install -Dm 644 LICENSE.md "$pkgdir/usr/share/licenses/$pkgname/LICENSE.md"

  rm README.md LICENSE.md

  cp -a * "$pkgdir/usr/share/$pkgname/"

  cat > "$pkgdir/usr/bin/$pkgname" << EOF
#!/bin/sh
cd /usr/share/$pkgname
exec php $pkgname "\$@"
EOF

  chmod +x "$pkgdir/usr/bin/$pkgname"
}

