# This file is part of BlackArch Linux ( https://www.blackarch.org/ ).
# See COPYING for license details.

pkgbase=python-proxy-db
pkgname=('python2-proxy-db' 'python-proxy-db')
_pkgname=proxy-db
pkgver=0.3.0
pkgrel=1
pkgdesc='Manage free and private proxies on local db for Python Projects.'
arch=('any')
url='https://pypi.org/project/proxy-db/#files'
makedepends=('python2-setuptools' 'python-setuptools')
license=('GPL3')
source=("https://files.pythonhosted.org/packages/a6/f9/e48c24a5fb1c91abe8fa21ca391f3fa5800f54648009857c59fede1a8d03/$_pkgname-$pkgver.tar.gz")
sha512sums=('f0d37f00de6c0ac6edf55786117f971894a105ca9eae92ae57edc8eb3803f4cc6c349f48731b028a0ad4f7c91b6b8168a89876d233db9e6d3ede1e07bd56e4b5')

prepare() {
  cp -a "$_pkgname-$pkgver"{,-py2}
}

build() {
  cd "$_pkgname-$pkgver-py2"

  python2 setup.py build

  cd "$srcdir/$_pkgname-$pkgver"

  python setup.py build
}

package_python2-proxy-db() {
  depends=('python2' 'python2-requests' 'python2-click' 'python2-sqlalchemy'
           'python2-beautifulsoup4' 'python2-geoip2')

  cd "$_pkgname-$pkgver-py2"

  python2 setup.py install --root="$pkgdir" --prefix=/usr -O1 --skip-build

  mv "$pkgdir/usr/bin/proxy-db" "$pkgdir/usr/bin/proxy-db2"

  rm -rf "$pkgdir/usr/lib/python2.7/site-packages/tests"
}

package_python-proxy-db() {
  depends=('python' 'python-requests' 'python-click' 'python-sqlalchemy'
           'python-beautifulsoup4' 'python-geoip2')

  cd "$_pkgname-$pkgver"

  python setup.py install --root="$pkgdir" --prefix=/usr -O1 --skip-build

  rm -rf "$pkgdir/usr/lib/python3.8/site-packages/tests"
}

