# This file is part of BlackArch Linux ( https://www.blackarch.org/ ).
# See COPYING for license details.

pkgname=wascan
pkgver=22.1840571
pkgrel=1
pkgdesc='Web Application Scanner.'
groups=('blackarch' 'blackarch-webapp' 'blackarch-scanner')
arch=('any')
url='https://github.com/m4ll0k/WAScan'
license=('GPL3')
depends=('python2' 'python2-requests' 'python2-beautifulsoup4')
makedepends=('git')
source=('git+https://github.com/m4ll0k/WAScan.git')
sha512sums=('SKIP')

pkgver() {
  cd WAScan

  echo $(git rev-list --count HEAD).$(git rev-parse --short HEAD)
}

package() {
  cd WAScan

  install -dm 755 "$pkgdir/usr/bin"
  install -dm 755 "$pkgdir/usr/share/$pkgname"

  install -Dm644 -t "$pkgdir/usr/share/doc/$pkgname/" README.md

  rm README.md

  cp -a * "$pkgdir/usr/share/$pkgname/"

  cat > "$pkgdir/usr/bin/$pkgname" << EOF
#!/bin/sh
cd /usr/share/$pkgname
exec python2 $pkgname.py "\$@"
EOF

  chmod +x "$pkgdir/usr/bin/$pkgname"
}

