# This file is part of BlackArch Linux ( https://www.blackarch.org/ ).
# See COPYING for license details.

pkgname=enumerate-iam
pkgver=14.4529114
pkgrel=1
pkgdesc='Enumerate the permissions associated with an AWS credential set.'
groups=('blackarch' 'blackarch-recon' 'blackarch-scanner')
arch=('any')
url='https://github.com/andresriancho/enumerate-iam'
license=('GPL3')
depends=('python' 'python-boto3' 'python-botocore')
makedepends=('git')
source=("git+https://github.com/andresriancho/$pkgname.git")
sha512sums=('SKIP')

pkgver() {
  cd $pkgname

  echo $(git rev-list --count HEAD).$(git rev-parse --short HEAD)
}

package() {
  cd $pkgname

  install -dm 755 "$pkgdir/usr/bin"
  install -dm 755 "$pkgdir/usr/share/$pkgname"

  install -Dm 644 -t "$pkgdir/usr/share/doc/$pkgname/" README.md
  install -Dm 644 LICENSE "$pkgdir/usr/share/licenses/$pkgname/LICENSE"

  rm README.md LICENSE .gitignore requirements.txt

  cp -a --no-preserve=ownership * "$pkgdir/usr/share/$pkgname/"

  cat > "$pkgdir/usr/bin/$pkgname" << EOF
#!/bin/sh
cd /usr/share/$pkgname
exec python $pkgname.py "\$@"
EOF

  chmod a+x "$pkgdir/usr/bin/$pkgname"
}

