_author=PJACKLAM
_perlmod=Math-BigInt-GMP
pkgname=perl-math-bigint-gmp
pkgver=1.37
pkgrel=4
pkgdesc="Big integer calculations using the GNU Multiple Precision Arithmetic Library."
arch=('i686' 'x86_64')
url='http://search.cpan.org/~PJACKLAM/Math-BigInt-GMP-1.37/'
license=('GPL' 'PerlArtistic')
depends=('perl>=5.16.2' 'gmp')
options=(!emptydirs)
source=(http://cpan.perl.org/modules/by-authors/id/P/PJ/$_author/$_perlmod-$pkgver.tar.gz)
md5sums=('d11bf69c0471e38191f33144079d0373')

build() {
  cd "$srcdir/$_perlmod-$pkgver"
  # Install module in vendor directories.
  PERL_MM_USE_DEFAULT=1 perl Makefile.PL INSTALLDIRS=vendor
  make
}

check() {
  cd "$srcdir/$_perlmod-$pkgver"
  make test
}

package() {
  cd "$srcdir/$_perlmod-$pkgver"
  make install DESTDIR="$pkgdir/"

  find ${pkgdir} -name '.packlist' -delete
  find ${pkgdir} -name '*.pod' -delete
}
