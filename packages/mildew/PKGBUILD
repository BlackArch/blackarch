# This file is part of BlackArch Linux ( https://www.blackarch.org/ ).
# See COPYING for license details.

pkgname=mildew
pkgver=11.df49c23
pkgrel=1
groups=('blackarch' 'blackarch-recon')
pkgdesc='Dotmil subdomain discovery tool that scrapes domains from official DoD website directories and certificate transparency logs.'
arch=('x86_64' 'aarch64')
url='https://github.com/daehee/mildew'
license=('custom:unknown')
makedepends=('git' 'go')
source=("git+https://github.com/daehee/$pkgname.git")
sha512sums=('SKIP')

pkgver() {
  cd $pkgname

  echo $(git rev-list --count HEAD).$(git rev-parse --short HEAD)
}

build() {
  cd $pkgname

  GOPATH="$srcdir" go mod download
  GOPATH="$srcdir" go build \
    -trimpath \
    -buildmode=pie \
    -mod=readonly \
    -modcacherw \
    -ldflags "-s -w" \
    -o $pkgname-bin ./cmd/$pkgname
}

package() {
  cd $pkgname

  install -Dm 755 $pkgname-bin "$pkgdir/usr/bin/$pkgname"
  install -Dm 644 -t "$pkgdir/usr/share/doc/$pkgname/" README.md
}

