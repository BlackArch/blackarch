# This file is part of BlackArch Linux ( https://www.blackarch.org/ ).
# See COPYING for license details.

pkgname=osint-spy
pkgver=13.76f2c7a
pkgrel=2
pkgdesc='Performs OSINT scan on email/domain/ip_address/organization.'
groups=('blackarch' 'blackarch-recon' 'blackarch-social')
arch=('any')
url='https://github.com/SharadKumar97/OSINT-SPY'
license=('GPL3')
depends=('python2' 'python2-argparse' 'python2-requests' 'python2-clearbit'
         'python2-pythonwhois' 'python2-steganography' 'python2-fullcontact.py'
         'python2-shodan' 'sslscan')
makedepends=('git')
source=('git+https://github.com/SharadKumar97/OSINT-SPY.git')
sha512sums=('SKIP')

pkgver() {
  cd OSINT-SPY

  echo $(git rev-list --count HEAD).$(git rev-parse --short HEAD)
}

package() {
  cd OSINT-SPY

  install -dm 755 "$pkgdir/usr/bin"
  install -dm 755 "$pkgdir/usr/share/$pkgname"

  install -Dm644 -t "$pkgdir/usr/share/doc/$pkgname/" README.md
  install -Dm644 LICENSE "$pkgdir/usr/share/licenses/$pkgname/LICENSE"

  rm README.md LICENSE

  cp -a * "$pkgdir/usr/share/$pkgname/"

  cat > "$pkgdir/usr/bin/$pkgname" << EOF
#!/bin/sh
cd /usr/share/$pkgname
exec python2 osint-spy.py "\$@"
EOF

  chmod +x "$pkgdir/usr/bin/$pkgname"
}

