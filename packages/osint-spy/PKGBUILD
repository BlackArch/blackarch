# This file is part of BlackArch Linux ( http://blackarch.org ).
# See COPYING for license details.

pkgname='osint-spy'
pkgver=12.1fe81d9
pkgrel=1
pkgdesc='Performs OSINT scan on email/domain/ip_address/organization.'
groups=('blackarch' 'blackarch-recon' 'blackarch-social')
arch=('any')
url='https://github.com/SharadKumar97/OSINT-SPY'
license=('GPL3')
depends=('python2' 'python2-argparse' 'python2-requests' 'python2-clearbit'
         'python2-pythonwhois' 'python2-steganography' 'python2-fullcontact.py'
         'shodan' 'sslscan')
makedepends=('git')
source=('git+https://github.com/SharadKumar97/OSINT-SPY.git')
sha1sums=('SKIP')

pkgver() {
  cd "$srcdir/OSINT-SPY"

  echo $(git rev-list --count HEAD).$(git rev-parse --short HEAD)
}

package() {
  cd "$srcdir/OSINT-SPY"

  mkdir -p "$pkgdir/usr/bin"
  mkdir -p "$pkgdir/usr/share/osint-spy"

  install -Dm644 -t "$pkgdir/usr/share/doc/osint-spy/" README.md
  install -Dm644 LICENSE "$pkgdir/usr/share/licenses/osint-spy/LICENSE"

  rm README.md LICENSE

  cp -a * "$pkgdir/usr/share/osint-spy/"

  cat > "$pkgdir/usr/bin/osint-spy" << EOF
#!/bin/sh
cd /usr/share/osint-spy
exec python2 osint-spy.py "\${@}"
EOF

  chmod a+x "$pkgdir/usr/bin/osint-spy"
}
