# This file is part of BlackArch Linux ( https://www.blackarch.org/ ).
# See COPYING for license details.

pkgname=osint-spy
pkgver=25.03dcf48
pkgrel=3
pkgdesc='Performs OSINT scan on email/domain/ip_address/organization.'
groups=('blackarch' 'blackarch-recon' 'blackarch-social')
arch=('any')
url='https://github.com/SharadKumar97/OSINT-SPY'
license=('GPL3')
depends=('python' 'python-argparse' 'python-requests' 'python-clearbit'
         'python-pythonwhois' 'python-steganography' 'python-fullcontactpy'
         'python-shodan' 'sslscan' 'python-prospector')
makedepends=('git')
source=("$pkgname::git+https://github.com/SharadKumar97/OSINT-SPY.git")
sha512sums=('SKIP')

pkgver() {
  cd $pkgname

  echo $(git rev-list --count HEAD).$(git rev-parse --short HEAD)
}

package() {
  cd $pkgname

  install -dm 755 "$pkgdir/usr/bin"
  install -dm 755 "$pkgdir/usr/share/$pkgname"

  install -Dm 644 -t "$pkgdir/usr/share/doc/$pkgname/" README.md
  install -Dm 644 LICENSE "$pkgdir/usr/share/licenses/$pkgname/LICENSE"

  rm README.md LICENSE

  cp -a * "$pkgdir/usr/share/$pkgname/"

  cat > "$pkgdir/usr/bin/$pkgname" << EOF
#!/bin/sh
cd /usr/share/$pkgname
exec python $pkgname.py "\$@"
EOF

  chmod +x "$pkgdir/usr/bin/$pkgname"
}

