# This file is part of BlackArch Linux ( https://www.blackarch.org/ ).
# See COPYING for license details.
#
# Contributor: xgdgsc <xgdgsc @t gmail dot com>

_android=android-11
_sdk=android-sdk
pkgname=android-sdk-build-tools
pkgver=r30.0.3
pkgrel=2
pkgdesc='Build-Tools for Google Android SDK (aapt, aidl, dexdump, dx, llvm-rs-cc).'
arch=('x86_64' 'aarch64')
url='https://developer.android.com/studio/releases/build-tools'
license=('custom:unknown')
depends=('zlib')
source=("https://dl-ssl.google.com/android/repository/build-tools_$pkgver-linux.zip")
sha512sums=('7e5d1f672b249d074ba3b1a175bff987983d8e47c573856d5088ebde0be2db0de83c9ee8b8d4a99a358040d50e67a5a135a593feca8997921d818e2d74cdc090')
options=('!strip')

package() {
  cd $pkgdir

  ver=$(cat "$srcdir/$_android/source.properties" |
  grep ^Pkg.Revision=|sed 's/Pkg.Revision=\([0-9.]*\).*/\1/')

  install -dm 755 "opt/$_sdk/build-tools/$ver"
  install -dm 755 "usr/bin"
  install -Dm 644 "$srcdir/$_android/NOTICE.txt" \
    "usr/share/licenses/$pkgname/NOTICE.txt"
  cp -r "$srcdir/$_android/"* "opt/$_sdk/build-tools/$ver"
  chmod +Xr -R "$pkgdir/opt/$_sdk/build-tools/$ver"

  binaries=("aapt" "aapt2" "aidl" "apksigner" "bcc_compat" "d8" "dexdump" "dx" \
    "llvm-rs-cc" "mainDexClasses" "split-select" "zipalign")

  for f in "${binaries[@]}"
  do
    ln -s "/opt/$_sdk/build-tools/$ver/$f" "usr/bin/$f"
  done
}

