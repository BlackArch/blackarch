# This file is part of BlackArch Linux ( https://www.blackarch.org/ ).
# See COPYING for license details.

pkgname=pivotsuite
pkgver=19.9078d1e
pkgrel=2
pkgdesc='A portable, platform independent and powerful network pivoting toolkit.'
groups=('blackarch' 'blackarch-networking' 'blackarch-backdoor')
arch=('any')
url='https://github.com/RedTeamOperations/PivotSuite'
license=('GPL3')
depends=('python' 'python-ntlm-auth' 'python-six')
makedepends=('git' 'python-setuptools')
source=("$pkgname::git+https://github.com/RedTeamOperations/PivotSuite.git")
sha512sums=('SKIP')

pkgver() {
  cd $pkgname

  echo $(git rev-list --count HEAD).$(git rev-parse --short HEAD)
}

build() {
  cd $pkgname

  python setup.py build
}

package() {
  cd $pkgname

  python setup.py install --root="$pkgdir" --prefix=/usr --skip-build

  install -Dm 644 -t "$pkgdir/usr/share/doc/$pkgname/" README.md
  install -Dm 644 LICENSE.md "$pkgdir/usr/share/licenses/$pkgname/LICENSE"
}

