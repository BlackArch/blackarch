# This file is part of BlackArch Linux ( http://blackarch.org ).
# See COPYING for license details.

pkgname=python2-telnetsrv
pkgver=0.4
pkgrel=3
pkgdesc='Telnet server handler library.'
arch=('any')
url='https://github.com/ianepperson/telnetsrvlib'
license=('GPL3')
depends=('python2' 'python2-gevent' 'python2-paramiko')
makedepends=('python2-setuptools')
source=("https://pypi.python.org/packages/source/t/telnetsrv/telnetsrv-${pkgver}.tar.gz")
sha512sums=('75c69378fecc5f344560d05f2195d9832add03c53a2955b95b030e2f72da3351cc9712fedd08ae576f2e175f8a887f3bc3f67155760fdc2749cb270cef5d4fba')

prepare() {
	cd "$srcdir/telnetsrv-$pkgver"

	find "$srcdir" -type f -name '*.py' |
  xargs sed -i 's|#!/usr/bin/python|#!/usr/bin/python2|'
}

build() {
  cd "$srcdir/telnetsrv-$pkgver"

  python2 setup.py build
}

package() {
  cd "$srcdir/telnetsrv-$pkgver"

  python2 setup.py install --root="$pkgdir" --optimize=1
}
