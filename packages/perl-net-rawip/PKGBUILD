# This file is part of BlackArch Linux ( http://blackarch.org ).
# See COPYING for license details.

pkgname='perl-net-rawip'
pkgver='0.25'
pkgrel=8
pkgdesc='Perl extension to manipulate raw IP packets with interface to B<libpcap>.'
arch=('x86_64' 'armv6h' 'armv7h' 'aarch64')
license=('PerlArtistic' 'GPL')
options=('!emptydirs')
depends=('libpcap>=1.3.0' 'perl')
url='http://search.cpan.org/dist/Net-RawIP'
source=("http://search.cpan.org/CPAN/authors/id/S/SA/SAPER/Net-RawIP-${pkgver}.tar.gz")
sha1sums=('e1e2a85374436e314bce7cfb091415ebf72e6fba')

build() {
  cd "$srcdir/Net-RawIP-$pkgver"

  PERL_MM_USE_DEFAULT=1 perl Makefile.PL INSTALLDIRS=vendor

  make
}

package() {
  cd "$srcdir/Net-RawIP-$pkgver"

  make install DESTDIR="$pkgdir"

  find "$pkgdir" -name '.packlist' -o -name '*.pod' -delete
}
