pkgname=perl-net-rawip
pkgver=0.25
pkgrel=2
pkgdesc="Perl extension to manipulate raw IP packets with interface to B<libpcap>"
arch=('i686' 'x86_64')
license=('PerlArtistic' 'GPL')
options=('!emptydirs')
depends=('libpcap>=1.3.0' 'perl')
url='http://search.cpan.org/dist/Net-RawIP'
source=("http://search.cpan.org/CPAN/authors/id/S/SA/SAPER/Net-RawIP-$pkgver.tar.gz")
md5sums=('a99f461e20e7894154f64729a4652448')

build() {
  cd "$srcdir/Net-RawIP-$pkgver"
  PERL_MM_USE_DEFAULT=1 perl Makefile.PL INSTALLDIRS=vendor
  make
}

check() {
  cd "$srcdir/Net-RawIP-$pkgver"
  PERL_MM_USE_DEFAULT=1
  make test
}

package() {
  cd "$srcdir/Net-RawIP-$pkgver"
  make install DESTDIR="$pkgdir"
  find "$pkgdir" -name '.packlist' -o -name '*.pod' -delete
}
