# This file is part of BlackArch Linux ( https://www.blackarch.org/ ).
# See COPYING for license details.

pkgbase=python-doc2dash
pkgname=('python2-doc2dash' 'python-doc2dash')
_pkgname=doc2dash
pkgver=2.3.0
pkgrel=4
pkgdesc='Convert docs to Dash.app.'
arch=('any')
url='https://pypi.org/project/doc2dash/#files'
license=('MIT')
makedepends=('python2-setuptools' 'python-setuptools')
source=("https://files.pythonhosted.org/packages/b0/4f/8c2ad135e9de65477eff855c5f35494d92ec7226cc63e7f48c8f5aa47784/$_pkgname-$pkgver.tar.gz")
sha512sums=('97baf8df644a3ddb1046699f3ed450a1156b9c851b8cd40b33b267a3a3e28e8d23fc415c0c3970312e43129a1e0680db2e75bdf1330f692b62eccf52ed5b2308')

prepare() {
  cp -a "$_pkgname-$pkgver"{,-2}
}

build() {
  cd "$_pkgname-$pkgver-2"

  python2 setup.py build

  cd "$srcdir/$_pkgname-$pkgver"

  python setup.py build
}

package_python2-doc2dash() {
  depends=('python2' 'python2-sphinx' 'python2-attrs' 'python2-beautifulsoup4'
           'python2-click' 'python2-colorama' 'python2-lxml' 'python2-six'
           'python2-zope-interface')

  cd "$_pkgname-$pkgver-2"

  python2 setup.py install --prefix=/usr --root="$pkgdir" --optimize=1 \
    --skip-build

  mv "$pkgdir/usr/bin/$_pkgname" "$pkgdir/usr/bin/${_pkgname}2"
}

package_python-doc2dash() {
  depends=('python' 'python-sphinx' 'python-attrs' 'python-beautifulsoup4'
           'python-click' 'python-colorama' 'python-lxml' 'python-six'
           'python-zope-interface')

  cd "$_pkgname-$pkgver"

  python setup.py install --prefix=/usr --root="$pkgdir" --optimize=1 \
    --skip-build
}

