# This file is part of BlackArch Linux ( https://www.blackarch.org/ ).
# See COPYING for license details.

pkgbase=python-timeout-decorator
pkgname=('python2-timeout-decorator' 'python-timeout-decorator')
_pkgname=timeout-decorator
pkgver=0.5.0
pkgrel=2
pkgdesc='Timeout decorator.'
arch=('any')
url='https://pypi.org/project/timeout-decorator/#files'
license=('MIT')
makedepends=('python2-setuptools' 'python-setuptools')
source=("https://files.pythonhosted.org/packages/80/f8/0802dd14c58b5d3d72bb9caa4315535f58787a1dc50b81bbbcaaa15451be/$_pkgname-$pkgver.tar.gz")
sha512sums=('ee6d47a27ad3ed7921bc5b98391c4f933e247dc3834c9571248bf2a3b0237acaad88daa8b071eb8d443f74c792318b5be5bb9fad99fa99df682198f90cd86b1b')

prepare() {
  cp -a "$_pkgname-$pkgver"{,-2}
}

build() {
  cd "$_pkgname-$pkgver-2"

  python2 setup.py build

  cd "$srcdir/$_pkgname-$pkgver"

  python setup.py build
}

package_python2-timeout-decorator() {
  depends=('python2')

  cd "$_pkgname-$pkgver-2"

  python2 setup.py install --prefix=/usr --root="$pkgdir" --optimize=1 \
    --skip-build
}

package_python-timeout-decorator() {
  depends=('python')

  cd "$_pkgname-$pkgver"

  python setup.py install --prefix=/usr --root="$pkgdir" --optimize=1 \
    --skip-build
}

