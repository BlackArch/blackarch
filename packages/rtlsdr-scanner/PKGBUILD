# This file is part of BlackArch Linux ( https://www.blackarch.org/ ).
# See COPYING for license details.

pkgname=rtlsdr-scanner
pkgver=v1.3.2.r1.g3c032de
pkgrel=1
epoch=1
pkgdesc='A cross platform Python frequency scanning GUI for the OsmoSDR rtl-sdr library.'
arch=('any')
groups=('blackarch' 'blackarch-scanner')
url='https://github.com/EarToEarOak/RTLSDR-Scanner'
license=('GPL-3.0-or-later')
depends=('python2' 'python2-pyrtlsdr' 'wxpython' 'python2-pyserial'
         'python2-pillow' 'python2-numpy' 'rtl-sdr' 'python2-matplotlib')
makedepends=('git' 'python2-setuptools')
source=("$pkgname::git+https://github.com/EarToEarOak/RTLSDR-Scanner.git")
sha512sums=('SKIP')

pkgver() {
  cd $pkgname

  ( set -o pipefail
    git describe --long --tags --abbrev=7 2>/dev/null |
      sed 's/\([^-]*-g\)/r\1/;s/-/./g' ||
    printf "%s.%s" "$(git rev-list --count HEAD)" \
      "$(git rev-parse --short=7 HEAD)"
  )
}

build() {
  cd $pkgname

  python2 setup.py build
}

package() {
  cd $pkgname

  python2 setup.py install --root="$pkgdir" --skip-build

  install -Dm 644 -t "$pkgdir/usr/share/doc/$pkgname/" readme.md
  install -Dm 644 COPYING "$pkgdir/usr/share/licenses/$pkgname/COPYING"
}

