# This file is part of BlackArch Linux ( http://blackarch.org ).
# See COPYING for license details.

pkgname='ivre'
pkgver=1570.ca15b7a
pkgrel=1
groups=('blackarch' 'blackarch-recon' 'blackarch-networking')
pkgdesc='Network recon framework.'
arch=('any')
url='https://ivre.rocks/'
license=('GPL3')
depends=('python2' 'python2-crypto' 'python2-pymongo' 'python2-matplotlib'
         'python2-py2neo' 'python2-sqlalchemy' 'scapy' 'python2-psycopg2'
         'python2-future')
optdepends=('python2-py2neo: flow analysis support')
makedepends=('git' 'python2-setuptools')
source=('git+https://github.com/cea-sec/ivre.git')
sha1sums=('SKIP')

pkgver() {
  cd "$srcdir/ivre"

  echo $(git rev-list --count HEAD).$(git rev-parse --short HEAD)
}

build() {
  cd "$srcdir/ivre"

  python2 setup.py build
}

package() {
  cd "$srcdir/ivre"

  install -Dm644 -t "$pkgdir/usr/share/licenses/ivre/" doc/LICENSE*

  python2 setup.py install --root="$pkgdir" --optimize=1
}
