# This file is part of BlackArch Linux ( https://www.blackarch.org/ ).
# See COPYING for license details.

pkgname=ivre
pkgver=0.9.12
pkgrel=1
epoch=1
groups=('blackarch' 'blackarch-recon' 'blackarch-networking')
pkgdesc='Network recon framework.'
arch=('any')
url='https://ivre.rocks/'
license=('GPL3')
depends=('python' 'python-pymongo' 'python-future' 'python-bottle')
optdepends=('python-py2neo: experimental flow analysis (Neo4j backend)'
            'python-sqlalchemy: experimental PostgreSQL & SQLite backends'
            'python-psycopg2: experimental PostgreSQL backend'
            'python-pillow: trim screenshots on insertion'
            'tesseract: extract words from screenshots on insertion'
            'python-pycryptodome: extract data from public keys ("ivre getmoduli")'
            'python-scapy: parse PCAP files for ARP inspection (flow analysis)'
            'python-matplotlib: create graphs from command line tools'
            'python-dbus: 3D traceroute graphs'
            'mongodb: database server'
            'postgresql: database server (experimental backend)'
            'neo4j-community: database server (experimental flow analysis)'
            'apache: Web server'
            'mod_wsgi: Web server'
            'dokuwiki: Web server (notebook)'
            'nmap: Network scan'
            'masscan: Network scan'
            'bro: Network traffic analysis'
            'argus: Network traffic analysis'
            'nfdump: Netflow analysis'
            'imagemagick: Screenshots via Nmap scripts'
            'phantomjs: HTTP screenshots via Nmap script'
            'ffmpeg: RTSP Screenshots via Nmap script')
makedepends=('git' 'python-setuptools')
backup=('etc/httpd/conf/extra/ivre.conf')
source=('git+https://github.com/cea-sec/ivre.git')
sha512sums=('SKIP')

pkgver() {
  cd $pkgname

  ver=$(python setup.py --version 2>/dev/null)
  echo ${ver}
}

build() {
  cd $pkgname

  python setup.py build
}

package() {
  cd $pkgname

  install -Dm 0644 -t "$pkgdir/usr/share/licenses/$pkgname/" doc/LICENSE*
  install -Dm 0644 "pkg/apache/ivre.conf" "$pkgdir/etc/httpd/conf/extra/ivre.conf"

  python setup.py install --root="$pkgdir" --optimize=1
}

