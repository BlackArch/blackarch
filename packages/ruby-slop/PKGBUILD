# This file is part of BlackArch Linux ( https://www.blackarch.org/ ).
# See COPYING for license details.

pkgname=ruby-slop
pkgver=4.6.2
pkgrel=6
pkgdesc='Simple Lightweight Option Parsing.'
arch=('any')
url='http://github.com/leejarvis/slop'
license=('MIT')
depends=('ruby')
makedepends=('ruby-rdoc')
options=(!emptydirs)
source=("https://rubygems.org/gems/slop-$pkgver.gem")
noextract=("slop-$pkgver.gem")
sha512sums=('9472e394ed264069a530e075ab8f0ed54ef75dc22d6fede1fc3972370f46879723b5f52197aac6da87e55d638ef2626775f9ac2ac1686639710889df90ec3da6')

package() {
  _gemdir="$(ruby -e'puts Gem.default_dir')"

  gem install --ignore-dependencies --no-user-install -i "$pkgdir/$_gemdir" \
    -n "$pkgdir/usr/bin" slop-$pkgver.gem

  rm "$pkgdir/$_gemdir/cache/slop-$pkgver.gem"

  install -D -m644 "$pkgdir/$_gemdir/gems/slop-$pkgver/LICENSE" \
    "$pkgdir/usr/share/licenses/ruby-slop/LICENSE"
}

