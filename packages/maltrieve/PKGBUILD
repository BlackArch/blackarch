# This file is part of BlackArch Linux ( http://blackarch.org ).
# See COPYING for license details.

pkgname='maltrieve'
pkgver=342.b9e7560
pkgrel=2
groups=('blackarch' 'blackarch-malware' 'blackarch-disassembler')
pkgdesc='Originated as a fork of mwcrawler. It retrieves malware directly from the sources as listed at a number of sites.'
arch=('any')
url='https://github.com/technoskald/maltrieve'
license=('GPL3')
depends=('python2-beautifulsoup4' 'python2-lxml' 'python2-feedparser'
         'python2-gevent' 'python2-greenlet' 'python2-grequests'
         'python2-requests' 'python2-wsgiref')
makedepends=('git')
source=('git+https://github.com/technoskald/maltrieve.git')
sha1sums=('SKIP')

pkgver() {
  cd "$srcdir/maltrieve"

  echo $(git rev-list --count HEAD).$(git rev-parse --short HEAD)
}

package() {
  cd "$srcdir/maltrieve"

  mkdir -p "$pkgdir/usr/bin"
  mkdir -p "$pkgdir/usr/share/maltrieve"

  install -Dm644 README.md "$pkgdir/usr/share/doc/maltrieve/README.md"
  install -Dm644 LICENSE "$pkgdir/usr/share/licenses/maltrieve/LICENSE"

  cp -a * "$pkgdir/usr/share/maltrieve"

  rm "$pkgdir/usr/share/maltrieve/README.md"
  rm "$pkgdir/usr/share/maltrieve/LICENSE"

  cat > "$pkgdir/usr/bin/maltrieve" << EOF
#!/bin/sh
exec python2 /usr/share/maltrieve/maltrieve.py "\$@"
EOF

  chmod +x "$pkgdir/usr/bin/maltrieve"

  cat > "$pkgdir/usr/bin/maltrievecategorizer" << EOF
#!/bin/sh
exec sh /usr/share/maltrieve/maltrievecategorizer.sh "\$@"
EOF

  chmod +x "$pkgdir/usr/bin/maltrievecategorizer"
}
