# This file is part of BlackArch Linux ( https://www.blackarch.org/ ).
# See COPYING for license details.

pkgname=bless
pkgver=176.8480198
pkgrel=4
pkgdesc='Gtk# Hex Editor.'
groups=('blackarch' 'blackarch-misc')
arch=('x86_64' 'aarch64')
url='https://github.com/afrantzis/bless'
license=('GPL2')
depends=('gtk-sharp-2' 'mono')
makedepends=('git' 'rarian' 'automake')
source=("git://github.com/afrantzis/$pkgname.git")
sha512sums=('SKIP')

pkgver() {
  cd $pkgname

  echo "$(git rev-list --count HEAD)"."$(git rev-parse --short HEAD)"
}

prepare() {
  cd $pkgname

  ./autogen.sh
}

build() {
  cd $pkgname

  ./configure --prefix=/usr

  make
}

package() {
  cd $pkgname

  make DESTDIR="$pkgdir" install

  install -Dm 644 -t "$pkgdir/usr/share/doc/$pkgname/" AUTHORS ChangeLog NEWS \
    README README.packaging
  install -Dm 644 COPYING "$pkgdir/usr/share/licenses/$pkgname/LICENSE"
}

