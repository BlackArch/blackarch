# This file is part of BlackArch Linux ( https://www.blackarch.org/ ).
# See COPYING for license details.

pkgname=ndpi
_name=nDPI
pkgver=3.2
pkgrel=1
epoch=1
pkgdesc='Open and Extensible GPLv3 Deep Packet Inspection Library.'
arch=('x86_64' 'aarch64')
url='https://github.com/ntop/nDPI'
depends=('libpcap')
makedepends=('autogen' 'clang')
license=('GPL3')
source=("https://github.com/ntop/$_name/archive/$pkgver.tar.gz")
sha512sums=('ed5a22b6ddc14ad707a18a0bd96746c1df489969faaa42016fa9aad8d414fc4ee303b96cac15c3ba86f484a80a0aaa2dd1be5f92be672912e0e0d30da4bdad4c')

build() {
  cd "$_name-$pkgver"

  ./autogen.sh

  ./configure --prefix=/usr --with-pic --includedir=/usr/include \
    --libdir=/usr/lib --sbindir=/usr/bin

  make
}

package() {
  cd "$_name-$pkgver"

  make DESTDIR="$pkgdir" install

  # dirt temp hack to circumvent the error from xplico search for header files
  # in /usr/include/libndpi ...
  mkdir -p "$pkgdir/usr/include/libndpi"
  cp -a src/include/*.h "$pkgdir/usr/include/libndpi"

  # fix symlinks
  ln -fs /usr/lib/libndpi.so.2.8.0 "$pkgdir/usr/lib/libndpi.so"
  ln -fs /usr/lib/libndpi.so.2.8.0 "$pkgdir/usr/lib/libndpi.so.2"

  rm -rf "$pkgdir/usr/sbin"
}

