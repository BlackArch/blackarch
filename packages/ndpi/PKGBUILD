# This file is part of BlackArch Linux ( https://www.blackarch.org/ ).
# See COPYING for license details.

pkgname=ndpi
_name=nDPI
pkgver=2.8
pkgrel=1
pkgdesc='Open and Extensible GPLv3 Deep Packet Inspection Library.'
arch=('x86_64' 'armv6h' 'armv7h' 'aarch64')
url='https://github.com/ntop/nDPI'
depends=('libpcap')
makedepends=('autogen' 'automake' 'autoconf' 'clang' 'gcc' 'libtool')
license=('GPL3')
source=("https://github.com/ntop/$_name/archive/$pkgver.tar.gz")
sha512sums=('aae757047810bbc32700ecf7ac2e5df4b93528021a50da4a0b83865a5248dfca8b1643c75aa7eeb6eac4f155638d45666db487318b2be9adb2f3412cb8ec8497')

build() {
  cd "$_name-$pkgver"

  ./autogen.sh

  ./configure --prefix=/usr --with-pic --includedir=/usr/include \
    --libdir=/usr/lib

  make
}

package() {
  cd "$_name-$pkgver"

  make DESTDIR="$pkgdir" install

  # dirt temp hack to circumvent the error from xplico search for header files
  # in /usr/include/libndpi ...
  mkdir -p "$pkgdir/usr/include/libndpi"
  cp -a src/include/*.h "$pkgdir/usr/include/libndpi"

  # fix symlinks
  ln -fs /usr/lib/libndpi.so.2.8.0 "$pkgdir/usr/lib/libndpi.so"
  ln -fs /usr/lib/libndpi.so.2.8.0 "$pkgdir/usr/lib/libndpi.so.2"
}

