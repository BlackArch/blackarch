# This file is part of BlackArch Linux ( https://www.blackarch.org/ ).
# See COPYING for license details.

pkgname=ndpi
_name=nDPI
pkgver=4.2
pkgrel=1
epoch=1
pkgdesc='Open and Extensible GPLv3 Deep Packet Inspection Library.'
arch=('x86_64' 'aarch64')
url='https://github.com/ntop/nDPI'
depends=('libpcap')
makedepends=('autogen' 'clang')
license=('GPL3')
source=("https://github.com/ntop/$_name/archive/$pkgver.tar.gz")
sha512sums=('c58b228b7de2b5fa111234bb065f9be259cd0282d0058f5985180aab6567c9a18e3453ce9062bd3c057a096ec3dfdc21b7c9680e0148e34af10f707530d853b8')

build() {
  cd "$_name-$pkgver"

  ./autogen.sh

  ./configure --prefix=/usr --with-pic --includedir=/usr/include \
    --libdir=/usr/lib --sbindir=/usr/bin

  make
}

package() {
  cd "$_name-$pkgver"

  make DESTDIR="$pkgdir" install

  # dirt temp hack to circumvent the error from xplico search for header files
  # in /usr/include/libndpi ...
  mkdir -p "$pkgdir/usr/include/libndpi"
  cp -a src/include/*.h "$pkgdir/usr/include/libndpi"

  # fix symlinks
  ln -fs /usr/lib/libndpi.so.2.8.0 "$pkgdir/usr/lib/libndpi.so"
  ln -fs /usr/lib/libndpi.so.2.8.0 "$pkgdir/usr/lib/libndpi.so.2"

  rm -rf "$pkgdir/usr/sbin"
}

