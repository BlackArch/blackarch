# This file is part of BlackArch Linux ( https://www.blackarch.org/ ).
# See COPYING for license details.

pkgname=flare-floss
pkgver=v3.0.1.r21.g50c9071
pkgrel=2
epoch=1
pkgdesc='Obfuscated String Solver - Automatically extract obfuscated strings from malware.'
groups=('blackarch' 'blackarch-malware')
arch=('x86_64' 'aarch64')
url='https://github.com/mandiant/flare-floss'
license=('Apache')
depends=('python' 'python-tabulate' 'vivisect' 'python-viv-utils'
         'python-pydantic' 'python-tqdm' 'python-networkx' 'python-halo'
         'python-rich' 'python-pefile')
makedepends=('git' 'python-setuptools')
source=("git+https://github.com/mandiant/$pkgname.git")
sha512sums=('SKIP')

pkgver() {
  cd $pkgname

  git describe --long --tags | sed 's/\([^-]*-g\)/r\1/;s/-/./g'
}

prepare() {
  cd $pkgname

  sed -i 's/==/>=/' setup.py
}

build() {
  cd $pkgname

  python setup.py build
}

package() {
  cd $pkgname

  python setup.py install --root="$pkgdir" --prefix=/usr -O1 --skip-build
}

