# This file is part of BlackArch Linux ( https://www.blackarch.org/ ).
# See COPYING for license details.

pkgname=flare-floss
pkgver=1.7.0
pkgrel=1
pkgdesc='Obfuscated String Solver - Automatically extract obfuscated strings from malware.'
groups=('blackarch' 'blackarch-malware')
arch=('x86_64')
url='https://github.com/fireeye/flare-floss'
license=('Apache')
depends=('python2' 'python2-utils' 'python2-simplejson' 'python2-tabulate'
         'python2-plugnplay' 'python2-pyaml' 'python2-viv-utils' 'python2-q'
         'python2-enum34' 'vivisect')
makedepends=('git' 'python2-setuptools')
source=("https://github.com/fireeye/$pkgname/archive/v$pkgver.tar.gz")
#source=("https://github.com/fireeye/flare-floss/releases/download/v$pkgver/floss-$pkgver-GNU.Linux.zip")
sha512sums=('a2beb6c71669f47e30330b2366c52e998c54df8173fb0456916ac277fa6af31b6acabbd4c83f371e213fa9739d1cd072cb55741ef458d227c00245a3369f0512')

prepare() {
  cd "$pkgname-$pkgver"

  sed -i 's/vivisect==0.1.0/vivisect/' setup.py
  sed -i 's/viv-utils==0.3.17/viv-utils/' setup.py
}

build() {
  cd "$pkgname-$pkgver"

  python2 setup.py build
}

package() {
  cd "$pkgname-$pkgver"

  python2 setup.py install --root="$pkgdir" --prefix=/usr -O1 --skip-build
}

