# This file is part of BlackArch Linux ( https://www.blackarch.org/ ).
# See COPYING for license details.

pkgname=flare-floss
pkgver=675.0b2d097
pkgrel=2
pkgdesc='Obfuscated String Solver - Automatically extract obfuscated strings from malware.'
groups=('blackarch' 'blackarch-malware')
arch=('x86_64')
url='https://github.com/fireeye/flare-floss'
license=('Apache')
depends=('python' 'python-tabulate' 'python-simplejson' 'python-viv-utils'
         'vivisect')
makedepends=('git' 'python-setuptools')
#source=("https://github.com/fireeye/$pkgname/archive/v$pkgver.tar.gz")
source=("git+https://github.com/fireeye/$pkgname.git"
        'setup.py.patch')
sha512sums=('SKIP'
            '74db2205c2aedec7e94e193a0ae8c57e66a6593b51518034b9e658feb4a43263c23ee414dc833342ef29e0eb4f81e944d9fc73806c2247235daf143f4addff7d')

pkgver() {
  cd $pkgname

  echo $(git rev-list --count HEAD).$(git rev-parse --short HEAD)
}

prepare() {
  cd $pkgname

  patch -p0 < ../setup.py.patch
}

build() {
  cd $pkgname

  python setup.py build
}

package() {
  cd $pkgname

  python setup.py install --root="$pkgdir" --prefix=/usr -O1 --skip-build
}

