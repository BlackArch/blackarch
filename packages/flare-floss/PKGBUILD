# This file is part of BlackArch Linux ( https://www.blackarch.org/ ).
# See COPYING for license details.

pkgname=flare-floss
pkgver=v3.1.1.r47.g40da5c9
pkgrel=1
epoch=1
pkgdesc='Obfuscated String Solver - Automatically extract obfuscated strings from malware.'
groups=('blackarch' 'blackarch-malware')
arch=('x86_64' 'aarch64')
url='https://github.com/mandiant/flare-floss'
license=('Apache')
depends=('python' 'python-tabulate' 'vivisect' 'python-viv-utils'
         'python-pydantic' 'python-tqdm' 'python-networkx' 'python-halo'
         'python-rich' 'python-pefile')
makedepends=('git' 'python-build' 'python-pip')
source=("git+https://github.com/mandiant/$pkgname.git")
sha512sums=('SKIP')

pkgver() {
  cd $pkgname

  git describe --long --tags | sed 's/\([^-]*-g\)/r\1/;s/-/./g'
}

build() {
  cd $pkgname

  python -m build --wheel --outdir="$startdir/dist"
}

package() {
  cd $pkgname

	pip install \
    --verbose \
    --disable-pip-version-check \
    --no-warn-script-location \
    --ignore-installed \
    --no-compile \
    --no-deps \
    --root="$pkgdir" \
    --prefix=/usr \
    --no-index \
    --find-links="file://$startdir/dist" \
    $pkgname
}

