# This file is part of BlackArch Linux ( https://www.blackarch.org/ ).
# See COPYING for license details.

pkgname=flare-floss
pkgver=860.7138b61
pkgrel=2
pkgdesc='Obfuscated String Solver - Automatically extract obfuscated strings from malware.'
groups=('blackarch' 'blackarch-malware')
arch=('x86_64')
url='https://github.com/fireeye/flare-floss'
license=('Apache')
depends=('python' 'python-tabulate' 'python-simplejson' 'python-viv-utils'
         'vivisect')
makedepends=('git' 'python-setuptools')
#source=("https://github.com/fireeye/$pkgname/archive/v$pkgver.tar.gz")
source=("git+https://github.com/fireeye/$pkgname.git"
        'setup.py.patch')
sha512sums=('SKIP'
            'ee135a3097238d928831d82481fbe8af24d054ad7b9d07b9893086109c063ed5a6605a364d80853799b29f988d7a4b7c979d9d8663a2ce9dd6201cedbd7f4f73')

pkgver() {
  cd $pkgname

  echo $(git rev-list --count HEAD).$(git rev-parse --short HEAD)
}

prepare() {
  cd $pkgname

  patch -p0 < ../setup.py.patch
}

build() {
  cd $pkgname

  python setup.py build
}

package() {
  cd $pkgname

  python setup.py install --root="$pkgdir" --prefix=/usr -O1 --skip-build
}

