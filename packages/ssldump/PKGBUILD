# This file is part of BlackArch Linux ( https://www.blackarch.org/ ).
# See COPYING for license details.

pkgname=ssldump
pkgver=1.8
pkgrel=1
groups=('blackarch' 'blackarch-sniffer')
pkgdesc='An SSLv3/TLS network protocol analyzer.'
arch=('x86_64' 'aarch64')
url='http://www.rtfm.com/ssldump/'
license=('custom:unknown')
depends=('openssl' 'json-c' 'libnet' 'libpcap')
source=("https://github.com/adulau/ssldump/archive/refs/tags/v$pkgver.tar.gz")
sha512sums=('5435187fa851cddd167a5ee7e79f8051752780512873195b215e8695430282338f22719e6ddd32bd4eae3cc9a385abd44a4b369f93dc201908696aaed214b9bc')

prepare() {
  sed -i 's|/usr/local|/usr|' $pkgname-$pkgver/CMakeLists.txt
}

build() {
  cmake -G Ninja -B $pkgname-$pkgver/build -S $pkgname-$pkgver \
    -DCMAKE_BUILD_TYPE=None \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -Wno-dev
  ninja -C $pkgname-$pkgver/build
  $pkgname-$pkgver/build/ssldump -v
}

package() {
  install -Dm644 $pkgname-$pkgver/COPYRIGHT "$pkgdir/usr/share/licenses/$pkgname/LICENSE"
  DESTDIR="$pkgdir" ninja -C $pkgname-$pkgver/build install
}
