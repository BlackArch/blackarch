# This file is part of BlackArch Linux ( http://blackarch.org ).
# See COPYING for license details.

pkgname=ssldump
pkgver=0.9b3
pkgrel=12
groups=('blackarch' 'blackarch-sniffer')
pkgdesc="an SSLv3/TLS network protocol analyzer"
url='http://www.rtfm.com/ssldump/'
license=('custom')
arch=('x86_64' 'armv6h' 'armv7h' 'aarch64')
depends=('libpcap>=1.0.0' 'openssl')
source=("http://downloads.sourceforge.net/project/ssldump/ssldump/0.9b3/ssldump-0.9b3.tar.gz"
        'openssl-0.9.8.patch')
sha512sums=('ea81558a243950ab43354c9f33c0a4feae0ae859bc2bd6e6b58838a01f4a1e7a6447f2a9ab1fa40bbe8dbd6c3630c489c17fc9c066cacfddfb64269b0cd5090a'
            '21be9134e64d57434e13f4ccf072f1a52f874ce326051fa00f243ad7de8ceec4f0b3d6291cf06749c367c2120ae4c131f940cb994d05232245af0ce040102428')
#http://www.rtfm.com/ssldump/ssldump-0.9b3.tar.gz

prepare() {
  cd $srcdir/ssldump-$pkgver
  patch -Np0 -i ../openssl-0.9.8.patch
  sed -in 's/libpcap.a/libpcap.so/g' configure
}

build() {
  cd $srcdir/ssldump-$pkgver

  # Arch64 fixes.
  ./configure --prefix=/usr \
              --build=i686-pc-linux-gnu \
              --host=i686-pc-linux-gnu \
              "--sbindir=$pkgdir/usr/bin"

  make
}

package() {
  cd $srcdir/ssldump-$pkgver

  make "prefix=$pkgdir/usr" install

  # Install license.
  install -D -m644 COPYRIGHT $pkgdir/usr/share/licenses/ssldump/LICENSE
}
