pkgname=ssldump
pkgver=0.9b3
pkgrel=9
pkgdesc='an SSLv3/TLS network protocol analyzer'
url="http://www.rtfm.com/ssldump/"
license=('custom')
arch=('i686' 'x86_64')
depends=('libpcap>=1.0.0' 'openssl')
groups=(blackarch blackarch-intel)
source=(http://www.rtfm.com/$pkgname/$pkgname-$pkgver.tar.gz
        openssl-0.9.8.patch)
md5sums=('ac8c28fe87508d6bfb06344ec496b1dd'
         '5757ee10663a6ded190537f3599453eb')
#http://www.rtfm.com/ssldump/ssldump-0.9b3.tar.gz

prepare() {
  cd $srcdir/$pkgname-$pkgver
  patch -Np0 -i ../openssl-0.9.8.patch
  sed -in 's/libpcap.a/libpcap.so/g' configure
}

build() {
  cd $srcdir/$pkgname-$pkgver

  # Arch64 fixes.
  ./configure --prefix=/usr \
              --build=i686-pc-linux-gnu \
              --host=i686-pc-linux-gnu

  make
}

package() {
  cd $srcdir/$pkgname-$pkgver

  make prefix=$pkgdir/usr install

  # Install license.
  install -D -m644 COPYRIGHT $pkgdir/usr/share/licenses/$pkgname/LICENSE
}
