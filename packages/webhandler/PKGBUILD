# This file is part of BlackArch Linux ( http://blackarch.org ).
# See COPYING for license details.

pkgname=webhandler
pkgver=344.a7490cf
pkgrel=1
groups=('blackarch' 'blackarch-webapp')
pkgdesc="A handler for PHP system functions & also an alternative 'netcat' handler."
url='https://github.com/lnxg33k/webhandler'
license=('custom')
arch=('any')
depends=('python2')
makedepends=('git')
source=('git+https://github.com/lnxg33k/webhandler.git')
sha512sums=('SKIP')

pkgver() {
  cd "$srcdir/webhandler"

  echo $(git rev-list --count HEAD).$(git rev-parse --short HEAD)
}

package() {
  cd "$srcdir/webhandler"

  install -dm 755 "$pkgdir/usr/bin"
  install -dm 755 "$pkgdir/usr/share/webhandler"

  cp -a --no-preserve=ownership * "$pkgdir/usr/share/webhandler"

  cat > "$pkgdir/usr/bin/webhandler" << EOF
#!/bin/sh
exec python2 /usr/share/webhandler/webhandler.py "\$@"
EOF

  chmod +x "$pkgdir/usr/bin/webhandler"
}
