# This file is part of BlackArch Linux ( https://www.blackarch.org/ ).
# See COPYING for license details.

pkgname=littleblackbox
pkgver=0.1.3
pkgrel=3
pkgdesc='Penetration testing tool, search in a collection of thousands of private SSL keys extracted from various embedded devices.'
arch=('x86_64' 'aarch64')
groups=('blackarch' 'blackarch-scanner' 'blackarch-fuzzer' 'blackarch-crypto')
url='http://code.google.com/p/littleblackbox/wiki/FAQ'
license=('MIT')
depends=('openssl-1.0' 'libpcap' 'sqlite' 'curl')
makedepends=('tar')
source=("https://web.archive.org/web/20231209111455/https://storage.googleapis.com/google-code-archive-downloads/v2/code.google.com/$pkgname/$pkgname-$pkgver.tar.gz")
sha512sums=('5bcc61b49126b4a17387cd308756dabf2c2a7252f99eaf891b59ad4dd2ccfef00c36cf7fd1fe906678f0949421d386261f99323312e411c2e95d1262f2d72533')

prepare() {
  cd "$pkgname-$pkgver"

  sed -i '/#include <curl\/types.h>/d' src/dbupdate.c
}

build() {
  cd "$pkgname-$pkgver"

  rm -f bin/littleblackbox 2>/dev/null

  cd src

  make distclean

  ./configure --prefix=/usr

  make
}

package() {
  cd "$pkgname-$pkgver"

  install -dm 755 "$pkgdir/usr/share/doc/$pkgname"

  install -Dm 755 bin/littleblackbox "$pkgdir/usr/bin/$pkgname"
  install -Dm 644 bin/lbb.db "$pkgdir/usr/share/$pkgname/lbb.db"
  install -Dm 644 docs/LICENSE "$pkgdir/usr/share/licenses/$pkgname/LICENSE"
  install -Dm 644 -t "$pkgdir/usr/share/doc/$pkgname/" \
    docs/{FAQ,README,submission.txt} \

  ln -s /usr/share/littleblackbox/lbb.db "$pkgdir/usr/bin/lbb.db"
}

