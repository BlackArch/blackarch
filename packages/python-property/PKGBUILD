# This file is part of BlackArch Linux ( https://www.blackarch.org/ ).
# See COPYING for license details.

pkgbase=python-property
pkgname=('python2-property' 'python-property')
_pkgname=property
pkgver=2.2
pkgrel=2
pkgdesc='A python module to read a property file.'
arch=('any')
url='https://pypi.org/project/property/#files'
license=('custom:unknown')
makedepends=('python2-setuptools' 'python-setuptools')
source=("https://files.pythonhosted.org/packages/c5/72/61b8bbf54755ed01f7ec567b65182eae98f1e4081e7396a9201649a30d18/$_pkgname-$pkgver.tar.gz")
sha512sums=('274f0dc749f1c7fd2ba2bac78abce4abfb630378447b6b6e638b9bf2d9fde2b78abfeea2d7ad813dcecebd7889bc5e582fccab77968c489f7ed51b67b7166818')

prepare() {
  cp -a "$_pkgname-$pkgver"{,-2}
}

build() {
  cd "$_pkgname-$pkgver-2"

  python2 setup.py build

  cd "$srcdir/$_pkgname-$pkgver"

  python setup.py build
}

package_python2-property() {
  depends=('python2')

  cd "$_pkgname-$pkgver-2"

  python2 setup.py install --prefix=/usr --root="$pkgdir" -O1 --skip-build
}

package_python-property() {
  depends=('python')

  cd "$_pkgname-$pkgver"

  python setup.py install --prefix=/usr --root="$pkgdir" -O1 --skip-build
}

