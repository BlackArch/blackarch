# This file is part of BlackArch Linux ( https://www.blackarch.org/ ).
# See COPYING for license details.

pkgbase=python-property
pkgname=('python2-property' 'python-property')
_pkgname=property
pkgver=2.6.2
pkgrel=1
pkgdesc='A python module to read a property file.'
arch=('any')
url='https://pypi.org/project/property/#files'
license=('custom:unknown')
makedepends=('python2-setuptools' 'python-setuptools' 'python2-pip' 'python-pip'
             'python2-wheel' 'python-wheel')
source=("https://files.pythonhosted.org/packages/source/${_pkgname::1}/$_pkgname/$_pkgname-$pkgver.tar.gz")
sha512sums=('0eed7208a60f7c76ca412e7582584f99566b597e9dfe2ef1245a30e3f7f401c7fc42ecd8b0d85e2033c0b4172de30a20b9f03e37aa7e87d32ee95951489cc82a')

prepare() {
  cp -a "$_pkgname-$pkgver"{,-2}
}

build() {
  cd "$_pkgname-$pkgver-2"

  python2 setup.py build

  cd "$srcdir/$_pkgname-$pkgver"

  python setup.py build
}

package_python2-property() {
  depends=('python2')

  cd "$_pkgname-$pkgver-2"

  python2 setup.py install --prefix=/usr --root="$pkgdir" -O1 --skip-build
}

package_python-property() {
  depends=('python')

  cd "$_pkgname-$pkgver"

  python setup.py install --prefix=/usr --root="$pkgdir" -O1 --skip-build
}

