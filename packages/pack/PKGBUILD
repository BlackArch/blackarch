pkgname=pack
_pkgname=PACK
pkgver=0.0.3
groups=(blackarch blackarch-password-attacks)
pkgrel=3
pkgdesc="Password Analysis and Cracking Kit"
url='http://thesprawl.org/projects/pack/'
arch=(any)
license=('unknown')
depends=('python2' 'aspell' 'aspell-en')
#makedepends=('patch')
source=("http://thesprawl.org/media/projects/PACK-0.0.3.tar.gz"
        "fix-python2-invocation.patch"
        "reorganize-enchant-modules.patch"
        "fix-rulegen-module-refs.patch")
md5sums=('eac43c9d09766caa340bafe9fcd27985'
         'cbef5aa59f4681b30fb2fe8569b96764'
         '855ca08a56f0d0a5310c9d3877806cef'
         '084e527798684c69012281c76f85c6b3')

prepare() {
  cd ${srcdir}/${_pkgname}-${pkgver}

  patch -Np1 -i ../fix-python2-invocation.patch
  patch -Np1 -i ../reorganize-enchant-modules.patch
  patch -Np1 -i ../fix-rulegen-module-refs.patch
}

package() {
  cd ${srcdir}/${_pkgname}-${pkgver}

  install -dm755 $pkgname
  touch $pkgname/__init__.py

  install -d $pkgdir/usr/bin
  install -d $pkgdir/usr/share/doc/$pkgname

  install -Dm755 dictstat.py $pkgdir/usr/bin/dictstat
  install -Dm755 maskgen.py $pkgdir/usr/bin/maskgen
  install -Dm755 policygen.py $pkgdir/usr/bin/policygen
  install -Dm755 rulegen.py $pkgdir/usr/bin/rulegen
  install -Dm644 README $pkgdir/usr/share/doc/$pkgname/README

  install -dm755 $pkgdir/usr/lib/python2.7/$pkgname
  install -Dm644 $pkgname/__init__.py $pkgdir/usr/lib/python2.7/$pkgname

  install -dm755 $pkgdir/usr/lib/python2.7/$pkgname/enchant
  install -Dm644 enchant/*.py $pkgdir/usr/lib/python2.7/$pkgname/enchant
  install -dm755 $pkgdir/usr/lib/python2.7/$pkgname/enchant/checker
  install -Dm644 enchant/checker/*.py $pkgdir/usr/lib/python2.7/$pkgname/enchant/checker
  install -dm755 $pkgdir/usr/lib/python2.7/$pkgname/enchant/lib/enchant
  install -Dm644 enchant/lib/enchant/README.txt $pkgdir/usr/lib/python2.7/$pkgname/enchant/lib/enchant/README.txt

  install -dm755 $pkgdir/usr/lib/python2.7/$pkgname/enchant/share/enchant
  install -Dm644 enchant/share/enchant/README.txt $pkgdir/usr/lib/python2.7/$pkgname/enchant/share/enchant/README.txt
  install -dm755 $pkgdir/usr/lib/python2.7/$pkgname/enchant/share/enchant/ispell
  install -Dm644 enchant/share/enchant/ispell/README.txt $pkgdir/usr/lib/python2.7/$pkgname/enchant/share/enchant/ispell/README.txt
  install -dm755 $pkgdir/usr/lib/python2.7/$pkgname/enchant/share/enchant/myspell
  install -Dm644 enchant/share/enchant/myspell/README.txt $pkgdir/usr/lib/python2.7/$pkgname/enchant/share/enchant/myspell/README.txt

  install -dm755 $pkgdir/usr/lib/python2.7/$pkgname/enchant/tokenize
  install -Dm644 enchant/tokenize/*.py $pkgdir/usr/lib/python2.7/$pkgname/enchant/tokenize
}
