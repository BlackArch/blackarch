# This file is part of BlackArch Linux ( https://www.blackarch.org/ ).
# See COPYING for license details.
#
# Original Maintainer: Behruz <extreme29@proton.me>

pkgname=promptfoo
pkgver=0.120.20
pkgrel=1
pkgdesc='Test and evaluate LLM outputs - AI red teaming, pentesting, and vulnerability scanning.'
arch=('x86_64' 'aarch64')
groups=('blackarch' 'blackarch-ai')
url='https://github.com/promptfoo/promptfoo'
license=('MIT')
depends=('nodejs' 'sqlite')
options=('!strip')
makedepends=('npm' 'jq' 'python' 'make' 'gcc')
optdepends=(
  'python: for Python providers'
  'ollama: for local Ollama models'
)
source=("https://registry.npmjs.org/$pkgname/-/$pkgname-$pkgver.tgz")
noextract=("$pkgname-$pkgver.tgz")
sha512sums=('0c7309136617c6621d66b0dd80b153f2afc9adcf2b62f026498dbab043d3a2e04aa2f95592c0a57f41bca00a5a4940b0837e748300eda572d89338f4645e79cd')

package() {
  npm install -g --cache npm-cache --prefix "$pkgdir/usr" \
    "$pkgname-$pkgver.tgz"

  # Non-deterministic race in npm gives 777 permissions to random directories.
  # See https://github.com/npm/cli/issues/1103 for details.
  find "$pkgdir/usr" -type d -exec chmod 755 {} +

  # npm gives ownership of ALL FILES to build user
  # https://bugs.archlinux.org/task/63396
  chown -R root:root "${pkgdir}"

  # Remove references to pkgdir
  find "$pkgdir" -name package.json -print0 | xargs -r -0 sed -i '/_where/d'

  # Remove references to srcdir
  local tmppackage="$(mktemp)"
  local pkgjson="$pkgdir/usr/lib/node_modules/$pkgname/package.json"
  jq '.|=with_entries(select(.key|test("_.+")|not))' "$pkgjson" > "$tmppackage"
  mv "$tmppackage" "$pkgjson"
  chmod 644 "$pkgjson"

  # Install license
  install -Dm 644 "$pkgdir/usr/lib/node_modules/$pkgname/LICENSE" \
    "$pkgdir/usr/share/licenses/$pkgname/LICENSE"
}

