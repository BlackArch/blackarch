# This file is part of BlackArch Linux ( http://blackarch.org ).
# See COPYING for license details.

pkgname='apkid'
pkgver=145.8363303
pkgrel=1
pkgdesc='Android Application Identifier for Packers, Protectors, Obfuscators and Oddities.'
groups=('blackarch' 'blackarch-mobile')
arch=('any')
url='https://github.com/rednaga/APKiD'
license=('GPL3')
depends=('python2' 'python2-yara' 'python2-argparse')
makedepends=('git' 'python2-setuptools')
source=('git+https://github.com/rednaga/APKiD.git')
sha1sums=('SKIP')

pkgver() {
  cd "$srcdir/APKiD"

  echo $(git rev-list --count HEAD).$(git rev-parse --short HEAD)
}

prepare() {
  cd "$srcdir/APKiD"

  sed -i 's|yara-python==3.5.0.999|yara-python|' setup.py
}

build() {
  cd "$srcdir/APKiD"

  python2 setup.py build
}

package() {
  cd "$srcdir/APKiD"

  python2 setup.py install --root="$pkgdir" --optimize=1

  cp -a apkid/rules "$pkgdir/usr/lib/python2.7/site-packages/apkid/rules"

  install -Dm644 -t "$pkgdir/usr/share/doc/apkid/" README.md README.rst \
    AUTHORS.txt
  install -Dm644 -t "$pkgdir/usr/share/licenses/apkid" LICENSE.GPL \
    LICENSE.COMMERCIAL
}
