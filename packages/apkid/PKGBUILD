# This file is part of BlackArch Linux ( https://www.blackarch.org/ ).
# See COPYING for license details.

pkgname=apkid
_pkgname=APKiD
pkgver=2.0.3
pkgrel=2
epoch=2
pkgdesc='Android Application Identifier for Packers, Protectors, Obfuscators and Oddities.'
groups=('blackarch' 'blackarch-mobile')
arch=('any')
url='https://github.com/rednaga/APKiD'
license=('GPL3')
depends=('python' 'python-argparse' 'python-yara-rednaga')
makedepends=('python-setuptools' 'python-yara-rednaga')
source=("https://github.com/rednaga/$_pkgname/archive/v$pkgver.tar.gz"
        'setup.py.patch')
sha512sums=('ec53334c283875d4e6521c30f0156ab71870bdfd8f6da36daccb6869670d036a71aa94cc4693d4cb28e398a31d145d351bdf6dde876e424f47f727cf8a2340cb'
            '9a769012701d1587bf5daefd6e430faa8fd62d7e49383c3f3b7d0bd1080bb986e5b741b85df11c6469aeda1356bcd902ceb36872035a6177b293416ad491cffb')

prepare() {
  patch -p1 < setup.py.patch

  cd "$_pkgname-$pkgver"

  ./prep-release.py
}

build() {
  cd "$_pkgname-$pkgver"

  python setup.py build
}

package() {
  cd "$_pkgname-$pkgver"

  python setup.py install --root="$pkgdir" --prefix=/usr -O1 --skip-build

  install -Dm 644 -t "$pkgdir/usr/share/doc/$pkgname/" README.md README.rst \
    AUTHORS.txt
  install -Dm 644 -t "$pkgdir/usr/share/licenses/$pkgname" LICENSE.GPL \
    LICENSE.COMMERCIAL
}

