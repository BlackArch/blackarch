# This file is part of BlackArch Linux ( http://blackarch.org ).
# See COPYING for license details.

pkgname=apkid
_pkgname=APKid
pkgver=v1.2.1.r3.g956d6dd
pkgrel=3
epoch=1
pkgdesc='Android Application Identifier for Packers, Protectors, Obfuscators and Oddities.'
groups=('blackarch' 'blackarch-mobile')
arch=('any')
url='https://github.com/rednaga/APKiD'
license=('GPL3')
depends=('python' 'python-argparse' 'python-yara-rednaga')
makedepends=('git' 'python-setuptools')
source=("git+https://github.com/rednaga/$_pkgname.git")
sha512sums=('SKIP')

pkgver() {
  cd "$_pkgname"

  git describe --long --tags | sed 's/\([^-]*-g\)/r\1/;s/-/./g'
}

prepare() {
  cd "$_pkgname"

  ./prep-release.py
}

build() {
  cd "$_pkgname"

  python setup.py build
}

package() {
  cd "$_pkgname"

  python setup.py install --root="$pkgdir" --optimize=1

  install -Dm 644 -t "$pkgdir/usr/share/doc/apkid/" README.md README.rst \
    AUTHORS.txt
  install -Dm 644 -t "$pkgdir/usr/share/licenses/apkid" LICENSE.GPL \
    LICENSE.COMMERCIAL
}

