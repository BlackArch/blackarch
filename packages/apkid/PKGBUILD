# This file is part of BlackArch Linux ( https://www.blackarch.org/ ).
# See COPYING for license details.

pkgname=apkid
pkgver=2.1.0
pkgrel=2
epoch=2
pkgdesc='Android Application Identifier for Packers, Protectors, Obfuscators and Oddities.'
groups=('blackarch' 'blackarch-mobile')
arch=('any')
url='https://github.com/rednaga/APKiD'
license=('GPL3')
depends=('python' 'python-argparse' 'python-yara-rednaga')
makedepends=('python-setuptools' 'python-yara-rednaga')
source=("https://files.pythonhosted.org/packages/d7/ee/e79f103568256cc700fc233e8b0843ed7db8ee078c3cd80e895ce9307819/$pkgname-$pkgver.tar.gz"
        'setup.py.patch')
sha512sums=('93e04eb21db257fcb1fb11751e3c08c0d60e1da56bb8c0f3d8fbd1e21544bb825ea70c63e1020fe9e55bfe0cef34d4928fd04699466268ac3afdc28608acd284'
            '3a079d065c560f9b625432e490131d5f6021ba73bd90ba94b161c8296073651a6ba68e83872e340cb7b996a4b1b68bc42bb542006d54e030afe17ed46489d1fe')

prepare() {
  patch -p1 < setup.py.patch

  cd "$pkgname-$pkgver"

  ./prep-release.py
}

build() {
  cd "$pkgname-$pkgver"

  python setup.py build
}

package() {
  cd "$pkgname-$pkgver"

  python setup.py install --root="$pkgdir" --prefix=/usr -O1 --skip-build

  install -Dm 644 -t "$pkgdir/usr/share/doc/$pkgname/" README.md README.rst \
    AUTHORS.txt
  install -Dm 644 -t "$pkgdir/usr/share/licenses/$pkgname" LICENSE.GPL \
    LICENSE.COMMERCIAL
}

