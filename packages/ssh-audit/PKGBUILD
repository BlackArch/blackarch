# This file is part of BlackArch Linux ( https://www.blackarch.org/ ).
# See COPYING for license details.

pkgname=ssh-audit
pkgver=v2.2.0.r0.ge447c42
pkgrel=1
epoch=1
groups=('blackarch' 'blackarch-scanner')
pkgdesc='SSH server auditing (banner, key exchange, encryption, mac, compression, compatbility, etc).'
arch=('any')
url='https://github.com/jtesta/ssh-audit'
license=('MIT')
depends=('python')
makedepends=('git' 'python-setuptools')
source=("git+https://github.com/jtesta/$pkgname.git")
sha512sums=('SKIP')

pkgver() {
  cd $pkgname

  git describe --long --tags | sed 's/\([^-]*-g\)/r\1/;s/-/./g'
}

build() {
  cd "$pkgname/pypi"

  # Symbolic link workaround, https://github.com/jtesta/ssh-audit/issues/46
  cp ../ssh-audit.py sshaudit/sshaudit.py
  cp ../README.md sshaudit/README.md

  python setup.py build
}

package() {
  cd "$pkgname/pypi"

  python setup.py install --root="$pkgdir" --prefix=/usr -O1 --skip-build
}

