# This file is part of BlackArch Linux ( http://blackarch.org ).
# See COPYING for license details.

pkgname='ssh-audit'
pkgver=165.22b671e
pkgrel=1
groups=('blackarch' 'blackarch-scanner')
pkgdesc='SSH server auditing (banner, key exchange, encryption, mac, compression, compatbility, etc).'
arch=('any')
url='https://github.com/arthepsy/ssh-audit'
license=('MIT')
depends=('python2')
makedepends=('git')
source=('git+https://github.com/arthepsy/ssh-audit.git')
sha1sums=('SKIP')

pkgver() {
  cd "$srcdir/ssh-audit"

  echo $(git rev-list --count HEAD).$(git rev-parse --short HEAD)
}

package() {
  cd "$srcdir/ssh-audit"

  mkdir -p "$pkgdir/usr/bin"
  mkdir -p "$pkgdir/usr/share/ssh-audit"

  install -Dm644 -t "$pkgdir/usr/share/doc/ssh-audit/" README.md
  install -Dm644 LICENSE "$pkgdir/usr/share/licenses/ssh-audit/LICENSE"

  rm README.md LICENSE

  cp -a * "$pkgdir/usr/share/ssh-audit"

  cat > "$pkgdir/usr/bin/ssh-audit" << EOF
#!/bin/sh
cd /usr/share/ssh-audit
exec python2 ssh-audit.py "\${@}"
EOF

  chmod a+x "$pkgdir/usr/bin/ssh-audit"
}
