# This file is part of BlackArch Linux ( https://www.blackarch.org/ ).
# See COPYING for license details.

pkgname=python-halo
_pkgname=halo
pkgver=0.0.26
pkgrel=2
pkgdesc='Beautiful terminal spinners in Python.'
arch=('any')
url='https://pypi.org/project/halo/#files'
license=('MIT')
depends=('python' 'python-backports.shutil-get-terminal-size'
         'python-colorama' 'python-cursor' 'python-log_symbols'
         'python-six' 'python-spinners' 'python-termcolor')
makedepends=('python-setuptools')
source=("https://files.pythonhosted.org/packages/a7/23/3961d7ecabcd56412716b22faf3963b6d9aed137a6995dd625869043eff1/$_pkgname-$pkgver.tar.gz"
        'requirements.txt.patch')
sha512sums=('4371362e348fc1aa814dcf099f6d62cae8e777d8f95c644810651dcf6859d1d58da83da98eeb14c22ef40086e16b626d099155246e78ce74e7bf1666fd4a2b3f'
            'ebec28c00f79c4ea449012d55d0491b6b9313b0008b6464f4fe50cb260bd777b2ead26c46437261f5e28f86494e5abb477b5eab658fedeb37c2d388046625b99')

prepare() {
  cd "$_pkgname-$pkgver"

  patch < ../requirements.txt.patch
}

build() {
  cd "$_pkgname-$pkgver"

  python setup.py build
}

package() {
  cd "$_pkgname-$pkgver"

  python setup.py install --prefix=/usr --root="$pkgdir" --optimize=1 \
    --skip-build
}

