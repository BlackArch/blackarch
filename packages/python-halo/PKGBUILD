# This file is part of BlackArch Linux ( https://www.blackarch.org/ ).
# See COPYING for license details.

pkgname=python-halo
_pkgname=halo
pkgver=0.0.28
pkgrel=1
pkgdesc='Beautiful terminal spinners in Python.'
arch=('any')
url='https://pypi.org/project/halo/#files'
license=('MIT')
depends=('python' 'python-backports.shutil-get-terminal-size'
         'python-colorama' 'python-cursor' 'python-log_symbols'
         'python-six' 'python-spinners' 'python-termcolor')
makedepends=('python-setuptools')
source=("https://files.pythonhosted.org/packages/bf/d5/9ba27eb18687fa8eed52e304ad2e8a8839c0e29c87fe8ec5819a15ab0143/$_pkgname-$pkgver.tar.gz"
        'requirements.txt.patch')
sha512sums=('79da2e04f0a5395fdd37560534ce912ddd6f28cbf6edce6a1254cd11c76cdfe1d9ab7bd20a097ce38cb1d2b14c5167b4108ad552786bffd5b71db78bea4a5d45'
            'ebec28c00f79c4ea449012d55d0491b6b9313b0008b6464f4fe50cb260bd777b2ead26c46437261f5e28f86494e5abb477b5eab658fedeb37c2d388046625b99')

prepare() {
  cd "$_pkgname-$pkgver"

  patch < ../requirements.txt.patch
}

build() {
  cd "$_pkgname-$pkgver"

  python setup.py build
}

package() {
  cd "$_pkgname-$pkgver"

  python setup.py install --prefix=/usr --root="$pkgdir" --optimize=1 \
    --skip-build
}

