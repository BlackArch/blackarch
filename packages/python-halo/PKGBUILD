# This file is part of BlackArch Linux ( https://www.blackarch.org/ ).
# See COPYING for license details.

pkgname=python-halo
_pkgname=halo
pkgver=0.0.30
pkgrel=1
pkgdesc='Beautiful terminal spinners in Python.'
arch=('any')
url='https://pypi.org/project/halo/#files'
license=('MIT')
depends=('python' 'python-backports.shutil-get-terminal-size'
         'python-colorama' 'python-cursor' 'python-log_symbols'
         'python-six' 'python-spinners' 'python-termcolor')
makedepends=('python-setuptools')
source=("https://files.pythonhosted.org/packages/ae/47/80742a791ea21ceb9e6e1355e341b3c9223d7710df5874fe6faba9734484/$_pkgname-$pkgver.tar.gz"
        'requirements.txt.patch')
sha512sums=('6b6a2b82903abbb9999550f18619b00ab4fabcbde04874a6cb16eca0ca9e423c176a078dcb176e63f3eb40c61289c0fc6c749bde0948b405edddf4131f48afca'
            '34e967f72e5fae82c3d88f7c9dc5cf25afaa9b9e4f6f479ab36207cd54b4e3e98bc7a91a340b11a556c771a68d214d5d0fce6ac2de51a4e753259e6fc0ffd710')

prepare() {
  cd "$_pkgname-$pkgver"

  patch < ../requirements.txt.patch
}

build() {
  cd "$_pkgname-$pkgver"

  python setup.py build
}

package() {
  cd "$_pkgname-$pkgver"

  python setup.py install --root="$pkgdir" --prefix=/usr -O1 --skip-build
}

