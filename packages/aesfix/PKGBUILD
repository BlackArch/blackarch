pkgname=aesfix
pkgver=1.0.1
pkgrel=2
pkgdesc="A tool to find AES key in RAM"
arch=('i686' 'x86_64')
url="http://citp.princeton.edu/memory/code/"
license=('BSD')
depends=('gcc-libs')
groups=('blackarch' 'blackarch-forensics' 'blackarch-password-attacks')
source=("http://citp.princeton.edu/memory-content/src/aesfix-$pkgver.tar.gz")
md5sums=('f89fb82b69475bfa31f1919aaca5a58b')

build() {
  cd "$srcdir/aesfix"
  make
}

package(){
  cd "$srcdir/aesfix"

  # Base directories
  install -dm755 "$pkgdir/usr/bin"
  install -dm755 "$pkgdir/usr/share/aesfix"
  install -dm755 "$pkgdir/usr/share/licenses/aesfix"

  # Docs
  cp LICENSE "$pkgdir/usr/share/licenses/aesfix"
  cp -R samples "$pkgdir/usr/share/aesfix"

  # Bin
  install -Dm755 aesfix "$pkgdir/usr/bin/aesfix"
}
