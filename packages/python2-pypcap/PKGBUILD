# This file is part of BlackArch Linux ( http://blackarch.org ).
# See COPYING for license details.

pkgname='python2-pypcap'
pkgver=196.6ff26d1
pkgrel=1
pkgdesc='Python extension module for libpcap.'
url='http://code.google.com/p/pypcap/'
license=('custom')
arch=('x86_64' 'armv6h' 'armv7h' 'aarch64')
depends=('python2' 'libpcap')
makedepends=('git' 'python2-setuptools')
source=('git+https://github.com/hellais/pypcap.git')
sha1sums=('SKIP')

pkgver() {
  cd "$srcdir/pypcap"

  echo $(git rev-list --count HEAD).$(git rev-parse --short HEAD)
}

build() {
  cd "$srcdir/pypcap"

  python2 setup.py config
  python2 setup.py build
}

package() {
  cd "$srcdir/pypcap"

  python2 setup.py install "--root=$pkgdir"

  install -Dm644 LICENSE "$pkgdir/usr/share/licenses/pypcap/LICENSE"
}
