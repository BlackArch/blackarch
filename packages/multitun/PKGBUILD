# This file is part of BlackArch Linux ( http://blackarch.org ).
# See COPYING for license details.

pkgname='multitun'
pkgver=43.9804513
pkgrel=2
groups=('blackarch' 'blackarch-tunnel')
pkgdesc='Tunnel arbitrary traffic through an innocuous WebSocket.'
arch=('any')
url='https://github.com/covertcodes/multitun'
license=('custom:unknown')
depends=('python2' 'python2-twisted' 'python2-iniparse' 'python2-autobahn'
         'python-pytun' 'python2-crypto')
makedepends=('git')
source=('git+https://github.com/covertcodes/multitun.git')
sha1sums=('SKIP')

pkgver() {
  cd "$srcdir/multitun"

  echo $(git rev-list --count HEAD).$(git rev-parse --short HEAD)
}

prepare() {
  cd "$srcdir/multitun"

  sed -i 's|multitun.conf|/etc/multitun.conf|g' multitun.py
}

package() {
  cd "$srcdir/multitun"

  install -Dm755 multitun.py "$pkgdir/usr/bin/multitun"
  install -Dm644 multitun.conf "$pkgdir/etc/multitun.conf"
  install -Dm644 README.md "$pkgdir/usr/share/doc/multitun/README.md"
}
