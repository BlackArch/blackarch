# This file is part of BlackArch Linux ( https://www.blackarch.org/ ).
# See COPYING for license details.

pkgbase=python-pyric
pkgname=('python2-pyric' 'python-pyric')
_pkgname=PyRIC
pkgver=0.1.6.3
pkgrel=4
pkgdesc='Python Radio Interface Controller Linux wireless library for the Python Wireless Developer and Pentester.'
arch=('any')
url='https://pypi.org/project/pyric/#files'
license=('GPL3')
makedepends=('python2-setuptools' 'python-setuptools')
source=("https://files.pythonhosted.org/packages/08/64/a99f27d3b4347486c7bfc0aa516016c46dc4c0f380ffccbd742a61af1eda/$_pkgname-$pkgver.tar.gz")
sha512sums=('ed39dbac3d963131d667ae17eb800508aad78662d4dc53b0155e741c388783214cd27b4083dd0009807946e2ec401b5deb6522c351beb9110b7ca523d66e97a1')

prepare() {
  cp -a "$_pkgname-$pkgver"{,-2}
}

build() {
  cd "$_pkgname-$pkgver-2"

  python2 setup.py build

  cd "$srcdir/$_pkgname-$pkgver"

  python setup.py build
}

package_python2-pyric() {
  depends=('python2')

  cd "$_pkgname-$pkgver-2"

  python2 setup.py install --prefix=/usr --root="$pkgdir" --optimize=1 \
    --skip-build
}

package_python-pyric() {
  depends=('python')

  cd "$_pkgname-$pkgver"

  python setup.py install --prefix=/usr --root="$pkgdir" --optimize=1 \
    --skip-build
}

