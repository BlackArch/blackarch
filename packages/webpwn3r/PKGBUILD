# This file is part of BlackArch Linux ( http://blackarch.org ).
# See COPYING for license details.

pkgname='webpwn3r'
pkgver=35.3fb27bb
pkgrel=3
groups=('blackarch' 'blackarch-scanner' 'blackarch-webapp')
pkgdesc='A python based Web Applications Security Scanner.'
url='https://github.com/zigoo0/webpwn3r'
arch=('any')
license=('GPL2')
depends=('python2')
makedepends=('git')
source=('git+https://github.com/zigoo0/webpwn3r.git')
sha1sums=('SKIP')

pkgver() {
  cd "$srcdir/webpwn3r"

  echo $(git rev-list --count HEAD).$(git rev-parse --short HEAD)
}

package() {
  cd "$srcdir/webpwn3r"

  mkdir -p "$pkgdir/usr/bin"
  mkdir -p "$pkgdir/usr/share/webpwn3r"

  install -Dm644 README.md "$pkgdir/usr/share/doc/webpwn3r/README.md"
  install -Dm644 LICENSE "$pkgdir/usr/share/licenses/webpwn3r/LICENSE"

  rm README.md LICENSE

  cp -a * "$pkgdir/usr/share/webpwn3r"

  cat > "$pkgdir/usr/bin/webpwn3r" << EOF
#!/bin/sh
cd /usr/share/webpwn3r
exec python2 scan.py "\$@"
EOF

  chmod +x "$pkgdir/usr/bin/webpwn3r"
}
