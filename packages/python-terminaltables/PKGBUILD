# This file is part of BlackArch Linux ( https://www.blackarch.org/ ).
# See COPYING for license details.

pkgbase=python-terminaltables
pkgname=('python2-terminaltables' 'python-terminaltables')
_pkgname=terminaltables
pkgver=3.1.0
pkgrel=8
pkgdesc='Generate simple tables in terminals from a nested list of strings.'
arch=('any')
url='https://pypi.org/project/terminaltables/#files'
license=('MIT')
makedepends=('python2-setuptools' 'python-setuptools')
source=("https://files.pythonhosted.org/packages/9b/c4/4a21174f32f8a7e1104798c445dacdc1d4df86f2f26722767034e4de4bff/terminaltables-$pkgver.tar.gz")
sha512sums=('f30620f57c4d40b4ee5736d2886e744f7ed34aae702252b2ecf90844d314c4ccce7a7bc9146637504e6996c148567ba16247136f081f086a2b264f2f4920ecd8')

prepare() {
  cp -a "$_pkgname-$pkgver"{,-2}
}

build() {
  cd "$_pkgname-$pkgver-2"

  python2 setup.py build

  cd "$srcdir/$_pkgname-$pkgver"

  python setup.py build
}

package_python2-terminaltables() {
  depends=('python2')

  cd "$_pkgname-$pkgver-2"

  python2 setup.py install --root="$pkgdir" --prefix=/usr -O1 --skip-build

  install -Dm 644 README.rst -t "$pkgdir/usr/share/doc/${pkgname[0]}"
}

package_python-terminaltables() {
  depends=('python')

  cd "$_pkgname-$pkgver"

  python setup.py install --root="$pkgdir" --prefix=/usr -O1 --skip-build

  install -Dm 644 README.rst -t "$pkgdir/usr/share/doc/${pkgname[1]}"
}

