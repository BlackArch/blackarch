# This file is part of BlackArch Linux ( https://www.blackarch.org/ ).
# See COPYING for license details.
#
# Taken from community repo; adjusted to our style.
#
# Old Maintainer: Christian Rebischke <chris.rebischke[at]archlinux[dot]org>
# Old Maintainer: Levente Polyak <anthraxx[at]archlinux[dot]org>

pkgname=python-terminaltables
_pkgname=terminaltables
pkgver=3.1.0
pkgrel=10
pkgdesc='Generate simple tables in terminals from a nested list of strings.'
arch=('any')
url='https://github.com/Robpol86/terminaltables'
license=('MIT')
depends=('python')
makedepends=('python-setuptools')
source=("https://github.com/Robpol86/$_pkgname/archive/v$pkgver.tar.gz"
        'python-3.8.patch')
sha512sums=('dc49458652fff8bc6094d316d84c9b8e9fca1a26e3230c0b668bc03ec8528793f4ef024e8032d4a56fbfabfdfd4a1142870f550f0b373ba6a42dd2e3ead3f501'
            '9a33ef51cbd2854bf9acc247e2d966332229446158e7dae9cad25e03335eb9689d50b0a22234285aa56c707e6e93c12f950299efcfbe7e42ed527216090592f2')

prepare() {
  cd "$_pkgname-$pkgver"

  patch -Np1 < ../python-3.8.patch
}

build() {
  cd "$_pkgname-$pkgver"

  python setup.py build
}

package() {
  cd "$_pkgname-$pkgver"

  python setup.py install --root="$pkgdir" --prefix=/usr -O1 --skip-build

  install -Dm 644 README.rst -t "$pkgdir/usr/share/doc/$pkgname/"
  install -Dm 644 example*.py -t "$pkgdir/usr/share/doc/$pkgname/examples"
  install -Dm 644 LICENSE "$pkgdir/usr/share/licenses/$pkgname/LICENSE"
}

