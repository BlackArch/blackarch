# This file is part of BlackArch Linux ( https://www.blackarch.org/ ).
# See COPYING for license details.

pkgname=missionplanner
pkgver=1.3.61
pkgrel=1
groups=('blackarch' 'blackarch-drone')
pkgdesc='A GroundControl Station for Ardupilot.'
arch=('x86_64' 'armv6h' 'armv7h' 'aarch64')
url='https://code.google.com/p/ardupilot-mega/wiki/Mission'
license=('GPL3')
depends=('mono')
makedepends=('unzip')
options=('!strip')
source=("http://firmware.ardupilot.org/Tools/MissionPlanner/MissionPlanner-$pkgver.zip")
sha512sums=('5642d4046c12ba84a771b80b30dd791ba22ea2d14586fa487e2e1826ecee938c922b61aad53162f30006d778737b4e76269c028335ad83267771d05bdbe1e6ee')

package() {
  install -dm 755 "$pkgdir/usr/bin"
  install -dm 755 "$pkgdir/usr/share/$pkgname"

  cp -a * "$pkgdir/usr/share/$pkgname/"

  rm -rf "$pkgdir/usr/share/$pkgname/MissionPlanner-$pkgver.zip"

  cat > "$pkgdir/usr/bin/$pkgname" << EOF
#!/bin/sh
cd /usr/share/$pkgname
exec mono ArdupilotMegaPlanner10.exe "\$@"
EOF

  chmod +x "$pkgdir/usr/bin/$pkgname"
}

