# This file is part of BlackArch Linux ( https://www.blackarch.org/ ).
# See COPYING for license details.

pkgname=missionplanner
pkgver=1.3.72
pkgrel=1
groups=('blackarch' 'blackarch-drone')
pkgdesc='A GroundControl Station for Ardupilot.'
arch=('x86_64' 'aarch64')
url='https://firmware.ardupilot.org/Tools/MissionPlanner/'
license=('GPL3')
depends=('mono')
makedepends=('unzip')
options=('!strip')
source=("http://firmware.ardupilot.org/Tools/MissionPlanner/MissionPlanner-$pkgver.zip")
sha512sums=('3cce0ef357c6d931470d726444b6f73032ec5ff3f0bd69ccdff2df509d03b1ea9539922e121c15f6eca3c17db36193297437acd13fd9e4bcf093276c43d684b4')

package() {
  install -dm 755 "$pkgdir/usr/bin"
  install -dm 755 "$pkgdir/usr/share/$pkgname"

  cp -a * "$pkgdir/usr/share/$pkgname/"

  rm -rf "$pkgdir/usr/share/$pkgname/MissionPlanner-$pkgver.zip"

  cat > "$pkgdir/usr/bin/$pkgname" << EOF
#!/bin/sh
cd /usr/share/$pkgname
exec mono MissionPlanner.exe "\$@"
#exec mono ArdupilotMegaPlanner10.exe "\$@"
EOF

  chmod +x "$pkgdir/usr/bin/$pkgname"
}

