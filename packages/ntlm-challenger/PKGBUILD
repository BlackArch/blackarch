# This file is part of BlackArch Linux ( https://www.blackarch.org/ ).
# See COPYING for license details.

pkgname=ntlm-challenger
_pkgname=ntlm_challenger
pkgver=3.e7d3ba8
pkgrel=3
pkgdesc='Parse NTLM over HTTP challenge messages.'
arch=('any')
groups=('blackarch' 'blackarch-scanner')
url='https://github.com/b17zr/ntlm_challenger'
license=('MIT')
depends=('python' 'python-argparse' 'python-requests' 'impacket')
makedepends=('git')
source=("$pkgname::git+https://github.com/b17zr/$_pkgname.git")
sha512sums=('SKIP')

pkgver() {
  cd $pkgname

  echo $(git rev-list --count HEAD).$(git rev-parse --short HEAD)
}

package() {
  cd $pkgname

  install -dm 755 "$pkgdir/usr/bin"

  install -Dm 755 -t "$pkgdir/usr/share/$pkgname/" *.py

  install -Dm 644 -t "$pkgdir/usr/share/doc/$pkgname/" README.md
  install -Dm 644 LICENSE "$pkgdir/usr/share/licenses/$pkgname/LICENSE"

  cat > "$pkgdir/usr/bin/$pkgname" << EOF
#!/bin/sh
cd /usr/share/$pkgname
exec python $_pkgname.py "\$@"
EOF

  chmod +x "$pkgdir/usr/bin/$pkgname"
}

