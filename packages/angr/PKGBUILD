# This file is part of BlackArch Linux ( https://www.blackarch.org/ ).
# See COPYING for license details.

pkgname=angr
pkgver=9.2.194
pkgrel=1
epoch=2
groups=('blackarch' 'blackarch-binary' 'blackarch-disassembler'
        'blackarch-reversing')
pkgdesc='The next-generation binary analysis platform from UC Santa Barbaras Seclab.'
arch=('x86_64' 'aarch64')
url='https://pypi.org/project/angr/#files'
license=('BSD-2-Clause')
depends=('python' 'python-capstone' 'python-networkx' 'python-rpyc'
         'python-progressbar' 'python-mulpyplexer' 'python-unicorn'
         'python-cooldict' 'python-archinfo' 'python-pyvex' 'python-ana'
         'python-claripy' 'python-simuvex' 'python-cle' 'libffi'
         'python-colorama' 'python-cachetools' 'python-pyelftools' 'python-cffi'
         'python-psutil' 'python-bintrees' 'python-dpkt' 'python-z3-solver'
         'python-gitpython' 'python-sortedcontainers' 'python-pycparser'
         'python-ailment' 'python-pyformlang' 'python-protobuf'
         'python-itanium-demangler' 'python-cppheaderparser' 'python-sympy'
         'python-nampa' 'python-unique-log-filter' 'python-lmdb' 'python-rich'
         'python-msgspec' 'python-cxxheaderparser' 'python-pydemumble')
makedepends=('python-build' 'python-installer' 'python-setuptools'
             'python-wheel' 'python-setuptools-rust' 'rust')
source=("https://files.pythonhosted.org/packages/source/${pkgname::1}/$pkgname/$pkgname-$pkgver.tar.gz")
sha512sums=('16bfa106d62f79576736be9149eea171aaab8095d97a5eb5f4c4a136cc9987c1e09f80799f3c53c6801825ff2d8fdf29551feb7ca2f25dc9f9e58deccd4f7717')

prepare() {
  cd "$pkgname-$pkgver"

  sed -i 's|unicorn==1.0.2rc4|unicorn|g' setup.py
  sed -i 's|pyvex==9.2.193|pyvex|g' pyproject.toml
}

build() {
  cd "$pkgname-$pkgver"

  python -m build --wheel --no-isolation
}

package() {
  cd "$pkgname-$pkgver"

  python -m installer --destdir="$pkgdir" dist/*.whl
}

