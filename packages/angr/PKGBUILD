# This file is part of BlackArch Linux ( https://www.blackarch.org/ ).
# See COPYING for license details.

pkgname=angr
pkgver=9.2.194
pkgrel=1
epoch=1
groups=('blackarch' 'blackarch-binary' 'blackarch-disassembler'
        'blackarch-reversing')
pkgdesc='The next-generation binary analysis platform from UC Santa Barbaras Seclab.'
arch=('x86_64' 'aarch64')
url='https://pypi.org/project/angr/#files'
license=('BSD-2-Clause')
depends=('python' 'python-capstone' 'python-networkx' 'python-rpyc'
         'python-progressbar' 'python-mulpyplexer' 'python-unicorn'
         'python-cooldict' 'python-archinfo' 'python-pyvex' 'python-ana'
         'python-claripy' 'python-simuvex' 'python-cle' 'libffi'
         'python-colorama' 'python-cachetools' 'python-pyelftools' 'python-cffi'
         'python-psutil' 'python-bintrees' 'python-dpkt' 'python-z3-solver'
         'python-gitpython' 'python-sortedcontainers' 'python-pycparser'
         'python-ailment' 'python-pyformlang' 'python-protobuf'
         'python-itanium-demangler' 'python-cppheaderparser' 'python-sympy'
         'python-nampa' 'python-unique-log-filter' 'python-lmdb' 'python-rich'
         'python-msgspec' 'python-cxxheaderparser' 'python-pydemumble')
makedepends=('python-build' 'python-setuptools-rust' 'rust' 'python-pip')
source=("https://files.pythonhosted.org/packages/source/${pkgname::1}/$pkgname/$pkgname-$pkgver.tar.gz")
sha512sums=('2e8d9ed55c7aa0a0ac38d252d7e3eac55d9529bb2f3599d1eb8fa2228fef75326a10850ef7f47d651d9537c032447dcd57d79dfde326784d42088f04cd7d5b07')

prepare() {
  cd "$pkgname-$pkgver"

  sed -i 's|unicorn==1.0.2rc4|unicorn|g' setup.py
}

build() {
  cd "$pkgname-$pkgver"

  python -m build --wheel --outdir="$startdir/dist"
}

package() {
  cd "$pkgname-$pkgver"

  pip install \
    --verbose \
    --disable-pip-version-check \
    --no-warn-script-location \
    --ignore-installed \
    --no-compile \
    --no-deps \
    --root="$pkgdir" \
    --prefix=/usr \
    --no-index \
    --find-links="file://$startdir/dist" \
    $pkgname
}

