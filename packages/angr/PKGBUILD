# This file is part of BlackArch Linux ( https://www.blackarch.org/ ).
# See COPYING for license details.

pkgname=angr
pkgver=9.0.8021
pkgrel=4
epoch=1
groups=('blackarch' 'blackarch-binary' 'blackarch-disassembler'
        'blackarch-reversing')
pkgdesc='The next-generation binary analysis platform from UC Santa Barbaras Seclab.'
arch=('x86_64' 'aarch64')
url='https://pypi.org/project/angr/#files'
license=('BSD')
depends=('python' 'python-capstone' 'python-networkx' 'python-rpyc'
         'python-progressbar' 'python-mulpyplexer' 'python-unicorn'
         'python-cooldict' 'python-archinfo' 'python-pyvex' 'python-ana'
         'python-claripy' 'python-simuvex' 'python-cle' 'libffi'
         'python-cachetools' 'python-pyelftools' 'python-cffi' 'python-psutil'
         'python-bintrees' 'python-dpkt' 'python-z3' 'python-gitpython'
         'python-sortedcontainers' 'python-pycparser' 'python-ailment'
         'python-protobuf' 'python-itanium-demangler' 'python-cppheaderparser')
makedepends=('git' 'python-setuptools' 'python-pip' 'python-wheel')
source=("https://files.pythonhosted.org/packages/cc/70/152475db26c513fc98ae7153e06d781721d0bd8c573c4cd8c5b5fca31566/$pkgname-$pkgver.tar.gz")
sha512sums=('904b30e8ed8d53445c4df0ffe82f9d3c65e0810e28cea0f598cd14e693034ed164b4d4ad8d2e1583b622479d0b77fbe80bb112fbe8a43f36dbb34a2900f8e2e5')

prepare() {
  cd "$pkgname-$pkgver"

  sed -i 's|unicorn==1.0.2rc4|unicorn|g' setup.py
}

build() {
  cd "$pkgname-$pkgver"

  python setup.py build
}

package() {
  cd "$pkgname-$pkgver"

  python setup.py install --root="$pkgdir" --prefix=/usr -O1 --skip-build
}

