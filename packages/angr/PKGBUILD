# This file is part of BlackArch Linux ( http://blackarch.org ).
# See COPYING for license details.

pkgname='angr'
pkgver=2581.3d44ccc
pkgrel=1
groups=('blackarch' 'blackarch-binary' 'blackarch-disassembler'
        'blackarch-reversing')
pkgdesc="The next-generation binary analysis platform from UC Santa Barbara's Seclab."
arch=('any')
url='https://github.com/angr/angr'
license=('BSD')
depends=('python2' 'python2-capstone' 'python2-networkx' 'python2-rpyc'
         'python2-futures' 'python2-progressbar' 'python2-mulpyplexer'
         'python2-cooldict' 'python2-archinfo' 'python2-pyvex' 'python2-ana'
         'python2-claripy' 'python2-simuvex' 'python2-cle' 'libffi')
makedepends=('git')
source=('git+https://github.com/angr/angr.git')
sha1sums=('SKIP')

pkgver() {
  cd "$srcdir/angr"

  echo $(git rev-list --count HEAD).$(git rev-parse --short HEAD)
}

build() {
  cd "$srcdir/angr"

  python2 setup.py build
}

package() {
  cd "$srcdir/angr"

  mkdir -p "$pkgdir/usr/share/angr"

  python2 setup.py install --root="$pkgdir" --optimize=1

  install -Dm644 -t "$pkgdir/usr/share/doc/angr/" README.md
  install -Dm644 LICENSE "$pkgdir/usr/share/licenses/angr/LICENSE"
}
