# This file is part of BlackArch Linux ( https://www.blackarch.org/ ).
# See COPYING for license details.

pkgname=angr
pkgver=8.19.4.5
pkgrel=1
epoch=1
groups=('blackarch' 'blackarch-binary' 'blackarch-disassembler'
        'blackarch-reversing')
pkgdesc='The next-generation binary analysis platform from UC Santa Barbaras Seclab.'
arch=('x86_64' 'armv6h' 'armv7h' 'aarch64')
url='https://pypi.org/project/angr/#files'
license=('BSD')
depends=('python' 'python-capstone' 'python-networkx' 'python-rpyc'
         'python-progressbar' 'python-mulpyplexer' 'python-unicorn'
         'python-cooldict' 'python-archinfo' 'python-pyvex' 'python-ana'
         'python-claripy' 'python-simuvex' 'python-cle' 'libffi'
         'python-cachetools' 'python-pyelftools' 'python-cffi'
         'python-bintrees' 'python-dpkt' 'python-z3' 'python-gitpython'
         'python-sortedcontainers' 'python-pycparser' 'python-ailment')
makedepends=('git' 'python-setuptools')
source=("https://files.pythonhosted.org/packages/ac/32/37868d159227904165acf6dea806d94cff6ce40bb5733da060958e3f1870/$pkgname-$pkgver.tar.gz")
sha512sums=('808bad423f6221eed3c3cbcc995896c2b2c0df1c2c3364909fe4057ad990555ead8cdcfe70d4f607d6fb7c8a13736606d19959b32ef251a08dc510a4f0dd3df8')

build() {
  cd "$pkgname-$pkgver"

  python setup.py build
}

package() {
  cd "$pkgname-$pkgver"

  python setup.py install --prefix=/usr --root="$pkgdir" --optimize=1 \
    --skip-build
}

