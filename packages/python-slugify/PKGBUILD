# This file is part of BlackArch Linux ( https://www.blackarch.org/ ).
# See COPYING for license details.

pkgbase=python-slugify
pkgname=('python2-slugify' 'python-slugify')
_pkgname=python-slugify
pkgver=1.2.6
pkgrel=2
pkgdesc='A Python slugify application that handles unicode.'
arch=('any')
url='https://pypi.org/project/slugify/#files'
license=('BSD')
makedepends=('python2-setuptools' 'python-setuptools')
source=("https://files.pythonhosted.org/packages/00/ad/c778a6df614b6217c30fe80045b365bfa08b5dd3cb02e8b37a6d25126781/$_pkgname-$pkgver.tar.gz")
sha512sums=('632fdd2162ebbec0bc6ae0cf0df4a7316ecb4974603370084ea55b4f0af9e04aff8c96888e562fd8db31e645c999a3a178ee2ae534f55ea3fa364080ded82f66')

prepare() {
  cp -a "$_pkgname-$pkgver" "$_pkgname-$pkgver-2"
}

build() {
  cd "$_pkgname-$pkgver-2"

  python2 setup.py build

  cd "$srcdir/$_pkgname-$pkgver"

  python setup.py build
}

package_python2-slugify() {
  depends=('python2' 'python2-unidecode')
  optdepends=('python2-text-unidecode: text-unidecode support')

  cd "$_pkgname-$pkgver-2"

  python2 setup.py install --prefix=/usr --root="$pkgdir" --optimize=1 \
    --skip-build

  mv "$pkgdir/usr/bin/slugify" "$pkgdir/usr/bin/slugify2"
}

package_python-slugify() {
  depends=('python' 'python-unidecode')
  optdepends=('python-text-unidecode: text-unidecode support')

  cd "$_pkgname-$pkgver"

  python setup.py install --prefix=/usr --root="$pkgdir" --optimize=1 \
    --skip-build
}

