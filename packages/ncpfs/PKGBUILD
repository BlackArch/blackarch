# This file is part of BlackArch Linux ( http://blackarch.org ).
# See COPYING for license details.

pkgname='ncpfs'
pkgver='2.2.6'
pkgrel=11
pkgdesc='Allows you to mount volumes of NetWare servers under Linux.'
url='http://www.novell.com/'
groups=('blackarch' 'blackarch-networking')
arch=('x86_64' 'armv6h' 'armv7h' 'aarch64')
license=('GPL')
depends=('linux>=3.0.0' 'pam')
_url="http://ftp.de.debian.org/debian/pool/main/n/ncpfs"
_with_php=0
source=("${_url}/${pkgname}_${pkgver}.orig.tar.gz"
        '01-linux3.0_uname.patch'
        '02-legacy.patch'
        '03-multiple_security_issues.patch')
sha1sums=('00d83771ef14f2aeb039430a44201e33cd80a894'
          '58b4ceb211a300b70d3215866cbf9f16b9c806de'
          'ed185199bf91ab547208bd2397faae0a91d3c1fd'
          'ddd98dc1ddaebb8063291358409304f81703eef4')
if [ $_with_php -eq 1 ]; then
  makedepends=('php')
  optdepends=('php: PHP NCP authentication support')
fi

build() {
  cd ncpfs-$pkgver

  patch -p1 -i "$srcdir"/01-linux3.0_uname.patch
  patch -p1 -i "$srcdir"/02-legacy.patch
  patch -p1 -i "$srcdir"/03-multiple_security_issues.patch

  # fix glibc 2.16.0-2
  export ncp_cv_pam_directory='/usr/lib/security'

  if [[ "$CARCH" == "i686" || "$CARCH" == "x86_64" ]];
  then
    if [ $_with_php -eq 1 ]; then
      ./configure --prefix=/usr --mandir=/usr/share
      sed 's/function_entry/zend_function_entry/' -i contrib/php/php_auth_nds.c
    else
      ./configure --prefix=/usr --mandir=/usr/share --disable-php
    fi
  else
    if [ $_with_php -eq 1 ]; then
      ./configure --prefix=/usr --mandir=/usr/share --build=arm
      sed 's/function_entry/zend_function_entry/' -i contrib/php/php_auth_nds.c
    else
      ./configure --prefix=/usr --mandir=/usr/share --disable-php --build=arm
    fi
  fi

  make
}

package() {
  cd ncpfs-$pkgver

  make DESTDIR="$pkgdir" install install-dev

  # fix libncp symlink
  rm "$pkgdir"/usr/lib/libncp.so
  ln -s libncp.so.2.3.0 "$pkgdir"/usr/lib/libncp.so
  ln -s libncp.so.2.3.0 "$pkgdir"/usr/lib/libncp.so.2.3

  rm -rf "$pkgdir/sbin"
  mv "$pkgdir/usr/sbin/"* "$pkgdir/usr/bin"
  rmdir "$pkgdir/usr/sbin"
}
