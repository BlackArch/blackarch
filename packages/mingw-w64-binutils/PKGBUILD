# This file is part of BlackArch Linux ( https://www.blackarch.org/ ).
# See COPYING for license details.

pkgname=mingw-w64-binutils
pkgver=2.33.1
pkgrel=1
pkgdesc='Cross binutils for the MinGW-w64 cross-compiler.'
arch=('x86_64')
url='http://www.gnu.org/software/binutils'
license=('GPL')
depends=('zlib')
options=('!libtool' '!emptydirs')
source=("https://ftp.gnu.org/gnu/binutils/binutils-$pkgver.tar.gz"{,.sig})
sha512sums=('67f97a480f1e25313bbe826d4bcfaf0cd66a39e3bc724ba137bf81c146c119827b5d07b98789f5cba0bbe296a7c065b68f377e82f5eb2063350594c34079cbc8'
            'SKIP')
validpgpkeys=('3A24BC1E8FB409FA9F14371813FCEF89DD9E3C4F')
_targets='i686-w64-mingw32 x86_64-w64-mingw32'

prepare() {
  cd "binutils-$pkgver"

  #do not install libiberty
  sed -i 's/install_to_$(INSTALL_DEST) //' libiberty/Makefile.in

  # hack! - libiberty configure tests for header files using "$CPP $CPPFLAGS"
  sed -i "/ac_cpp=/s/\$CPPFLAGS/\$CPPFLAGS -O2/" libiberty/configure
}

build() {
  for t in $_targets ; do
    mkdir -p "$srcdir/binutils-$t" && cd "$srcdir/binutils-$t"

    "$srcdir/binutils-$pkgver/"configure --prefix=/usr --target=$t \
      --infodir=/usr/share/info/$t --enable-lto --enable-plugins \
      --disable-multilib --disable-nls --disable-werror

     make
  done
}

package() {
  for t in $_targets ; do
    cd "$srcdir/binutils-$t"

    make DESTDIR="$pkgdir" install
  done
}

