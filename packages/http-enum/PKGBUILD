# This file is part of BlackArch Linux ( http://blackarch.org ).
# See COPYING for license details.

pkgname=http-enum
pkgver=0.4
pkgrel=2
pkgdesc='A tool to enumerate the enabled HTTP methods supported on a webserver.'
arch=('any')
url='https://www.thexero.co.uk/tools/http-enum/'
license=('custom:unknown')
depends=('python2')
groups=('blackarch' 'blackarch-scanner')
source=("https://github.com/nullsecuritynet/tools/raw/master/scanner/http-enum/release/http-enum-${pkgver}.tar.gz")
sha512sums=('968c51c047163dccd8e51da4169dc4ef33b8d2f32775aef6e60e44791030c1abbc987a2d3486c713539d38109596b546e39f18948d73b0d80708d823a526a723')

prepare() {
  cd "$srcdir/http-enum-$pkgver"

  sed -i '1s|python$|env python2|' http-enum
}

package() {
  cd "$srcdir/http-enum-$pkgver"

  install -dm 755 "$pkgdir/usr/bin"
  install -dm 755 "$pkgdir/usr/share/http-enum"

  cp -a * "$pkgdir/usr/share/http-enum"

  cat > "$pkgdir/usr/bin/http-enum" << EOF
#!/bin/sh
cd /usr/share/http-enum
exec python2 http-enum "\${@}"
EOF

  chmod a+x "$pkgdir/usr/bin/http-enum"
}
