# This file is part of BlackArch Linux ( https://www.blackarch.org/ ).
# See COPYING for license details.

pkgname=hash-buster
pkgver=44.41ef108
pkgrel=1
pkgdesc='A python script which scraps online hash crackers to find cleartext of a hash.'
groups=('blackarch' 'blackarch-crypto')
arch=('any')
url='https://github.com/UltimateHackers/Hash-Buster/'
license=('MIT')
depends=('python2')
makedepends=('git')
source=("$pkgname::git+https://github.com/UltimateHackers/Hash-Buster.git")
sha512sums=('SKIP')

pkgver() {
  cd $pkgname

  echo $(git rev-list --count HEAD).$(git rev-parse --short HEAD)
}

prepare() {
  cd $pkgname

  sed -i 's|env python|env python2|' hash.py
}

package() {
  cd $pkgname

  install -Dm 755 hash.py "$pkgdir/usr/bin/$pkgname"
  install -Dm 644 -t "$pkgdir/usr/share/doc/$pkgname/" README.md
}

