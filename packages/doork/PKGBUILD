# This file is part of BlackArch Linux ( http://blackarch.org ).
# See COPYING for license details.

pkgname='doork'
pkgver=6.90c7260
pkgrel=2
groups=('blackarch' 'blackarch-webapp' 'blackarch-recon')
pkgdesc='Passive Vulnerability Auditor.'
arch=('any')
url='https://github.com/AeonDave/doork'
license=('MIT')
depends=('python2' 'python2-requests' 'python2-beautifulsoup4'
         'python2-xgoogle' 'python2-simplejson')
makedepends=('git')
source=('git+https://github.com/AeonDave/doork.git')
sha1sums=('SKIP')

pkgver() {
  cd "$srcdir/doork"

  echo $(git rev-list --count HEAD).$(git rev-parse --short HEAD)
}

package() {
  cd "$srcdir/doork"

  mkdir -p "$pkgdir/usr/bin"
  mkdir -p "$pkgdir/usr/share/doork"

  install -Dm644 -t "$pkgdir/usr/share/doc/doork/" README.md
  install -Dm644 LICENSE "$pkgdir/usr/share/licenses/doork/LICENSE"

  rm README.md LICENSE

  cp -a * "$pkgdir/usr/share/doork"

  cat > "$pkgdir/usr/bin/doork" << EOF
#!/bin/sh
cd /usr/share/doork
exec python2 doork.py "\$@"
EOF

  chmod a+x "$pkgdir/usr/bin/doork"
}
