# This file is part of BlackArch Linux ( https://www.blackarch.org/ ).
# See COPYING for license details.

pkgname=doork
pkgver=6.90c7260
pkgrel=3
groups=('blackarch' 'blackarch-webapp' 'blackarch-recon')
pkgdesc='Passive Vulnerability Auditor.'
arch=('any')
url='https://github.com/AeonDave/doork'
license=('MIT')
depends=('python2' 'python2-requests' 'python2-beautifulsoup4'
         'python2-xgoogle' 'python2-simplejson' 'python2-django')
makedepends=('git')
source=("git+https://github.com/AeonDave/$pkgname.git")
sha512sums=('SKIP')

pkgver() {
  cd $pkgname

  echo $(git rev-list --count HEAD).$(git rev-parse --short HEAD)
}

package() {
  cd $pkgname

  install -dm 755 "$pkgdir/usr/bin"
  install -dm 755 "$pkgdir/usr/share/$pkgname"

  install -Dm 644 -t "$pkgdir/usr/share/doc/$pkgname/" README.md
  install -Dm 644 LICENSE "$pkgdir/usr/share/licenses/$pkgname/LICENSE"

  rm README.md LICENSE

  cp -a * "$pkgdir/usr/share/$pkgname/"

  cat > "$pkgdir/usr/bin/$pkgname" << EOF
#!/bin/sh
cd /usr/share/$pkgname
exec python2 $pkgname.py "\$@"
EOF

  chmod +x "$pkgdir/usr/bin/$pkgname"
}

