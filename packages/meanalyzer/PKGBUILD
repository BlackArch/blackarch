# This file is part of BlackArch Linux ( https://www.blackarch.org/ ).
# See COPYING for license details.

pkgname=meanalyzer
pkgver=1.174.1
_rev=218
pkgrel=2
pkgdesc='Intel Engine Firmware Analysis Tool.'
arch=('x86_64')
groups=('blackarch' 'blackarch-firmware')
url='https://github.com/platomav/MEAnalyzer'
license=('GPL3')
depends=('python' 'python-colorama' 'python-crccheck' 'python-ptable-boxchar'
         'python-prettytable')
source=("https://github.com/platomav/MEAnalyzer/archive/v$pkgver-r$_rev.tar.gz")
sha512sums=('8d0f0b1dbf2b088f8f5e9c3b232177454d7c371b8998ad7e6d6905a533cf53db707b04cff5b47a444dd0a9d744e6188222629e87cb65e0a1d1cebdb736d82baa')
noextract=("ME_Analyzer_v${pkgver}_r$_rev.rar")

package() {
  cd "MEAnalyzer-$pkgver-r$_rev"

  install -dm 755 "$pkgdir/usr/bin"
  install -dm 755 "$pkgdir/usr/share/$pkgname"

  install -Dm 644 -t "$pkgdir/usr/share/doc/$pkgname/" Changelog.txt README.md
  install -Dm 644 LICENSE "$pkgdir/usr/share/licenses/$pkgname/LICENSE"

  rm -rf Changelog.txt README.md LICENSE

  cp -a --no-preserve=owner * "$pkgdir/usr/share/$pkgname/"

  cat > "$pkgdir/usr/bin/$pkgname" << EOF
#!/bin/sh
cd /usr/share/$pkgname
exec python MEA.py "\$@"
EOF
   chmod +x "$pkgdir/usr/bin/$pkgname"
}

