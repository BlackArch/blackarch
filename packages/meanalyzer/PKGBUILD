# This file is part of BlackArch Linux ( https://www.blackarch.org/ ).
# See COPYING for license details.

pkgname=meanalyzer
_pkgname=MEAnalyzer
pkgver=1.186.2
_rev=229
pkgrel=1
pkgdesc='Intel Engine Firmware Analysis Tool.'
arch=('x86_64')
groups=('blackarch' 'blackarch-firmware')
url='https://github.com/platomav/MEAnalyzer'
license=('GPL3')
depends=('python' 'python-colorama' 'python-crccheck' 'python-ptable-boxchar'
         'python-prettytable')
source=("https://github.com/platomav/$_pkgname/archive/v$pkgver-r$_rev.tar.gz")
sha512sums=('7520760d9b6ca2a99a278d6e2ecd11eb626b92974c1bcce15136d4ae25f758daea5b996b55a87441d2cc3d15e2f0a5a5769c951ad494255e687f92928d4277fb')
noextract=("ME_Analyzer_v${pkgver}_r$_rev.rar")

package() {
  cd "$_pkgname-$pkgver-r$_rev"

  install -dm 755 "$pkgdir/usr/bin"
  install -dm 755 "$pkgdir/usr/share/$pkgname"

  install -Dm 644 -t "$pkgdir/usr/share/doc/$pkgname/" Changelog.txt README.md
  install -Dm 644 LICENSE "$pkgdir/usr/share/licenses/$pkgname/LICENSE"

  rm -rf Changelog.txt README.md LICENSE

  cp -a --no-preserve=owner * "$pkgdir/usr/share/$pkgname/"

  cat > "$pkgdir/usr/bin/$pkgname" << EOF
#!/bin/sh
cd /usr/share/$pkgname
exec python MEA.py "\$@"
EOF
   chmod +x "$pkgdir/usr/bin/$pkgname"
}

