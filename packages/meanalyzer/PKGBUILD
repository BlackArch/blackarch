# This file is part of BlackArch Linux ( https://www.blackarch.org/ ).
# See COPYING for license details.

pkgname=meanalyzer
_pkgname=MEAnalyzer
pkgver=1.210.2
_rev=249
pkgrel=2
pkgdesc='Intel Engine Firmware Analysis Tool.'
arch=('x86_64')
groups=('blackarch' 'blackarch-firmware')
url='https://github.com/platomav/MEAnalyzer'
license=('GPL3')
depends=('python' 'python-colorama' 'python-crccheck' 'python-ptable-boxchar'
         'python-prettytable')
source=("https://github.com/platomav/$_pkgname/archive/v$pkgver-r$_rev.tar.gz")
sha512sums=('59b372d5da4717314159562d999ada7ca5ede17e179d3df9ee57dd5f7a306e6c0995bb02a742d8b2d068fb63b525773f8f9a81f54f4c7b7340f9e06cd289fb5b')
noextract=("ME_Analyzer_v${pkgver}_r$_rev.rar")

package() {
  cd "$_pkgname-$pkgver-r$_rev"

  install -dm 755 "$pkgdir/usr/bin"
  install -dm 755 "$pkgdir/usr/share/$pkgname"

  install -Dm 644 -t "$pkgdir/usr/share/doc/$pkgname/" Changelog.txt README.md
  install -Dm 644 LICENSE "$pkgdir/usr/share/licenses/$pkgname/LICENSE"

  rm -rf Changelog.txt README.md LICENSE

  cp -a --no-preserve=owner * "$pkgdir/usr/share/$pkgname/"

  cat > "$pkgdir/usr/bin/$pkgname" << EOF
#!/bin/sh
cd /usr/share/$pkgname
exec python MEA.py "\$@"
EOF
   chmod +x "$pkgdir/usr/bin/$pkgname"
}

