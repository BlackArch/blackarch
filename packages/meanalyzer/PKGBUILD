# This file is part of BlackArch Linux ( http://blackarch.org ).
# See COPYING for license details.

pkgname=meanalyzer
pkgver=1.70.2
pkgrel=1
pkgdesc='Intel Engine Firmware Analysis Tool.'
arch=('x86_64')
groups=('blackarch' 'blackarch-firmware')
url='https://github.com/platomav/MEAnalyzer'
license=('GPL3')
depends=('python' 'python-colorama' 'python-crccheck' 'python-ptable-boxchar')
source=("https://github.com/platomav/MEAnalyzer/archive/v$pkgver-r142.zip")
sha512sums=('1f7e32df22dc4ec820bac102c7b5e6f24f920d04dd4b5148176da059cc11f4f93fce5ecd18f15855a2ee4263925ed2a39243c8ab3c457ddb2e7cc880126230ae')
_dir_name_=MEAnalyzer

package() {

  cd "$_dir_name_-$pkgver-r142"

  install -dm 755 "$pkgdir/usr/bin"
  install -dm 755 "$pkgdir/usr/share/$pkgname"

  install -Dm 644 -t "$pkgdir/usr/share/doc/$pkgname/" Changelog\ DB.txt Changelog\ MEA.txt README.md
  install -Dm 644 LICENSE "$pkgdir/usr/share/licenses/$pkgname/LICENSE" 

  rm -rf Changelog* README.md LICENSE

  cp -a --no-preserve=owner * "$pkgdir/usr/share/$pkgname/"
  cat > "$pkgdir/usr/bin/$pkgname" << EOF
#!/bin/sh
cd /usr/share/meanalyzer/
exec python MEA.py "\${@}"
EOF
   chmod a+x "$pkgdir/usr/bin/$pkgname"
}

