# This file is part of BlackArch Linux ( https://www.blackarch.org/ ).
# See COPYING for license details.

pkgname=python2-twisted
_pkgname=Twisted
pkgver=19.10.0
pkgrel=1
pkgdesc='An asynchronous networking framework written in Python.'
url='https://pypi.org/project/Twisted/#files'
arch=('x86_64' 'aarch64')
license=('MIT')
depends=('python2' 'python2-automat' 'python2-hyperlink' 'python2-attrs'
         'python2-pyhamcrest' 'python2-incremental' 'python2-zope-interface')
makedepends=('python2-setuptools')
source=("https://files.pythonhosted.org/packages/0b/95/5fff90cd4093c79759d736e5f7c921c8eb7e5057a70d753cdb4e8e5895d7/$_pkgname-$pkgver.tar.bz2")
sha512sums=('de8d7fd0b2081cebeff68b060c8469377011648bc563a94a993d3530fb007ed42c3a54925c9a10c465ee7a3065cc9108ace12d10d358223fab13494becb9ac4b')

build() {
  cd "$_pkgname-$pkgver"

  python2 setup.py build
}

package() {
  cd "$_pkgname-$pkgver"

  python2 setup.py install --root="$pkgdir" --prefix=/usr -O1 --skip-build

  for i in "$pkgdir/usr/bin/"*; do
    mv $i "${i}2"
  done
}

