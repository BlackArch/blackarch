# This file is part of BlackArch Linux ( https://www.blackarch.org/ ).
# See COPYING for license details.

pkgbase=python-pypssl
pkgname=('python2-pypssl' 'python-pypssl')
_pkgname=pypssl
pkgver=2.2
pkgrel=1
pkgdesc='Python API for PSSL.'
arch=('any')
url='https://pypi.org/project/pypssl/#files'
license=('BSD')
makedepends=('python2-setuptools' 'python-setuptools')
source=("https://files.pythonhosted.org/packages/d5/de/03fd3ba152c7b53a36b42273d2db491d9573030f7abfa11e29e52aaaf785/$_pkgname-$pkgver.tar.gz")
sha512sums=('ef54c476e9e95472a3f98ab449bb964658db4a1cb9ad24ca97a44ec44f185bc0b586935af94c61f16789400cb895f2ac389793c13ed111b9cc518296c7e22058')

prepare() {
  cp -a "$_pkgname-$pkgver"{,-2}
}

build() {
  cd "$_pkgname-$pkgver-2"

  python2 setup.py build

  cd "$srcdir/$_pkgname-$pkgver"

  python setup.py build
}

package_python2-pypssl() {
  depends=('python2' 'python2-dateutil' 'python2-requests')

  cd "$_pkgname-$pkgver-2"

  python2 setup.py install --prefix=/usr --root="$pkgdir" --optimize=1 \
    --skip-build

  mv "$pkgdir/usr/bin/$_pkgname" "$pkgdir/usr/bin/${_pkgname}2"
}

package_python-pypssl() {
  depends=('python' 'python2-dateutil' 'python2-requests')

  cd "$_pkgname-$pkgver"

  python setup.py install --prefix=/usr --root="$pkgdir" --optimize=1 \
    --skip-build
}

