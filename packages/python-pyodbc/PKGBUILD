# This file is part of BlackArch Linux ( https://www.blackarch.org/ ).
# See COPYING for license details.

pkgname=python-pyodbc
_pkgname=pyodbc
pkgver=4.0.32
pkgrel=1
pkgdesc='DB API Module for ODBC'
arch=('x86_64' 'aarch64')
url='https://pypi.org/project/pyodbc/'
license=('MIT')
depends=('python' 'unixodbc')
source=("https://files.pythonhosted.org/packages/d1/21/755f5fa373d01d1803d992c7611799dc4e9fcbb5db7d0852990d6ab3c9ad/$_pkgname-$pkgver.tar.gz")
sha512sums=('a282f9b83e6cbf874db8135f1088dea86544dda49a16e86f988302f1675ff16d33473fbef092c6c0acdae7de1df6c4528cb1ea5ce3db0e6df49a7c457fcb34f8')

prepare() {
  echo "Version: $pkgver.0" > "$_pkgname-$pkgver/PKG-INFO"
}

build() {
  cd "$_pkgname-$pkgver"

  python setup.py build
}

package() {
  cd "$_pkgname-$pkgver"

  python setup.py install --root="$pkgdir" --prefix=/usr -O1 --skip-build
  install -Dm 644 LICENSE.txt "$pkgdir/usr/share/licenses/$pkgname/LICENSE"

  rm "$pkgdir/usr/pyodbc.pyi"
}

