# This file is part of BlackArch Linux ( https://www.blackarch.org/ ).
# See COPYING for license details.

pkgname=python-pyodbc
_pkgname=pyodbc
pkgver=4.0.34
pkgrel=1
pkgdesc='DB API Module for ODBC'
arch=('x86_64' 'aarch64')
url='https://pypi.org/project/pyodbc/'
license=('MIT')
depends=('python' 'unixodbc')
source=("https://files.pythonhosted.org/packages/1c/c3/b40c1e33fe5285158bf15e6eedd7bdbb929a250d4f4ed435bb1a62a8f1a3/$_pkgname-$pkgver.tar.gz")
sha512sums=('b9b4e23c0536401b5b893dd7244b29955a111ff74762f8105a7477e780b8bb3a857199a198f24bf6bdaa36ae50235dd7c73deec14d3dda552d2cb0867fc9a0e2')

prepare() {
  echo "Version: $pkgver.0" > "$_pkgname-$pkgver/PKG-INFO"
}

build() {
  cd "$_pkgname-$pkgver"

  python setup.py build
}

package() {
  cd "$_pkgname-$pkgver"

  python setup.py install --root="$pkgdir" --prefix=/usr -O1 --skip-build
  install -Dm 644 LICENSE.txt "$pkgdir/usr/share/licenses/$pkgname/LICENSE"

  rm "$pkgdir/usr/pyodbc.pyi"
}

