# This file is part of BlackArch Linux ( https://www.blackarch.org/ ).
# See COPYING for license details.

pkgname=python-pyodbc
_pkgname=pyodbc
pkgver=4.0.39
pkgrel=1
pkgdesc='DB API Module for ODBC'
arch=('x86_64' 'aarch64')
url='https://pypi.org/project/pyodbc/'
license=('MIT')
depends=('python' 'unixodbc')
source=("https://files.pythonhosted.org/packages/source/${_pkgname::1}/$_pkgname/$_pkgname-$pkgver.tar.gz")
sha512sums=('a260dbad5a95c09cb08662f9d284acbcbd8cdb4748fdff04687d872736819bb741e44c365372a260c9b510ea2a304564d87b6e37cb6bf5f4c053dea857ffb03d')

prepare() {
  echo "Version: $pkgver.0" > "$_pkgname-$pkgver/PKG-INFO"
}

build() {
  cd "$_pkgname-$pkgver"

  python setup.py build
}

package() {
  cd "$_pkgname-$pkgver"

  python setup.py install --root="$pkgdir" --prefix=/usr -O1 --skip-build
  install -Dm 644 LICENSE.txt "$pkgdir/usr/share/licenses/$pkgname/LICENSE"

  rm "$pkgdir/usr/pyodbc.pyi"
}

