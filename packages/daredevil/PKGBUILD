# This file is part of BlackArch Linux ( https://www.blackarch.org/ ).
# See COPYING for license details.

pkgname=daredevil
pkgver=37.897f602
pkgrel=1
pkgdesc='A tool to perform (higher-order) correlation power analysis attacks (CPA).'
groups=('blackarch' 'blackarch-crypto')
arch=('x86_64' 'armv6h' 'armv7h' 'aarch64')
url='https://github.com/SideChannelMarvels/Daredevil'
license=('GPL3')
depends=('openmp')
makedepends=('git' 'clang')
source=('git+https://github.com/SideChannelMarvels/Daredevil.git')
sha512sums=('SKIP')

pkgver() {
  cd Daredevil

  echo $(git rev-list --count HEAD).$(git rev-parse --short HEAD)
}

build() {
  cd Daredevil

  make
}

package() {
  cd Daredevil

  install -dm 755 "$pkgdir/usr/bin"
  install -dm 755 "$pkgdir/usr/share/$pkgname"

  cp -a LUT CONFIG "$pkgdir/usr/share/$pkgname/"

  install -Dm 755 daredevil "$pkgdir/usr/share/$pkgname/$pkgname"
  install -Dm 644 -t "$pkgdir/usr/share/doc/$pkgname/" README.md
  install -Dm 644 LICENSE "$pkgdir/usr/share/licenses/$pkgname/LICENSE"

  cat > "$pkgdir/usr/bin/$pkgname" << EOF
#!/bin/sh
cd /usr/share/$pkgname
exec ./daredevil "\$@"
EOF

  chmod a+x "$pkgdir/usr/bin/$pkgname"
}

