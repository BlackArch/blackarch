# This file is part of BlackArch Linux ( https://www.blackarch.org/ ).
# See COPYING for license details.

pkgname=elettra
pkgver=1.0
pkgrel=5
pkgdesc='A plausible deniable cryptography tool that supports a dynamic number of files and requires a password for each file.'
arch=('x86_64' 'aarch64')
groups=('blackarch' 'blackarch-misc')
license=('custom')
url='https://www.winstonsmith.info/julia/elettra/'
depends=('glibc' 'libmcrypt' 'mhash' 'zlib')
makedepends=('cmake')
source=("https://www.winstonsmith.info/julia/$pkgname/$pkgname-src.tar.gz"
        "fix-build.patch")
sha512sums=('341a83436d9c163391eabbc06600b29e8ee837bb805f2f9ea922ffa8d8dcb9ec53e50ae475e13e2810ac69419b86072cd7e6c44d13c80e43ee67071e6864d5b3'
            '1202f1f2dd9aa24eaf28c1daf96ef8607589d2efe9e9aafdff81dfe4fb297bc52ffdc9ea33c6e52fa5bae5bed3cc613542b432fb0524a185c5002535f6001e8f')

prepare() {
  cd "$pkgname/src"

  patch -p1 < ../../fix-build.patch
}

build() {
  cd "$pkgname/src"

  cmake -B build
  cmake --build build
}

package() {
  cd $pkgname

  install -Dm 755 "src/build/$pkgname" "$pkgdir/usr/bin/$pkgname"
  install -Dm 644 -t "$pkgdir/usr/share/doc/$pkgname/README" README
}

