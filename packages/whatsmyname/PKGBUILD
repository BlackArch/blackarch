# This file is part of BlackArch Linux ( https://www.blackarch.org/ ).
# See COPYING for license details.

pkgname=whatsmyname
pkgver=265.1f90346
pkgrel=1
pkgdesc='Tool to perform user and username enumeration on various websites.'
groups=('blackarch' 'blackarch-webapp' 'blackarch-recon')
arch=('any')
url='https://github.com/WebBreacher/WhatsMyName'
license=('MIT')
depends=('python2' 'python2-requests')
makedepends=('git')
source=('git+https://github.com/WebBreacher/WhatsMyName.git')
sha512sums=('SKIP')

pkgver() {
  cd WhatsMyName

  echo $(git rev-list --count HEAD).$(git rev-parse --short HEAD)
}

package() {
  cd WhatsMyName

  install -dm 755 "$pkgdir/usr/bin"
  install -dm 755 "$pkgdir/usr/share/$pkgname"

  install -Dm 644 -t "$pkgdir/usr/share/doc/$pkgname/" README.md
  install -Dm 644 LICENSE.md "$pkgdir/usr/share/licenses/$pkgname/LICENSE.md"

  rm *.md

  cp -a * "$pkgdir/usr/share/$pkgname/"

  cat > "$pkgdir/usr/bin/$pkgname" << EOF
#!/bin/sh
cd /usr/share/$pkgname
exec python2 web_accounts_list_checker.py "\$@"
EOF

  chmod +x "$pkgdir/usr/bin/$pkgname"
}

