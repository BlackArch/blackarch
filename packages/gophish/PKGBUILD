# This file is part of BlackArch Linux ( https://www.blackarch.org/ ).
# See COPYING for license details.

pkgname=gophish
pkgver=0.6.0
pkgrel=1
groups=('blackarch' 'blackarch-social')
pkgdesc='Open-Source Phishing Framework.'
arch=('x86_64' 'armv6h' 'armv7h' 'aarch64')
url='https://getgophish.com/'
license=('MIT')
options=(!strip)
source=("https://github.com/gophish/gophish/releases/download/v$pkgver/gophish-v$pkgver-linux-64bit.zip")
sha512sums=('b637580fef4b5a3b48530bf759ea3e2cc47e8ee653b676b444eb9bfa198724ff8f411b24ad132a0d0d0c03f52fe9013c7431d7e94113d985ffd3bc990c3a6af5')

package() {
  install -dm 755 "$pkgdir/usr/bin"
  install -dm 755 "$pkgdir/usr/share/$pkgname"

  rm "gophish-v$pkgver-linux-64bit.zip"

  install -Dm 644 -t "$pkgdir/usr/share/doc/$pkgname/" README.md VERSION
  install -Dm 644 LICENSE "$pkgdir/usr/share/licenses/$pkgname/LICENSE"

  cp -a * "$pkgdir/usr/share/$pkgname/"

  chmod -R g-w "$pkgdir/usr/share/$pkgname"
  chmod +x "$pkgdir/usr/share/gophish/$pkgname"

  cat > "$pkgdir/usr/bin/$pkgname" << EOF
#!/bin/sh
cd /usr/share/$pkgname
exec ./gophish "\$@"
EOF

  chmod +x "$pkgdir/usr/bin/$pkgname"
}

