# This file is part of BlackArch Linux ( https://www.blackarch.org/ ).
# See COPYING for license details.

pkgname=ipba2
pkgver=95.c03bd85
pkgrel=5
epoch=1
pkgdesc='IOS Backup Analyzer.'
arch=('any')
groups=('blackarch' 'blackarch-forensic')
url='http://www.ipbackupanalyzer.com/'
license=('GPL-2.0-or-later')
depends=('python2' 'python2-pillow' 'python2-pyside' 'python2-six' 'qt4')
makedepends=('git')
#source=("http://www.ipbackupanalyzer.com/downloads/python-pre-assembled-files/python-pre-assembled-files-$pkgver/ipba2-python-build-$pkgver-zip")
source=("$pkgname::git+https://github.com/PicciMario/iPhone-Backup-Analyzer-2.git")
sha512sums=('SKIP')

pkgver() {
  cd $pkgname

  ( set -o pipefail
    git describe --long --tags --abbrev=7 2>/dev/null |
      sed 's/\([^-]*-g\)/r\1/;s/-/./g' ||
    printf "%s.%s" "$(git rev-list --count HEAD)" \
      "$(git rev-parse --short=7 HEAD)"
  )
}

package() {
  cd $pkgname

  install -dm 755 "$pkgdir/usr/bin/"
  install -dm 755 "$pkgdir/usr/share/$pkgname"

  cp -a * "$pkgdir/usr/share/$pkgname/"

cat > "$pkgdir/usr/bin/$pkgname" << EOF
#!/bin/sh
exec python2 /usr/share/$pkgname/main.py "\$@"
EOF

  chmod +x "$pkgdir/usr/bin/$pkgname"
}

