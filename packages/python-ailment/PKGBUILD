# This file is part of BlackArch Linux ( https://www.blackarch.org/ ).
# See COPYING for license details.

pkgbase=python-ailment
pkgname=('python2-ailment' 'python-ailment')
_pkgname=ailment
pkgver=9.0.5450
pkgrel=1
pkgdesc='The angr Imtermediate Language.'
arch=('any')
url='https://pypi.org/project/ailment/#files'
license=('BSD')
makedepends=('python2-setuptools' 'python-setuptools')
source=("https://files.pythonhosted.org/packages/3d/f4/6e159c5e89fba06d04762838b00ba17aaa5f4b262761c9386b1588f72d45/$_pkgname-$pkgver.tar.gz")
sha512sums=('61d31584a2a83d6030487eb3866b7676f8a471ec3f4a97c5c3b2d0fc1765b8ad516a0b8d3ddbb0b6189a861bc7a078966d2d973877ca3999a225f51957f2c460')

prepare() {
  cp -a "$_pkgname-$pkgver"{,-2}
}

build() {
  cd "$_pkgname-$pkgver-2"

  python2 setup.py build

  cd "$srcdir/$_pkgname-$pkgver"

  python setup.py build
}

package_python2-ailment() {
  depends=('python2')

  cd "$_pkgname-$pkgver-2"

  python2 setup.py install --root="$pkgdir" --prefix=/usr -O1 --skip-build
}

package_python-ailment() {
  depends=('python')

  cd "$_pkgname-$pkgver"

  python setup.py install --root="$pkgdir" --prefix=/usr -O1 --skip-build
}

