# This file is part of BlackArch Linux ( https://www.blackarch.org/ ).
# See COPYING for license details.

pkgbase=python-ailment
pkgname=('python2-ailment' 'python-ailment')
_pkgname=ailment
pkgver=9.0.6885
pkgrel=1
pkgdesc='The angr Imtermediate Language.'
arch=('any')
url='https://pypi.org/project/ailment/#files'
license=('BSD')
makedepends=('python2-setuptools' 'python-setuptools')
source=("https://files.pythonhosted.org/packages/84/18/e8cd50a7a34b6da90e33f4f195a9b81b371e479a746f2d8e4622dd7b5916/$_pkgname-$pkgver.tar.gz")
sha512sums=('d7bae65809fc290ba66625dfbc61c62f0e9061fe4dd29c2758ecd30a3c86c5fa095cea5c2dd7350a186a722e03cba7d2b13663ecfc80e77d9986308bab96435d')

prepare() {
  cp -a "$_pkgname-$pkgver"{,-2}
}

build() {
  cd "$_pkgname-$pkgver-2"

  python2 setup.py build

  cd "$srcdir/$_pkgname-$pkgver"

  python setup.py build
}

package_python2-ailment() {
  depends=('python2')

  cd "$_pkgname-$pkgver-2"

  python2 setup.py install --root="$pkgdir" --prefix=/usr -O1 --skip-build
}

package_python-ailment() {
  depends=('python')

  cd "$_pkgname-$pkgver"

  python setup.py install --root="$pkgdir" --prefix=/usr -O1 --skip-build
}

