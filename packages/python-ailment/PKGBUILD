# This file is part of BlackArch Linux ( https://www.blackarch.org/ ).
# See COPYING for license details.

pkgbase=python-ailment
pkgname=('python2-ailment' 'python-ailment')
_pkgname=ailment
pkgver=9.0.9947
pkgrel=1
pkgdesc='The angr Imtermediate Language.'
arch=('any')
url='https://pypi.org/project/ailment/#files'
license=('BSD')
makedepends=('python2-setuptools' 'python-setuptools')
source=("https://files.pythonhosted.org/packages/52/05/c9801fb2627ffeb9503ff0afe9e28231ea5d6df2666193c796ecc10ef735/$_pkgname-$pkgver.tar.gz")
sha512sums=('547b9e6997adc7fdda2754454e029a10c78acf02ec49d39313037a7a2434d9e197ed451a6e2eea0aa3a6e92bc270e099edf9a62c53820538f6350858ff836294')

prepare() {
  cp -a "$_pkgname-$pkgver"{,-2}
}

build() {
  cd "$_pkgname-$pkgver-2"

  python2 setup.py build

  cd "$srcdir/$_pkgname-$pkgver"

  python setup.py build
}

package_python2-ailment() {
  depends=('python2')

  cd "$_pkgname-$pkgver-2"

  python2 setup.py install --root="$pkgdir" --prefix=/usr -O1 --skip-build
}

package_python-ailment() {
  depends=('python')

  cd "$_pkgname-$pkgver"

  python setup.py install --root="$pkgdir" --prefix=/usr -O1 --skip-build
}

