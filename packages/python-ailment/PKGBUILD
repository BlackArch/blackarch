# This file is part of BlackArch Linux ( https://www.blackarch.org/ ).
# See COPYING for license details.

pkgbase=python-ailment
pkgname=('python2-ailment' 'python-ailment')
_pkgname=ailment
pkgver=9.0.10730
pkgrel=1
pkgdesc='The angr Imtermediate Language.'
arch=('any')
url='https://pypi.org/project/ailment/#files'
license=('BSD')
makedepends=('python2-setuptools' 'python-setuptools')
source=("https://files.pythonhosted.org/packages/d8/f6/341b6929cec0fa6e7c16ad93ecc4b98c8da0e4c6dd01db05f491dd333036/$_pkgname-$pkgver.tar.gz")
sha512sums=('22d42f85cde26533bee8f143fdc06158642eff1fc4b457ed596efa33c86772050a771fc78417d508f7ebacc109bc8d3e9886a93b626ef86ba44a946533031b59')

prepare() {
  cp -a "$_pkgname-$pkgver"{,-2}
}

build() {
  cd "$_pkgname-$pkgver-2"

  python2 setup.py build

  cd "$srcdir/$_pkgname-$pkgver"

  python setup.py build
}

package_python2-ailment() {
  depends=('python2')

  cd "$_pkgname-$pkgver-2"

  python2 setup.py install --root="$pkgdir" --prefix=/usr -O1 --skip-build
}

package_python-ailment() {
  depends=('python')

  cd "$_pkgname-$pkgver"

  python setup.py install --root="$pkgdir" --prefix=/usr -O1 --skip-build
}

