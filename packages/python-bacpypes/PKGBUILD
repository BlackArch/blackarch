# This file is part of BlackArch Linux ( https://www.blackarch.org/ ).
# See COPYING for license details.

pkgbase=python-bacpypes
pkgname=('python2-bacpypes' 'python-bacpypes')
_pkgname=bacpypes
pkgver=0.17.6
pkgrel=2
pkgdesc='Testing multiple versions of python.'
arch=('any')
url='https://github.com/JoelBender/bacpypes'
license=('MIT')
makedepends=('python2-setuptools' 'python-setuptools')
source=("https://github.com/JoelBender/bacpypes/archive/v$pkgver.tar.gz")
sha512sums=('3ae048792deb073a93c64e7ee5f7416c654681e01d94587ab85c61bd16d2113bf757071e5de3fa04e201e1e2febf4daea9c2944db4320b45571c404877c9af51')

prepare() {
  cp -a "$_pkgname-$pkgver"{,-2}
}

build() {
  cd "$_pkgname-$pkgver-2"

  python2 setup.py build

  cd "$srcdir/$_pkgname-$pkgver"

  python setup.py build
}

package_python2-bacpypes() {
  depends=('python2')

  cd "$_pkgname-$pkgver-2"

  python2 setup.py install --root="$pkgdir" --prefix=/usr -O1 --skip-build
}

package_python-bacpypes() {
  depends=('python')

  cd "$_pkgname-$pkgver"

  python setup.py install --root="$pkgdir" --prefix=/usr -O1 --skip-build
}

