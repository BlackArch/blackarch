# This file is part of BlackArch Linux ( http://blackarch.org ).
# See COPYING for license details.

pkgname='packetsender'
pkgver=334.9bc5376
pkgrel=1
groups=('blackarch' 'blackarch-networking')
pkgdesc='An open source utility to allow sending and receiving TCP and UDP packets.'
arch=('x86_64' 'armv6h' 'armv7h' 'aarch64')
url='https://github.com/dannagle/PacketSender'
license=('GPL2')
depends=('openssl' 'qt5-base' 'libxcb' 'libcap' 'libgcrypt' 'libstdc++5'
         'libpng' 'glib2')
makedepends=('git')
source=('git+https://github.com/dannagle/PacketSender.git')
sha1sums=('SKIP')

pkgver() {
  cd "$srcdir/PacketSender"

  echo $(git rev-list --count HEAD).$(git rev-parse --short HEAD)
}

build() {
  cd "$srcdir/PacketSender/src"

  qmake

  make
}

package() {
  cd "$srcdir/PacketSender"

  install -Dm755 src/PacketSender "$pkgdir/usr/bin/packetsender"
  install -Dm644 -t "$pkgdir/usr/share/packetsender/screenshots/" screenshots/*
  install -Dm644 README.md "$pkgdir/usr/share/doc/packetsender/README.md"
  install -Dm644 LICENSE "$pkgdir/usr/share/licenses/packetsender/LICENSE"
}
