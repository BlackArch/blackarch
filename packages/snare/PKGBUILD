# This file is part of BlackArch Linux ( https://www.blackarch.org/ ).
# See COPYING for license details.

pkgname=snare
pkgver=173.5714b50
pkgrel=1
pkgdesc='Super Next generation Advanced Reactive honEypot'
groups=('blackarch' 'blackarch-honeypot' 'blackarch-webapp')
arch=('any')
url='https://github.com/mushorg/snare'
license=('GPL3')
depends=('python' 'python-aiohttp' 'python-aiohttp-jinja2' 'python-cssutils'
         'python-beautifulsoup4' 'python-gitpython' 'python-pycodestyle')
makedepends=('git')
source=("$pkgname::git+https://github.com/mushorg/$pkgname.git")
sha512sums=('SKIP')

pkgver() {
  cd $pkgname

  echo $(git rev-list --count HEAD).$(git rev-parse --short HEAD)
}

build() {
  cd $pkgname

  python setup.py build
}

package() {
  cd $pkgname

  python setup.py install --root="$pkgdir" --prefix=/usr -O1 --skip-build

  install -Dm 644 -t "$pkgdir/usr/share/doc/$pkgname/" README.md
  install -Dm 644 LICENSE "$pkgdir/usr/share/licenses/$pkgname/LICENSE"
}

