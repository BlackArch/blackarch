# This file is part of BlackArch Linux ( https://www.blackarch.org/ ).
# See COPYING for license details.

pkgname=sploitctl
pkgver=2.2.2
pkgrel=1
epoch=1
groups=('blackarch' 'blackarch-automation' 'blackarch-exploitation')
pkgdesc='Fetch, install and search exploit archives from exploit sites like exploit-db and packetstorm.'
arch=('any')
url='https://github.com/BlackArch/sploitctl'
license=('custom:unknown')
depends=('bash' 'curl' 'git' 'unzip' 'xdg-utils' 'tar' 'wget')
makedepends=('git')
source=('git+https://github.com/BlackArch/sploitctl.git')
sha512sums=('SKIP')

pkgver() {
  cd $pkgname

  grep '^VERSION=' "$pkgname.sh" | cut -d 'v' -f 2 | tr -d '"'
}

package() {
  cd $pkgname

  install -Dm 755 "$pkgname.sh" "$pkgdir/usr/bin/$pkgname"
  install -Dm 644 web/url.lst "$pkgdir/usr/share/$pkgname/web/url.lst"
  install -Dm 644 "bash-completion/$pkgname" \
    "$pkgdir/usr/share/bash-completion/completions/$pkgname"
  install -Dm 644 "$pkgname.1" "$pkgdir/usr/share/man/man1/$pkgname.1"
}

