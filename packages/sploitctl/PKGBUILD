# This file is part of BlackArch Linux ( http://blackarch.org ).
# See COPYING for license details.

pkgname=sploitctl
pkgver=2.0.1
pkgrel=1
epoch=1
groups=('blackarch' 'blackarch-automation' 'blackarch-exploitation')
pkgdesc='Fetch, install and search exploit archives from exploit sites like exploit-db and packetstorm.'
arch=('any')
url='https://github.com/BlackArch/sploitctl'
license=('custom:unknown')
depends=('bash' 'curl' 'git' 'unzip' 'xdg-utils')
makedepends=('git')
source=('git+https://github.com/BlackArch/sploitctl.git')
sha512sums=('SKIP')

pkgver() {
  cd "$pkgname"

  grep '^VERSION=' sploitctl.sh | cut -d 'v' -f 2 | tr -d '"'
}

package() {
  cd "$pkgname"

  install -Dm 755 sploitctl.sh "$pkgdir/usr/bin/sploitctl"
  install -Dm 644 web/url.lst "$pkgdir/usr/share/$pkgname/web/url.lst"
  install -Dm 644 bash-completion/sploitctl \
    "$pkgdir/usr/share/bash-completion/completions/sploitctl"
}

