# This file is part of BlackArch Linux ( https://www.blackarch.org/ ).
# See COPYING for license details.

pkgname=gdir.pl
pkgver=9.d2cc05d
pkgrel=1
pkgdesc='Perl wrapper on gcrypt for directory encryption/decryption.'
arch=('any')
groups=('blackarch' 'blackarch-crypto')
url='https://gitlab.com/GasparVardanyan/gdir.pl'
license=('custom:unknown')
depends=('gcrypt' 'perl')
makedepends=('git')
source=("git+https://gitlab.com/GasparVardanyan/$pkgname.git")
sha512sums=('SKIP')

pkgver() {
  cd $pkgname

  ( set -o pipefail
    git describe --long --tags --abbrev=7 2>/dev/null |
      sed 's/\([^-]*-g\)/r\1/;s/-/./g' ||
    printf "%s.%s" "$(git rev-list --count HEAD)" \
      "$(git rev-parse --short=7 HEAD)"
  )
}

package() {
  cd $pkgname

  install -dm 755 "$pkgdir/usr/"{bin,share/$pkgname}
  install -Dm 644 -t "$pkgdir/usr/share/doc/$pkgname/" *.md

  install -Dm 644 LICENSE "$pkgdir/usr/share/licenses/$pkgname/LICENSE"

  rm -rf LICENSE *.md

  cp -a * "$pkgdir/usr/share/$pkgname/"

  cat > "$pkgdir/usr/bin/$pkgname" << EOF
#!/bin/sh
exec perl /usr/share/$pkgname/gdir.pl "\$@"
EOF

  chmod a+x "$pkgdir/usr/bin/$pkgname"
}

