# Maintainer: noraj <printf %s 'YWxleGFuZHJlLnphbm5pQGV1cm9wZS5jb20='|base64 -d>

pkgname=aspisec
pkgver=0.1.0
pkgrel=2
groups=('blackarch' 'blackarch-misc')
pkgdesc='Removes the traces left by offensive security tools.'
arch=('any')
url='https://noraj.github.io/aspisec/'
license=('MIT')
depends=('ruby' 'ruby-bundler')
source=("https://github.com/noraj/$pkgname/archive/refs/tags/$pkgver.tar.gz")
sha512sums=('5c6eef959d19c0ba3aeec53b2fd062bbf627e9bc9aca82784a41575f1d5ed76983b5c6ddfeb5a830628127241db73bc1f3eb6c25890c384e6110618e7decb029')
install="$pkgname.install"

package() {
  cd "$pkgname-$pkgver"

  install -dm 755 "$pkgdir/usr/bin"
  install -dm 755 "$pkgdir/usr/share/$pkgname"

  install -Dm 644 -t "$pkgdir/usr/share/doc/$pkgname" *.md
  cp -rT --no-preserve=ownership --preserve=all --no-dereference docs/ "$pkgdir/usr/share/doc/$pkgname"

  install -Dm 644 LICENSE "$pkgdir/usr/share/licenses/$pkgname/LICENSE"

  rm -rf docs/ docs-tools/ *.md LICENSE .*

  cp --no-preserve=ownership -a * "$pkgdir/usr/share/$pkgname/"

  cat > "$pkgdir/usr/bin/$pkgname" << EOF
#!/bin/sh
cd /usr/share/$pkgname
bundle exec ruby bin-ruby/$pkgname "\$@"
EOF

  chmod +x "$pkgdir"/usr/bin/*
}
