# This file is part of BlackArch Linux ( https://www.blackarch.org/ ).
# See COPYING for license details.

pkgname=sandcastle
pkgver=73.10af7c7
pkgrel=1
pkgdesc='A Python script for AWS S3 bucket enumeration.'
groups=('blackarch' 'blackarch-scanner')
arch=('any')
url='https://github.com/0xSearches/sandcastle'
license=('MIT')
depends=('python2' 'python2-requests')
makedepends=('git')
source=("git+https://github.com/0xSearches/$pkgname.git")
sha512sums=('SKIP')

pkgver() {
  cd $pkgname

  echo $(git rev-list --count HEAD).$(git rev-parse --short HEAD)
}

prepare() {
  cd $pkgname

  sed -i 's|bin/env python|bin/env python2|' "$pkgname.py"
}

package() {
  cd $pkgname

  install -Dm 755 "$pkgname.py" "$pkgdir/usr/bin/$pkgname"
  install -Dm 644 bucket-names.txt "$pkgdir/usr/share/$pkgname/bucket-names.txt"
  install -Dm 644 -t "$pkgdir/usr/share/doc/$pkgname/" *.md
  install -Dm 644 LICENSE "$pkgdir/usr/share/licenses/$pkgname/LICENSE"
}

