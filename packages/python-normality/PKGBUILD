# This file is part of BlackArch Linux ( https://www.blackarch.org/ ).
# See COPYING for license details.

pkgbase=python-normality
pkgname=('python2-normality' 'python-normality')
_pkgname=normality
pkgver=2.0.0
pkgrel=3
pkgdesc='Micro-library to normalize text strings.'
arch=('any')
url='https://pypi.org/project/normality/#files'
license=('MIT')
makedepends=('python2-setuptools' 'python-setuptools')
source=("https://files.pythonhosted.org/packages/52/7b/2fd3d8bb39485f61c92cec809054c2c2eaa2168fb8dc1bb4e10740b0ad76/$_pkgname-$pkgver.tar.gz")
sha512sums=('9d942d13a3f18fd0d8cda14eca579eeba361d437f6227a4df0f1b6eaf165237a264a5c3045a93fa8d3d1da5904f41d323b0667a5ecb72fdaf7cfde6b42f09d3e')

prepare() {
  cp -a "$_pkgname-$pkgver"{,-2}
}

build() {
  cd "$_pkgname-$pkgver-2"

  python2 setup.py build

  cd "$srcdir/$_pkgname-$pkgver"

  python setup.py build
}

package_python2-normality() {
  depends=('python2' 'python2-six' 'python2-chardet' 'python2-pyicu'
           'python2-banal')

  cd "$_pkgname-$pkgver-2"

  python2 setup.py install --prefix=/usr --root="$pkgdir" --optimize=1 \
    --skip-build

  rm -rf "$pkgdir/usr/lib/python2.7/site-packages/tests"
}

package_python-normality() {
  depends=('python' 'python-six' 'python-chardet' 'python-pyicu' 'python-banal')

  cd "$_pkgname-$pkgver"

  python setup.py install --prefix=/usr --root="$pkgdir" --optimize=1 \
    --skip-build

  rm -rf "$pkgdir/usr/lib/python3.8/site-packages/tests"
}

