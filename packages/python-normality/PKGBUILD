# This file is part of BlackArch Linux ( https://www.blackarch.org/ ).
# See COPYING for license details.

pkgbase=python-normality
pkgname=('python2-normality' 'python-normality')
_pkgname=normality
pkgver=1.0.1
pkgrel=1
pkgdesc='Micro-library to normalize text strings.'
arch=('any')
url='https://pypi.org/project/normality/#files'
license=('MIT')
makedepends=('python2-setuptools' 'python-setuptools')
source=("https://files.pythonhosted.org/packages/32/47/ba23594ca8ae101720b7386a5d4ddf21587e1beee1def945dbc9736dfa28/$_pkgname-$pkgver.tar.gz")
sha512sums=('539e754d24f4c3d7f01bfd7a33dd1ae0fdc377819df1386a2be014d3663f06bf20e2b266173c22b8f473ba8a064356d2d9cf2471d9f2b57b96e1040a5f6487c2')

prepare() {
  cp -a "$_pkgname-$pkgver"{,-2}
}

build() {
  cd "$_pkgname-$pkgver-2"

  python2 setup.py build

  cd "$srcdir/$_pkgname-$pkgver"

  python setup.py build
}

package_python2-normality() {
  depends=('python2' 'python2-six' 'python2-chardet' 'python2-pyicu'
           'python2-banal')

  cd "$_pkgname-$pkgver-2"

  python2 setup.py install --prefix=/usr --root="$pkgdir" --optimize=1 \
    --skip-build

  rm -rf "$pkgdir/usr/lib/python2.7/site-packages/tests"
}

package_python-normality() {
  depends=('python' 'python-six' 'python-chardet' 'python-pyicu' 'python-banal')

  cd "$_pkgname-$pkgver"

  python setup.py install --prefix=/usr --root="$pkgdir" --optimize=1 \
    --skip-build

  rm -rf "$pkgdir/usr/lib/python3.7/site-packages/tests"
}

