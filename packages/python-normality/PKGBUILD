# This file is part of BlackArch Linux ( https://www.blackarch.org/ ).
# See COPYING for license details.

pkgbase=python-normality
pkgname=('python2-normality' 'python-normality')
_pkgname=normality
pkgver=1.0.0
pkgrel=2
pkgdesc='Micro-library to normalize text strings.'
arch=('any')
url='https://pypi.org/project/normality/#files'
license=('MIT')
makedepends=('python2-setuptools' 'python-setuptools')
source=("https://files.pythonhosted.org/packages/61/eb/fc7b6df01168fc091b27edee87602fb6996ff1054340fda09f45a24ecfa1/$_pkgname-$pkgver.tar.gz")
sha512sums=('7dc9dab2fa196521535c06d96029b364b01ddc1d49530affee185a053af4d5d662d11b33f47fac52f06f2fa463db0949bc2241a0bd6772a1d13154dd50d87d9e')

prepare() {
  cp -a "$_pkgname-$pkgver" "$_pkgname-$pkgver-2"
}

build() {
  cd "$_pkgname-$pkgver-2"

  python2 setup.py build

  cd "$srcdir/$_pkgname-$pkgver"

  python setup.py build
}

package_python2-normality() {
  depends=('python2' 'python2-six' 'python2-chardet' 'python2-pyicu'
           'python2-bala')

  cd "$_pkgname-$pkgver-2"

  python2 setup.py install --prefix=/usr --root="$pkgdir" --optimize=1 \
    --skip-build

  rm -rf "$pkgdir/usr/lib/python2.7/site-packages/tests"
}

package_python-normality() {
  depends=('python' 'python-six' 'python-chardet' 'python-pyicu' 'python-banal')

  cd "$_pkgname-$pkgver"

  python setup.py install --prefix=/usr --root="$pkgdir" --optimize=1 \
    --skip-build

  rm -rf "$pkgdir/usr/lib/python3.7/site-packages/tests"
}

