# This file is part of BlackArch Linux ( https://www.blackarch.org/ ).
# See COPYING for license details.

pkgbase=python-normality
pkgname=('python2-normality' 'python-normality')
_pkgname=normality
pkgver=2.1.1
pkgrel=1
pkgdesc='Micro-library to normalize text strings.'
arch=('any')
url='https://pypi.org/project/normality/#files'
license=('MIT')
makedepends=('python2-setuptools' 'python-setuptools')
source=("https://files.pythonhosted.org/packages/94/44/6a6beb3d70008c3ede6785e15772181f79a7d8366c912f8ca43cc056f53a/$_pkgname-$pkgver.tar.gz")
sha512sums=('24f4fdf964e4b9fff87eebca25c3b51d08120b4294ffa9e9c1e81f10d52f1f676591dd944fe456165708c6e15e58edb1d4020aae65f58f1951f4c755bb7d7e40')

prepare() {
  cp -a "$_pkgname-$pkgver"{,-2}
}

build() {
  cd "$_pkgname-$pkgver-2"

  python2 setup.py build

  cd "$srcdir/$_pkgname-$pkgver"

  python setup.py build
}

package_python2-normality() {
  depends=('python2' 'python2-six' 'python2-chardet' 'python2-pyicu'
           'python2-banal')

  cd "$_pkgname-$pkgver-2"

  python2 setup.py install --prefix=/usr --root="$pkgdir" --optimize=1 \
    --skip-build

  rm -rf "$pkgdir/usr/lib/python2.7/site-packages/tests"
}

package_python-normality() {
  depends=('python' 'python-six' 'python-chardet' 'python-pyicu' 'python-banal')

  cd "$_pkgname-$pkgver"

  python setup.py install --prefix=/usr --root="$pkgdir" --optimize=1 \
    --skip-build

  rm -rf "$pkgdir/usr/lib/python3.8/site-packages/tests"
}

