# This file is part of BlackArch Linux ( https://www.blackarch.org/ ).
# See COPYING for license details.

pkgname=scanless
pkgver=69.0f5fe0c
pkgrel=1
pkgdesc='Utility for using websites that can perform port scans on your behalf.'
groups=('blackarch' 'blackarch-scanner')
arch=('any')
url='https://github.com/vesche/scanless'
license=('custom:unknown')
depends=('python2' 'python2-argparse' 'python2-beautifulsoup4'
         'python2-requests')
makedepends=('git')
source=("git+https://github.com/vesche/$pkgname.git")
sha512sums=('SKIP')

pkgver() {
  cd $pkgname

  echo $(git rev-list --count HEAD).$(git rev-parse --short HEAD)
}

build() {
  cd $pkgname

  python2 setup.py build
}

package() {
  cd $pkgname

  python2 setup.py install --root="$pkgdir" --prefix=/usr --skip-build

  install -Dm 644 -t "$pkgdir/usr/share/doc/$pkgname/" README.md
  install -Dm 644 UNLICENSE "$pkgdir/usr/share/licenses/$pkgname/UNLICENSE"
}

