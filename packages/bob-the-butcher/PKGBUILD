# This file is part of BlackArch Linux ( https://www.blackarch.org/ ).
# See COPYING for license details.

pkgname=bob-the-butcher
pkgver=0.8.1
pkgrel=1
pkgdesc='A distributed password cracker package.'
arch=('x86_64' 'aarch64')
url='https://btb.banquise.net/'
license=('GPL-2.0-or-later')
depends=('libevent')
groups=('blackarch' 'blackarch-cracker')
source=("https://github.com/Obsidian-Covenant/$pkgname/archive/refs/tags/$pkgver.tar.gz")
sha512sums=('ca6df8103fcd5325b8c61100eabcca963491b31eccad15ebe33a58d971f66a92d8ebc28ff5693bdfcd86e8d41a6b7ceaf4a7e911a8bc0d2626a1465c01e3b176')

build() {
  cd "$pkgname-$pkgver"

  autoreconf -i

  if [[ "$CARCH" == "aarch64" ]];
  then
    ./configure --build=arm --prefix=/usr --program-suffix="-sse2"
  else
    ./configure --prefix=/usr --program-suffix="-sse2"
  fi

  make
}

package() {
  cd "$pkgname-$pkgver"

  make DESTDIR="$pkgdir" install
}

