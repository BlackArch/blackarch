# This file is part of BlackArch Linux ( https://www.blackarch.org/ ).
# See COPYING for license details.

pkgname=python2-libmsiecf-python
pkgver=20181227
pkgrel=1
pkgdesc='Library and tools to access the Microsoft Internet Explorer (MSIE) Cache File (index.dat) files.'
groups=('blackarch' 'blackarch-misc')
url='https://github.com/libyal/libmsiecf'
arch=('x86_64' 'armv6h' 'armv7h' 'aarch64')
license=('LGPLv3')
depends=('python2')
makedepends=('python2-setuptools' 'gcc')
source=("https://pypi.python.org/packages/source/l/libmsiecf-python/libmsiecf-python-$pkgver.tar.gz")
sha512sums=('9b7e24549cb8edc37afbb467d92b0ede193178a0d69cf9902a8c61fcc7c5d06f18a6fceb0768c62a3ed5d7730097ba27506d516ec53c5db0e8c15c690c1aaa7b')

build() {
  cd "libmsiecf-$pkgver"

  python2 setup.py build
}

check(){
  cd "libmsiecf-$pkgver/tests"

  chmod +x ./test_runner.sh
  ./test_runner.sh
}

package() {
  cd "libmsiecf-$pkgver"

  python2 setup.py install --root="$pkgdir" --optimize=1 --skip-build
}

