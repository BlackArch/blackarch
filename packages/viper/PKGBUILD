# This file is part of BlackArch Linux ( http://blackarch.org ).
# See COPYING for license details.

pkgname='viper'
pkgver=1627.641d1ea
pkgrel=1
groups=('blackarch' 'blackarch-disassembler' 'blackarch-binary'
        'blackarch-malware')
pkgdesc='A Binary analysis framework.'
arch=('any')
url='https://github.com/botherder/viper'
license=('custom')
depends=('python2' 'python2-magic' 'python2-pefile' 'python2-pydeep'
         'python2-requests' 'python2-sqlalchemy' 'python2-crypto'
         'python2-beautifulsoup4' 'python2-olefile' 'python2-prettytable'
         'python2-pypdns' 'python2-pypssl' 'python2-pype32' 'python2-bottle'
         'python2-pyexiftool' 'python2-pylzma' 'python2-pyelftools'
         'python2-bitstring' 'python2-dnspython' 'python2-pyasn1'
         'python2-pymisp' 'python2-dateutil' 'python2-requests-cache'
         'python2-terminaltables' 'python2-pydeep' 'python2-ssdeep' 'ssdeep'
         'oletools')
makedepends=('git' 'python2-sphinx')
source=('git+https://github.com/botherder/viper.git')
install='viper.install'
sha1sums=('SKIP')

pkgver() {
  cd "$srcdir/viper"

  echo $(git rev-list --count HEAD).$(git rev-parse --short HEAD)
}

build() {
  cd "$srcdir/viper"

  python2 -m compileall .
  python2 -O -m compileall .
}

package() {
  cd "$srcdir/viper"

  mkdir -p "$pkgdir/usr/bin"
  mkdir -p "$pkgdir/usr/share/viper"

  install -Dm644 README.md "$pkgdir/usr/share/doc/viper/README.md"
  install -Dm644 requirements.txt "$pkgdir/usr/share/doc/viper/requirements.txt"
  install -Dm644 LICENSE "$pkgdir/usr/share/licenses/viper/LICENSE"

  rm README.md requirements.txt LICENSE

  cp -a * "$pkgdir/usr/share/viper"

  cat > "$pkgdir/usr/bin/viper-cli" << EOF
#!/bin/sh
cd /usr/share/viper
exec python2 viper-cli "\$@"
EOF

  chmod +x "$pkgdir/usr/bin/viper-cli"

  cat > "$pkgdir/usr/bin/viper-api" << EOF
#!/bin/sh
cd /usr/share/viper
exec python2 viper-api "\$@"
EOF

  chmod +x "$pkgdir/usr/bin/viper-api"

  cat > "$pkgdir/usr/bin/viper-web" << EOF
#!/bin/sh
cd /usr/share/viper
exec python2 viper-web "\$@"
EOF

  chmod +x "$pkgdir/usr/bin/viper-web"
}
