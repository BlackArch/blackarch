# This file is part of BlackArch Linux ( https://www.blackarch.org/ ).
# See COPYING for license details.

pkgname=viper
pkgver=1960.1bd4ab0
pkgrel=1
groups=('blackarch' 'blackarch-disassembler' 'blackarch-binary'
        'blackarch-malware')
pkgdesc='A Binary analysis framework.'
arch=('any')
url='https://github.com/botherder/viper'
license=('custom')
depends=('python' 'python-magic' 'python-pefile' 'python-pydeep'
         'python-requests' 'python-sqlalchemy' 'python-pycryptodome'
         'python-beautifulsoup4' 'python-olefile' 'python-prettytable'
         'python-pypdns' 'python-pypssl' 'python-pype32' 'python-bottle'
         'python-pyexiftool' 'python-pylzma' 'python-pyelftools'
         'python-bitstring' 'python-dnspython' 'python-pyasn1' 'python-six'
         'python-pymisp' 'python-dateutil' 'python-requests-cache'
         'python-terminaltables' 'python-pydeep' 'python-ssdeep' 'ssdeep'
         'oletools' 'python-django' 'python-r2pipe' 'python-pbkdf2'
         'python-django-extensions' 'python-whitenoise' 'python-jsonschema')
makedepends=('git' 'python-sphinx' 'python-pip')
source=('git+https://github.com/botherder/viper.git')
install='viper.install'
sha512sums=('SKIP')

pkgver() {
  cd $pkgname

  echo $(git rev-list --count HEAD).$(git rev-parse --short HEAD)
}

prepare() {
  cd $pkgname

  git submodule init
  git submodule update
}

build() {
  cd $pkgname

  python -m compileall .
  python -O -m compileall .
}

package() {
  cd $pkgname

  install -dm 755 "$pkgdir/usr/bin"
  install -dm 755 "$pkgdir/usr/share/$pkgname"

  python setup.py install --record installed_files.txt --root="$pkgdir"

  install -Dm 644 README.md "$pkgdir/usr/share/doc/$pkgname/README.md"
  install -Dm 644 LICENSE "$pkgdir/usr/share/licenses/$pkgname/LICENSE"

  rm README.md LICENSE

  cp -a data/* "$pkgdir/usr/share/$pkgname/"
  cp -a * "$pkgdir/usr/share/$pkgname/"

  cat > "$pkgdir/usr/bin/viper-cli" << EOF
#!/bin/sh
cd /usr/share/$pkgname
exec python viper-cli "\$@"
EOF

  chmod +x "$pkgdir/usr/bin/viper-cli"

  cat > "$pkgdir/usr/bin/viper-api" << EOF
#!/bin/sh
cd /usr/share/$pkgname
exec python viper-api "\$@"
EOF

  chmod +x "$pkgdir/usr/bin/viper-api"

  cat > "$pkgdir/usr/bin/viper-web" << EOF
#!/bin/sh
cd /usr/share/$pkgname
exec python viper-web "\$@"
EOF

  chmod +x "$pkgdir/usr/bin/viper-web"

  #mv "$pkgdir"/viper.conf.sample "$pkgdir/usr/share/${pkgname}/"
}

