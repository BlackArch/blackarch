# This file is part of BlackArch Linux ( http://blackarch.org ).
# See COPYING for license details.

pkgname='viper'
pkgver=336.f9c11d7
pkgrel=1
groups=('blackarch' 'blackarch-disassembler' 'blackarch-binary'
        'blackarch-malware')
pkgdesc='A Binary analysis framework.'
arch=('any')
url='https://github.com/botherder/viper'
license=('custom')
depends=('python2' 'python2-magic' 'python2-pefile' 'python2-pydeep'
         'python2-requests' 'python2-sqlalchemy' 'python2-crypto'
         'python2-beautifulsoup4' 'python2-olefileio-pl')
makedepends=('git' 'python2-sphinx')
source=('git+https://github.com/botherder/viper.git')
sha1sums=('SKIP')

pkgver() {
  cd "$srcdir/viper"

  echo $(git rev-list --count HEAD).$(git rev-parse --short HEAD)
}

package() {
  cd "$srcdir/viper"

  mkdir -p "$pkgdir/usr/bin"
  mkdir -p "$pkgdir/usr/share/viper"

  install -Dm644 README.md "$pkgdir/usr/share/doc/viper/README.md"
  install -Dm644 AUTHORS "$pkgdir/usr/share/doc/viper/AUTHORS"
  install -Dm644 requirements.txt "$pkgdir/usr/share/doc/viper/requirements.txt"
  install -Dm644 LICENSE "$pkgdir/usr/share/licenses/viper/LICENSE"

  rm README.md AUTHORS requirements.txt LICENSE

  cp -a * "$pkgdir/usr/share/viper"

  cat > "$pkgdir/usr/bin/viper" << EOF
#!/bin/sh
cd /usr/share/viper
exec python2 viper.py "\$@"
EOF

  chmod +x "$pkgdir/usr/bin/viper"
}
