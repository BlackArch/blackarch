#compdef blackarch-helper

_blackarch_helper() {
    local -a commands
    local state

    commands=(
        'sync:Download/update the local package database'
        'search:Search for a package by keyword'
        'info:Show info for a specific package'
        'install:Build and install a package from source'
        'interactive:Open the interactive menu'
        'fzf:Alias for interactive'
        'help:Show the help message'
    )

    _arguments -C \
        '1: :->command' \
        '2: :->argument'

    case $state in
        command)
            _describe "command" commands
            ;;
        argument)
            case $words[2] in
                install|info)
                    local cache_file="${HOME}/.cache/blackarch-helper/tool_index.txt"
                    if [[ -f "$cache_file" ]]; then
                        local -a tools
                        tools=($(cut -d: -f3 "$cache_file"))
                        _describe -t tools "tool" tools
                    else
                        _message "run 'sync' first to generate tool list"
                    fi
                    ;;
                search)
                    _message "keyword to search for"
                    ;;
            esac
            ;;
    esac
}

_blackarch_helper "$@"