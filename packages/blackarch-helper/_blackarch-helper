#compdef blackarch-helper

_blackarch_helper() {
    local -a commands
    local state

    commands=(
        'sync:Download/update the local package database'
        'interactive:Open the interactive menu'
        'review:Review a PKGBUILD before building'
        'install:Build and install a package from source'
        'info:Show info for a specific package'
        'search:Search for a package by keyword'
        'clean-cache:Delete the local cache directory'
        'help:Show the help message'
    )

    _arguments -C \
        '1: :->command' \
        '2: :->argument'

    case $state in
        command)
            _describe "command" commands
            ;;
        argument)
            case $words[2] in
                install|info|review)
                    local cache_file="${HOME}/.cache/blackarch-helper/tool_index.txt"
                    if [[ -f "$cache_file" ]]; then
                        # Suggest both base names and versioned names for convenience
                        local -a tools
                        tools=($(cut -d: -f2,3 "$cache_file" | sed 's/:/\n/' | sort -u))
                        _describe -t tools "tool" tools
                    else
                        _message "run 'sync' first to generate tool list"
                    fi
                    ;;
                search)
                    _message "keyword to search for"
                    ;;
            esac
            ;;
    esac
}

_blackarch_helper "$@"