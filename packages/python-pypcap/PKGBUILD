# This file is part of BlackArch Linux ( https://www.blackarch.org/ ).
# See COPYING for license details.

pkgbase=python-pypcap
pkgname=('python2-pypcap' 'python-pypcap')
_pkgname=pypcap
pkgver=1.2.2
pkgrel=2
epoch=1
pkgdesc='Python extension module for libpcap.'
arch=('x86_64' 'armv6h' 'armv7h' 'aarch64')
url='https://pypi.org/project/pypcap/#files'
license=('custom:unknown')
makedepends=('python2-setuptools' 'python-setuptools')
source=("https://files.pythonhosted.org/packages/33/21/d1f24d8a93e4e11bf604d77e04080c05ecb0308a5606936a051bd2b2b5da/$_pkgname-$pkgver.tar.gz")
sha512sums=('652a7830ffbccfaeca499b7316195504797717dd8553d70b660438997c9ded81e79677b94cb0e95b3feb2861a1cf45e0809e5c319a7bac0298449b1f2e819bf1')

prepare() {
  cp -a "$_pkgname-$pkgver"{,-2}
}

build() {
  cd "$_pkgname-$pkgver-2"

  python2 setup.py build

  cd "$srcdir/$_pkgname-$pkgver"

  python setup.py build
}

package_python2-pypcap() {
  depends=('python2' 'libpcap')

  cd "$_pkgname-$pkgver-2"

  python2 setup.py install --prefix=/usr --root="$pkgdir" --optimize=1 \
    --skip-build
}

package_python-pypcap() {
  depends=('python' 'libpcap')

  cd "$_pkgname-$pkgver"

  python setup.py install --prefix=/usr --root="$pkgdir" --optimize=1 \
    --skip-build
}

