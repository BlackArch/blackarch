# This file is part of BlackArch Linux ( https://www.blackarch.org/ ).
# See COPYING for license details.

pkgbase=python-pypcap
pkgname=('python2-pypcap' 'python-pypcap')
_pkgname=pypcap
pkgver=1.2.3
pkgrel=3
epoch=1
pkgdesc='Python extension module for libpcap.'
arch=('x86_64' 'aarch64')
url='https://pypi.org/project/pypcap/#files'
license=('custom:unknown')
makedepends=('python2-setuptools' 'python-setuptools')
source=("https://files.pythonhosted.org/packages/12/71/a689ca8c6ef4570e105efdc6b46d4ac16cdef7af36a8fbfaff0f6fa2b754/pypcap-1.2.3.tar.gz")
sha512sums=('c2cac8c724e162f6753aec28a3346f4e1f336daf4fcdf64606d84ee24e83a082bf8aae29aa73cf3c837ee8ab3ea53f5c0269cb09ce876e37dd039f9908a6001a')

prepare() {
  cp -a "$_pkgname-$pkgver"{,-2}
}

build() {
  cd "$_pkgname-$pkgver-2"

  python2 setup.py build

  cd "$srcdir/$_pkgname-$pkgver"

  python setup.py build
}

package_python2-pypcap() {
  depends=('python2' 'libpcap')

  cd "$_pkgname-$pkgver-2"

  python2 setup.py install --prefix=/usr --root="$pkgdir" --optimize=1 \
    --skip-build
}

package_python-pypcap() {
  depends=('python' 'libpcap')

  cd "$_pkgname-$pkgver"

  python setup.py install --prefix=/usr --root="$pkgdir" --optimize=1 \
    --skip-build
}

