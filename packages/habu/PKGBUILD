# This file is part of BlackArch Linux ( http://blackarch.org ).
# See COPYING for license details.

pkgname='habu'
pkgver=62.6fc3715
pkgrel=1
pkgdesc='Python Network Hacking Toolkit.'
groups=('blackarch' 'blackarch-scanner' 'blackarch-spoof' 'blackarch-dos'
        'blackarch-cracker' 'blackarch-dos')
arch=('any')
url='https://github.com/portantier/habu'
license=('custom:unknown')
depends=('python' 'python-ipaddress' 'python-requests' 'python-argparse'
         'python-requests-cache' 'python-click' 'python-delegator.py'
         'python-beautifulsoup4' 'scapy3k')
makedepends=('git')
source=('git+https://github.com/portantier/habu.git')
sha1sums=('SKIP')

pkgver() {
  cd "$srcdir/habu"

  echo $(git rev-list --count HEAD).$(git rev-parse --short HEAD)
}

build() {
  cd "$srcdir/habu"

  python setup.py build
}

package() {
  cd "$srcdir/habu"

  python setup.py install --root="$pkgdir"

  install -Dm644 -t "$pkgdir/usr/share/doc/habu/" README.rst todo.rst
  install -Dm644 LICENSE "$pkgdir/usr/share/licenses/habu/LICENSE"
}
