# This file is part of BlackArch Linux ( https://www.blackarch.org/ ).
# See COPYING for license details.

pkgname=artillery
pkgver=357.805a5d8
pkgrel=1
epoch=1
groups=('blackarch' 'blackarch-defensive' 'blackarch-honeypot')
pkgdesc='A combination of a honeypot, file-system monitoring, system hardening, and overall health of a server to create a comprehensive way to secure a system.'
arch=('any')
url='https://www.trustedsec.com/downloads/artillery/'
license=('BSD')
depends=('python2' 'python2-yaml')
makedepends=('git')
options=('!emptydirs')
source=("git+https://github.com/BinaryDefense/$pkgname.git"
        'artillery.service')
sha512sums=('SKIP'
            'c815236556097b36c21d91025d48326d96d0f33b8d511a8daac6a3e79bed97de2946ef208027ca1d2e0426f65ce4a67a59137c13facda08c05ccc4def723f399')

pkgver() {
  cd $pkgname

  echo $(git rev-list --count HEAD).$(git rev-parse --short HEAD)
}

prepare() {
  cd $pkgname

  sed -i "s|/var|$pkgdir/var|g" setup.py
  sed -i "s|/etc|$pkgdir/etc|g" setup.py
  sed -i "s|/var/artillery|/usr/share/artillery|g" artillery.py \
    src/ssh_monitor.py src/monitor.py restart_server.py src/core.py \
    src/email_handler.py src/ftp_monitor.py src/monitor.py src/startup_artillery
  sed -i "s|/var/log/apache|/var/log/httpd|g" src/apache_monitor.py
}

package() {
  cd $pkgname

  install -dm 755 "$pkgdir/usr/bin"
  install -dm 755 "$pkgdir/usr/share/$pkgname"

  python2 setup.py <<< $'y\nn\nn'

  install -Dm 644 -t "$pkgdir/usr/share/doc/$pkgname" readme/*
  install -Dm 644 readme/LICENSE "$pkgdir/usr/share/licenses/$pkgname/LICENSE"

  rm readme/LICENSE

  mv "$pkgdir/var/artillery/"* "$pkgdir/usr/share/$pkgname"

  cat > "$pkgdir/usr/bin/$pkgname" << EOF
#!/bin/sh
if test \$EUID != 0 ; then
  echo 'Must be run as root.'
  exit 1
fi
exec python2 /usr/share/$pkgname/$pkgname.py "\$@"
EOF

  chmod +x "$pkgdir/usr/bin/$pkgname"
}

