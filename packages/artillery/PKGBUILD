# This file is part of BlackArch Linux ( http://blackarch.org ).
# See COPYING for license details.

pkgname='artillery'
pkgver=211.ff2157b
pkgrel=1
epoch=1
groups=('blackarch' 'blackarch-defensive' 'blackarch-honeypot')
pkgdesc='A combination of a honeypot, file-system monitoring, system hardening, and overall health of a server to create a comprehensive way to secure a system.'
arch=('any')
url='https://www.trustedsec.com/downloads/artillery/'
license=('BSD')
depends=('python2' 'python2-yaml')
makedepends=('git')
options=('!emptydirs')
source=('git+https://github.com/BinaryDefense/artillery.git'
        'artillery.service')
sha1sums=('SKIP'
          '395a9b70f9222cb216e1d97551b82d348e8c3c3a')

pkgver() {
  cd "$srcdir/artillery"

  echo $(git rev-list --count HEAD).$(git rev-parse --short HEAD)
}

prepare() {
  cd "$srcdir/artillery"

  sed -i "s|/var|$pkgdir/var|g" setup.py
  sed -i "s|/etc|$pkgdir/etc|g" setup.py
  sed -i "s|/var/artillery|/usr/share/artillery|g" artillery.py \
    src/ssh_monitor.py src/monitor.py restart_server.py src/core.py \
    src/email_handler.py src/ftp_monitor.py src/monitor.py src/startup_artillery
  sed -i "s|/var/log/apache|/var/log/httpd|g" src/apache_monitor.py
}

package() {
  cd "$srcdir/artillery"

  mkdir -p "$pkgdir/usr/bin"
  mkdir -p "$pkgdir/usr/share/artillery"

  python2 setup.py <<< $'y\nn\nn'

  install -Dm644 readme/LICENSE "$pkgdir/usr/share/licenses/artillery/LICENSE"
  rm readme/LICENSE
  install -Dm644 -t "$pkgdir/usr/share/doc/artillery" readme/*

  mv "$pkgdir/var/artillery/"* "$pkgdir/usr/share/artillery"

  cat > "$pkgdir/usr/bin/artillery" << EOF
#!/bin/sh
if test \$EUID != 0 ; then
  echo 'Must be run as root.'
  exit 1
fi
exec python2 /usr/share/artillery/artillery.py "\$@"
EOF

  chmod +x "$pkgdir/usr/bin/artillery"
}
