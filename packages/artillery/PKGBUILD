# This file is part of BlackArch Linux ( http://blackarch.org ).
# See COPYING for license details.

pkgname='artillery'
pkgver='1.0.2'
pkgrel=2
groups=('blackarch' 'blackarch-defensive' 'blackarch-honeypot')
pkgdesc="A combination of a honeypot, file-system monitoring, system hardening, and overall health of a server to create a comprehensive way to secure a system"
arch=('any')
url='https://www.trustedsec.com/downloads/artillery/'
license=('BSD')
depends=('python2')
source=("https://github.com/trustedsec/artillery/archive/$pkgver.tar.gz")
sha1sums=('5eb9d0016e865294b869e7a930ebb1e09c1fcc7b')

prepare() {
  cd "$srcdir/artillery-$pkgver"

  sed -i "s|/var|$pkgdir/var|g" setup.py
  sed -i "s|/etc|$pkgdir/etc|g" setup.py
}

package() {
  cd "$srcdir/artillery-$pkgver"

  python2 setup.py <<< $'y\nn\nn'

  rm "$pkgdir/var/artillery/setup.py"

  mkdir -p "$pkgdir/usr/bin"
  cat > "$pkgdir/usr/bin/artillery" <<EOF
#!/bin/sh
if test \$EUID != 0 ; then
  echo 'Must be run as root.'
  exit 1
fi
exec python2 /var/artillery/artillery.py "\$@"
EOF
  chmod +x "$pkgdir/usr/bin/artillery"
}
