# This file is part of BlackArch Linux ( http://blackarch.org ).
# See COPYING for license details.

pkgname='bladerf'
pkgver=0.9.0
pkgrel=1
pkgdesc='Driver for bladeRF, allowing general purpose software defined radio (SDR).'
arch=('x86_64' 'armv6h' 'armv7h' 'aarch64')
url='https://github.com/Nuand/bladeRF'
license=('GPL2')
depends=('libusbx>=1.0.12')
makedepends=('git' 'cmake' 'doxygen')
#backup=("etc/bladerf.cfg")
_git_commit='1d5aa6e2a4f2665b89c6e03ba35fa3d562828053'
_firmware_file='bladeRF_fw_v1.5.3.img'
source=("git+http://github.com/Nuand/bladeRF#commit=$_git_commit"
        "http://www.nuand.com/fx3/$_firmware_file"
        'http://nuand.com/fpga/ddc81d0fb1b653227b5824d8d5fcb444556da175/hostedx40.rbf'
        'http://nuand.com/fpga/ddc81d0fb1b653227b5824d8d5fcb444556da175/hostedx115.rbf'
        '88-bladerf.rules')
sha1sums=('SKIP'
          '6ef06d83af6af289c1ed922c37495183627d6d92'
          '10251c8d12bf32bd4d2ec987f904fa3e644883cd'
          '211d3d997b56624d46abfa2eeaeefde345a38c87'
          'd952ff2411b849b90e0bd387a0f31d5fbc2f3257')

_extract_ver() {
  head -n1 | sed -e "s/[^ ]* *\([0-9]*\))/\1/"
}

pkgver() {
  _MAJOR=$(grep VERSION_INFO_MAJOR "$srcdir/bladeRF/host/libraries/libbladeRF/CMakeLists.txt" | _extract_ver)
  _MINOR=$(grep VERSION_INFO_MINOR "$srcdir/bladeRF/host/libraries/libbladeRF/CMakeLists.txt" | _extract_ver)
  _PATCH=$(grep VERSION_INFO_PATCH "$srcdir/bladeRF/host/libraries/libbladeRF/CMakeLists.txt" | _extract_ver)
  printf "%s.%s.%s" $_MAJOR $_MINOR $_PATCH
}

build() {
  cd "$srcdir/bladeRF/host"

  mkdir -p build
  cd build

  cmake \
    -DCMAKE_BUILD_TYPE=Release \
    -DINSTALL_UDEV_RULES=OFF \
    -DBUILD_DOCUMENTATION=ON \
    -DCMAKE_INSTALL_PREFIX=/usr ../
  make
}

package() {
  cd "$srcdir/bladeRF/host/build"

  make DESTDIR="$pkgdir" install

  cd "$srcdir"
  install -Dm644 88-bladerf.rules "$pkgdir/usr/lib/udev/rules.d/88-bladerf.rules"
  install -Dm644 $_firmware_file "$pkgdir/usr/share/bladerf/firmware/$_firmware_file"
  install -Dm644 hostedx40.rbf "$pkgdir/usr/share/bladerf/fpga/hostedx40.rbf"
  install -Dm644 hostedx115.rbf "$pkgdir/usr/share/bladerf/fpga/hostedx115.rbf"

  if [[ $CARCH == x86_64 ]] ; then
    mv "$pkgdir/usr/lib64"/* "$pkgdir/usr/lib"
  fi
}
