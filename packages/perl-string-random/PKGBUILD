pkgname="perl-string-random"
pkgver=0.22
pkgrel=2
pkgdesc="Perl module to generate random strings based on a pattern"
arch=('any')
url="http://search.cpan.org/~steve/String-Random-${pkgver}/Random.pm"
license=('PerlArtistic' 'GPL')
depends=('perl')
options=(!emptydirs)
source=("http://search.cpan.org/CPAN/authors/id/S/ST/STEVE/String-Random-${pkgver}.tar.gz")
md5sums=('c0319f95c56450624cc646f7425e327e')

_env() {
  export PERL_MM_USE_DEFAULT=1 PERL_AUTOINSTALL=--skipdeps \
    PERL_MM_OPT="INSTALLDIRS=vendor DESTDIR='$pkgdir'" \
    PERL_MB_OPT="--installdirs vendor --destdir '$pkgdir'" \
    MODULEBUILDRC=/dev/null
}

build() {
  cd "$srcdir/String-Random-$pkgver"

  _env

  /usr/bin/perl Build.PL INSTALLDIRS=vendor
  ./Build
}

check() {
  cd "$srcdir/String-Random-$pkgver"

  _env

  ./Build test
}

package() {
  cd "$srcdir/String-Random-$pkgver"

  _env

  ./Build install

  # remove perllocal.pod and .packlist
  find "$pkgdir" -name .packlist -o -name perllocal.pod -delete
}
