# This file is part of BlackArch Linux ( https://www.blackarch.org/ ).
# See COPYING for license details.

pkgname=dnsmap
pkgver=23.03ce28b
pkgrel=2
pkgdesc='Passive DNS network mapper.'
arch=('any')
groups=('blackarch' 'blackarch-fingerprint')
url='https://github.com/pagvac/dnsmap'
license=('GPL')
depends=('python')
makedepends=('git')
source=("git+https://github.com/pagvac/$pkgname.git")
sha512sums=('SKIP')
install="$pkgname.install"

pkgver() {
  cd $pkgname

  ( set -o pipefail
    git describe --long --tags --abbrev=7 2>/dev/null |
      sed 's/\([^-]*-g\)/r\1/;s/-/./g' ||
    printf "%s.%s" "$(git rev-list --count HEAD)" \
      "$(git rev-parse --short=7 HEAD)"
  )
}

package() {
  cd $pkgname

  install -dm 755 "$pkgdir/usr/bin"
  install -Dm 644 -t "$pkgdir/usr/share/doc/$pkgname/" *.md

  install -Dm 644 LICENSE "$pkgdir/usr/share/licenses/$pkgname/LICENSE"

  rm -rf LICENSE *.md

  install -Dm 644 -t "$pkgdir/usr/share/$pkgname/" $pkgname.py

  cat > "$pkgdir/usr/bin/$pkgname" << EOF
#!/bin/sh
exec python /usr/share/$pkgname/dnsmap.py "\$@"
EOF

  chmod +x "$pkgdir/usr/bin/$pkgname"
}

