# This file is part of BlackArch Linux ( https://www.blackarch.org/ ).
# See COPYING for license details.

pkgname=dnsmap
pkgver=0.36.r6.g96b17ad
pkgrel=1
pkgdesc='Passive DNS network mapper.'
arch=('x86_64' 'aarch64')
groups=('blackarch' 'blackarch-fingerprint')
url='https://github.com/resurrecting-open-source-projects/dnsmap'
license=('GPL3')
makedepends=('git' 'make')
install=dnsmap.install
source=("git+https://github.com/resurrecting-open-source-projects/$pkgname.git")
sha512sums=('SKIP')

pkgver() {
  cd $pkgname

  ( set -o pipefail
    git describe --long --tags --abbrev=7 2>/dev/null |
      sed 's/\([^-]*-g\)/r\1/;s/-/./g' ||
    printf "%s.%s" "$(git rev-list --count HEAD)" \
      "$(git rev-parse --short=7 HEAD)"
  )
}

build() {
  cd $pkgname

  ./autogen.sh
  ./configure --prefix=/usr
  make
}

package() {
  cd $pkgname

  make DESTDIR=$pkgdir install

  install -Dm 644 doc/wordlist_TLAs.txt \
    "$pkgdir/usr/share/$pkgname/wordlist_TLAs.txt"
}

