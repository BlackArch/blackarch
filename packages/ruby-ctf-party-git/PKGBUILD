# This file is part of BlackArch Linux ( https://www.blackarch.org/ ).
# See COPYING for license details.

_gemname=ctf-party
pkgname=ruby-$_gemname-git
pkgver=v1.0.0.r8.g0d9a22a
pkgrel=1
groups=('blackarch' 'blackarch-misc')
pkgdesc='A library to enhance and speed up script/exploit writting for CTF players'
arch=('x86_64')
url='https://orange-cyberdefense.github.io/ctf-party/'
license=('MIT')
depends=('ruby')
conflicts=('ruby-ctf-party')
provides=('ruby-ctf-party')
options=(!emptydirs)
source=("git+https://github.com/Orange-Cyberdefense/$_gemname.git")
b2sums=('SKIP')

pkgver() {
  cd $_gemname

  git describe --long | sed 's/\([^-]*-g\)/r\1/;s/-/./g'
}

build() {
  cd $_gemname

  gem build ctf_party.gemspec
}

package() {
  cd $_gemname

  local _gemdir="$(ruby -e'puts Gem.default_dir')"
  local _release=$(gem build ctf_party.gemspec | grep Version | cut -d' ' -f4)
  gem install --ignore-dependencies --no-user-install --no-document -i "$pkgdir/$_gemdir" -n "$pkgdir/usr/bin" $_gemname-$_release.gem
  rm "$pkgdir/$_gemdir/cache/$_gemname-$_release.gem"
  find "$pkgdir/$_gemdir/extensions/" -name *.so -delete
  rm -r "$pkgdir/$_gemdir/gems/$_gemname-$_release/test"
  install -D -m644 "$pkgdir/$_gemdir/gems/$_gemname-$_release/LICENSE.txt" "$pkgdir/usr/share/licenses/$pkgname/LICENSE"
}

