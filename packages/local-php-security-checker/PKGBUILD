# This file is part of BlackArch Linux ( https://www.blackarch.org/ ).
# See COPYING for license details.

pkgname=local-php-security-checker
pkgver=v2.0.6.r0.g7001dd5
pkgrel=1
pkgdesc='A command line tool that checks your PHP application packages with known security vulnerabilities.'
arch=('x86_64' 'aarch64')
groups=('blackarch' 'blackarch-code-audit')
url='https://github.com/fabpot/local-php-security-checker'
license=('AGPL3')
makedepends=('git' 'go')
source=("git+https://github.com/fabpot/$pkgname.git")
sha512sums=('SKIP')

pkgver() {
  cd $pkgname

  git describe --long --tags | sed 's/\([^-]*-g\)/r\1/;s/-/./g'
}

build() {
  cd $pkgname

  GOPATH="$srcdir" go mod download
  GOPATH="$srcdir" go build \
    -trimpath \
    -buildmode=pie \
    -mod=readonly \
    -modcacherw \
    -ldflags "-s -w" \
    -o $pkgname .
}

package() {
  cd $pkgname

  install -Dm 755 $pkgname "$pkgdir/usr/bin/$pkgname"
  install -Dm 644 -t "$pkgdir/usr/share/doc/$pkgname" README.md
}

