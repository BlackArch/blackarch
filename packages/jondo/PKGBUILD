# This file is part of BlackArch Linux ( http://blackarch.org ).
# See COPYING for license details.

pkgname='jondo'
pkgver='00.19.001'
pkgrel=2
pkgdesc='Redirects internet traffic trough a mix of proxy servers to hide the origin of the requests.'
arch=('any')
license=('BSD')
url='https://anonymous-proxy-servers.net/'
depends=('java-environment')
install="jondo.install"
source=('https://anonymous-proxy-servers.net/downloads/jondo_linux.tar.bz2'
        'https://anonymous-proxy-servers.net/downloads/jondo_linux.tar.bz2.asc'
        'jondo'
        'jondo.1'
        'licence')
sha256sums=('68fc519960c788ba5a36c3ef4871088d669f7d2ffcaf77cc8b4df25505233e33'
            'SKIP'
            '0a25c9f3ad376ea6b3c3ec87d7b6a534a30270e77ff1fc8b8c936f83f1d8df03'
            '8e5f582e5f6e64d7b134110f4475d5a8679cd0f2c1d5538f5b970ae0f8b3d24c'
            '9c7de55f80fd78ef6f8d39fa0309defbc42ac848c2c9f5a995901326a1a43e8d')

package() {
  cd $srcdir/

  # Bin
  install -Dm755 jondo ${pkgdir}/usr/bin/jondo

  # Man
  install -Dm644 jondo.1 ${pkgdir}/usr/share/man/man1/jondo.1

  # License
  install -Dm644 licence ${pkgdir}/usr/share/licenses/jondo/LICENCE

  cd jondo_linux/

  # Jar
  install -Dm644 JAP.jar $pkgdir/usr/share/java/jondo/JAP.jar

  # Sharable
  install -Dm644 desktop/jondo.desktop $pkgdir/usr/share/applications/jondo.desktop
  install -Dm644 desktop/jondo.menu $pkgdir/usr/share/menu/jondo.menu
  install -Dm644 icons/jondo-48.png $pkgdir/usr/share/pixmaps/jondo.png
}
