# This file is part of BlackArch Linux ( https://www.blackarch.org/ ).
# See COPYING for license details.

pkgname=factordb-pycli
pkgver=27.3e7bc73
pkgrel=1
pkgdesc='CLI for factordb and Python API Client.'
arch=('any')
groups=('blackarch' 'blackarch-crypto')
url='https://github.com/ryosan-470/factordb-pycli'
license=('MIT')
depends=('python' 'python-requests')
makedepends=('python-setuptools')
source=('git+https://github.com/ryosan-470/factordb-pycli.git')
sha512sums=('SKIP')

pkgver() {
  cd $pkgname

  echo $(git rev-list --count HEAD).$(git rev-parse --short HEAD)
}

prepare() {
  cd "$pkgname"

  cp --no-preserve=ownership README.md README.rst
}

build() {
  cd "$pkgname"

  python setup.py build
}

package() {
  cd "$pkgname"

  python setup.py install --prefix=/usr --root="$pkgdir" --optimize=1 \
    --skip-build
}

