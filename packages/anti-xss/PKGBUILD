# This file is part of BlackArch Linux ( https://www.blackarch.org/ ).
# See COPYING for license details.

pkgname=anti-xss
pkgver=166.2725dc9
pkgrel=1
pkgdesc='A XSS vulnerability scanner.'
groups=('blackarch' 'blackarch-webapp' 'blackarch-scanner')
arch=('any')
url='https://github.com/lewangbtcc/anti-XSS'
license=('MIT')
depends=('python2')
makedepends=('git')
source=('git+https://github.com/lewangbtcc/anti-XSS')
sha512sums=('SKIP')

pkgver() {
  cd anti-XSS

  echo $(git rev-list --count HEAD).$(git rev-parse --short HEAD)
}

package() {
  cd anti-XSS

  install -dm 755 "$pkgdir/usr/bin"
  install -dm 755 "$pkgdir/usr/share/$pkgname"

  install -Dm644 -t "$pkgdir/usr/share/doc/$pkgname/" README.md doc/*
  install -Dm644 LICENSE "$pkgdir/usr/share/licenses/$pkgname/LICENSE"

  rm -rf README.md LICENSE doc

  cp -a * "$pkgdir/usr/share/$pkgname"

  cat > "$pkgdir/usr/bin/$pkgname" << EOF
#!/bin/sh
cd /usr/share/anti-xss
exec python2 anti-XSS.py "\$@"
EOF

  chmod +x "$pkgdir/usr/bin/$pkgname"
}

