# This file is part of BlackArch Linux ( https://www.blackarch.org/ ).
# See COPYING for license details.

pkgname=aiodnsbrute
pkgver=v0.3.2.r0.ge773a4c
pkgrel=1
epoch=1
pkgdesc='Python 3 DNS asynchronous brute force utility.'
groups=('blackarch' 'blackarch-recon')
arch=('any')
url='https://github.com/blark/aiodnsbrute'
license=('GPL-3.0-or-later')
depends=('python' 'python-aiodns' 'python-click' 'python-tqdm' 'python-uvloop'
         'python-asyncio')
makedepends=('git' 'python-setuptools')
source=("git+https://github.com/blark/$pkgname.git")
sha512sums=('SKIP')

pkgver() {
  cd $pkgname

  ( set -o pipefail
    git describe --long --tags --abbrev=7 2>/dev/null |
      sed 's/\([^-]*-g\)/r\1/;s/-/./g' ||
    printf "%s.%s" "$(git rev-list --count HEAD)" \
      "$(git rev-parse --short=7 HEAD)"
  )
}

build() {
  cd $pkgname

  python setup.py build
}

package() {
  cd $pkgname

  python setup.py install --root="$pkgdir" --prefix=/usr -O1 --skip-build

  install -dm 755 "$pkgdir/usr/share/$pkgname"

  install -Dm 644 -t "$pkgdir/usr/share/doc/$pkgname/" README.md CHANGELOG

  rm LICENSE.txt

  cp -a screenshot.png aiodnsbrute/wordlists "$pkgdir/usr/share/$pkgname/"
}

