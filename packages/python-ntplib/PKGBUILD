# This file is part of BlackArch Linux ( https://www.blackarch.org/ ).
# See COPYING for license details.

pkgbase=python-ntplib
pkgname=('python2-ntplib' 'python-ntplib')
_pkgname=ntplib
pkgver=0.3.4
pkgrel=3
pkgdesc='Python NTP library.'
arch=('any')
url='https://pypi.org/project/ntplib/'
license=('GPL2')
makedepends=('python2-setuptools' 'python-setuptools')
source=("https://github.com/cf-natali/ntplib/archive/$pkgver.tar.gz")
sha512sums=('368852f3fcc9e9a1c90340656bcf8e9b1143d62f616a1f177567eed419125466d572e57bac9a25465c775499cfb6b5ce1b1ee9a93c54e93667d751285757709e')

prepare() {
  cp -a "$_pkgname-$pkgver"{,-2}
}

build() {
  cd "$_pkgname-$pkgver-2"

  python2 setup.py build

  cd "$srcdir/$_pkgname-$pkgver"

  python setup.py build
}

package_python2-ntplib() {
  depends=('python2')

  cd "$_pkgname-$pkgver-2"

  python2 setup.py install --root="$pkgdir" --prefix=/usr -O1 --skip-build
}

package_python-ntplib() {
  depends=('python')

  cd "$_pkgname-$pkgver"

  python setup.py install --root="$pkgdir" --prefix=/usr -O1 --skip-build
}

