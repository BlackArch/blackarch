# This file is part of BlackArch Linux ( http://blackarch.org ).
# See COPYING for license details.

pkgname='python-pyusb'
pkgver=355.5ff1d10
pkgrel=1
pkgdesc="This module provides for Python easy access to the host machine's Universal Serial Bus (USB) system."
arch=('any')
url='http://pyusb.berlios.de/'
license=('custom')
depends=('python' 'libusb' 'libusb-compat')
makedepends=('git' 'python-distribute')
source=('git+https://github.com/walac/pyusb.git')
sha1sums=('SKIP')

pkgver() {
  cd "$srcdir/pyusb"

  echo $(git rev-list --count HEAD).$(git rev-parse --short HEAD)
}

build() {
  cd "$srcdir/pyusb"

  python setup.py build
}

package() {
  cd "$srcdir/pyusb"

  python setup.py install -f --root="$pkgdir" --optimize=1

  install -Dm644 LICENSE "$pkgdir/usr/share/licenses/python-pyusb/LICENSE"
  install -Dm644 -t "$pkgdir/usr/share/doc/python-pyusb" README.rst \
    ACKNOWLEDGEMENTS MANIFEST.in
}
