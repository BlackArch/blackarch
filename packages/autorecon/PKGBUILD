# This file is part of BlackArch Linux ( https://www.blackarch.org/ ).
# See COPYING for license details.

pkgname=autorecon
pkgver=83.da59c5b
pkgrel=1
pkgdesc='A multi-threaded network reconnaissance tool which performs automated enumeration of services.'
groups=('blackarch' 'blackarch-automation' 'blackarch-recon'
        'blackarch-scanner')
arch=('any')
url='https://github.com/Tib3rius/AutoRecon'
license=('GPL3')
depends=('python' 'python-colorama' 'python-toml' 'nmap')
optdepends=('curl'
            'enum4linux'
            'gobuster'
            'nbtscan'
            'nikto'
            'onesixtyone'
            'oscanner'
            'smbclient'
            'smbmap'
            'smtp-user-enum'
            'sslscan'
            'whatweb')
makedepends=('git')
source=("$pkgname::git+https://github.com/Tib3rius/AutoRecon.git")
sha512sums=('SKIP')

pkgver() {
  cd $pkgname

  echo $(git rev-list --count HEAD).$(git rev-parse --short HEAD)
}

package() {
  cd $pkgname

  install -dm 755 "$pkgdir/usr/bin"
  install -dm 755 "$pkgdir/usr/share/$pkgname"

  install -Dm 644 -t "$pkgdir/usr/share/doc/$pkgname/" README.md
  install -Dm 644 LICENSE "$pkgdir/usr/share/licenses/$pkgname/LICENSE"

  rm README.md LICENSE

  mv "src/$pkgname/"* "$pkgdir/usr/share/$pkgname"
  rm -rf src
  cp -a * "$pkgdir/usr/share/$pkgname/"

  cat > "$pkgdir/usr/bin/$pkgname" << EOF
#!/bin/sh
cd /usr/share/$pkgname
exec python $pkgname.py "\$@"
EOF

  chmod +x "$pkgdir/usr/bin/$pkgname"
}

