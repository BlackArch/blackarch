# This file is part of BlackArch Linux ( https://www.blackarch.org/ ).
# See COPYING for license details.

pkgname=garak
pkgver=0.14.0.r66.ge64bded
pkgrel=1
pkgdesc='The LLM vulnerability scanner.'
arch=('any')
groups=('blackarch' 'blackarch-scanner')
url='https://github.com/NVIDIA/garak'
license=('Apache-2.0')
depends=('python')
makedepends=('git' 'python-setuptools' 'python-pip')
source=("git+https://github.com/NVIDIA/$pkgname.git")
sha512sums=('SKIP')
install="$pkgname.install"

pkgver() {
  cd $pkgname

  ( set -o pipefail
    git describe --long --tags --abbrev=7 2>/dev/null |
      sed 's/\([^-]*-g\)/r\1/;s/-/./g' |
      sed 's/^v//' ||
    printf "%s.%s" "$(git rev-list --count HEAD)" \
      "$(git rev-parse --short=7 HEAD)"
  )
}

package() {
  cd $pkgname

  install -dm 755 "$pkgdir/usr/bin"
  install -Dm 644 requirements.txt "$pkgdir/usr/share/$pkgname/requirements.txt"
  install -Dm 644 -t "$pkgdir/usr/share/doc/$pkgname/" *.md
  install -Dm 644 docs/README.md "$pkgdir/usr/share/doc/$pkgname/README-docs.md"
  install -Dm 644 README.md "$pkgdir/usr/share/$pkgname/README.md"

  rm -rf *.md docs/*.md .gitignore

  cp -a * "$pkgdir/usr/share/$pkgname/"

  cat > "$pkgdir/usr/bin/$pkgname" << EOF
#!/bin/sh
source /usr/share/$pkgname/venv/bin/activate
exec python /usr/share/$pkgname/garak/__main__.py "\$@"
EOF

  chmod +x "$pkgdir/usr/bin/$pkgname"
}

