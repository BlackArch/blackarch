# This file is part of BlackArch Linux ( https://www.blackarch.org/ ).
# See COPYING for license details.

pkgbase=python-hll
pkgname=('python2-hll' 'python-hll')
_pkgname=HLL
pkgver=1.2.7
pkgrel=1
pkgdesc='HyperLogLog implementation in C for python.'
arch=('x86_64' 'aarch64')
url='https://pypi.org/project/HLL/#files'
license=('APACHE')
makedepends=('python2-setuptools' 'python-setuptools')
source=("https://files.pythonhosted.org/packages/d6/68/8f7ae23cb0b0e6da07776f17240570b433e785e3b4c34259b363d2ab8fbd/$_pkgname-$pkgver.tar.gz")
sha512sums=('6ffe30fe2e4d71e678064cd02d908197bd13d60ec13040010269e7e25bbf7424d99723b5a64eb4eee4cf3adcc27ec3cc3fd5752a33ddf8be14e109ec3946fe49')

prepare() {
  cp -a "$_pkgname-$pkgver"{,-2}
}

build() {
  cd "$_pkgname-$pkgver-2"

  python2 setup.py build

  cd "$srcdir/$_pkgname-$pkgver"

  python setup.py build
}

package_python2-hll() {
  depends=('python2')

  cd "$_pkgname-$pkgver-2"

  python2 setup.py install --prefix=/usr --root="$pkgdir" -O1 --skip-build
}

package_python-hll() {
  depends=('python')

  cd "$_pkgname-$pkgver"

  python setup.py install --prefix=/usr --root="$pkgdir" -O1 --skip-build
}

