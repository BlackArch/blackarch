# This file is part of BlackArch Linux ( http://blackarch.org ).
# See COPYING for license details.
# Original Maintainer on AUR: zwindl

pkgname='gdbgui'
pkgver=225.8abdff5
pkgrel=1
pkgdesc="Browser-based gdb frontend using Flask and JavaScript to visually debug C, C++, Go, or Rust."
url='https://github.com/cs01/gdbgui'
depends=('python' 'python-eventlet' 'python-flask' 'python-flask-socketio-git' 'python-pygdbmi' 'python-pypugjs' 'python-enum-compat' 'python-gevent' 'python-pygments')
optdepends=('gdb: GNU Project Debugger')
groups=('blackarch' 'blackarch-binary' 'blackarch-debugger')
makedepends=('git' 'python-setuptools')
license=('GPL3')
arch=('any')
install='python-gdbgui.install'
source=('git+https://github.com/cs01/gdbgui.git')
md5sums=('SKIP')

pkgver() {
  cd "$srcdir/gdbgui"
  echo $(git rev-list --count HEAD).$(git rev-parse --short HEAD)
}

build() {
  cd "$srcdir/gdbgui"
  python setup.py build
}

package() {
  cd "$srcdir/gdbgui"
  python setup.py install --root="$pkgdir" --optimize=1 --skip-build
  install -D -m644 LICENSE "${pkgdir}/usr/share/licenses/gdbgui/LICENSE"
}
