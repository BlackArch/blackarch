# This file is part of BlackArch Linux ( https://www.blackarch.org/ ).
# See COPYING for license details.

pkgbase=python-esmre
pkgname=('python2-esmre' 'python-esmre')
_pkgname=esmre
pkgver=0.3.1
pkgrel=6
pkgdesc='Modules used to accelerate execution of a large collection of regular expressions using the Aho-Corasick algorithms.'
arch=('x86_64' 'aarch64')
url='https://pypi.org/project/esmre/#files'
license=('LGPL')
makedepends=('python2-setuptools' 'python-setuptools')
source=("https://files.pythonhosted.org/packages/50/b0/17401277ddaa334622c047f9c9a083956e67f8b08c7b8b43ade0b588f349/$_pkgname-$pkgver.tar.gz")
sha512sums=('446c8470b658db6be8d97b70071c7586fcc61bd740488133b57fb71ae2e668d93698916f3249bc9fa417451279f5b84379c88ba9d54907262c77010054a5c0b1')

prepare() {
  cp -a "$_pkgname-$pkgver"{,-2}
}

build() {
  cd "$_pkgname-$pkgver-2"

  python2 setup.py build

  cd "$srcdir/$_pkgname-$pkgver"

  python setup.py build
}

package_python2-esmre() {
  depends=('python2')

  cd "$_pkgname-$pkgver-2"

  python2 setup.py install --root="$pkgdir" --prefix=/usr -O1 --skip-build
}

package_python-esmre() {
  depends=('python')

  cd "$_pkgname-$pkgver"

  python setup.py install --root="$pkgdir" --prefix=/usr -O1 --skip-build
}

