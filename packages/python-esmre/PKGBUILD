# This file is part of BlackArch Linux ( https://www.blackarch.org/ ).
# See COPYING for license details.

pkgbase=python-esmre
pkgname=('python2-esmre' 'python-esmre')
_pkgname=esmre
pkgver=0.5.2
pkgrel=2
pkgdesc='Modules used to accelerate execution of a large collection of regular expressions using the Aho-Corasick algorithms.'
arch=('x86_64' 'aarch64')
url='https://pypi.org/project/esmre/#files'
license=('LGPL')
makedepends=('python2-setuptools' 'python-setuptools')
source=("https://files.pythonhosted.org/packages/96/99/5f6882cdb32708711b7724fee76eb6ce44033309ed0b12f71da1731e7d22/$_pkgname-$pkgver.tar.gz")
sha512sums=('6e8c2860e57feb8c747a5a6e93f6b3af70e7587682c7824a54c15a84cf7966b2d9d91063f4caa733a80cd71d8eefde8a75ac01b05d7b1cee8342596d4c382b8d')

prepare() {
  cp -a "$_pkgname-$pkgver"{,-2}
}

build() {
  cd "$_pkgname-$pkgver-2"

  python2 setup.py build

  cd "$srcdir/$_pkgname-$pkgver"

  python setup.py build
}

package_python2-esmre() {
  depends=('python2')

  cd "$_pkgname-$pkgver-2"

  python2 setup.py install --root="$pkgdir" --prefix=/usr -O1 --skip-build
}

package_python-esmre() {
  depends=('python')

  cd "$_pkgname-$pkgver"

  python setup.py install --root="$pkgdir" --prefix=/usr -O1 --skip-build
}

