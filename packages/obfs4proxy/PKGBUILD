# This file is part of BlackArch Linux ( http://blackarch.org ).
# See COPYING for license details.

pkgname=obfs4proxy
pkgver=0.0.7
pkgrel=1
pkgdesc='A pluggable transport proxy written in Go'
groups=('blackarch' 'blackarch-proxy' 'blackarch-networking')
arch=('i686' 'x86_64')
url='https://gitweb.torproject.org/pluggable-transports/obfs4.git'
license=('BSD')
makedepends=('git' 'go')
optdepends=('tor')
source=("git+https://git.torproject.org/pluggable-transports/obfs4.git#tag=obfs4proxy-${pkgver}")
sha1sums=('SKIP')

build() {
  cd "$srcdir/obfs4/obfs4proxy"
  
  GOPATH="$srcdir" GOBIN="$PWD" go get
}

package() {
  cd "$srcdir/obfs4"
  
  install -Dm 755 obfs4proxy/obfs4proxy "$pkgdir/usr/bin/obfs4proxy"
  install -Dm 644 doc/obfs4proxy.1 "$pkgdir/usr/share/man/man1/obfs4proxy.1"
  install -Dm 644 ChangeLog "$pkgdir/usr/share/doc/obfs4proxy/ChangeLog"
  install -Dm 644 LICENSE "$pkgdir/usr/share/licenses/obfs4proxy/LICENSE"
}
