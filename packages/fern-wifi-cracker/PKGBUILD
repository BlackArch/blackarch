# This file is part of BlackArch Linux ( http://blackarch.org ).
# See COPYING for license details.

pkgname='fern-wifi-cracker'
pkgver='222'
pkgrel=2
groups=('blackarch' 'blackarch-cracker' 'blackarch-wireless')
pkgdesc='WEP, WPA wifi cracker for wireless penetration testing'
arch=('any')
url='http://code.google.com/p/fern-wifi-cracker/'
license=('GPL2')
depends=('python2' 'python2-pyqt4' 'aircrack-ng' 'xterm' 'subversion' 'scapy'
         'macchanger' 'gksu')
makedepends=('subversion')
_svnmod='fern-wifi-cracker'
source=("svn+http://fern-wifi-cracker.googlecode.com/svn/Fern-Wifi-Cracker#revision=${pkgver}")
sha1sums=('SKIP')

prepare() {
  cd "$srcdir/Fern-Wifi-Cracker"

  sed -i 's|python$|python2|g' execute.py
}

package() {
  cd "$srcdir/Fern-Wifi-Cracker"

  mkdir -p "$pkgdir/usr/bin"
  mkdir -p "$pkgdir/usr/share/fern-wifi-cracker/Settings"

  cp -a --no-preserve=ownership * "$pkgdir/usr/share/fern-wifi-cracker"

  cat > "$pkgdir/usr/bin/fern" << EOF
#!/bin/sh
cd /usr/share/fern-wifi-cracker
exec python2 ./execute.py "\$@"
EOF

  chmod +x "$pkgdir/usr/bin/fern"
}
