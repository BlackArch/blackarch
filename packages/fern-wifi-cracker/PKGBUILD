# This file is part of BlackArch Linux ( https://www.blackarch.org/ ).
# See COPYING for license details.

pkgname=fern-wifi-cracker
pkgver=289.0da03ab
pkgrel=3
groups=('blackarch' 'blackarch-cracker' 'blackarch-wireless')
pkgdesc='WEP, WPA wifi cracker for wireless penetration testing'
arch=('any')
url='http://code.google.com/p/fern-wifi-cracker/'
license=('GPL2')
depends=('python' 'python-pyqt5' 'aircrack-ng' 'xterm' 'subversion'
         'python-scapy' 'macchanger')
makedepends=('git')
_svnmod='fern-wifi-cracker'
source=("git+https://github.com/savio-code/$pkgname.git"
        'remove-update.patch'
        "$pkgname.install")
sha512sums=('SKIP'
            '16b33ac75e72c8dd0ce4710ae2bf5d897026732c259afa53b95951447a9d909ced2d0423fdf4317bcbdd461ef6dc86d594f202e9d989e09e44c52f3b6eda2456'
            'f467e58a7b96c17d36ee423b89347ef96eb8c984ec26dcf36857a89a629ae8f8137c60b9111720c5e73c8bf80504b422c21bbec7783acb4f55661278c4414ed2')
install="$pkgname.install"

pkgver() {
  cd $pkgname

  echo $(git rev-list --count HEAD).$(git rev-parse --short HEAD)
}

prepare() {
  cd "$pkgname/Fern-Wifi-Cracker"

  patch -p2 < ../../remove-update.patch
}

package() {
  cd "$pkgname/Fern-Wifi-Cracker"

  install -dm 755 "$pkgdir/usr/bin"
  install -dm 755 "$pkgdir/usr/share/$pkgname/Settings"

  cp -a --no-preserve=ownership * "$pkgdir/usr/share/$pkgname"

  cat > "$pkgdir/usr/bin/$pkgname" << EOF
#!/bin/sh
cd /usr/share/$pkgname
exec python ./execute.py "\$@"
EOF

  chmod +x "$pkgdir/usr/bin/$pkgname"
}

