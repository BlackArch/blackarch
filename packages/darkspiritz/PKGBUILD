# This file is part of BlackArch Linux ( http://blackarch.org ).
# See COPYING for license details.

pkgname=darkspiritz
_pkgname=DarkSpiritz
pkgver=2.0
pkgrel=1
pkgdesc='A penetration testing framework for Linux, MacOS, and Windows systems.'
arch=('any')
groups=('blackarch' 'blackarch-exploitation' 'blackarch-automation')
url='https://github.com/DarkSpiritz/DarkSpiritz'
license=('MIT')
depends=('python' 'impacket' 'python-pysmb' 'python-pycryptodome' 'python-paramiko' 'python-scapy'
         'python-whois' 'python-requests' 'python-terminaltables')
makedepends=()
source=("https://github.com/$_pkgname/$_pkgname/archive/$pkgver.tar.gz")
sha512sums=('f6da05c63b28ab56c48cbe8fd6b95cb920c4a6d78c5d001df77a8b10b0ada611028c3c9932784dc97a71c83c4350a740cf81032c62b1d11593b983b7cba26e30')

package() {
  cd "$_pkgname-$pkgver"

  install -dm 755 "$pkgdir/usr/share/$pkgname"
  install -dm 755 "$pkgdir/usr/bin"

  install -Dm 644 README.md "$pkgdir/usr/share/doc/$pkgname/README"
  install -Dm 644 LICENSE "$pkgdir/usr/share/licenses/$pkgname/LICENSE"

  cp -a --no-preserve=ownership * "$pkgdir/usr/share/$pkgname"

  cat > "$pkgdir/usr/bin/$pkgname" << EOF
#!/bin/sh
cd /usr/share/$pkgname
exec python start.py "\$@"
EOF

chmod +x "$pkgdir/usr/bin/$pkgname"

}

