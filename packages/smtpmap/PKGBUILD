# This file is part of BlackArch Linux ( http://blackarch.org ).
# See COPYING for license details.

pkgname='smtpmap'
pkgver='0.8.234_BETA'
pkgrel=4
groups=('blackarch' 'blackarch-fingerprint')
pkgdesc='Tool to identify the running smtp software on a given host.'
url='http://www.projectiwear.org/~plasmahh/software.html'
arch=('x86_64' 'armv6h' 'armv7h' 'aarch64')
license=('GPL')
depends=('libpcap')
source=("http://www.projectiwear.org/~plasmahh/smtpmap-${pkgver/_/-}.tar.bz2"
        "gcc-3.4.patch"
        "smtpmap-64bit.patch")
sha1sums=('0efd069cb1257e9603f5e8571e5c35ed103b8a46'
          '0acb55a6f4619ebda44b51f26deaa35d9d84d8a0'
          '27bbea58b2440e41b63bdb98edba181e15fc48e0')

prepare() {
  cd "$srcdir/smtpmap-${pkgver/_/-}"

  patch -Np1 < "$srcdir/gcc-3.4.patch"
  patch -Np0 < "$srcdir/smtpmap-64bit.patch"

  sed -i "s|-O -Wall|$CFLAGS|g" makefile.conf
}

build() {
  cd "$srcdir/smtpmap-${pkgver/_/-}"

  ./configure --prefix=/usr
  make
}

package() {
  cd "$srcdir/smtpmap-${pkgver/_/-}"

  install -Dm755 "src/smtpmap" "$pkgdir/usr/bin/smtmap"
  install -Dm644 "COPYING" "$pkgdir/usr/share/licenses/smtpmap/COPYING"

  mkdir -p "$pkgdir/usr/share/smtpmap"
  cp share/* "$pkgdir/usr/share/smtpmap/"

  mkdir -p "$pkgdir/usr/share/doc/smtpmap/"
  cp ChangeLog README TODO "$pkgdir/usr/share/doc/smtpmap/"
}
