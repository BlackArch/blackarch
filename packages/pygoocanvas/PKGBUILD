# This file is part of BlackArch Linux ( https://www.blackarch.org/ ).
# See COPYING for license details.

pkgname=pygoocanvas
pkgver=0.14.1
pkgrel=1
pkgdesc='GooCanvas Python bindings.'
arch=('x86_64' 'aarch64')
url='https://wiki.gnome.org/Projects/PyGoocanvas'
license=('LGPL')
depends=('python2' 'goocanvas1' 'pygtk')
makedepends=('pkgconfig' 'git' 'gtk-doc')
options=('docs')
source=('git+https://github.com/dnut/pygoocanvas.git')
sha512sums=('SKIP')

prepare() {
  cd $pkgname

  ./autogen.sh
}

build() {
  cd $pkgname

  ./configure --prefix=/usr --disable-docs

  make
}

package() {
  make -C "$pkgname" DESTDIR="$pkgdir" install
}

