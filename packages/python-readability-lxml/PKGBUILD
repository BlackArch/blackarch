# This file is part of BlackArch Linux ( https://www.blackarch.org/ ).
# See COPYING for license details.

pkgbase=python-readability-lxml
pkgname=('python2-readability-lxml' 'python-readability-lxml')
_pkgname=readability-lxml
pkgver=0.8.1
pkgrel=2
pkgdesc='Fast html to text parser (article readability tool) with python3 support.'
arch=('any')
url='https://pypi.org/project/readability-lxml/#files'
license=('APACHE')
makedepends=('python2-setuptools' 'python-setuptools')
source=("https://files.pythonhosted.org/packages/b9/62/6de3a9a8524c1a1ee0f2aee0dfbad13a36ebbca0db402abcf4e790496512/$_pkgname-$pkgver.tar.gz")
sha512sums=('e975116c8e34d7395e22acab2257dc9e90d75835317deadab6fb4bfd8aea5a8af9eacaabf26e96230665006dee63a0e7dd2afceba480da4d5f31b2b3bf7129c2')

prepare() {
  cp -a "$_pkgname-$pkgver"{,-2}
}

build() {
  cd "$_pkgname-$pkgver-2"

  python2 setup.py build

  cd "$srcdir/$_pkgname-$pkgver"

  python setup.py build
}

package_python2-readability-lxml() {
  depends=('python2' 'python2-lxml')

  cd "$_pkgname-$pkgver-2"

  python2 setup.py install --prefix=/usr --root="$pkgdir" --optimize=1 \
    --skip-build
}

package_python-readability-lxml() {
  depends=('python' 'python-lxml')

  cd "$_pkgname-$pkgver"

  python setup.py install --prefix=/usr --root="$pkgdir" --optimize=1 \
    --skip-build
}

