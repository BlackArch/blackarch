# This file is part of BlackArch Linux ( https://www.blackarch.org/ ).
# See COPYING for license details.

pkgbase=python-readability-lxml
pkgname=('python2-readability-lxml' 'python-readability-lxml')
_pkgname=readability-lxml
pkgver=0.7.1
pkgrel=2
pkgdesc='Fast html to text parser (article readability tool) with python3 support.'
arch=('any')
url='https://pypi.org/project/readability-lxml/#files'
license=('APACHE')
makedepends=('python2-setuptools' 'python-setuptools')
source=("https://files.pythonhosted.org/packages/af/a7/8ea52b2d3de4a95c3ed8255077618435546386e35af8969744c0fa82d0d6/$_pkgname-$pkgver.tar.gz")
sha512sums=('8bdbd8d96f405a2a07a6f6ca4e724b037906eda2e98a1cf7086f84125d282d9bd9f7422d28560e33800f1af87d215ba02ebde8fa6355f824670ea17d8194a810')

prepare() {
  cp -a "$_pkgname-$pkgver"{,-2}
}

build() {
  cd "$_pkgname-$pkgver-2"

  python2 setup.py build

  cd "$srcdir/$_pkgname-$pkgver"

  python setup.py build
}

package_python2-readability-lxml() {
  depends=('python2' 'python2-lxml')

  cd "$_pkgname-$pkgver-2"

  python2 setup.py install --prefix=/usr --root="$pkgdir" --optimize=1 \
    --skip-build
}

package_python-readability-lxml() {
  depends=('python' 'python-lxml')

  cd "$_pkgname-$pkgver"

  python setup.py install --prefix=/usr --root="$pkgdir" --optimize=1 \
    --skip-build
}

