# This file is part of BlackArch Linux ( http://blackarch.org ).
# See COPYING for license details.

pkgname=pidense
pkgver=29.ef26704
pkgrel=1
pkgdesc='Monitor illegal wireless network activities. (Fake Access Points)'
groups=('blackarch' 'blackarch-wireless' 'blackarch-defensive')
arch=('any')
url='https://github.com/WiPi-Hunter/PiDense'
license=('GPL3')
depends=('python2' 'python2-argparse' 'python2-termcolor' 'python2-scapy')
makedepends=('git')
source=('git+https://github.com/WiPi-Hunter/PiDense.git')
sha512sums=('SKIP')

pkgver() {
  cd "$srcdir/PiDense"

  echo $(git rev-list --count HEAD).$(git rev-parse --short HEAD)
}

package() {
  cd "$srcdir/PiDense"

  install -dm 755 "$pkgdir/usr/bin"
  install -dm 755 "$pkgdir/usr/share/pidense"

  install -Dm644 -t "$pkgdir/usr/share/doc/pidense/" README.md
  install -Dm644 LICENCE "$pkgdir/usr/share/licenses/pidense/LICENSE"

  rm README.md LICENCE

  cp -a * "$pkgdir/usr/share/pidense/"

  cat > "$pkgdir/usr/bin/pidense" << EOF
#!/bin/sh
cd /usr/share/pidense
exec python2 pidense.py "\${@}"
EOF

  chmod a+x "$pkgdir/usr/bin/pidense"
}
