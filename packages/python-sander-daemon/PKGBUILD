# This file is part of BlackArch Linux ( https://www.blackarch.org/ ).
# See COPYING for license details.

pkgbase=python-sander-daemon
pkgname=('python2-sander-daemon' 'python-sander-daemon')
_pkgname=sander-daemon
pkgver=1.0.0
pkgrel=1
pkgdesc='Jejik daemon class improved by Server Density.'
arch=('any')
url='https://pypi.org/project/sander-daemon/#files'
license=('APACHE')
makedepends=('python2-setuptools' 'python-setuptools')
source=("https://files.pythonhosted.org/packages/5d/a2/cc1b5734ddb40e7dd71e731073902a7d4cac8c71fa324fff4f82c2ad5cdc/$_pkgname-$pkgver.tar.gz")
sha512sums=('3e6cb362f875a21cb7f804ebc18fedb4ca1314c7cf2568d04ee282b82cbd05172b936f2632def4b2c1cd66fdbc57c7ca049b06cb244b5d2b42ad282dbb499ec4')

prepare() {
  cp -a "$_pkgname-$pkgver"{,-2}
}

build() {
  cd "$_pkgname-$pkgver-2"

  python2 setup.py build

  cd "$srcdir/$_pkgname-$pkgver"

  python setup.py build
}

package_python2-sander-daemon() {
  depends=('python2')

  cd "$_pkgname-$pkgver-2"

  python2 setup.py install --prefix=/usr --root="$pkgdir" --optimize=1 \
    --skip-build
}

package_python-sander-daemon() {
  depends=('python')

  cd "$_pkgname-$pkgver"

  python setup.py install --prefix=/usr --root="$pkgdir" --optimize=1 \
    --skip-build
}

