# This file is part of BlackArch Linux ( https://www.blackarch.org/ ).
# See COPYING for license details.

pkgbase=python-esprima
pkgname=('python2-esprima' 'python-esprima')
pkgver=4.0.1
pkgrel=3
pkgdesc='ECMAScript parsing infrastructure for multipurpose analysis in Python.'
arch=('any')
url='https://pypi.org/project/esprima/#files'
license=('BSD')
makedepends=('python2-setuptools' 'python-setuptools')
options=(!emptydirs)
source=("https://files.pythonhosted.org/packages/cc/a1/50fccd68a12bcfc27adfc9969c090286670a9109a0259f3f70943390b721/esprima-$pkgver.tar.gz")
sha512sums=('c7e21739ef75c5e8e75ecbe8b8f63ca26e9fbf58fc5753c86913c4ee65a8be9b07b2192c1db035bc3ed375c6f7a6b3fd9ae13a53ec38dcf542e65b40b2ec5b2c')

prepare() {
  cp -a "esprima-$pkgver" "esprima2-$pkgver"
}

build() {
  cd "esprima-$pkgver"

  python setup.py build

  cd "$srcdir/esprima2-$pkgver"

  python2 setup.py build
}

package_python2-esprima() {
  depends=('python')

  cd "esprima2-$pkgver"

  python2 setup.py install --prefix=/usr --root="$pkgdir" --optimize=1 \
    --skip-build

  mv "$pkgdir/usr/bin/esprima" "$pkgdir/usr/bin/esprima2"
}

package_python-esprima() {
  depends=('python')

  cd "esprima-$pkgver"

  python setup.py install --prefix=/usr --root="$pkgdir" --optimize=1 \
    --skip-build
}

