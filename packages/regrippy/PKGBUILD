# This file is part of BlackArch Linux ( https://www.blackarch.org/ ).
# See COPYING for license details.

pkgname=regrippy
_pkgname=regrippy
pkgver=1.0.1
pkgrel=2
pkgdesc='Framework for reading and extracting useful forensics data from Windows registry hives.'
groups=('blackarch' 'blackarch-forensic')
arch=('any')
url='https://pypi.org/project/regrippy/#files'
license=('Apache')
depends=('python' 'python-python-registry' 'python-wheel')
makedepends=('python-setuptools')
source=("https://files.pythonhosted.org/packages/99/c0/411d7d22811b2bace744ed6cbc0b13c5e1d989128e89910382a83f011b18/$_pkgname-$pkgver.tar.gz")
sha512sums=('9c8ae41514b036a7fb2c0262c6be1ae4a7705a9cf2f454aadf861b026f7686de0948206ca7a94bf530e3f797b3713ec11232da26585d58921d61ef2eab2e9f5e')

build() {
  cd "$_pkgname-$pkgver"

  python setup.py build
}

package() {
  cd "$_pkgname-$pkgver"

  python setup.py install --prefix=/usr --root="$pkgdir" -O1 --skip-build

  mv "$pkgdir/usr/bin/regrip.py" "$pkgdir/usr/bin/regrip"

  for i in "$pkgdir/usr/bin/"*; do
    if [ "$(echo $i| awk -F/ '{print $NF}')" != "regrip" ]; then
      mv $i "$(echo ${i//_/-})"
    fi
  done
}

