# This file is part of BlackArch Linux ( https://www.blackarch.org/ ).
# See COPYING for license details.

pkgbase=python-branca
pkgname=('python2-branca' 'python-branca')
_pkgname=branca
pkgver=0.4.0
pkgrel=1
pkgdesc='Spinoff from folium, that would host the non-map-specific features.'
arch=('any')
url='https://pypi.org/project/branca/#files'
license=('MIT')
makedepends=('python2-setuptools' 'python-setuptools')
source=("https://files.pythonhosted.org/packages/37/29/2e8c0c9caf7e7da1c1fbb3f58d7cf4398760887e7f59cb3850d645d067ce/$_pkgname-$pkgver.tar.gz")
sha512sums=('52523fbd56819aecb0afc3f6b3e8aab07ae47b923817d7cfabe8ebfbca0dd6a172516464355914755ea28d809583a2d92d19248936042a126a171b81d45967ac')

prepare() {
  cp -a "$_pkgname-$pkgver"{,-2}
}

build() {
  cd "$_pkgname-$pkgver-2"

  python2 setup.py build

  cd "$srcdir/$_pkgname-$pkgver"

  python setup.py build
}

package_python2-branca() {
  depends=('python2-jinja' 'python2-six')

  cd "$_pkgname-$pkgver-2"

  python2 setup.py install --prefix=/usr --root="$pkgdir" --optimize=1 \
    --skip-build
}

package_python-branca() {
  depends=('python-jinja' 'python-six')

  cd "$_pkgname-$pkgver"

  python setup.py install --prefix=/usr --root="$pkgdir" --optimize=1 \
    --skip-build
}

