# This file is part of BlackArch Linux ( https://www.blackarch.org/ ).
# See COPYING for license details.

pkgbase=python-branca
pkgname=('python2-branca' 'python-branca')
_pkgname=branca
pkgver=0.4.1
pkgrel=2
pkgdesc='Spinoff from folium, that would host the non-map-specific features.'
arch=('any')
url='https://pypi.org/project/branca/#files'
license=('MIT')
makedepends=('python2-setuptools' 'python-setuptools')
source=("https://files.pythonhosted.org/packages/41/6d/7da2a773e6833b5498cbc4d64e26f77d473084b6cdf74cb0d31b4718a000/$_pkgname-$pkgver.tar.gz")
sha512sums=('b4993e77e5070e8a6915b9e016e895340e6e3029c204c24f742b136c1781d5a2e59cf4802e206efcd1b5384583c9e2543f2112c0c5f368cda5fc46ea77371b7c')

prepare() {
  cp -a "$_pkgname-$pkgver"{,-2}
}

build() {
  cd "$_pkgname-$pkgver-2"

  python2 setup.py build

  cd "$srcdir/$_pkgname-$pkgver"

  python setup.py build
}

package_python2-branca() {
  depends=('python2-jinja' 'python2-six')

  cd "$_pkgname-$pkgver-2"

  python2 setup.py install --prefix=/usr --root="$pkgdir" --optimize=1 \
    --skip-build
}

package_python-branca() {
  depends=('python-jinja' 'python-six')

  cd "$_pkgname-$pkgver"

  python setup.py install --prefix=/usr --root="$pkgdir" --optimize=1 \
    --skip-build
}

