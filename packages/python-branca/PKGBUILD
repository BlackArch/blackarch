# This file is part of BlackArch Linux ( https://www.blackarch.org/ ).
# See COPYING for license details.

pkgbase=python-branca
pkgname=('python2-branca' 'python-branca')
_pkgname=branca
pkgver=0.3.1
pkgrel=1
pkgdesc='Spinoff from folium, that would host the non-map-specific features.'
arch=('any')
url='https://pypi.org/project/branca/#files'
license=('MIT')
makedepends=('python2-setuptools' 'python-setuptools')
source=("https://files.pythonhosted.org/packages/9a/89/1cb3c27d82755dac6443f7ba5f8b7e328518862ec441e6ce63caf9e6ac80/$_pkgname-$pkgver.tar.gz")
sha512sums=('d2a9e24b56bc2a99e5b967a7100336d15b53091cede78d689364bc333d86912011798d40172c2dcf447fef4a1552b19010356415a13bd8d4e3a0d874bab540bb')

prepare() {
  cp -a "$_pkgname-$pkgver" "$_pkgname-$pkgver-2"
}

build() {
  cd "$_pkgname-$pkgver-2"

  python2 setup.py build

  cd "$srcdir/$_pkgname-$pkgver"

  python setup.py build
}

package_python2-branca() {
  depends=('python2-jinja' 'python2-six')

  cd "$_pkgname-$pkgver-2"

  python2 setup.py install --prefix=/usr --root="$pkgdir" --optimize=1 \
    --skip-build
}

package_python-branca() {
  depends=('python-jinja' 'python-six')

  cd "$_pkgname-$pkgver"

  python setup.py install --prefix=/usr --root="$pkgdir" --optimize=1 \
    --skip-build
}

