# This file is part of BlackArch Linux ( https://www.blackarch.org/ ).
# See COPYING for license details.

pkgname=cloudmare
pkgver=56.baf01dc
pkgrel=2
pkgdesc='A simple tool to find origin servers of websites protected by CloudFlare with a misconfiguration DNS.'
groups=('blackarch' 'blackarch-recon' 'blackarch-scanner')
arch=('any')
url='https://github.com/MrH0wl/Cloudmare'
license=('GPL3')
depends=('python' 'python-pip' 'python-certifi' 'python-lxml'
         'python-beautifulsoup4')
makedepends=('git' 'dos2unix')
source=("$pkgname::git+https://github.com/MrH0wl/Cloudmare.git")
sha512sums=('SKIP')

pkgver() {
  cd $pkgname

  echo $(git rev-list --count HEAD).$(git rev-parse --short HEAD)
}

prepare() {
  cd $pkgname

  dos2unix Cloudmare.py
}

package() {
  cd $pkgname

  install -dm 755 "$pkgdir/usr/bin"
  install -dm 755 "$pkgdir/usr/share/$pkgname"

  install -Dm 644 -t "$pkgdir/usr/share/doc/$pkgname/" README.md
  install -Dm 644 LICENSE "$pkgdir/usr/share/licenses/$pkgname/LICENSE"

  rm README.md LICENSE

  cp -a * "$pkgdir/usr/share/$pkgname/"

  cat > "$pkgdir/usr/bin/$pkgname" << EOF
#!/bin/sh
cd /usr/share/$pkgname
exec python3 Cloudmare.py "\$@"
EOF

  chmod +x "$pkgdir/usr/bin/$pkgname"
}

