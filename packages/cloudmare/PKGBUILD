# This file is part of BlackArch Linux ( https://www.blackarch.org/ ).
# See COPYING for license details.

pkgname=cloudmare
pkgver=40.1cc4773
pkgrel=1
pkgdesc='A simple tool to find origin servers of websites protected by CloudFlare with a misconfiguration DNS.'
groups=('blackarch' 'blackarch-recon' 'blackarch-scanner')
arch=('any')
url='https://github.com/MrH0wl/Cloudmare'
license=('GPL3')
depends=('python2' 'python2-colorama' 'python2-dnspython' 'python2-requests'
         'python2-urllib3' 'python2-html-similarity' 'punter')
makedepends=('git' 'dos2unix')
source=("$pkgname::git+https://github.com/MrH0wl/Cloudmare.git")
sha512sums=('SKIP')

pkgver() {
  cd $pkgname

  echo $(git rev-list --count HEAD).$(git rev-parse --short HEAD)
}

prepare() {
  cd $pkgname

  dos2unix Cloudmare.py

  sed -i 's|/bin/env python|/bin/env python2|g' Cloudmare.py
}

package() {
  cd $pkgname

  install -Dm 755 Cloudmare.py "$pkgdir/usr/bin/$pkgname"
  install -Dm 644 -t "$pkgdir/usr/share/doc/$pkgname/" README.md
  install -Dm 644 LICENSE "$pkgdir/usr/share/licenses/$pkgname/LICENSE"
}

