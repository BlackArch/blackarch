# This file is part of BlackArch Linux ( http://blackarch.org ).
# See COPYING for license details.

pkgname=chiron
pkgver=0.9.0.1
pkgrel=3
groups=('blackarch' 'blackarch-scanner' 'blackarch-networking'
        'blackarch-exploitation' 'blackarch-proxy')
pkgdesc='An all-in-one IPv6 Penetration Testing Framework.'
arch=('any')
url='http://www.secfu.net/tools-scripts/'
license=('GPL2')
depends=('python2' 'python2-scapy')
source=("http://www.secfu.net/app/download/10788790797/Chiron_${pkgver}.tar.gz")
sha512sums=('a5c19ffe3ee0a4ec3ac337820986e4d60a62824aa7e2a6e24a7011aa5ab8c983c89afa72eae337aa195e6f4892c2d175a0695e39f108c0586f2e56d35e021a80')


package() {
  cd "$srcdir/Chiron_$pkgver"

  install -dm 755 "$pkgdir/usr/bin"
  install -dm 755 "$pkgdir/usr/share/chiron"

  install -Dm644 licence.txt "$pkgdir/usr/share/licenses/chiron/license.txt"

  rm licence.txt

  cp -a bin/* lib/* files/ "$pkgdir/usr/share/chiron/"
  ln -s files/* "$pkgdir/usr/share/chiron/."

  local tools="chiron-combinations chiron-nd chiron-proxy chiron-scanner"
  local tools="$tools chiron-attacks chiron-local-link"

  for tool in ${tools}
  do
    cat > "$pkgdir/usr/bin/$tool" << EOF
#!/bin/sh
cd /usr/share/chiron
exec python2 ${tool/-/_}.py "\$@"
EOF
    chmod +x "$pkgdir/usr/bin/$tool"
  done

  chmod 755 "$pkgdir/usr/share/chiron/"*.py
}
