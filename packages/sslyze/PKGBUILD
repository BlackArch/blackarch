# This file is part of BlackArch Linux ( https://www.blackarch.org/ ).
# See COPYING for license details.

pkgname=sslyze
pkgver=6.0.0
_pyver=3.12
pkgrel=1
groups=('blackarch' 'blackarch-crypto' 'blackarch-scanner')
pkgdesc='Python tool for analyzing the configuration of SSL servers and for identifying misconfigurations.'
arch=('x86_64' 'aarch64')
url='https://github.com/nabla-c0d3/sslyze'
license=('GPL2')
depends=('python' 'python-tls-parser' 'python-cryptography' 'python-nassl'
         'python-typing_extensions' 'python-pydantic' 'python-pyopenssl')
makedepends=('python-build' 'python-pip')
source=("https://github.com/nabla-c0d3/$pkgname/archive/$pkgver.zip")
sha512sums=('d5aeab1ba42ede7597adad0c02386a35f91c4b6eae24ba5658297f891c62c328f7e567e84a90501517250b2cd2f88b7192349761c2e150bd234919a07b1fbf37')

build() {
  cd "$pkgname-$pkgver"

  python -m build --wheel --outdir="$startdir/dist"
}

prepare() {
  cd "$pkgname-$pkgver"

  sed -i 's|pydantic>=1.7,<1.10|pydantic|g' setup.py
}

package() {
  cd "$pkgname-$pkgver"

  pip install \
    --verbose \
    --disable-pip-version-check \
    --no-warn-script-location \
    --ignore-installed \
    --no-compile \
    --no-deps \
    --root="$pkgdir" \
    --prefix=/usr \
    --no-index \
    --find-links="file://$startdir/dist" \
    "$pkgname"

  install -Dm 644 LICENSE.txt "$pkgdir/usr/share/licenses/$pkgname/LICENSE"

  rm -rf "$pkgdir/usr/lib/python$_pyver/site-packages/tests"

  sed -i "s/sslyze==$pkgver/sslyze/" "$pkgdir/usr/bin/$pkgname"
}

