# This file is part of BlackArch Linux ( https://www.blackarch.org/ ).
# See COPYING for license details.

pkgname=sslyze
pkgver=6.3.0
_pyver=3.14
pkgrel=1
groups=('blackarch' 'blackarch-crypto' 'blackarch-scanner')
pkgdesc='Python tool for analyzing the configuration of SSL servers and for identifying misconfigurations.'
arch=('x86_64' 'aarch64')
url='https://github.com/nabla-c0d3/sslyze'
license=('AGPL-3.0-or-later')
depends=('python' 'python-tls-parser' 'python-cryptography' 'python-nassl'
         'python-typing_extensions' 'python-pydantic' 'python-pyopenssl')
makedepends=('python-build' 'python-installer' 'python-setuptools'
             'python-wheel')
source=("https://github.com/nabla-c0d3/$pkgname/archive/$pkgver.zip")
sha512sums=('1beec53cbc7aa4651a5e500eb62311be784a8e4c3dc02b0c0857c40805c1fcb4d3bb2821a62311086454319acf23c44b9baf31cb7ad83cb1adde7d1640403c1f')

prepare() {
  cd "$pkgname-$pkgver"

  sed -i 's|pydantic>=1.7,<1.10|pydantic|g' setup.py
}

build() {
  cd "$pkgname-$pkgver"

  python -m build --wheel --no-isolation
}

package() {
  cd "$pkgname-$pkgver"

  python -m installer --destdir="$pkgdir" dist/*.whl

  install -Dm 644 LICENSE.txt "$pkgdir/usr/share/licenses/$pkgname/LICENSE"

  rm -rf "$pkgdir/usr/lib/python$_pyver/site-packages/tests"

  sed -i "s/sslyze==$pkgver/sslyze/" "$pkgdir/usr/bin/$pkgname"
}

