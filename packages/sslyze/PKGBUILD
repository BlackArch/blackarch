# This file is part of BlackArch Linux ( https://www.blackarch.org/ ).
# See COPYING for license details.

pkgname=sslyze
pkgver=6.2.0
_pyver=3.13
pkgrel=1
groups=('blackarch' 'blackarch-crypto' 'blackarch-scanner')
pkgdesc='Python tool for analyzing the configuration of SSL servers and for identifying misconfigurations.'
arch=('x86_64' 'aarch64')
url='https://github.com/nabla-c0d3/sslyze'
license=('AGPL-3.0-or-later')
depends=('python' 'python-tls-parser' 'python-cryptography' 'python-nassl'
         'python-typing_extensions' 'python-pydantic' 'python-pyopenssl')
makedepends=('python-build' 'python-pip')
source=("https://github.com/nabla-c0d3/$pkgname/archive/$pkgver.zip")
sha512sums=('c20024ab4085b68f0587561bd192f6b3e78bed0507abde924fdbc1afe4e295cd644b637b4b6a0d2efdc80396ec2e7a8e473cb1d5a98dffac62b540c29115809c')

build() {
  cd "$pkgname-$pkgver"

  python -m build --wheel --outdir="$startdir/dist"
}

prepare() {
  cd "$pkgname-$pkgver"

  sed -i 's|pydantic>=1.7,<1.10|pydantic|g' setup.py
}

package() {
  cd "$pkgname-$pkgver"

  pip install \
    --verbose \
    --disable-pip-version-check \
    --no-warn-script-location \
    --ignore-installed \
    --no-compile \
    --no-deps \
    --root="$pkgdir" \
    --prefix=/usr \
    --no-index \
    --find-links="file://$startdir/dist" \
    "$pkgname"

  install -Dm 644 LICENSE.txt "$pkgdir/usr/share/licenses/$pkgname/LICENSE"

  rm -rf "$pkgdir/usr/lib/python$_pyver/site-packages/tests"

  sed -i "s/sslyze==$pkgver/sslyze/" "$pkgdir/usr/bin/$pkgname"
}

