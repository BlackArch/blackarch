# This file is part of BlackArch Linux ( https://www.blackarch.org/ ).
# See COPYING for license details.

pkgname=sslyze
pkgver=2.1.3
pkgrel=1
groups=('blackarch' 'blackarch-crypto' 'blackarch-scanner')
pkgdesc='Python tool for analyzing the configuration of SSL servers and for identifying misconfigurations.'
arch=('x86_64' 'aarch64')
url='https://github.com/nabla-c0d3/sslyze'
license=('GPL2')
depends=('python' 'python-tls-parser' 'python-cryptography' 'python-nassl')
makedepends=('python-setuptools')
source=("https://github.com/nabla-c0d3/sslyze/archive/${pkgver}.zip"
        'setup.py.patch')
sha512sums=('f39581dea954974088942621abb79c072001d390a82e3785086009d1894fef678616b8186c6dd6e344ce83238796c08047d8899a32377fb3016c0fdd5f8fdf74'
            '58fb167d0b822426ca84238e26f1a362ceb4366fdf13621670808e473db1ffd34b6342b42eb82ff9f7aa9e4388a83fa3c2318db183e397186b03db2a0fa2a6d9')

prepare() {
  patch -p1 < setup.py.patch
}

build() {
  cd "$pkgname-$pkgver"

  python setup.py build
}

package() {
  cd "$pkgname-$pkgver"

  python setup.py install --root="$pkgdir" --optimize=1 --skip-build

  install -Dm 644 LICENSE.txt "$pkgdir/usr/share/licenses/$pkgname/LICENSE"
}

