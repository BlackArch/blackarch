# This file is part of BlackArch Linux ( https://www.blackarch.org/ ).
# See COPYING for license details.

pkgname=sslyze
pkgver=3.0.7
pkgrel=1
groups=('blackarch' 'blackarch-crypto' 'blackarch-scanner')
pkgdesc='Python tool for analyzing the configuration of SSL servers and for identifying misconfigurations.'
arch=('x86_64' 'aarch64')
url='https://github.com/nabla-c0d3/sslyze'
license=('GPL2')
depends=('python' 'python-tls-parser' 'python-cryptography' 'python-nassl'
         'python-typing_extensions')
makedepends=('python-setuptools')
source=("https://github.com/nabla-c0d3/$pkgname/archive/$pkgver.zip"
        'setup.py.patch')
sha512sums=('7ebeeda71f297e3d9b7708d78c6de35a90836e614b99ffa9b846d4c9f184e32c3063749f0a12365ce5e39e174842e505242112de8e8f70ddfce759a7805744c5'
            '738972735afb19d103c06d69d7a80a6cdcb998a5ac272c1db15ea502be8bbd2358146d0d170cf76b92807d5fc9db763304984d3d7b9712d501abbbf4b979059a')

prepare() {
  patch -p1 < setup.py.patch
}

build() {
  cd "$pkgname-$pkgver"

  python setup.py build
}

package() {
  cd "$pkgname-$pkgver"

  python setup.py install --root="$pkgdir" --prefix=/usr -O1 --skip-build

  install -Dm 644 LICENSE.txt "$pkgdir/usr/share/licenses/$pkgname/LICENSE"

  rm -rf "$pkgdir/usr/lib/python3.8/site-packages/tests"
}

