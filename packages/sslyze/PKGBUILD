# This file is part of BlackArch Linux ( http://blackarch.org ).
# See COPYING for license details.

pkgname='sslyze'
pkgver='1.1.1'
pkgrel=1
groups=('blackarch' 'blackarch-misc')
pkgdesc='Python tool for analyzing the configuration of SSL servers and for identifying misconfigurations.'
arch=('any')
url='https://github.com/nabla-c0d3/sslyze/'
license=('GPL2')
depends=('python2' 'python2-nassl' 'python2-lxml' 'python2-typing')
makedepends=('python2-setuptools')
source=("https://github.com/nabla-c0d3/sslyze/archive/${pkgver}.tar.gz")
sha1sums=('c484e876bf032cedf881bdb7a45c7168be6bff04')

build() {
  cd "$srcdir/sslyze-$pkgver"

  python2 setup.py build
}

package() {
  cd "$srcdir/sslyze-$pkgver"

  python2 setup.py install --root="$pkgdir" --optimize=1

  #mv "$pkgdir/usr/bin/sslyze_cli.py" "$pkgdir/usr/bin/sslyze"

  install -Dm644 README.md "$pkgdir/usr/share/doc/sslyze/README.md"
  install -Dm644 LICENSE.txt "$pkgdir/usr/share/licenses/sslyze/LICENSE.txt"
}
