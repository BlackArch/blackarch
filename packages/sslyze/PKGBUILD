# This file is part of BlackArch Linux ( https://www.blackarch.org/ ).
# See COPYING for license details.

pkgname=sslyze
pkgver=5.0.6
_pyver=3.10
pkgrel=3
groups=('blackarch' 'blackarch-crypto' 'blackarch-scanner')
pkgdesc='Python tool for analyzing the configuration of SSL servers and for identifying misconfigurations.'
arch=('x86_64' 'aarch64')
url='https://github.com/nabla-c0d3/sslyze'
license=('GPL2')
depends=('python' 'python-tls-parser' 'python-cryptography' 'python-nassl'
         'python-typing_extensions' 'python-pydantic')
makedepends=('python-setuptools')
source=("https://github.com/nabla-c0d3/$pkgname/archive/$pkgver.zip")
sha512sums=('a8108d5bd3f6106a3e1fd8691b21419c9f4e27324f0801287cc58da671b9a9bfdb3273a0051f01e520529ff5cf196358d5f3e3ef6647ab80814083c6b6ea9d79')

build() {
  cd "$pkgname-$pkgver"

  python setup.py build
}

prepare() {
  cd "$pkgname-$pkgver"

  sed -i 's|pydantic>=1.7,<1.10|pydantic|g' setup.py
}

package() {
  cd "$pkgname-$pkgver"

  python setup.py install --root="$pkgdir" --prefix=/usr -O1 --skip-build

  install -Dm 644 LICENSE.txt "$pkgdir/usr/share/licenses/$pkgname/LICENSE"

  rm -rf "$pkgdir/usr/lib/python$_pyver/site-packages/tests"

  sed -i "s/sslyze==$pkgver/sslyze/" "$pkgdir/usr/bin/$pkgname"
}

