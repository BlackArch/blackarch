# This file is part of BlackArch Linux ( https://www.blackarch.org/ ).
# See COPYING for license details.

pkgname=sslyze
pkgver=3.0.5
pkgrel=1
groups=('blackarch' 'blackarch-crypto' 'blackarch-scanner')
pkgdesc='Python tool for analyzing the configuration of SSL servers and for identifying misconfigurations.'
arch=('x86_64' 'aarch64')
url='https://github.com/nabla-c0d3/sslyze'
license=('GPL2')
depends=('python' 'python-tls-parser' 'python-cryptography' 'python-nassl'
         'python-typing_extensions')
makedepends=('python-setuptools')
source=("https://github.com/nabla-c0d3/$pkgname/archive/$pkgver.zip"
        'setup.py.patch')
sha512sums=('7f23985ba5d838cc1bd694e4bfe16f58727fe23f78186d00a55a1106d5aa3a250a058435a427d17ccd9dbf6c7588150acb97f65ee2880707a4be7596e4fc8615'
            '8efff9b74c31b50e8bad71e1f515ebc8cdcf4e0f1df88c516f9b1ecb3e0d78bd0ec7b1983da5a2786664fa109a99726f18886d3c45cd7e57bb695129532241eb')

prepare() {
  patch -p1 < setup.py.patch
}

build() {
  cd "$pkgname-$pkgver"

  python setup.py build
}

package() {
  cd "$pkgname-$pkgver"

  python setup.py install --root="$pkgdir" --prefix=/usr -O1 --skip-build

  install -Dm 644 LICENSE.txt "$pkgdir/usr/share/licenses/$pkgname/LICENSE"

  rm -rf "$pkgdir/usr/lib/python3.8/site-packages/tests/openssl_server"
}

