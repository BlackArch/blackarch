# This file is part of BlackArch Linux ( https://www.blackarch.org/ ).
# See COPYING for license details.

pkgname=shootback
pkgver=72.86f0c1d
pkgrel=1
pkgdesc='A reverse TCP tunnel let you access target behind NAT or firewall.'
groups=('blackarch' 'blackarch-backdoor' 'blackarch-tunnel')
arch=('any')
url='https://github.com/aploium/shootback'
license=('MIT')
depends=('python2')
makedepends=('git')
source=('git+https://github.com/aploium/shootback.git')
sha512sums=('SKIP')

pkgver() {
  cd $pkgname

  echo $(git rev-list --count HEAD).$(git rev-parse --short HEAD)
}

package() {
  cd $pkgname

  install -dm 755 "$pkgdir/usr/bin"
  install -dm 755 "$pkgdir/usr/share/$pkgname"

  install -Dm 644 -t "$pkgdir/usr/share/doc/$pkgname/" README.md
  install -Dm 644 LICENSE "$pkgdir/usr/share/licenses/$pkgname/LICENSE"

  rm README.md LICENSE

  cp -a * "$pkgdir/usr/share/$pkgname/"

  cat > "$pkgdir/usr/bin/shootback-master" << EOF
#!/bin/sh
cd /usr/share/$pkgname
exec python2 master.py "\$@"
EOF

  chmod +x "$pkgdir/usr/bin/shootback-master"

  cat > "$pkgdir/usr/bin/shootback-slaver" << EOF
#!/bin/sh
cd /usr/share/$pkgname
exec python2 slaver.py "\$@"
EOF

  chmod +x "$pkgdir/usr/bin/shootback-slaver"
}

