# This file is part of BlackArch Linux ( http://blackarch.org ).
# See COPYING for license details.

pkgname='vivisect'
pkgver='20140803'
pkgrel=1
groups=('blackarch' 'blackarch-debugger' 'blackarch-disassembler' 'blackarch-reversing')
pkgdesc='A Python based static analysis and reverse engineering framework, Vdb is a Python based research/reversing focused debugger and programatic debugging API by invisigoth of kenshoto'
arch=('any')
url='http://visi.kenshoto.com/'
license=('BSD')
depends=('python2' 'python2-pyqt4' 'pyqt4-common' 'python2-msgpack')
source=("http://visi.kenshoto.com/static/releases/vivisect_${pkgver}.zip")
options=('!emptydirs')
sha1sums=('4217485c0b50c544ae234bdaeebb1b35d4682f99')

prepare() {
  cd "$srcdir/vivisect_$pkgver"

  sed -i 's|#!/usr/bin/python|#!/usr/bin/python2|' \
    "$srcdir/vivisect_$pkgver/vdbbin"
  sed -i 's|#!/usr/bin/python|#!/usr/bin/python2|' \
    "$srcdir/vivisect_$pkgver/vivbin"
}

package() {
  cd "$srcdir/vivisect_$pkgver"

  mkdir -p "$pkgdir/usr/bin"
  mkdir -p "$pkgdir/usr/share/vivisect"

  cp -a * "$pkgdir/usr/share/vivisect"

  cat > "$pkgdir/usr/bin/vdbbin" << EOF
#!/bin/sh
cd /usr/share/vivisect
exec python2 /usr/share/vivisect/vdbbin "\$@"
EOF

  chmod +x "$pkgdir/usr/bin/vdbbin"

  cat > "$pkgdir/usr/bin/vivbin" << EOF
#!/bin/sh
cd /usr/share/vivisect
exec python2 /usr/share/vivisect/vivbin "\$@"
EOF

  chmod +x "$pkgdir/usr/bin/vivbin"
}
