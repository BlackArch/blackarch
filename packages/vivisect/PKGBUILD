# This file is part of BlackArch Linux ( https://www.blackarch.org/ ).
# See COPYING for license details.

pkgname=vivisect
pkgver=1545.b90026b4
pkgrel=1
epoch=1
groups=('blackarch' 'blackarch-debugger' 'blackarch-disassembler'
        'blackarch-reversing')
pkgdesc='A Python based static analysis and reverse engineering framework, Vdb is a Python based research/reversing focused debugger and programatic debugging API by invisigoth of kenshoto'
arch=('any')
url='http://visi.kenshoto.com/'
license=('BSD')
depends=('python2' 'python2-pyqt4' 'pyqt4-common' 'python2-msgpack')
makedepends=('git')
source=("git+https://github.com/$pkgname/$pkgname.git")
options=('!emptydirs')
sha512sums=('SKIP')

pkgver() {
  cd $pkgname

  echo $(git rev-list --count HEAD).$(git rev-parse --short HEAD)
}

package() {
  cd $pkgname

  install -dm 755 "$pkgdir/usr/bin"
  install -dm 755 "$pkgdir/usr/share/$pkgname"

  cp -a * "$pkgdir/usr/share/$pkgname/"

  cat > "$pkgdir/usr/bin/vdbbin" << EOF
#!/bin/sh
cd /usr/share/$pkgname
exec python2 /usr/share/$pkgname/vdbbin "\$@"
EOF

  chmod +x "$pkgdir/usr/bin/vdbbin"

  cat > "$pkgdir/usr/bin/vivbin" << EOF
#!/bin/sh
cd /usr/share/$pkgname
exec python2 /usr/share/$pkgname/vivbin "\$@"
EOF

  chmod +x "$pkgdir/usr/bin/vivbin"
}

