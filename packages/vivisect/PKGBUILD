# This file is part of BlackArch Linux ( https://www.blackarch.org/ ).
# See COPYING for license details.

pkgname=vivisect
pkgver=1593.6b214526
pkgrel=2
epoch=2
groups=('blackarch' 'blackarch-debugger' 'blackarch-disassembler'
        'blackarch-reversing')
pkgdesc='A Python based static analysis and reverse engineering framework.'
arch=('any')
url='http://visi.kenshoto.com/'
license=('BSD')
depends=('python2' 'python2-pyqt4' 'pyqt4-common' 'python2-msgpack'
         'python2-pyasn1' 'python2-pyasn1-modules' 'python2-pycparser'
         'python2-cxxfilt' 'python-qtpy' 'qtwebkit')
makedepends=('git' 'python2-setuptools')
# Avoid commits from the migration to Python3, it isn't ready yet
source=("git+https://github.com/$pkgname/$pkgname.git#commit=6b2145268976e4748eabfb9470a4edcc19bf36ff"
        'setup.py.patch')
options=('!emptydirs')
sha512sums=('SKIP'
            'da4efb4c71fc82cbd669b119357c5d4cf94b93587fa4e789951d88adb965d04e36b5efdbbde68d5d2b620039ce2c59c3cea16a5678a0719269ecdf4d0bf60dcd')

pkgver() {
  cd $pkgname

  echo $(git rev-list --count HEAD).$(git rev-parse --short HEAD)
}

prepare() {
  patch -p1 < setup.py.patch
}

build() {
  cd $pkgname

  python2 setup.py build
}

package() {
  cd $pkgname

  python2 setup.py install --root="$pkgdir" --prefix=/usr -O1 --skip-build

  sed -i 's/f"u/"u/' \
    "$pkgdir/usr/lib/python2.7/site-packages/vivisect/__init__.py"

  rm -rf "$pkgdir/usr/lib/python2.7/site-packages/vstruct/"
}

