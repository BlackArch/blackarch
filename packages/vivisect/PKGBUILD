# This file is part of BlackArch Linux ( https://www.blackarch.org/ ).
# See COPYING for license details.

pkgname=vivisect
pkgver=v1.1.1.r8.g5655b84e
_pyver=3.11
pkgrel=1
epoch=3
groups=('blackarch' 'blackarch-debugger' 'blackarch-disassembler'
        'blackarch-reversing')
pkgdesc='A Python based static analysis and reverse engineering framework.'
arch=('any')
url='https://github.com/vivisect/vivisect'
license=('BSD')
depends=('python' 'python-pyasn1' 'python-pyasn1-modules' 'python-cxxfilt'
         'python-msgpack' 'python-pycparser' 'python-pyqt5'
         'python-pyqt5-webengine')
makedepends=('git' 'python-setuptools')
source=("git+https://github.com/$pkgname/$pkgname.git")
options=('!emptydirs')
sha512sums=('SKIP')

prepare() {
  cd $pkgname

  sed -i 's/==/>=/' setup.py
}

pkgver() {
  cd $pkgname

  git describe --long --tags | sed 's/\([^-]*-g\)/r\1/;s/-/./g'
}

build() {
  cd $pkgname

  python setup.py build
}

package() {
  cd $pkgname

  python setup.py install --root="$pkgdir" --prefix=/usr -O1 --skip-build

  sed -i 's/f"u/"u/' \
    "$pkgdir/usr/lib/python$_pyver/site-packages/vivisect/__init__.py"

  #rm -rf "$pkgdir/usr/lib/python$_pyver/site-packages/vstruct/"
}

