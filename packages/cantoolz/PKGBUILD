# This file is part of BlackArch Linux ( http://blackarch.org ).
# See COPYING for license details.

pkgname=cantoolz
pkgver=336.61fc163
pkgrel=1
pkgdesc='Framework for black-box CAN network analysis https://asintsov.blogspot.de/.'
groups=('blackarch' 'blackarch-automobile' 'blackarch-recon' 'blackarch-fuzzer'
        'blackarch-scanner')
arch=('any')
url='https://github.com/eik00d/CANToolz'
license=('Apache2')
depends=('python' 'python-pyserial' 'python-numpy' 'python-mido')
makedepends=('git' 'python-setuptools')
source=('git+https://github.com/eik00d/CANToolz.git' 'setup.py.patch')
sha512sums=('SKIP'
            '9203c0fb106a9be588631686498a62ef1a8a42abdccf0438886270886f9059c27712792d222f139dfe9af69044ca355b779f9f81bee3a44ec08dcbd3a1c4d5a6')

pkgver() {
  cd "$srcdir/CANToolz"

  echo $(git rev-list --count HEAD).$(git rev-parse --short HEAD)
}

prepare() {
  cd "$srcdir"

  patch -p1 < setup.py.patch
}

package() {
  cd "$srcdir/CANToolz"

  install -dm 755 "$pkgdir/usr/bin"
  install -dm 755 "$pkgdir/usr/share/cantoolz"

  python3 setup.py install --root="$pkgdir" --optimize=1

  install -Dm755 cantoolz.py "$pkgdir/usr/bin/cantoolz"
  install -Dm644 -t "$pkgdir/usr/share/doc/cantoolz/" README.md NOTICE.md \
    CONTRIBUTORS.md VERSION.md
  install -Dm644 LICENSE.md "$pkgdir/usr/share/licenses/cantoolz/LICENSE.md"

  rm *.md

  cp -a * "$pkgdir/usr/share/cantoolz"

  cat > "$pkgdir/usr/bin/cantoolz" << EOF
#!/bin/sh
python /usr/share/cantoolz/cantoolz.py "\$@"
EOF

  chmod a+x "$pkgdir/usr/bin/cantoolz"
}
