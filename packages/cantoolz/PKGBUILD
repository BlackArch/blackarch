# This file is part of BlackArch Linux ( https://www.blackarch.org/ ).
# See COPYING for license details.

pkgname=cantoolz
_pkgname=CANToolz
pkgver=424.bc4c2bf
pkgrel=1
epoch=1
pkgdesc='Framework for black-box CAN network analysis.'
groups=('blackarch' 'blackarch-automobile' 'blackarch-recon' 'blackarch-fuzzer'
        'blackarch-scanner')
arch=('any')
url='https://github.com/CANToolz/CANToolz'
license=('Apache2')
depends=('python' 'python-pyserial' 'python-numpy' 'python-mido'
         'python-bitstring')
makedepends=('git' 'python-setuptools')
source=("git+https://github.com/$_pkgname/$_pkgname.git")
sha512sums=('SKIP')

pkgver() {
  cd $_pkgname

  echo $(git rev-list --count HEAD).$(git rev-parse --short HEAD)
}

package() {
  cd $_pkgname

  install -dm 755 "$pkgdir/usr/bin"
  install -dm 755 "$pkgdir/usr/share/$pkgname"

  python setup.py install --root="$pkgdir" --optimize=1

  install -Dm 644 -t "$pkgdir/usr/share/doc/$pkgname/" README.md NOTICE.md \
    CONTRIBUTORS.md VERSION.md
  install -Dm 644 LICENSE.md "$pkgdir/usr/share/licenses/$pkgname/LICENSE.md"

  cp -a --no-preserve=ownership * "$pkgdir/usr/share/$pkgname/"

  cat > "$pkgdir/usr/bin/$pkgname" << EOF
#!/bin/sh
cd /usr/share/$pkgname
exec python /usr/share/$pkgname/bin/$pkgname "\$@"
EOF

  chmod +x "$pkgdir/usr/bin/$pkgname"

  rm "$pkgdir/usr/lib/python3.7/site-packages/tests/__init__.py" \
    "$pkgdir/usr/lib/python3.7/site-packages/tests/__pycache__/__init__.cpython-37.opt-1.pyc" \
    "$pkgdir/usr/lib/python3.7/site-packages/tests/__pycache__/__init__.cpython-37.pyc"
}

