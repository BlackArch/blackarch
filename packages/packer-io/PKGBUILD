# This file is part of BlackArch Linux ( http://blackarch.org ).
# See COPYING for license details.
# Original Maintainer on AUR: Danilo Kuehn <dk[at]nogo-software[dot]de>

_pkgname=packer
pkgname=packer-io
pkgver=1.0.3
pkgrel=1
pkgdesc="Packer is a tool for creating identical machine images for multiple platforms from a single source configuration."
url="http://www.packer.io"
arch=('i686' 'x86_64' 'armv6h' 'armv7h' 'aarch64')
license=('MPL2')
depends=('glibc')
source_i686=("${_pkgname}-${pkgver}_i686.zip::https://releases.hashicorp.com/packer/${pkgver}/packer_${pkgver}_linux_386.zip"
             'https://raw.githubusercontent.com/hashicorp/packer/master/contrib/zsh-completion/_packer')
source_x86_64=("${_pkgname}-${pkgver}_x86_64.zip::https://releases.hashicorp.com/packer/${pkgver}/packer_${pkgver}_linux_amd64.zip"
               'https://raw.githubusercontent.com/hashicorp/packer/master/contrib/zsh-completion/_packer')
source_armv6h=("${_pkgname}-${pkgver}_arm.zip::https://releases.hashicorp.com/packer/${pkgver}/packer_${pkgver}_linux_arm.zip"
               'https://raw.githubusercontent.com/hashicorp/packer/master/contrib/zsh-completion/_packer')
source_armv7h=("${_pkgname}-${pkgver}_arm.zip::https://releases.hashicorp.com/packer/${pkgver}/packer_${pkgver}_linux_arm.zip"
               'https://raw.githubusercontent.com/hashicorp/packer/master/contrib/zsh-completion/_packer')
source_aarch64=("${_pkgname}-${pkgver}_arm64.zip::https://releases.hashicorp.com/packer/${pkgver}/packer_${pkgver}_linux_arm64.zip"
               'https://raw.githubusercontent.com/hashicorp/packer/master/contrib/zsh-completion/_packer')
md5sums_i686=('cf4af9d20511c1396b6e076a26cdbc74'
              'bb059fc00488ff869c45d05839c650f7')
md5sums_x86_64=('2715cedc4f29dae92706718eef07333e'
                'bb059fc00488ff869c45d05839c650f7')
md5sums_armv6h=('6d3312f7c59aca058ba06179353588f2'
                'bb059fc00488ff869c45d05839c650f7')
md5sums_armv7h=('6d3312f7c59aca058ba06179353588f2'
                'bb059fc00488ff869c45d05839c650f7')
md5sums_aarch64=('f43cc6b56ad8c68ebb0ef8c54cc1707d'
                 'bb059fc00488ff869c45d05839c650f7')

build() {
  sed 's/^#compdef packer/#compdef packer-io/' "${srcdir}/_packer" >"${srcdir}/_packer-io"
}

package() {
  install -dm755 "${pkgdir}/usr/bin"
  install -Dm755 "${srcdir}/${_pkgname}" "${pkgdir}/usr/bin/${_pkgname}-io"
  install -Dm644 ${srcdir}/_packer-io ${pkgdir}/usr/share/zsh/site-functions/_packer-io
}
