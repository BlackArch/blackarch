# This file is part of BlackArch Linux ( https://www.blackarch.org/ ).
# See COPYING for license details.

pkgbase=python-libbde-python
pkgname=('python2-libbde-python' 'python-libbde-python')
_pkgname=libbde-python
pkgver=20210605
pkgrel=2
pkgdesc='Library and tools to access the BitLocker Drive Encryption (BDE) encrypted volumes.'
arch=('x86_64' 'aarch64')
url='https://pypi.org/project/libbde-python/#files'
license=('LGPL3')
makedepends=('python2-setuptools' 'python-setuptools')
source=("https://files.pythonhosted.org/packages/1d/36/3ff44bf7fe3c41fd4c57d445aa2da8fcd296903c1f93b8ca54a25cd2a730/$_pkgname-$pkgver.tar.gz")
sha512sums=('75eed6776b8adb1db5750e75b87de5f5fe1904d3234a397492532ff146623247766ace0db439e90a5e29b1845206a558b7bfac6f15c857b31cfc194242076bb6')

package_python2-libbde-python() {
  depends=('python2')

  cd "libbde-$pkgver"

  python2 setup.py install --prefix=/usr --root="$pkgdir" --optimize=1
}

package_python-libbde-python() {
  depends=('python')

  cd "libbde-$pkgver"

  python setup.py install --prefix=/usr --root="$pkgdir" --optimize=1
}

