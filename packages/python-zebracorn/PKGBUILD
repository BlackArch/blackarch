# This file is part of BlackArch Linux ( https://www.blackarch.org/ ).
# See COPYING for license details.

pkgname=python-zebracorn
_pkgname=zebracorn
pkgver=0.0.1
pkgrel=2
pkgdesc='A Unicorn CPU emulator fork customized for the zelos binary emulator.'
arch=('x86_64' 'aarch64')
url='https://pypi.org/project/zebracorn/#files'
depends=('python')
makedepends=('python-setuptools')
license=('AGPL')
options=(!emptydirs)
source=("https://files.pythonhosted.org/packages/b5/51/66acde99302092d7c75b16ef555428490d1145db5db7ed0535c488bf46b5/$_pkgname-$pkgver.tar.gz"
        'setup.py.patch')
sha512sums=('90d9f152bb2fee1a9520c2994d9ec96ca87caf93cd030e68ca3e0f999741c31953a273b03c7192dde3ae60dfd3b1cf9d9508680e3929348eb73b8b13e4f4d733'
            '7b219b639a3ba7fac2a6d0870069f9229e2a6502f7325cf966cf9e5cc8930f21d208061bfff6d9fcdf0adc23c2c1837869610204ec253d6975813845e4e3e9ab')

prepare() {
  cd "$_pkgname-$pkgver"

  cp README.TXT README.md

  patch -p0 < "$srcdir/setup.py.patch"
}

build() {
  cd "$_pkgname-$pkgver"

  python setup.py build
}

package() {
  cd "$_pkgname-$pkgver"

  python setup.py install --root="$pkgdir" --prefix=/usr -O1 --skip-build
}

