# This file is part of BlackArch Linux ( https://www.blackarch.org/ ).
# See COPYING for license details.

pkgname=scylla
pkgver=76.34cb51c
pkgrel=1
pkgdesc='The Simplistic Information Gathering Engine | Find Advanced Information on a Username, Website, Phone Number, etc'
groups=('blackarch' 'blackarch-recon' 'blackarch-social')
arch=('any')
url='https://github.com/MandConsultingGroup/Scylla'
license=('MIT')
depends=('python' 'python-beautifulsoup4' 'python-requests' 'python-termcolor'
         'python-lxml' 'python-shodan' 'python-pythonwhois' 'python-google')
makedepends=('git')
source=("$pkgname::git+https://github.com/MandConsultingGroup/Scylla.git")
sha512sums=('SKIP')

pkgver() {
  cd $pkgname

  echo $(git rev-list --count HEAD).$(git rev-parse --short HEAD)
}

package() {
  cd $pkgname

  install -dm 755 "$pkgdir/usr/bin"
  install -dm 755 "$pkgdir/usr/share/$pkgname"

  install -Dm 644 -t "$pkgdir/usr/share/doc/$pkgname/" README.md
  install -Dm 644 LICENSE "$pkgdir/usr/share/licenses/$pkgname/LICENSE"

  rm -rf README.md LICENSE requirements.txt imgs

  cp -a * "$pkgdir/usr/share/$pkgname/"

  cat > "$pkgdir/usr/bin/$pkgname" << EOF
#!/bin/sh
cd /usr/share/$pkgname
exec python $pkgname.py "\$@"
EOF

  chmod +x "$pkgdir/usr/bin/$pkgname"
}

