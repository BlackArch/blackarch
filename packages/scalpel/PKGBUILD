# This file is part of BlackArch Linux ( http://blackarch.org ).
# See COPYING for license details.

pkgname=scalpel
pkgver=2.0
pkgrel=6
groups=('blackarch' 'blackarch-forensic')
pkgdesc='A frugal, high performance file carver'
url='http://www.digitalforensicssolutions.com/Scalpel/'
arch=('x86_64' 'armv6h' 'armv7h' 'aarch64')
license=('GPL2')
depends=('glibc' 'tre')
makedepends=('git')
source=('git+http://github.com/BlackArch/Scalpel-2.0'
        'conf-location.patch')
sha512sums=('SKIP'
            'a1b03fe033a6941740b41f0c7febe3effccbfbdd8bc13400684997d92da15517d5747361971640e8dc68b8bfd54ff002900b468a918cf2d6d022ab64b2a8287d')

prepare() {
  cd "$srcdir/Scalpel-$pkgver"

  patch -Np1 -i $srcdir/conf-location.patch
}

build() {
  cd "$srcdir/Scalpel-$pkgver"

  ./configure --prefix=/usr

  make
}

package() {
  cd "$srcdir/Scalpel-$pkgver"

  make DESTDIR="$pkgdir" install

  install -Dm644 conf/scalpel.conf "$pkgdir/etc/scalpel/scalpel.conf"
}
