# This file is part of BlackArch Linux ( http://blackarch.org ).
# See COPYING for license details.

pkgname='scalpel'
pkgver='2.0'
pkgrel=6
groups=('blackarch' 'blackarch-forensic')
pkgdesc='A frugal, high performance file carver'
url='http://www.digitalforensicssolutions.com/Scalpel/'
arch=('x86_64' 'armv6h' 'armv7h' 'aarch64')
license=('GPL2')
depends=('glibc' 'tre')
makedepends=('git')
source=('git+http://github.com/BlackArch/Scalpel-2.0'
        'conf-location.patch')
sha1sums=('SKIP'
          'b1a4b614cb477c701028e8c59f0d6d5f111e40a4')

prepare() {
  cd "$srcdir/Scalpel-$pkgver"

  patch -Np1 -i $srcdir/conf-location.patch
}

build() {
  cd "$srcdir/Scalpel-$pkgver"

  ./configure --prefix=/usr

  make
}

package() {
  cd "$srcdir/Scalpel-$pkgver"

  make DESTDIR="$pkgdir" install

  install -Dm644 conf/scalpel.conf "$pkgdir/etc/scalpel/scalpel.conf"
}
