# This file is part of BlackArch Linux ( http://blackarch.org ).
# See COPYING for license details.

pkgname='xprobe2'
pkgver='0.3'
pkgrel=8
groups=('blackarch' 'blackarch-fingerprint')
pkgdesc='An active OS fingerprinting tool.'
url='http://sourceforge.net/apps/mediawiki/xprobe/index.php?title=Main_Page'
arch=('x86_64' 'armv6h' 'armv7h' 'aarch64')
license=('GPL2')
depends=('gcc' 'libpcap')
source=("http://downloads.sourceforge.net/project/xprobe/xprobe2/Xprobe2%200.3/xprobe2-${pkgver}.tar.gz"
        'ip.patch'
        'misc.patch'
        'tcp.patch')
sha1sums=('c28d48823c1b953f73fd1b1fbced5c77a63d2bf0'
          'd4aba80a05632251563f835959f454be78b721de'
          '569fb8c1d93ba0e07ccf1fd899afddf10e96f622'
          'a867c5dc640859112f37cbb64b798a3767546791')

build() {
  cd "$srcdir/xprobe2-$pkgver"

  patch -p1 < $srcdir/ip.patch
  patch -p1 < $srcdir/misc.patch
  patch -p1 < $srcdir/tcp.patch

  if [[ "$CARCH" == "i686" || "$CARCH" == "x86_64" ]];
  then
    ./configure --prefix=/usr --sysconfdir=/etc
  else
    ./configure --build=arm --prefix=/usr --sysconfdir=/etc
  fi

  make
}

package() {
  cd "$srcdir/xprobe2-$pkgver"

  make DESTDIR="$pkgdir" install
}
