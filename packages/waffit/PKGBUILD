# This file is part of BlackArch Linux ( http://blackarch.org ).
# See COPYING for license details.

pkgname='waffit'
pkgver=189.0e3f479
pkgrel=1
groups=('blackarch' 'blackarch-scanner' 'blackarch-webapp')
pkgdesc='Identify and fingerprint Web Application Firewall (WAF) products protecting a website.'
arch=('any')
url='https://github.com/sandrogauci/wafw00f'
license=('GPL')
depends=('python2' 'python2-pluginbase' 'python2-httpretty'
         'python2-functools32' 'python2-beautifulsoup4')
makedepends=('git' 'python2-setuptools')
source=('git+https://github.com/sandrogauci/wafw00f.git')
sha1sums=('SKIP')

pkgver() {
  cd "$srcdir/wafw00f"

  echo $(git rev-list --count HEAD).$(git rev-parse --short HEAD)
}

build() {
  cd "$srcdir/wafw00f"

  python2 setup.py build
}

package() {
  cd "$srcdir/wafw00f"

  python2 setup.py install --root="$pkgdir" --optimize=1
}
