# This file is part of BlackArch Linux ( http://blackarch.org ).
# See COPYING for license details.

pkgname=shellme
pkgver=5.d5206f0
pkgrel=1
groups=('blackarch' 'blackarch-exploitation' 'blackarch-automation')
pkgdesc='Because sometimes you just need shellcode and opcodes quickly. This essentially just wraps some nasm/objdump calls into a neat script.'
url='https://github.com/hatRiot/shellme'
arch=('any')
license=('MIT')
depends=('python2')
makedepends=('git')
source=('git+https://github.com/hatRiot/shellme.git')
sha512sums=('SKIP')

pkgver() {
  cd $pkgname

  echo $(git rev-list --count HEAD).$(git rev-parse --short HEAD)
}

package() {
  cd $pkgname

  install -dm 755 "$pkgdir/usr/bin"

  install -Dm 755 shellme.py "$pkgdir/usr/share/$pkgname/shellme.py"
  install -Dm 644 README.md "$pkgdir/usr/share/doc/$pkgname/README.md"
  install -Dm 644 LICENSE.md "$pkgdir/usr/share/licenses/$pkgname/LICENSE.md"

  cat > "$pkgdir/usr/bin/$pkgname" << EOF
#!/bin/sh
cd /usr/share/$pkgname
exec python2 shellme.py "\$@"
EOF

  chmod +x "$pkgdir/usr/bin/$pkgname"
}

