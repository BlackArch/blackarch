# This file is part of BlackArch Linux ( https://www.blackarch.org/ ).
# See COPYING for license details.

pkgbase=python-tls-parser
pkgname=('python2-tls-parser' 'python-tls-parser')
pkgver=1.2.1
pkgrel=4
pkgdesc='Small library to parse TLS records.'
arch=('any')
url='https://pypi.python.org/pypi/tls-parser/'
license=('custom:unknown')
makedepends=('python2-setuptools' 'python-setuptools')
source=("https://pypi.python.org/packages/49/c4/aa379256eb83469154c671b700b3edb42ae781044a4cd40ae92bff8259c7/tls_parser-$pkgver.tar.gz")
sha512sums=('0524c328852ca20d8c6d8c419ab8aa019afd80b41bb862cc639bb63f1f7b8ed0869a47f5791aaaa1aba54171d19ed0785c0e98d7433e93d902f48bc0d8069415')

prepare() {
  cp -a "tls_parser-$pkgver" "tls_parser2-$pkgver"
}

build() {
  cd "tls_parser2-$pkgver"

  python2 setup.py build

  cd "$srcdir/tls_parser-$pkgver"

  python setup.py build
}

package_python2-tls-parser() {
  cd "tls_parser2-$pkgver"

  python2 setup.py install --prefix=/usr --root="$pkgdir" --optimize=1 \
    --skip-build
}

package_python-tls-parser() {
  cd "tls_parser-$pkgver"

  python setup.py install --prefix=/usr --root="$pkgdir" --optimize=1 \
    --skip-build
}

