# This file is part of BlackArch Linux ( https://www.blackarch.org/ ).
# See COPYING for license details.

pkgbase=python-tls-parser
pkgname=('python2-tls-parser' 'python-tls-parser')
_pkgname='tls_parser'
pkgver=2.0.0
pkgrel=1
pkgdesc='Small library to parse TLS records.'
arch=('any')
url='https://pypi.python.org/pypi/tls-parser/'
license=('custom:unknown')
makedepends=('python2-setuptools' 'python-setuptools')
source=("https://files.pythonhosted.org/packages/12/fc/282d5dd9e90d3263e759b0dfddd63f8e69760617a56b49ea4882f40a5fc5/tls_parser-$pkgver.tar.gz")
sha512sums=('1353891c0e5a571aeb181563839bb01387f6f6aa7b5afb6d034e46b29121867c077e2f064c7158eb6e33cf008ada7b87512afa01db8a48a8d778bdaecaa407b1')

prepare() {
  cp -a "tls_parser-$pkgver" "tls_parser2-$pkgver"
}

build() {
  cd "tls_parser2-$pkgver"

  python2 setup.py build

  cd "$srcdir/tls_parser-$pkgver"

  python setup.py build
}

package_python2-tls-parser() {
  cd "tls_parser2-$pkgver"

  python2 setup.py install --prefix=/usr --root="$pkgdir" --optimize=1 \
    --skip-build
}

package_python-tls-parser() {
  cd "tls_parser-$pkgver"

  python setup.py install --prefix=/usr --root="$pkgdir" --optimize=1 \
    --skip-build
}

