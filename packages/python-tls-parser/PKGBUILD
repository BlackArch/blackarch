# This file is part of BlackArch Linux ( https://www.blackarch.org/ ).
# See COPYING for license details.

pkgbase=python-tls-parser
pkgname=('python2-tls-parser' 'python-tls-parser')
_pkgname='tls_parser'
pkgver=2.0.1
pkgrel=1
pkgdesc='Small library to parse TLS records.'
arch=('any')
url='https://pypi.python.org/pypi/tls-parser/'
license=('custom:unknown')
makedepends=('python2-setuptools' 'python-setuptools')
source=("https://files.pythonhosted.org/packages/source/${_pkgname::1}/$_pkgname/tls_parser-$pkgver.tar.gz")
sha512sums=('693d18f0f3baaa8bf122f0166a9041cfe816772930dcfb73cfc1e16b0cedef2cfd19623d91a6d9e745bfcc1d2cbfb001a031d8c0d6fe1dd46319c3469ba60812')

prepare() {
  cp -a "tls_parser-$pkgver" "tls_parser2-$pkgver"
}

build() {
  cd "tls_parser2-$pkgver"

  python2 setup.py build

  cd "$srcdir/tls_parser-$pkgver"

  python setup.py build
}

package_python2-tls-parser() {
  cd "tls_parser2-$pkgver"

  python2 setup.py install --prefix=/usr --root="$pkgdir" --optimize=1 \
    --skip-build
}

package_python-tls-parser() {
  cd "tls_parser-$pkgver"

  python setup.py install --prefix=/usr --root="$pkgdir" --optimize=1 \
    --skip-build
}

