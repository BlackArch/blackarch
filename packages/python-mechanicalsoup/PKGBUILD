# This file is part of BlackArch Linux ( https://www.blackarch.org/ ).
# See COPYING for license details.

pkgbase=python-mechanicalsoup
pkgname=('python2-mechanicalsoup' 'python-mechanicalsoup')
_pkgname=MechanicalSoup
pkgver=0.11.0
pkgrel=5
pkgdesc='A Python library for automating interaction with websites.'
arch=('any')
url='https://pypi.org/project/MechanicalSoup/#files'
license=('MIT')
makedepends=('python2-setuptools' 'python-setuptools')
source=("https://files.pythonhosted.org/packages/58/96/5a3b6814d4d7a079b61295d9701be0515eab25a5219257f098dca4282502/$_pkgname-$pkgver.tar.gz")
sha512sums=('3fab6ae1aace2581f6d0a031ba3b144a78404f5deebc0fc1b8eede606f72a912ac9a9506da78e0f5ca30483676b170570d72bcbf0712a7e193587ec264bdbff7')

prepare() {
  cp -a "$_pkgname-$pkgver" "$_pkgname-$pkgver-2"
}

build() {
  cd "$_pkgname-$pkgver-2"

  python2 setup.py build

  cd "$srcdir/$_pkgname-$pkgver"

  python setup.py build
}

package_python2-mechanicalsoup() {
  depends=('python2' 'python2-requests' 'python2-six' 'python2-beautifulsoup4'
           'python2-lxml')

  cd "$_pkgname-$pkgver-2"

  python2 setup.py install --prefix=/usr --root="$pkgdir" --optimize=1 \
    --skip-build
}

package_python-mechanicalsoup() {
  depends=('python' 'python-requests' 'python-six' 'python-beautifulsoup4'
           'python-lxml')

  cd "$_pkgname-$pkgver"

  python setup.py install --prefix=/usr --root="$pkgdir" --optimize=1 \
    --skip-build
}

