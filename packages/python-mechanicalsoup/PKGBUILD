# This file is part of BlackArch Linux ( https://www.blackarch.org/ ).
# See COPYING for license details.

pkgbase=python-mechanicalsoup
pkgname=('python2-mechanicalsoup' 'python-mechanicalsoup')
_pkgname=MechanicalSoup
pkgver=1.2.0
pkgrel=2
pkgdesc='A Python library for automating interaction with websites.'
arch=('any')
url='https://pypi.org/project/MechanicalSoup/#files'
license=('MIT')
makedepends=('python2-setuptools' 'python-setuptools')
source=("https://files.pythonhosted.org/packages/source/${_pkgname::1}/$_pkgname/MechanicalSoup-$pkgver.tar.gz")
sha512sums=('5857274e07c678dc0e19ad4fef828ccf41bb846edad566c2da9914144ced8802efd546265efac792acbb885b29cab5cde4ac3cd2002826a8bb31f91004eb2caa')

prepare() {
  cp -a "$_pkgname-$pkgver"{,-2}
}

build() {
  cd "$_pkgname-$pkgver-2"

  python2 setup.py build

  cd "$srcdir/$_pkgname-$pkgver"

  python setup.py build
}

package_python2-mechanicalsoup() {
  depends=('python2' 'python2-requests' 'python2-six' 'python2-beautifulsoup4'
           'python2-lxml')

  cd "$_pkgname-$pkgver-2"

  python2 setup.py install --prefix=/usr --root="$pkgdir" --optimize=1 \
    --skip-build
}

package_python-mechanicalsoup() {
  depends=('python' 'python-requests' 'python-six' 'python-beautifulsoup4'
           'python-lxml')

  cd "$_pkgname-$pkgver"

  python setup.py install --prefix=/usr --root="$pkgdir" --optimize=1 \
    --skip-build
}

