# This file is part of BlackArch Linux ( https://www.blackarch.org/ ).
# See COPYING for license details.

pkgbase=python-mechanicalsoup
pkgname=('python2-mechanicalsoup' 'python-mechanicalsoup')
_pkgname=MechanicalSoup
pkgver=0.12.0
pkgrel=3
pkgdesc='A Python library for automating interaction with websites.'
arch=('any')
url='https://pypi.org/project/MechanicalSoup/#files'
license=('MIT')
makedepends=('python2-setuptools' 'python-setuptools')
source=("https://files.pythonhosted.org/packages/c3/f7/68b90159109031391aa0872f611aebeca425aa432d26a74ea28aad43e969/MechanicalSoup-$pkgver.tar.gz")
sha512sums=('6cce80ea5a90e7f19f2b13749f6ee48222d973af66cb350ac867a554580b0d619defe5a93741e6ec8aa808c585a8c01703158c7484521ac069977388de8bb7e3')

prepare() {
  cp -a "$_pkgname-$pkgver"{,-2}
}

build() {
  cd "$_pkgname-$pkgver-2"

  python2 setup.py build

  cd "$srcdir/$_pkgname-$pkgver"

  python setup.py build
}

package_python2-mechanicalsoup() {
  depends=('python2' 'python2-requests' 'python2-six' 'python2-beautifulsoup4'
           'python2-lxml')

  cd "$_pkgname-$pkgver-2"

  python2 setup.py install --prefix=/usr --root="$pkgdir" --optimize=1 \
    --skip-build
}

package_python-mechanicalsoup() {
  depends=('python' 'python-requests' 'python-six' 'python-beautifulsoup4'
           'python-lxml')

  cd "$_pkgname-$pkgver"

  python setup.py install --prefix=/usr --root="$pkgdir" --optimize=1 \
    --skip-build
}

