# This file is part of BlackArch Linux ( https://www.blackarch.org/ ).
# See COPYING for license details.

pkgbase=python-mechanicalsoup
pkgname=('python2-mechanicalsoup' 'python-mechanicalsoup')
_pkgname=MechanicalSoup
pkgver=1.0.0
pkgrel=1
pkgdesc='A Python library for automating interaction with websites.'
arch=('any')
url='https://pypi.org/project/MechanicalSoup/#files'
license=('MIT')
makedepends=('python2-setuptools' 'python-setuptools')
source=("https://files.pythonhosted.org/packages/d4/f8/72edbe4c08e84f2588e4c656288e1d849063448f93827fc1a06ef75834a0/MechanicalSoup-$pkgver.tar.gz")
sha512sums=('00e2c5220c065b517301d9381a9bc5019f6f75dc5951a5ab4427fd7939b296b6b33a20b8f8a7e82c6feed2c9100e3481f38a424d63065883d71a6927119ad88c')

prepare() {
  cp -a "$_pkgname-$pkgver"{,-2}
}

build() {
  cd "$_pkgname-$pkgver-2"

  python2 setup.py build

  cd "$srcdir/$_pkgname-$pkgver"

  python setup.py build
}

package_python2-mechanicalsoup() {
  depends=('python2' 'python2-requests' 'python2-six' 'python2-beautifulsoup4'
           'python2-lxml')

  cd "$_pkgname-$pkgver-2"

  python2 setup.py install --prefix=/usr --root="$pkgdir" --optimize=1 \
    --skip-build
}

package_python-mechanicalsoup() {
  depends=('python' 'python-requests' 'python-six' 'python-beautifulsoup4'
           'python-lxml')

  cd "$_pkgname-$pkgver"

  python setup.py install --prefix=/usr --root="$pkgdir" --optimize=1 \
    --skip-build
}

