# Maintainer: Zhang Hai <dreaming.in.code.zh@gmail.com>
# Contributor: Vlad M. <vlad@archlinux.net>
# Contributor: Gordin <9ordin @t gmail dot com>
# Contributor: Christoph Bayer <chrbayer@criby.de>

pkgname=android-sdk-platform-tools
pkgver=r29.0.1
pkgrel=1
pkgdesc='Platform-Tools for Google Android SDK (adb and fastboot)'
arch=('x86_64')
url='http://developer.android.com/sdk/index.html'
license=('custom')
depends_x86_64=('zlib' 'ncurses')
provides=('adb' 'android-tools')
conflicts=('adb')
replaces=('android-tools')
install="$pkgname.install"
source=("https://dl.google.com/android/repository/platform-tools_$pkgver-linux.zip"
        'adb.service'
        'license.html')
sha512sums=('a940d3a4da6634630bb33d2943a9d122659dc5fe3d22ce91426a90463cd729b718b6e9b68685a236a628d18e2b3c2a4c90317dd4501c96255ed083b6f073a4bb'
            'faf315d2bb7a9caa099db32e21451a1fd8fcc76475f25b36742356adc4f758e6fc7512106f9539b2db687c9566ef5889a4fa051c3c1a84555090d4b175e485d4'
            '23f1533682c39e2ae2ae40083de343abcd6ed0a523eacdac58768067f8f450b1cec30073993dd7b972a5b8125136ed29a043ae894dff046fd75bb04c4d21a0f4')

package() {
  install -Dm 644 adb.service "$pkgdir/usr/lib/systemd/system/adb.service"
  install -Dm 644 license.html "$pkgdir/usr/share/licenses/$pkgname/license.html"

  install -d "$pkgdir/etc/profile.d"
  echo 'export PATH="${PATH}:/opt/android-sdk/platform-tools"' > "$pkgdir/etc/profile.d/$pkgname.sh"
  echo 'setenv PATH "${PATH}:/opt/android-sdk/platform-tools"' > "$pkgdir/etc/profile.d/$pkgname.csh"
  chmod 755 "$pkgdir/etc/profile.d/${pkgname}".{csh,sh}

  install -d "$pkgdir/opt/android-sdk/"
  cp -a platform-tools "$pkgdir/opt/android-sdk/platform-tools"
  chmod -R +rX "${pkgdir}/opt/android-sdk/platform-tools"
}

