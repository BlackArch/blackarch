# This file is part of BlackArch Linux ( https://www.blackarch.org/ ).
# See COPYING for license details.

pkgname=whatportis
pkgver=46.496f81a
pkgrel=1
groups=('blackarch' 'blackarch-misc')
pkgdesc='A command to search port names and numbers.'
arch=('any')
url='https://github.com/ncrocfer/whatportis'
license=('MIT')
depends=('python2' 'python2-click' 'python2-simplejson' 'python2-flask'
         'python2-tinydb' 'python2-prettytable')
makedepends=('git' 'python2-setuptools')
source=('git+https://github.com/ncrocfer/whatportis.git')
sha512sums=('SKIP')

pkgver() {
  cd $pkgname

  echo $(git rev-list --count HEAD).$(git rev-parse --short HEAD)
}

prepare() {
  cd $pkgname

  cut -d '=' -f 1 requirements.txt | sort -u -o requirements.txt
}

build() {
  cd $pkgname

  python2 setup.py build
}

package() {
  cd $pkgname

  install -dm 755 "$pkgdir/usr/share/$pkgname"

  python2 setup.py install --root="$pkgdir" --optimize=1 --skip-build

  install -Dm 644 -t "$pkgdir/usr/share/doc/$pkgname/" README.rst
  install -Dm 644 LICENSE.txt "$pkgdir/usr/share/licenses/$pkgname/LICENSE"

  chmod 666 "$pkgdir/usr/lib/python2.7/site-packages/$pkgname/ports.json"
}

