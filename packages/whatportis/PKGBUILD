# This file is part of BlackArch Linux ( https://www.blackarch.org/ ).
# See COPYING for license details.

pkgname=whatportis
pkgver=46.496f81a
pkgrel=1
groups=('blackarch' 'blackarch-misc')
pkgdesc='A command to search port names and numbers.'
arch=('any')
url='https://github.com/ncrocfer/whatportis'
license=('MIT')
depends=('python' 'python-click' 'python-simplejson' 'python-flask'
         'python-tinydb' 'python-prettytable')
makedepends=('git' 'python-setuptools')
source=('git+https://github.com/ncrocfer/whatportis.git')
sha512sums=('SKIP')

pkgver() {
  cd $pkgname

  echo $(git rev-list --count HEAD).$(git rev-parse --short HEAD)
}

prepare() {
  cd $pkgname

  cut -d '=' -f 1 requirements.txt | sort -u -o requirements.txt
}

build() {
  cd $pkgname

  python setup.py build
}

package() {
  cd $pkgname

  python setup.py install --root="$pkgdir" --prefix=/usr -O1 --skip-build

  install -Dm 644 -t "$pkgdir/usr/share/doc/$pkgname/" README.rst
  install -Dm 644 LICENSE.txt "$pkgdir/usr/share/licenses/$pkgname/LICENSE"

  #chmod 666 "$pkgdir/usr/lib/python3.8/site-packages/$pkgname/ports.json"
}

