# This file is part of BlackArch Linux ( http://blackarch.org ).
# See COPYING for license details.

pkgname='shellsploit-framework'
pkgver=2.2cd3f08
pkgrel=1
groups=('blackarch' 'blackarch-exploitation' 'blackarch-backdoor')
pkgdesc='New Generation Exploit Development Kit.'
arch=('any')
url='https://github.com/b3mb4m/shellsploit-framework'
license=('MIT')
depends=('python2' 'python2-capstone' 'python2-psutil' 'python2-colorama'
         'python2-pefile')
makedepends=('git' 'python2-setuptools')
source=('git+https://github.com/b3mb4m/shellsploit-framework.git')
sha1sums=('SKIP')

pkgver() {
  cd "$srcdir/shellsploit-framework"

  echo $(git rev-list --count HEAD).$(git rev-parse --short HEAD)
}

build() {
  cd "$srcdir/shellsploit-framework"

  python2 easyinstall.py build
}

package() {
  cd "$srcdir/shellsploit-framework"


  python2 easyinstall.py install --root="$pkgdir"

  install -Dm644 README.md \
    "$pkgdir/usr/share/doc/shellsploit-framework/README.md"
  install -Dm644 LICENSE \
    "$pkgdir/usr/share/licenses/shellsploit-framework/LICENSE"
}
