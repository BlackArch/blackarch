# This file is part of BlackArch Linux ( https://www.blackarch.org/ ).
# See COPYING for license details.

pkgname=shellsploit-framework
pkgver=2.2cd3f08
pkgrel=1
groups=('blackarch' 'blackarch-exploitation' 'blackarch-backdoor')
pkgdesc='New Generation Exploit Development Kit.'
arch=('any')
url='https://github.com/b3mb4m/shellsploit-framework'
license=('MIT')
depends=('python2' 'python2-capstone' 'python2-psutil' 'python2-colorama'
         'python2-pefile')
makedepends=('git' 'python2-setuptools')
source=("git+https://github.com/b3mb4m/$pkgname.git")
sha512sums=('SKIP')

pkgver() {
  cd "$pkgname"

  echo $(git rev-list --count HEAD).$(git rev-parse --short HEAD)
}

build() {
  cd "$pkgname"

  python2 easyinstall.py build
}

package() {
  cd "$pkgname"

  python2 easyinstall.py install --root="$pkgdir"

  install -Dm 644 -t "$pkgdir/usr/share/doc/$pkgname/" README.md
  install -Dm 644 LICENSE "$pkgdir/usr/share/licenses/$pkgname/LICENSE"
}

