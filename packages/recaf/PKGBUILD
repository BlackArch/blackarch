# This file is part of BlackArch Linux ( https://www.blackarch.org/ ).
# See COPYING for license details.

pkgname=recaf
pkgver=2.16.6.r2889.g70cccf1
pkgrel=1
groups=('blackarch' 'blackarch-decompiler' 'blackarch-reversing')
pkgdesc='Modern Java bytecode editor.'
arch=('any')
url='https://github.com/Col-E/Recaf'
license=('MIT')
depends=('java-runtime' 'java-environment')
makedepends=('gradle' 'git' 'jdk-openjdk')
# TODO package openjfx ?
source=("$pkgname::git+https://github.com/Col-E/Recaf.git")
sha512sums=('SKIP')

pkgver() {
  cd $pkgname

  ( set -o pipefail
    git describe --long --tags --abbrev=7 2>/dev/null |
      sed 's/\([^-]*-g\)/r\1/;s/-/./g' ||
    printf "%s.%s" "$(git rev-list --count HEAD)" \
      "$(git rev-parse --short=7 HEAD)"
  )
}

build() {
  cd $pkgname

  # https://github.com/Col-E/Recaf/blob/master/.github/workflows/build.yml

  # Do not use `./gradlew` wrapper as it force older version of gradle not compatible with latest Java version
  # e.g. 8.11.1 compatible up to Java 23 where we need Gradle 8.14 for Java 24 support
  # https://docs.gradle.org/current/userguide/compatibility.html
  gradle assemble -x compileTestJava -Dskip_jfx_bundle=true --stacktrace
  # gradle publish --stacktrace
}

package() {
  cd $pkgname

  install -dm 755 "$pkgdir/usr/bin"
  install -dm 755 "$pkgdir/usr/share/$pkgname"

  cp target/recaf-*-jar-with-dependencies.jar \
    "$pkgdir/usr/share/$pkgname/$pkgname.jar"

  cat > "$pkgdir/usr/bin/$pkgname" << EOF
#!/bin/sh
java -jar /usr/share/$pkgname/$pkgname.jar "\$@"
EOF

  chmod +x "$pkgdir/usr/bin/$pkgname"
}

