# This file is part of BlackArch Linux ( https://www.blackarch.org/ ).
# See COPYING for license details.

pkgbase=python-pluginbase
pkgname=('python2-pluginbase' 'python-pluginbase')
_pkgname=pluginbase
pkgver=1.0.1
pkgrel=2
pkgdesc='A support library for building plugins sytems in Python.'
arch=('any')
url='https://pypi.org/project/pluginbase/#files'
license=('BSD')
makedepends=('python2-setuptools' 'python-setuptools')
source=("https://files.pythonhosted.org/packages/f3/07/753451e80d2b0bf3bceec1162e8d23638ac3cb18d1c38ad340c586e90b42/$_pkgname-$pkgver.tar.gz")
sha512sums=('20844fb78daf3e03a7a8013f82a059bf6b0723a184e89061ce5cb674b5a978900623f2eceed56fc9f9a34410e7e6ca8d41a5ac0a21e374b1b3ce32d4e522348e')

prepare() {
  cp -a "$_pkgname-$pkgver"{,-2}
}

build() {
  cd "$_pkgname-$pkgver-2"

  python2 setup.py build

  cd "$srcdir/$_pkgname-$pkgver"

  python setup.py build
}

package_python2-pluginbase() {
  depends=('python2')

  cd "$_pkgname-$pkgver-2"

  python2 setup.py install --prefix=/usr --root="$pkgdir" --optimize=1 \
    --skip-build
}

package_python-pluginbase() {
  depends=('python')

  cd "$_pkgname-$pkgver"

  python setup.py install --prefix=/usr --root="$pkgdir" --optimize=1 \
    --skip-build
}

