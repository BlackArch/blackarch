# This file is part of BlackArch Linux ( https://www.blackarch.org/ ).
# See COPYING for license details.

pkgname=python-aiosmtplib
_pkgname=${pkgname#python-}
pkgver=2.0.0
pkgrel=1
pkgdesc='Asynchronous SMTP client for use with asyncio.'
arch=('any')
url='https://github.com/cole/aiosmtplib'
license=('MIT')
depends=('python')
makedepends=('python-build' 'python-pip')
options=(!emptydirs)
source=("https://files.pythonhosted.org/packages/source/${_pkgname::1}/$_pkgname/$_pkgname-$pkgver.tar.gz")
sha512sums=('3fdea00062c95f1c457a954b005a993136c150575d505f16ad1b92ef2c2997bb4699c5e8057757d7accad047ab15d49f48fa1abb37f6f2aa4368f993455824a6')

build() {
  cd "$_pkgname-$pkgver"

  python -m build --wheel --outdir="$startdir/dist"
}

package() {
  cd "$_pkgname-$pkgver"

  pip install \
    --verbose \
    --disable-pip-version-check \
    --no-warn-script-location \
    --ignore-installed \
    --no-compile \
    --no-deps \
    --root="$pkgdir" \
    --prefix=/usr \
    --no-index \
    --find-links="file://$startdir/dist" \
    "$_pkgname"

  # workaround https://github.com/cole/aiosmtplib/issues/195
  rm -rf "$pkgdir/usr/lib/python3.10/site-packages/tests"
}

