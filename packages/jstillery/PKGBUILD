# This file is part of BlackArch Linux ( http://blackarch.org ).
# See COPYING for license details.

pkgname='jstillery'
pkgver=48.c32a914
pkgrel=1
groups=('blackarch' 'blackarch-webapp')
pkgdesc='Advanced JavaScript Deobfuscation via Partial Evaluation.'
url='https://github.com/mindedsecurity/JStillery'
arch=('any')
license=('GPL3')
depends=('nodejs')
makedepends=('git' 'npm')
source=('git+https://github.com/mindedsecurity/JStillery.git')
sha1sums=('SKIP')

pkgver() {
  cd "$srcdir/JStillery"

  echo $(git rev-list --count HEAD).$(git rev-parse --short HEAD)
}

package() {
  cd "$srcdir/JStillery"

  mkdir -p "$pkgdir/usr/bin"
  mkdir -p "$pkgdir/usr/share/jstillery"

  npm install --user root -g --prefix "$pkgdir/usr"

  install -Dm644 README.md "$pkgdir/usr/share/doc/jstillery/README.md"
  install -Dm644 LICENSE "$pkgdir/usr/share/licenses/jstillery/LICENSE"

  rm README.md LICENSE

  cp -a * "$pkgdir/usr/share/jstillery/"

  cat > "$pkgdir/usr/bin/jstillery" << EOF
#!/bin/sh
exec node /usr/share/jstillery/jstillery_cli.js "\${@}"
EOF

  chmod a+x "$pkgdir/usr/bin/jstillery"
}
