# This file is part of BlackArch Linux ( https://www.blackarch.org/ ).
# See COPYING for license details

pkgname=python-py010parser
_pkgname=py010parser
pkgver=0.1.15
pkgrel=1
pkgdesc='Library parsing 010 templates into an AST.'
arch=('any')
url='https://github.com/d0c-s4vage/py010parser'
license=('custom')
depends=('python' 'python-six')
makedepends=('python-setuptools')
source=("https://files.pythonhosted.org/packages/ee/24/87c8ce6818eb1161e7835360d3ecb66e2c147c9c9ce922a0c668fc26f2e5/$_pkgname-$pkgver.tar.gz")
sha512sums=('dad7a7c62101417c866a4d969ce8d166dd2f1f54c348b01550ab773270e80956e467c15bc4b24978f74511f0282f2de64f780ed7576a5375af995b3644b773d3')

prepare() {
  cd $_pkgname-$pkgver
  sed -i 's/six==1.10.0/six/g' requirements.txt
}

build() {
  cd $_pkgname-$pkgver
  python setup.py build
}

package() {
  cd $_pkgname-$pkgver
  python setup.py install --root="$pkgdir" --optimize=1
}

