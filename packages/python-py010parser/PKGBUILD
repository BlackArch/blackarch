# This file is part of BlackArch Linux ( https://www.blackarch.org/ ).
# See COPYING for license details

pkgname=python-py010parser
_pkgname=py010parser
pkgver=0.1.10
pkgrel=1
pkgdesc='Library parsing 010 templates into an AST.'
arch=('any')
url='https://github.com/d0c-s4vage/py010parser'
license=('custom')
depends=('python' 'python-six')
makedepends=('python-setuptools')
source=("https://github.com/d0c-s4vage/$_pkgname/archive/v$pkgver.tar.gz")
sha512sums=('6a1ae4061637655d9336e4899548a12381489ce94c913615d380ad6d1f9ddb1bd70043cc6eccd0582083a547679aced5dd8eedd56f0a469694bd9e396f2e84fa')

prepare() {
  cd $_pkgname-$pkgver
  sed -i 's/six==1.10.0/six/g' requirements.txt
}

build() {
  cd $_pkgname-$pkgver
  python setup.py build
}

package() {
  cd $_pkgname-$pkgver
  python setup.py install --root="$pkgdir" --optimize=1
}

