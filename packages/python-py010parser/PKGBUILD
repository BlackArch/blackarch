# This file is part of BlackArch Linux ( https://www.blackarch.org/ ).
# See COPYING for license details

pkgname=python-py010parser
_pkgname=py010parser
pkgver=0.1.16
pkgrel=1
pkgdesc='Library parsing 010 templates into an AST.'
arch=('any')
url='https://github.com/d0c-s4vage/py010parser'
license=('custom')
depends=('python' 'python-six')
makedepends=('python-setuptools')
source=("https://files.pythonhosted.org/packages/dd/bb/55db12070424fe202ef73986436dddda374187041f5381845cd1c6992ae4/$_pkgname-$pkgver.tar.gz")
sha512sums=('d8d49be05f07e5056818ff576ec7a360f64bad6ecfc1585c10db31e4446276e7cdea792780282e7e4522ab936e6a81b80bbae0f8e4abe73facb255856fd5011d')

prepare() {
  cd $_pkgname-$pkgver
  sed -i 's/six==1.10.0/six/g' requirements.txt
}

build() {
  cd $_pkgname-$pkgver
  python setup.py build
}

package() {
  cd $_pkgname-$pkgver
  python setup.py install --root="$pkgdir" --optimize=1
}

