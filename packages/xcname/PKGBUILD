# This file is part of BlackArch Linux ( https://www.blackarch.org/ ).
# See COPYING for license details.

pkgname=xcname
pkgver=11.9c475a1
pkgrel=4
groups=('blackarch' 'blackarch-scanner')
pkgdesc='A tool for enumerating expired domains in CNAME records.'
arch=('any')
url='https://github.com/mandatoryprogrammer/xcname'
license=('custom:unknown')
depends=('python2' 'python2-pywhois' 'python2-python-whois' 'python2-ipwhois'
         'python2-tldextract' 'python2-requests')
makedepends=('git')
source=("git+https://github.com/mandatoryprogrammer/$pkgname.git")
sha512sums=('SKIP')

pkgver() {
  cd $pkgname

  echo $(git rev-list --count HEAD).$(git rev-parse --short HEAD)
}

prepare() {
  cd $pkgname

  sed -i 's|/usr/bin/env python|/usr/bin/env python2|' "$pkgname.py"
}

package() {
  cd $pkgname

  install -dm 755 "$pkgdir/usr/bin"
  install -dm 755 "$pkgdir/usr/share/$pkgname"

  install -Dm 644 -t "$pkgdir/usr/share/doc/$pkgname/" README.md

  rm README.md

  cp -a * "$pkgdir/usr/share/$pkgname/"

  cat > "$pkgdir/usr/bin/$pkgname" << EOF
#!/bin/sh
cd /usr/share/$pkgname
exec python2 $pkgname.py "\$@"
EOF

  chmod +x "$pkgdir/usr/bin/$pkgname"
}

