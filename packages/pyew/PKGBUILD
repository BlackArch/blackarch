# This file is part of BlackArch Linux ( http://blackarch.org ).
# See COPYING for license details.

pkgname='pyew'
pkgver='2.3.0'
pkgrel=1
groups=('blackarch' 'blackarch-malware')
pkgdesc='A python tool to analyse malware.'
arch=('any')
url='https://code.google.com/p/pyew/'
license=('GPL2')
depends=('distorm' 'python2')
makedepends=('mercurial')
source=('hg+https://code.google.com/p/pyew/')
sha1sums=('SKIP')

package(){
  cd "$srcdir/pyew"

  install -dm755 "$pkgdir/usr/bin"
  install -dm755 "$pkgdir/usr/share/pyew"

  for i in *.py
  do
    install -Dm755 $i "$pkgdir/usr/share/pyew/$i"
  done

  install -Dm755 pyew "$pkgdir/usr/share/pyew/pyew"

  for i in Elf anal contrib logo plugins scripts
  do
    install -dm755 "$pkgdir/usr/share/pyew/$i"
    cp -a $i/* "$pkgdir/usr/share/pyew/$i/";
  done

  install -Dm644 LICENSE "$pkgdir/usr/share/licenses/pyew/LICENSE"
  install -Dm644 ChangeLog "$pkgdir/usr/share/doc/pyew/ChangeLog"
  install -Dm644 AUTHORS "$pkgdir/usr/share/doc/pyew/AUTHORS"
  install -Dm644 COPYING "$pkgdir/usr/share/doc/pyew/COPYING"

  cat > "$pkgdir/usr/bin/pyew" << EOF
#!/bin/sh
cd /usr/share/pyew
exec python2 pyew.py "\$@"
EOF

  chmod +x $pkgdir/usr/bin/pyew
}
