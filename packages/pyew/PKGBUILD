# This file is part of BlackArch Linux ( http://blackarch.org ).
# See COPYING for license details.

pkgname='pyew'
pkgver=104.4754f38
pkgrel=1
groups=('blackarch' 'blackarch-malware')
pkgdesc='A python tool to analyse malware.'
arch=('any')
url='https://code.google.com/p/pyew/'
license=('GPL2')
depends=('distorm' 'python2')
makedepends=('git')
source=('git+https://github.com/joxeankoret/pyew')
sha1sums=('SKIP')

pkgver() {
  cd "$srcdir/pyew"

  echo $(git rev-list --count HEAD).$(git rev-parse --short HEAD)
}

package() {
  cd "$srcdir/pyew"

  mkdir -p "$pkgdir/usr/bin"
  mkdir -p "$pkgdir/usr/share/pyew"

  for i in *.py
  do
    install -Dm755 $i "$pkgdir/usr/share/pyew/$i"
  done

  install -Dm755 pyew "$pkgdir/usr/share/pyew/pyew"

  for i in Elf anal contrib logo plugins scripts
  do
    mkdir -p "$pkgdir/usr/share/pyew/$i"
    cp -a $i/* "$pkgdir/usr/share/pyew/$i/";
  done

  install -Dm644 LICENSE "$pkgdir/usr/share/licenses/pyew/LICENSE"
  install -Dm644 ChangeLog "$pkgdir/usr/share/doc/pyew/ChangeLog"
  install -m644 AUTHORS "$pkgdir/usr/share/doc/pyew/AUTHORS"
  install -m644 COPYING "$pkgdir/usr/share/doc/pyew/COPYING"

  cat > "$pkgdir/usr/bin/pyew" << EOF
#!/bin/sh
cd /usr/share/pyew
exec python2 pyew.py "\$@"
EOF

  chmod +x "$pkgdir/usr/bin/pyew"
}
