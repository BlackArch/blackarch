# This file is part of BlackArch Linux ( https://www.blackarch.org/ ).
# See COPYING for license details.

pkgname=manul
pkgver=197.f525df9
pkgrel=2
pkgdesc='A coverage-guided parallel fuzzer for open-source and blackbox binaries on Windows, Linux and MacOS.'
groups=('blackarch' 'blackarch-fuzzer' 'blackarch-binary')
arch=('any')
url='https://github.com/mxmssh/manul'
license=('Apache')
depends=('python' 'python-psutil')
makedepends=('git')
source=("git+https://github.com/mxmssh/$pkgname.git")
sha512sums=('SKIP')

pkgver() {
  cd $pkgname

  echo $(git rev-list --count HEAD).$(git rev-parse --short HEAD)
}

package() {
  cd $pkgname

  install -dm 755 "$pkgdir/usr/bin"
  install -dm 755 "$pkgdir/usr/share/$pkgname"

  install -Dm 644 -t "$pkgdir/usr/share/doc/$pkgname/" README.md
  install -Dm 644 LICENSE "$pkgdir/usr/share/licenses/$pkgname/LICENSE"

  rm README.md LICENSE

  cp -a * "$pkgdir/usr/share/$pkgname/"

  cat > "$pkgdir/usr/bin/$pkgname" << EOF
#!/bin/sh
cd /usr/share/$pkgname
exec python $pkgname.py "\$@"
EOF

  chmod +x "$pkgdir/usr/bin/$pkgname"
}

