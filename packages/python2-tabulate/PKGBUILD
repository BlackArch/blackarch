# This file is part of BlackArch Linux ( https://www.blackarch.org/ ).
# See COPYING for license details.

pkgname=python2-tabulate
pkgver=0.8.3
pkgrel=3
pkgdesc='Pretty-print tabular data in Python, a library and a command-line utility.'
arch=('any')
url='https://bitbucket.org/astanin/python-tabulate'
license=('MIT')
depends=('python2' 'python2-setuptools')
makedepends=('python2-setuptools')
checkdepends=('python2-nose' 'python2-numpy' 'python2-pandas' 'python2-wcwidth')
optdepends=('python2-numpy: NumPy array support'
            'python2-pandas: pandas.DataFrame support'
            'python2-wcwidth: wide-character support')
source=("https://files.pythonhosted.org/packages/c2/fd/202954b3f0eb896c53b7b6f07390851b1fd2ca84aa95880d7ae4f434c4ac/tabulate-$pkgver.tar.gz")
sha512sums=('7f0205a4d80ed0b426a45622a3b8ec0a9b77cf6d67c1207db7fbc20a12e83a15748e63d5f8e5e65fa348b6265fd0cac93be4d04f9c1c182476b93507e1131cbf')

build() {
  cd "tabulate-$pkgver"

  python2 setup.py build
}

package() {
  cd "tabulate-$pkgver"

  python2 setup.py install --prefix=/usr --root="$pkgdir" --skip-build \
    --optimize=1

  install -Dm 644 README.rst "$pkgdir/usr/share/doc/$pkgname/README.rst"
  install -Dm 644 LICENSE "$pkgdir/usr/share/licenses/$pkgname/LICENSE"
}

