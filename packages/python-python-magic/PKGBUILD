# This file is part of BlackArch Linux ( https://www.blackarch.org/ ).
# See COPYING for license details.

pkgbase=python-python-magic
pkgname=('python2-python-magic' 'python-python-magic')
_pkgname=python-magic
pkgver=0.4.26
_pyver=3.10
pkgrel=1
pkgdesc='File type identification using libmagic.'
arch=('any')
url='https://pypi.org/project/python-magic/#files'
license=('MIT')
makedepends=('python2-setuptools' 'python-setuptools' 'python2-pip' 'python-pip'
             'python2-wheel' 'python-wheel')
source=("https://files.pythonhosted.org/packages/ac/68/2e05edacfbf350cf8c7a4c382aed89a918be03a8f27383de77aa3c3b53df/$_pkgname-$pkgver.tar.gz")
sha512sums=('4f7c968e0c38360b5fea933c60a74ebbfa044f4f56c41eecb34bc69c3b044f39e712c4cac92295ab8764d5c9ed282148d312ce030a3772f8cca7aad79b4df92b')

prepare() {
  cp -a "$_pkgname-$pkgver"{,-2}
}

build() {
  cd "$_pkgname-$pkgver-2"

  python2 setup.py build

  cd "$srcdir/$_pkgname-$pkgver"

  python setup.py build
}

package_python2-python-magic() {
  depends=('python2' 'python2-libmagic')

  cd "$_pkgname-$pkgver"

  python2 setup.py install --root="$pkgdir" --prefix=/usr -O1 --skip-build

  mkdir -p "$pkgdir/usr/lib/python2.7/site-packages/"{python-magic,python_magic}
  cp -af magic.py* "$pkgdir/usr/lib/python2.7/site-packages/python-magic/"
  cp -af magic.py* "$pkgdir/usr/lib/python2.7/site-packages/python_magic/"
  find $pkgdir -type d -name '__pycache__' -exec rm -rf {} +
  rm -f "$pkgdir/usr/lib/python2.7/site-packages/magic."{py,pyc,pyo}
}

package_python-python-magic() {
  depends=('python' 'python-libmagic')

  cd "$_pkgname-$pkgver"

  python setup.py install --root="$pkgdir" --prefix=/usr -O1 --skip-build

  mkdir -p "$pkgdir/usr/lib/python$_pyver/site-packages/"{python-magic,python_magic}
  cp -af magic.py* "$pkgdir/usr/lib/python$_pyver/site-packages/python-magic/"
  cp -af magic.py* "$pkgdir/usr/lib/python$_pyver/site-packages/python_magic/"
  find $pkgdir -type d -name '__pycache__' -exec rm -rf {} +
  rm -f "$pkgdir/usr/lib/python$_pyver/site-packages/magic.py"
}

