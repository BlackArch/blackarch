# This file is part of BlackArch Linux ( https://www.blackarch.org/ ).
# See COPYING for license details.

pkgbase=python-python-magic
pkgname=('python2-python-magic' 'python-python-magic')
_pkgname=python-magic
pkgver=0.4.24
_pyver=3.10
pkgrel=3
pkgdesc='File type identification using libmagic.'
arch=('any')
url='https://pypi.org/project/python-magic/#files'
license=('MIT')
makedepends=('python2-setuptools' 'python-setuptools')
source=("https://files.pythonhosted.org/packages/3a/70/76b185393fecf78f81c12f9dc7b1df814df785f6acb545fc92b016e75a7e/$_pkgname-$pkgver.tar.gz")
sha512sums=('1d1dfea5b7b4b8246ddff7c25bec6df500423f0d26bbc08d490aa7120e6a66c50650ed6a52e958592056a59fcbd02af53c614a77aa7d43898de3ff90d2ca16de')

prepare() {
  cp -a "$_pkgname-$pkgver"{,-2}
}

build() {
  cd "$_pkgname-$pkgver-2"

  python2 setup.py build

  cd "$srcdir/$_pkgname-$pkgver"

  python setup.py build
}

package_python2-python-magic() {
  depends=('python2' 'python2-libmagic')

  cd "$_pkgname-$pkgver"

  python2 setup.py install --root="$pkgdir" --prefix=/usr -O1 --skip-build

  mkdir -p "$pkgdir/usr/lib/python2.7/site-packages/"{python-magic,python_magic}
  cp -a magic.py* "$pkgdir/usr/lib/python2.7/site-packages/python-magic/"
  cp -a magic.py* "$pkgdir/usr/lib/python2.7/site-packages/python_magic/"
  find $pkgdir -type d -name '__pycache__' -exec rm -rf {} +
  rm -f "$pkgdir/usr/lib/python2.7/site-packages/magic."{py,pyc,pyo}
}

package_python-python-magic() {
  depends=('python' 'python-libmagic')

  cd "$_pkgname-$pkgver"

  python setup.py install --root="$pkgdir" --prefix=/usr -O1 --skip-build

  mkdir -p "$pkgdir/usr/lib/python$_pyver/site-packages/"{python-magic,python_magic}
  cp -a magic.py* "$pkgdir/usr/lib/python$_pyver/site-packages/python-magic/"
  cp -a magic.py* "$pkgdir/usr/lib/python$_pyver/site-packages/python_magic/"
  find $pkgdir -type d -name '__pycache__' -exec rm -rf {} +
  rm -f "$pkgdir/usr/lib/python$_pyver/site-packages/magic.py"
}

