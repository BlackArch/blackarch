# This file is part of BlackArch Linux ( https://www.blackarch.org/ ).
# See COPYING for license details.

pkgbase=python-python-magic
pkgname=('python2-python-magic' 'python-python-magic')
_pkgname=python-magic
pkgver=0.4.15
pkgrel=6
pkgdesc='File type identification using libmagic.'
arch=('any')
url='https://pypi.org/project/python-magic/#files'
license=('MIT')
makedepends=('python2-setuptools' 'python-setuptools')
source=("https://files.pythonhosted.org/packages/84/30/80932401906eaf787f2e9bd86dc458f1d2e75b064b4c187341f29516945c/$_pkgname-$pkgver.tar.gz")
sha512sums=('df028669209e02cde06f4db0481d84c316cefc191273ab3c749e88a23c92cb79a703077f601a0cc194fccbbcf62b38de8bab79290827a8505bc06e2f4aa0c2bb')

prepare() {
  cp -a "$_pkgname-$pkgver"{,-2}
}

build() {
  cd "$_pkgname-$pkgver-2"

  python2 setup.py build

  cd "$srcdir/$_pkgname-$pkgver"

  python setup.py build
}

package_python2-python-magic() {
  depends=('python2' 'python2-libmagic')

  cd "$_pkgname-$pkgver"

  python2 setup.py install --root="$pkgdir" --prefix=/usr -O1 --skip-build

  mkdir -p "$pkgdir/usr/lib/python2.7/site-packages/"{python-magic,python_magic}
  cp -a magic.py* "$pkgdir/usr/lib/python2.7/site-packages/python-magic/"
  cp -a magic.py* "$pkgdir/usr/lib/python2.7/site-packages/python_magic/"
  rm -rf "$pkgdir/usr/lib/python2.7/site-packages/"magic.*
  find $pkgdir -type d -name '__pycache__' -exec rm -rf {} +
}

package_python-python-magic() {
  depends=('python' 'python-libmagic')

  cd "$_pkgname-$pkgver"

  python setup.py install --root="$pkgdir" --prefix=/usr -O1 --skip-build

  mkdir -p "$pkgdir/usr/lib/python3.8/site-packages/"{python-magic,python_magic}
  cp -a magic.py* "$pkgdir/usr/lib/python3.8/site-packages/python-magic/"
  cp -a magic.py* "$pkgdir/usr/lib/python3.8/site-packages/python_magic/"
  rm -rf "$pkgdir/usr/lib/python3.8/site-packages/"magic.*
  find $pkgdir -type d -name '__pycache__' -exec rm -rf {} +
}

