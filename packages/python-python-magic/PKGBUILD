# This file is part of BlackArch Linux ( https://www.blackarch.org/ ).
# See COPYING for license details.

pkgbase=python-python-magic
pkgname=('python2-python-magic' 'python-python-magic')
_pkgname=python-magic
pkgver=0.4.22
pkgrel=1
pkgdesc='File type identification using libmagic.'
arch=('any')
url='https://pypi.org/project/python-magic/#files'
license=('MIT')
makedepends=('python2-setuptools' 'python-setuptools')
source=("https://files.pythonhosted.org/packages/26/60/6d45e0e7043f5a7bf15238ca451256a78d3c5fe02cd372f0ed6d888a16d5/$_pkgname-$pkgver.tar.gz")
sha512sums=('7c49ba87625abb2fab8f813de85978ce6e9732a5a4be425dc498221008a46a172fd900a42740929964cb0984f925c8d440f4795f5b18fed2b3f8f869092351ff')

prepare() {
  cp -a "$_pkgname-$pkgver"{,-2}
}

build() {
  cd "$_pkgname-$pkgver-2"

  python2 setup.py build

  cd "$srcdir/$_pkgname-$pkgver"

  python setup.py build
}

package_python2-python-magic() {
  depends=('python2' 'python2-libmagic')

  cd "$_pkgname-$pkgver"

  python2 setup.py install --root="$pkgdir" --prefix=/usr -O1 --skip-build

  mkdir -p "$pkgdir/usr/lib/python2.7/site-packages/"{python-magic,python_magic}
  cp -a magic.py* "$pkgdir/usr/lib/python2.7/site-packages/python-magic/"
  cp -a magic.py* "$pkgdir/usr/lib/python2.7/site-packages/python_magic/"
  find $pkgdir -type d -name '__pycache__' -exec rm -rf {} +
  rm -f "$pkgdir/usr/lib/python2.7/site-packages/magic."{py,pyc,pyo}
}

package_python-python-magic() {
  depends=('python' 'python-libmagic')

  cd "$_pkgname-$pkgver"

  python setup.py install --root="$pkgdir" --prefix=/usr -O1 --skip-build

  mkdir -p "$pkgdir/usr/lib/python3.9/site-packages/"{python-magic,python_magic}
  cp -a magic.py* "$pkgdir/usr/lib/python3.9/site-packages/python-magic/"
  cp -a magic.py* "$pkgdir/usr/lib/python3.9/site-packages/python_magic/"
  find $pkgdir -type d -name '__pycache__' -exec rm -rf {} +
  rm -f "$pkgdir/usr/lib/python3.9/site-packages/magic.py"
}

