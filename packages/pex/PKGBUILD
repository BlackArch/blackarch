# This file is part of BlackArch Linux ( https://www.blackarch.org/ ).
# See COPYING for license details.

pkgname=pex
pkgver=466.bfab73d
pkgrel=1
pkgdesc='A collection of special tools for providing high quality penetration testing.'
arch=('any')
url='https://pypi.org/project/pex/#files'
license=('MIT')
depends=('python' 'python-requests' 'python-paramiko' 'python-requests'
         'python-scapy' 'python-pydantic' 'python-netaddr'
         'python-alive-progress')
makedepends=('git' 'python-setuptools')
source=("$pkgname::git+https://github.com/EntySec/Pex.git")
sha512sums=('SKIP')

pkgver() {
  cd $pkgname

  echo $(git rev-list --count HEAD).$(git rev-parse --short HEAD)
}

build() {
  cd $pkgname

  python setup.py build
}

package() {
  cd $pkgname

  python setup.py install --root="$pkgdir" --prefix=/usr -O1 --skip-build

  install -Dm 644 -t "$pkgdir/usr/share/doc/$pkgname/" *.md
  install -Dm 644 LICENSE "$pkgdir/usr/share/licenses/$pkgname/LICENSE"
}

