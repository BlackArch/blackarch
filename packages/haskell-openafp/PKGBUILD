# This file is part of BlackArch Linux ( http://blackarch.org ).
# See COPYING for license details.

pkgname='haskell-openafp'
pkgver='1.4.2'
pkgrel=1
pkgdesc='IBM AFP document format parser and generator.'
arch=('x86_64' 'armv6h' 'armv7h' 'aarch64')
url='http://hackage.haskell.org/package/OpenAFP'
license=('GPL')
makedepends=('ghc')
depends=('haskell-regex-compat' 'haskell-mtl' 'haskell-hashable'
         'haskell-hashtables')
install='haskell-openafp.install'
source=("http://hackage.haskell.org/packages/archive/OpenAFP/$pkgver/OpenAFP-${pkgver}.tar.gz")
sha1sums=('c476d4ee8815054385ee758cdf5adc59f9b44140')
options=('staticlibs')

build() {
  cd "$srcdir/OpenAFP-$pkgver"

  runhaskell Setup configure -O -p --enable-split-objs --enable-shared \
    --prefix=/usr --docdir="/usr/share/doc/haskell-openafp" \
    --libsubdir=\$compiler/site-local/\$pkgid
  runhaskell Setup build
  runhaskell Setup haddock
  runhaskell Setup register   --gen-script
  runhaskell Setup unregister --gen-script

  sed -i -r -e "s|ghc-pkg.*unregister[^ ]* |&'--force' |" unregister.sh
}

package() {
  cd "$srcdir/OpenAFP-$pkgver"

  mkdir -p "$pkgdir/usr/share/doc/ghc/html/libraries"

  install -Dm744 register.sh \
    "$pkgdir/usr/share/haskell/haskell-openafp/register.sh"
  install -m744 unregister.sh \
    "$pkgdir/usr/share/haskell/haskell-openafp/unregister.sh"

  ln -s "/usr/share/doc/haskell-openafp/html" \
    "$pkgdir/usr/share/doc/ghc/html/libraries/OpenAFP"

  runhaskell Setup copy --destdir="$pkgdir"
}
