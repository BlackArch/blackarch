# This file is part of BlackArch Linux ( http://blackarch.org ).
# See COPYING for license details.

pkgname='haskell-openafp'
pkgver='1.3.2'
pkgrel=3
pkgdesc="IBM AFP document format parser and generator"
arch=('i686' 'x86_64' 'armv6h' 'armv7h')
url="http://hackage.haskell.org/package/OpenAFP"
license=('GPL')
makedepends=('ghc')
depends=('haskell-regex-compat' 'haskell-mtl')
install=haskell-openafp.install
source=(http://hackage.haskell.org/packages/archive/OpenAFP/$pkgver/OpenAFP-$pkgver.tar.gz)
sha1sums=('8f4d11a7dc8f033d93a6cb867d71bd50c23c0e61')
options=('staticlibs')

build() {
  cd "$srcdir/OpenAFP-$pkgver"

  runhaskell Setup configure -O -p --enable-split-objs --enable-shared \
    --prefix=/usr --docdir="/usr/share/doc/haskell-openafp" \
    --libsubdir=\$compiler/site-local/\$pkgid
  runhaskell Setup build
  runhaskell Setup haddock
  runhaskell Setup register   --gen-script
  runhaskell Setup unregister --gen-script
  sed -i -r -e "s|ghc-pkg.*unregister[^ ]* |&'--force' |" unregister.sh
}

package() {
  cd "$srcdir/OpenAFP-$pkgver"

  install -Dm744 register.sh   "$pkgdir/usr/share/haskell/haskell-openafp/register.sh"
  install -m744 unregister.sh "$pkgdir/usr/share/haskell/haskell-openafp/unregister.sh"
  install -dm755 "$pkgdir/usr/share/doc/ghc/html/libraries"
  ln -s "/usr/share/doc/haskell-openafp/html" "$pkgdir/usr/share/doc/ghc/html/libraries/OpenAFP"
  runhaskell Setup copy --destdir="$pkgdir"
}
