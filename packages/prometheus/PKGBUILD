# This file is part of BlackArch Linux ( http://blackarch.org ).
# See COPYING for license details.

pkgname='prometheus'
pkgver=176.a316d66
pkgrel=3
groups=('blackarch' 'blackarch-networking')
pkgdesc='A Firewall analyzer written in ruby'
arch=('any')
url='https://github.com/averagesecurityguy/prometheus'
license=('GPL2')
depends=('ruby')
makedepends=('git')
source=('git+https://github.com/averagesecurityguy/prometheus.git')
sha1sums=('SKIP')

pkgver() {
  cd "$srcdir/prometheus"

  echo $(git rev-list --count HEAD).$(git rev-parse --short HEAD)
}

package() {
  cd "$srcdir/prometheus"

  mkdir -p "$pkgdir/usr/bin"
  mkdir -p "$pkgdir/usr/share/prometheus"

  install -Dm644 README.md "$pkgdir/usr/share/doc/prometheus/README.md"
  install -Dm644 LICENSE "$pkgdir/usr/share/licenses/prometheus/LICENSE"

  rm README.md LICENSE

  cp -a * "$pkgdir/usr/share/prometheus"

  cat > "$pkgdir/usr/bin/prometheus" << EOF
#!/bin/sh
cd /usr/share/prometheus
exec ruby prometheus.rb "\$@"
EOF

  chmod +x "$pkgdir/usr/bin/prometheus"
}
