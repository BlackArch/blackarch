pkgname=perl-moose
_cpanname=Moose
pkgver='2.1005'
pkgrel='3'
pkgdesc="A postmodern object system for Perl 5"
arch=('i686' 'x86_64')
license=('PerlArtistic' 'GPL')
options=('!emptydirs')
depends=('perl-class-load>=0.09' 'perl-class-load-xs>=0.01'
         'perl-data-optlist>=0.107' 'perl-devel-globaldestruction'
         'perl-dist-checkconflicts>=0.02' 'perl-eval-closure>=0.04'
         'perl-list-moreutils>=0.28' 'perl-mro-compat>=0.05'
         'perl-package-deprecationmanager>=0.11' 'perl-package-stash>=0.32'
         'perl-package-stash-xs>=0.24' 'perl-params-util>=1.00'
         'perl-sub-exporter>=0.980' 'perl-sub-name>=0.05' 'perl-task-weaken'
         'perl-test-checkdeps' 'perl-try-tiny>=0.02')
makedepends=('perl-dist-checkconflicts>=0.02' 'perl-test-fatal>=0.001' 'perl-test-requires>=0.05' 'perl-test-without-module')
url="https://metacpan.org/release/$_cpanname"
source=("http://cpan.metacpan.org/authors/id/E/ET/ETHER/$_cpanname-$pkgver.tar.gz")
md5sums=('8878c5b28ee5312c31983f2de3167db1')

build() {
  cd "$srcdir/$_cpanname-$pkgver"
  PERL_MM_USE_DEFAULT=1 perl Makefile.PL INSTALLDIRS=vendor
  make
}

check() {
  cd "$srcdir/$_cpanname-$pkgver"
  export PERL_MM_USE_DEFAULT=1
  make test
}

package() {
  cd "$srcdir/$_cpanname-$pkgver"
  make install DESTDIR="$pkgdir"
  find "$pkgdir" -name '.packlist' -o -name '*.pod' -delete
}
