# This file is part of BlackArch Linux ( https://www.blackarch.org/ ).
# See COPYING for license details.

pkgname=uniflash
pkgver=8.7.0.4818
pkgrel=1
pkgdesc="Universal Flash Programmer for Texas Instruments devices. Provides a single interface for programming Flash memory and executing Flash based operations on supported targets."
arch=('x86_64' 'aarch64')
url="https://processors.wiki.ti.com/index.php/Category:CCS_UniFlash"
license=('custom:TECHNOLOGY SOFTWARE PUBLICLY AVAILABLE by Texas Instruments Incorporated')
depends=('gconf' 'libudev0-shim' 'libusb-compat' 'libcanberra')
optdepends=(
	'python2: The SimpleLink CC31xx/CC32xx families require Python2.7'
)
source=(${pkgname}_sl.$pkgver.run::https://dr-download.ti.com/software-development/software-programming-tool/MD-QeJBJLj8gq/8.7.0/${pkgname}_sl.$pkgver.run
		62-msp430uif.rules)
noextract=("${pkgname}_sl.$pkgver.run" )
options=(!strip)
sha512sums=('bd6e5933b3efaa3743f374ff1bdce4912236328be3a9105e9d7bda4f4c04b19b56bd61f20da8e0ab60a3f64a525b0a337d990023108e02bdd679e02d85492799'
            '144993d8e63a7b9c30f5f41acc578513a78f92bf5180124b6d755069045194b935daeec294749be5013cfdfb21f7f00ee3152392d1b3d886f0ce20d58e41cd31')
DLAGENTS=('https::/usr/bin/curl -fLC - --cookie nada -o %o %u')

prepare() {
  cd "$srcdir"
 
  echo "If you continue you will accept the license, more information at https://processors.wiki.ti.com/index.php/Category:CCS_UniFlash."
  chmod +x ${pkgname}_sl.$pkgver.run
}

package() {
  cd "$srcdir"
 
  ./${pkgname}_sl.$pkgver.run --unattendedmodeui none --mode unattended --prefix $pkgdir/opt/ti/uniflash
  cd "$pkgdir/opt/ti/uniflash"

  sed -s "s|$pkgdir||g" -i UniFlash.desktop
  install -d "$pkgdir/usr/share/applications"
  install -m 644 "$pkgdir/opt/ti/uniflash/UniFlash.desktop" "$pkgdir/usr/share/applications/UniFlash.desktop"

  cd "TICloudAgentHostApp/install_scripts/"
  install -d "$pkgdir/etc/udev/rules.d/"
  install -m 644 -D "70-mm-no-ti-emulators.rules" "$pkgdir/etc/udev/rules.d/72-mm-no-ti-emulators.rules"
  install -m 644 -D "71-ti-permissions.rules" "$pkgdir/etc/udev/rules.d/73-ti-permissions.rules"
  install -m 644 "$srcdir/62-msp430uif.rules" "$pkgdir/etc/udev/rules.d/62-msp430uif.rules"
}

