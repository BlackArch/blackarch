# This file is part of BlackArch Linux ( https://www.blackarch.org/ ).
# See COPYING for license details.

_pkgname=gOSINT
pkgname=gosint
pkgver=196.9c86ed2
pkgrel=1
pkgdesc='OSINT framework in Go.'
groups=('blackarch' 'blackarch-recon')
arch=('x86_64' 'aarch64')
url='https://github.com/Nhoya/gOSINT'
license=('GPL3')
depends=('glibc')
makedepends=('git' 'go-pie' 'dep' 'tesseract')
source=("git+https://github.com/Nhoya/$_pkgname.git")
sha512sums=('SKIP')

pkgver() {
  cd $_pkgname

  echo $(git rev-list --count HEAD).$(git rev-parse --short HEAD)
}

prepare() {
  mkdir -p "gopath/src/github.com/Nhoya"
  ln -rTsf $_pkgname "gopath/src/github.com/Nhoya/$_pkgname"

  export GOPATH="$srcdir/gopath"
  cd "$GOPATH/src/github.com/Nhoya/$_pkgname"

  dep ensure
}

build() {
  export GOPATH="$srcdir/gopath"
  
  cd "$GOPATH/src/github.com/Nhoya/$_pkgname"
  go build -o $pkgname -v -ldflags="-s -w" cmd/gosint/main.go
}

package() {
  cd "$_pkgname"

  install -Dm 755 $pkgname "$pkgdir/usr/bin/$pkgname"
  install -Dm 755 LICENSE "$pkgdir/usr/share/licenses/$pkgname/LICENSE"
  install -Dm 755 README.md "$pkgdir/usr/share/doc/$pkgname/README"
}

