# Maintainer: lsfxz (https://github.com/lsfxz/quarry)
# Many thanks to anatol for quarry and gem2arch!
# This file is part of BlackArch Linux ( http://blackarch.org ).
# See COPYING for license details.
pkgname='ruby-celluloid'
_gemname='celluloid'
pkgver='0.17.3'
pkgrel=2
pkgdesc="Actor-based concurrent object framework for Ruby"
arch=('any')
url='https://github.com/celluloid/celluloid'
license=("MIT")
depends=('ruby' 'ruby-celluloid-essentials' 'ruby-celluloid-extras' 'ruby-celluloid-fsm' 'ruby-celluloid-pool' 'ruby-celluloid-supervision' 'ruby-timers')
options=('!emptydirs')
source=("https://rubygems.org/downloads/$_gemname-$pkgver.gem" )
noextract=("$_gemname-$pkgver.gem")
sha1sums=('bca83104beee5600b014108270a5d25218da2a91' )


package() {
  local _gemdir="$(ruby -rubygems -e 'puts Gem.default_dir')"
  gem install --ignore-dependencies --no-document --no-user-install -i "$pkgdir/$_gemdir" -n "$pkgdir"/usr/bin $_gemname-$pkgver.gem 
  rm "$pkgdir/$_gemdir/cache/$_gemname-$pkgver.gem"
  install -D -m644 "$pkgdir/$_gemdir/gems/$_gemname-$pkgver/LICENSE.txt" "$pkgdir/usr/share/licenses/$pkgname/LICENSE.txt"

  rm -rf "$pkgdir/$_gemdir/gems/$_gemname-$pkgver"/{CHANGES.md,LICENSE.txt,README.md,culture,examples,spec}
}
