# This file is part of BlackArch Linux ( https://www.blackarch.org/ ).
# See COPYING for license details.

pkgname=injectus
pkgver=11.f63590c
pkgrel=2
pkgdesc='CRLF and open redirect fuzzer.'
groups=('blackarch' 'blackarch-webapp' 'blackarch-scanner' 'blackarch-fuzzer')
arch=('any')
url='https://github.com/BountyStrike/Injectus'
license=('custom:unknown')
depends=('python' 'python-aiohttp' 'python-aiodns' 'python-aiofiles')
makedepends=('git')
source=("$pkgname::git+https://github.com/BountyStrike/Injectus.git")
sha512sums=('SKIP')

pkgver() {
  cd $pkgname

  echo $(git rev-list --count HEAD).$(git rev-parse --short HEAD)
}

package() {
  cd $pkgname

  install -dm 755 "$pkgdir/usr/bin"
  install -dm 755 "$pkgdir/usr/share/$pkgname"

  install -Dm 644 -t "$pkgdir/usr/share/doc/$pkgname/" README.md

  rm README.md requirements.txt

  cp -a * "$pkgdir/usr/share/$pkgname/"

  cat > "$pkgdir/usr/bin/$pkgname" << EOF
#!/bin/sh
cd /usr/share/$pkgname
exec python Injectus.py "\$@"
EOF

  chmod +x "$pkgdir/usr/bin/$pkgname"
}

