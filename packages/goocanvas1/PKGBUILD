# This file is part of BlackArch Linux ( https://www.blackarch.org/ ).
# See COPYING for license details.

pkgname=goocanvas1
_pkgname=goocanvas
pkgver=1.0.0
pkgrel=1
pkgdesc='A cairo canvas widget for GTK+.'
arch=('x86_64' 'aarch64')
url='http://live.gnome.org/GooCanvas'
options=('docs')
license=('LGPL')
makedepends=('python')
depends=('gtk2')
source=("http://ftp.gnome.org/pub/GNOME/sources/$_pkgname/1.0/$_pkgname-$pkgver.tar.bz2")
sha512sums=('fef4364b57dc777b6dcceb026f940121a24a8a3792e8d37d320eb47b7747ab0d54d8d7911b4ff77c21ca2b251937c9312da04221bbc246a07bbf4062689de644')

build() {
  cd "$_pkgname-$pkgver"

  if [[ "$CARCH" = "aarch64" ]]; then
    ./configure --prefix=/usr --disable-static --build=arm
  else
    ./configure --prefix=/usr --disable-static --build=x86_64
  fi

  make
}

package() {
  cd "$_pkgname-$pkgver"

  make DESTDIR="$pkgdir" install
}

