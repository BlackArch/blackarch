# This file is part of BlackArch Linux ( https://www.blackarch.org/ ).
# See COPYING for license details.

pkgname=massbleed
pkgver=16.cf7c5d6
pkgrel=2
pkgdesc='SSL Vulnerability Scanner.'
groups=('blackarch' 'blackarch-recon' 'blackarch-automation'
         'blackarch-scanner')
url='https://github.com/1N3/Sn1per'
license=('custom:unknown')
arch=('any')
depends=('unicornscan' 'nmap' 'python' 'sslscan' 'bash' 'testssl.sh')
makedepends=('git')
source=('git+https://github.com/1N3/MassBleed.git')
install='massbleed.install'
sha512sums=('SKIP')

pkgver() {
  cd MassBleed

  echo $(git rev-list --count HEAD).$(git rev-parse --short HEAD)
}

package() {
  cd MassBleed

  install -dm 755 "$pkgdir/usr/bin"
  install -dm 755 "$pkgdir/usr/share/$pkgname"

  install -Dm 644 -t "$pkgdir/usr/share/doc/$pkgname/" README.md

  rm *.md

  cp -a * "$pkgdir/usr/share/$pkgname/"

  cat > "$pkgdir/usr/bin/$pkgname" << EOF
#!/bin/sh
cd /usr/share/$pkgname
exec bash ./massbleed "\$@"
EOF

  chmod +x "$pkgdir/usr/bin/$pkgname"
}

