# This file is part of BlackArch Linux ( https://www.blackarch.org/ ).
# See COPYING for license details.

pkgbase=python-flask-cors
pkgname=('python2-flask-cors' 'python-flask-cors')
_pkgname=Flask-Cors
pkgver=3.0.8
pkgrel=3
pkgdesc='A Flask extension adding a decorator for CORS support.'
arch=('any')
url='https://pypi.org/project/Flask-Cors/#files'
license=('MIT')
makedepends=('python2-setuptools' 'python-setuptools')
source=("https://files.pythonhosted.org/packages/9e/11/ca8b95c5bf9644471601e425f0de8cbd09a506bb6c24842cb17a6cd1eea8/$_pkgname-$pkgver.tar.gz")
sha512sums=('1cbc6848af4d74e73e84b4ab3df7f41c48b558917599ab4eaa8b9edc36e4685118761ad045bb3a8a5879a634c1b36d4e33b81e36fbe54d5b2a45f297b2c332b7')

prepare() {
  cp -a "$_pkgname-$pkgver"{,-2}
}

build() {
  cd "$_pkgname-$pkgver-2"

  python2 setup.py build

  cd "$srcdir/$_pkgname-$pkgver"

  python setup.py build
}

package_python2-flask-cors() {
  depends=('python2-flask' 'python2-six')

  cd "$_pkgname-$pkgver-2"

  python2 setup.py install --prefix=/usr --root="$pkgdir" --optimize=1 \
    --skip-build
}

package_python-flask-cors() {
  depends=('python-flask' 'python-six')

  cd "$_pkgname-$pkgver"

  python setup.py install --prefix=/usr --root="$pkgdir" --optimize=1 \
    --skip-build
}

