# This file is part of BlackArch Linux ( https://www.blackarch.org/ ).
# See COPYING for license details.

pkgbase=python-flask-cors
pkgname=('python2-flask-cors' 'python-flask-cors')
_pkgname=Flask-Cors
pkgver=3.0.10
pkgrel=1
pkgdesc='A Flask extension adding a decorator for CORS support.'
arch=('any')
url='https://pypi.org/project/Flask-Cors/#files'
license=('MIT')
makedepends=('python2-setuptools' 'python-setuptools')
source=("https://files.pythonhosted.org/packages/cf/25/e3b2553d22ed542be807739556c69621ad2ab276ae8d5d2560f4ed20f652/Flask-Cors-$pkgver.tar.gz")
sha512sums=('5a44f30936c34901e99060b19860e63835f6621311beecdeb23181bcf1ee99034e751cce29163cb7dd1abdccacc4ae34e491055d6fe7b995a0e635e1520ba6e9')

prepare() {
  cp -a "$_pkgname-$pkgver"{,-2}
}

build() {
  cd "$_pkgname-$pkgver-2"

  python2 setup.py build

  cd "$srcdir/$_pkgname-$pkgver"

  python setup.py build
}

package_python2-flask-cors() {
  depends=('python2-flask' 'python2-six')

  cd "$_pkgname-$pkgver-2"

  python2 setup.py install --prefix=/usr --root="$pkgdir" --optimize=1 \
    --skip-build
}

package_python-flask-cors() {
  depends=('python-flask' 'python-six')

  cd "$_pkgname-$pkgver"

  python setup.py install --prefix=/usr --root="$pkgdir" --optimize=1 \
    --skip-build
}

