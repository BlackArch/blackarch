# This file is part of BlackArch Linux ( https://www.blackarch.org/ ).
# See COPYING for license details.

pkgbase=python-flask-cors
pkgname=('python2-flask-cors' 'python-flask-cors')
_pkgname=Flask-Cors
pkgver=3.0.9
pkgrel=2
pkgdesc='A Flask extension adding a decorator for CORS support.'
arch=('any')
url='https://pypi.org/project/Flask-Cors/#files'
license=('MIT')
makedepends=('python2-setuptools' 'python-setuptools')
source=("https://files.pythonhosted.org/packages/99/fc/cd117ea122e28037a5ec60356a7ffae8b77af527713f7b5e4eb63089f669/Flask-Cors-$pkgver.tar.gz")
sha512sums=('befa4a7ad0e7da4404f4b7ee5c2f06cadfb91e64501cef5e9083084968dd86135b7048b4d3e31ca65d0719813334ad5f4641734f21c20c519ccf438d92584bc3')

prepare() {
  cp -a "$_pkgname-$pkgver"{,-2}
}

build() {
  cd "$_pkgname-$pkgver-2"

  python2 setup.py build

  cd "$srcdir/$_pkgname-$pkgver"

  python setup.py build
}

package_python2-flask-cors() {
  depends=('python2-flask' 'python2-six')

  cd "$_pkgname-$pkgver-2"

  python2 setup.py install --prefix=/usr --root="$pkgdir" --optimize=1 \
    --skip-build
}

package_python-flask-cors() {
  depends=('python-flask' 'python-six')

  cd "$_pkgname-$pkgver"

  python setup.py install --prefix=/usr --root="$pkgdir" --optimize=1 \
    --skip-build
}

