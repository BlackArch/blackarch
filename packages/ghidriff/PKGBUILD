# This file is part of BlackArch Linux ( https://www.blackarch.org/ ).
# See COPYING for license details.

pkgname=ghidriff
pkgver=v1.0.0.r2.g53e16cf
pkgrel=1
pkgdesc='Python Command-Line Ghidra Binary Diffing Engine.'
arch=('x86_64' 'aarch64')
groups=('blackarch' 'blackarch-automation' 'blackarch-reversing')
url='https://github.com/clearbluejar/ghidriff'
license=('GPL-3.0-or-later')
depends=('python' 'python-pyghidra' 'python-mdutils')
makedepends=('git' 'python-setuptools')
source=("git+https://github.com/clearbluejar/$pkgname.git")
sha512sums=('SKIP')

pkgver() {
  cd $pkgname

  ( set -o pipefail
    git describe --long --tags --abbrev=7 2>/dev/null |
      sed 's/\([^-]*-g\)/r\1/;s/-/./g' ||
    printf "%s.%s" "$(git rev-list --count HEAD)" \
      "$(git rev-parse --short=7 HEAD)"
  )
}

build() {
  cd $pkgname

  python setup.py build
}

package() {
  cd $pkgname

  python setup.py install --root="$pkgdir" --prefix=/usr -O1 --skip-build
}

