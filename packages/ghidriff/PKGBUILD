# This file is part of BlackArch Linux ( https://www.blackarch.org/ ).
# See COPYING for license details.

pkgname=ghidriff
pkgver=v0.7.3.r0.g853ff79
pkgrel=1
pkgdesc='Python Command-Line Ghidra Binary Diffing Engine.'
arch=('any')
groups=('blackarch' 'blackarch-automation' 'blackarch-reversing')
url='https://github.com/clearbluejar/ghidriff'
license=('GPL3')
depends=('python' 'python-pyhidra' 'python-mdutils' 'ghidra')
makedepends=('python-setuptools')
source=("git+https://github.com/clearbluejar/$pkgname.git")
sha512sums=('SKIP')

pkgver() {
  cd $pkgname

  ( set -o pipefail
    git describe --long --tags --abbrev=7 2>/dev/null |
      sed 's/\([^-]*-g\)/r\1/;s/-/./g' ||
    printf "%s.%s" "$(git rev-list --count HEAD)" \
      "$(git rev-parse --short=7 HEAD)"
  )
}

build() {
  cd $pkgname

  python setup.py build
}

package() {
  cd $pkgname

  python setup.py install --root="$pkgdir" --prefix=/usr -O1 --skip-build
}

