# This file is part of BlackArch Linux ( http://blackarch.org ).
# See COPYING for license details.

pkgname='spike-proxy'
pkgver='148'
pkgrel=9
pkgdesc="A Proxy for detecting vulnerabilities in web applications"
arch=('any')
url="http://www.immunitysec.com/resources-freesoftware.shtml"
license=('GPL')
groups=('blackarch' 'blackarch-webapp')
depends=("python2" "python2-pyopenssl" "bash")
source=(http://www.immunitysec.com/downloads/SP148.tgz
        fix-encoding.patch)
sha1sums=('c755057bf345b285ef47e8ae664c911bba69625f'
          'c26fd70de945c78f8c017f04b9e9ad76cb06c962')

prepare(){
  cd "$srcdir/spkproxy"
  patch -Np1 -i $srcdir/fix-encoding.patch
}

package() {
  cd "$srcdir/spkproxy"

  # Base directories
  mkdir -p $pkgdir/usr/share/spike-proxy
  mkdir -p $pkgdir/usr/share/doc/spike-proxy
  mkdir -p $pkgdir/usr/bin

  mv *.txt $pkgdir/usr/share/doc/spike-proxy/
  cp -a --no-preserve=ownership * $pkgdir/usr/share/spike-proxy/
  cat > $pkgdir/usr/bin/spike-proxy << EOF
#!/bin/bash
cd /usr/share/spike-proxy
exec python2 ./spkproxy.py "\$@"
EOF
  chmod 755 $pkgdir/usr/bin/spike-proxy
}
