# This file is part of BlackArch Linux ( http://blackarch.org ).
# See COPYING for license details.

pkgname='haskell-iconv'
pkgver='0.4.1.1'
pkgrel=3
pkgdesc="Provides an interface to the POSIX iconv library functions for string encoding conversion"
arch=('i686' 'x86_64' 'armv6h' 'armv7h')
url="http://hackage.haskell.org/package/iconv"
license=('GPL')
makedepends=('ghc')
depends=('haskell-regex-compat' 'haskell-mtl')
install=haskell-iconv.install
source=(http://hackage.haskell.org/packages/archive/iconv/$pkgver/iconv-$pkgver.tar.gz)
sha1sums=('e1c9e4f27823eba9d978af9ce37716cef4c2c969')
options=('staticlibs')

build() {
  cd "$srcdir/iconv-$pkgver"

  runhaskell Setup configure -O -p --enable-split-objs --enable-shared \
    --prefix=/usr --docdir="/usr/share/doc/haskell-iconv" \
    --libsubdir=\$compiler/site-local/\$pkgid
  runhaskell Setup build
  runhaskell Setup haddock
  runhaskell Setup register   --gen-script
  runhaskell Setup unregister --gen-script
  sed -i -r -e "s|ghc-pkg.*unregister[^ ]* |&'--force' |" unregister.sh
}

package() {
  cd "$srcdir/iconv-$pkgver"

  install -Dm744 register.sh   "$pkgdir/usr/share/haskell/haskell-iconv/register.sh"
  install -m744 unregister.sh "$pkgdir/usr/share/haskell/haskell-iconv/unregister.sh"
  mkdir -p "$pkgdir/usr/share/doc/ghc/html/libraries"
  ln -s "/usr/share/doc/haskell-iconv/html" "$pkgdir/usr/share/doc/ghc/html/libraries/iconv"
  runhaskell Setup copy --destdir="$pkgdir"
}
