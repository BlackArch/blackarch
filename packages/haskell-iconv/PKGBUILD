# This file is part of BlackArch Linux ( http://blackarch.org ).
# See COPYING for license details.

pkgname='haskell-iconv'
pkgver='0.4.1.3'
pkgrel=1
pkgdesc='Provides an interface to the POSIX iconv library functions for string encoding conversion.'
arch=('x86_64' 'armv6h' 'armv7h' 'aarch64')
url='http://hackage.haskell.org/package/iconv'
license=('GPL')
makedepends=('ghc')
depends=('haskell-regex-compat' 'haskell-mtl')
install='haskell-iconv.install'
source=("http://hackage.haskell.org/packages/archive/iconv/$pkgver/iconv-${pkgver}.tar.gz")
sha1sums=('e7a4509f7c32d562e10fb6e98aeabfb1b4ccdd3e')
options=('staticlibs')

build() {
  cd "$srcdir/iconv-$pkgver"

  runhaskell Setup configure -O -p --enable-split-objs --enable-shared \
    --prefix=/usr --docdir="/usr/share/doc/haskell-iconv" \
    --libsubdir=\$compiler/site-local/\$pkgid
  runhaskell Setup build
  runhaskell Setup haddock
  runhaskell Setup register   --gen-script
  runhaskell Setup unregister --gen-script

  sed -i -r -e "s|ghc-pkg.*unregister[^ ]* |&'--force' |" unregister.sh
}

package() {
  cd "$srcdir/iconv-$pkgver"

  mkdir -p "$pkgdir/usr/share/doc/ghc/html/libraries"

  install -Dm744 register.sh \
    "$pkgdir/usr/share/haskell/haskell-iconv/register.sh"
  install -m744 unregister.sh \
    "$pkgdir/usr/share/haskell/haskell-iconv/unregister.sh"
  ln -s "/usr/share/doc/haskell-iconv/html" \
    "$pkgdir/usr/share/doc/ghc/html/libraries/iconv"

  runhaskell Setup copy --destdir="$pkgdir"
}
