# This file is part of BlackArch Linux ( http://blackarch.org ).
# See COPYING for license details.

pkgname='tweets-analyzer'
pkgver=38.09bfb84
pkgrel=1
pkgdesc='Tweets metadata scraper & activity analyzer.'
groups=('blackarch' 'blackarch-social' 'blackarch-recon')
arch=('any')
url='https://github.com/x0rz/tweets_analyzer'
license=('GPL3')
depends=('python2' 'python2-argparse' 'python2-tweepy' 'python2-numpy'
         'python2-tqdm' 'python2-ascii_graph')
makedepends=('git')
source=('git+https://github.com/x0rz/tweets_analyzer.git')
sha1sums=('SKIP')

pkgver() {
  cd "$srcdir/tweets_analyzer"

  echo $(git rev-list --count HEAD).$(git rev-parse --short HEAD)
}

package() {
  cd "$srcdir/tweets_analyzer"

  mkdir -p "$pkgdir/usr/bin"
  mkdir -p "$pkgdir/usr/share/tweets-analyzer"

  install -Dm644 -t "$pkgdir/usr/share/doc/tweets-analyzer/" README.md
  install -Dm644 LICENSE "$pkgdir/usr/share/licenses/tweets-analyzer/LICENSE"

  rm README.md LICENSE

  cp -a * "$pkgdir/usr/share/tweets-analyzer/"

  cat > "$pkgdir/usr/bin/tweets-analyzer" << EOF
#!/bin/sh
cd /usr/share/tweets-analyzer
exec python2 tweets_analyzer.py "\${@}"
EOF

  chmod a+x "$pkgdir/usr/bin/tweets-analyzer"
}
