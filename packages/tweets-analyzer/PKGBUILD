# This file is part of BlackArch Linux ( https://www.blackarch.org/ ).
# See COPYING for license details.

pkgname=tweets-analyzer
pkgver=55.8d6bd3c
pkgrel=1
pkgdesc='Tweets metadata scraper & activity analyzer.'
groups=('blackarch' 'blackarch-social' 'blackarch-recon')
arch=('any')
url='https://github.com/x0rz/tweets_analyzer'
license=('GPL3')
depends=('python2' 'python2-argparse' 'python2-tweepy' 'python2-numpy'
         'python2-tqdm' 'python2-ascii_graph')
makedepends=('git')
source=("$pkgname::git+https://github.com/x0rz/tweets_analyzer.git")
sha512sums=('SKIP')

pkgver() {
  cd $pkgname

  echo $(git rev-list --count HEAD).$(git rev-parse --short HEAD)
}

package() {
  cd $pkgname

  install -dm 755 "$pkgdir/usr/bin"
  install -dm 755 "$pkgdir/usr/share/$pkgname"

  install -Dm 644 -t "$pkgdir/usr/share/doc/$pkgname/" README.md
  install -Dm 644 LICENSE "$pkgdir/usr/share/licenses/$pkgname/LICENSE"

  rm README.md LICENSE

  cp -a * "$pkgdir/usr/share/$pkgname/"

  cat > "$pkgdir/usr/bin/$pkgname" << EOF
#!/bin/sh
cd /usr/share/$pkgname
exec python2 tweets_analyzer.py "\$@"
EOF

  chmod +x "$pkgdir/usr/bin/$pkgname"
}

