# This file is part of BlackArch Linux ( https://www.blackarch.org/ ).
# See COPYING for license details.

pkgname=ifchk
pkgver=1.1.1
pkgrel=1
groups=('blackarch' 'blackarch-defensive' 'blackarch-networking')
pkgdesc='A network interface promiscuous mode detection tool.'
arch=('x86_64' 'aarch64')
url='http://www.noorg.org/ifchk/'
license=('GPL2')
depends=('sysfsutils')
source=("http://www.noorg.org/ifchk/dist/$pkgname-$pkgver.tar.gz")
sha512sums=('8331007d2f8dcaa138f9b067d69d439a5147a5c8a9a191f4fcb3740537ce50869a88f3e118c58914654a5c5a3f30a3a624f583a32e72a0424f79ebbc953699b5')

build() {
  cd "$pkgname-$pkgver"

  make
}

package() {
  cd "$pkgname-$pkgver"

  install -Dm 755 $pkgname "$pkgdir/usr/bin/$pkgname"
  install -Dm 644 "$pkgname.conf" "$pkgdir/usr/share/$pkgname/ifchk.conf"
  install -Dm 644 "docs/$pkgname.1" "$pkgdir/usr/share/man/man1/ifchk.1"
  install -Dm 644 "docs/$pkgname.conf.5" \
    "$pkgdir/usr/share/man/man5/ifchk.conf.5"
  install -Dm 644 -t "$pkgdir/usr/share/doc/$pkgname/" CHANGES MANIFEST \
    README_FIRST papers/*
  install -Dm 644 COPYING "$pkgdir/usr/share/licenses/$pkgname/COPYING"
}

