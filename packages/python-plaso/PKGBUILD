# This file is part of BlackArch Linux ( https://www.blackarch.org/ ).
# See COPYING for license details.

pkgbase=python-plaso
pkgname=('python2-plaso' 'python-plaso')
_pkgname=plaso
pkgver=20190916
pkgrel=1
pkgdesc='Super timeline all the things.'
arch=('x86_64' 'aarch64')
url='https://github.com/log2timeline/plaso'
license=('Apache')
makedepends=('python2-setuptools' 'python-setuptools')
replaces=('python2-plaso')
source=("https://files.pythonhosted.org/packages/b4/e5/7b48fb0148988260e0e6e63d5b6a167878df4d4b711b30547a6a4e39afb7/$_pkgname-$pkgver.tar.gz")
sha512sums=('65821810ed0abb17a1981ff6793518044e6e1d99f986fc3d6bca68a99fe49e2f9c62ddc15c835e3740907e8e256761b661c9055124a66fbe0f5213258cc4fd92')

package_python2-plaso() {
  depends=('python2' 'python2-artifacts' 'python2-backports.lzma'
    'python2-bencode.py' 'python2-biplist' 'python2-certifi' 'python2-chardet'
    'python2-dfdatetime' 'python2-dfvfs' 'python2-dfwinreg' 'python2-dtfabric'
    'python2-efilter' 'python2-elasticsearch' 'python2-future' 'python2-idna'
    'python2-libbde-python' 'python2-libesedb-python' 'python2-libevt-python'
    'python2-libevtx-python' 'python2-libewf-python' 'python2-libfsapfs-python'
    'python2-libfsntfs-python' 'python2-libfvde-python' 'python2-libfwnt-python'
    'python2-libfwsi-python' 'python2-liblnk-python' 'python2-libmsiecf-python'
    'python2-libolecf-python' 'python2-libqcow-python' 'python2-libregf-python'
    'python2-libscca-python' 'python2-libsigscan-python'
    'python2-libsmdev-python' 'python2-libsmraw-python' 'python2-libvhdi-python'
    'python2-libvmdk-python' 'python2-libvshadow-python'
    'python2-libvslvm-python' 'python2-lz4' 'python2-pefile' 'python2-pip'
    'python2-psutil' 'python2-pycryptodome' 'python2-pyparsing'
    'python2-dateutil' 'python2-pytsk3' 'python2-pytz' 'python2-pyaml'
    'python2-pyzmq' 'python2-requests' 'python2-six' 'python2-urllib3'
    'python2-xlsxwriter' 'python2-yara')
  conflicts=('python-plaso')

  cd "plaso-$pkgver"

  python2 setup.py install --root="$pkgdir" --optimize=1
}

package_python-plaso() {
  depends=('python' 'python-artifacts' 'python-bencode.py' 'python-biplist'
    'python-certifi' 'python-chardet'
    'python-dfdatetime' 'python-dfvfs' 'python-dfwinreg' 'python-dtfabric'
    'python-efilter' 'python-elasticsearch' 'python-future' 'python-idna'
    'python-libbde-python' 'python-libesedb-python' 'python-libevt-python'
    'python-libevtx-python' 'python-libewf-python' 'python-libfsapfs-python'
    'python-libfsntfs-python' 'python-libfvde-python' 'python-libfwnt-python'
    'python-libfwsi-python' 'python-liblnk-python' 'python-libmsiecf-python'
    'python-libolecf-python' 'python-libqcow-python' 'python-libregf-python'
    'python-libscca-python' 'python-libsigscan-python' 'python-libsmdev-python'
    'python-libsmraw-python' 'python-libvhdi-python' 'python-libvmdk-python'
    'python-libvshadow-python' 'python-libvslvm-python' 'python-lz4'
    'python-pefile' 'python-pip' 'python-psutil' 'python-pycryptodome'
    'python-pyparsing' 'python-dateutil' 'python-pytsk3' 'python-pytz'
    'python-pyaml' 'python-pyzmq' 'python-requests' 'python-six'
    'python-urllib3' 'python-xlsxwriter' 'python-yara')
  conflicts=('python2-plaso')

  cd "plaso-$pkgver"

  python setup.py install --root="$pkgdir" --optimize=1
}

