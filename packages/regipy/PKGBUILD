# This file is part of BlackArch Linux ( https://www.blackarch.org/ ).
# See COPYING for license details.

pkgname=regipy
pkgver=1.3.5
pkgrel=1
pkgdesc='Library for parsing offline registry hives.'
groups=('blackarch' 'blackarch-forensic')
arch=('any')
url='https://github.com/mkorman90/regipy'
license=('MIT')
depends=('python' 'python-construct' 'python-attrs' 'python-click' 'python-tqdm'
         'python-inflection' 'python-jsonlines' 'python-pytz' 'python-logbook'
         'python-tabulate')
makedepends=('python-setuptools')
source=("https://files.pythonhosted.org/packages/11/de/17eaaf91a41bcc549f8e11dc2baf1d7ade6e51fa3132226de2336fe4ea99/$pkgname-$pkgver.tar.gz")
sha512sums=('c8f54075ac66065f50019b9835f7a91151fb454b5f73dcd786a98ff0a8d9cb4ba59772d0f140970e1c605374dc299338a1783f169145d13177b863e66011d92e')

prepare() {
  cd "$pkgname-$pkgver"

  sed "s|==[0-9].*|\',|g" -i setup.py
  sed "31!s|jsonlines',|jsonlines'\],|" -i setup.py
}

build() {
  cd "$pkgname-$pkgver"

  python setup.py build
}

package() {
  cd "$pkgname-$pkgver"

  python setup.py install --root="$pkgdir" --prefix=/usr -O1 --skip-build
}

