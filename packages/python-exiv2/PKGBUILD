# This file is part of BlackArch Linux ( https://www.blackarch.org/ ).
# See COPYING for license details.

pkgbase=python-exiv2
pkgname=('python2-exiv2' 'python-exiv2')
_pkgname=pyexiv2
pkgver=0.3.2
pkgrel=22
pkgdesc='A Python binding to exiv2, the C++ library for manipulation of EXIF, IPTC and XMP image metadata.'
arch=('x86_64' 'aarch64')
url='http://tilloy.net/dev/pyexiv2'
license=('GPL')
depends=('python2' 'boost-libs' 'exiv2')
makedepends=('scons' 'boost')
source=("http://launchpad.net/$_pkgname/0.3.x/$pkgver/+download/$_pkgname-$pkgver.tar.bz2")
sha512sums=('d27bd6cf93ab6143b4d09e04d6d4a728f3cbcd6f4b80b2dd0bea19ae1a7cb7bb8be45c6f54e24488e8e933941bbddccc0ba79331d133a54b6ca632c73f84cce7')

prepare() {
  cp -a "$_pkgname-$pkgver"{,-2}
}

build() {
  cd "$_pkgname-$pkgver-2"

  scons

  cd "$srcdir/$_pkgname-$pkgver"

  scons
}

package_python2-exiv2() {
  depends=('python2' 'boost-libs' 'exiv2')

  cd "$_pkgname-$pkgver-2"

  scons DESTDIR="$pkgdir" install
}

package_python-exiv2() {
  depends=('python' 'boost-libs' 'exiv2')

  cd "$_pkgname-$pkgver"

  scons DESTDIR="$pkgdir" install
}

