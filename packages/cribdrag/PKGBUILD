# This file is part of BlackArch Linux ( https://www.blackarch.org/ ).
# See COPYING for license details.

pkgname=cribdrag
pkgver=5.2d27dbf
pkgrel=1
pkgdesc='An interactive crib dragging tool for cryptanalysis on ciphertext generated with reused or predictable stream cipher keys.'
groups=('blackarch' 'blackarch-crypto')
arch=('any')
url='https://github.com/SpiderLabs/cribdrag'
license=('GPL3')
depends=('python2')
makedepends=('git')
source=("git+https://github.com/SpiderLabs/$pkgname.git")
sha512sums=('SKIP')

pkgver() {
  cd $pkgname

  echo $(git rev-list --count HEAD).$(git rev-parse --short HEAD)
}

package() {
  cd $pkgname

  install -dm 755 "$pkgdir/usr/bin"
  install -dm 755 "$pkgdir/usr/share/$pkgname"

  install -Dm 644 -t "$pkgdir/usr/share/doc/$pkgname/" README.md
  install -Dm 644 LICENSE.txt "$pkgdir/usr/share/licenses/$pkgname/LICENSE"

  rm README.md LICENSE.txt

  cp -a * "$pkgdir/usr/share/$pkgname/"

  cat > "$pkgdir/usr/bin/$pkgname" << EOF
#!/bin/sh
cd /usr/share/$pkgname
exec python2 $pkgname.py "\$@"
EOF

  chmod +x "$pkgdir/usr/bin/$pkgname"

  cat > "$pkgdir/usr/bin/cribdrag-xorstrings" << EOF
#!/bin/sh
cd /usr/share/$pkgname
exec python2 xorstrings.py "\${@}"
EOF

  chmod +x "$pkgdir/usr/bin/cribdrag-xorstrings"
}

