# This file is part of BlackArch Linux ( http://blackarch.org ).
# See COPYING for license details.

pkgname='hackredis'
pkgver=1.67eeb6c
pkgrel=2
groups=('blackarch' 'blackarch-exploitation' 'blackarch-scanner')
pkgdesc='A simple tool to scan and exploit redis servers.'
arch=('any')
url='https://github.com/Ridter/hackredis'
license=('custom:unknown')
depends=('python2' 'python2-redis' 'python2-pexpect')
makedepends=('git')
source=('git+https://github.com/Ridter/hackredis.git')
sha1sums=('SKIP')

pkgver() {
  cd "$srcdir/hackredis"

  echo $(git rev-list --count HEAD).$(git rev-parse --short HEAD)
}

prepare() {
  cd "$srcdir/hackredis"

  sed -i 's|bin/python|bin/python2|' hackredis.py
}

package() {
  cd "$srcdir/hackredis"

  install -Dm755 hackredis.py "$pkgdir/usr/bin/hackredis"
  install -Dm644 -t "$pkgdir/usr/share/doc/hackredis/" README.MD
}
