# This file is part of BlackArch Linux ( https://www.blackarch.org/ ).
# See COPYING for license details.

pkgbase=python-mulpyplexer
pkgname=('python2-mulpyplexer' 'python-mulpyplexer')
_pkgname=mulpyplexer
pkgver=0.09
pkgrel=1
pkgdesc='Multiplex interactions with lists of python objects.'
arch=('any')
url='https://pypi.org/project/mulpyplexer/#files'
license=('BSD')
makedepends=('python2-setuptools' 'python-setuptools')
source=("https://files.pythonhosted.org/packages/d7/6f/f036d4fb9f5a511262345bb18215c70fa69a250aca2e5ffe1a9c7e4cb85c/$_pkgname-$pkgver.tar.gz")
sha512sums=('2621d02072c4233b4a48e2fad3ae7ad212e91015cd1fd3e82d016ca20ea8c339e1eaa4fa32f3f76ae9337ee07170029c23d0ffe86ecee18aa41dc58ed1ebdb28')

prepare() {
  cp -a "$_pkgname-$pkgver"{,-2}
}

build() {
  cd "$_pkgname-$pkgver-2"

  python2 setup.py build

  cd "$srcdir/$_pkgname-$pkgver"

  python setup.py build
}

package_python2-mulpyplexer() {
  depends=('python2')

  cd "$_pkgname-$pkgver-2"

  python2 setup.py install --prefix=/usr --root="$pkgdir" --optimize=1 \
    --skip-build
}

package_python-mulpyplexer() {
  depends=('python')

  cd "$_pkgname-$pkgver"

  python setup.py install --prefix=/usr --root="$pkgdir" --optimize=1 \
    --skip-build
}

