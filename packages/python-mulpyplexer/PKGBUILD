# This file is part of BlackArch Linux ( https://www.blackarch.org/ ).
# See COPYING for license details.

pkgbase=python-mulpyplexer
pkgname=('python2-mulpyplexer' 'python-mulpyplexer')
_pkgname=mulpyplexer
pkgver=0.08
pkgrel=6
pkgdesc='Multiplex interactions with lists of python objects.'
arch=('any')
url='https://pypi.org/project/mulpyplexer/#files'
license=('BSD')
makedepends=('python2-setuptools' 'python-setuptools')
source=("https://files.pythonhosted.org/packages/37/98/d9313a5f3d491729b7902ac70783022f08150e6036e7d1e565ea342db968/$_pkgname-$pkgver.tar.gz")
sha512sums=('6a8e3aaeaa42a1ec90bbd34aefa6c9c259e8b3e8a01e0824fce9cf4e82ae65aedec747eb1849ef7341bab144be739ae86378fdd825d4eefa3bbf89ca96a87ab3')

prepare() {
  cp -a "$_pkgname-$pkgver"{,-2}
}

build() {
  cd "$_pkgname-$pkgver-2"

  python2 setup.py build

  cd "$srcdir/$_pkgname-$pkgver"

  python setup.py build
}

package_python2-mulpyplexer() {
  depends=('python2')

  cd "$_pkgname-$pkgver-2"

  python2 setup.py install --prefix=/usr --root="$pkgdir" --optimize=1 \
    --skip-build
}

package_python-mulpyplexer() {
  depends=('python')

  cd "$_pkgname-$pkgver"

  python setup.py install --prefix=/usr --root="$pkgdir" --optimize=1 \
    --skip-build
}

