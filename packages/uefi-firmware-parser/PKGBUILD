# This file is part of BlackArch Linux ( https://www.blackarch.org/ ).
# See COPYING for license details.

pkgname=uefi-firmware-parser
pkgver=158.2f5c83a
pkgrel=1
groups=('blackarch' 'blackarch-firmware' 'blackarch-reversing')
pkgdesc='Parse BIOS/Intel ME/UEFI firmware related structures: Volumes, FileSystems, Files, etc'
arch=('x86_64' 'aarch64')
url='https://github.com/theopolis/uefi-firmware-parser'
license=('GPL3')
depends=('python2' 'python2-pefile')
makedepends=('git' 'python2-setuptools')
source=('git+https://github.com/theopolis/uefi-firmware-parser.git')
sha512sums=('SKIP')

pkgver() {
  cd $pkgname

  echo $(git rev-list --count HEAD).$(git rev-parse --short HEAD)
}

package() {
  cd $pkgname

  install -Dm 644 -t "$pkgdir/usr/share/doc/$pkgname" README.rst AUTHORS \
    requirements.txt
  install -Dm 644 LICENSE "$pkgdir/usr/share/licenses/$pkgname/LICENSE"

  python2 setup.py install --root="$pkgdir" --optimize=1
}

