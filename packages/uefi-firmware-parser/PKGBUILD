# This file is part of BlackArch Linux ( https://www.blackarch.org/ ).
# See COPYING for license details.

pkgname=uefi-firmware-parser
pkgver=171.37883ad
pkgrel=1
groups=('blackarch' 'blackarch-firmware' 'blackarch-reversing')
pkgdesc='Parse BIOS/Intel ME/UEFI firmware related structures: Volumes, FileSystems, Files, etc.'
arch=('x86_64' 'aarch64')
url='https://github.com/theopolis/uefi-firmware-parser'
license=('GPL3')
depends=('python' 'python-pefile')
makedepends=('git' 'python-setuptools')
source=("git+https://github.com/theopolis/$pkgname.git")
sha512sums=('SKIP')

pkgver() {
  cd $pkgname

  echo $(git rev-list --count HEAD).$(git rev-parse --short HEAD)
}

build() {
  cd $pkgname

  python setup.py build
}

package() {
  cd $pkgname

  python setup.py install --root="$pkgdir" --prefix=/usr -O1 --skip-build

  install -Dm 644 -t "$pkgdir/usr/share/doc/$pkgname" README.rst AUTHORS
  install -Dm 644 LICENSE "$pkgdir/usr/share/licenses/$pkgname/LICENSE"
}

