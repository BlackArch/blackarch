# This file is part of BlackArch Linux ( https://www.blackarch.org/ ).
# See COPYING for license details.

pkgbase=python-serial
pkgname=('python2-serial' 'python-serial')
_pkgname=serial
pkgver=0.0.97
pkgrel=1
pkgdesc='A framework for serializing/deserializing JSON/YAML/XML into python class instances and vice versa.'
arch=('any')
url='https://pypi.org/project/serial/#files'
license=('APACHE')
makedepends=('python2-setuptools' 'python-setuptools')
source=("https://files.pythonhosted.org/packages/9b/05/54c6fb8861d973ded4bc16f2cd4d85582a97c6fff81320dc1a281995cecc/$_pkgname-$pkgver.tar.gz")
sha512sums=('22eda419da8c8275e2c5c72b8ab7f26f17e3af0a70d229b2106690871da96881a6be703c3eda8614222ae93cc518329d90574f6d47351d17b1b2d76c9e38cc2a')

prepare() {
  cp -a "$_pkgname-$pkgver"{,-2}
}

build() {
  cd "$_pkgname-$pkgver-2"

  python2 setup.py build

  cd "$srcdir/$_pkgname-$pkgver"

  python setup.py build
}

package_python2-serial() {
  depends=('python2' 'python2-future' 'python2-yaml' 'python2-iso8601')

  cd "$_pkgname-$pkgver-2"

  python2 setup.py install --prefix=/usr --root="$pkgdir" -O1 --skip-build
}

package_python-serial() {
  depends=('python' 'python-future' 'python-yaml' 'python-iso8601')

  cd "$_pkgname-$pkgver"

  python setup.py install --prefix=/usr --root="$pkgdir" -O1 --skip-build
}

