# This file is part of BlackArch Linux ( https://www.blackarch.org/ ).
# See COPYING for license details.

pkgbase=python-rarfile
pkgname=('python2-rarfile' 'python-rarfile')
_pkgname=rarfile
pkgver=3.3
pkgrel=1
pkgdesc='RAR archive reader for Python.'
arch=('any')
url='https://pypi.org/project/rarfile/#files'
license=('ISC')
makedepends=('python2-setuptools' 'python-setuptools')
source=("https://files.pythonhosted.org/packages/59/66/d2a475dce12051fa93d80c07cb1aea663e6ab15afc2c2973ab53cd14a0f0/$_pkgname-$pkgver.tar.gz")
sha512sums=('6c66efae84d960bdda09e4d379744ebca7f5e280dcdc287183e22e7e633bbfd9b25f0672910e0ae80f4434b336aee6aa1036325786cbf82639bcff11ee34a02d')

prepare() {
  cp -a "$_pkgname-$pkgver"{,-2}
}

build() {
  cd "$_pkgname-$pkgver-2"

  python2 setup.py build

  cd "$srcdir/$_pkgname-$pkgver"

  python setup.py build
}

package_python2-rarfile() {
  depends=('python2')

  cd "$_pkgname-$pkgver-2"

  python2 setup.py install --prefix=/usr --root="$pkgdir" --optimize=1 \
    --skip-build
}

package_python-rarfile() {
  depends=('python')

  cd "$_pkgname-$pkgver"

  python setup.py install --prefix=/usr --root="$pkgdir" --optimize=1 \
    --skip-build
}

