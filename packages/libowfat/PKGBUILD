# This file is part of BlackArch Linux ( http://blackarch.org ).
# See COPYING for license details.

pkgname=libowfat
pkgver=0.29
pkgrel=6
pkgdesc="GPL reimplementation of libdjb"
arch=('i686' 'x86_64' 'armv6h' 'armv7h')
url='http://www.fefe.de/libowfat/'
depends=('dietlibc')
license=('GPL')
install=libowfat.install
source=("http://dl.fefe.de/libowfat-$pkgver.tar.bz2")
sha1sums=('f944ebac326f4a8bd1437ff995d0b8201280434c')

build() {
  cd "$srcdir/libowfat-$pkgver"
  DIET= make
}

package() {
  cd $srcdir/libowfat-$pkgver
  DIET= make "prefix=$pkgdir/usr/" "LIBDIR=$pkgdir/usr/lib" "MAN3DIR=$pkgdir/usr/share/man/man3" install

  cd "$pkgdir/usr/share/man/man3"
  mv buffer.3 buffer-libowfat.3

  # Fix a conflict with extra/tinycdb.
  rm "$pkgdir/usr/include/cdb.h"
}
