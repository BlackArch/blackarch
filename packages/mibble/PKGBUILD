# This file is part of BlackArch Linux ( https://www.blackarch.org/ ).
# See COPYING for license details.

pkgname=mibble
pkgver=424.47314e8
pkgrel=1
pkgdesc='An open-source SNMP MIB parser (or SMI parser) written in Java. It can be used to read SNMP MIB files as well as simple ASN.1 files.'
url='http://www.mibble.org/'
groups=('blackarch' 'blackarch-misc')
license=('GPL')
depends=('java-environment' 'bash')
arch=('any')
source=('git+https://github.com/cederberg/mibble.git')
sha512sums=('SKIP')

pkgver() {
  cd $pkgname

  echo $(git rev-list --count HEAD).$(git rev-parse --short HEAD)
}

package() {
  cd "$pkgname"

  install -dm 755 "$pkgdir/usr/bin"
  install -dm 755 "$pkgdir/usr/share/doc/$pkgname"
  install -dm 755 "$pkgdir/usr/share/$pkgname"

  mv doc/api "$pkgdir/usr/share/doc/$pkgname/"
  install -Dm 644 -t "$pkgdir/usr/share/doc/$pkgname/" doc/*
  install -Dm 644 LICENSE.txt "$pkgdir/usr/share/licenses/$pkgname/LICENSE"

  rm -rf doc LICENSE.txt

  cp -a * "$pkgdir/usr/share/$pkgname"

  cat > "$pkgdir/usr/bin/mibblebrowser" << EOF
#!/bin/sh
cd /usr/share/$pkgname/bin/
exec ./MibbleBrowser.sh "\$@"
EOF

  cat > "$pkgdir/usr/bin/mibbleprinter" << EOF
#!/bin/sh
cd /usr/share/$pkgname/bin/
exec ./MibblePrinter.sh "\$@"
EOF

  cat > "$pkgdir/usr/bin/mibblevalidator" << EOF
#!/bin/sh
cd /usr/share/$pkgname/bin/
exec ./MibbleValidator.sh "\$@"
EOF

  chmod +x "$pkgdir/usr/bin/$pkgname"*
}

