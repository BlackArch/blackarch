# This file is part of BlackArch Linux ( http://blackarch.org ).
# See COPYING for license details.

pkgname=mibble
pkgver=2.10.1
pkgrel=1
pkgdesc='An open-source SNMP MIB parser (or SMI parser) written in Java. It can be used to read SNMP MIB files as well as simple ASN.1 files.'
url='http://www.mibble.org/'
groups=('blackarch' 'blackarch-misc')
license=('GPL')
depends=('java-environment' 'bash')
arch=('any')
source=("https://github.com/cederberg/mibble/releases/download/v${pkgver}/mibble-${pkgver}.tar.gz")
sha512sums=('7d06ea48b1ee15cff82a54dffcc4e53ba5333e69275fd13bdfd2317afe7cd5a47fac94faa4e0eca32f6d4edda3aa71482390a7fdc01d836a4e2de6669cfc6fe0')

package() {
  cd "$srcdir/mibble-$pkgver"

  install -dm 755 "$pkgdir/usr/bin"
  install -dm 755 "$pkgdir/usr/share/doc/mibble"
  install -dm 755 "$pkgdir/usr/share/mibble"

  mv doc/api "$pkgdir/usr/share/doc/mibble/"
  install -Dm644 -t "$pkgdir/usr/share/doc/mibble" doc/*
  install -Dm644 LICENSE.txt "$pkgdir/usr/share/licenses/mibble/LICENSE.txt"

  rm -rf doc LICENSE.txt

  cp -a * "$pkgdir/usr/share/mibble"

  cat > "$pkgdir/usr/bin/mibblebrowser" << EOF
#!/bin/sh
cd /usr/share/mibble/bin/
exec ./MibbleBrowser.sh "\${@}"
EOF

  cat > "$pkgdir/usr/bin/mibbleprinter" << EOF
#!/bin/sh
cd /usr/share/mibble/bin/
exec ./MibblePrinter.sh "\${@}"
EOF

  cat > "$pkgdir/usr/bin/mibblevalidator" << EOF
#!/bin/sh
cd /usr/share/mibble/bin/
exec ./MibbleValidator.sh "\${@}"
EOF

  chmod a+x "$pkgdir/usr/bin/mibble"*
}
