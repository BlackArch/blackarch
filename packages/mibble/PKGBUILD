# This file is part of BlackArch Linux ( http://blackarch.org ).
# See COPYING for license details.

pkgname='mibble'
pkgver='2.10.1'
pkgrel=1
pkgdesc='An open-source SNMP MIB parser (or SMI parser) written in Java. It can be used to read SNMP MIB files as well as simple ASN.1 files.'
url='http://www.mibble.org/'
groups=('blackarch' 'blackarch-misc')
license=('GPL')
depends=('java-environment' 'bash')
arch=('any')
source=("https://github.com/cederberg/mibble/releases/download/v${pkgver}/mibble-${pkgver}.tar.gz")
sha1sums=('c68ddcddda945598594123a62fb8a22e94e551dd')

package() {
  cd "$srcdir/mibble-$pkgver"

  mkdir -p "$pkgdir/usr/bin"
  mkdir -p "$pkgdir/usr/share/doc/mibble"
  mkdir -p "$pkgdir/usr/share/mibble"

  mv doc/api "$pkgdir/usr/share/doc/mibble/"
  install -Dm644 -t "$pkgdir/usr/share/doc/mibble" doc/*
  install -Dm644 LICENSE.txt "$pkgdir/usr/share/licenses/mibble/LICENSE.txt"

  rm -rf doc LICENSE.txt

  cp -a * "$pkgdir/usr/share/mibble"

  cat > "$pkgdir/usr/bin/mibblebrowser" << EOF
#!/bin/sh
cd /usr/share/mibble/bin/
exec ./MibbleBrowser.sh "\${@}"
EOF

  cat > "$pkgdir/usr/bin/mibbleprinter" << EOF
#!/bin/sh
cd /usr/share/mibble/bin/
exec ./MibblePrinter.sh "\${@}"
EOF

  cat > "$pkgdir/usr/bin/mibblevalidator" << EOF
#!/bin/sh
cd /usr/share/mibble/bin/
exec ./MibbleValidator.sh "\${@}"
EOF

  chmod a+x "$pkgdir/usr/bin/mibble"*
}
