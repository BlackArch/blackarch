# This file is part of BlackArch Linux ( https://www.blackarch.org/ ).
# See COPYING for license details.

pkgname=python-faraday-plugins
_pkgname=faraday_plugins
pkgver=1.27.2
pkgrel=1
pkgdesc='Faraday plugins package.'
arch=('any')
url='https://pypi.org/project/faraday-plugins/#files'
license=('GPL-3.0-or-later')
depends=('python' 'python-html2text' 'python-click' 'python-simplejson'
         'python-requests' 'python-lxml' 'python-beautifulsoup4' 'python-dateutil'
         'python-pytz')
makedepends=('python-build' 'python-installer' 'python-setuptools'
             'python-wheel')
options=(!emptydirs)
source=("https://files.pythonhosted.org/packages/source/${_pkgname::1}/$_pkgname/$_pkgname-$pkgver.tar.gz")
sha512sums=('5d4cd35e2fdb1ad2c6e252e35f7c6b2829c4d570e9d084b7409e602b0e848029e6cce36a5efeb4dba631d93392b25cf031c7a00eae85b39b7212ca2a35639983')

build() {
  cd "$_pkgname-$pkgver"

  python -m build --wheel --no-isolation
}

package() {
  cd "$_pkgname-$pkgver"

  python -m installer --destdir="$pkgdir" dist/*.whl
}

