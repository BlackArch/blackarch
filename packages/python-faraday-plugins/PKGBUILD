# This file is part of BlackArch Linux ( https://www.blackarch.org/ ).
# See COPYING for license details.

pkgname=python-faraday-plugins
_pkgname=faraday_plugins
pkgver=1.27.1
pkgrel=1
pkgdesc='Faraday plugins package.'
arch=('any')
url='https://pypi.org/project/faraday-plugins/#files'
license=('GPL-3.0-or-later')
depends=('python' 'python-html2text' 'python-click' 'python-simplejson'
         'python-requests' 'python-lxml' 'python-beautifulsoup4' 'python-dateutil'
         'python-pytz')
makedepends=('python-build' 'python-installer' 'python-setuptools'
             'python-wheel')
options=(!emptydirs)
source=("https://files.pythonhosted.org/packages/source/${_pkgname::1}/$_pkgname/$_pkgname-$pkgver.tar.gz")
sha512sums=('143b5132907d326605db856391d5340b20c766c3aa3e99221cd22278731241e78e5f37f9aecbd2fb876276ec55e63f9e5a9384263e9609b8873fdb1219dbd039')

build() {
  cd "$_pkgname-$pkgver"

  python -m build --wheel --no-isolation
}

package() {
  cd "$_pkgname-$pkgver"

  python -m installer --destdir="$pkgdir" dist/*.whl
}

