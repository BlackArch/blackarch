# Maintainer: lsfxz (https://github.com/lsfxz/quarry)
# This file is part of BlackArch Linux ( http://blackarch.org ).
# See COPYING for license details.

pkgname='ruby-sass-3.4'
_gemname='sass'
pkgver='3.4.25'
pkgrel=1
pkgdesc="A powerful but elegant CSS compiler that makes CSS fun again."
arch=('any')
url='http://sass-lang.com/'
license=("MIT")
depends=('ruby')
options=('!emptydirs')
source=("https://rubygems.org/downloads/$_gemname-$pkgver.gem" )
noextract=("$_gemname-$pkgver.gem")
sha1sums=('08ed8126336cc1de89a4e17be00ba3d1e1fc14b2' )


package() {
  local _gemdir="$(ruby -rubygems -e 'puts Gem.default_dir')"
  gem install --ignore-dependencies --no-document --no-user-install -i "$pkgdir/$_gemdir" -n "$pkgdir"/usr/bin $_gemname-$pkgver.gem 
  rm "$pkgdir/$_gemdir/cache/$_gemname-$pkgver.gem"
  install -D -m644 "$pkgdir/$_gemdir/gems/$_gemname-$pkgver/MIT-LICENSE" "$pkgdir/usr/share/licenses/$pkgname/MIT-LICENSE"
  # non-HEAD version should not install any files in /usr/bin
  rm -r "$pkgdir"/usr/bin/

  # Preserve library and other required directories. Remove junk that gem installs.
  local _copydir=`mktemp -d -t $_gemname.XXX`
  chmod 755 $_copydir
  (cd "$pkgdir/$_gemdir/gems/$_gemname-$pkgver"/ && cp -r --parents lib VERSION VERSION_NAME $_copydir)
  rm -rf "$pkgdir/$_gemdir/gems/$_gemname-$pkgver"
  mv $_copydir "$pkgdir/$_gemdir/gems/$_gemname-$pkgver"

}
