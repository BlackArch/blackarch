# This file is part of BlackArch Linux ( https://www.blackarch.org/ ).
# See COPYING for license details.

pkgbase=python-validator-collection
pkgname=('python2-validator-collection' 'python-validator-collection')
_pkgname=validator-collection
pkgver=1.3.6
pkgrel=1
pkgdesc='Collection of 60+ Python functions for validating data.'
arch=('any')
url='https://pypi.org/project/validator-collection/#files'
license=('MIT')
makedepends=('python2-setuptools' 'python-setuptools')
source=("https://github.com/insightindustry/$_pkgname/archive/v.$pkgver.tar.gz")
sha512sums=('6f316ad84a5952aa8167ad388f8cfe6d3bbfe82ace0b1b0bbdff9826e5f2d31af3f5a509421ab446b7bdc573352f5e8cd8bcd202954ae95f7bd090674b505581')

prepare() {
  cp -a "$_pkgname-v.$pkgver"{,-2}
}

build() {
  cd "$_pkgname-v.$pkgver-2"

  python2 setup.py build

  cd "$srcdir/$_pkgname-v.$pkgver"

  python setup.py build
}

package_python2-validator-collection() {
  depends=('python2' 'python2-jsonschema' 'python2-regex')

  cd "$_pkgname-v.$pkgver-2"

  python2 setup.py install --prefix=/usr --root="$pkgdir" -O1 --skip-build
}

package_python-validator-collection() {
  depends=('python' 'python-jsonschema')

  cd "$_pkgname-v.$pkgver"

  python setup.py install --prefix=/usr --root="$pkgdir" -O1 --skip-build
}

