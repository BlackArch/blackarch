# This file is part of BlackArch Linux ( https://www.blackarch.org/ ).
# See COPYING for license details.

pkgbase=python-validator-collection
pkgname=('python2-validator-collection' 'python-validator-collection')
_pkgname=validator-collection
pkgver=1.4.1
pkgrel=1
pkgdesc='Collection of 60+ Python functions for validating data.'
arch=('any')
url='https://pypi.org/project/validator-collection/#files'
license=('MIT')
makedepends=('python2-setuptools' 'python-setuptools')
source=("https://github.com/insightindustry/$_pkgname/archive/v.$pkgver.tar.gz")
sha512sums=('042b03e8065c177562e53cf1966d11e18c01fdd3e9143639a4cf4977f2895f71a8c498d32968ae6324b7633e219fe521032bf8119230e5f911eeb8e4925c3058')

prepare() {
  cp -a "$_pkgname-v.$pkgver"{,-2}
}

build() {
  cd "$_pkgname-v.$pkgver-2"

  python2 setup.py build

  cd "$srcdir/$_pkgname-v.$pkgver"

  python setup.py build
}

package_python2-validator-collection() {
  depends=('python2' 'python2-jsonschema' 'python2-regex')

  cd "$_pkgname-v.$pkgver-2"

  python2 setup.py install --root="$pkgdir" --prefix=/usr -O1 --skip-build
}

package_python-validator-collection() {
  depends=('python' 'python-jsonschema')

  cd "$_pkgname-v.$pkgver"

  python setup.py install --root="$pkgdir" --prefix=/usr -O1 --skip-build
}

