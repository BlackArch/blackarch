# This file is part of BlackArch Linux ( https://www.blackarch.org/ ).
# See COPYING for license details.

pkgbase=python-py-zabbix
pkgname=('python2-py-zabbix' 'python-py-zabbix')
_pkgname=py-zabbix
pkgver=1.1.5
pkgrel=4
pkgdesc='Python module to work with zabbix.'
arch=('any')
url='https://pypi.org/project/py-zabbix/#files'
license=('GPL2')
makedepends=('python2-setuptools' 'python-setuptools')
source=("https://files.pythonhosted.org/packages/c4/4b/18b8876b43e5aa747ae9dcb57a2f8364e9dfe607f4046936d31f7af0351f/$_pkgname-$pkgver.tar.gz")
sha512sums=('7bf39c43e728801aab4a7b4caa195afb120454eb725b4202d4dffa1a2a9bfdf1b0e8af2f85bfe546b77a80d4fadf866657e7db53cd25d3ce181df2ca36a7dfcc')

prepare() {
  cp -a "$_pkgname-$pkgver"{,-2}
}

build() {
  cd "$_pkgname-$pkgver-2"

  python2 setup.py build

  cd "$srcdir/$_pkgname-$pkgver"

  python setup.py build
}

package_python2-py-zabbix() {
  depends=('python2')

  cd "$_pkgname-$pkgver-2"

  python2 setup.py install --prefix=/usr --root="$pkgdir" --optimize=1 \
    --skip-build
}

package_python-py-zabbix() {
  depends=('python')

  cd "$_pkgname-$pkgver"

  python setup.py install --prefix=/usr --root="$pkgdir" --optimize=1 \
    --skip-build
}

