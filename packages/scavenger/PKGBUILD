# This file is part of BlackArch Linux ( https://www.blackarch.org/ ).
# See COPYING for license details.

pkgname=scavenger
pkgver=93.2326de0
pkgrel=2
pkgdesc='Crawler (Bot) searching for credential leaks on different paste sites.'
arch=('any')
groups=('blackarch' 'blackarch-recon' 'blackarch-social')
url='https://github.com/rndinfosecguy/Scavenger'
license=('Apache')
depends=('python' 'python-tweepy' 'python-argparse' 'python-httplib2'
         'python-requests' 'python-beautifulsoup4')
makedepends=('git')
source=("$pkgname::git+https://github.com/rndinfosecguy/Scavenger.git")
sha512sums=('SKIP')

pkgver() {
  cd $pkgname

  echo $(git rev-list --count HEAD).$(git rev-parse --short HEAD)
}

package() {
  cd $pkgname

  install -dm 755 "$pkgdir/usr/bin"
  install -dm 755 "$pkgdir/usr/share/$pkgname"

  install -Dm 644 -t "$pkgdir/usr/share/doc/$pkgname/" README.md
  install -Dm 644 LICENSE "$pkgdir/usr/share/licenses/$pkgname/LICENSE"

  rm README.md LICENSE

  cp -a --no-preserve=ownership * "$pkgdir/usr/share/$pkgname/"

  cat > "$pkgdir/usr/bin/$pkgname" << EOF
#!/bin/sh
cd /usr/share/$pkgname
exec python run.py "\$@"
EOF

  chmod +x "$pkgdir/usr/bin/$pkgname"
}

