# This file is part of BlackArch Linux ( https://www.blackarch.org/ ).
# See COPYING for license details.

pkgname=pyinstxtractor
pkgver=2024.04.r1.g90974de
pkgrel=1
pkgdesc='PyInstaller Extractor.'
arch=('any')
groups=('blackarch' 'blackarch-reversing')
url='https://github.com/extremecoders-re/pyinstxtractor'
license=('GPL3')
depends=('python')
makedepends=('git')
source=("git+https://github.com/extremecoders-re/$pkgname.git")
sha512sums=('SKIP')

pkgver() {
  cd $pkgname

  ( set -o pipefail
    git describe --long --tags --abbrev=7 2>/dev/null |
      sed 's/\([^-]*-g\)/r\1/;s/-/./g' ||
    printf "%s.%s" "$(git rev-list --count HEAD)" \
      "$(git rev-parse --short=7 HEAD)"
  )
}

package() {
  cd $pkgname

  install -dm 755 "$pkgdir/usr/bin"
  install -dm 755 "$pkgdir/usr/share/$pkgname"
  install -Dm 644 -t "$pkgdir/usr/share/doc/$pkgname/" *.md

  rm -rf LICENSE *.md

  cp -a * "$pkgdir/usr/share/$pkgname/"

  cat > "$pkgdir/usr/bin/$pkgname" << EOF
#!/bin/sh
exec python /usr/share/$pkgname/pyinstxtractor.py "\$@"
EOF

  chmod a+x "$pkgdir/usr/bin/$pkgname"
}

