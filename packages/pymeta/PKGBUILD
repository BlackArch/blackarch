# This file is part of BlackArch Linux ( https://www.blackarch.org/ ).
# See COPYING for license details.

pkgname=pymeta
pkgver=1.0.4.r25.gdb8a80b
pkgrel=1
epoch=1
pkgdesc='Auto Scanning to SSL Vulnerability.'
groups=('blackarch' 'blackarch-recon')
arch=('any')
url='https://github.com/m8r0wn/pymeta'
license=('GPL-3.0-or-later')
depends=('python' 'python-requests' 'python-beautifulsoup4' 'python-urllib3'
         'perl-image-exiftool')
makedepends=('git' 'python-build' 'python-installer' 'python-setuptools'
             'python-wheel')
source=("git+https://github.com/m8r0wn/$pkgname.git"
        "$pkgname.py.patch")
sha512sums=('SKIP'
            'a1077212195a20423cfcf5b57d3e6081d3e985d53729f0d9c04d1322e516d6e7d6145141ee5539ed3b12b104e3849ab75132d2880dbfe6820088c40bf0d09520')

pkgver() {
  cd $pkgname

  ( set -o pipefail
    git describe --long --tags --abbrev=7 2>/dev/null |
      sed 's/\([^-]*-g\)/r\1/;s/-/./g' ||
    printf "%s.%s" "$(git rev-list --count HEAD)" \
      "$(git rev-parse --short=7 HEAD)"
  )
}

build() {
  cd $pkgname

  python -m build --wheel --no-isolation
}

package() {
  cd $pkgname

  python -m installer --destdir="$pkgdir" dist/*.whl

  install -Dm 644 -t "$pkgdir/usr/share/doc/$pkgname/" README.md
}

