# This file is part of BlackArch Linux ( https://www.blackarch.org/ ).
# See COPYING for license details.

pkgname=ssllabs-scan
pkgver=249.a7f3d49
pkgrel=2
pkgdesc='Command-line client for the SSL Labs APIs'
groups=('blackarch' 'blackarch-scanner' 'blackarch-crypto')
arch=('x86_64' 'aarch64')
url='https://github.com/ssllabs/ssllabs-scan'
license=('APACHE')
makedepends=('git' 'go')
source=("git+https://github.com/ssllabs/$pkgname.git")
sha512sums=('SKIP')

pkgver() {
  cd $pkgname

  echo $(git rev-list --count HEAD).$(git rev-parse --short HEAD)
}

build() {
  cd $pkgname

  GOPATH="$srcdir" go mod init go.mod
  GOPATH="$srcdir" go mod tidy
  GOPATH="$srcdir" go mod download
  GOPATH="$srcdir" go build \
    -trimpath \
    -buildmode=pie \
    -mod=readonly \
    -modcacherw \
    -ldflags "-s -w" \
    -o $pkgname .
}

package() {
  cd $pkgname

  install -Dm 755 $pkgname "$pkgdir/usr/bin/$pkgname"
  install -Dm 644 -t "$pkgdir/usr/share/$pkgname" *.sql
  install -Dm 644 -t "$pkgdir/usr/share/doc/$pkgname" README.md AUTHORS TODO.md
}

