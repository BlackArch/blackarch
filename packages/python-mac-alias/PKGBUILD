# This file is part of BlackArch Linux ( https://www.blackarch.org/ ).
# See COPYING for license details.

pkgbase=python-mac-alias
pkgname=('python-mac-alias' 'python2-mac-alias')
_pkgname=mac_alias
pkgver=2.0.7
pkgrel=2
pkgdesc='Generate or read binary Alias and Bookmark records from Python code.'
arch=('any')
url='https://pypi.org/project/mac_alias/2.0.7/#files'
license=('MIT')
makedepends=('python2-setuptools' 'python-setuptools')
source=("https://files.pythonhosted.org/packages/42/19/cabae650ad5f97367796495c8748ca748cfde6a05c10ead0382f369075d3/$_pkgname-$pkgver.tar.gz")
sha512sums=('7df7f78c51e86c70d786bb69a4626bfbe69a1c845c563dc2bdc84f289ec2d06a5a56e31cdd83561fd36714a9eb1e5e5faa150b0b14ca1836f8cd87205abb8244')

prepare() {
  cp -a "$_pkgname-$pkgver"{,-2}
}

build() {
  cd "$_pkgname-$pkgver-2"

  python2 setup.py build

  cd "$srcdir/$_pkgname-$pkgver"

  python setup.py build
}

package_python2-mac-alias() {
  depends=('python2')

  cd "$_pkgname-$pkgver-2"

  python2 setup.py install --prefix=/usr --root="$pkgdir" --optimize=1 \
    --skip-build
}

package_python-mac-alias() {
  depends=('python')

  cd "$_pkgname-$pkgver-2"

  python setup.py install --prefix=/usr --root="$pkgdir" --optimize=1 \
    --skip-build
}

