# This file is part of BlackArch Linux ( https://www.blackarch.org/ ).
# See COPYING for license details.

pkgname=libvshadow
pkgver=144.2eb2b89
pkgrel=1
pkgdesc='A library to access the Volume Shadow Snapshot (VSS) format.'
arch=('x86_64' 'aarch64')
url='https://github.com/libyal/libvshadow'
license=('LGPL3')
depends=('libbfio')
makedepends=('git' 'autoconf' 'automake' 'libtool' 'gettext' 'pkg-config'
             'bison' 'flex')
source=("git+https://github.com/libyal/$pkgname.git")
sha512sums=('SKIP')

pkgver() {
  cd $pkgname

  echo $(git rev-list --count HEAD).$(git rev-parse --short HEAD)
}

build() {
  cd $pkgname

  ./synclibs.sh
  ./autogen.sh
  ./configure --prefix=/usr --sysconfdir=/etc

  make
}

package() {
  cd $pkgname

  make DESTDIR="$pkgdir" install
}

