# This file is part of BlackArch Linux ( https://www.blackarch.org/ ).
# See COPYING for license details.

pkgbase=python-stix2
pkgname=('python2-stix2' 'python-stix2')
_pkgname=cti-python-stix2
pkgver=1.1.2
pkgrel=1
pkgdesc='Produce and consume STIX 2 JSON content.'
url='https://github.com/oasis-open/cti-python-stix2'
license=('BSD3')
arch=('any')
makedepends=('git' 'python2-setuptools' 'python-setuptools')
source=("https://github.com/oasis-open/cti-python-stix2/archive/v${pkgver}.tar.gz")
sha512sums=('017c34edcc534dc659a62d4bdb742cc6af6da18aa0cf2412ce2f64aec4b652b7018c2c8d6de78dfa5b5dc1c3a7d9b1b09c2a9631ce0937f56cf9a2b0de596057')

pkgver() {
  git ls-remote --sort=v:refname -t --refs "$url" 'v*' \
  | sed -e 's/.*v//' -e '$!d'
}

prepare() {
  cp -a "$_pkgname-$pkgver"{,-2}
}

build() {
  cd "$_pkgname-$pkgver-2"
  python2 setup.py build
  cd "$srcdir/$_pkgname-$pkgver"
  python setup.py build
}

package_python2-stix2() {
  depends=('python2' 'python2-dateutil' 'python2-pytz' 'python2-requests'
           'python2-simplejson' 'python2-six' 'python2-stix2-patterns')
  cd "$_pkgname-$pkgver-2"
  python2 setup.py install --prefix=/usr --root="$pkgdir" --optimize=1 \
    --skip-build
}

package_python-stix2() {
  depends=('python' 'python-dateutil' 'python-pytz' 'python-requests'
           'python-simplejson' 'python-six' 'python-stix2-patterns')
  cd "$_pkgname-$pkgver"
  python setup.py install --prefix=/usr --root="$pkgdir" --optimize=1 \
    --skip-build
}


