# This file is part of BlackArch Linux ( http://blackarch.org ).
# See COPYING for license details.

pkgname=perl-getopt-long
pkgver=2.50
pkgrel=3
pkgdesc='Extended processing of command line options.'
arch=('any')
license=('PerlArtistic' 'GPL')
options=('!emptydirs')
depends=('perl>=0')
url='http://search.cpan.org/~jv/Getopt-Long-2.42/lib/Getopt/Long.pm'
source=("http://search.cpan.org/CPAN/authors/id/J/JV/JV/Getopt-Long-${pkgver}.tar.gz")
sha512sums=('be4e8e3511d84f7919cd3df9e9aa9540dd522af47fc34bf4560a0212dd3c5d6571369ad502dfb4641918a43f8cecec3faa3d43adacfe706cdf9fe498d1ef0b6d')

build() {
  cd "$srcdir/Getopt-Long-$pkgver"

  (
    export PERL_MM_USE_DEFAULT=1 PERL5LIB="" PERL_AUTOINSTALL=--skipdeps \
    PERL_MM_OPT="INSTALLDIRS=vendor DESTDIR='$pkgdir'" \
    PERL_MB_OPT="--installdirs vendor --destdir '$pkgdir'" \
    MODULEBUILDRC=/dev/null

    /usr/bin/perl Makefile.PL

    make
  )
}

package() {
  cd "$srcdir/Getopt-Long-$pkgver"

  make install

  find "$pkgdir" -name .packlist -o -name perllocal.pod -delete
}
