# This file is part of BlackArch Linux ( https://www.blackarch.org/ ).
# See COPYING for license details.

pkgname=python2-ropper
_pkgname=Ropper
pkgver=1.12.5
pkgrel=2
pkgdesc='Display information about files in different file formats and find gadgets to build rop chains for different architectures (x86/x86_64, ARM/ARM64, MIPS, PowerPC, SPARC64).'
url='https://github.com/sashs/Ropper'
arch=('any')
license=('BSD')
depends=('python2' 'python2-capstone' 'python2-filebytes')
makedepends=('python2-setuptools' 'python2-capstone' 'python2-filebytes')
source=("https://github.com/sashs/$_pkgname/archive/v$pkgver.tar.gz")
sha512sums=('d5afe158e16569a8bd7ec634f0666a8af2f057a72d0a0d0b974b72b3e44a7f826ebaa46b83a0c2c7bcc2a52b660f2f7cc94ecc54562be08d06079cb57e16ca41')

build() {
  cd "$_pkgname-$pkgver"

  python2 setup.py build
}

package() {
  cd "$_pkgname-$pkgver"

  python2 setup.py install --root="$pkgdir" --prefix=/usr -O1 --skip-build

  mv "$pkgdir/usr/bin/ropper" "$pkgdir/usr/bin/ropper2"
}

