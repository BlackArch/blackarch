# This file is part of BlackArch Linux ( http://blackarch.org ).
# See COPYING for license details.

pkgname=box-js
pkgver=360.d867e4b
pkgrel=1
pkgdesc='A tool for studying JavaScript malware.'
groups=('blackarch' 'blackarch-malware')
arch=('any')
url='https://github.com/CapacitorSet/box-js'
license=('MIT')
depends=('nodejs' 'gnuplot')
makedepends=('git' 'npm')
source=('git+https://github.com/CapacitorSet/box-js.git')
sha512sums=('SKIP')

pkgver() {
  cd "$srcdir/box-js"

  echo $(git rev-list --count HEAD).$(git rev-parse --short HEAD)
}

package() {
  cd "$srcdir/box-js"

  install -dm 755 "$pkgdir/usr/bin"
  install -dm 755 "$pkgdir/usr/share/box-js"

  npm install --production

  find . -name package.json | xargs sed -i "s|$srcdir|/usr/share/box-js|"

  install -Dm644 -t "$pkgdir/usr/share/doc/box-js" README.md
  install -Dm644 LICENSE "$pkgdir/usr/share/licenses/box-js/LICENSE"

  rm README.md LICENSE

  cp -a * "$pkgdir/usr/share/box-js"

cat > "$pkgdir/usr/bin/box-js" << EOF
#!/bin/sh
cd /usr/share/box-js
exec node run.js "\${@}"
EOF

  chmod a+x "$pkgdir/usr/bin/box-js"
}
