# This file is part of BlackArch Linux ( http://blackarch.org ).
# See COPYING for license details.

pkgname='waidps'
pkgver=16.ff8d270
pkgrel=3
groups=('blackarch' 'blackarch-wireless')
pkgdesc='Wireless Auditing, Intrusion Detection & Prevention System.'
arch=('any')
url='https://github.com/SYWorks/waidps'
license=('GPL3')
depends=('python2' 'python2-crypto')
makedepends=('git')
source=('git+https://github.com/SYWorks/waidps.git')
sha1sums=('SKIP')

pkgver() {
  cd "$srcdir/waidps"

  echo $(git rev-list --count HEAD).$(git rev-parse --short HEAD)
}

prepare() {
  cd "$srcdir/waidps"

  # it supports py3 but let's stick first with py2...
  sed -i 's|/usr/bin/python|/usr/bin/python2|' waidps.py
}

package() {
  cd "$srcdir/waidps"

  install -Dm755 waidps.py "$pkgdir/usr/bin/waidps"
  install -Dm644 README.md "$pkgdir/usr/share/doc/waidps/README"
  install -Dm644 LICENSE "$pkgdir/usr/share/licenses/waidps/LICENSE"
}
