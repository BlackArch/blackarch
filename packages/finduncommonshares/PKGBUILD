# This file is part of BlackArch Linux ( https://www.blackarch.org/ ).
# See COPYING for license details.

pkgname=finduncommonshares
pkgver=3.2.r11.ga1f94dd
pkgrel=2
pkgdesc='Python script allowing to quickly find uncommon shares in vast Windows Domains.'
arch=('any')
groups=('blackarch' 'blackarch-windows')
url='https://github.com/p0dalirius/pyFindUncommonShares'
license=('GPL-2.0-only')
depends=('impacket' 'python' 'python-dnspython' 'python-pycryptodome'
         'python-sectools>=1.5.1' 'python-xlsxwriter')
makedepends=('git')
source=("$pkgname::git+$url.git")
sha512sums=('SKIP')

pkgver() {
  cd $pkgname

  ( set -o pipefail
    git describe --long --tags --abbrev=7 2>/dev/null |
      sed 's/\([^-]*-g\)/r\1/;s/-/./g' ||
    printf "%s.%s" "$(git rev-list --count HEAD)" \
      "$(git rev-parse --short=7 HEAD)"
  )
}

package() {
  cd $pkgname

  install -dm 755 "$pkgdir/usr/bin"
  install -dm 755 "$pkgdir/usr/share/$pkgname"

  install -Dm 644 -t "$pkgdir/usr/share/doc/$pkgname/" README.md

  cp -a FindUncommonShares.py "$pkgdir/usr/share/$pkgname/"

  cat > "$pkgdir/usr/bin/$pkgname" << EOF
#!/bin/sh
exec python /usr/share/$pkgname/FindUncommonShares.py "\$@"
EOF

  chmod +x "$pkgdir/usr/bin/$pkgname"
}

