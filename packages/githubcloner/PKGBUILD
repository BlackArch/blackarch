# This file is part of BlackArch Linux ( http://blackarch.org ).
# See COPYING for license details.

pkgname=githubcloner
pkgver=19.282d8d1
pkgrel=2
pkgdesc='A script that clones Github repositories of users and organizations automatically.'
groups=('blackarch' 'blackarch-misc' 'blackarch-automation')
arch=('any')
url='https://github.com/mazen160/GithubCloner'
license=('MIT')
depends=('python' 'python-argparse' 'python-requests')
makedepends=('git')
source=('git+https://github.com/mazen160/GithubCloner')
sha512sums=('SKIP')

pkgver() {
  cd GithubCloner

  echo $(git rev-list --count HEAD).$(git rev-parse --short HEAD)
}

package() {
  cd GithubCloner

  install -dm 755 "$pkgdir/usr/bin"
  install -dm 755 "$pkgdir/usr/share/$pkgname"

  install -Dm 644 -t "$pkgdir/usr/share/doc/$pkgname/" README.md
  install -Dm 644 LICENSE.txt "$pkgdir/usr/share/licenses/$pkgname/LICENSE"

  rm README.md LICENSE.txt

  cp -a * "$pkgdir/usr/share/$pkgname/"

  cat > "$pkgdir/usr/bin/$pkgname" << EOF
#!/bin/sh
cd /usr/share/$pkgname
exec python githubcloner.py "\$@"
EOF

  chmod a+x "$pkgdir/usr/bin/$pkgname"
}

