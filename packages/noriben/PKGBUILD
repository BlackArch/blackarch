# This file is part of BlackArch Linux ( https://www.blackarch.org/ ).
# See COPYING for license details.

pkgname=noriben
pkgver=2.0.r5.g35327a6
pkgrel=1
epoch=1
pkgdesc='Portable, Simple, Malware Analysis Sandbox.'
groups=('blackarch' 'blackarch-malware')
arch=('any')
url='https://github.com/Rurik/Noriben'
license=('custom:unknown')
depends=('python' 'python-magic')
makedepends=('git')
source=("$pkgname::git+https://github.com/Rurik/Noriben.git")
sha512sums=('SKIP')

pkgver() {
  cd $pkgname

  ( set -o pipefail
    git describe --long --tags --abbrev=7 2>/dev/null |
      sed 's/\([^-]*-g\)/r\1/;s/-/./g' ||
    printf "%s.%s" "$(git rev-list --count HEAD)" \
      "$(git rev-parse --short=7 HEAD)"
  )
}

prepare() {
  cd $pkgname

  cp ProcmonConfiguration.pmc ProcmonConfiguration.PMC
}

package() {
  cd $pkgname

  install -dm 755 "$pkgdir/usr/bin"
  install -dm 755 "$pkgdir/usr/share/$pkgname"

  install -Dm 644 -t "$pkgdir/usr/share/doc/$pkgname/" README.md

  rm README.md

  cp -a * "$pkgdir/usr/share/$pkgname/"

  cat > "$pkgdir/usr/bin/$pkgname" << EOF
#!/bin/sh
cd /usr/share/$pkgname
exec python3 Noriben.py "\$@"
EOF

  chmod +x "$pkgdir/usr/bin/$pkgname"

  cat > "$pkgdir/usr/bin/noriben-sandbox" << EOF
#!/bin/sh
cd /usr/share/$pkgname
exec python3 NoribenSandbox.py "\$@"
EOF

  chmod +x "$pkgdir/usr/bin/noriben-sandbox"
}

