# This file is part of BlackArch Linux ( https://www.blackarch.org/ ).
# See COPYING for license details.

pkgname=bluebox-ng
pkgver=404.66d16a3
pkgrel=1
epoch=1
groups=('blackarch' 'blackarch-voip' 'blackarch-scanner' 'blackarch-fuzzer')
pkgdesc='A GPL VoIP/UC vulnerability scanner.'
url='https://github.com/jesusprubio/bluebox-ng'
arch=('any')
license=('GPL3')
depends=('nodejs' 'coffee-script' 'python2' 'nmap')
source=('git+https://github.com/jesusprubio/bluebox-ng.git')
sha512sums=('SKIP')

pkgver() {
  cd $pkgname

  echo $(git rev-list --count HEAD).$(git rev-parse --short HEAD)
}

prepare() {
  cd "$pkgname"

  npm --python=/usr/bin/python2 install 2>/dev/null

  sed -i -re 's|#!\s*(/usr)?(/local)?/bin/.*python.*$|#!/usr/bin/env python2|g' $(egrep -rl '^\s*#!\s*(/usr)?(/local)?/bin/.*python')
}

package() {
  cd "$pkgname"

  install -dm 755 "$pkgdir/usr/bin"

  install -Dm 644 README.md "$pkgdir/usr/share/doc/$pkgname/README.md"
  install -Dm 644 package.json \
    "$pkgdir/usr/lib/node_modules/$pkgname/package.json"
  install -m 755 *.js "$pkgdir/usr/lib/node_modules/$pkgname/"

  install -Dm 755 bin/client.js \
    "$pkgdir/usr/lib/node_modules/$pkgname/bin/blueboxClient.js"

  rm README.md package.json

  cp -a * "$pkgdir/usr/lib/node_modules/$pkgname/"

  cat > "$pkgdir/usr/bin/$pkgname" << EOF
#!/bin/sh
 node /usr/lib/node_modules/$pkgname/bin/blueboxClient.js "\$@"
EOF

  chmod +x "$pkgdir/usr/bin/$pkgname"
}

