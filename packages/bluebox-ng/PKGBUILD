# This file is part of BlackArch Linux ( http://blackarch.org ).
# See COPYING for license details.

pkgname=bluebox-ng
pkgver=1.1.0
pkgrel=3
epoch=1
groups=('blackarch' 'blackarch-voip' 'blackarch-scanner' 'blackarch-fuzzer')
pkgdesc='A GPL VoIP/UC vulnerability scanner.'
url='https://github.com/jesusprubio/bluebox-ng'
arch=('any')
license=('GPL3')
depends=('nodejs' 'coffee-script' 'python2' 'nmap')
source=("https://github.com/jesusprubio/bluebox-ng/archive/${pkgver}.tar.gz")
sha512sums=('d477d043d6bb5ba66a6c16d002a49fe7b77e66d0fa404e6feb1cb548f76bdd68806ce42a1d841affd45789dc0cd02ffa8f552ecfadf8a9ca9b768eb947f8862a')

prepare() {
  cd "$srcdir/bluebox-ng-$pkgver"

  npm --python=/usr/bin/python2 install 2>/dev/null

  sed -i -re 's|#!\s*(/usr)?(/local)?/bin/.*python.*$|#!/usr/bin/env python2|g' $(egrep -rl '^\s*#!\s*(/usr)?(/local)?/bin/.*python')
}

package() {
  cd "$srcdir/bluebox-ng-$pkgver"

  install -dm 755 "$pkgdir/usr/bin"

  install -Dm644 README.md "$pkgdir/usr/share/doc/bluebox-ng/README.md"
  install -Dm644 package.json \
    "$pkgdir/usr/lib/node_modules/bluebox-ng/package.json"
  install -m755 *.js "$pkgdir/usr/lib/node_modules/bluebox-ng/"

  install -Dm755 bin/client.js \
    "$pkgdir/usr/lib/node_modules/bluebox-ng/bin/blueboxClient.js"

  rm README.md package.json

  cp -a * "$pkgdir/usr/lib/node_modules/bluebox-ng/"

  cat > "$pkgdir/usr/bin/bluebox-ng" << EOF
#!/bin/sh
 node /usr/lib/node_modules/bluebox-ng/bin/blueboxClient.js
EOF

  chmod a+x "$pkgdir/usr/bin/bluebox-ng"
}
