# This file is part of BlackArch Linux ( https://www.blackarch.org/ ).
# See COPYING for license details.

pkgname=kubestriker
pkgver=39.e1776ea
pkgrel=2
pkgdesc='A Blazing fast Security Auditing tool for Kubernetes.'
groups=('blackarch' 'blackarch-scanner')
arch=('any')
url='https://github.com/vchinnipilli/kubestriker'
license=('Apache')
depends=('python' 'python-virtualenv')
makedepends=('git' 'python-pip')
source=("git+https://github.com/vchinnipilli/$pkgname.git")
sha512sums=('SKIP')
install="$pkgname.install"

pkgver() {
  cd $pkgname

  echo $(git rev-list --count HEAD).$(git rev-parse --short HEAD)
}

package() {
  cd $pkgname

  install -dm 755 "$pkgdir/usr/bin"
  install -dm 755 "$pkgdir/usr/share/$pkgname"

  install -Dm 644 -t "$pkgdir/usr/share/doc/$pkgname/" *.md

  rm -rf *.md LICENSE

  cp -a * "$pkgdir/usr/share/$pkgname/"

  cat > "$pkgdir/usr/bin/$pkgname" << EOF
#!/bin/sh
cd /usr/share/$pkgname
source venv/bin/activate
exec python -m $pkgname "\$@"
EOF

  chmod +x "$pkgdir/usr/bin/$pkgname"
}

