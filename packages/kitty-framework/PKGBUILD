# This file is part of BlackArch Linux ( https://www.blackarch.org/ ).
# See COPYING for license details.

pkgname=kitty-framework
pkgver=352.cb07609
pkgrel=1
groups=('blackarch' 'blackarch-fuzzer')
pkgdesc='Fuzzing framework written in python.'
arch=('any')
url='https://github.com/cisco-sas/kitty'
license=('GPL2')
depends=('python2' 'python2-docopt' 'python2-six' 'python2-requests'
         'python2-bitstring')
makedepends=('git' 'python2-setuptools')
source=("$pkgname::git+https://github.com/cisco-sas/kitty.git")
sha512sums=('SKIP')

pkgver() {
  cd $pkgname

  echo $(git rev-list --count HEAD).$(git rev-parse --short HEAD)
}

build() {
  cd $pkgname

  python2 setup.py build
}

package() {
  cd $pkgname

  python2 setup.py install --root="$pkgdir" --prefix=/usr -O1 --skip-build

  install -Dm 644 COPYING "$pkgdir/usr/share/licenses/kitty/COPYING"
}

