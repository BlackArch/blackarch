# This file is part of BlackArch Linux ( https://www.blackarch.org/ ).
# See COPYING for license details.

pkgbase=python-github3.py
pkgname=('python2-github3.py' 'python-github3.py')
_pkgname=github3.py
pkgver=1.3.0
pkgrel=3
pkgdesc='Python wrapper for the GitHub API (http://developer.github.com/v3).'
arch=('any')
url='https://pypi.org/project/github3.py/#files'
license=('BSD')
makedepends=('python2-setuptools' 'python-setuptools')
source=("https://files.pythonhosted.org/packages/2c/78/b593098dc5a16c03a91ef2a2f6341d17943b5d5359c53335a7a04beced42/$_pkgname-$pkgver.tar.gz")
sha512sums=('b57526f9ac2ce70569abbe8e8efcdc77376885b8f81a48d10ed255c9cdb530d0b9e544612781ffeadbedef08e04fe63cec3dbe029b6d86d15aeff143ce620366')

prepare() {
  cp -a "$_pkgname-$pkgver"{,-2}
}

build() {
  cd "$_pkgname-$pkgver-2"

  python2 setup.py build

  cd "$srcdir/$_pkgname-$pkgver"

  python setup.py build
}

package_python2-github3.py() {
  depends=('python2' 'python2-dateutil' 'python2-jwcrypto' 'python2-requests'
           'python2-uritemplate')

  cd "$_pkgname-$pkgver-2"

  python2 setup.py install --prefix=/usr --root="$pkgdir" -O1 --skip-build
}

package_python-github3.py() {
  depends=('python' 'python-dateutil' 'python-jwcrypto' 'python-uritemplate'
           'python-requests')

  cd "$_pkgname-$pkgver"

  python setup.py install --prefix=/usr --root="$pkgdir" -O1 --skip-build
}

