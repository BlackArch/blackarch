# This file is part of BlackArch Linux ( https://www.blackarch.org/ ).
# See COPYING for license details.

pkgbase=python-github3.py
pkgname=('python2-github3.py' 'python-github3.py')
_pkgname=github3.py
pkgver=2.0.0
pkgrel=1
pkgdesc='Python wrapper for the GitHub API (http://developer.github.com/v3).'
arch=('any')
url='https://pypi.org/project/github3.py/#files'
license=('BSD')
makedepends=('python2-setuptools' 'python-setuptools')
source=("https://files.pythonhosted.org/packages/b1/0a/e7ac54833ce6a7aea6cf53f0937459ef5341a88b9fcd82f44c6d6841eb97/$_pkgname-$pkgver.tar.gz")
sha512sums=('14316ce47d3f4734d371cdad631e9538e5bae0201f0e146d89cd91f699ffb6cb61255d1f5b050cfad65db089921c2d93ae3c1ba5dfd38b40d3cd169e8b8a0ef6')

prepare() {
  cp -a "$_pkgname-$pkgver"{,-2}
}

build() {
  cd "$_pkgname-$pkgver-2"

  python2 setup.py build

  cd "$srcdir/$_pkgname-$pkgver"

  python setup.py build
}

package_python2-github3.py() {
  depends=('python2' 'python2-dateutil' 'python2-jwcrypto' 'python2-requests'
           'python2-uritemplate')

  cd "$_pkgname-$pkgver-2"

  python2 setup.py install --prefix=/usr --root="$pkgdir" -O1 --skip-build
}

package_python-github3.py() {
  depends=('python' 'python-dateutil' 'python-jwcrypto' 'python-uritemplate'
           'python-requests')

  cd "$_pkgname-$pkgver"

  python setup.py install --prefix=/usr --root="$pkgdir" -O1 --skip-build
}

