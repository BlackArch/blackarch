# This file is part of BlackArch Linux ( https://www.blackarch.org/ ).
# See COPYING for license details.

pkgname=cr3dov3r
pkgver=46.99a1660
pkgrel=3
pkgdesc='Search for public leaks for email addresses + check creds against 16 websites.'
groups=('blackarch' 'blackarch-recon' 'blackarch-social')
arch=('any')
url='https://github.com/D4Vinci/Cr3dOv3r'
license=('MIT')
depends=('python' 'python-argparse' 'python-mechanicalsoup' 'python-pyopenssl'
         'python-requests')
makedepends=('git')
source=("$pkgname::git+https://github.com/D4Vinci/Cr3dOv3r.git")
sha512sums=('SKIP')

pkgver() {
  cd $pkgname

  echo $(git rev-list --count HEAD).$(git rev-parse --short HEAD)
}

package() {
  cd $pkgname

  install -dm 755 "$pkgdir/usr/bin"
  install -dm 755 "$pkgdir/usr/share/$pkgname"

  install -Dm 644 -t "$pkgdir/usr/share/doc/$pkgname/" README.md
  install -Dm 644 LICENSE "$pkgdir/usr/share/licenses/$pkgname/LICENSE"

  rm README.md LICENSE

  cp -a --no-preserve=ownership * "$pkgdir/usr/share/$pkgname/"

  cat > "$pkgdir/usr/bin/$pkgname" << EOF
#!/bin/sh
cd /usr/share/$pkgname
exec python Cr3d0v3r.py "\$@"
EOF

  chmod +x "$pkgdir/usr/bin/$pkgname"
}

