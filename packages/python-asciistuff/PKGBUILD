# This file is part of BlackArch Linux ( https://www.blackarch.org/ ).
# See COPYING for license details.

pkgbase=python-asciistuff
pkgname=('python2-asciistuff' 'python-asciistuff')
_pkgname=asciistuff
pkgver=1.0.9
pkgrel=2
pkgdesc='Library for producing ASCII arts from a text or an image.'
arch=('any')
url='https://pypi.org/project/asciistuff/#files'
license=('AGPL3')
makedepends=('python2-setuptools' 'python-setuptools')
source=("https://files.pythonhosted.org/packages/2a/62/d6e20489b70233a42f963956087cd056d588ea5b79e81e8987d5f6d92161/$_pkgname-$pkgver.tar.gz")
sha512sums=('59173a30f03e8d201869c9ff631c2cb9edb7ae16649d26ab30d474385b3017f2df71c8dba9cfd5286f97157442b36c961015e28b9b3645a2369cce2b059496d0')

prepare() {
  cp -a "$_pkgname-$pkgver"{,-2}
}

build() {
  cd "$_pkgname-$pkgver-2"

  python2 setup.py build

  cd "$srcdir/$_pkgname-$pkgver"

  python setup.py build
}

package_python2-asciistuff() {
  depends=('python2' 'python2-terminaltables' 'python2-termcolor'
           'python2-pyfiglet' 'python2-cowpy')

  cd "$_pkgname-$pkgver-2"

  python2 setup.py install --prefix=/usr --root="$pkgdir" -O1 --skip-build

  mv "$pkgdir/usr/bin/$_pkgname" "$pkgdir/usr/bin/${_pkgname}2"
}

package_python-asciistuff() {
  depends=('python' 'python-terminaltables' 'python-termcolor' 'python-cowpy'
           'python-pyfiglet')

  cd "$_pkgname-$pkgver"

  python setup.py install --prefix=/usr --root="$pkgdir" -O1 --skip-build
}

