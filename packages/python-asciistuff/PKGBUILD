# This file is part of BlackArch Linux ( https://www.blackarch.org/ ).
# See COPYING for license details.

pkgbase=python-asciistuff
pkgname=('python2-asciistuff' 'python-asciistuff')
_pkgname=asciistuff
pkgver=1.0.8
pkgrel=1
pkgdesc='Library for producing ASCII arts from a text or an image.'
arch=('any')
url='https://pypi.org/project/asciistuff/#files'
license=('AGPL3')
makedepends=('python2-setuptools' 'python-setuptools')
source=("https://files.pythonhosted.org/packages/b4/82/c683b9cea3c39037878c9124ef6d66c29b387b503a69271273018b3ec338/$_pkgname-$pkgver.tar.gz")
sha512sums=('9b8128ba7d99f0ed6b020086ee57f07e5040e85f16b273b238a365356132de4976c0ca55c141c8c473a08be3f430231af88ba58c17cfb9cf599b225a61b1425b')

prepare() {
  cp -a "$_pkgname-$pkgver"{,-2}
}

build() {
  cd "$_pkgname-$pkgver-2"

  python2 setup.py build

  cd "$srcdir/$_pkgname-$pkgver"

  python setup.py build
}

package_python2-asciistuff() {
  depends=('python2' 'python2-terminaltables' 'python2-termcolor'
           'python2-pyfiglet' 'python2-cowpy')

  cd "$_pkgname-$pkgver-2"

  python2 setup.py install --prefix=/usr --root="$pkgdir" -O1 --skip-build

  mv "$pkgdir/usr/bin/$_pkgname" "$pkgdir/usr/bin/${_pkgname}2"
}

package_python-asciistuff() {
  depends=('python' 'python-terminaltables' 'python-termcolor' 'python-cowpy'
           'python-pyfiglet')

  cd "$_pkgname-$pkgver"

  python setup.py install --prefix=/usr --root="$pkgdir" -O1 --skip-build
}

