# This file is part of BlackArch Linux ( https://www.blackarch.org/ ).
# See COPYING for license details.

pkgbase=python-asciistuff
pkgname=('python2-asciistuff' 'python-asciistuff')
_pkgname=asciistuff
pkgver=1.2.5
pkgrel=3
pkgdesc='Library for producing ASCII arts from a text or an image.'
arch=('any')
url='https://pypi.org/project/asciistuff/#files'
license=('AGPL3')
makedepends=('python2-setuptools' 'python-setuptools')
source=("https://files.pythonhosted.org/packages/source/${_pkgname::1}/$_pkgname/$_pkgname-$pkgver.tar.gz")
sha512sums=('5e90dc3c4ce69679cdc50eff9ab4028f3a7c641978f9e87d2d0cfed188e58d7747e7bd18550cde3957cf677fc11ea647eee60d62dbe5a6cbb87f25a11587fd85')

prepare() {
  cp -a "$_pkgname-$pkgver"{,-2}
}

build() {
  cd "$_pkgname-$pkgver-2"

  python2 setup.py build

  cd "$srcdir/$_pkgname-$pkgver"

  python setup.py build
}

package_python2-asciistuff() {
  depends=('python2' 'python2-terminaltables' 'python2-termcolor'
           'python2-pyfiglet' 'python2-cowpy')

  cd "$_pkgname-$pkgver-2"

  python2 setup.py install --prefix=/usr --root="$pkgdir" -O1 --skip-build

  mv "$pkgdir/usr/bin/$_pkgname" "$pkgdir/usr/bin/${_pkgname}2"
}

package_python-asciistuff() {
  depends=('python' 'python-terminaltables' 'python-termcolor' 'python-cowpy'
           'python-pyfiglet')

  cd "$_pkgname-$pkgver"

  python setup.py install --prefix=/usr --root="$pkgdir" -O1 --skip-build
}

