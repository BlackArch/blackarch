# This file is part of BlackArch Linux ( https://www.blackarch.org/ ).
# See COPYING for license details.

pkgbase=python-asciistuff
pkgname=('python2-asciistuff' 'python-asciistuff')
_pkgname=asciistuff
pkgver=1.0.6
pkgrel=1
pkgdesc='Library for producing ASCII arts from a text or an image.'
arch=('any')
url='https://pypi.org/project/asciistuff/#files'
license=('AGPL3')
makedepends=('python2-setuptools' 'python-setuptools')
source=("https://files.pythonhosted.org/packages/5d/78/bf95e86ef23399f864115c7d4fa4b98a19108ecf231f38f65c3648199546/$_pkgname-$pkgver.tar.gz")
sha512sums=('e266cfbc144f9f700b5c698599842706d24de29dbecbf5d365c40270d5ee667ca4626a421c2c65f3ff2f7956b53c78bb66d8d43a0a336fea2162545026b8283a')

prepare() {
  cp -a "$_pkgname-$pkgver"{,-2}
}

build() {
  cd "$_pkgname-$pkgver-2"

  python2 setup.py build

  cd "$srcdir/$_pkgname-$pkgver"

  python setup.py build
}

package_python2-asciistuff() {
  depends=('python2')

  cd "$_pkgname-$pkgver-2"

  python2 setup.py install --prefix=/usr --root="$pkgdir" -O1 --skip-build

  mv "$pkgdir/usr/bin/$_pkgname" "$pkgdir/usr/bin/${_pkgname}2"
}

package_python-asciistuff() {
  depends=('python')

  cd "$_pkgname-$pkgver"

  python setup.py install --prefix=/usr --root="$pkgdir" -O1 --skip-build
}

