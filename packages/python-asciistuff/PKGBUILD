# This file is part of BlackArch Linux ( https://www.blackarch.org/ ).
# See COPYING for license details.

pkgbase=python-asciistuff
pkgname=('python2-asciistuff' 'python-asciistuff')
_pkgname=asciistuff
pkgver=1.2.1
pkgrel=1
pkgdesc='Library for producing ASCII arts from a text or an image.'
arch=('any')
url='https://pypi.org/project/asciistuff/#files'
license=('AGPL3')
makedepends=('python2-setuptools' 'python-setuptools')
source=("https://files.pythonhosted.org/packages/28/ba/5cf5a316882af73ecdf40734304dcfa701045c8727e0686eceff080d78a3/$_pkgname-$pkgver.tar.gz")
sha512sums=('1461eedf8f715480bcc891ed23db2b508e57a789b23fee66a4595213bb30e3175d6d5b2ef941ed86b1a7fca81601ea09a72ae6fa83179e664fa2e18310a1c86c')

prepare() {
  cp -a "$_pkgname-$pkgver"{,-2}
}

build() {
  cd "$_pkgname-$pkgver-2"

  python2 setup.py build

  cd "$srcdir/$_pkgname-$pkgver"

  python setup.py build
}

package_python2-asciistuff() {
  depends=('python2' 'python2-terminaltables' 'python2-termcolor'
           'python2-pyfiglet' 'python2-cowpy')

  cd "$_pkgname-$pkgver-2"

  python2 setup.py install --prefix=/usr --root="$pkgdir" -O1 --skip-build

  mv "$pkgdir/usr/bin/$_pkgname" "$pkgdir/usr/bin/${_pkgname}2"
}

package_python-asciistuff() {
  depends=('python' 'python-terminaltables' 'python-termcolor' 'python-cowpy'
           'python-pyfiglet')

  cd "$_pkgname-$pkgver"

  python setup.py install --prefix=/usr --root="$pkgdir" -O1 --skip-build
}

