# This file is part of BlackArch Linux ( https://www.blackarch.org/ ).
# See COPYING for license details.

pkgname=jeopardize
pkgver=5.34f1d07
pkgrel=2
pkgdesc='A low(zero) cost threat intelligence & response tool against phishing domains.'
groups=('blackarch' 'blackarch-defensive' 'blackarch-social')
arch=('any')
url='https://github.com/utkusen/jeopardize'
license=('BSD')
depends=('python' 'python-colorama' 'python-lxml' 'python-selenium'
         'python-termcolor' 'python-requests' 'python-dnspython'
         'python-chardet' 'python-psutil' 'python-dateutil' 'python-pytz'
         'python-urllib3' 'python-certifi' 'python-python-whois')
makedepends=('git')
source=("git+https://github.com/utkusen/$pkgname.git")
sha512sums=('SKIP')

pkgver() {
  cd $pkgname

  echo $(git rev-list --count HEAD).$(git rev-parse --short HEAD)
}

package() {
  cd $pkgname

  install -dm 755 "$pkgdir/usr/bin"
  install -dm 755 "$pkgdir/usr/share/$pkgname"

  install -Dm 644 -t "$pkgdir/usr/share/doc/$pkgname/" README.md
  install -Dm 644 LICENSE "$pkgdir/usr/share/licenses/$pkgname/LICENSE"

  rm README.md LICENSE

  cp -a * "$pkgdir/usr/share/$pkgname/"

  cat > "$pkgdir/usr/bin/$pkgname" << EOF
#!/bin/sh
cd /usr/share/$pkgname
exec python $pkgname.py "\$@"
EOF

  chmod +x "$pkgdir/usr/bin/$pkgname"
}

