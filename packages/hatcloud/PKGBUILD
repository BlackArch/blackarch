# This file is part of BlackArch Linux ( http://blackarch.org ).
# See COPYING for license details.

pkgname='hatcloud'
pkgver=25.6700f99
pkgrel=1
pkgdesc='Bypass CloudFlare with Ruby.'
groups=('blackarch' 'blackarch-recon')
arch=('any')
url='https://github.com/HatBashBR/HatCloud'
license=('GPL3')
depends=('ruby')
makedepends=('git')
source=('git+https://github.com/HatBashBR/HatCloud')
sha1sums=('SKIP')

pkgver() {
  cd "$srcdir/HatCloud"

  echo $(git rev-list --count HEAD).$(git rev-parse --short HEAD)
}

prepare() {
  cd "$srcdir/HatCloud"

  sed -i 's|local/bin|/bin|' hatcloud.rb
}

package() {
  cd "$srcdir/HatCloud"

  install -Dm755 hatcloud.rb "$pkgdir/usr/bin/hatcloud"
  install -Dm644 -t "$pkgdir/usr/share/doc/hatcloud/" README.md
  install -Dm644 LICENSE "$pkgdir/usr/share/licenses/hatcloud/LICENSE"
}
