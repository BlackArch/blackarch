# This file is part of BlackArch Linux ( https://www.blackarch.org/ ).
# See COPYING for license details.

pkgname=dagon
pkgver=244.f065d7b
pkgrel=2
pkgdesc='Advanced Hash Manipulation.'
groups=('blackarch' 'blackarch-crypto')
arch=('any')
url='https://github.com/Ekultek/Dagon'
license=('custom:unknown')
depends=('python2' 'python2-colorlog' 'python2-requests' 'python2-passlib'
         'python2-pysha3' 'python2-bcrypt' 'python2-pydes')
makedepends=('git')
source=("$pkgname::git+https://github.com/Ekultek/Dagon.git")
sha512sums=('SKIP')

pkgver() {
  cd $pkgname

  echo $(git rev-list --count HEAD).$(git rev-parse --short HEAD)
}

package() {
  cd $pkgname

  install -dm 755 "$pkgdir/usr/bin"
  install -dm 755 "$pkgdir/usr/share/$pkgname"

  install -Dm 644 -t "$pkgdir/usr/share/doc/$pkgname/" README.md

  rm README.md

  cp -a * "$pkgdir/usr/share/$pkgname/"

  cat > "$pkgdir/usr/bin/$pkgname" << EOF
#!/bin/sh
cd /usr/share/$pkgname
exec python2 $pkgname.py "\$@"
EOF

  chmod +x "$pkgdir/usr/bin/$pkgname"
}

