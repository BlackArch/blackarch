pkgname=pev
pkgver=0.60
pkgrel=3
groups=('blackarch' 'blackarch-forensic' 'blackarch-reversing')
pkgdesc="Command line based tool for PE32/PE32+ file analysis"
arch=('i686' 'x86_64' 'armv6h' 'armv7h')
url='http://pev.sourceforge.net/'
license=('GPL')
depends=('glibc')
source=("http://sourceforge.net/projects/pev/files/pev-$pkgver/pev-$pkgver.tar.gz")
md5sums=('9eb79416be853c697228975a12280507')

build() {
  cd "$srcdir/pev-$pkgver"
  make
}

package() {
  cd "$srcdir/pev-$pkgver"

  # Create base directories.
  install -dm755 "$pkgdir/usr/bin"
  install -dm755 "$pkgdir/usr/lib"
  install -dm755 "$pkgdir/usr/share/man/man1"

  # Correct hard coded DESTDIR in Makefiles.
  sed -i "s|\$(DESTDIR)/\$(PREFIX)/bin|\$(DESTDIR)\$(PREFIX)/bin|" src/Makefile
  sed -i "s|/usr/share/pev|$pkgdir/usr/share/pev|" src/Makefile
  sed -i "s|/usr/share/man/man1|$pkgdir/usr/share/man/man1|" src/Makefile
  sed -i "s|\$(DESTDIR)/\$(PREFIX)/lib|\$(DESTDIR)\$(PREFIX)/lib|" lib/libpe/Makefile

  make "DESTDIR=$pkgdir" install
}
