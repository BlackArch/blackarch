# This file is part of BlackArch Linux ( http://blackarch.org ).
# See COPYING for license details.

pkgname='pev'
pkgver='0.80'
pkgrel=1
groups=('blackarch' 'blackarch-forensic' 'blackarch-reversing')
pkgdesc='Command line based tool for PE32/PE32+ file analysis.'
arch=('x86_64' 'armv6h' 'armv7h' 'aarch64')
url='http://pev.sourceforge.net/'
license=('GPL3')
depends=('glibc' 'openssl' 'pcre')
source=("http://sourceforge.net/projects/pev/files/pev-${pkgver}/pev-${pkgver}.tar.gz")
sha1sums=('18dcb65269344b5047504cca8598477b33532bab')

prepare() {
  cd "$srcdir/pev"

  find . -type f -name "Makefile" -exec \
    sed -i 's|^datarootdir = |datarootdir = \$(DESTDIR)/|' {} +
}

build() {
  cd "$srcdir/pev"

  make
}

package() {
  cd "$srcdir/pev"

  make DESTDIR="$pkgdir" install
}
