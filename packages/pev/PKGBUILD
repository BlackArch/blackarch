# This file is part of BlackArch Linux ( http://blackarch.org ).
# See COPYING for license details.

pkgname='pev'
pkgver='0.70'
pkgrel=2
groups=('blackarch' 'blackarch-forensic' 'blackarch-reversing')
pkgdesc='Command line based tool for PE32/PE32+ file analysis.'
arch=('i686' 'x86_64' 'armv6h' 'armv7h')
url='http://pev.sourceforge.net/'
license=('GPL3')
depends=('glibc' 'openssl' 'pcre')
source=("http://sourceforge.net/projects/pev/files/pev-${pkgver}/pev-${pkgver}.tar.gz")
sha1sums=('b2d1191c3b57049c78ef77b8f54f7f78838af129')

prepare() {
  cd "$srcdir/pev"

  find . -type f -name "Makefile" -exec \
    sed -i 's|^datarootdir = |datarootdir = \$(DESTDIR)/|' {} +
}

build() {
  cd "$srcdir/pev"

  make
}

package() {
  cd "$srcdir/pev"

  make DESTDIR="$pkgdir" install
}
