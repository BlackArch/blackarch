pkgname='pev'
pkgver=0.60
pkgrel=1
groups=(blackarch blackarch-forensics blackarch-reversing)
pkgdesc='Command line based tool for PE32/PE32+ file analysis'
arch=('i686' 'x86_64')
url='http://pev.sourceforge.net/'
license=('GPL')
depends=('glibc')
source=(http://sourceforge.net/projects/${pkgname}/files/${pkgname}-${pkgver}/${pkgname}-${pkgver}.tar.gz)
md5sums=('9eb79416be853c697228975a12280507')

build() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  make
}

package() {
  cd "${srcdir}/${pkgname}-${pkgver}"

  # Create base directories.
  install -d $pkgdir/usr/bin
  install -d $pkgdir/usr/lib
  install -d $pkgdir/usr/share/man/man1

  # Correct hard coded DESTDIR in Makefiles (bad practice from upstream).
  sed -i "s|\$(DESTDIR)/\$(PREFIX)/bin|\$(DESTDIR)\$(PREFIX)/bin|" src/Makefile
  sed -i "s|/usr/share/pev|${pkgdir}/usr/share/pev|" src/Makefile
  sed -i "s|/usr/share/man/man1|${pkgdir}/usr/share/man/man1|" src/Makefile
  sed -i "s|\$(DESTDIR)/\$(PREFIX)/lib|\$(DESTDIR)\$(PREFIX)/lib|" lib/libpe/Makefile

  make DESTDIR=$pkgdir install
}
