# This file is part of BlackArch Linux ( http://blackarch.org ).
# See COPYING for license details.

pkgname='visql'
pkgver=49.3082e30
pkgrel=1
pkgdesc='Scan SQL vulnerability on target site and sites of on server.'
groups=('blackarch' 'blackarch-scanner' 'blackarch-webapp')
arch=('any')
url='https://github.com/blackvkng/viSQL'
license=('MIT')
depends=('python2' 'python2-requests' 'python2-colorama')
makedepends=('git')
source=('git+https://github.com/blackvkng/viSQL.git')
sha1sums=('SKIP')

pkgver() {
  cd "$srcdir/viSQL"

  echo $(git rev-list --count HEAD).$(git rev-parse --short HEAD)
}

package() {
  cd "$srcdir/viSQL"

  mkdir -p "$pkgdir/usr/bin"
  mkdir -p "$pkgdir/usr/share/visql"

  install -Dm644 -t "$pkgdir/usr/share/doc/visql/" README.md
  install -Dm644 LICENSE "$pkgdir/usr/share/licenses/visql/LICENSE"

  rm README.md LICENSE

  cp -a * "$pkgdir/usr/share/visql/"

  cat > "$pkgdir/usr/bin/visql" << EOF
#!/bin/sh
cd /usr/share/visql
exec python2 viSQL.py "\${@}"
EOF

  chmod a+x "$pkgdir/usr/bin/visql"
}
