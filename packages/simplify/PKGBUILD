# This file is part of BlackArch Linux ( https://www.blackarch.org/ ).
# See COPYING for license details.

pkgname=simplify
pkgver=1.2.1
pkgrel=1
pkgdesc='Generic Android Deobfuscator.'
groups=('blackarch' 'blackarch-mobile')
arch=('any')
url='https://github.com/CalebFenton/simplify'
license=('GPL3')
depends=('java-environment')
makedepends=('git')
source=("https://github.com/CalebFenton/$pkgname/releases/download/v$pkgver/$pkgname-$pkgver.jar")
sha512sums=('8eecf0e4707e308bce07778a7a89c1ca58d51936ff07a8736200ac31d4f5b4209f859a2a834aeb0356504b4c66fd939d96ec590f262ef46667f3c0ca286d4c8c')
install="$pkgname.install"

package() {
  install -dm 755 "$pkgdir/usr/bin"

  install -Dm 644 "$pkgname-$pkgver.jar" \
    "$pkgdir/usr/share/$pkgname/$pkgname-$pkgver.jar"

  cat > "$pkgdir/usr/bin/$pkgname" << EOF
#!/bin/sh
exec java -jar /usr/share/$pkgname/$pkgname-$pkgver.jar "\$@"
EOF

  chmod +x "$pkgdir/usr/bin/$pkgname"
}

