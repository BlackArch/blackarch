# This file is part of BlackArch Linux ( https://www.blackarch.org/ ).
# See COPYING for license details.

# Initial PKGBUILD from AUR.
# Old Maintainer: Niklas Krafczyk <krafczyk.n at gmail dot com>

pkgname=klee-uclibc
pkgver=1.4
pkgrel=1
pkgdesc="KLEE's version of uClibc."
arch=('x86_64' 'aarch64')
url='https://github.com/klee/klee-uclibc'
license=('LGPL')
makedepends=('llvm' 'clang' 'python' 'wget')
source=("https://github.com/klee/klee-uclibc/archive/refs/tags/klee_uclibc_v$pkgver.tar.gz")
sha512sums=('e9732f9b7cf5d8d493ad7885b97921eb24cf0df9943cec1623c26a594044ba637b19829061ed9cc866f7c7d95db8dfcd6f3a10950ccde0bf4c10cb5c7b1323c7')

prepare() {
  mv "$pkgname-klee_uclibc_v$pkgver" $pkgname

  cd $pkgname

  DEVEL_PREFIX="/usr/share/klee-uclibc/" ./configure --make-llvm-lib

  sed -i 's|DEVEL_PREFIX="[^"]*"|DEVEL_PREFIX="/usr/share/klee-uclibc/usr"|' \
    .config
  sed -i 's|RUNTIME_PREFIX="[^"]*"|RUNTIME_PREFIX="/usr/share/klee-uclibc/usr"|' \
    .config
}

build() {
  cd $pkgname

  make -j$(nproc)
}

package() {
  cd $pkgname

  make DESTDIR="$pkgdir" install
}

