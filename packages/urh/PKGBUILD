# This file is part of BlackArch Linux ( http://blackarch.org ).
# See COPYING for license details.

pkgname='urh'
pkgver=2072.1cca1844
pkgrel=1
pkgdesc='Universal Radio Hacker: investigate wireless protocols like a boss.'
groups=('blackarch' 'blackarch-radio')
arch=('i686' 'x86_64' 'armv6h' 'armv7h')
url='https://github.com/jopohl/urh'
license=('GPL3')
depends=('python' 'python-numpy' 'python-psutil' 'python-pyqt5' 'python-pyzmq'
         'python-lxml' 'python-pytest' 'cython' 'gnuradio')
makedepends=('git')
source=('git+https://github.com/jopohl/urh')
sha1sums=('SKIP')

pkgver() {
  cd "$srcdir/urh"

  echo $(git rev-list --count HEAD).$(git rev-parse --short HEAD)
}

build() {
  cd "$srcdir/urh"

  python setup.py build
}

package() {
  cd "$srcdir/urh"

  mkdir -p "$pkgdir/usr/share/doc/urh"

  python setup.py install --root="$pkgdir" --prefix=/usr --optimize=1

  cp -a README.md doc/* "$pkgdir/usr/share/doc/urh/"
  install -Dm644 LICENSE "$pkgdir/usr/share/licenses/urh/LICENSE"
}
