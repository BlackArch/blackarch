# This file is part of BlackArch Linux ( https://www.blackarch.org/ ).
# See COPYING for license details.

pkgname=aranea
pkgver=6.469b9ee
pkgrel=4
groups=('blackarch' 'blackarch-spoof')
pkgdesc='A fast and clean dns spoofing tool.'
arch=('any')
url='https://github.com/TigerSecurity'
license=('custom:unknown')
depends=('libpcap')
makedepends=('git')
source=("git+https://github.com/TigerSecurity/$pkgname.git")
sha512sums=('SKIP')

pkgver() {
  cd $pkgname

  ( set -o pipefail
    git describe --long --tags --abbrev=7 2>/dev/null |
      sed 's/\([^-]*-g\)/r\1/;s/-/./g' ||
    printf "%s.%s" "$(git rev-list --count HEAD)" \
      "$(git rev-parse --short=7 HEAD)"
  )
}

build() {
  cd $pkgname

  make
}

package() {
  cd $pkgname

  install -Dm 755 $pkgname "$pkgdir/usr/bin/$pkgname"
  install -Dm 644 -t "$pkgdir/usr/share/doc/$pkgname/" README
}

