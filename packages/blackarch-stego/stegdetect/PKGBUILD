# This file is part of BlackArch Linux ( https://www.blackarch.org/ ).
# See COPYING for license details.

pkgname=stegdetect
pkgver=19.ac1df7a
pkgrel=1
pkgdesc='An automated tool for detecting steganographic content in images.'
groups=('blackarch' 'blackarch-stego' 'blackarch-defensive'
        'blackarch-forensic')
arch=('x86_64' 'aarch64')
url='https://github.com/redNixon/stegdetect'
license=('BSD')
depends=('gdk-pixbuf2')
makedepends=('git')
source=("git+https://github.com/redNixon/$pkgname.git")
sha512sums=('SKIP')

pkgver() {
  cd $pkgname

  echo $(git rev-list --count HEAD).$(git rev-parse --short HEAD)
}

build() {
  cd $pkgname

  if [ $CARCH = "x86_64" ]
  then
    linux32 ./configure --prefix=/usr
    linux32 make
  else
    ./configure --prefix=/usr
    make
  fi
}

package() {
  cd $pkgname

  install -dm 755 "$pkgdir/usr/share/$pkgname"

  install -Dm 755 -t "$pkgdir/usr/bin" stegcompare stegdeimage $pkgname \
    stegbreak
  install -Dm 644 -t "$pkgdir/usr/share/man/man1/" *.1
  install -Dm 644 LICENSE "$pkgdir/usr/share/licenses/$pkgname/LICENSE"
}

