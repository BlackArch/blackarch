# This file is part of BlackArch Linux ( https://www.blackarch.org/ ).
# See COPYING for license details.

pkgbase=python-cooldict
pkgname=('python2-cooldict' 'python-cooldict')
_pkgname=cooldict
pkgver=1.04
pkgrel=4
pkgdesc='Some useful dict-like structures.'
arch=('any')
url='https://pypi.org/project/cooldict/#files'
license=('custom:unknown')
makedepends=('python2-setuptools' 'python-setuptools')
source=("https://files.pythonhosted.org/packages/3b/fb/0c18df97f1e3aaf2aac909f274047217c02e5d6fd8c30c641a1c14668feb/$_pkgname-$pkgver.tar.gz")
sha512sums=('ad23eb05167582a3d8772ff8680f29651614e2694a452316e5b5c5d7f6dc50281b86ffc821015abd79ba49f78411a023f63645617491a241a1e1428140afd4d1')

prepare() {
  cp -a "$_pkgname-$pkgver"{,-2}
}

build() {
  cd "$_pkgname-$pkgver-2"

  python2 setup.py build

  cd "$srcdir/$_pkgname-$pkgver"

  python setup.py build
}

package_python2-cooldict() {
  depends=('python2' 'python2-ana')

  cd "$_pkgname-$pkgver-2"

  python2 setup.py install --prefix=/usr --root="$pkgdir" --optimize=1 \
    --skip-build
}

package_python-cooldict() {
  depends=('python' 'python-ana')

  cd "$_pkgname-$pkgver"

  python setup.py install --prefix=/usr --root="$pkgdir" --optimize=1 \
    --skip-build
}

