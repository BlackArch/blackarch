# This file is part of BlackArch Linux ( https://www.blackarch.org/ ).
# See COPYING for license details.

pkgname=pepe
pkgver=13.b81889b
pkgrel=2
pkgdesc='Collect information about email addresses from Pastebin.'
groups=('blackarch' 'blackarch-social' 'blackarch-recon')
arch=('any')
url='https://github.com/woj-ciech/pepe'
license=('custom:unknown')
depends=('python' 'python-colorama' 'python-urllib3' 'python-argparse'
         'python-elasticsearch' 'python-google' 'python-requests'
         'python-validate-email' 'python-python-pushsafer' 'python-pyhibp'
         'python-fullcontactpy' 'python-piplapis-python')
makedepends=('git')
source=("git+https://github.com/woj-ciech/$pkgname.git")
sha512sums=('SKIP')

pkgver() {
  cd $pkgname

  echo $(git rev-list --count HEAD).$(git rev-parse --short HEAD)
}

package() {
  cd $pkgname

  install -dm 755 "$pkgdir/usr/bin"
  install -dm 755 "$pkgdir/usr/share/$pkgname"

  install -Dm 644 -t "$pkgdir/usr/share/doc/$pkgname/" README.md

  rm README.md

  cp -a * "$pkgdir/usr/share/$pkgname/"

  cat > "$pkgdir/usr/bin/$pkgname" << EOF
#!/bin/sh
cd /usr/share/$pkgname
exec python3 $pkgname.py "\$@"
EOF

  chmod +x "$pkgdir/usr/bin/$pkgname"
}

