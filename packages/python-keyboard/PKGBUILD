# This file is part of BlackArch Linux ( https://www.blackarch.org/ ).
# See COPYING for license details.

pkgbase=python-keyboard
pkgname=('python2-keyboard' 'python-keyboard')
_pkgname=keyboard
pkgver=0.13.4
pkgrel=1
pkgdesc='Hook and simulate keyboard events on Windows and Linux.'
url='https://pypi.org/project/keyboard/#files'
arch=('any')
license=('MIT')
makedepends=('python2-setuptools' 'python-setuptools')
source=("https://files.pythonhosted.org/packages/5d/32/6f782da2738d88ca3ba8bd7597af3fb3fa379f2c3e465adf1bec9d0396d1/$_pkgname-$pkgver.zip")
sha512sums=('332a3cf5e19a502f946203f1e5c0cef064637f778c4abb4d46fbee1cad01e7567c789c9fa9ae4fcd0f09a4b49c42b61b0ed622f45d36fadeb5ee3b75d751661b')

prepare() {
  echo '0.13.4' > "$_pkgname-$pkgver/CHANGES.md"

  cp -a "$_pkgname-$pkgver"{,-2}
}

build() {
  cd "$_pkgname-$pkgver-2"

  python2 setup.py build

  cd "$srcdir/$_pkgname-$pkgver"

  python setup.py build
}

package_python2-keyboard() {
  depends=('python2')

  cd "$_pkgname-$pkgver-2"

  python2 setup.py install --prefix=/usr --root="$pkgdir" --optimize=1 \
    --skip-build
}

package_python-keyboard() {
  depends=('python')

  cd "$_pkgname-$pkgver"

  python setup.py install --prefix=/usr --root="$pkgdir" --optimize=1 \
    --skip-build
}

