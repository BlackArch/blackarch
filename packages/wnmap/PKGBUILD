pkgname=wnmap
pkgver=0.1
pkgrel=1
pkgdesc="A shell script written with the purpose to automate and chain scans via nmap. You can run nmap with a custom mode written by user and create directories for every mode with the xml/nmap files inside."
groups=('blackarch' 'blackarch-intel')
arch=('i686' 'x86_64')
url="http://nullsecurity.net/tools/automation.html"
license=('GPL')
source=("http://nullsecurity.net/tools/automation/wnmap-$pkgver.tar.gz")
md5sums=('a75dab88bbf057f6cae49ca64ce8c51e')

package() {
  cd "$srcdir/wnmap-$pkgver"

  install -dm755 "$pkgdir/usr/bin/"
  install -dm755 "$pkgdir/usr/share/wnmap/src"
  install -dm755 "$pkgdir/usr/share/doc/wnmap"

  cp -LR --no-preserve=ownership src/* "$pkgdir/usr/share/wnmap/src/"
  cp -L --no-preserve=ownership docs/* "$pkgdir/usr/share/doc/wnmap/"

  sed -i 's|WNMAP_PATH="/path_to/wnmap"|WNMAP_PATH="/usr/share/wnmap"|' \
    "$pkgdir/usr/share/wnmap/src/core/wnmap"

  chmod 755 "$pkgdir/usr/share/wnmap/src/core/wnmap"

  ln -s "/usr/share/wnmap/src/core/wnmap" "$pkgdir/usr/share/wnmap/"
  ln -s "/usr/share/wnmap/src/core/wnmap" "$pkgdir/usr/bin/wnmap"
}
