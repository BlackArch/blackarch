# This file is part of BlackArch Linux ( https://www.blackarch.org/ ).
# See COPYING for license details.

pkgname=dnsfilexfer
pkgver=24.126edcd
pkgrel=2
groups=('blackarch' 'blackarch-networking' 'blackarch-misc')
pkgdesc='File transfer via DNS.'
arch=('any')
url='https://github.com/leonjza/dnsfilexfer'
license=('MIT')
depends=('python2' 'python2-dnspython' 'python2-pycryptodome')
makedepends=('git')
source=("git+https://github.com/leonjza/$pkgname.git")
sha512sums=('SKIP')

pkgver() {
  cd $pkgname

  echo $(git rev-list --count HEAD).$(git rev-parse --short HEAD)
}

package() {
  cd $pkgname

  install -dm 755 "$pkgdir/usr/bin"
  install -dm 755 "$pkgdir/usr/share/$pkgname"

  install -Dm 644 -t "$pkgdir/usr/share/doc/$pkgname/" README.md
  install -Dm 644 LICENSE "$pkgdir/usr/share/licenses/$pkgname/LICENSE"

  rm README.* LICENSE

  cp -a * "$pkgdir/usr/share/$pkgname/"

  cat > "$pkgdir/usr/bin/dnsfilexfer-send" << EOF
#!/bin/sh
cd /usr/share/$pkgname
exec python2 dns_send.py "\$@"
EOF

  chmod +x "$pkgdir/usr/bin/dnsfilexfer-send"

  cat > "$pkgdir/usr/bin/dnsfilexfer-recv" << EOF
#!/bin/sh
cd /usr/share/$pkgname
exec python2 dns_recv.py "\$@"
EOF

  chmod +x "$pkgdir/usr/bin/dnsfilexfer-recv"
}

