# This file is part of BlackArch Linux ( http://blackarch.org ).
# See COPYING for license details.

pkgname='socketfuzz'
pkgver=26.089add2
pkgrel=1
pkgdesc='Simple socket fuzzer.'
groups=('blackarch' 'blackarch-fuzzer')
arch=('any')
url='https://github.com/landw1re/socketfuzz'
license=('MIT')
depends=('python2')
makedepends=('git')
source=('git+https://github.com/landw1re/socketfuzz.git')
sha1sums=('SKIP')

pkgver() {
  cd "$srcdir/socketfuzz"

  echo $(git rev-list --count HEAD).$(git rev-parse --short HEAD)
}

prepare() {
  cd "$srcdir/socketfuzz"

  sed -i 's|env python|env python2|' socketfuzz.py
}

package() {
  cd "$srcdir/socketfuzz"

  install -Dm755 socketfuzz.py "$pkgdir/usr/bin/socketfuzz"
  install -Dm644 -t "$pkgdir/usr/share/doc/socketfuzz/" README.md
  install -Dm644 LICENSE "$pkgdir/usr/share/licenses/socketfuzz/LICENSE"
}
