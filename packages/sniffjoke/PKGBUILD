# This file is part of BlackArch Linux ( http://blackarch.org ).
# See COPYING for license details.

pkgname=sniffjoke
pkgver=0.4.1
pkgrel=8
license=('GPL')
groups=('blackarch' 'blackarch-defensive' 'blackarch-networking'
        'blackarch-sniffer')
pkgdesc='Injects packets in the transmission flow that are able to seriously disturb passive analysis like sniffing, interception and low level information theft.'
arch=('x86_64' 'armv6h' 'armv7h' 'aarch64')
url='http://www.delirandom.net/sniffjoke/'
depends=('iptables' 'tcpdump')
makedepends=('git' 'cmake')
source=('git+https://github.com/vecna/sniffjoke'
        'CMake.patch')
sha512sums=('SKIP'
            'bed70225b877245286c818a58f22e34f8a3a995e52ec1113d5f0f4ad3f93716d2d804d7bdd298a1f7949d31dd10a93b081ecf7892ddf6cc4bb80903083f4eb76')

prepare() {
  cd "$srcdir"

  patch -p1 -i CMake.patch
}

build() {
  cd "$srcdir/sniffjoke"

  [[ -d build ]] || mkdir build

  cd build

  cmake -DCMAKE_INSTALL_PREFIX=/usr ..

  make
}

package() {
  cd "$srcdir/sniffjoke"

  cd build

  make DESTDIR="$pkgdir" install

  mv "$pkgdir/usr/etc" "$pkgdir/etc"
}
