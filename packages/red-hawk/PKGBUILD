# This file is part of BlackArch Linux ( https://www.blackarch.org/ ).
# See COPYING for license details.

pkgname=red-hawk
pkgver=27.f560071
pkgrel=1
pkgdesc='All in one tool for Information Gathering, Vulnerability Scanning and Crawling.'
groups=('blackarch' 'blackarch-recon' 'blackarch-scanner' 'blackarch-webapp')
arch=('any')
url='https://github.com/Tuhinshubhra/RED_HAWK'
license=('MIT')
depends=('php')
makedepends=('git')
source=('git+https://github.com/Tuhinshubhra/RED_HAWK.git')
sha512sums=('SKIP')

pkgver() {
  cd RED_HAWK

  echo $(git rev-list --count HEAD).$(git rev-parse --short HEAD)
}

package() {
  cd RED_HAWK

  install -dm 755 "$pkgdir/usr/bin"
  install -dm 755 "$pkgdir/usr/share/$pkgname"

  install -Dm 644 -t "$pkgdir/usr/share/doc/$pkgname/" README.md
  install -Dm 644 LICENSE "$pkgdir/usr/share/licenses/$pkgname/LICENSE"

  rm README.md LICENSE

  cp -a * "$pkgdir/usr/share/$pkgname/"

  cat > "$pkgdir/usr/bin/$pkgname" << EOF
#!/bin/sh
cd /usr/share/$pkgname
exec php rhawk.php "\$@"
EOF

  chmod +x "$pkgdir/usr/bin/$pkgname"
}

