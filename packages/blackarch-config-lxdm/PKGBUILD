# This file is part of BlackArch Linux ( http://blackarch.org ).
# See COPYING for license details.

pkgname=blackarch-config-lxdm
pkgver=9.eec7e01
pkgrel=3
pkgdesc='BlackArch config for lxdm X11 login manager.'
arch=('any')
groups=('blackarch-config')
url='https://github.com/BlackArch/blackarch-config-lxdm'
license=('custom')
depends=('lxdm' 'blackarch-wallpaper' 'terminus-font')
makedepends=('git')
source=('git+https://github.com/BlackArch/blackarch-config-lxdm.git')
install='blackarch-config-lxdm.install'
sha512sums=('SKIP')

pkgver() {
  cd "$srcdir/blackarch-config-lxdm"

  echo $(git rev-list --count HEAD).$(git rev-parse --short HEAD)
}

package() {
  cd "$srcdir/blackarch-config-lxdm"

  install -dm 755 "$pkgdir/etc/lxdm-blackarch/"
  install -dm 755 "$pkgdir/usr/share/blackarch/"
  install -dm 755 "$pkgdir/usr/share/lxdm-blackarch/"
  install -dm 755 "$pkgdir/usr/share/xsessions-blackarch/"

  cp -p -r etc/lxdm/* "$pkgdir/etc/lxdm-blackarch/"
  cp -p -r usr/share/blackarch/* "$pkgdir/usr/share/blackarch/"
  cp -p -r usr/share/lxdm/* "$pkgdir/usr/share/lxdm-blackarch/"
  cp -p -r usr/share/xsessions/* "$pkgdir/usr/share/xsessions-blackarch/"

  #wallpaper.png clashes with blackarch-wallpaper dependency
  mv $pkgdir/usr/share/blackarch/wallpaper{,-cyberpunk}.png
}
