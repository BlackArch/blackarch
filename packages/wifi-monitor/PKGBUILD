# This file is part of BlackArch Linux ( http://blackarch.org ).
# See COPYING for license details.

pkgname='wifi-monitor'
pkgver=24.33b682e
pkgrel=3
groups=('blackarch' 'blackarch-sniffer')
pkgdesc="Prints the IPs on your local network that're sending the most packets."
url='https://github.com/DanMcInerney/wifi-monitor'
arch=('any')
license=('custom')
depends=('python2' 'scapy' 'nbtscan' 'aircrack-ng')
makedepends=('git')
source=('git+https://github.com/DanMcInerney/wifi-monitor.git')
sha1sums=('SKIP')

pkgver() {
  cd "$srcdir/wifi-monitor"

  echo $(git rev-list --count HEAD).$(git rev-parse --short HEAD)
}

prepare() {
  cd "$srcdir"

  find . -type f -name '*.py' -exec sed -i \
    's|#!.*/usr/bin/python|#!/usr/bin/python2|;s|#!.*/usr/bin/env python$|#!/usr/bin/env python2|' {} \;
}

package() {
  cd "$srcdir/wifi-monitor"

  install -Dm755 wifi-monitor.py "$pkgdir/usr/bin/wifi-monitor"
  install -Dm644 README.md "$pkgdir/usr/share/doc/wifi-monitor/README.md"
}
