# This file is part of BlackArch Linux ( https://www.blackarch.org/ ).
# See COPYING for license details.

pkgname=graffiti
pkgver=24.4af61b4
pkgrel=3
pkgdesc='A tool to generate obfuscated one liners to aid in penetration testing.'
groups=('blackarch' 'blackarch-misc')
arch=('any')
url='https://github.com/Ekultek/Graffiti'
license=('custom:unknown')
depends=('python2' 'python2-argparse' 'python2-crypto')
makedepends=('git')
source=("$pkgname::git+https://github.com/Ekultek/Graffiti.git")
sha512sums=('SKIP')

pkgver() {
  cd $pkgname

  echo $(git rev-list --count HEAD).$(git rev-parse --short HEAD)
}

package() {
  cd $pkgname

  install -dm 755 "$pkgdir/usr/bin"
  install -dm 755 "$pkgdir/usr/share/$pkgname"

  install -Dm 644 -t "$pkgdir/usr/share/doc/$pkgname/" README.md

  rm README.md

  cp -a * "$pkgdir/usr/share/$pkgname/"

  cat > "$pkgdir/usr/bin/$pkgname" << EOF
#!/bin/sh
cd /usr/share/$pkgname
exec python2 graffiti.py "\$@"
EOF

  chmod +x "$pkgdir/usr/bin/$pkgname"
}

