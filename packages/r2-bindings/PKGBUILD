# This file is part of BlackArch Linux ( http://blackarch.org ).
# See COPYING for license details.

pkgname='r2-bindings'
pkgver='0.10.1'
pkgrel=1
pkgdesc='Language bindings of the radare2 api for valabind and friends.'
url='http://radare.org'
arch=('i686' 'x86_64' 'armv6h' 'armv7h')
depends=('radare2' 'python2' 'ruby' 'lua' 'ruby1.8' 'ruby1.9' 'gawk'
         'java-environment' 'vala')
makedepends=('valabind' 'swig' 'vala' 'gnuradio-fcdproplus')
license=('GPL3')
source=("https://github.com/radare/radare2-bindings/archive/${pkgver}.tar.gz")
sha1sums=('6adb7b3e8b0d233dbead64f1b95fee656c2150f0')

build() {
  cd "$srcdir/radare2-bindings-$pkgver"

  ./configure --prefix=/usr \
    --enable=ctypes,cxx,duktape,guile,lua,php5,python,valac,newlisp,dlang,\
    r2pipe,vapi

  make
}

package() {
  cd "$srcdir/radare2-bindings-$pkgver"

  make DESTDIR="$pkgdir" install

  cd r2pipe/python

  python2 setup.py install --prefix=/usr "--root=$pkgdir"
}
