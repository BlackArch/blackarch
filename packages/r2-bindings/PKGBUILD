# This file is part of BlackArch Linux ( http://blackarch.org ).
# See COPYING for license details.

pkgname=r2-bindings
pkgver=2.5.0
pkgrel=1
pkgdesc='Language bindings of the radare2 api for valabind and friends.'
url='http://radare.org'
arch=('x86_64' 'armv6h' 'armv7h' 'aarch64')
depends=('radare2' 'python2' 'ruby' 'lua' 'ruby' 'gawk' 'java-environment'
         'vala' 'valabind')
makedepends=('swig' 'vala' 'gnuradio-fcdproplus')
license=('GPL3')
source=("https://github.com/radare/radare2-bindings/archive/${pkgver}.tar.gz")
sha512sums=('f69023f650b9626d04915edec5a91bf3fd56eb413c4c8c0b936547ef782f6f32a058ec4928111b3c73f8038776f3d19a3cfa8d79ee2ebfa5252f4d4639522b76')

build() {
  cd "$srcdir/radare2-bindings-$pkgver"

  ./configure --prefix=/usr \
    --enable=go,java,ctypes,cxx,duktape,guile,php5,python,newlisp,dlang,r2pipe,valac,lua,vapi

  make
}

package() {
  cd "$srcdir/radare2-bindings-$pkgver"

  make DESTDIR="$pkgdir" install

  cd r2-pipe/python

  python2 setup.py install --prefix=/usr "--root=$pkgdir"
}
