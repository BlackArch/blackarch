# This file is part of BlackArch Linux ( http://blackarch.org ).
# See COPYING for license details.

pkgname=r2-bindings
pkgver=3.0.1
pkgrel=1
pkgdesc='Language bindings of the radare2 api for valabind and friends.'
url='http://radare.org'
arch=('x86_64' 'armv6h' 'armv7h' 'aarch64')
depends=('radare2' 'python2' 'ruby' 'lua' 'ruby' 'gawk' 'java-environment'
         'vala' 'valabind' 'go')
makedepends=('swig' 'vala' 'gnuradio-fcdproplus')
license=('GPL3')
source=("https://github.com/radare/radare2-bindings/archive/$pkgver.tar.gz")
sha512sums=('4e0ad1eb8400b89046c2d834a8db22ad490defcfe9a7bc2d3cb59a14ab9214875913900adf16941f42debe2eb2f8dc8a2eab6371d9dde7853964e87cccb13c30')

build() {
  cd "radare2-bindings-$pkgver"

  ./configure --prefix=/usr \
    --enable=go,java,ctypes,cxx,duktape,guile,php5,python,newlisp,dlang,r2pipe,valac,vapi

  make
}

package() {
  cd "radare2-bindings-$pkgver"

  make DESTDIR="$pkgdir" install

  cd r2-pipe/python

  python2 setup.py install --prefix=/usr --root="$pkgdir"
}

