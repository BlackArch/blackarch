# This file is part of BlackArch Linux ( https://www.blackarch.org/ ).
# See COPYING for license details.

pkgname=striker
pkgver=77.c15ab0d
pkgrel=1
pkgdesc='An offensive information and vulnerability scanner.'
groups=('blackarch' 'blackarch-scanner' 'blackarch-recon' 'blackarch-webapp')
arch=('any')
url='https://github.com/UltimateHackers/Striker'
license=('GPL3')
depends=('python2' 'python2-ipy' 'python2-python-whois' 'python2-mechanize'
         'python2-requests' 'python2-beautifulsoup4' 'python2-shodan')
makedepends=('git')
source=('git+https://github.com/UltimateHackers/Striker.git')
sha512sums=('SKIP')

pkgver() {
  cd Striker

  echo $(git rev-list --count HEAD).$(git rev-parse --short HEAD)
}

package() {
  cd Striker

  install -dm 755 "$pkgdir/usr/bin"
  install -dm 755 "$pkgdir/usr/share/$pkgname"

  install -Dm 644 -t "$pkgdir/usr/share/doc/$pkgname/" README.md
  install -Dm 644 LICENSE "$pkgdir/usr/share/licenses/$pkgname/LICENSE"

  rm README.md LICENSE

  cp -a * "$pkgdir/usr/share/$pkgname/"

  cat > "$pkgdir/usr/bin/$pkgname" << EOF
#!/bin/sh
cd /usr/share/$pkgname
exec python2 striker.py "\$@"
EOF

  chmod +x "$pkgdir/usr/bin/$pkgname"
}

