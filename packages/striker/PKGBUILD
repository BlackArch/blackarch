# This file is part of BlackArch Linux ( http://blackarch.org ).
# See COPYING for license details.

pkgname=striker
pkgver=57.763ff3f
pkgrel=1
pkgdesc='An offensive information and vulnerability scanner.'
groups=('blackarch' 'blackarch-scanner' 'blackarch-recon' 'blackarch-webapp')
arch=('any')
url='https://github.com/UltimateHackers/Striker'
license=('GPL3')
depends=('python2' 'python2-ipy' 'python2-python-whois' 'python2-mechanize'
         'python2-requests' 'python2-beautifulsoup4' 'shodan')
makedepends=('git')
source=('git+https://github.com/UltimateHackers/Striker.git')
sha512sums=('SKIP')

pkgver() {
  cd "$srcdir/Striker"

  echo $(git rev-list --count HEAD).$(git rev-parse --short HEAD)
}

package() {
  cd "$srcdir/Striker"

  install -dm 755 "$pkgdir/usr/bin"
  install -dm 755 "$pkgdir/usr/share/striker"

  install -Dm644 -t "$pkgdir/usr/share/doc/striker/" README.md
  install -Dm644 LICENSE "$pkgdir/usr/share/licenses/striker/LICENSE"

  rm README.md LICENSE

  cp -a * "$pkgdir/usr/share/striker/"

  cat > "$pkgdir/usr/bin/striker" << EOF
#!/bin/sh
cd /usr/share/striker
exec python2 striker.py "\${@}"
EOF

  chmod a+x "$pkgdir/usr/bin/striker"
}
