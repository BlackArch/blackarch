# This file is part of BlackArch Linux ( https://www.blackarch.org/ ).
# See COPYING for license details.

pkgname=striker
pkgver=85.87c184d
pkgrel=2
pkgdesc='An offensive information and vulnerability scanner.'
groups=('blackarch' 'blackarch-webapp' 'blackarch-recon' 'blackarch-scanner')
arch=('any')
url='https://github.com/UltimateHackers/Striker'
license=('GPL3')
depends=('python' 'python-ipy' 'python-python-whois' 'python-mechanize'
         'python-requests' 'python-beautifulsoup4' 'python-shodan'
         'python-urllib3')
makedepends=('git')
source=("$pkgname::git+https://github.com/UltimateHackers/Striker.git")
sha512sums=('SKIP')

pkgver() {
  cd $pkgname

  echo $(git rev-list --count HEAD).$(git rev-parse --short HEAD)
}

package() {
  cd $pkgname

  install -dm 755 "$pkgdir/usr/bin"
  install -dm 755 "$pkgdir/usr/share/$pkgname"

  install -Dm 644 -t "$pkgdir/usr/share/doc/$pkgname/" README.md
  install -Dm 644 LICENSE "$pkgdir/usr/share/licenses/$pkgname/LICENSE"

  rm README.md LICENSE

  cp -a * "$pkgdir/usr/share/$pkgname/"

  cat > "$pkgdir/usr/bin/$pkgname" << EOF
#!/bin/sh
cd /usr/share/$pkgname
exec python $pkgname.py "\$@"
EOF

  chmod +x "$pkgdir/usr/bin/$pkgname"
}

