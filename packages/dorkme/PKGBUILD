# This file is part of BlackArch Linux ( https://www.blackarch.org/ ).
# See COPYING for license details.

pkgname=dorkme
pkgver=56.73305d6
pkgrel=4
pkgdesc='Tool designed with the purpose of making easier the searching of vulnerabilities with Google Dorks, such as SQL Injection vulnerabilities.'
groups=('blackarch' 'blackarch-scanner')
arch=('any')
url='https://github.com/blueudp/DorkMe'
license=('GPL3')
depends=('python' 'python-google-search-api')
makedepends=('git')
source=("$pkgname::git+https://github.com/blueudp/DorkMe.git")
sha512sums=('SKIP')

pkgver() {
  cd $pkgname

  echo $(git rev-list --count HEAD).$(git rev-parse --short HEAD)
}

package() {
  cd $pkgname

  install -dm 755 "$pkgdir/usr/bin"
  install -dm 755 "$pkgdir/usr/share/$pkgname"

  install -Dm 644 -t "$pkgdir/usr/share/doc/$pkgname/" README.md
  install -Dm 644 LICENSE "$pkgdir/usr/share/licenses/$pkgname/LICENSE"

  rm README.md LICENSE

  cp -a * "$pkgdir/usr/share/$pkgname/"

  cat > "$pkgdir/usr/bin/$pkgname" << EOF
#!/bin/sh
cd /usr/share/$pkgname
exec python DorkMe.py "\$@"
EOF

  chmod +x "$pkgdir/usr/bin/$pkgname"
}

