# This file is part of BlackArch Linux ( http://blackarch.org ).
# See COPYING for license details.

pkgname='tachyon-scanner'
pkgver=439.8030371
pkgrel=1
pkgdesc='Fast Multi-Threaded Web Discovery Tool.'
groups=('blackarch' 'blackarch-scanner')
arch=('any')
url='https://github.com/delvelabs/tachyon'
license=('custom:unknown')
depends=('python' 'python-urllib3' 'python-easyinject' 'python-click'
         'python-hammertime-http')
makedepends=('git' 'python-setuptools' 'python-pip')
source=('git+https://github.com/delvelabs/tachyon.git' 'setup.py.patch')
sha1sums=('SKIP'
          'e8349610a12e6b192898525c0a375927bf063011')

pkgver() {
  cd "$srcdir/tachyon"

  echo $(git rev-list --count HEAD).$(git rev-parse --short HEAD)
}

prepare() {
  cd "$srcdir"

  patch -p1 < setup.py.patch
}

build() {
  cd "$srcdir/tachyon"

  python setup.py build
}

package() {
  cd "$srcdir/tachyon"

  python setup.py install --root="$pkgdir" --prefix=/usr

  install -Dm644 -t "$pkgdir/usr/share/doc/tachyon/" README.md CHANGELOG THANKS

  #mv "$pkgdir/usr/bin/tachyon.py" "$pkgdir/usr/bin/tachyon-scanner"
}
