# This file is part of BlackArch Linux ( http://blackarch.org ).
# See COPYING for license details.

pkgname=freeipmi
pkgver=1.6.2
pkgrel=1
pkgdesc='Sensor monitoring, system event monitoring, power control, and serial-over-LAN (SOL).'
arch=('x86_64' 'armv6h' 'armv7h' 'aarch64')
groups=('blackarch' 'blackarch-networking' 'blackarch-misc')
url='http://www.gnu.org/software/freeipmi/'
license=('GPL')
depends=('libgcrypt')
options=('!libtool')
source=("http://ftp.gnu.org/gnu/freeipmi/freeipmi-${pkgver}.tar.gz")
sha512sums=('5c3f0b3f0632ad89a49a69aae28420c6ec342c9924aff5a8b6c0ccc1dc2f26f8c68906fbd1bc00f0f102dc50690596bb9e8c3443cdcbf93dabb91a7529d87ee8')

build() {
  cd "$srcdir/freeipmi-$pkgver"

  if [[ "$CARCH" == "i686" || "$CARCH" == "x86_64" ]];
  then
    ./configure --prefix=/usr --sbindir=/usr/bin --mandir=/usr/share/man \
      --sysconfdir=/etc --localstatedir=/var/lib --infodir=/usr/share/info
  else
    ./configure --prefix=/usr --sbindir=/usr/bin --mandir=/usr/share/man \
      --sysconfdir=/etc --localstatedir=/var/lib --infodir=/usr/share/info \
      --build=arm
  fi


  make
}

package() {
  cd "$srcdir/freeipmi-$pkgver"

  make DESTDIR=$pkgdir/ install

  mv "$pkgdir/etc/init.d" "$pkgdir/etc/rc.d"
  mv "$pkgdir/etc/sysconfig" "$pkgdir/etc/conf.d"

  sed -e 's|/etc/init.d/|/etc/rc.d/|g' \
    -e 's|/etc/rc.d/init.d/functions|/etc/rc.d/functions|g' \
    -i "$pkgdir"/etc/rc.d/*

  sed -e 's|CONFFILE=/etc/sysconfig/bmc-watchdog|CONFFILE=/etc/cond.f/bmc-watchdog|g' \
    -i "$pkgdir/etc/rc.d/bmc-watchdog"

  rm -Rf "$pkgdir/usr/share/info/dir"
}
