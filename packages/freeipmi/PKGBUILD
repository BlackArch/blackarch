# This file is part of BlackArch Linux ( https://www.blackarch.org/ ).
# See COPYING for license details.

pkgname=freeipmi
pkgver=1.6.4
pkgrel=1
pkgdesc='Sensor monitoring, system event monitoring, power control, and serial-over-LAN (SOL).'
arch=('x86_64' 'aarch64')
groups=('blackarch' 'blackarch-networking' 'blackarch-misc')
url='http://www.gnu.org/software/freeipmi/'
license=('GPL')
depends=('libgcrypt')
options=('!libtool')
source=("http://ftp.gnu.org/gnu/$pkgname/$pkgname-$pkgver.tar.gz")
sha512sums=('c0a543abed2c6c20f797379423efe56c165ed5b89885568eafc9bfb78c31f409f367140f85f86c01e378be83085e979dfce30bbd560e6d6e242e5a7e2a923e0b')

build() {
  cd "$pkgname-$pkgver"

  if [[ "$CARCH" == "i686" || "$CARCH" == "x86_64" ]];
  then
    ./configure --prefix=/usr --sbindir=/usr/bin --mandir=/usr/share/man \
      --sysconfdir=/etc --localstatedir=/var/lib --infodir=/usr/share/info
  else
    ./configure --prefix=/usr --sbindir=/usr/bin --mandir=/usr/share/man \
      --sysconfdir=/etc --localstatedir=/var/lib --infodir=/usr/share/info \
      --build=arm
  fi

  make
}

package() {
  cd "$pkgname-$pkgver"

 make DESTDIR="$pkgdir" install

  mv "$pkgdir/etc/sysconfig" "$pkgdir/etc/conf.d"

  rm -Rf "$pkgdir/usr/share/info/dir"
}

