# This file is part of BlackArch Linux ( https://www.blackarch.org/ ).
# See COPYING for license details.

pkgname=freeipmi
pkgver=1.6.6
pkgrel=1
pkgdesc='Sensor monitoring, system event monitoring, power control, and serial-over-LAN (SOL).'
arch=('x86_64' 'aarch64')
groups=('blackarch' 'blackarch-networking' 'blackarch-misc')
url='http://www.gnu.org/software/freeipmi/'
license=('GPL')
depends=('libgcrypt')
options=('!libtool')
source=("http://ftp.gnu.org/gnu/$pkgname/$pkgname-$pkgver.tar.gz")
sha512sums=('8a7be74bf003b8858c054bac24615f6fba0133e38e6f759ce81ed734a9ab107eb286c70554ad9663062c92eeccf342b80536aac0da74e4ede1ec51eedd497366')

build() {
  cd "$pkgname-$pkgver"

  if [[ "$CARCH" == "i686" || "$CARCH" == "x86_64" ]];
  then
    ./configure --prefix=/usr --sbindir=/usr/bin --mandir=/usr/share/man \
      --sysconfdir=/etc --localstatedir=/var/lib --infodir=/usr/share/info
  else
    ./configure --prefix=/usr --sbindir=/usr/bin --mandir=/usr/share/man \
      --sysconfdir=/etc --localstatedir=/var/lib --infodir=/usr/share/info \
      --build=arm
  fi

  make
}

package() {
  cd "$pkgname-$pkgver"

 make DESTDIR="$pkgdir" install

  mv "$pkgdir/etc/sysconfig" "$pkgdir/etc/conf.d"

  rm -Rf "$pkgdir/usr/share/info/dir"
}

