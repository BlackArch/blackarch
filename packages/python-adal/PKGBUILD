# This file is part of BlackArch Linux ( https://www.blackarch.org/ ).
# See COPYING for license details.

pkgbase=python-adal
pkgname=('python2-adal' 'python-adal')
_pkgname=adal
pkgver=1.2.5
pkgrel=2
pkgdesc='The ADAL for Python library makes it easy for python application to authenticate to Azure Active Directory (AAD) in order to access AAD protected web resources.'
arch=('any')
url='https://pypi.org/project/adal/#files'
license=('MIT')
makedepends=('python2-setuptools' 'python-setuptools')
source=("https://files.pythonhosted.org/packages/ae/8d/530a05e4e21759a3bd59ed75aed73056398906999e23a7a758c714d87762/$_pkgname-$pkgver.tar.gz")
sha512sums=('8b51a819e7073cd396b1c37aaa7b80e2a4c1529797223a1d52b96167f9dc20c2af5fd5f62226bb5d6bcfc93222474a2920d868f5e4eb26af1b5e4ea50ab910e7')

prepare() {
  cp -a "$_pkgname-$pkgver"{,-2}
}

build() {
  cd "$_pkgname-$pkgver-2"

  python2 setup.py build

  cd "$srcdir/$_pkgname-$pkgver"

  python setup.py build
}

package_python2-adal() {
  depends=('python2' 'python2-pyjwt')

  cd "$_pkgname-$pkgver-2"

  python2 setup.py install --prefix=/usr --root="$pkgdir" --optimize=1 \
    --skip-build
}

package_python-adal() {
  depends=('python' 'python-pyjwt')

  cd "$_pkgname-$pkgver"

  python setup.py install --prefix=/usr --root="$pkgdir" --optimize=1 \
    --skip-build
}

