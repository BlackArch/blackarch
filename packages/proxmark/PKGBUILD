# This file is part of BlackArch Linux ( https://www.blackarch.org/ ).
# See COPYING for license details.

pkgname=proxmark
pkgver=2292.9ebbfd89
pkgrel=1
groups=('blackarch' 'blackarch-radio' 'blackarch-recon' 'blackarch-scanner')
pkgdesc='A powerful general purpose RFID tool, the size of a deck of cards, designed to snoop, listen and emulate everything from Low Frequency (125kHz) to High Frequency (13.56MHz) tags.'
arch=('x86_64' 'armv6h' 'armv7h' 'aarch64')
url='https://github.com/Proxmark/proxmark3'
license=('GPL2')
depends=('libusb' 'perl' 'qt4' 'termcap')
makedepends=('git' 'arm-none-eabi-gcc' 'arm-none-eabi-newlib')
source=('git+https://github.com/Proxmark/proxmark3.git')
sha512sums=('SKIP')

pkgver() {
  cd proxmark3

  echo $(git rev-list --count HEAD).$(git rev-parse --short HEAD)
}

build() {
  cd proxmark3

  make -j1
}

package() {
  cd proxmark3

  install -dm 755 "$pkgdir/usr/bin"
  install -dm 755 "$pkgdir/usr/share/$pkgname"

  install -Dm 644 -t "$pkgdir/usr/share/doc/$pkgname/" README.md HACKING.txt \
    COMPILING.txt CHANGELOG.md doc/*
  install -Dm 644 LICENSE.txt "$pkgdir/usr/share/licenses/$pkgname/LICENSE"

  rm -rf *.txt *.md doc/

  cp -a * "$pkgdir/usr/share/$pkgname/"

  cat > "$pkgdir/usr/bin/$pkgname" << EOF
#!/bin/sh
exec /usr/share/$pkgname/client/proxmark3 "\$@"
EOF

  chmod +x "$pkgdir/usr/bin/$pkgname"

  cat > "$pkgdir/usr/bin/proxmark-flasher" << EOF
#!/bin/sh
exec /usr/share/$pkgname/client/flasher "\$@"
EOF

  chmod +x "$pkgdir/usr/bin/proxmark-flasher"

  cat > "$pkgdir/usr/bin/proxmark-fpga-compress" << EOF
#!/bin/sh
exec /usr/share/$pkgname/client/fpga_compress "\$@"
EOF

  chmod +x "$pkgdir/usr/bin/proxmark-fpga-compress"

  cat > "$pkgdir/usr/bin/proxmark-unbind-proxmark" << EOF
#!/bin/sh
exec /usr/share/$pkgname/client/unbind-proxmark "\$@"
EOF

  chmod +x "$pkgdir/usr/bin/proxmark-unbind-proxmark"
}

