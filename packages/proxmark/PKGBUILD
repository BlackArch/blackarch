# This file is part of BlackArch Linux ( https://www.blackarch.org/ ).
# See COPYING for license details.

pkgname=proxmark
pkgver=2413.61163344
pkgrel=1
groups=('blackarch' 'blackarch-radio' 'blackarch-recon' 'blackarch-scanner')
pkgdesc='A powerful general purpose RFID tool, the size of a deck of cards, designed to snoop, listen and emulate everything from Low Frequency (125kHz) to High Frequency (13.56MHz) tags.'
arch=('x86_64' 'aarch64')
url='https://github.com/Proxmark/proxmark3'
license=('GPL2')
depends=('libusb' 'perl' 'qt4' 'termcap' 'pcsclite')
makedepends=('git' 'arm-none-eabi-gcc' 'arm-none-eabi-newlib' 'pcsclite')
source=("$pkgname::git+https://github.com/Proxmark/proxmark3.git")
sha512sums=('SKIP')

pkgver() {
  cd $pkgname

  echo $(git rev-list --count HEAD).$(git rev-parse --short HEAD)
}

build() {
  cd $pkgname

  make -j1
}

package() {
  cd $pkgname

  install -dm 755 "$pkgdir/usr/bin"
  install -dm 755 "$pkgdir/usr/share/$pkgname"

  install -Dm 644 -t "$pkgdir/usr/share/doc/$pkgname/" README.md HACKING.txt \
    COMPILING.txt CHANGELOG.md doc/*
  install -Dm 644 LICENSE.txt "$pkgdir/usr/share/licenses/$pkgname/LICENSE"

  rm -rf *.txt *.md doc/

  cp -a * "$pkgdir/usr/share/$pkgname/"

  cat > "$pkgdir/usr/bin/$pkgname" << EOF
#!/bin/sh
exec /usr/share/$pkgname/client/proxmark3 "\$@"
EOF

  chmod +x "$pkgdir/usr/bin/$pkgname"

  cat > "$pkgdir/usr/bin/$pkgname-flasher" << EOF
#!/bin/sh
exec /usr/share/$pkgname/client/flasher "\$@"
EOF

  chmod +x "$pkgdir/usr/bin/$pkgname-flasher"

  cat > "$pkgdir/usr/bin/$pkgname-fpga-compress" << EOF
#!/bin/sh
exec /usr/share/$pkgname/client/fpga_compress "\$@"
EOF

  chmod +x "$pkgdir/usr/bin/$pkgname-fpga-compress"

  cat > "$pkgdir/usr/bin/$pkgname-unbind-$pkgname" << EOF
#!/bin/sh
exec /usr/share/$pkgname/client/unbind-$pkgname "\$@"
EOF

  chmod +x "$pkgdir/usr/bin/$pkgname-unbind-$pkgname"
}

