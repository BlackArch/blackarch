# This file is part of BlackArch Linux ( http://blackarch.org ).
# See COPYING for license details.

pkgname=spipscan
pkgver=69.4ad3235
pkgrel=3
epoch=1
groups=('blackarch' 'blackarch-webapp' 'blackarch-scanner')
pkgdesc='SPIP (CMS) scanner for penetration testing purpose written in Python.'
arch=('any')
url='https://github.com/PaulSec/SPIPScan'
license=('MIT')
depends=('python' 'python2' 'python2-beautifulsoup4')
makedepends=('git')
source=('git+https://github.com/PaulSec/SPIPScan.git')
sha512sums=('SKIP')

pkgver() {
  cd "$srcdir/SPIPScan"

  echo $(git rev-list --count HEAD).$(git rev-parse --short HEAD)
}

package() {
  cd "$srcdir/SPIPScan"

  install -dm 755 "$pkgdir/usr/bin"
  install -dm 755 "$pkgdir/usr/share/spipscan"

  install -Dm744 README.md "$pkgdir/usr/share/doc/spipscan/README.md"
  install -Dm744 LICENSE "$pkgdir/usr/share/licenses/spipscan/LICENSE"

  rm -rf README.md LICENSE

  cp -a * "$pkgdir/usr/share/spipscan/"

  cat > "$pkgdir/usr/bin/spipscan" << EOF
#!/bin/sh
cd /usr/share/spipscan/
exec python2 spipscan.py "\$@"
EOF

  chmod +x "$pkgdir/usr/bin/spipscan"
}
