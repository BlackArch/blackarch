# This file is part of BlackArch Linux ( https://www.blackarch.org/ ).
# See COPYING for license details.

pkgname=crowbar
pkgver=79.a338de6
pkgrel=1
groups=('blackarch' 'blackarch-cracker')
pkgdesc='A brute forcing tool that can be used during penetration tests. It is developed to support protocols that are not currently supported by thc-hydra and other popular brute forcing tools.'
arch=('any')
url='https://github.com/galkan/crowbar'
license=('custom:unknown')
depends=('python2' 'python2-paramiko' 'openvpn' 'openssh' 'freerdp' 'tigervnc')
makedepends=('git')
source=('git+https://github.com/galkan/crowbar.git')
sha512sums=('SKIP')

pkgver() {
  cd $pkgname

  echo $(git rev-list --count HEAD).$(git rev-parse --short HEAD)
}

package() {
  cd $pkgname

  install -dm 755 "$pkgdir/usr/bin"
  install -dm 755 "$pkgdir/usr/share/$pkgname"

  install -Dm 644 README.md "$pkgdir/usr/share/doc/$pkgname/README"
  install -Dm 644 LICENSE.txt "$pkgdir/usr/share/licenses/$pkgname/LICENSE.txt"

  rm README.md LICENSE.txt

  cp -a * "$pkgdir/usr/share/$pkgname/"

  cat > "$pkgdir/usr/bin/$pkgname" << EOF
#!/bin/sh
cd /usr/share/$pkgname
exec python2 crowbar.py "\$@"
EOF

  chmod +x "$pkgdir/usr/bin/$pkgname"
}

