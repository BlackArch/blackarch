# This file is part of BlackArch Linux ( http://blackarch.org ).
# See COPYING for license details.

pkgname='crowbar'
pkgver=79.a338de6
pkgrel=1
groups=('blackarch' 'blackarch-cracker')
pkgdesc='A brute forcing tool that can be used during penetration tests. It is developed to support protocols that are not currently supported by thc-hydra and other popular brute forcing tools.'
arch=('any')
url='https://github.com/galkan/crowbar'
license=('custom:unknown')
depends=('python2' 'python2-paramiko' 'openvpn' 'openssh' 'freerdp' 'tigervnc')
makedepends=('git')
source=('git+https://github.com/galkan/crowbar.git')
sha1sums=('SKIP')

pkgver() {
  cd "$srcdir/crowbar"

  echo $(git rev-list --count HEAD).$(git rev-parse --short HEAD)
}

package() {
  cd "$srcdir/crowbar"

  mkdir -p "$pkgdir/usr/bin"
  mkdir -p "$pkgdir/usr/share/crowbar"

  install -Dm644 README.md "$pkgdir/usr/share/doc/crowbar/README"
  install -Dm644 LICENSE.txt "$pkgdir/usr/share/licenses/crowbar/LICENSE.txt"

  rm README.md LICENSE.txt

  cp -a * "$pkgdir/usr/share/crowbar"

  cat > "$pkgdir/usr/bin/crowbar" << EOF
#!/bin/sh
cd /usr/share/crowbar
exec python2 crowbar.py "\$@"
EOF

  chmod a+x "$pkgdir/usr/bin/crowbar"
}
