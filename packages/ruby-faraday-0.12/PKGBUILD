# Maintainer: lsfxz (https://github.com/lsfxz/quarry)
# This file is part of BlackArch Linux ( http://blackarch.org ).
# See COPYING for license details.

pkgname='ruby-faraday-0.12'
_gemname='faraday'
pkgver='0.12.2'
pkgrel=1
pkgdesc="HTTP/REST API client library."
arch=('any')
url='https://github.com/lostisland/faraday'
license=("MIT")
depends=('ruby' 'ruby-multipart-post')
options=('!emptydirs')
source=("https://rubygems.org/downloads/$_gemname-$pkgver.gem" )
noextract=("$_gemname-$pkgver.gem")
sha1sums=('e107d71d1f6878c99c243bc18a80f146b13fabe5' )


package() {
  local _gemdir="$(ruby -rubygems -e 'puts Gem.default_dir')"
  gem install --ignore-dependencies --no-document --no-user-install -i "$pkgdir/$_gemdir" -n "$pkgdir"/usr/bin $_gemname-$pkgver.gem 
  rm "$pkgdir/$_gemdir/cache/$_gemname-$pkgver.gem"
  install -D -m644 "$pkgdir/$_gemdir/gems/$_gemname-$pkgver/LICENSE.md" "$pkgdir/usr/share/licenses/$pkgname/LICENSE.md"

  # Preserve library and other required directories. Remove junk that gem installs.
  local _copydir=`mktemp -d -t $_gemname.XXX`
  chmod 755 $_copydir
  (cd "$pkgdir/$_gemdir/gems/$_gemname-$pkgver"/ && cp -r --parents lib $_copydir)
  rm -rf "$pkgdir/$_gemdir/gems/$_gemname-$pkgver"
  mv $_copydir "$pkgdir/$_gemdir/gems/$_gemname-$pkgver"

}
