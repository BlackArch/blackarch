# This file is part of BlackArch Linux ( https://www.blackarch.org/ ).
# See COPYING for license details.

pkgname=mingw-w64-crt
pkgver=7.0.0
_pkgver=${pkgver/rc/-rc}
pkgrel=1
pkgdesc='MinGW-w64 CRT for Windows.'
arch=('any')
url='http://mingw-w64.sourceforge.net'
license=('custom:unknown')
makedepends=('mingw-w64-gcc' 'mingw-w64-binutils' 'mingw-w64-headers')
options=('!strip' '!buildflags' 'staticlibs' '!emptydirs')
source=("https://sourceforge.net/projects/mingw-w64/files/mingw-w64/mingw-w64-release/mingw-w64-v$_pkgver.tar.bz2"{,.sig})
sha256sums=('aa20dfff3596f08a7f427aab74315a6cb80c2b086b4a107ed35af02f9496b628'
            'SKIP')
validpgpkeys=('CAF5641F74F7DFBA88AE205693BDB53CD4EBC740')
_targets='i686-w64-mingw32 x86_64-w64-mingw32'

prepare() {
  cd "mingw-w64-v$_pkgver/mingw-w64-crt"

  #aclocal
  #automake
}

build() {
  for t in $_targets ; do
    if [ "$t" == 'i686-w64-mingw32' ]; then
      _crt_configure_args='--disable-lib64 --enable-lib32'
    else
      _crt_configure_args="--disable-lib32 --enable-lib64"
    fi
    mkdir -p "$srcdir/crt-$t" && cd "$srcdir/crt-$t"
    "$srcdir/mingw-w64-v$_pkgver/mingw-w64-crt/"configure \
      --prefix="/usr/$t" --host=$t --enable-wildcard $_crt_configure_args
    make
  done
}

package() {
  for t in $_targets ; do
    cd "$srcdir/crt-$t"
    make DESTDIR="$pkgdir" install
  done
}

