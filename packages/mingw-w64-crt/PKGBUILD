# This file is part of BlackArch Linux ( https://www.blackarch.org/ ).
# See COPYING for license details.

pkgname=mingw-w64-crt
pkgver=6.0.0
_pkgver=${pkgver/rc/-rc}
pkgrel=1
pkgdesc='MinGW-w64 CRT for Windows.'
arch=('any')
url='http://mingw-w64.sourceforge.net'
license=('custom:unknown')
makedepends=('mingw-w64-gcc' 'mingw-w64-binutils' 'mingw-w64-headers')
options=('!strip' '!buildflags' 'staticlibs' '!emptydirs')
source=("https://sourceforge.net/projects/mingw-w64/files/mingw-w64/mingw-w64-release/mingw-w64-v$_pkgver.tar.bz2"{,.sig})
sha256sums=('805e11101e26d7897fce7d49cbb140d7bac15f3e085a91e0001e80b2adaf48f0'
            'SKIP')
validpgpkeys=('CAF5641F74F7DFBA88AE205693BDB53CD4EBC740')
_target='x86_64-w64-mingw32'

prepare() {
  cd "mingw-w64-v$_pkgver/mingw-w64-crt"

  aclocal
  automake
}

build() {
  _crt_configure_args="--disable-lib32 --enable-lib64"

  mkdir -p "$srcdir/crt-$_target"

  cd "crt-$_target"

  "$srcdir/mingw-w64-v$_pkgver/mingw-w64-crt/"configure \
    --prefix="/usr/$_target" --host=$_target \
    --enable-wildcard $_crt_configure_args

  make
}

package() {
  cd "$srcdir/crt-$_target"

  make DESTDIR="$pkgdir" install
}

