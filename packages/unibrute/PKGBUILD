# This file is part of BlackArch Linux ( http://blackarch.org ).
# See COPYING for license details.

pkgname='unibrute'
pkgver=1.b3fb4b7
pkgrel=3
groups=('blackarch' 'blackarch-exploitation' 'blackarch-database')
pkgdesc='Multithreaded SQL union bruteforcer.'
arch=('any')
url='https://github.com/GDSSecurity/Unibrute'
license=('custom:unknown')
depends=('python2')
makedepends=('git')
source=('git+https://github.com/GDSSecurity/Unibrute.git')
sha1sums=('SKIP')

pkgver() {
  cd "$srcdir/Unibrute"

  echo $(git rev-list --count HEAD).$(git rev-parse --short HEAD)
}

prepare() {
  cd "$srcdir/Unibrute"

  sed -i 's|#!/bin/sh|#!/usr/bin/python2|' unibrute.py
}

package() {
  cd "$srcdir/Unibrute"

  install -Dm755 unibrute.py "$pkgdir/usr/bin/unibrute"
  install -Dm644 README "$pkgdir/usr/share/doc/unibrute/README"
}
