# This file is part of BlackArch Linux ( http://blackarch.org ).
# See COPYING for license details.

pkgname='cuckoo'
pkgver='2.0'
pkgrel=4
pkgdesc='A malware analysis system.'
url='http://cuckoosandbox.org/'
arch=('x86_64' 'armv6h' 'armv7h' 'aarch64')
license=('custom')
install='cuckoo.install'
backup=('opt/cuckoo/conf/auxiliary.conf'
        'opt/cuckoo/conf/cuckoo.conf'
        'opt/cuckoo/conf/esx.conf'
        'opt/cuckoo/conf/kvm.conf'
        'opt/cuckoo/conf/memory.conf'
        'opt/cuckoo/conf/processing.conf'
        'opt/cuckoo/conf/reporting.conf'
        'opt/cuckoo/conf/virtualbox.conf'
        'opt/cuckoo/conf/vmware.conf')
groups=('blackarch' 'blackarch-malware')
depends=('python2' 'python2-sqlalchemy' 'python2-dpkt' 'python2-jinja'
         'python2-magic' 'python2-pydeep' 'libvirt' 'python2-bottle'
         'python2-pefile' 'python2-pymongo' 'tcpdump' 'yara' 'python2-yara'
         'python2-alembic' 'python2-beautifulsoup4' 'python2-cffi'
         'python2-chardet' 'python2-django' 'python2-ecdsa' 'python2-enum34'
         'python2-flask' 'python2-httpreplay' 'python2-idna' 'python2-ipaddress'
         'python2-itsdangerous' 'python2-mako' 'python2-markupsafe'
         'python2-ndg-httpsclient' 'python2-pyasn1' 'python2-pycparser'
         'python2-pyopenssl' 'python2-dateutil' 'python2-requests' 'python2-six'
         'python2-werkzeug' 'python2-tlslite-ng' 'python2-wakeonlan'
         'python2-bson')
source=("https://downloads.cuckoosandbox.org/cuckoo-current.tar.gz"
        'https://raw.githubusercontent.com/BlackArch/tcpdump_net_raw/master/tcpdump_net_raw.c')
sha1sums=('c6a5a75da50efceb37a204bcd7463a885ff33038'
          'c2c2f7864658a9a34053bfbaa5f756ddb5447815')

prepare() {
   cd "$srcdir/cuckoo"

   find "$srcdir" -type f -name '*.py' -exec \
     sed -i 's|#!/usr/bin/env python|#!/usr/bin/env python2|' {} \;
   sed -i 's|^version_check =.*|version_check = off|' conf/cuckoo.conf
   sed -i 's|^tcpdump =.*|tcpdump = /opt/cuckoo/cuckoo_tcpdump|' \
     conf/auxiliary.conf
   sed -i 's|^platform =.*|platform = linux|' \
     conf/{kvm.conf,virtualbox.conf,vmware.conf,esx.conf}

}

build() {
  cd "$srcdir"

  make tcpdump_net_raw
}

package() {
  cd "$srcdir/cuckoo"

  mkdir -p "$pkgdir/usr/bin/"
  mkdir -p "$pkgdir/opt/cuckoo"

  install -m754 "$srcdir/tcpdump_net_raw" "$pkgdir/opt/cuckoo/cuckoo_tcpdump"

  cp --no-preserve=ownership -a * "$pkgdir/opt/cuckoo"

  chmod u+rwX,g+rwX,o+rX -R "$pkgdir/opt"

  cat > "$pkgdir/usr/bin/cuckoo" << EOF
#!/bin/sh

if ! id | grep -q '(cuckoo)' && ! [ \$EUID -eq 0 ] ; then
  echo "ERROR: Add yourself to the cuckoo group to use cuckoo as an unprivledged user."
  exit 1
fi

cd /opt/cuckoo
exec python2 cuckoo.py "\${@}"
EOF

  chmod a+x "$pkgdir/usr/bin/cuckoo"
}
