# This file is part of BlackArch Linux ( http://blackarch.org ).
# See COPYING for license details.

pkgname='cuckoo'
pkgver='1.2'
pkgrel=1
pkgdesc='A malware analysis system.'
url='http://cuckoosandbox.org/'
arch=('i686' 'x86_64' 'armv6h' 'armv7h')
license=('custom')
install='cuckoo.install'
backup=('opt/cuckoo/conf/auxiliary.conf'
        'opt/cuckoo/conf/cuckoo.conf'
        'opt/cuckoo/conf/esx.conf'
        'opt/cuckoo/conf/kvm.conf'
        'opt/cuckoo/conf/memory.conf'
        'opt/cuckoo/conf/processing.conf'
        'opt/cuckoo/conf/reporting.conf'
        'opt/cuckoo/conf/virtualbox.conf'
        'opt/cuckoo/conf/vmware.conf')
groups=('blackarch' 'blackarch-malware')
depends=('python2' 'python2-sqlalchemy' 'python2-dpkt' 'python2-jinja'
         'python2-magic' 'python2-pydeep' 'libvirt' 'python2-bottle'
         'python2-pefile' 'python2-pymongo' 'tcpdump' 'yara' 'python2-yara')
source=("http://downloads.cuckoosandbox.org/$pkgver/cuckoo_$pkgver.tar.gz"
        'https://raw.githubusercontent.com/BlackArch/tcpdump_net_raw/master/tcpdump_net_raw.c')
sha1sums=('9ad3293dcda35feda4bdf254fca5fe67dd09d586'
          'c2c2f7864658a9a34053bfbaa5f756ddb5447815')

prepare() {
   cd "$srcdir/cuckoo"

   # Set some configuration defaults.
   sed -i 's|env python|env python2|' cuckoo.py
   sed -i 's|^version_check =.*|version_check = off|' conf/cuckoo.conf
   sed -i 's|^tcpdump =.*|tcpdump = /opt/cuckoo/cuckoo_tcpdump|' \
     conf/auxiliary.conf
   sed -i 's|^platform =.*|platform = linux|' \
     conf/{kvm.conf,virtualbox.conf,vmware.conf,esx.conf}

}

build() {
  cd "$srcdir"

  make tcpdump_net_raw
}

package() {
  cd "$srcdir/cuckoo"

  # Base directories.
  mkdir -p "$pkgdir/usr/bin/"
  mkdir -p "$pkgdir/opt/cuckoo"

  install -m754 "$srcdir/tcpdump_net_raw" "$pkgdir/opt/cuckoo/cuckoo_tcpdump"

  cp --no-preserve=ownership -a * "$pkgdir/opt/cuckoo"
  chmod u+rwX,g+rwX,o+rX -R "$pkgdir/opt"

  cat > "$pkgdir/usr/bin/cuckoo" <<EOF
#!/bin/sh

if ! id | grep -q '(cuckoo)' && ! [ \$EUID -eq 0 ] ; then
  echo "ERROR: Add yourself to the cuckoo group to use cuckoo as an unprivledged user."
  exit 1
fi

cd /opt/cuckoo
exec python2 cuckoo.py "\$@"
EOF
 chmod +x "$pkgdir/usr/bin/cuckoo"
}
