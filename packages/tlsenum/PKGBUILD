# This file is part of BlackArch Linux ( http://blackarch.org ).
# See COPYING for license details.

pkgname='tlsenum'
pkgver=78.787c88b
pkgrel=1
groups=('blackarch' 'blackarch-crypto' 'blackarch-scanner')
pkgdesc='A command line tool to enumerate TLS cipher-suites supported by a server.'
arch=('any')
url='https://github.com/Ayrx/tlsenum'
license=('MIT')
depends=('python2' 'python2-coverage' 'python2-flake8' 'python2-pytest'
         'python2-tox' 'python2-pretend' 'python2-idna' 'python2-enum34'
         'python2-construct' 'python2-click')
makedepends=('git')
source=('git+https://github.com/Ayrx/tlsenum.git')
sha1sums=('SKIP')

pkgver() {
  cd "$srcdir/tlsenum"

  echo $(git rev-list --count HEAD).$(git rev-parse --short HEAD)
}

build() {
  cd "$srcdir/tlsenum"

  python2 setup.py build
}

package() {
  cd "$srcdir/tlsenum"

  python2 setup.py install --root="$pkgdir/" --optimize=1 --skip-build
}
