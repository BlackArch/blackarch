# This file is part of BlackArch Linux ( http://blackarch.org ).
# See COPYING for license details.

pkgname='lynis'
pkgver='2.1.1'
pkgrel=1
groups=('blackarch' 'blackarch-scanner')
pkgdesc='An auditing tool for Unix (specialists).'
url='http://www.rootkit.nl/projects/lynis.html'
license=('GPL3')
arch=('any')
depends=('bash')
source=("http://cisofy.com/files/lynis-${pkgver}.tar.gz")
sha1sums=('23b53b4b8f02d340c833c5184215f0a1ebbcbc7e')

package() {
  cd "$srcdir/lynis"

  mkdir -p "$pkgdir/usr/share/lynis"
  mkdir -p "$pkgdir/usr/share/doc/lynis"

  install -Dm644 default.prf "$pkgdir/etc/lynis/default.prf"
  install -Dm755 lynis "$pkgdir/usr/bin/lynis"

  cp -a --no-preserve=ownership db include plugins "$pkgdir/usr/share/lynis"

  chmod -R u=rwX,go=rX "$pkgdir/usr/share/lynis"

  chmod 600 "$pkgdir"/usr/share/lynis/include/*

  install -m644 -t "$pkgdir/usr/share/doc/lynis" README INSTALL CHANGELOG FAQ
  install -Dm644 lynis.8 "$pkgdir/usr/share/man/man8/lynis.8"
}
