pkgname=lynis
pkgver=1.3.7
pkgrel=1
groups=('blackarch' 'blackarch-scanner')
pkgdesc="An auditing tool for Unix (specialists)"
url='http://www.rootkit.nl/projects/lynis.html'
license=('GPL3')
arch=('i686' 'x86_64' 'armv6h' 'armv7h')
source=("http://cisofy.com/files/lynis-$pkgver.tar.gz")
md5sums=('33027cf0f1583a9966c3a59d78397760')

package() {
  cd "$srcdir/lynis-$pkgver"

  # Profile.
  install -Dm 644 default.prf "$pkgdir/etc/lynis/default.prf"

  # Bin.
  install -Dm 755 lynis "$pkgdir/usr/bin/lynis"

  # Install plugins, include, db.
  install -d "$pkgdir/usr/share/lynis"
  cp -a --no-preserve=ownership db include plugins "$pkgdir/usr/share/lynis"
  chmod -R u=rwX,go=rX "$pkgdir/usr/share/lynis"

  # Install misc files.
  install -m644 -t "$pkgdir/usr/share/lynis" README INSTALL CHANGELOG FAQ

  # Install manpage.
  install -Dm644 lynis.8 "$pkgdir/usr/share/man/man8/lynis.8"
}
