# This file is part of BlackArch Linux ( http://blackarch.org ).
# See COPYING for license details.

pkgname=certgraph
pkgver=138.3bc53e0
pkgrel=1
groups=('blackarch' 'blackarch-recon')
pkgdesc='Crawl the graph of certificate Alternate Names.'
arch=('any')
url='https://github.com/lanrat/certgraph'
license=('GPL3')
depends=('glibc' 'go')
makedepends=('git')
source=('git+https://github.com/lanrat/certgraph.git')
sha512sums=('SKIP')
_url='github.com/lanrat/certgraph'

pkgver() {
  cd "$srcdir/certgraph"

  echo $(git rev-list --count HEAD).$(git rev-parse --short HEAD)
}

build() {
  cd "$srcdir"

  install -dm 755 src/$_url && rm -rf src/$_url && mv certgraph src/$_url && cd src/$_url

  GOPATH="$srcdir" go get -d -t $_url
}

package() {
  cd "$srcdir"

  install -dm 755 "$pkgdir/usr/bin"

  GOPATH="$srcdir" go build -v $_url

  install -Dm755 "$srcdir/certgraph" "$pkgdir/usr/bin/certgraph"
  install -Dm644 src/$_url/docs/index.html "$pkgdir/usr/share/certgraph/index.html"
  install -Dm644 src/$_url/README.md "$pkgdir/usr/share/doc/certgraph/README.md"
  install -Dm644 src/$_url/LICENSE "$pkgdir/usr/share/licenses/certgraph/LICENSE"
}
