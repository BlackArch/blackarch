# This file is part of BlackArch Linux ( https://www.blackarch.org/ ).
# See COPYING for license details.

pkgbase=python-keystone-engine
pkgname=('python2-keystone-engine' 'python-keystone-engine')
_pkgname=keystone-engine
pkgver=0.9.1
pkgrel=2
pkgdesc='Keystone assembler engine.'
arch=('x86_64' 'armv6h' 'armv7h' 'aarch64')
url='https://pypi.org/project/keystone-engine/#files'
license=('BSD')
makedepends=('python2-setuptools' 'python-setuptools')
source=("https://files.pythonhosted.org/packages/9a/fc/ed0d3f46921bfaa612d9e8ce8313f99f4149ecf6635659510220c994cb72/$_pkgname-$pkgver-3.tar.gz")
sha512sums=('43bb3d4dfb704c11155561d435144cce1d8486f860cd1bed0a04c36260d097ea316b51ff46ac8cc2552a3a774546443e1885f8bb22c414e07f77317607106217')

prepare() {
  cp -a "$_pkgname-$pkgver-3" "$_pkgname-$pkgver-3-2"
}

build() {
  cd "$_pkgname-$pkgver-3-2"

  python2 setup.py build

  cd "$srcdir/$_pkgname-$pkgver-3"

  python setup.py build
}

package_python2-keystone-engine() {
  depends=('python2')

  cd "$_pkgname-$pkgver-3-2"

  python2 setup.py install --prefix=/usr --root="$pkgdir" --optimize=1 \
    --skip-build
}

package_python-keystone-engine() {
  depends=('python')

  cd "$_pkgname-$pkgver-3"

  python setup.py install --prefix=/usr --root="$pkgdir" --optimize=1 \
    --skip-build
}

