# This file is part of BlackArch Linux ( https://www.blackarch.org/ ).
# See COPYING for license details.

pkgname=python-keylogger
pkgver=2.7.3
pkgrel=2
pkgdesc='Simple keystroke logger.'
groups=('blackarch' 'blackarch-keylogger')
arch=('any')
url='https://github.com/GiacomoLaw/Keylogger'
license=('MIT')
depends=('python' 'python-six' 'python-xlib')
makedepends=('python-setuptools')
source=("https://files.pythonhosted.org/packages/5b/6a/50321cd2015195b13c8dabbb055f09c2d7f4561af02cb98c3c7a043a8df0/keylogger-2.7.3.tar.gz")
sha512sums=('93bdad48b8a20a4c968d9bcaf03cdcd8654efc2cb18b445b025c7ae2afbffd8164a02610f064cb35a4710b7a2d7a395314ac2a5ff793af4b088bbd95e08fe8f5')

build() {
  cd "keylogger-$pkgver"

  touch CHANGES.md
  python setup.py build
}

package() {
  cd "keylogger-$pkgver"

  python setup.py install --prefix=/usr --root="$pkgdir" --optimize=1 \
    --skip-build

  install -Dm 644 -t "$pkgdir/usr/share/doc/$pkgname/" README.md

  # 'keylogger' for sure is a conflicting name so let's rename it here :)
  mv "$pkgdir/usr/bin/keylogger" "$pkgdir/usr/bin/$pkgname"
}

