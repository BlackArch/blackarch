# This file is part of BlackArch Linux ( https://www.blackarch.org/ ).
# See COPYING for license details.

pkgname=xmltojson
pkgver=22.dee0fc7
pkgrel=1
pkgdesc='CLI tool and Python module to convert XML to JSON.'
arch=('any')
groups=('blackarch' 'blackarch-misc')
url='https://github.com/shanahanjrs/xmltojson'
license=('Apache-2.0')
depends=('python' 'python-xmltodict')
makedepends=('git' 'python-build' 'python-pip')
source=("git+https://github.com/shanahanjrs/$pkgname.git")
sha512sums=('SKIP')

pkgver() {
  cd $pkgname

  ( set -o pipefail
    git describe --long --tags --abbrev=7 2>/dev/null |
      sed 's/\([^-]*-g\)/r\1/;s/-/./g' ||
    printf "%s.%s" "$(git rev-list --count HEAD)" \
      "$(git rev-parse --short=7 HEAD)"
  )
}

build() {
  cd $pkgname

  python -m build --wheel --outdir="$startdir/dist"
}

package() {
  cd $pkgname

  pip install \
    --verbose \
    --disable-pip-version-check \
    --no-warn-script-location \
    --ignore-installed \
    --no-compile \
    --no-deps \
    --root="$pkgdir" \
    --prefix=/usr \
    --no-index \
    --find-links="file://$startdir/dist" \
    $pkgname
}

