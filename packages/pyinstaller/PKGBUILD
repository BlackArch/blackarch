# This file is part of BlackArch Linux ( http://blackarch.org ).
# See COPYING for license details.

pkgname='pyinstaller'
pkgver='3.2'
pkgrel=1
epoch=2
groups=('blackarch' 'blackarch-misc')
pkgdesc='A program that converts (packages) Python programs into stand-alone executables, under Windows, Linux, Mac OS X, Solaris and AIX.'
arch=('any')
url="http://www.pyinstaller.org/"
license=('GPL')
depends=('python2')
source=("https://pypi.python.org/packages/33/f9/034a89276301ef5e88efd11e5ea592e3d3b2324706e65bdff7445d271077/PyInstaller-${pkgver}.tar.gz")
sha1sums=('a53b882adbd3f9f4fd14b482ad9467f90ad5154e')
options=('!strip' '!emptydirs')

prepare() {
  cd "$srcdir/PyInstaller-$pkgver"
  grep -RIl '^#!.*python' . | xargs sed -i '/^#!/ s|.*|#! /usr/bin/env python2|'
}

package() {
  cd "$srcdir/PyInstaller-$pkgver"

  # Clean up.
  rm -rf "bootloader" "old" "tests" "PKG-INFO" "setup.py"

  mkdir -p "$pkgdir/usr/bin"
  mkdir -p "$pkgdir/usr/share/doc/pyinstaller"
  mkdir -p "$pkgdir/usr/share/pyinstaller"

  mv "doc/Manual.pdf" "$pkgdir/usr/share/doc/pyinstaller"
  rm -rf "doc"
  cp -a --no-preserve=ownership . "$pkgdir/usr/share/pyinstaller"

  ln -s "/usr/share/pyinstaller/pyinstaller.py" "$pkgdir/usr/bin/pyinstaller"
}
