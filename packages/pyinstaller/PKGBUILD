# This file is part of BlackArch Linux ( http://blackarch.org ).
# See COPYING for license details.

pkgname=pyinstaller
pkgver=2.1
pkgrel=3
epoch=2
groups=('blackarch' 'blackarch-misc')
pkgdesc="A program that converts (packages) Python programs into stand-alone executables, under Windows, Linux, Mac OS X, Solaris and AIX."
arch=('any')
url="http://www.pyinstaller.org/"
license=('GPL')
depends=('python2')
source=("https://pypi.python.org/packages/source/P/PyInstaller/PyInstaller-$pkgver.tar.gz")
sha1sums=('530e0496087ea955ebed6f11b0172c50c73952af')
options=('!strip' '!emptydirs')

prepare() {
  cd "$srcdir/PyInstaller-$pkgver"
  grep -RIl '^#!.*python' . | xargs sed -i '/^#!/ s|.*|#! /usr/bin/env python2|'
}

package() {
  cd "$srcdir/PyInstaller-$pkgver"

  # Clean up.
  rm -rf "bootloader" "old" "tests" "PKG-INFO" "setup.py"

  # Base directories
  install -dm 755 "$pkgdir/usr/bin"
  install -dm 755 "$pkgdir/usr/share/doc/pyinstaller"
  install -dm 755 "$pkgdir/usr/share/pyinstaller"

  mv "doc/Manual.pdf" "$pkgdir/usr/share/doc/pyinstaller"
  rm -rf "doc"
  cp -a --no-preserve=ownership . "$pkgdir/usr/share/pyinstaller"

  ln -s "/usr/share/pyinstaller/pyinstaller.py" "$pkgdir/usr/bin/pyinstaller"
}
