# This file is part of BlackArch Linux ( https://www.blackarch.org/ ).
# See COPYING for license details.

pkgname=pyinstaller
pkgver=6.19.0
pkgrel=1
epoch=2
groups=('blackarch' 'blackarch-misc')
pkgdesc='Bundles a Python application and all its dependencies into a single package.'
arch=('x86_64' 'aarch64')
url='https://pyinstaller.org'
license=('Apache-2.0' 'GPL-2.0-or-later' 'MIT')
depends=(
  'python' 'python-setuptools' 'python-altgraph' 'pyinstaller-hooks-contrib' # project python dependencies
  'glibc' 'binutils' 'gcc' 'base-devel' # project system dependencies
)
makedepends=(
  'python-build' 'python-installer' 'python-wheel' 'python-setuptools' 'python-hatchling' # python package
  'waf' # bootloader
)
source=("https://files.pythonhosted.org/packages/source/${pkgname::1}/$pkgname/$pkgname-$pkgver.tar.gz"
        'fortify-source-fix.patch')
sha512sums=('1859e80852e486272bddfb509c1763536dcae28de2de0d6df1b5b215652736cfc144e1a56c350f99ae4aae25f7bf9379ab70c5517c36595b9be94290a913d3b5'
            '765a2f0c984d966c27309194e73a50b325309c5e65253c92a7140d8b179f94394dd0e01aa1b46b3777a1a06da33ce92919070dd70fde87bf5d81eb750d73f5ed')
options=('!strip' '!emptydirs')

prepare() {
  cd "$pkgname-$pkgver"

  # Remove pre-built binaries
  rm -rf PyInstaller/bootloader/{Darwin,Linux,Windows}*

  # Avoid redefining _FORTIFY_SOURCE if default makepkg CFLAGS are used
  patch -Np1 -i "${srcdir}/fortify-source-fix.patch"
}

build() {
  cd "$pkgname-$pkgver"

  # Compile bootloader
  #cd bootloader
  #python ./waf all
  #cd ..

  # Build package
  export CFLAGS="$CFLAGS -Wno-error=discarded-qualifiers" # Temporary fix for 6.19
  python -m build --wheel --no-isolation
}

package() {
  cd "$pkgname-$pkgver"

  # Package
  python -m installer --destdir="$pkgdir" dist/*.whl

  # Man pages
  install -Dm 644 doc/{"$pkgname",pyi-makespec}.1 -t \
    "$pkgdir/usr/share/man/man1/"

  # Custom license
  install -Dm 644 COPYING.txt -t "$pkgdir/usr/share/licenses/$pkgname/"
}

