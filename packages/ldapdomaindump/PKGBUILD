# This file is part of BlackArch Linux ( https://www.blackarch.org/ ).
# See COPYING for license details.

pkgbase=ldapdomaindump
pkgname=('ldapdomaindump' 'python2-ldapdomaindump')
pkgver=0.9.2
pkgrel=1
pkgdesc='Active Directory information dumper via LDAP.'
groups=('blackarch' 'blackarch-scanner' 'blackarch-networking')
arch=('any')
url='https://pypi.org/project/ldapdomaindump/#files'
license=('MIT')
makedepends=('python-setuptools' 'python2-setuptools')
source=("https://files.pythonhosted.org/packages/c5/11/730c51fdaff1bcaa0214b30034cc8d5254dd43c3fed5a1c90221e41722b8/$pkgname-$pkgver.tar.gz")
sha512sums=('14d38834de7580bf062e3876e564a18535f9df7861ac28e1a8aeeb2bba0260078ed07a3aac470dbbb5c3e1bbb65ffcd624d6223a318fa79391b4a1e5b32bcaef')

prepare() {
  sed -i 's|ldap3==2.5.1|ldap3|g' "$pkgbase-$pkgver/setup.py"

  cp -a "$pkgbase-$pkgver"{,-2}
}

build() {
  cd "$pkgbase-$pkgver-2"

  python2 setup.py build

  cd "$srcdir/$pkgbase-$pkgver"

  python setup.py build
}

package_ldapdomaindump() {
  depends=('python' 'python-ldap3' 'python-future' 'python-dnspython')

  cd "$pkgbase-$pkgver"

  python setup.py install --root="$pkgdir" --prefix=/usr -O1 --skip-build
}

package_python2-ldapdomaindump() {
  depends=('python2' 'python2-ldap3' 'python2-future' 'python2-dnspython')

  install -dm 755 "$pkgdir/usr"

  cd "$pkgbase-$pkgver-2"

  python2 setup.py install --root="$pkgdir" --prefix=/isr -O1 --skip-build

  mv "$pkgdir/isr/"* "$pkgdir/" && rmdir "$pkgdir/isr"
  mv "$pkgdir/"{lib,bin} "$pkgdir/usr/"

  for i in "$pkgdir/usr/bin/"*; do
    mv $i "${i}2"
  done
}

