# This file is part of BlackArch Linux ( https://www.blackarch.org/ ).
# See COPYING for license details.

pkgbase=ldapdomaindump
pkgname=('ldapdomaindump' 'python2-ldapdomaindump')
pkgver=0.9.3
pkgrel=2
pkgdesc='Active Directory information dumper via LDAP.'
groups=('blackarch' 'blackarch-scanner' 'blackarch-networking')
arch=('any')
url='https://pypi.org/project/ldapdomaindump/#files'
license=('MIT')
makedepends=('python-setuptools' 'python2-setuptools')
source=("https://files.pythonhosted.org/packages/36/e4/76b5337e176ab4716409212b1b8b74f701b4c41238d0efcfcf016f51953b/$pkgname-$pkgver.tar.gz")
sha512sums=('b24083d271033e3c38e280d8cedd822ca503840716cd21361b1956595179dd6d67a9d75f0e3e4b47d8d21e313ee1f4e370d20c5453a3703ed370b75ba7d505e1')

prepare() {
  sed -i 's|ldap3==2.5.1|ldap3|g' "$pkgbase-$pkgver/setup.py"

  cp -a "$pkgbase-$pkgver"{,-2}
}

build() {
  cd "$pkgbase-$pkgver-2"

  python2 setup.py build

  cd "$srcdir/$pkgbase-$pkgver"

  python setup.py build
}

package_ldapdomaindump() {
  depends=('python' 'python-ldap3' 'python-future' 'python-dnspython')

  cd "$pkgbase-$pkgver"

  python setup.py install --root="$pkgdir" --prefix=/usr -O1 --skip-build
}

package_python2-ldapdomaindump() {
  depends=('python2' 'python2-ldap3' 'python2-future' 'python2-dnspython')

  install -dm 755 "$pkgdir/usr"

  cd "$pkgbase-$pkgver-2"

  python2 setup.py install --root="$pkgdir" --prefix=/isr -O1 --skip-build

  mv "$pkgdir/isr/"* "$pkgdir/" && rmdir "$pkgdir/isr"
  mv "$pkgdir/"{lib,bin} "$pkgdir/usr/"

  for i in "$pkgdir/usr/bin/"*; do
    mv $i "${i}2"
  done
}

