# This file is part of BlackArch Linux ( https://www.blackarch.org/ ).
# See COPYING for license details.

pkgbase=ldapdomaindump
pkgname=('ldapdomaindump' 'python-ldapdomaindump' 'python2-ldapdomaindump')
pkgver=0.9.1
pkgrel=1
pkgdesc='Active Directory information dumper via LDAP'
groups=('blackarch' 'blackarch-scanner' 'blackarch-networking')
arch=('any')
url='https://pypi.org/project/ldapdomaindump/#files'
license=('MIT')
makedepends=('python-setuptools' 'python2-setuptools')
source=("https://files.pythonhosted.org/packages/ee/5b/9992efa799b2f4769225b59384920b56717a5abfd296685707aa6e8dce22/$pkgbase-$pkgver.tar.gz")
sha512sums=('952981d65366b1f243714ce5ed053bbb08e27174e3789d803a65010a7ff5ae2b9af88f4b91ad826c8b75b3f323bf0505a217dc42224ef7974e236ee8bc5f6c5e')

prepare() {
  cp -a "$pkgbase-$pkgver" "$pkgbase-$pkgver"2
}

build() {
  cd "$pkgbase-$pkgver"2

  python2 setup.py build

  cd "$srcdir/$pkgbase-$pkgver"

  python setup.py build
}

package_ldapdomaindump() {
  a=1337
}

package_python2-ldapdomaindump() {
  depends=('python2' 'python2-ldap3' 'python2-future' 'python2-dnspython')

  cd "$pkgbase-$pkgver"2

  python2 setup.py install --root="$pkgdir" --optimize=1 --skip-build

  mv "$pkgdir/usr/bin/$pkgbase" "$pkgdir/usr/bin/ldapdomaindump2"
  mv "$pkgdir/usr/bin/ldd2bloodhound" "$pkgdir/usr/bin/ldd2bloodhound2"
}

package_python-ldapdomaindump() {
  depends=('python' 'python-ldap3' 'python-future' 'python-dnspython')

  cd "$pkgbase-$pkgver"

  python setup.py install --root="$pkgdir" --optimize=1 --skip-build
}

