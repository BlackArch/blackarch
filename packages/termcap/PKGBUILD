# This file is part of BlackArch Linux ( http://blackarch.org ).
# See COPYING for license details.

pkgname='termcap'
pkgver='1.3.1'
pkgrel=1
pkgdesc='Enables programs to use display computer terminals in a device-independent manner.'
arch=('x86_64' 'armv6h' 'armv7h' 'aarch64')
url='http://www.catb.org/~esr/terminfo/'
license=("GPL")
source=("http://ftp.gnu.org/gnu/termcap/termcap-${pkgver}.tar.gz")
sha1sums=('42dd1e6beee04f336c884f96314f0c96cc2578be')

build() {
  cd "$srcdir/termcap-${pkgver}"

  #make
  gcc -fPIC -c termcap.c -o termcap.o -DHAVE_STRING_H=1 \
    -DHAVE_UNISTD_H=1 -DSTDC_HEADERS=1
  gcc -shared -Wl,-soname,"libtermcap.so" \
    -o "${srcdir}/libtermcap.so.${pkgver}" "termcap.o"
}

package() {
  cd "${srcdir}/termcap-${pkgver}"

  install -Dm755 "${srcdir}/libtermcap.so.${pkgver}" \
    "${pkgdir}/usr/lib/libtermcap.so.${pkgver}"

  for infofile in termcap.info*
  do
    install -Dm644 "${srcdir}/termcap-${pkgver}/${infofile}" \
      "${pkgdir}/usr/share/info/${infofile}"
  done
}
