pkgname=oh-my-zsh-git
pkgver=2183.a38af27
pkgrel=1
epoch=100
groups=('blackarch' 'blackarch-misc')
pkgdesc="A community-driven framework for managing your zsh configuration."
arch=('any')
url="https://github.com/robbyrussell/oh-my-zsh"
license=('MIT')
depends=('zsh')
makedepends=('git')
optdepends=('ruby: for some plugin functionality')
install='oh-my-zsh-git.install'
conflicts=('oh-my-zsh-git')
provides=('oh-my-zsh-git')
source=("git+https://github.com/BlackArch/oh-my-zsh.git")
md5sums=('SKIP')
 
pkgver() {
  cd oh-my-zsh
  printf "%s.%s" "$(git rev-list --count HEAD)" "$(git rev-parse --short HEAD)" 
}
 
package() {
  mkdir -p "${pkgdir}/usr/share/oh-my-zsh"
  cd oh-my-zsh
 
  # We don't need anything related to git in the package.
  rm -rf .git*
 
  # The license should be in /usr/share/licenses.
  install -D -m644 MIT-LICENSE.txt "${pkgdir}/usr/share/licenses/oh-my-zsh/LICENSE"
  rm MIT-LICENSE.txt
 
  # Copy everything else.
  cp -r . "${pkgdir}/usr/share/oh-my-zsh/"
}
