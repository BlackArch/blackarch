# This file is part of BlackArch Linux ( https://www.blackarch.org/ ).
# See COPYING for license details.

pkgname=n1qlmap
pkgver=2.5365444
pkgrel=1
pkgdesc='An N1QL exploitation tool.'
groups=('blackarch' 'blackarch-exploitation')
arch=('any')
url='https://github.com/FSecureLABS/N1QLMap'
license=('custom:unknown')
depends=('python' 'python-requests')
makedepends=('git')
source=("$pkgname::git+https://github.com/FSecureLABS/N1QLMap.git")
sha512sums=('SKIP')

pkgver() {
  cd $pkgname

  echo $(git rev-list --count HEAD).$(git rev-parse --short HEAD)
}

package() {
  cd $pkgname

  install -dm 755 "$pkgdir/usr/bin"
  install -dm 755 "$pkgdir/usr/share/$pkgname"

  install -Dm 644 -t "$pkgdir/usr/share/doc/$pkgname/" README.md

  rm README.md

  cp -a * "$pkgdir/usr/share/$pkgname/"

  cat > "$pkgdir/usr/bin/$pkgname" << EOF
#!/bin/sh
cd /usr/share/$pkgname
exec python n1qlMap.py "\$@"
EOF

  chmod +x "$pkgdir/usr/bin/$pkgname"
}

