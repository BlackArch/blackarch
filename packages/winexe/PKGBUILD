# This file is part of BlackArch Linux ( http://blackarch.org ).
# See COPYING for license details.

pkgname='winexe'
pkgver='1.00'
pkgrel=5
pkgdesc='Remotely execute commands on Windows NT/2000/XP/2003 systems.'
arch=('x86_64' 'armv6h' 'armv7h' 'aarch64')
url='http://sourceforge.net/projects/winexe/'
license=('GPL3')
depends=('gnutls' 'libsasl' 'popt' 'talloc' 'tdb')
makedepends=('python2')
groups=('blackarch' 'blackarch-misc' 'blackarch-windows')
source=("http://downloads.sourceforge.net/project/winexe/winexe-${pkgver}.tar.gz"
        'getopts.patch'
        'gnutls.patch'
        'pidl.patch')
sha1sums=('bdb598745953fcad3a9b6bba8f728c2b714a7aeb'
          '72f1ff7536e4245b3c085beef69d5760ef463cec'
          '4a1913c32622789bba46e5a77d7ca72fcf641342'
          '71123c4996be736ab1e3c53ed7c6bf2885a502e9')

prepare() {
  cd "$srcdir/winexe-$pkgver"

  patch -Np1 -i "$srcdir/pidl.patch"

  cd "source4"
  patch -Np1 -i "$srcdir/getopts.patch"
  patch -Np2 -i "$srcdir/gnutls.patch"
}

build() {
  cd "$srcdir/winexe-$pkgver"

  cd "source4"

  ./autogen.sh

  if [[ "$CARCH" == "i686" || "$CARCH" == "x86_64" ]];
  then
    PYTHON_VER=2 ./configure --prefix=/usr --enable-fhs
  else
    PYTHON_VER=2 ./configure --build=arm --prefix=/usr --enable-fhs
  fi

  make basics

  make bin/winexe
}

package() {
  cd "$srcdir/winexe-$pkgver/source4"

  install -Dm755 bin/winexe "$pkgdir/usr/bin/winexe"
}
