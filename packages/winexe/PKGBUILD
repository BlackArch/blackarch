# This file is part of BlackArch Linux ( http://blackarch.org ).
# See COPYING for license details.

pkgname='winexe'
pkgver='1.00'
pkgrel=2
pkgdesc='Remotely execute commands on Windows NT/2000/XP/2003 systems.'
arch=('i686' 'x86_64' 'armv6h' 'armv7h')
url='http://sourceforge.net/projects/winexe/'
license=('GPL3')
depends=('gnutls' 'libsasl' 'popt' 'talloc' 'tdb')
makedepends=('python2')
groups=('blackarch' 'blackarch-misc' 'blackarch-windows')
source=("http://downloads.sourceforge.net/project/winexe/winexe-$pkgver.tar.gz"
        "getopts.patch"
        "gnutls.patch"
        "pidl.patch")
sha1sums=('bdb598745953fcad3a9b6bba8f728c2b714a7aeb'
          '72f1ff7536e4245b3c085beef69d5760ef463cec'
          '933978541c8efd523bb5ea46639b5c2e77dfe5fd'
          'f3e733496dbf90bc2ebfa853cb1b1da9c9de4bba')

prepare() {
  cd "$srcdir/winexe-$pkgver"

  patch -Np1 -i "$srcdir/pidl.patch"

  cd "source4"
  patch -Np1 -i "$srcdir/getopts.patch"
  patch -Np2 -i "$srcdir/gnutls.patch"
}

build() {
  cd "$srcdir/winexe-$pkgver"

  cd "source4"

  ./autogen.sh
  PYTHON_VER=2 ./configure --enable-fhs --prefix=/usr

  make basics
  make bin/winexe
}

package() {
  cd "$srcdir/winexe-$pkgver/source4"

  install -D -m755 "bin/winexe" "$pkgdir/usr/bin/winexe"
}
