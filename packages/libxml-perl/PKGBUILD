pkgname=libxml-perl
pkgver=0.08
pkgrel=6
pkgdesc="Perl library for working with XML"
arch=('any')
url='http://search.cpan.org/~KMACLEOD/libxml-perl'
license=(GPL PerlArtistic)
depends=(perl-xml-parser)
options=(!emptydirs)
source=(http://www.cpan.org/authors/id/K/KM/KMACLEOD/libxml-perl-$pkgver.tar.gz)
md5sums=('0ed5fbdda53d1301ddaed88db10503bb')

build() {
  cd "libxml-perl-$pkgver"
  perl Makefile.PL INSTALLDIRS=vendor PERL_MM_USE_DEFAULT=1
  make
}

package() {
  make -C libxml-perl-$pkgver/ DESTDIR="$pkgdir" install
  find "$pkgdir" -name '.packlist' -delete
  find "$pkgdir" -name '*.pod' -delete
}
