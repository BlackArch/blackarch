# This file is part of BlackArch Linux ( http://blackarch.org ).
# See COPYING for license details.

pkgname=parsero
pkgver=81.e5b585a
pkgrel=3
groups=('blackarch' 'blackarch-recon')
pkgdesc='A robots.txt audit tool.'
arch=('any')
url='https://github.com/behindthefirewalls/Parsero'
license=('GPL2')
depends=('python3' 'python-urllib3' 'python-beautifulsoup4')
makedepends=('git')
source=('git+https://github.com/behindthefirewalls/Parsero.git')
sha512sums=('SKIP')

pkgver() {
  cd Parsero

  echo $(git rev-list --count HEAD).$(git rev-parse --short HEAD)
}

package() {
  cd Parsero

  install -Dm 755 parsero.py "$pkgdir/usr/bin/$pkgname"
  install -Dm 644 -t "$pkgdir/usr/share/doc/$pkgname/" README.md MANIFEST.in
  install -Dm 644 LICENSE.txt "$pkgdir/usr/share/licenses/$pkgname/LICENSE"
}

