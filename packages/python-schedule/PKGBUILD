# This file is part of BlackArch Linux ( https://www.blackarch.org/ ).
# See COPYING for license details.

pkgbase=python-schedule
pkgname=('python2-schedule' 'python-schedule')
_pkgname=schedule
pkgver=1.2.1
pkgrel=1
pkgdesc='Job scheduling for humans.'
arch=('any')
url='https://pypi.org/project/schedule/#files'
license=('MIT')
makedepends=('python2-setuptools' 'python-setuptools')
source=("https://files.pythonhosted.org/packages/source/${_pkgname::1}/$_pkgname/$_pkgname-$pkgver.tar.gz")
sha512sums=('f2802bb7c49afd649f3e4650366bcd03c64db0301e929c981e1888323b6debac1280d668dad0e2fd7149534cfccefc46eaaafc0a009828ba11606c6a2cfbcc81')

prepare() {
  cp -a "$_pkgname-$pkgver"{,-2}
}

build() {
  cd "$_pkgname-$pkgver-2"

  python2 setup.py build

  cd "$srcdir/$_pkgname-$pkgver"

  python setup.py build
}

package_python2-schedule() {
  depends=('python2')

  cd "$_pkgname-$pkgver-2"

  python2 setup.py install --prefix=/usr --root="$pkgdir" --optimize=1 \
    --skip-build
}

package_python-schedule() {
  depends=('python')

  cd "$_pkgname-$pkgver"

  python setup.py install --prefix=/usr --root="$pkgdir" --optimize=1 \
    --skip-build
}

