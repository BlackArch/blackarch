# This file is part of BlackArch Linux ( https://www.blackarch.org/ ).
# See COPYING for license details.

pkgbase=python-schedule
pkgname=('python2-schedule' 'python-schedule')
_pkgname=schedule
pkgver=1.0.0
pkgrel=1
pkgdesc='Job scheduling for humans.'
arch=('any')
url='https://pypi.org/project/schedule/#files'
license=('MIT')
makedepends=('python2-setuptools' 'python-setuptools')
source=("https://files.pythonhosted.org/packages/65/e5/479fa972fc6906501c8378775c304c0936dfa7001914a93b0bc9eed1c973/$_pkgname-$pkgver.tar.gz")
sha512sums=('528e415382fed39749be1a375815a7475288c8bd6f77133a39e79fc9512e1032c99185a53140db4f7153f1479e6aab57ed05637a09aa92316683f310353c1b11')

prepare() {
  cp -a "$_pkgname-$pkgver"{,-2}
}

build() {
  cd "$_pkgname-$pkgver-2"

  python2 setup.py build

  cd "$srcdir/$_pkgname-$pkgver"

  python setup.py build
}

package_python2-schedule() {
  depends=('python2')

  cd "$_pkgname-$pkgver-2"

  python2 setup.py install --prefix=/usr --root="$pkgdir" --optimize=1 \
    --skip-build
}

package_python-schedule() {
  depends=('python')

  cd "$_pkgname-$pkgver"

  python setup.py install --prefix=/usr --root="$pkgdir" --optimize=1 \
    --skip-build
}

