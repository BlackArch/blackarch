# This file is part of BlackArch Linux ( https://www.blackarch.org/ ).
# See COPYING for license details.

pkgbase=python-schedule
pkgname=('python2-schedule' 'python-schedule')
_pkgname=schedule
pkgver=0.6.0
pkgrel=3
pkgdesc='Job scheduling for humans.'
arch=('any')
url='https://pypi.org/project/schedule/#files'
license=('MIT')
makedepends=('python2-setuptools' 'python-setuptools')
source=("https://files.pythonhosted.org/packages/00/07/6a9953ff83e003eaadebf0a51d33c6b596f9451fcbea36a3a2e575f6af99/$_pkgname-$pkgver.tar.gz")
sha512sums=('0a2704055c7f2b3bbf08582a7f099b5118b0e97e39cda0a299b66620ab17cedb63c44e7bda7d3480649c8b4d609f3e051f8111e7370c39210c305444c287ae93')

prepare() {
  cp -a "$_pkgname-$pkgver"{,-2}
}

build() {
  cd "$_pkgname-$pkgver-2"

  python2 setup.py build

  cd "$srcdir/$_pkgname-$pkgver"

  python setup.py build
}

package_python2-schedule() {
  depends=('python2')

  cd "$_pkgname-$pkgver-2"

  python2 setup.py install --prefix=/usr --root="$pkgdir" --optimize=1 \
    --skip-build
}

package_python-schedule() {
  depends=('python')

  cd "$_pkgname-$pkgver"

  python setup.py install --prefix=/usr --root="$pkgdir" --optimize=1 \
    --skip-build
}

