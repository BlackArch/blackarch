# This file is part of BlackArch Linux ( https://www.blackarch.org/ ).
# See COPYING for license details.

_rubyver=2.3
pkgname=ruby2.3
pkgver=2.3.8
pkgdesc='An object-oriented language for quick and easy programming'
pkgrel=2
arch=('x86_64' 'aarch64')
url='http://www.ruby-lang.org/en/'
license=('BSD' 'custom:unknown')
depends=('gdbm' 'openssl-1.0' 'libffi' 'libyaml' 'gmp' 'zlib')
optdepends=('tk: for Ruby/TK')
makedepends=('gdbm' 'openssl-1.0' 'libffi' 'doxygen' 'graphviz' 'libyaml'
             'ttf-dejavu' 'tk')
options=(!emptydirs)
source=("https://cache.ruby-lang.org/pub/ruby/${pkgver:0:3}/ruby-$pkgver.tar.xz")
sha512sums=('06373050e6c1af9cb6a5863aef878b21c8a45fd0e68414e3d546cb73ec3028207d3acc0a9326428f172b9347a30bbe69a16f9dc0bdb739161d677adb2d888095')

build() {
  cd "ruby-$pkgver"

  export PKG_CONFIG_PATH=/usr/lib/openssl-1.0/pkgconfig

  PKG_CONFIG=/usr/bin/pkg-config ./configure \
    --prefix=/opt/ruby${_rubyver} \
    --program-suffix=-${_rubyver} \
    --sysconfdir=/etc \
    --localstatedir=/var \
    --sharedstatedir=/var/lib \
    --libexecdir=/usr/lib/ruby \
    --enable-shared \
    --disable-rpath \
    --with-dbm-type=gdbm_compat

  make ruby
}

package() {
  cd "ruby-$pkgver"

  make DESTDIR="$pkgdir" install-nodoc

  install -dm 755 "$pkgdir/usr/bin"
  install -dm 755 "$pkgdir/usr/lib"

  for i in erb irb rdoc ri ruby testrb rake gem; do
    ln -s "/opt/$pkgname/bin/$i-$_rubyver" "$pkgdir/usr/bin/$i-$_rubyver"
    ln -s "/opt/$pkgname/bin/$i-$_rubyver" "$pkgdir/opt/$pkgname/bin/$i"
  done

  ln -s "/opt/$pkgname/lib/libruby.so.$_rubyver" \
    "$pkgdir/usr/lib/libruby.so.$_rubyver"

  install -Dm 644 COPYING "$pkgdir/usr/share/licenses/$pkgname/LICENSE"
  install -Dm 644 BSDL "$pkgdir/usr/share/licenses/$pkgname/BSDL"
}

