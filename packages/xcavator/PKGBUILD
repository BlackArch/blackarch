# This file is part of BlackArch Linux ( https://www.blackarch.org/ ).
# See COPYING for license details.

pkgname=xcavator
pkgver=5.bd9e2d8
pkgrel=9
pkgdesc="Man-In-The-Middle and phishing attack tool that steals the victim's credentials of some web services like Facebook."
groups=('blackarch' 'blackarch-sniffer' 'blackarch-spoof')
arch=('any')
url='https://github.com/nccgroup/xcavator'
license=('GPL3')
depends=('python2' 'python2-pysmbc' 'python2-ipaddr' 'python2-configparser'
         'python2-ipaddress' 'python2-pysmb')
makedepends=('git')
options=(!emptydirs)
source=("git+https://github.com/nccgroup/$pkgname.git")
sha512sums=('SKIP')

pkgver() {
  cd $pkgname

  echo $(git rev-list --count HEAD).$(git rev-parse --short HEAD)
}

package() {
  cd $pkgname

  install -dm 755 "$pkgdir/usr/bin"
  install -dm 755 "$pkgdir/usr/share/$pkgname"

  install -Dm 644 -t "$pkgdir/usr/share/doc/$pkgname/" README.md
  install -Dm 644 LICENSE "$pkgdir/usr/share/licenses/$pkgname/LICENSE"

  rm README.md LICENSE

  cp -a * "$pkgdir/usr/share/$pkgname/"

  cat > "$pkgdir/usr/bin/$pkgname" << EOF
#!/bin/sh
cd /usr/share/$pkgname
exec python2 $pkgname.py "\$@"
EOF

  chmod +x "$pkgdir/usr/bin/$pkgname"
}

