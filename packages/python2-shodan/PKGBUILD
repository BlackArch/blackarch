# This file is part of BlackArch Linux ( https://www.blackarch.org/ ).
# See COPYING for license details.

pkgname=python2-shodan
_pkgname=shodan
pkgver=1.27.0
pkgrel=2
pkgdesc='Python library and command-line utility for Shodan (https://developer.shodan.io).'
groups=('blackarch' 'blackarch-recon' 'blackarch-scanner'
        'blackarch-automation')
arch=('any')
url='https://pypi.org/project/shodan/#files'
depends=('python2' 'python2-requests' 'python2-simplejson' 'python2-click'
         'python2-click-plugins' 'python2-xlsxwriter' 'python2-colorama')
makedepends=('python2-setuptools')
license=('MIT')
source=("https://files.pythonhosted.org/packages/0a/3d/4c5aa163102d6b1991cfa26b662df0920643fc7694d5fbafa03b298adb6f/$_pkgname-$pkgver.tar.gz")
sha512sums=('0394a1d94bacc3c70035dc7ef37a34f4fe1ebed0791c7bd62b0f3b1981ff9844463766589cfa899e6485d7881378da11e3c3d4dfc9391902ca794b0efa85ff7f')

build() {
  cd "$_pkgname-$pkgver"

  python2 setup.py build
}

package() {
  cd "$_pkgname-$pkgver"

  python2 setup.py install --root="$pkgdir" --prefix=/usr -O1

  install -Dm 644 LICENSE "$pkgdir/usr/share/licenses/$pkgname/LICENSE"

  mv "$pkgdir/usr/bin/$_pkgname" "$pkgdir/usr/bin/${_pkgname}2"
}

