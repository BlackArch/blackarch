# This file is part of BlackArch Linux ( https://www.blackarch.org/ ).
# See COPYING for license details.

pkgname=dfir-ntfs
_pkgname=dfir_ntfs
pkgver=1.0.3
pkgrel=1
pkgdesc='An NTFS parser for digital forensics & incident response.'
arch=('any')
groups=('blackarch' 'blackarch-forensic')
url='https://github.com/msuhanov/dfir_ntfs'
license=('GPL')
depends=('python')
makedepends=('python-setuptools')
source=("https://github.com/msuhanov/$_pkgname/archive/$pkgver.zip")
sha512sums=('fbf622995baa3e51b0b846bd31fc8acd6678451814fc43686a2da2dc62832cc2b4f7330733b6cc95481e86069260b5d6e0383a0a817d2226f136cbb27a51238a')

build() {
  cd "$_pkgname-$pkgver"

  python setup.py build
}

package() {
  cd "$_pkgname-$pkgver"

  python setup.py install --root="$pkgdir" --prefix=/usr -O1 --skip-build

  install -Dm 644 -t "$pkgdir/usr/share/doc/$pkgname/" ReadMe ChangeLog License

  for i in "$pkgdir/usr/bin/"*; do
    mv $i "$(echo $i | tr -s '_' '-')"
  done
}

