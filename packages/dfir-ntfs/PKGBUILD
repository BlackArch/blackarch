# This file is part of BlackArch Linux ( https://www.blackarch.org/ ).
# See COPYING for license details.

pkgname=dfir-ntfs
_pkgname=dfir_ntfs
pkgver=1.1.18
pkgrel=1
pkgdesc='An NTFS parser for digital forensics & incident response.'
arch=('any')
groups=('blackarch' 'blackarch-forensic')
url='https://github.com/msuhanov/dfir_ntfs'
license=('GPL')
depends=('python')
makedepends=('python-setuptools')
source=("https://github.com/msuhanov/$_pkgname/archive/$pkgver.zip")
sha512sums=('ad0cb3a28d2acbf89ffbb68c6dfe064d94777d8fc1580307862d265490e24da630fc29a29edd96daf3981804f6f372aa09a866453fe88b67eb458ae2310c32d9')

build() {
  cd "$_pkgname-$pkgver"

  python setup.py build
}

package() {
  cd "$_pkgname-$pkgver"

  python setup.py install --root="$pkgdir" --prefix=/usr -O1 --skip-build

  install -Dm 644 -t "$pkgdir/usr/share/doc/$pkgname/" ReadMe ChangeLog License

  for i in "$pkgdir/usr/bin/"*; do
    mv $i "$(echo $i | tr -s '_' '-')"
  done
}

