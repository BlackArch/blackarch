# This file is part of BlackArch Linux ( https://www.blackarch.org/ ).
# See COPYING for license details.

pkgname=snow
pkgver=20130616
pkgrel=2
groups=('blackarch' 'blackarch-crypto' 'blackarch-misc')
pkgdesc='Steganography program for concealing messages in text files.'
arch=('x86_64' 'aarch64')
url='http://darkside.com.au/snow/index.html'
license=('Apache')
makedepends=('wget')
source=("http://darkside.com.au/$pkgname/$pkgname-$pkgver.tar.gz")
DLAGENTS=('http::/usr/bin/wget -O %o %u')
sha512sums=('4e3167a9f5c706d117c8af534c4f324ab405bd909b67e3cb3e98d6534f322272a54c776bbad595055d58aaed2890a7bb610236470ec3e49b9228c2c83b152b8d')

build() {
  cd "$pkgname-$pkgver"

  make
}

package() {
  cd "$pkgname-$pkgver"

  install -Dm 755 $pkgname "$pkgdir/usr/bin/$pkgname"
  install -Dm 644 COPYING "$pkgdir/usr/share/licenses/$pkgname/COPYING"
  install -Dm 644 "$pkgname.1" "$pkgdir/usr/share/man/man1/$pkgname.1"
  install -Dm 644 -t "$pkgdir/usr/share/doc/$pkgname/" AUTHORS README ChangeLog
}

