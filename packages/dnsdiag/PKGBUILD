# This file is part of BlackArch Linux ( https://www.blackarch.org/ ).
# See COPYING for license details.

pkgname=dnsdiag
pkgver=v2.9.2.r5.g19f3dc2
pkgrel=1
epoch=1
pkgdesc='DNS Diagnostics and Performance Measurement Tools.'
groups=('blackarch' 'blackarch-networking')
arch=('any')
url='https://dnsdiag.org/'
depends=('python' 'python-dnspython' 'python-cymruwhois' 'python-httpx'
         'python-typing' 'python-aioquic' 'python-h2' 'python-cryptography')
makedepends=('git' 'python-build' 'python-pip')
source=("git+https://github.com/farrokhi/$pkgname.git")
sha512sums=('SKIP')

pkgver() {
  cd $pkgname

  ( set -o pipefail
    git describe --long --tags --abbrev=7 2>/dev/null |
      sed 's/\([^-]*-g\)/r\1/;s/-/./g' ||
    printf "%s.%s" "$(git rev-list --count HEAD)" \
      "$(git rev-parse --short=7 HEAD)"
  )
}

build() {
  cd $pkgname

  python -m build --wheel --outdir="$startdir/dist"
}

package() {
  cd $pkgname

  pip install \
    --verbose \
    --disable-pip-version-check \
    --no-warn-script-location \
    --ignore-installed \
    --no-compile \
    --no-deps \
    --root="$pkgdir" \
    --prefix=/usr \
    --no-index \
    --find-links="file://$startdir/dist" \
    $pkgname
}

