# This file is part of BlackArch Linux ( https://www.blackarch.org/ ).
# See COPYING for license details.

pkgname=ofp-sniffer
pkgver=189.3898088
pkgrel=3
pkgdesc='An OpenFlow sniffer to help network troubleshooting in production networks.'
groups=('blackarch' 'blackarch-sniffer')
arch=('any')
url='https://github.com/amlight/ofp_sniffer'
license=('APACHE')
depends=('python' 'python-pcapy' 'python-termcolor' 'python-flask' 'python-yaml'
         'python-dateutil' 'python-hexdump' 'python-influxdb'
         'python-python-openflow')
makedepends=('git')
source=("$pkgname::git+https://github.com/amlight/ofp_sniffer.git")
sha512sums=('SKIP')

pkgver() {
  cd $pkgname

  echo $(git rev-list --count HEAD).$(git rev-parse --short HEAD)
}

package() {
  cd $pkgname

  install -dm 755 "$pkgdir/usr/bin"
  install -dm 755 "$pkgdir/usr/share/$pkgname"

  install -Dm 644 -t "$pkgdir/usr/share/doc/$pkgname/" README.md
  install -Dm 644 LICENSE "$pkgdir/usr/share/licenses/$pkgname/LICENSE"

  rm README.md LICENSE

  cp -a * "$pkgdir/usr/share/$pkgname/"

  cat > "$pkgdir/usr/bin/$pkgname" << EOF
#!/bin/sh
cd /usr/share/$pkgname
exec python ofp_sniffer.py "\$@"
EOF

  chmod +x "$pkgdir/usr/bin/$pkgname"
}

