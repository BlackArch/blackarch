# This file is part of BlackArch Linux ( https://www.blackarch.org/ ).
# See COPYING for license details.

pkgbase=python-gramfuzz
pkgname=('python2-gramfuzz' 'python-gramfuzz')
_pkgname=gramfuzz
pkgver=1.3.2
pkgrel=1
pkgdesc='A python-based grammar fuzzer.'
arch=('any')
url='https://pypi.org/project/gramfuzz/'
license=('MIT')
makedepends=('python2-setuptools' 'python-setuptools')
source=("https://files.pythonhosted.org/packages/25/43/2ff2caecc18c13e56517db2a39ebbe1ea0cbc5b777b8cfe79fb3ef265737/$_pkgname-$pkgver.tar.gz")
sha512sums=('fb6b64b7bc35535e665edc8d5dbca1655ad1ba006e9ebc3e6f2f86f54724ab5c77d3304a2202930558c2e8e275a8ce45e169e6cf7db2cdc5fd86c7ac8edbc429')

prepare() {
  cp -a "$_pkgname-$pkgver"{,-2}
}

build() {
  cd "$_pkgname-$pkgver-2"

  python2 setup.py build

  cd "$srcdir/$_pkgname-$pkgver"

  python setup.py build
}

package_python2-gramfuzz() {
  depends=('python2')

  cd "$_pkgname-$pkgver-2"

  python2 setup.py install --prefix=/usr --root="$pkgdir" --optimize=1 \
    --skip-build
}

package_python-gramfuzz() {
  depends=('python')

  cd "$_pkgname-$pkgver"

  python setup.py install --prefix=/usr --root="$pkgdir" --optimize=1 \
    --skip-build
}

