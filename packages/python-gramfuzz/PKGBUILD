# This file is part of BlackArch Linux ( https://www.blackarch.org/ ).
# See COPYING for license details.

pkgbase=python-gramfuzz
pkgname=('python2-gramfuzz' 'python-gramfuzz')
_pkgname=gramfuzz
pkgver=1.4.1
pkgrel=1
pkgdesc='A python-based grammar fuzzer.'
arch=('any')
url='https://pypi.org/project/gramfuzz/'
license=('MIT')
makedepends=('python2-setuptools' 'python-setuptools')
source=("https://files.pythonhosted.org/packages/48/8a/58cde98bc65b4845212b5a27902d9fd797f5a51a9953fba6514572844fd8/$_pkgname-$pkgver.tar.gz")
sha512sums=('d69e922a30302a40a21ccf20602741d69c9cc46c0fd97ad0398ed2e01da211a5d5b2b6846057fe7c403de574f438f69fb367948d74bd6e62ee52b0c5b994874f')

prepare() {
  cp -a "$_pkgname-$pkgver"{,-2}
}

build() {
  cd "$_pkgname-$pkgver-2"

  python2 setup.py build

  cd "$srcdir/$_pkgname-$pkgver"

  python setup.py build
}

package_python2-gramfuzz() {
  depends=('python2')

  cd "$_pkgname-$pkgver-2"

  python2 setup.py install --prefix=/usr --root="$pkgdir" --optimize=1 \
    --skip-build
}

package_python-gramfuzz() {
  depends=('python')

  cd "$_pkgname-$pkgver"

  python setup.py install --prefix=/usr --root="$pkgdir" --optimize=1 \
    --skip-build
}

