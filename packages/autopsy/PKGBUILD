# This file is part of BlackArch Linux ( https://www.blackarch.org/ ).
# See COPYING for license details.

pkgname=autopsy
pkgver=4.17.0
_skver=4.10.1
pkgrel=1
epoch=1
pkgdesc='The forensic browser. A GUI for the Sleuth Kit.'
groups=('blackarch' 'blackarch-forensic')
arch=('x86_64')
url='http://www.sleuthkit.org/autopsy/'
license=('MIT' 'Apache')
depends=('java-runtime=8' 'testdisk' 'sleuthkit' 'sleuthkit-java'
         'java8-openjfx' 'python2-pytsk3' 'python-pytsk3')
source=("https://github.com/sleuthkit/$pkgname/releases/download/$pkgname-$pkgver/$pkgname-$pkgver.zip"
        'Autopsy.desktop')
sha512sums=('2a720a351755bd44ba71f08e1422800663fe2805faee53279fe30dedb0221c61107c0ac7c90e1bde92949dd71a6fccfcbd89e23db14867bc107ec29679c86f68'
            '5182844950b8930277a92b9c99f39e77b1d2828134b5cb0a65997277a892e768b50daaeb85ba2b52c8decf7a8fa1d7200055a34510f408ef705639b6d0d7a93a')

package() {
  cd "$pkgname-$pkgver"

  install -dm 755 "$pkgdir/usr/bin"
  install -dm 755 "$pkgdir/opt/$pkgname"

  cp -r * "$pkgdir/opt/$pkgname/"

  rm -f \
    "$pkgdir/opt/$pkgname/$pkgname/modules/ext/sleuthkit-postgresql-$_skver.jar"
  ln -sf "/usr/share/java/sleuthkit-$_skver.jar" \
    "$pkgdir/opt/$pkgname/$pkgname/modules/ext/sleuthkit-postgresql-$_skver.jar"

  install -m 755 "bin/$pkgname" "$pkgdir/opt/$pkgname/bin/$pkgname"
  install -Dm 644 icon.ico "$pkgdir/usr/share/pixmaps/$pkgname.ico"
  install -Dm 644 "$srcdir/Autopsy.desktop" \
    "$pkgdir/usr/share/applications/Autopsy.desktop"

  ln -sf "/opt/$pkgname/bin/$pkgname" "$pkgdir/usr/bin/$pkgname"
}

