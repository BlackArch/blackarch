# This file is part of BlackArch Linux ( https://www.blackarch.org/ ).
# See COPYING for license details.

pkgname=autopsy
pkgver=4.13.0
_skver=4.7.0
pkgrel=2
epoch=1
pkgdesc='The forensic browser. A GUI for the Sleuth Kit.'
groups=('blackarch' 'blackarch-forensic')
arch=('x86_64')
url='http://www.sleuthkit.org/autopsy/'
license=('MIT' 'Apache')
depends=('java-runtime' 'testdisk' 'sleuthkit' 'sleuthkit-java' 'java-openjfx'
         'python2-pytsk3' 'python-pytsk3')
source=("https://github.com/sleuthkit/$pkgname/releases/download/$pkgname-$pkgver/$pkgname-$pkgver.zip"
        'Autopsy.desktop')
sha512sums=('5bdebd7cb3b5fc9099d0520ae99f64da94155795921e26aab61e09801459ad3af2625c30caa5cf8d06976d90a7a85f25130af177e200a4b59b61bd492ed480ba'
            '5182844950b8930277a92b9c99f39e77b1d2828134b5cb0a65997277a892e768b50daaeb85ba2b52c8decf7a8fa1d7200055a34510f408ef705639b6d0d7a93a')

package() {
  cd "$pkgname-$pkgver"

  install -dm 755 "$pkgdir/usr/bin"
  install -dm 755 "$pkgdir/opt/$pkgname"

  cp -r * "$pkgdir/opt/autopsy/"
  ln -sf "/usr/share/java/sleuthkit-$_skver.jar" \
    "$pkgdir/opt/$pkgname/$pkgname/modules/ext/sleuthkit-postgresql-$_skver.jar"

  install -m 755 "bin/$pkgname" "$pkgdir/opt/$pkgname/bin/$pkgname"
  install -Dm 644 icon.ico "$pkgdir/usr/share/pixmaps/$pkgname.ico"
  install -Dm 644 "$srcdir/Autopsy.desktop" \
    "$pkgdir/usr/share/applications/Autopsy.desktop"

  ln -sf "/opt/$pkgname/bin/$pkgname" "$pkgdir/usr/bin/$pkgname"
}

