pkgname=autopsy
groups=(blackarch blackarch-forensics)
pkgver=2.24
pkgrel=5
pkgdesc="A GUI for The Sleuth Kit."
arch=(any)
url='http://www.sleuthkit.org/autopsy'
license=("GPL2")
depends=("perl" "sleuthkit")
options=(!emptydirs)
source=("http://downloads.sourceforge.net/autopsy/autopsy-$pkgver.tar.gz"
        "autopsy.pl")
md5sums=('4ed18aa9f79453d74957b5db220d0d59'
         'e5c67fdd92c6203c0c34ff5b07269aef')

prepare() {
  cd "$srcdir/autopsy-$pkgver"

  cat > autopsy <<EOF
#!/usr/bin/perl -wT
use lib '/usr/lib/autopsy/';
use lib '/usr/lib/autopsy/lib/';
EOF
  cat base/autopsy.base >> autopsy

  sed -i 's:conf.pl:/etc/autopsy.pl:' autopsy lib/Main.pm
}

package() {
  cd "$srcdir/autopsy-$pkgver"

  install -dm755 "$pkgdir/etc"
  install -dm755 "$pkgdir/usr/lib/autopsy/help"
  install -dm755 "$pkgdir/usr/lib/autopsy/lib"
  install -dm755 "$pkgdir/usr/lib/autopsy/pict"
  install -dm755 "$pkgdir/usr/man/man1"
  install -dm755 "$pkgdir/usr/bin"

  install -m755 autopsy "$pkgdir/usr/lib/autopsy/"
  chmod +x "$pkgdir/usr/lib/autopsy/autopsy"

  install -m644 "$srcdir/autopsy.pl" "$pkgdir/etc"
  cp --no-preserve=ownership -r help lib pict "$pkgdir/usr/lib/autopsy/"
  cp --no-preserve=ownership -r man "$pkgdir/usr"

  ln -s ../lib/autopsy/autopsy "$pkgdir/usr/bin/autopsy"
}
