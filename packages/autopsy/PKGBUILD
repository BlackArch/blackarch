# This file is part of BlackArch Linux ( http://blackarch.org ).
# See COPYING for license details.

pkgname='autopsy'
pkgver='2.24'
pkgrel=10
groups=('blackarch' 'blackarch-forensic')
pkgdesc='A GUI for The Sleuth Kit.'
arch=('any')
url='http://www.sleuthkit.org/autopsy'
license=("GPL2")
depends=("perl" "sleuthkit")
options=(!emptydirs)
source=("http://downloads.sourceforge.net/autopsy/autopsy-${pkgver}.tar.gz"
        "autopsy.pl")
sha1sums=('084a6554a1494f5f34df4a5a3635c8d3dc3b8822'
          '0a5c32785bc0f7866103966f65d00a904118705c')

prepare() {
  cd "$srcdir/autopsy-$pkgver"

  cat > autopsy << EOF
#!/usr/bin/perl -wT
use lib '/usr/lib/autopsy/';
use lib '/usr/lib/autopsy/lib/';
EOF
  cat base/autopsy.base >> autopsy

  sed -i 's:conf.pl:/etc/autopsy.pl:' autopsy lib/Main.pm
}

package() {
  cd "$srcdir/autopsy-$pkgver"

  mkdir -p "$pkgdir/etc"
  mkdir -p "$pkgdir/usr/lib/autopsy/help"
  mkdir -p "$pkgdir/usr/lib/autopsy/lib"
  mkdir -p "$pkgdir/usr/lib/autopsy/pict"
  mkdir -p "$pkgdir/usr/share/man/man1"
  mkdir -p "$pkgdir/usr/bin"

  install -m755 autopsy "$pkgdir/usr/lib/autopsy/"

  chmod +x "$pkgdir/usr/lib/autopsy/autopsy"

  install -m644 "$srcdir/autopsy.pl" "$pkgdir/etc"

  cp --no-preserve=ownership -r help lib pict "$pkgdir/usr/lib/autopsy/"
  cp --no-preserve=ownership -r man "$pkgdir/usr/share"

  ln -s ../lib/autopsy/autopsy "$pkgdir/usr/bin/autopsy"
}
