# This file is part of BlackArch Linux ( http://blackarch.org ).
# See COPYING for license details.
# Maintainer: Jaryl Chng <mrciku@gmail.com>
# Contributor: Olivier Bilodeau <obilodeau@gosecure.ca>

pkgname='autopsy'
pkgver='4.8.0'
pkgrel=1
epoch=1
pkgdesc='The Autopsy Forensic Browser is a GUI for The Sleuth Kit.'
groups=('blackarch' 'blackarch-forensic')
arch=('x86_64')
url='http://www.sleuthkit.org/autopsy/'
license=('MIT' 'Apache-2.0')
depends=('java-runtime' 'testdisk' 'sleuthkit' 'sleuthkit-java' 'java-openjfx')
source=("https://github.com/sleuthkit/autopsy/releases/download/autopsy-${pkgver}/autopsy-${pkgver}.zip"
        'Autopsy.desktop')
sha512sums=('8d27c9dbf6d113ef5964968faf0f9f9c502750ec289da349e18dcd495b43737ee322c05826d6250243bacbbb23f7d8b95086298ff7feeacbdad7ef13ab63099a'
            '5182844950b8930277a92b9c99f39e77b1d2828134b5cb0a65997277a892e768b50daaeb85ba2b52c8decf7a8fa1d7200055a34510f408ef705639b6d0d7a93a')

package() {
  cd "autopsy-${pkgver}"

  mkdir -p "$pkgdir/usr/bin"
  mkdir -p "$pkgdir/opt/autopsy"

  cp -r * $pkgdir/opt/autopsy/

  # copy sleuthkit jar into autopsy
  cp /usr/share/java/sleuthkit-4.6.2.jar $pkgdir/opt/autopsy/autopsy/modules/ext/sleuthkit-postgresql-4.6.2.jar

  # overwrite bin/autopsy with proper permissions
  install -m755 bin/autopsy $pkgdir/opt/autopsy/bin/autopsy

  install -Dm644 icon.ico $pkgdir/usr/share/pixmaps/autopsy.ico
  install -Dm644 $srcdir/Autopsy.desktop "$pkgdir/usr/share/applications/Autopsy.desktop"

  ln -sf "/opt/autopsy/bin/autopsy" "$pkgdir/usr/bin/autopsy"
}
