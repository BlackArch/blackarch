pkgname='autopsy'
groups=(blackarch blackarch-forensics)
pkgver=2.24
pkgrel=5
pkgdesc='A GUI for The Sleuth Kit.'
arch=("any")
url="http://www.sleuthkit.org/$pkgname"
license=("GPL2")
depends=("perl" "sleuthkit")
options=(!emptydirs)
source=(http://downloads.sourceforge.net/$pkgname/$pkgname-$pkgver.tar.gz
        autopsy.pl)
md5sums=('4ed18aa9f79453d74957b5db220d0d59'
         'e5c67fdd92c6203c0c34ff5b07269aef')

prepare() {
  cd $srcdir/$pkgname-$pkgver

  cat > $pkgname <<EOF
#!/usr/bin/perl -wT
use lib '/usr/lib/$pkgname/';
use lib '/usr/lib/$pkgname/lib/';
EOF
  cat base/$pkgname.base >> $pkgname

  sed -i 's:conf.pl:/etc/autopsy.pl:' $pkgname lib/Main.pm
}

package() {
  cd $srcdir/$pkgname-$pkgver

  install -dm755 $pkgdir/etc
  install -dm755 $pkgdir/usr/lib/$pkgname/help
  install -dm755 $pkgdir/usr/lib/$pkgname/lib
  install -dm755 $pkgdir/usr/lib/$pkgname/pict
  install -dm755 $pkgdir/usr/man/man1
  install -dm755 $pkgdir/usr/bin

  install -m755 $pkgname $pkgdir/usr/lib/$pkgname/
  chmod +x $pkgdir/usr/lib/$pkgname/$pkgname

  install -m644 $srcdir/autopsy.pl $pkgdir/etc
  cp --no-preserve=ownership -r help lib pict $pkgdir/usr/lib/$pkgname/
  cp --no-preserve=ownership -r man $pkgdir/usr

  ln -s ../lib/$pkgname/$pkgname $pkgdir/usr/bin/$pkgname
}
