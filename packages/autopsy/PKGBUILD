# This file is part of BlackArch Linux ( https://www.blackarch.org/ ).
# See COPYING for license details.

pkgname=autopsy
pkgver=4.18.0
_skver=4.10.2
pkgrel=1
epoch=1
pkgdesc='The forensic browser. A GUI for the Sleuth Kit.'
groups=('blackarch' 'blackarch-forensic')
arch=('x86_64')
url='http://www.sleuthkit.org/autopsy/'
license=('MIT' 'Apache')
depends=('java-runtime=8' 'testdisk' 'sleuthkit' 'sleuthkit-java'
         'java8-openjfx' 'python2-pytsk3' 'python-pytsk3')
source=("https://github.com/sleuthkit/$pkgname/releases/download/$pkgname-$pkgver/$pkgname-$pkgver.zip"
        "$pkgname.desktop")
sha512sums=('9e9f1aa49585109b26606e71b7c45ecce691eeb85afebc28f3b84bfd64f09a235e05d9c04a47f296c1df959b5eb901f0d49fd66142b2c48a8963d6a0bb7bf1f3'
            '08f98e0b4cc2f0791a4543ce77ec1a40214f9358973e2431e6b253d32e9954ee1998d61d234168699802a091de901b0661dffe1243ccb35fd299648d65b1907b')

package() {
  cd "$pkgname-$pkgver"

  install -dm 755 "$pkgdir/usr/bin"
  install -dm 755 "$pkgdir/opt/$pkgname"

  cp -r * "$pkgdir/opt/$pkgname/"

  rm -f \
    "$pkgdir/opt/$pkgname/$pkgname/modules/ext/sleuthkit-postgresql-$_skver.jar"
  ln -sf "/usr/share/java/sleuthkit-$_skver.jar" \
    "$pkgdir/opt/$pkgname/$pkgname/modules/ext/sleuthkit-postgresql-$_skver.jar"
  ln -sf "/usr/share/java/sleuthkit-$_skver.jar" \
    "$pkgdir/opt/autopsy/autopsy/modules/ext/sleuthkit-$_skver.jar"

  install -m 755 "bin/$pkgname" "$pkgdir/opt/$pkgname/bin/$pkgname"
  install -Dm 644 icon.ico "$pkgdir/usr/share/pixmaps/$pkgname.ico"
  install -Dm 644 "$srcdir/$pkgname.desktop" \
    "$pkgdir/usr/share/applications/$pkgname.desktop"

  ln -sf "/opt/$pkgname/bin/$pkgname" "$pkgdir/usr/bin/$pkgname"
}

