# This file is part of BlackArch Linux ( https://www.blackarch.org/ ).
# See COPYING for license details.

pkgname=autopsy
pkgver=4.12.0
pkgrel=1
epoch=1
pkgdesc='The forensic browser. A GUI for the Sleuth Kit.'
groups=('blackarch' 'blackarch-forensic')
arch=('x86_64')
url='http://www.sleuthkit.org/autopsy/'
license=('MIT' 'Apache-2.0')
depends=('java-runtime' 'testdisk' 'sleuthkit' 'sleuthkit-java' 'java-openjfx')
source=("https://github.com/sleuthkit/$pkgname/releases/download/$pkgname-$pkgver/$pkgname-$pkgver.zip"
        'Autopsy.desktop')
sha512sums=('f80be7076eb98af898067211796eaa84e808baf0b167398fa45ee36c2fc32219ff7c25ef91b55919b833dd9a070c46211ddada4a8697f0a1164847fd81e2af21'
            '5182844950b8930277a92b9c99f39e77b1d2828134b5cb0a65997277a892e768b50daaeb85ba2b52c8decf7a8fa1d7200055a34510f408ef705639b6d0d7a93a')

package() {
  cd "$pkgname-$pkgver"

  install -dm 755 "$pkgdir/usr/bin"
  install -dm 755 "$pkgdir/opt/$pkgname"

  cp -r * "$pkgdir/opt/autopsy/"
  cp /usr/share/java/sleuthkit-4.6.6.jar \
    "$pkgdir/opt/$pkgname/$pkgname/modules/ext/sleuthkit-postgresql-4.6.6.jar"

  install -m 755 bin/autopsy "$pkgdir/opt/$pkgname/bin/$pkgname"
  install -Dm 644 icon.ico "$pkgdir/usr/share/pixmaps/autopsy.ico"
  install -Dm 644 "$srcdir/Autopsy.desktop" \
    "$pkgdir/usr/share/applications/Autopsy.desktop"

  ln -sf "/opt/autopsy/bin/$pkgname" "$pkgdir/usr/bin/$pkgname"
}

