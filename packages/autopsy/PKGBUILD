# This file is part of BlackArch Linux ( https://www.blackarch.org/ ).
# See COPYING for license details.

pkgname=autopsy
pkgver=4.11.0
pkgrel=1
epoch=1
pkgdesc='The forensic browser. A GUI for the Sleuth Kit.'
groups=('blackarch' 'blackarch-forensic')
arch=('x86_64')
url='http://www.sleuthkit.org/autopsy/'
license=('MIT' 'Apache-2.0')
depends=('java-runtime' 'testdisk' 'sleuthkit' 'sleuthkit-java' 'java-openjfx')
source=("https://github.com/sleuthkit/autopsy/releases/download/autopsy-$pkgver/autopsy-$pkgver.zip"
        'Autopsy.desktop')
sha512sums=('a910e9cdd7f7f0f9fb65122f44a95b1edafd2925fa8f4155c6c1599375e0843c8b81799e360571a126734ce3ab30f6b6a6cdc4bdf5d4142a1977b06779d0caca'
            '5182844950b8930277a92b9c99f39e77b1d2828134b5cb0a65997277a892e768b50daaeb85ba2b52c8decf7a8fa1d7200055a34510f408ef705639b6d0d7a93a')

package() {
  cd "$pkgname-$pkgver"

  install -dm 755 "$pkgdir/usr/bin"
  install -dm 755 "$pkgdir/opt/$pkgname"

  cp -r * "$pkgdir/opt/autopsy/"
  cp /usr/share/java/sleuthkit-4.6.5.jar \
    "$pkgdir/opt/$pkgname/$pkgname/modules/ext/sleuthkit-postgresql-4.6.5.jar"

  install -m 755 bin/autopsy "$pkgdir/opt/$pkgname/bin/$pkgname"
  install -Dm 644 icon.ico "$pkgdir/usr/share/pixmaps/autopsy.ico"
  install -Dm 644 "$srcdir/Autopsy.desktop" \
    "$pkgdir/usr/share/applications/Autopsy.desktop"

  ln -sf "/opt/autopsy/bin/$pkgname" "$pkgdir/usr/bin/$pkgname"
}

