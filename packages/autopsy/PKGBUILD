# This file is part of BlackArch Linux ( http://blackarch.org ).
# See COPYING for license details.
# Maintainer: Jaryl Chng <mrciku@gmail.com>
# Contributor: Olivier Bilodeau <obilodeau@gosecure.ca>

pkgname='autopsy'
pkgver='4.7.0'
pkgrel=1
pkgdesc='The Autopsy Forensic Browser is a GUI for The Sleuth Kit.'
groups=('blackarch' 'blackarch-forensic')
arch=('x86_64')
url='http://www.sleuthkit.org/autopsy/'
license=('MIT' 'Apache-2.0')
depends=('java-runtime' 'testdisk' 'sleuthkit' 'sleuthkit-java' 'java-openjfx')
source=("https://github.com/sleuthkit/autopsy/releases/download/autopsy-${pkgver}/autopsy-${pkgver}.zip"
        'Autopsy.desktop')
sha1sums=('692c4fc3d21870f0c54f7d895f4f4dec8e5db843'
          'f82c020f433def256afd5d35f41bc8675ba5e326')

package() {
  cd "autopsy-${pkgver}"

  mkdir -p "$pkgdir/usr/bin"
  mkdir -p "$pkgdir/opt/autopsy"

  cp -r * $pkgdir/opt/autopsy/

  # copy sleuthkit jar into autopsy
  cp /usr/share/java/sleuthkit-4.6.1.jar $pkgdir/opt/autopsy/autopsy/modules/ext/sleuthkit-postgresql-4.6.1.jar

  # overwrite bin/autopsy with proper permissions
  install -m755 bin/autopsy $pkgdir/opt/autopsy/bin/autopsy

  install -Dm644 icon.ico $pkgdir/usr/share/pixmaps/autopsy.ico
  install -Dm644 $srcdir/Autopsy.desktop "$pkgdir/usr/share/applications/Autopsy.desktop"

  ln -sf "/opt/autopsy/bin/autopsy" "$pkgdir/usr/bin/autopsy"
}
