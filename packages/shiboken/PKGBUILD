# This file is part of BlackArch Linux ( http://blackarch.org ).
# See COPYING for license details.

pkgname='shiboken'
pkgver='1.2.2'
pkgrel=2
pkgdesc='Python bindings generator that uses API Extractor and outputs CPython code.'
arch=('x86_64' 'armv6h' 'armv7h' 'aarch64')
options=('!distcc')
license=('LGPL')
url='http://www.pyside.org/'
depends=('python2' 'python3' 'qt4>=4.8' 'libxslt')
optdepends=('python-sphinx: Documentation')
makedepends=('cmake')
conflicts=('libshiboken')
provides=('libshiboken' 'shiboken')
source=("http://download.qt-project.org/official_releases/pyside/shiboken-${pkgver}.tar.bz2")
sha1sums=('55731616791500750ef373f382057a43e133fa08')

build() {
  cd "$srcdir/shiboken-$pkgver"

  mkdir -p build_python2 && cd build_python2

  cmake ../ -DCMAKE_INSTALL_PREFIX=/usr  \
            -DCMAKE_BUILD_TYPE=Release   \
            -DBUILD_TESTS=OFF            \
            -DPYTHON_EXECUTABLE=/usr/bin/python2 \
            -DPYTHON_LIBRARY=/usr/lib/libpython2.7.so \
            -DPYTHON_INCLUDE_DIR=/usr/include/python2.7 \
            -DQT_QMAKE_EXECUTABLE=qmake4
  make

  cd "$srcdir/shiboken-$pkgver"

  mkdir -p build && cd build

  cmake ../ -DCMAKE_INSTALL_PREFIX=/usr  \
  -DCMAKE_BUILD_TYPE=Release   \
  -DBUILD_TESTS=OFF            \
  -DUSE_PYTHON3=yes            \
  -DQT_QMAKE_EXECUTABLE=qmake4

  make
}

package() {
  cd "$srcdir/shiboken-$pkgver/build"

  make DESTDIR="$pkgdir" install

  cd data

  install -Dm755 ShibokenConfig.cmake "$pkgdir/usr/lib/cmake/Shiboken-$pkgver"
  install -Dm755 ShibokenConfigVersion.cmake \
    "$pkgdir/usr/lib/cmake/Shiboken-$pkgver/"
  install -Dm755 ShibokenConfig.cpython-33m.cmake \
    "$pkgdir/usr/lib/cmake/Shiboken-$pkgver/"
  install -Dm755 shiboken.pc "$pkgdir/usr/lib/pkgconfig/"

  cd "$srcdir/shiboken-$pkgver/build_python2"

  make DESTDIR="$pkgdir" install

  cd data

  install -Dm755 ShibokenConfig-python2.7.cmake \
    "$pkgdir/usr/lib/cmake/Shiboken-$pkgver/"
}
