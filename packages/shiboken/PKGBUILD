# This file is part of BlackArch Linux ( https://www.blackarch.org/ ).
# See COPYING for license details.

pkgbase=shiboken
pkgname=('python2-shiboken' 'python-shiboken' 'shiboken')
_pkgname=Shiboken
pkgver=1.2.4
_pyver=3.13
pkgrel=12
arch=('x86_64' 'aarch64')
license=('GPL-2.0-only')
url='https://github.com/pyside/Shiboken/tags'
depends=('libicu64')
makedepends=('cmake' 'python2' 'python' 'qt4' 'libxslt' 'openssl'
             'python-distutils-extra')
source=("$pkgbase-$pkgver.tar.gz::https://github.com/PySide/Shiboken/archive/$pkgver.tar.gz"
        'support-new-python.patch'
        'basewrapper.cpp.patch'
        'sbkenum.cpp.patch'
        'sbkstring.cpp.patch')
sha512sums=('daa3fadf3daffaec52f199c0285a37431a4b6b0d450a43a035f49e5505a35b666a1cb0b334c7267af7530841dadbf0b97296812141c93de3b7cd07c7d9016a2a'
            '7372b8173a198d05272a76f7954625972d3d0a180c996dab10e9b4ed506640b8855c03e270a9dada55be922d44c71b918360243f19b83d28c6b809e5f284a351'
            '9deb7f5012bfc2c25827d3f3c250b11325add1385b18efc1c7a71ffaafb2200a2bdc4f6909eaa95a554e00191ad477be6801b165b00c78dcad12755a1996907e'
            '5a0b395a49db7cd349248cec319698197da2ee097547bec8c14db8c60d26369fe0c5a8f92b01b9464ee7db27d224f6ba587a821ae45032ed4142fe39634f7c81'
            'e8b7fa3bcf58b56f0ed43e44a53dd3866352233ef75322b49d78908f86b71f9838da28cf3e87ac6607a2545430e6bddf2a923a3128192fd0c892ceb925b8472b')

prepare() {
  cd "$_pkgname-$pkgver"

  patch -Np1 -i "$srcdir/support-new-python.patch"
  patch -Np2 -i "$srcdir/basewrapper.cpp.patch"
  patch -Np2 -i "$srcdir/sbkenum.cpp.patch"
  patch -Np2 -i "$srcdir/sbkstring.cpp.patch"
}

build() {
  cd "$_pkgname-$pkgver"

  mkdir -p build-py2 && cd build-py2

  cmake .. \
    -DCMAKE_POLICY_VERSION_MINIMUM=3.5 \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DCMAKE_BUILD_TYPE=Release \
    -DBUILD_TESTS=OFF \
    -DPYTHON_EXECUTABLE=/usr/bin/python2 \
    -DPYTHON_LIBRARY=/usr/lib/libpython2.7.so \
    -DPYTHON_INCLUDE_DIR=/usr/include/python2.7 \
    -DQT_QMAKE_EXECUTABLE=qmake-qt4

  make -j1

  cd "$srcdir/$_pkgname-$pkgver"

  mkdir -p build-py3 && cd build-py3

  cmake .. \
    -DCMAKE_POLICY_VERSION_MINIMUM=3.5 \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DCMAKE_BUILD_TYPE=Release \
    -DBUILD_TESTS=OFF \
    -DUSE_PYTHON3=yes \
    -DQT_QMAKE_EXECUTABLE=qmake-qt4 \
    -DUSE_PYTHON_VERSION=3 \
    -DPYTHON_EXECUTABLE=/usr/bin/python3 \
    -DPYTHON3_INCLUDE_DIR="/usr/include/python$_pyver" \
    -DPYTHON3_LIBRARY="/usr/lib/python$_pyver" \
    -DPYTHON_INCLUDE_DIR="/usr/include/python$_pyver" \
    -DPYTHON_LIBRARY="/usr/lib/python$_pyver"

  make -j1
}

package_shiboken() {
  pkgdesc="CPython bindings generator for C++ libraries"
  depends=('python' 'qt4' 'libxslt')
  optdepends=("python2-shiboken: for compilation against python2"
              "python-shiboken: for compilation against python")

  # Header files/ /usr/bin/shiboke, pkgconfig, man page
  cd "$_pkgname-$pkgver/build-py3"
  make -j1 DESTDIR="$pkgdir" install

  cd "$srcdir/$_pkgname-$pkgver/build-py2/data"
  install -Dm 644 ShibokenConfig-python2.7.cmake \
    "$pkgdir/usr/lib/cmake/$_pkgname-$pkgver/"
  install -Dm 644 shiboken.pc "$pkgdir/usr/lib/pkgconfig/shiboken-py2.pc"

  rm -rf "$pkgdir/usr/lib/python"*
  rm -rf "$pkgdir/usr/lib/libshiboken"*
  rm -rf "$pkgdir/usr/lib/pkgconfig/"
  rm "$pkgdir"/usr/lib/cmake/$_pkgname-$pkgver/ShibokenConfig*python*.cmake
}

package_python2-shiboken() {
  pkgdesc="Support library for Python2 bindings"
  depends=("qt4>=4.8" "libxslt" "python2" "shiboken")

  cd "$_pkgname-$pkgver/build-py2"

  make -j1 DESTDIR="$pkgdir" install

  cd "$srcdir/$_pkgname-$pkgver/build-py2/data"
  install -Dm 644 ShibokenConfig-python2.7.cmake \
    "$pkgdir/usr/lib/cmake/$_pkgname-$pkgver/"
  mv "$pkgdir"/usr/lib/pkgconfig/shiboken{,-py2}.pc

  rm -rf "$pkgdir"/usr/{include,bin,share}
  rm "$pkgdir/usr/lib/cmake/$_pkgname-$pkgver/ShibokenConfigVersion.cmake"
  rm "$pkgdir/usr/lib/cmake/$_pkgname-$pkgver/ShibokenConfig.cmake"
}

package_python-shiboken() {
  pkgdesc="Support library for Python bindings"
  depends=("qt4>=4.8" "libxslt" "python" "shiboken")

  cd "$_pkgname-$pkgver/build-py3"

  make -j1 DESTDIR="$pkgdir" install

  rm -rf "$pkgdir"/usr/{include,bin,share}
  rm "$pkgdir/usr/lib/cmake/$_pkgname-$pkgver/ShibokenConfigVersion.cmake"
  rm "$pkgdir/usr/lib/cmake/$_pkgname-$pkgver/ShibokenConfig.cmake"
}

