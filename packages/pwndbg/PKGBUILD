# This file is part of BlackArch Linux ( http://blackarch.org ).
# See COPYING for license details.

pkgname='pwndbg'
pkgver=577.dae257c
pkgrel=1
pkgdesc='Makes debugging with GDB suck less.'
groups=('blackarch' 'blackarch-debugger')
url='https://github.com/pwndbg/pwndbg'
arch=('any')
license=('MIT')
depends=('python-capstone' 'python-unicorn' 'python-pycparser' 'python-psutil'
         'python-pyelftools' 'python-six' 'python-future')
makedepends=('git')
source=('git+https://github.com/pwndbg/pwndbg')
sha1sums=('SKIP')

pkgver() {
  cd "$srcdir/pwndbg"

  echo $(git rev-list --count HEAD).$(git rev-parse --short HEAD)
}

prepare() {
  cd "$srcdir/pwndbg"

  sed '/0L/d' -i ida_script.py
}

build() {
  cd "$srcdir/pwndbg"

  python -m compileall .
  python -O -m compileall .
}

package() {
  cd "$srcdir/pwndbg"

  mkdir -p "$pkgdir/usr/share/pwndbg"

  cp -r *.py pwndbg __pycache__ "$pkgdir/usr/share/pwndbg"

  install -Dm644 -t "$pkgdir/usr/share/doc/pwndbg" *.md
  install -Dm644 LICENSE.md "$pkgdir/usr/share/licenses/pwndbg/LICENSE.md"
}
