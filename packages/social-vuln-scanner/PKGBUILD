# This file is part of BlackArch Linux ( http://blackarch.org ).
# See COPYING for license details.

pkgname='social-vuln-scanner'
pkgver=11.91794c6
pkgrel=4
pkgdesc='Gathers public information on companies to highlight social engineering risk.'
groups=('blackarch' 'blackarch-social' 'blackarch-recon')
arch=('any')
url='https://github.com/Betawolf/social-vuln-scanner'
license=('CCANS4')
depends=('python' 'python-beautifulsoup4' 'python-editdistance'
         'python-html2text' 'python-httplib2' 'python-psutil' 'python-oauth2'
         'python-packaging' 'python-pillow' 'python-pyparsing' 'python-six'
         'iotop')
makedepends=('git')
source=('git+https://github.com/Betawolf/social-vuln-scanner.git')
sha1sums=('SKIP')

pkgver() {
  cd "$srcdir/social-vuln-scanner"

  echo $(git rev-list --count HEAD).$(git rev-parse --short HEAD)
}

package() {
  cd "$srcdir/social-vuln-scanner"

  mkdir -p "$pkgdir/usr/bin"
  mkdir -p "$pkgdir/usr/share/social-vuln-scanner"

  install -Dm644 -t "$pkgdir/usr/share/doc/social-vuln-scanner/" README.md \
    CREDENTIALS
  install -Dm644 LICENSE "$pkgdir/usr/share/doc/social-vuln-scanner/LICENSE"

  rm *.md LICENSE CREDENTIALS

  cp -a * "$pkgdir/usr/share/social-vuln-scanner/"

  cat > "$pkgdir/usr/bin/social-vuln-scanner" << EOF
#!/bin/sh
cd /usr/share/social-vuln-scanner
exec python3 vuln_scorer.py "\$@"
EOF

  chmod a+x "$pkgdir/usr/bin/social-vuln-scanner"
}
