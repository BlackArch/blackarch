# This file is part of BlackArch Linux ( https://www.blackarch.org/ ).
# See COPYING for license details.

pkgname=above
pkgver=222.4789d6e
pkgrel=1
pkgdesc='Network Protocols Sniffer.'
arch=('any')
groups=('blackarch' 'blackarch-sniffer' 'blackarch-scanner'
        'blackarch-fingerprint' 'blackarch-networking' 'blackarch-recon'
        'blackarch-voip')
url='https://github.com/casterbyte/Above'
license=('GPL3')
depends=('python' 'python-virtualenv' 'scapy' 'python-colorama')
makedepends=('git' 'python-setuptools' 'python-pip')
source=("$pkgname::git+https://github.com/casterbyte/Above.git")
sha512sums=('SKIP')

pkgver() {
  cd $pkgname

  ( set -o pipefail
    git describe --long --tags --abbrev=7 2>/dev/null |
      sed 's/\([^-]*-g\)/r\1/;s/-/./g' ||
    printf "%s.%s" "$(git rev-list --count HEAD)" \
      "$(git rev-parse --short=7 HEAD)"
  )
}

build() {
  cd "$pkgname"

  python setup.py build
}

package() {
  cd "$pkgname"

  python setup.py install --root="$pkgdir" --prefix=/usr -O1 --skip-build
}

