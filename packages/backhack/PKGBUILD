# This file is part of BlackArch Linux ( https://www.blackarch.org/ ).
# See COPYING for license details.

pkgname=backhack
pkgver=39.561ec86
pkgrel=1
pkgdesc='Tool to perform Android app analysis by backing up and extracting apps, allowing you to analyze and modify file system contents for apps.'
groups=('blackarch' 'blackarch-mobile')
arch=('any')
url='https://github.com/l0gan/backHack'
license=('Apache')
depends=('python2' 'python2-pytz')
makedepends=('git')
source=("$pkgname::git+https://github.com/l0gan/backHack.git")
sha512sums=('SKIP')

pkgver() {
  cd $pkgname

  echo $(git rev-list --count HEAD).$(git rev-parse --short HEAD)
}

package() {
  cd $pkgname

  install -dm 755 "$pkgdir/usr/bin"
  install -dm 755 "$pkgdir/usr/share/$pkgname"

  install -Dm 644 -t "$pkgdir/usr/share/doc/$pkgname/" README.txt
  install -Dm 644 LICENSE "$pkgdir/usr/share/licenses/$pkgname/LICENSE"

  rm README.txt LICENSE

  cp -a * "$pkgdir/usr/share/$pkgname/"

  cat > "$pkgdir/usr/bin/$pkgname" << EOF
#!/bin/sh
cd /usr/share/$pkgname
exec python2 backHack.py "\$@"
EOF

  chmod +x "$pkgdir/usr/bin/$pkgname"
}

