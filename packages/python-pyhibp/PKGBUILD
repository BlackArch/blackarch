# This file is part of BlackArch Linux ( https://www.blackarch.org/ ).
# See COPYING for license details.

pkgbase=python-pyhibp
pkgname=('python2-pyhibp' 'python-pyhibp')
_pkgname=pyhibp
pkgver=4.1.0
pkgrel=2
pkgdesc="An interface to Troy Hunt's 'Have I Been Pwned' public API."
arch=('any')
url='https://pypi.org/project/pyHIBP/#files'
license=('AGPL3')
makedepends=('python2-setuptools' 'python-setuptools')
source=("https://files.pythonhosted.org/packages/92/e5/320e85b1b4de981a33f9ae23c8a6fd601444793e4fa6f67d954b76d31708/$_pkgname-$pkgver.tar.gz")
sha512sums=('c7df9acbde2f4684edab559b671e17e5cbeb2c2f28dbc8cae64ef8253bc7b72454e82678160002aa296314697cd34c75701282f3f9bcce93ba4455a119b49623')

prepare() {
  cp -a "pyhibp-$pkgver" "pyhibp2-$pkgver"
}

build() {
  cd "pyhibp2-$pkgver"

  python2 setup.py build

  cd "$srcdir/pyhibp-$pkgver"

  python setup.py build
}

package_python2-pyhibp() {
  depends=('python2-requests' 'python2-six')

  cd "pyhibp2-$pkgver"

  python2 setup.py install --prefix=/usr --root="$pkgdir" --optimize=1 \
    --skip-build
}

package_python-pyhibp() {
  depends=('python-requests' 'python-six')

  cd "pyhibp-$pkgver"

  python setup.py install --prefix=/usr --root="$pkgdir" --optimize=1 \
    --skip-build
}

