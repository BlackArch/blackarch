# This file is part of BlackArch Linux ( https://www.blackarch.org/ ).
# See COPYING for license details.

pkgbase=python-pyhibp
pkgname=('python2-pyhibp' 'python-pyhibp')
_pkgname=pyhibp
pkgver=4.2.0
pkgrel=1
pkgdesc="An interface to Troy Hunt's 'Have I Been Pwned' public API."
arch=('any')
url='https://pypi.org/project/pyHIBP/#files'
license=('AGPL3')
makedepends=('python2-setuptools' 'python-setuptools')
source=("https://files.pythonhosted.org/packages/3a/4a/2bebc8a6f3230b0b0c39aae4cd05eed23cdb9ccf5914e08277f443a66c4a/$_pkgname-$pkgver.tar.gz")
sha512sums=('407ed0f260c03f28415d2da531be0105ef95025e734bafc2d07fdad7f09138f4bb84acef83cdc463e28c4cb601e3a8aa64d0d09b9c14033d69154c3c587334df')

prepare() {
  cp -a "pyhibp-$pkgver" "pyhibp2-$pkgver"
}

build() {
  cd "pyhibp2-$pkgver"

  python2 setup.py build

  cd "$srcdir/pyhibp-$pkgver"

  python setup.py build
}

package_python2-pyhibp() {
  depends=('python2-requests' 'python2-six')

  cd "pyhibp2-$pkgver"

  python2 setup.py install --prefix=/usr --root="$pkgdir" --optimize=1 \
    --skip-build
}

package_python-pyhibp() {
  depends=('python-requests' 'python-six')

  cd "pyhibp-$pkgver"

  python setup.py install --prefix=/usr --root="$pkgdir" --optimize=1 \
    --skip-build
}

