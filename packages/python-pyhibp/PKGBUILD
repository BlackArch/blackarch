# This file is part of BlackArch Linux ( https://www.blackarch.org/ ).
# See COPYING for license details.

pkgbase=python-pyhibp
pkgname=('python2-pyhibp' 'python-pyhibp')
pkgver=3.1.0
pkgrel=1
pkgdesc="An interface to Troy Hunt's 'Have I Been Pwned' public API."
arch=('any')
url='https://pypi.org/project/pyHIBP/#files'
license=('AGPL3')
makedepends=('python2-setuptools' 'python-setuptools')
source=("https://files.pythonhosted.org/packages/7a/dc/6f3fe24387b6b55ccdb3afbf5fd45389307ad2b9bba69beaeb8152399ceb/pyhibp-3.1.0.tar.gz")
sha512sums=('a191d40d4442f31f1b2d45b0a36765425db0a0ff20682791f446a9f22838a455fce071f4e74315ffa58f44b02fa6304ad43e4d7aa5be1c7c78e64b88d8cb7df1')

prepare() {
  cp -a "pyhibp-$pkgver" "pyhibp2-$pkgver"
}

build() {
  cd "pyhibp2-$pkgver"

  python2 setup.py build

  cd "$srcdir/pyhibp-$pkgver"

  python setup.py build
}

package_python2-pyhibp() {
  depends=('python2-requests' 'python2-six')

  cd "pyhibp2-$pkgver"

  python2 setup.py install --prefix=/usr --root="$pkgdir" --optimize=1 \
    --skip-build
}

package_python-pyhibp() {
  depends=('python-requests' 'python-six')

  cd "pyhibp-$pkgver"

  python setup.py install --prefix=/usr --root="$pkgdir" --optimize=1 \
    --skip-build
}

