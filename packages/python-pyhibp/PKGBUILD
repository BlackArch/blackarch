# This file is part of BlackArch Linux ( https://www.blackarch.org/ ).
# See COPYING for license details.

pkgbase=python-pyhibp
pkgname=('python2-pyhibp' 'python-pyhibp')
pkgver=3.0.0
pkgrel=1
pkgdesc="An interface to Troy Hunt's 'Have I Been Pwned' public API."
arch=('any')
url='https://pypi.org/project/pyHIBP/#files'
license=('AGPL3')
makedepends=('python2-setuptools' 'python-setuptools')
source=("https://files.pythonhosted.org/packages/df/7d/e35649df880b34408b5075ed01fca5ac76e932f56e1884d637ae65ac3465/pyhibp-$pkgver.tar.gz")
sha512sums=('8f5d8a9b554bc5370007165bd83ab077b754c801148c0ed6e119701c4325e3122aa74fda128f6ee8d13076b212ea78be58dadf72c9852b231ef2f79e85175e2f')

prepare() {
  cp -a "pyhibp-$pkgver" "pyhibp2-$pkgver"
}

build() {
  cd "pyhibp2-$pkgver"

  python2 setup.py build

  cd "$srcdir/pyhibp-$pkgver"

  python setup.py build
}

package_python2-pyhibp() {
  depends=('python2-requests' 'python2-six')

  cd "pyhibp2-$pkgver"

  python2 setup.py install --prefix=/usr --root="$pkgdir" --optimize=1 \
    --skip-build
}

package_python-pyhibp() {
  depends=('python-requests' 'python-six')

  cd "pyhibp-$pkgver"

  python setup.py install --prefix=/usr --root="$pkgdir" --optimize=1 \
    --skip-build
}

