# This file is part of BlackArch Linux ( https://www.blackarch.org/ ).
# See COPYING for license details.

pkgbase=python-pyhibp
pkgname=('python2-pyhibp' 'python-pyhibp')
_pkgname=pyhibp
pkgver=4.0.0
pkgrel=1
pkgdesc="An interface to Troy Hunt's 'Have I Been Pwned' public API."
arch=('any')
url='https://pypi.org/project/pyHIBP/#files'
license=('AGPL3')
makedepends=('python2-setuptools' 'python-setuptools')
source=("https://files.pythonhosted.org/packages/ed/87/5eecb68da81fadb8c1227db9f9c0eb9978be8a80051639be0dba2a9f7d3f/$_pkgname-$pkgver.tar.gz")
sha512sums=('eebc1db58d282e9f950b82ae2b756145b69a666ad3b12b27293fb6627a0d877e656ab951c8da015ff7e6525ae56aad2d184bcded24b1bc235d24978818344429')

prepare() {
  cp -a "pyhibp-$pkgver" "pyhibp2-$pkgver"
}

build() {
  cd "pyhibp2-$pkgver"

  python2 setup.py build

  cd "$srcdir/pyhibp-$pkgver"

  python setup.py build
}

package_python2-pyhibp() {
  depends=('python2-requests' 'python2-six')

  cd "pyhibp2-$pkgver"

  python2 setup.py install --prefix=/usr --root="$pkgdir" --optimize=1 \
    --skip-build
}

package_python-pyhibp() {
  depends=('python-requests' 'python-six')

  cd "pyhibp-$pkgver"

  python setup.py install --prefix=/usr --root="$pkgdir" --optimize=1 \
    --skip-build
}

