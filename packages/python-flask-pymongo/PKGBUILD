# This file is part of BlackArch Linux ( https://www.blackarch.org/ ).
# See COPYING for license details.

pkgname=python-flask-pymongo
_pkgname=flask_pymongo
pkgver=3.0.1
_pyver=3.14
pkgrel=1
pkgdesc='PyMongo support for Flask applications.'
arch=('any')
url='https://pypi.python.org/pypi/Flask-PyMongo/'
license=('BSD-3-Clause')
depends=('python' 'python-flask')
makedepends=('python-build' 'python-installer' 'python-setuptools'
             'python-wheel' 'python-hatchling')
options=(!emptydirs)
source=("https://files.pythonhosted.org/packages/source/${_pkgname::1}/$_pkgname/$_pkgname-$pkgver.tar.gz")
sha512sums=('0d61c745b9df44c3c34fad229fa2603be34feee1509fd16dc60c879e24d35d9f40b000b24391b702b7cf68365af3af9afc86600f58348d917c2bda89143186e7')

build() {
  cd "$_pkgname-$pkgver"

  python -m build --wheel --no-isolation
}

package() {
  cd "$_pkgname-$pkgver"

  python -m installer --destdir="$pkgdir" dist/*.whl

  rm -rf "$pkgdir/usr/lib/python$_pyver/site-packages/tests"
}

