# This file is part of BlackArch Linux ( https://www.blackarch.org/ ).
# See COPYING for license details.

pkgbase=python-htmlentities
pkgname=('python2-htmlentities' 'python-htmlentities')
_pkgname=htmlentities
pkgver=0.3.0
pkgrel=1
pkgdesc='HTML Entities for Python.'
arch=('any')
url='https://pypi.org/project/htmlentities/#files'
license=('custom:unknown')
makedepends=('python2-setuptools' 'python-setuptools')
source=("https://github.com/cobrateam/python-htmlentities/archive/$pkgver.tar.gz")
sha512sums=('9f2f59c5ab0c07548ea76dc02cc3c1a34715c4bcfff58301589d39e730a3bf6ef8104a8fefc998e181966c59be074e7b34c430cfe862e943bcdca468b39c6bcc')

prepare() {
  cp -a "$pkgbase-$pkgver"{,-2}
}

build() {
  cd "$pkgbase-$pkgver-2"

  python2 setup.py build

  cd "$srcdir/$pkgbase-$pkgver"

  python setup.py build
}

package_python2-htmlentities() {
  depends=('python2')

  cd "$pkgbase-$pkgver-2"

  python2 setup.py install --prefix=/usr --root="$pkgdir" -O1 --skip-build

  rm -rf "$pkgdir/usr/lib/python2.7/site-packages/tests"
}

package_python-htmlentities() {
  depends=('python')

  cd "$pkgbase-$pkgver"

  python setup.py install --prefix=/usr --root="$pkgdir" -O1 --skip-build

  rm -rf "$pkgdir/usr/lib/python3.8/site-packages/tests"
}

