# This file is part of BlackArch Linux ( https://www.blackarch.org/ ).
# See COPYING for license details.

pkgname=identywaf
pkgver=198.03719dc
pkgrel=1
pkgdesc='Blind WAF identification tool.'
groups=('blackarch' 'blackarch-webapp' 'blackarch-fingerprint')
arch=('any')
url='https://github.com/stamparm/identYwaf'
license=('MIT')
depends=('python')
makedepends=('git')
source=("$pkgname::git+https://github.com/stamparm/identYwaf.git")
sha512sums=('SKIP')

pkgver() {
  cd $pkgname

  echo $(git rev-list --count HEAD).$(git rev-parse --short HEAD)
}

package() {
  cd $pkgname

  install -dm 755 "$pkgdir/usr/bin"
  install -dm 755 "$pkgdir/usr/share/$pkgname"

  install -Dm 644 -t "$pkgdir/usr/share/doc/$pkgname/" README.md
  install -Dm 644 LICENSE "$pkgdir/usr/share/licenses/$pkgname/LICENSE"

  rm README.md LICENSE

  cp -a * "$pkgdir/usr/share/$pkgname/"

  cat > "$pkgdir/usr/bin/$pkgname" << EOF
#!/bin/sh
cd /usr/share/$pkgname
exec python identYwaf.py "\$@"
EOF

  chmod +x "$pkgdir/usr/bin/$pkgname"
}

