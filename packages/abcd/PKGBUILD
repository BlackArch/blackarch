# This file is part of BlackArch Linux ( http://blackarch.org ).
# See COPYING for license details.

pkgname='abcd'
pkgver=4.2738809
pkgrel=2
pkgdesc='ActionScript ByteCode Disassembler.'
groups=('blackarch' 'blackarch-disassembler')
arch=('any')
url='https://github.com/MITRECND/abcd'
license=('BSD')
depends=('python2' 'python2-setuptools' 'python2-pyswf')
makedepends=('git')
source=('git+https://github.com/MITRECND/abcd.git')
sha1sums=('SKIP')

pkgver() {
  cd "$srcdir/abcd"

  echo $(git rev-list --count HEAD).$(git rev-parse --short HEAD)
}

prepare() {
  cd "$srcdir/abcd"

  sed -i '1 i\#!/usr/bin/python2' abcdump.py abcgraph.py
}

package() {
  cd "$srcdir/abcd"

  python2 setup.py install --root="$pkgdir"

  install -Dm755 abcdump.py "$pkgdir/usr/bin/abcdump"
  install -Dm755 abcgraph.py "$pkgdir/usr/bin/abcgraph"
  install -Dm644 README.md "$pkgdir/usr/share/doc/abcd/README.md"
  install -Dm644 LICENSE "$pkgdir/usr/share/licenses/abcd/LICENSE"
}
