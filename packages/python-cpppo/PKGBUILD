# This file is part of BlackArch Linux ( https://www.blackarch.org/ ).
# See COPYING for license details.

pkgbase=python-cpppo
pkgname=('python2-cpppo' 'python-cpppo')
_pkgname=cpppo
pkgver=4.2.3
pkgrel=1
pkgdesc='A Communication Protocol Python Parser and Originator.'
arch=('any')
url='https://pypi.org/project/cpppo/#files'
license=('GPL3')
makedepends=('python2-setuptools' 'python-setuptools')
source=("https://files.pythonhosted.org/packages/5d/20/63ce54331211b84095f05fd952597bfe06561b7e865af2aadfdb38d76a58/$_pkgname-$pkgver.tar.gz")
sha512sums=('1da87ec1897c2c6b5954645bdcdfb07a7183609c3d4178145c12d56790e4f4cd47689475c80abcd5bd87e304b9ec25e67fd17d57902e83ad9004de206157b7b4')

prepare() {
  cp -a "$_pkgname-$pkgver"{,-2}
}

build() {
  cd "$_pkgname-$pkgver-2"

  python2 setup.py build

  cd "$srcdir/$_pkgname-$pkgver"

  python setup.py build
}

package_python2-cpppo() {
  depends=('python2' 'python2-greenery' 'python2-ipaddress')

  cd "$_pkgname-$pkgver-2"

  python2 setup.py install --prefix=/usr --root="$pkgdir" -O1 --skip-build

  for i in "$pkgdir/usr/bin/"*; do
    mv $i "${i}2"
  done
}

package_python-cpppo() {
  depends=('python' 'python-greenery')

  cd "$_pkgname-$pkgver"

  python setup.py install --prefix=/usr --root="$pkgdir" -O1 --skip-build
}

