# This file is part of BlackArch Linux ( https://www.blackarch.org/ ).
# See COPYING for license details.

pkgbase=python-cpppo
pkgname=('python2-cpppo' 'python-cpppo')
_pkgname=cpppo
pkgver=4.0.4
pkgrel=1
pkgdesc='A Communication Protocol Python Parser and Originator.'
arch=('any')
url='https://pypi.org/project/cpppo/#files'
license=('GPL3')
makedepends=('python2-setuptools' 'python-setuptools')
source=("https://files.pythonhosted.org/packages/98/b5/2028df957fcf15106a50df0578899dd6de195974c3d24666bb981a66051d/$_pkgname-$pkgver.tar.gz")
sha512sums=('c9ea6566045666bfb681c0358c186c9a1f2d737f77458a6d0237010530999af8a863f20a7b0a666ff78a80af0b9dc0096faab1c38717f729de366eef93d79fc1')

prepare() {
  cp -a "$_pkgname-$pkgver"{,-2}
}

build() {
  cd "$_pkgname-$pkgver-2"

  python2 setup.py build

  cd "$srcdir/$_pkgname-$pkgver"

  python setup.py build
}

package_python2-cpppo() {
  depends=('python2' 'python2-greenery' 'python2-ipaddress')

  cd "$_pkgname-$pkgver-2"

  python2 setup.py install --prefix=/usr --root="$pkgdir" -O1 --skip-build

  for i in "$pkgdir/usr/bin/"*; do
    mv $i "${i}2"
  done
}

package_python-cpppo() {
  depends=('python' 'python-greenery')

  cd "$_pkgname-$pkgver"

  python setup.py install --prefix=/usr --root="$pkgdir" -O1 --skip-build
}

