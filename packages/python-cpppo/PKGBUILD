# This file is part of BlackArch Linux ( https://www.blackarch.org/ ).
# See COPYING for license details.

pkgbase=python-cpppo
pkgname=('python2-cpppo' 'python-cpppo')
_pkgname=cpppo
pkgver=4.3.4
pkgrel=1
pkgdesc='A Communication Protocol Python Parser and Originator.'
arch=('any')
url='https://pypi.org/project/cpppo/#files'
license=('GPL3')
makedepends=('python2-setuptools' 'python-setuptools')
source=("https://files.pythonhosted.org/packages/07/62/c64ca7c36d322778b38266bf8e4c75ff6a321749130b389c013e9e763f08/$_pkgname-$pkgver.tar.gz")
sha512sums=('e7098bdd543bb55f4360294245c2df33d03cfa0313424f6836ea6e58dd874cd538a5a3f1db37ace90d34f6ffc496157be5c3efd31a4f0d0762320f089b807dbb')

prepare() {
  cp -a "$_pkgname-$pkgver"{,-2}
}

build() {
  cd "$_pkgname-$pkgver-2"

  python2 setup.py build

  cd "$srcdir/$_pkgname-$pkgver"

  python setup.py build
}

package_python2-cpppo() {
  depends=('python2' 'python2-greenery' 'python2-ipaddress')

  cd "$_pkgname-$pkgver-2"

  python2 setup.py install --prefix=/usr --root="$pkgdir" -O1 --skip-build

  for i in "$pkgdir/usr/bin/"*; do
    mv $i "${i}2"
  done
}

package_python-cpppo() {
  depends=('python' 'python-greenery')

  cd "$_pkgname-$pkgver"

  python setup.py install --prefix=/usr --root="$pkgdir" -O1 --skip-build
}

