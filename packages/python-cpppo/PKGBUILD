# This file is part of BlackArch Linux ( https://www.blackarch.org/ ).
# See COPYING for license details.

pkgbase=python-cpppo
pkgname=('python2-cpppo' 'python-cpppo')
_pkgname=cpppo
pkgver=4.3.0
pkgrel=1
pkgdesc='A Communication Protocol Python Parser and Originator.'
arch=('any')
url='https://pypi.org/project/cpppo/#files'
license=('GPL3')
makedepends=('python2-setuptools' 'python-setuptools')
source=("https://files.pythonhosted.org/packages/df/1b/cc33a5cb65dcb0eb93d6e44a76c36ae4051ddde4f23edb6fde5c3f059267/$_pkgname-$pkgver.tar.gz")
sha512sums=('927c8deec090ace5f6d9ee0e076cb50ff917efe1a16bd34076bf7fa88f630fba58a20ec44f5f8c464cd24881d3bc98ad818eab474573ba0dd8f9198b7ba3a64a')

prepare() {
  cp -a "$_pkgname-$pkgver"{,-2}
}

build() {
  cd "$_pkgname-$pkgver-2"

  python2 setup.py build

  cd "$srcdir/$_pkgname-$pkgver"

  python setup.py build
}

package_python2-cpppo() {
  depends=('python2' 'python2-greenery' 'python2-ipaddress')

  cd "$_pkgname-$pkgver-2"

  python2 setup.py install --prefix=/usr --root="$pkgdir" -O1 --skip-build

  for i in "$pkgdir/usr/bin/"*; do
    mv $i "${i}2"
  done
}

package_python-cpppo() {
  depends=('python' 'python-greenery')

  cd "$_pkgname-$pkgver"

  python setup.py install --prefix=/usr --root="$pkgdir" -O1 --skip-build
}

