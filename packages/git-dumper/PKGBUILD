# This file is part of BlackArch Linux ( https://www.blackarch.org/ ).
# See COPYING for license details.

pkgname=git-dumper
pkgver=44.04543ee
pkgrel=1
pkgdesc='A tool to dump a git repository from a website.'
arch=('any')
groups=('blackarch' 'blackarch-scanner' 'blackarch-webapp')
url='https://github.com/arthaud/git-dumper'
license=('MIT')
depends=('python' 'python-beautifulsoup4' 'python-dulwich' 'python-pysocks'
         'python-requests')
makedepends=('git' 'python-setuptools')
source=("git+https://github.com/arthaud/$pkgname.git")
sha512sums=('SKIP')

pkgver() {
  cd $pkgname

  echo $(git rev-list --count HEAD).$(git rev-parse --short HEAD)
}

build() {
  cd $pkgname

  python setup.py build
}

package() {
  cd $pkgname

  python setup.py install --root="$pkgdir" --prefix=/usr -O1 --skip-build

  install -Dm 644 LICENSE "$pkgdir/usr/share/licenses/$pkgname/LICENSE"
}

