# This file is part of BlackArch Linux ( https://www.blackarch.org/ ).
#
# This PKGBUILD was extracted from AUR and adapted to our style
#
# Old Maintainer: twa022 <twa022 at gmail dot com>
# Old Contributor: Somasis <somasis@cryptolab.net>

pkgname=mugshot
pkgver=0.4.2
pkgrel=1
pkgdesc='Program to update personal user details.'
arch=('any')
url='https://github.com/bluesabre/mugshot'
license=('GPL3')
depends=('gtk3' 'python-pexpect' 'python-dbus' 'python-cairo' 'python-gobject'
         'accountsservice')
makedepends=('python-distutils-extra' 'intltool')
optdepends=('cheese: webcam support'
            'pidgin: update buddy icon'
            'libreoffice: update user details')
options=(!emptydirs)
source=("https://github.com/bluesabre/$pkgname/archive/$pkgname-$pkgver.tar.gz"
        "https://github.com/bluesabre/$pkgname/commit/4f6c280f43261b45f16dea348d33bb3bf02e3ca1.patch")
sha512sums=('721629cf4a7e4d84fd8505279cf88c88ac88c7b6c995ac84a2fd6a9b978c92716bbe20d7b0301ca70e99c007698e4cddf41b6cf1111bf496eec4d39591cd9ed1'
            'c3868bec2772e7834bae8de87c2407986aacca1d32125631da92788ac9ae03e5a688b36ff6090792990f6c3163ecae1623eccd84349dc0099b4809258f14793b')

prepare() {
  cd "$pkgname-$pkgname-$pkgver"

  patch -Np1 -i ../4f6c280f43261b45f16dea348d33bb3bf02e3ca1.patch
}

package() {
  cd "$pkgname-$pkgname-$pkgver"

  python setup.py install --root="$pkgdir" --prefix=/usr -O1

  install -Dm 644 -t "$pkgdir/usr/share/doc/$pkgname/" AUTHORS NEWS README.md
  install -Dm 644 COPYING "$pkgdir/usr/share/licenses/$pkgname/LICENSE"
}

