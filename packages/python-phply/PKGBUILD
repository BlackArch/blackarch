# This file is part of BlackArch Linux ( https://www.blackarch.org/ ).
# See COPYING for license details.

pkgbase=python-phply
pkgname=('python2-phply' 'python-phply')
_pkgname=phply
pkgver=1.2.5
pkgrel=11
pkgdesc='PHP parser written in Python using PLY.'
arch=('any')
url='https://pypi.org/project/phply/#files'
license=('BSD')
makedepends=('python2-setuptools' 'python-setuptools')
source=("https://files.pythonhosted.org/packages/e2/cd/8dd43fc77b3390e58237e5b3b000efcfbc4fc460941aee36ec0e2a7be4b3/$_pkgname-$pkgver.tar.gz")
sha512sums=('24e12bad4d7e44b3354717ff59ca751c337a1083f6d576af9c334fc53afd3921a3714f594cabcf107526b423ce9039fc5ed5d5713a6be208477e7ff41da114da')

prepare() {
  cp -a "$_pkgname-$pkgver"{,-2}
}

build() {
  cd "$_pkgname-$pkgver-2"

  python2 setup.py build

  cd "$srcdir/$_pkgname-$pkgver"
}

package_python2-phply() {
  depends=('python2' 'python2-ply')

  cd "$_pkgname-$pkgver-2"

  python2 setup.py install --prefix=/usr --root="$pkgdir" --optimize=1 \
    --skip-build

  mv "$pkgdir/usr/bin/phplex" "$pkgdir/usr/bin/phplex2"
  mv "$pkgdir/usr/bin/phpparse" "$pkgdir/usr/bin/phpparse2"

  rm -rf "$pkgdir/usr/lib/python2.7/site-packages/tests"
}

package_python-phply() {
  depends=('python' 'python-ply')

  cd "$_pkgname-$pkgver"

  python setup.py install --prefix=/usr --root="$pkgdir" --optimize=1 \
    --skip-build

  rm -rf "$pkgdir/usr/lib/python3.9/site-packages/tests"
}

