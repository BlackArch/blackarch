# This file is part of BlackArch Linux ( http://blackarch.org ).
# See COPYING for license details.

pkgname=sqlmap
pkgver=1.0dev
pkgrel=4
groups=('blackarch' 'blackarch-exploitation' 'blackarch-fuzzer' 'blackarch-webapp')
pkgdesc="An automatic SQL injection tool developed in Python"
url='http://sqlmap.sourceforge.net'
license=('GPL')
arch=('i686' 'x86_64' 'armv6h' 'armv7h')
depends=('python2')
options=(!strip)
source=("git+https://github.com/sqlmapproject/sqlmap")
md5sums=('SKIP')

package() {
  cd "$srcdir/sqlmap"

  install -dm755 "$pkgdir/usr/share/sqlmap"
  install -dm755 "$pkgdir/usr/bin"

  cp --no-preserve=ownership -R * "$pkgdir/usr/share/sqlmap"

  if [ $CARCH == i686 ]; then
    rm "$pkgdir/usr/share/sqlmap/udf/mysql/linux/64/lib_mysqludf_sys.so"
    rm "$pkgdir/usr/share/sqlmap/udf/postgresql/linux/64/8.2/lib_postgresqludf_sys.so"
    rm "$pkgdir/usr/share/sqlmap/udf/postgresql/linux/64/8.3/lib_postgresqludf_sys.so"
    rm "$pkgdir/usr/share/sqlmap/udf/postgresql/linux/64/8.4/lib_postgresqludf_sys.so"
  fi

  cat > "$pkgdir/usr/bin/sqlmap" << EOF
#!/bin/bash
cd /usr/share/sqlmap
python2 ./sqlmap.py "\$@"
EOF
  chmod +x "$pkgdir/usr/bin/sqlmap"
}
