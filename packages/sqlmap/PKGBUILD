pkgname=sqlmap
pkgver=0.9
groups=(blackarch blackarch-web-apps blackarch-analysis)
pkgrel=1
pkgdesc='An automatic SQL injection tool developed in Python'
url='http://sqlmap.sourceforge.net'
license=('GPL')
arch=('i686' 'x86_64')
depends=('python2')
source=(http://downloads.sourceforge.net/$pkgname/$pkgname-$pkgver.tar.gz)
md5sums=('608d5773e0925e96e618171829d679b9')

package() {
  install -dm755 "${pkgdir}"/opt/$pkgname

  cp --no-preserve=ownership -R "$srcdir/$pkgname"/* "$pkgdir/opt/$pkgname"

  if [ $CARCH == i686 ]; then
    rm "$pkgdir/opt/$pkgname/udf/mysql/linux/64/lib_mysqludf_sys.so"
    rm "$pkgdir/opt/$pkgname/udf/postgresql/linux/64/8.2/lib_postgresqludf_sys.so"
    rm "$pkgdir/opt/$pkgname/udf/postgresql/linux/64/8.3/lib_postgresqludf_sys.so"
    rm "$pkgdir/opt/$pkgname/udf/postgresql/linux/64/8.4/lib_postgresqludf_sys.so"
  fi

  install -dm755 "${pkgdir}"/usr/bin

  cat > "${pkgdir}"/usr/bin/sqlmap << EOF
#!/bin/bash
cd /opt/sqlmap
python2 ./sqlmap.py "\$@"
EOF

  chmod +x "${pkgdir}"/usr/bin/sqlmap
}
