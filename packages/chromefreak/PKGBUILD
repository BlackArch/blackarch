# This file is part of BlackArch Linux ( http://blackarch.org ).
# See COPYING for license details.

pkgname='chromefreak'
pkgver=24.12745b1
pkgrel=2
groups=('blackarch' 'blackarch-forensic')
pkgdesc='A Cross-Platform Forensic Framework for Google Chrome'
arch=('any')
depends=('python2' 'python2-simplejson' 'python2-pysqlite')
makedepends=('git')
url='http://osandamalith.github.io/ChromeFreak/'
license=('custom')
source=('git+https://github.com/OsandaMalith/ChromeFreak.git')
sha1sums=('SKIP')

pkgver() {
  cd "$srcdir/ChromeFreak"

  echo $(git rev-list --count HEAD).$(git rev-parse --short HEAD)
}

prepare() {
  cd "$srcdir/ChromeFreak"

  sed -i 's|bin/python$|bin/python2|' ChromeFreak.py
}

package() {
  cd "$srcdir/ChromeFreak"

  install -Dm755 ChromeFreak.py "$pkgdir/usr/bin/chromefreak"
  install -Dm644 README.md "$pkgdir/usr/share/doc/chromefreak/README.md"
  install -Dm755 EXE/ChromeFreak.exe \
    "$pkgdir/usr/share/windows/chromefreak/chromefreak.exe"
}
