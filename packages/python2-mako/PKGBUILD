# This file is part of BlackArch Linux ( https://www.blackarch.org/ ).
# See COPYING for license details.

pkgname=python2-mako
_pkgname=Mako
pkgver=1.0.14
pkgrel=1
pkgdesc='A super-fast templating language that borrows the best ideas from the existing templating languages.'
url='https://pypi.org/project/mako/#files'
arch=('any')
license=('MIT')
depends=('python2' 'python2-markupsafe' 'python2-beaker')
makedepends=('python2-setuptools')
source=("https://files.pythonhosted.org/packages/1b/a5/023aba3d69aacef6bfc13797bdc3dd03c6fb4ae2dcd2fde7dffc37233924/$_pkgname-$pkgver.tar.gz")
sha512sums=('e9a4840f4dddd77d55ad6d3b8724f0326937c04aa9c8c578ec385c70f897419b13ffc234cc673a316de2fa8cd7060fc837cf12f2317658181f8d2eb7dd4ac88f')

build() {
  cd "$_pkgname-$pkgver"

  python2 setup.py build
}

package() {
  cd "$_pkgname-$pkgver"

  python2 setup.py install --root="$pkgdir" --prefix=/usr -O1 --skip-build

  mv "$pkgdir/usr/bin/mako-render" "$pkgdir/usr/bin/mako-render2"
}

