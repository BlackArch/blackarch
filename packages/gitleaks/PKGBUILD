# This file is part of BlackArch Linux ( https://www.blackarch.org/ ).
# See COPYING for license details.

pkgname=gitleaks
pkgver=642.e74e629
pkgrel=1
groups=('blackarch' 'blackarch-recon')
pkgdesc='Audit Git repos for secrets and keys.'
url='https://github.com/zricethezav/gitleaks'
arch=('x86_64' 'aarch64')
license=('GPL3')
makedepends=('go' 'git')
source=("git+https://github.com/zricethezav/$pkgname.git")
sha512sums=('SKIP')

pkgver() {
  cd $pkgname

  echo $(git rev-list --count HEAD).$(git rev-parse --short HEAD)
}

build() {
  cd $pkgname

  GOPATH="$srcdir" go mod download
  GOPATH="$srcdir" go build \
    -trimpath \
    -buildmode=pie \
    -mod=readonly \
    -modcacherw \
    -ldflags "-s -w" \
    -o $pkgname .
}

package() {
  cd $pkgname

  install -Dm 755 $pkgname "$pkgdir/usr/bin/$pkgname"
  install -Dm 644 -t "$pkgdir/usr/share/doc/$pkgname" README.md
}

