# This file is part of BlackArch Linux ( http://blackarch.org ).
# See COPYING for license details.

pkgname=gitleaks
pkgver=223.88cc9bb
pkgrel=1
groups=('blackarch' 'blackarch-recon')
pkgdesc='Audit Git repos for secrets and keys.'
url='https://github.com/zricethezav/gitleaks'
arch=('x86_64' 'armv6h' 'armv7h' 'aarch64')
license=('GPL3')
depends=('go' 'glibc')
makedepends=('go-pie' 'git')
source=('git+https://github.com/zricethezav/gitleaks.git')
sha512sums=('SKIP')

pkgver() {
  cd "$srcdir/gitleaks"

  echo $(git rev-list --count HEAD).$(git rev-parse --short HEAD)
}

prepare() {
  cd "$srcdir"

  install -dm 755 src/github.com/zricethezav

  ln -rsnf gitleaks src/${url#*//}
}

build() {
  cd "$srcdir"

  cd src/${url#*//}

  export GOPATH="$srcdir"

  go build -v
}

package() {
  cd "$srcdir"

  cd gitleaks

  install -Dm755 gitleaks "$pkgdir"/usr/bin/gitleaks
  install -Dm644 -t "$pkgdir/usr/share/doc/gitleaks" README.md CHANGELOG.md
  install -Dm644 LICENSE.md "$pkgdir/usr/share/licenses/gitleaks/LICENSE.md"
}
