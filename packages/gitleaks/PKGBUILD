# This file is part of BlackArch Linux ( https://www.blackarch.org/ ).
# See COPYING for license details.

pkgname=gitleaks
pkgver=344.52cfad1
pkgrel=1
groups=('blackarch' 'blackarch-recon')
pkgdesc='Audit Git repos for secrets and keys.'
url='https://github.com/zricethezav/gitleaks'
arch=('x86_64' 'armv6h' 'armv7h' 'aarch64')
license=('GPL3')
depends=('go' 'glibc')
makedepends=('go-pie' 'git')
source=('git+https://github.com/zricethezav/gitleaks.git')
sha512sums=('SKIP')

pkgver() {
  cd $pkgname

  echo $(git rev-list --count HEAD).$(git rev-parse --short HEAD)
}

prepare() {
  install -dm 755 src/github.com/zricethezav

  ln -rsnf gitleaks src/${url#*//}
}

build() {
  cd src/${url#*//}

  export GOPATH="$srcdir"

  go build -v
}

package() {
  cd $pkgname

  install -Dm 755 gitleaks "$pkgdir/usr/bin/$pkgname"
  install -Dm 644 -t "$pkgdir/usr/share/doc/$pkgname" README.md CHANGELOG.md
  install -Dm 644 LICENSE.md "$pkgdir/usr/share/licenses/$pkgname/LICENSE.md"
}

