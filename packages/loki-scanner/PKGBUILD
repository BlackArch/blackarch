# This file is part of BlackArch Linux ( https://www.blackarch.org/ ).
# See COPYING for license details.

pkgname=loki-scanner
pkgver=1036.8ae2ff7
pkgrel=1
pkgdesc='Simple IOC and Incident Response Scanner.'
groups=('blackarch' 'blackarch-forensic' 'blackarch-scanner')
arch=('any')
url='https://github.com/Neo23x0/Loki'
license=('GPL3')
depends=('python2' 'python2-colorama' 'python2-netaddr'
         'python2-psutil' 'python2-yara')
makedepends=('git')
source=("$pkgname::git+https://github.com/Neo23x0/Loki.git")
sha512sums=('SKIP')

pkgver() {
  cd $pkgname

  echo $(git rev-list --count HEAD).$(git rev-parse --short HEAD)
}

package() {
  cd $pkgname

  install -dm 755 "$pkgdir/usr/bin"
  install -dm 755 "$pkgdir/usr/share/$pkgname"

  install -Dm 644 -t "$pkgdir/usr/share/doc/$pkgname/" README.md
  install -Dm 644 LICENSE "$pkgdir/usr/share/licenses/$pkgname/LICENSE"

  rm LICENSE README.md build.bat requirements.txt

  cp -a --no-preserve=ownership * "$pkgdir/usr/share/$pkgname/"

  cat > "$pkgdir/usr/bin/$pkgname" << EOF
#!/bin/sh
cd /usr/share/$pkgname
exec python2 loki.py "\$@"
EOF

  chmod +x "$pkgdir/usr/bin/$pkgname"
}

