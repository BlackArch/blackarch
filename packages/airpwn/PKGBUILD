# This file is part of BlackArch Linux ( http://blackarch.org ).
# See COPYING for license details.

pkgname='airpwn'
pkgver='1.4'
pkgrel=3
pkgdesc='A tool for generic packet injection on an 802.11 network.'
arch=('x86_64' 'armv6h' 'armv7h' 'aarch64')
url="http://airpwn.sourceforge.net"
license=('GPL')
depends=('pcre' 'libpcap' 'openssl' 'lorcon-old' 'wireless_tools' 'libnet'
         'python2')
groups=('blackarch' 'blackarch-wireless')
source=("http://downloads.sourceforge.net/airpwn/airpwn-${pkgver}.tgz")
sha1sums=('30df2f76cbbf35a9aa4053fa622f5249f2960e88')

build() {
  cd "$srcdir/airpwn-$pkgver"

  unset LDFLAGS

  ./configure --prefix=/usr --mandir=/usr/share/man

  sed -i 's/python2.4/python2/g' conf.h
  sed -i 's|python2/Python\.h|python2.7/Python.h|' conf.h
  sed -i 's|LDFLAGS = |LDFLAGS = -lssl -lcrypto -lorcon -lpthread -lpcre -lpcap -lnet -lpython2.7|g' Makefile

  make
}

package() {
  cd "$srcdir/airpwn-$pkgver"

  mkdir -p "$pkgdir/usr/bin"
  mkdir -p "$pkgdir/usr/share/airpwn"

  cp -aR * "$pkgdir/usr/share/airpwn/"

  ln -s /usr/share/airpwn/airpwn "$pkgdir/usr/bin/"
  ln -s /usr/share/airpwn/wep_keygen "$pkgdir/usr/bin/"
}
