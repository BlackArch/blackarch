# This file is part of BlackArch Linux ( http://blackarch.org ).
# See COPYING for license details.

pkgname='afflib'
pkgver='3.7.16'
pkgrel=2
groups=('blackarch' 'blackarch-forensic')
pkgdesc='An extensible open format for the storage of disk images and related forensic information.'
arch=('x86_64' 'armv6h' 'armv7h' 'aarch64')
url='https://github.com/sshock/AFFLIBv3'
license=('custom')
depends=('ncurses' 'zlib' 'openssl' 'curl' 'expat' 'readline')
makedepends=('make' 'gcc')
source=("https://github.com/sshock/AFFLIBv3/archive/v${pkgver}.tar.gz")
sha512sums=('19eb77d25f939e0bc941cc4106809e23a89f3f07fd0ddb8dfb03b2ddfa24e22c544deb011a5e4247a955529289de0e7ffc4b196c943f8cc352525fcc2b05a279')

build() {
  cd "$srcdir/AFFLIBv3-$pkgver"

  sh bootstrap.sh

  ./configure --prefix=/usr --sysconfdir=/etc

  make
}

package() {
  cd "$srcdir/AFFLIBv3-$pkgver"

  make DESTDIR="$pkgdir" install

  install -Dm644 COPYING "$pkgdir/usr/share/licenses/afflib/COPYING"
}
