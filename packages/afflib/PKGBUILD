# This file is part of BlackArch Linux ( https://www.blackarch.org/ ).
# See COPYING for license details.

pkgname=afflib
_pkgname=AFFLIBv3
pkgver=3.7.18
pkgrel=2
groups=('blackarch' 'blackarch-forensic')
pkgdesc='An extensible open format for the storage of disk images and related forensic information.'
arch=('any')
url='https://github.com/sshock/AFFLIBv3'
license=('custom:unknown')
depends=('ncurses' 'zlib' 'openssl' 'curl' 'expat' 'readline' 'fuse2')
makedepends=('make' 'gcc' 'ncurses' 'zlib' 'openssl' 'curl' 'expat' 'readline'
             'fuse2')
source=("https://github.com/sshock/AFFLIBv3/archive/v$pkgver.tar.gz")
sha512sums=('47ab74c86bbaedc2494558962f99463066e267f7a3f583902a53e1f488085a309b0d871093c67439033272c3ebf03d213649886930b9812013e66782ea84df85')

build() {
  cd "$_pkgname-$pkgver"

  sh bootstrap.sh

  ./configure --prefix=/usr --sysconfdir=/etc --enable-fuse --enable-s3

  make
}

package() {
  cd "$_pkgname-$pkgver"

  make DESTDIR="$pkgdir" install

  install -Dm 644 COPYING "$pkgdir/usr/share/licenses/$pkgname/COPYING"
}

