# This file is part of BlackArch Linux ( https://www.blackarch.org/ ).
# See COPYING for license details.

pkgname=pixd
pkgver=7.873db72
pkgrel=1
pkgdesc='Colourful visualization tool for binary files.'
groups=('blackarch' 'blackarch-binary' 'blackarch-misc')
arch=('x86_64' 'aarch64')
url='https://github.com/FireyFly/pixd'
license=('MIT')
depends=()
makedepends=('git')
source=("git+https://github.com/FireyFly/$pkgname.git")
sha512sums=('SKIP')

pkgver() {
  cd $pkgname

  echo $(git rev-list --count HEAD).$(git rev-parse --short HEAD)
}

build() {
  cd $pkgname

  make
}

package() {
  cd $pkgname

  install -dm 755 "$pkgdir/usr/share/$pkgname"

  install -Dm 755 $pkgname "$pkgdir/usr/bin/$pkgname"
  install -Dm 644 -t "$pkgdir/usr/share/doc/$pkgname/" README.md
  install -Dm 644 "$pkgname.1" "$pkgdir/usr/share/man/man1/$pkgname.1"
  install -Dm 644 LICENSE "$pkgdir/usr/share/licenses/$pkgname/LICENSE"

  rm README.md LICENSE pixd.*

  cp -a * "$pkgdir/usr/share/$pkgname/"
}

