pkgname=grabber
pkgver=0.1
pkgrel=5
groups=('blackarch' 'blackarch-webapp')
pkgdesc="A web application scanner. Basically it detects some kind of vulnerabilities in your website."
arch=('any')
url='http://rgaucher.info/beta/grabber/'
license=('BSD')
depends=('python')
makedepends=('dos2unix')
source=(http://rgaucher.info/beta/grabber/Grabber.zip)
md5sums=('29c34fabf5658d2246b03b1b790eedbf')

prepare() {
  cd "$srcdir/Grabber"
  # Fix line endings.
  dos2unix *

  # Fix log file location.
  sed -i "s|LogHandler('grabber.log')|LogHandler(os.getenv('HOME') + '/.grabber.log')|" grabber.py
}

package() {
  cd "$srcdir/Grabber"

  # Make base directories.
  install -dm755 "$pkgdir/usr/share/grabber"
  install -dm755 "$pkgdir/usr/bin"

  cp --no-preserve=ownership -r * "$pkgdir/usr/share/grabber"

  # The package's setup script is not adequate.
  rm "$pkgdir/usr/share/grabber/setup.py"

  # Make joint script.
  cat > "$pkgdir/usr/bin/grabber-scanner" <<EOF
#!/bin/sh
cd /usr/share/grabber
python2 grabber.py "\$@"
EOF
  chmod +x "$pkgdir/usr/bin/grabber-scanner"
}
