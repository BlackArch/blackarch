# This file is part of BlackArch Linux ( http://blackarch.org ).
# See COPYING for license details.

pkgname='obfsproxy'
pkgver='0.2.13'
pkgrel=1
pkgdesc='A pluggable transport proxy written in Python.'
arch=('any')
groups=('blackarch' 'blackarch-proxy' 'blackarch-networking')
url='https://pypi.python.org/pypi/obfsproxy'
license=('BSD')
depends=('python2>=2.7' 'python2-crypto' 'python2-pyptlib>=0.0.5' 'twisted'
         'python2-yaml')
makedepends=('python2-setuptools')
optdepends=('python2-gmpy: speed up some cryptographic operations')
conflicts=('pyobfsproxy' 'obfsproxy-git')
options=('!emptydirs')
source=("https://pypi.python.org/packages/source/o/obfsproxy/obfsproxy-${pkgver}.tar.gz")
sha1sums=('b95929b2757dee1f25880a7545eb6250ddb25620')

prepare() {
  cd "$srcdir/obfsproxy-$pkgver"

  sed -i "/'argparse',/d" setup.py
}

package() {
  cd "$srcdir/obfsproxy-$pkgver"

  python2 setup.py install --root="$pkgdir" --optimize=1
}
