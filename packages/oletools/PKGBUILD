# This file is part of BlackArch Linux ( https://www.blackarch.org/ ).
# See COPYING for license details.

pkgbase=oletools
pkgname=('python2-oletools' 'python-oletools')
pkgver=0.54.2
pkgrel=1
epoch=1
groups=('blackarch' 'blackarch-binary' 'blackarch-forensic')
pkgdesc='Tools to analyze Microsoft OLE2 files.'
arch=('any')
url='https://pypi.org/project/oletools/'
license=('GPL2')
makedepends=('python2-setuptools' 'python-setuptools')
source=("https://files.pythonhosted.org/packages/6a/f4/33710cf46f4b3dd9fecbf22cebf744a61d65ce653b458e50134010473271/$pkgbase-$pkgver.zip")
sha512sums=('a95eeebc317356ba0f16a2c6f91bbbd654fc1f99851c0a14b5a61f174f4f2ca191f99c8dfc219f9dc10417ac257c2ffd4cb6a0f7ecc44f8a651578231fd3aa35')

prepare() {
  cp -a "$pkgbase-$pkgver"{,-2}
}

build() {
  cd "oletools-$pkgver-2"

  python2 setup.py build

  cd "$srcdir/oletools-$pkgver"

  python setup.py build
}

package_python2-oletools() {
  depends=('python2' 'python2-colorclass' 'python2-pyparsing' 'python2-olefile'
           'python2-easygui')

  cd "oletools-$pkgver"

  python2 setup.py install --root="$pkgdir" --optimize=1 --skip-build

  install -Dm 644 -t "$pkgdir/usr/share/doc/$pkgname/" README.md
  install -Dm 644 oletools/LICENSE.txt \
    "$pkgdir/usr/share/licenses/$pkgname/LICENSE"

  cp oletools/doc/* "$pkgdir/usr/share/doc/$pkgname"

  # fixing shebang line
  cd "$pkgdir/usr/lib/python2.7/site-packages/oletools/"
  for file in *.py; do
    if [ "${file}" != "__init__.py" ]; then
      sed -i '1s/python/python2/' "${file}"
    fi
  done

  for t in "$pkgdir/usr/bin/"* ; do
    mv $t "${t}2"
  done
}

package_python-oletools() {
  depends=('python' 'python-colorclass' 'python-pyparsing' 'python-olefile'
           'python-easygui')

  cd "oletools-$pkgver"

  python setup.py install --root="$pkgdir" --optimize=1 --skip-build

  install -Dm 644 -t "$pkgdir/usr/share/doc/$pkgname/" README.md
  install -Dm 644 oletools/LICENSE.txt \
    "$pkgdir/usr/share/licenses/$pkgname/LICENSE"

  cp oletools/doc/* "$pkgdir/usr/share/doc/$pkgname"
}

