# This file is part of BlackArch Linux ( http://blackarch.org ).
# See COPYING for license details.

pkgname='oletools'
pkgver='0.50'
pkgrel=3
groups=('blackarch' 'blackarch-binary' 'blackarch-forensic')
pkgdesc='Tools to analyze Microsoft OLE2 files.'
arch=('any')
url='http://www.decalage.info/python/oletools'
license=('GPL2')
depends=('python2' 'tk')
source=("https://github.com/decalage2/oletools/archive/v${pkgver}.tar.gz")
#source=("https://bitbucket.org/decalage/oletools/downloads/oletools-${pkgver}.zip")
sha1sums=('3f7538b9476185b413e9f31324c80348593770d3')

prepare() {
  cd "$srcdir/oletools-$pkgver"

  sed -i 's/env python/env python2/' oletools/*.py
}

package() {
  cd "$srcdir/oletools-$pkgver"

  mkdir -p "$pkgdir/usr/bin"

  python2 setup.py install "--root=$pkgdir"

  cd "$pkgdir/usr/lib/python2.7/site-packages/oletools"

  rm __init__.py

  for f in *.py
  do
    cat > "$pkgdir/usr/bin/${f%.py}" <<EOF
#!/bin/sh
exec python2 /usr/lib/python2.7/site-packages/oletools/$f "\${@}"
EOF
    chmod +x "$pkgdir/usr/bin/${f%.py}"

  done
}
