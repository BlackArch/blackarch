# This file is part of BlackArch Linux ( http://blackarch.org ).
# See COPYING for license details.

pkgname=oletools
pkgver=0.53.1
pkgrel=1
groups=('blackarch' 'blackarch-binary' 'blackarch-forensic')
pkgdesc='Tools to analyze Microsoft OLE2 files.'
arch=('any')
url='http://www.decalage.info/python/oletools'
license=('GPL2')
depends=('python2' 'tk')
source=("https://github.com/decalage2/oletools/archive/v${pkgver}.tar.gz")
sha512sums=('e5ce467ef04c2327cf5661fe5319f98ad60c36e71be9f99c858cac7a49ae8a75c886edc797cd436c6680754ce19b5c711da29fcf012c3c776934eef354f650cb')

prepare() {
  cd "$srcdir/oletools-$pkgver"

  sed -i 's/env python/env python2/' oletools/*.py
}

package() {
  cd "$srcdir/oletools-$pkgver"

  install -dm 755 "$pkgdir/usr/bin"

  python2 setup.py install "--root=$pkgdir"

  cd "$pkgdir/usr/lib/python2.7/site-packages/oletools"

  rm __init__.py

  for f in *.py
  do
    cat > "$pkgdir/usr/bin/${f%.py}" <<EOF
#!/bin/sh
exec python2 /usr/lib/python2.7/site-packages/oletools/$f "\${@}"
EOF
    chmod +x "$pkgdir/usr/bin/${f%.py}"

  done
}
