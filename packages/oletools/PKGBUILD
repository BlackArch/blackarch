# This file is part of BlackArch Linux ( https://www.blackarch.org/ ).
# See COPYING for license details.

pkgbase=oletools
pkgname=('python2-oletools' 'python-oletools')
pkgver=0.60
pkgrel=4
epoch=1
groups=('blackarch' 'blackarch-binary' 'blackarch-forensic')
pkgdesc='Tools to analyze Microsoft OLE2 files.'
arch=('any')
url='https://pypi.org/project/oletools/'
license=('GPL2')
makedepends=('python2-setuptools' 'python-setuptools')
source=("https://files.pythonhosted.org/packages/e2/48/0103c6e2b89958245d6f16c7da9a7d4b0acc0efe11608b82d66d4a073b80/$pkgbase-$pkgver.zip"
        'setup.py.patch')
sha512sums=('4a8ed0c60228b80535fcac407d9c2495febd64000f15cc55dfeb3e390c59d86d042938259872676ca3ca33346691a230a376720cb761bb80b2a3e090efc49daf'
            '43a0216e598b191cb07e778a34caff4eaf759d68963a43e03a99cf406d11a96cc754d08b0b9f601f26fefda2ceaa7173c4d9aa4d1bbdbd1499279b290cafd3d4')

prepare() {
  patch -p1 < setup.py.patch

  cp -a "$pkgbase-$pkgver"{,-2}
}

build() {
  cd "$pkgbase-$pkgver-2"

  python2 setup.py build

  cd "$srcdir/$pkgbase-$pkgver"

  python setup.py build
}

package_python2-oletools() {
  depends=('python2' 'python2-colorclass' 'python2-pyparsing' 'python2-olefile'
           'python2-easygui' 'python2-pcodedmp' 'python2-msoffcrypto-tool')

  cd "$pkgbase-$pkgver-2"

  python2 setup.py install --root="$pkgdir" --prefix=/usr -O1 --skip-build

  install -Dm 644 -t "$pkgdir/usr/share/doc/${pkgname[0]}/" README.md
  install -Dm 644 oletools/LICENSE.txt \
    "$pkgdir/usr/share/licenses/${pkgname[0]}/LICENSE"

  # fixing shebang line
  cd "$pkgdir/usr/lib/python2.7/site-packages/$pkgbase/"
  for file in *.py; do
    if [ "${file}" != "__init__.py" ]; then
      sed -i '1s/python/python2/' "${file}"
    fi
  done

  for t in "$pkgdir/usr/bin/"* ; do
    mv $t "${t}2"
  done
}

package_python-oletools() {
  depends=('python' 'python-colorclass' 'python-pyparsing' 'python-olefile'
           'python-easygui' 'python-pcodedmp' 'python-msoffcrypto-tool')

  cd "$pkgbase-$pkgver"

  python setup.py install --root="$pkgdir" --prefix=/usr -O1 --skip-build

  install -Dm 644 -t "$pkgdir/usr/share/doc/$pkgbase/" README.md
  install -Dm 644 oletools/LICENSE.txt \
    "$pkgdir/usr/share/licenses/$pkgbase/LICENSE"

  cp oletools/doc/* "$pkgdir/usr/share/doc/$pkgbase"
}

