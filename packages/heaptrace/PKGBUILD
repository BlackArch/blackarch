# This file is part of BlackArch Linux ( https://www.blackarch.org/ ).
# See COPYING for license details.

pkgname=heaptrace
pkgver=2.2.8.2.r19.g6f66cad
pkgrel=1
groups=('blackarch' 'blackarch-debugger')
pkgdesc='Helps visualize heap operations for pwn and debugging.'
url='https://github.com/Arinerron/heaptrace'
license=('BSD')
arch=('x86_64' 'aarch64')
depends=('glibc')
makedepends=('git')
source=("git+https://github.com/Arinerron/$pkgname.git")
sha512sums=('SKIP')

pkgver() {
  cd $pkgname

  git describe --long --tags | sed 's/\([^-]*-g\)/r\1/;s/-/./g'
}

build() {
  cd $pkgname

  make
}

package() {
  cd $pkgname

  make DESTDIR="$pkgdir/usr" install
}

