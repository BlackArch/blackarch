# This file is part of BlackArch Linux ( http://blackarch.org ).
# See COPYING for license details.

pkgname='perl-parallel-forkmanager'
pkgver='1.19'
pkgrel=5
pkgdesc='A simple parallel processing fork manager.'
arch=('any')
license=('PerlArtistic' 'GPL')
options=('!emptydirs')
makedepends=('perl-extutils-makemaker')
url="http://search.cpan.org/~yanick/Parallel-ForkManager-${pkgver}"
source=("http://search.cpan.org/CPAN/authors/id/Y/YA/YANICK/Parallel-ForkManager-${pkgver}.tar.gz")
sha512sums=('fdd076de153a09e1e8fb681cadc6c36a05f4c8a6718df61937eb5d26729108a6f2f322a7fb9d4bf6166bbfe52cb857e3ea6cab8368c7b5959500018cc01747c6')

prepare() {
  cd "${srcdir}/Parallel-ForkManager-${pkgver}"

  export PERL_MM_USE_DEFAULT=1 PERL_AUTOINSTALL="--skipdeps" \
    PERL_MM_OPT="INSTALLDIRS=vendor DESTDIR='$pkgdir'" \
    PERL_MB_OPT="--installdirs vendor --destdir '$pkgdir'" \
    MODULEBUILDRC=/dev/null

  perl Makefile.PL
}

build() {
  cd "${srcdir}/Parallel-ForkManager-${pkgver}"

  make
}

package() {
  cd "${srcdir}/Parallel-ForkManager-${pkgver}"

  make DESTDIR="$pkgdir" install
}
