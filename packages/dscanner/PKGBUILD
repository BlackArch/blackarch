# This file is part of BlackArch Linux ( http://blackarch.org ).
# See COPYING for license details.

pkgname='dscanner'
pkgver=982.762dee0
pkgrel=3
pkgdesc='Swiss-army knife for D source code.'
arch=('i686' 'x86_64' 'armv6h' 'armv7h')
url='https://github.com/Hackerpilot/Dscanner'
groups=('blackarch' 'blackarch-code-audit')
license=('unknown:custom')
depends=('glibc')
makedepends=('libphobos-devel' 'dmd' 'git')
provides=('dscanner')
options=('!strip')
source=('git+https://github.com/Hackerpilot/Dscanner')
sha1sums=('SKIP')

pkgver() {
  cd "$srcdir/Dscanner"

  echo $(git rev-list --count HEAD).$(git rev-parse --short HEAD)
}

prepare() {
  cd "$srcdir/Dscanner"

  git submodule init
  git submodule update
}

build() {
  cd "$srcdir/Dscanner"

  make
}

package() {
  cd "$srcdir/Dscanner"

  install -Dm755 bin/dscanner "$pkgdir/usr/bin/dscanner"
  install -Dm644 README.md "$pkgdir/usr/share/doc/dscanner/README.md"
  install -Dm644 LICENSE_1_0.txt "$pkgdir/usr/share/licenses/dscanner/LICENSE"
}
