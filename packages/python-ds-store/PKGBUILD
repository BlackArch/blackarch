# This file is part of BlackArch Linux ( https://www.blackarch.org/ ).
# See COPYING for license details.

pkgbase=python-ds-store
pkgname=('python2-ds-store' 'python-ds-store')
_pkgname=ds_store
pkgver=1.3.1
pkgrel=1
pkgdesc='A parser + crawler for .DS_Store files exposed publically.'
arch=('any')
url='https://pypi.org/project/ds_store/#files'
license=('Apache')
makedepends=('python2-setuptools' 'python-setuptools')
source=("https://files.pythonhosted.org/packages/source/${_pkgname::1}/$_pkgname/ds_store-$pkgver.tar.gz")
sha512sums=('20d520cf1082f12843ae07652ccbf01c0ff1be11a54ed40839f153e0dfd283fa25997353bcc0a0c196e841eab32e467fa63079eddb22a4b5b7da3a9faffaf04a')

prepare() {
  cp -a "$_pkgname-$pkgver"{,-2}
}

build() {
  cd "$_pkgname-$pkgver-2"

  python2 setup.py build

  cd "$srcdir/$_pkgname-$pkgver"

  python setup.py build
}

package_python2-ds-store() {
  depends=('python2-future' 'python2-mac-alias' 'python2-biplist')

  cd "$_pkgname-$pkgver-2"

  python2 setup.py install --prefix=/usr --root="$pkgdir" --optimize=1 \
    --skip-build
}

package_python-ds-store() {
  depends=('python-future' 'python-mac-alias' 'python-biplist')

  cd "$_pkgname-$pkgver"

  python setup.py install --prefix=/usr --root="$pkgdir" --optimize=1 \
    --skip-build
}

