# This file is part of BlackArch Linux ( https://www.blackarch.org/ ).
# See COPYING for license details.

pkgbase=python-ds-store
pkgname=('python2-ds-store' 'python-ds-store')
_pkgname=ds_store
pkgver=1.1.2
pkgrel=2
pkgdesc='A parser + crawler for .DS_Store files exposed publically.'
arch=('any')
url='https://pypi.org/project/ds_store/#files'
license=('Apache')
makedepends=('python2-setuptools' 'python-setuptools')
source=("https://files.pythonhosted.org/packages/7a/58/e3caf42daddef72a7c92778dba181ff37edd04fb64869eea504ee6b6ea8f/$_pkgname-$pkgver.tar.gz")
sha512sums=('796c6eaf9f458b2893f5a5d0f90f62c34c3ab9eeb7f5180e41fee72e30c692e08fbcdfbdab7ab05cc9fefa2bf3eebd643d5c19ec898aa03e699b00ef9280c0f7')

prepare() {
  cp -a "$_pkgname-$pkgver"{,-2}
}

build() {
  cd "$_pkgname-$pkgver-2"

  python2 setup.py build

  cd "$srcdir/$_pkgname-$pkgver"

  python setup.py build
}

package_python2-ds-store() {
  depends=('python2-future' 'python2-mac-alias' 'python2-biplist')

  cd "$_pkgname-$pkgver-2"

  python2 setup.py install --prefix=/usr --root="$pkgdir" --optimize=1 \
    --skip-build
}

package_python-ds-store() {
  depends=('python-future' 'python-mac-alias' 'python-biplist')

  cd "$_pkgname-$pkgver"

  python setup.py install --prefix=/usr --root="$pkgdir" --optimize=1 \
    --skip-build
}

