# This file is part of BlackArch Linux ( https://www.blackarch.org/ ).
# See COPYING for license details.

pkgbase=python-ds-store
pkgname=('python2-ds-store' 'python-ds-store')
_pkgname=ds_store
pkgver=1.3.0
pkgrel=2
pkgdesc='A parser + crawler for .DS_Store files exposed publically.'
arch=('any')
url='https://pypi.org/project/ds_store/#files'
license=('Apache')
makedepends=('python2-setuptools' 'python-setuptools')
source=("https://files.pythonhosted.org/packages/38/a1/cab1b1cf3387eec963a18706854facdc5b699f782985a0001579e5dd6cda/ds_store-$pkgver.tar.gz")
sha512sums=('864c799fb86842fe890f4fdd852eb07834b3fa48b6fd537a89268afbeae8e10893a982200c7450c9238c6174066abeb7cde9ceed8dc06da3a03045faf00202fd')

prepare() {
  cp -a "$_pkgname-$pkgver"{,-2}
}

build() {
  cd "$_pkgname-$pkgver-2"

  python2 setup.py build

  cd "$srcdir/$_pkgname-$pkgver"

  python setup.py build
}

package_python2-ds-store() {
  depends=('python2-future' 'python2-mac-alias' 'python2-biplist')

  cd "$_pkgname-$pkgver-2"

  python2 setup.py install --prefix=/usr --root="$pkgdir" --optimize=1 \
    --skip-build
}

package_python-ds-store() {
  depends=('python-future' 'python-mac-alias' 'python-biplist')

  cd "$_pkgname-$pkgver"

  python setup.py install --prefix=/usr --root="$pkgdir" --optimize=1 \
    --skip-build
}

