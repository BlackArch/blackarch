# This file is part of BlackArch Linux ( https://www.blackarch.org/ ).
# See COPYING for license details.

pkgname=python-nassl
_pkgname=nassl
pkgver=5.0.0
pkgrel=1
epoch=1
pkgdesc='Experimental Python wrapper for OpenSSL.'
arch=('x86_64' 'aarch64')
url='https://github.com/nabla-c0d3/nassl'
license=('GPL')
depends=('python')
makedepends=('openssl' 'openssl-1.0' 'zlib' 'python-setuptools' 'python-invoke'
             'python-pipenv')
source=("https://github.com/nabla-c0d3/nassl/archive/$pkgver.tar.gz")
sha512sums=('1e147dd8590254272d81d68390d7b3bf244e051e2ab9f83f3b5f5772d7c23659662ee61dcfe68818ab3d6265c9a4137f54e76185101403b818523ee78ba69208')

build() {
  cd "$_pkgname-$pkgver"

  pipenv install --dev

  invoke build.zlib
  invoke build.legacy-openssl
  invoke build.modern-openssl

  python setup.py build_ext -i -I/usr/include/openssl-1.0/ \
    -I/usr/include/openssl/

  python setup.py build
}

package() {
  cd "$_pkgname-$pkgver"

  python setup.py install --root="$pkgdir" --prefix=/usr -O1 --skip-build
}

