# This file is part of BlackArch Linux ( https://www.blackarch.org/ ).
# See COPYING for license details.

pkgname=python-nassl
pkgver=2.2.0
pkgrel=1
pkgdesc='Experimental Python wrapper for OpenSSL.'
arch=('x86_64' 'armv6h' 'armv7h' 'aarch64')
url='https://github.com/nabla-c0d3/nassl'
license=('GPL')
depends=('python')
makedepends=('openssl' 'openssl-1.0' 'zlib' 'python-setuptools' 'python-invoke'
             'python-pipenv')
source=("$pkgname-$pkgver.tar.gz::https://github.com/nabla-c0d3/nassl/archive/$pkgver.tar.gz")

sha512sums=('75dc81adb2c01663dbb9b8f4e7d457e206c53ea988e55024c09bcfae16a9f13915b718721624377a8e0330e1fcc6019dbc3a8925a53a791d7f2582c0a6f9202f')

build() {
  cd "$pkgname-$pkgver"

  pipenv install --dev

  invoke build.zlib
  invoke build.legacy-openssl
  invoke build.modern-openssl

  python setup.py build_ext -i -I/usr/include/openssl-1.0/ \
    -I/usr/include/openssl/

  python setup.py build
}

package() {
  cd "$pkgname-$pkgver"

  python setup.py install --root="$pkgdir" --optimize=1 --skip-build
}

