# This file is part of BlackArch Linux ( https://www.blackarch.org/ ).
# See COPYING for license details.

pkgname=python-nassl
_pkgname=nassl
pkgver=3.1.0
pkgrel=3
pkgdesc='Experimental Python wrapper for OpenSSL.'
arch=('x86_64' 'aarch64')
url='https://github.com/nabla-c0d3/nassl'
license=('GPL')
depends=('python')
makedepends=('openssl' 'openssl-1.0' 'zlib' 'python-setuptools' 'python-invoke'
             'python-pipenv')
source=("https://github.com/nabla-c0d3/nassl/archive/$pkgver.tar.gz")
sha512sums=('ce0ae332e0da46341e320f76cc6bdc582232f4e28d0e6212e6b140d6e8aeab6a4e11e9ef776c01243c903c19899639f35e754ed6418a3dedd22801561107c08f')

prepare() {
  cd "$_pkgname-$pkgver"

  sed -i 's/3.7/3.9/' Pipfile
}

build() {
  cd "$_pkgname-$pkgver"

  pipenv install --dev

  invoke build.zlib
  invoke build.legacy-openssl
  invoke build.modern-openssl

  python setup.py build_ext -i -I/usr/include/openssl-1.0/ \
    -I/usr/include/openssl/

  python setup.py build
}

package() {
  cd "$_pkgname-$pkgver"

  python setup.py install --root="$pkgdir" --prefix=/usr -O1 --skip-build
}

