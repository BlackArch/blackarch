# This file is part of BlackArch Linux ( https://www.blackarch.org/ ).
# See COPYING for license details.

pkgname=python-nassl
_pkgname=nassl
pkgver=5.4.0
pkgrel=1
epoch=1
pkgdesc='Experimental Python wrapper for OpenSSL.'
arch=('x86_64' 'aarch64')
url='https://github.com/nabla-c0d3/nassl'
license=('AGPL-3.0-only')
depends=('python')
makedepends=('openssl' 'openssl-1.0' 'zlib' 'python-setuptools' 'python-invoke'
             'python-pipenv')
source=("https://github.com/nabla-c0d3/nassl/archive/$pkgver.tar.gz")
sha512sums=('6d740bf9e0ac6003281f094cd313aac7e9d3f0017902174cdfc571ee374a44770361c4c789f6fad11b042bbf383b189e2d4703fa882fec73af83a7cd71f5e98a')

build() {
  cd "$_pkgname-$pkgver"

  pipenv install --dev

  invoke build.zlib
  invoke build.legacy-openssl
  invoke build.modern-openssl

  python setup.py build_ext -i -I/usr/include/openssl-1.0/ \
    -I/usr/include/openssl/

  python setup.py build
}

package() {
  cd "$_pkgname-$pkgver"

  python setup.py install --root="$pkgdir" --prefix=/usr -O1 --skip-build
}

