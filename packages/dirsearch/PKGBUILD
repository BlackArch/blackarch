# This file is part of BlackArch Linux ( https://www.blackarch.org/ ).
# See COPYING for license details.

pkgname=dirsearch
pkgver=272.9c4209a
pkgrel=1
groups=('blackarch' 'blackarch-webapp' 'blackarch-scanner')
pkgdesc='HTTP(S) directory/file brute forcer.'
arch=('any')
url='https://github.com/maurosoria/dirsearch'
license=('GPL2')
depends=('python' 'python-simplejson' 'python-beautifulsoup4'
         'python2-pyopenssl')
makedepends=('git')
source=('git+https://github.com/maurosoria/dirsearch.git')
sha512sums=('SKIP')

pkgver() {
  cd $pkgname

  echo $(git rev-list --count HEAD).$(git rev-parse --short HEAD)
}

prepare() {
  cd $pkgname

  sed -i '/save-logs-home/s/^#//g' default.conf
}

package() {
  cd $pkgname

  install -dm 755 "$pkgdir/usr/bin"
  install -dm 755 "$pkgdir/usr/share/$pkgname"

  install -Dm 644 README.md "$pkgdir/usr/share/doc/$pkgname/README.md"

  rm README.md

  cp -a * "$pkgdir/usr/share/$pkgname/"

  cat > "$pkgdir/usr/bin/$pkgname" << EOF
#!/bin/sh
exec /usr/bin/python /usr/share/$pkgname/dirsearch.py "\$@"
EOF

  chmod +x "$pkgdir/usr/bin/$pkgname"
}

