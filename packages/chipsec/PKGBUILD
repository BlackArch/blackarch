# This file is part of BlackArch Linux ( http://blackarch.org ).
# See COPYING for license details.

pkgname=chipsec
pkgver=1.3.6rc1
pkgrel=1
epoch=1
pkgdesc='Platform Security Assessment Framework.'
groups=('blackarch' 'blackarch-hardware' 'blackarch-binary' 'blackarch-forensic'
        'blackarch-scanner' 'blackarch-fuzzer')
arch=('x86_64' 'armv6h' 'armv7h' 'armv8h')
url='https://github.com/chipsec/chipsec'
license=('GPL2')
depends=('python2' 'python2-lxml' 'nasm' 'linux-headers' 'libelf')
makedepends=('python2-setuptools')
source=("https://github.com/chipsec/chipsec/archive/v${pkgver}.tar.gz")
install='chipsec.install'
sha512sums=('620c29f5a5eafc9033e788a4344613e6989ecdf776c46915bea51a625b524a3965a63f0819d0a7c7a4488cdc862026791a731bddea4151e06e13a54c614f1bed')

build() {
  cd "$srcdir/chipsec-$pkgver"

  python2 setup.py build
}

package() {
  cd "$srcdir/chipsec-$pkgver"

  python2 setup.py install --root="$pkgdir" --skip-driver --optimize=1

  install -Dm644 drivers/linux/dkms.conf \
    "$pkgdir/usr/src/chipsec-$pkgver/dkms.conf"
  cp -a drivers/linux/* "$pkgdir/usr/src/chipsec-$pkgver/"
  install -Dm644 -t "$pkgdir/usr/share/doc/chipsec/" README.md \
    chipsec-manual.pdf
  install -Dm644 -t "$pkgdir/usr/share/licenses/chipsec/" LICENSE COPYING
}
