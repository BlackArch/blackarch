# This file is part of BlackArch Linux ( https://www.blackarch.org/ ).
# See COPYING for license details.

pkgname=chipsec
pkgver=1.3.6
pkgrel=1
epoch=2
pkgdesc='Platform Security Assessment Framework.'
groups=('blackarch' 'blackarch-hardware' 'blackarch-binary' 'blackarch-forensic'
        'blackarch-scanner' 'blackarch-fuzzer')
arch=('x86_64' 'armv6h' 'armv7h' 'armv8h')
url='https://github.com/chipsec/chipsec'
license=('GPL2')
depends=('python2' 'python2-lxml' 'nasm' 'linux-headers' 'libelf')
makedepends=('python2-setuptools')
source=("https://github.com/chipsec/chipsec/archive/v$pkgver.tar.gz")
install='chipsec.install'
sha512sums=('d4ee688dd31c8d44836fa9c8f12aa136a7560295b2d69ecc92eba3942088239b6001957b1f3c50978f7ac6b5613a699d4f25017f9b4b85d14e1cd7829ff11a73')

build() {
  cd "$pkgname-$pkgver"

  python2 setup.py build
}

package() {
  cd "$pkgname-$pkgver"

  python2 setup.py install --root="$pkgdir" --skip-driver --optimize=1

  install -Dm 644 drivers/linux/dkms.conf \
    "$pkgdir/usr/src/$pkgname-$pkgver/dkms.conf"
  cp -a drivers/linux/* "$pkgdir/usr/src/$pkgname-$pkgver/"
  install -Dm 644 -t "$pkgdir/usr/share/doc/$pkgname/" README.md \
    chipsec-manual.pdf
  install -Dm 644 -t "$pkgdir/usr/share/licenses/$pkgname/" LICENSE COPYING
}

