# This file is part of BlackArch Linux ( https://www.blackarch.org/ ).
# See COPYING for license details.

pkgname=chipsec
pkgver=526.e2d2442
pkgrel=1
epoch=2
pkgdesc='Platform Security Assessment Framework.'
groups=('blackarch' 'blackarch-hardware' 'blackarch-binary' 'blackarch-forensic'
        'blackarch-scanner' 'blackarch-fuzzer')
arch=('x86_64' 'armv6h' 'armv7h' 'armv8h')
url='https://github.com/chipsec/chipsec'
license=('GPL2')
depends=('python2' 'python2-lxml' 'nasm' 'linux-headers' 'libelf')
makedepends=('python2-setuptools')
source=('git+https://github.com/chipsec/chipsec.git')
install='chipsec.install'
sha512sums=('SKIP')

pkgver() {
  cd $pkgname

  echo $(git rev-list --count HEAD).$(git rev-parse --short HEAD)
}

build() {
  cd "$pkgname"

  python2 setup.py build
}

package() {
  cd "$pkgname"

  python2 setup.py install --root="$pkgdir" --skip-driver --optimize=1

  install -Dm 644 drivers/linux/dkms.conf \
    "$pkgdir/usr/src/$pkgname/dkms.conf"
  cp -a drivers/linux/* "$pkgdir/usr/src/$pkgname/"
  install -Dm 644 -t "$pkgdir/usr/share/doc/$pkgname/" README.md \
    chipsec-manual.pdf
  install -Dm 644 -t "$pkgdir/usr/share/licenses/$pkgname/" LICENSE COPYING
}

