# This file is part of BlackArch Linux ( https://www.blackarch.org/ ).
# See COPYING for license details.

pkgname=chipsec
pkgver=1070.7966175
pkgrel=3
epoch=4
pkgdesc='Platform Security Assessment Framework.'
groups=('blackarch' 'blackarch-hardware' 'blackarch-binary' 'blackarch-forensic'
        'blackarch-scanner' 'blackarch-fuzzer')
arch=('x86_64' 'aarch64')
url='https://github.com/chipsec/chipsec'
license=('GPL2')
depends=('dkms' 'libelf' 'linux-headers' 'nasm' 'python' 'python-lxml')
makedepends=('git' 'python-setuptools')
source=("git+https://github.com/chipsec/$pkgname.git"
        'Makefile.patch'
        '1095-kernel-5.10.patch')
sha512sums=('SKIP'
            'b3bd500b37871e8c0c9132613fd9254014c5288b24d7e88acacc695dbb3ff2d384c72186536e2fe5225c31b438f5d08fad991503c9335021cf03c3ed8c29e85f'
            'a8f9725f5c965a28c5ad92f163be18f3f58a69a7bd5f8a4cc247196b6a511adb86397ca03aeec2910b543afd0a027d3045b635c9149da98ab1b13bdbe3e76796')

pkgver() {
  cd $pkgname

  echo $(git rev-list --count HEAD).$(git rev-parse --short HEAD)
}

prepare() {
  cd $pkgname

  patch -Np1 < "$srcdir/1095-kernel-5.10.patch"

  cd "drivers/linux"
  patch -Np0 < "$srcdir/Makefile.patch"
}

build() {
  cd $pkgname

  python setup.py build
}

package() {
  cd $pkgname

  python setup.py install --prefix=/usr --root="$pkgdir" -O1 --skip-build \
    --skip-driver

  install -Dm 644 drivers/linux/dkms.conf \
    "$pkgdir/usr/src/$pkgname-$pkgver/dkms.conf"
  cp -a --no-preserve=ownership drivers/linux/* \
    "$pkgdir/usr/src/$pkgname-$pkgver/"

  install -Dm 644 -t "$pkgdir/usr/share/doc/$pkgname/" README.md \
    chipsec-manual.pdf
  install -Dm 644 -t "$pkgdir/usr/share/licenses/$pkgname/" LICENSE COPYING

  rm -f "$pkgdir/usr/chipsec-manual.pdf"

  for i in "$pkgdir/usr/bin/"*; do
    mv $i "$(echo $i | tr -s '_' '-')"
  done
}

