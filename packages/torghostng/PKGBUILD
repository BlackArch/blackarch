# This file is part of BlackArch Linux ( https://www.blackarch.org/ ).
# See COPYING for license details.

pkgname=torghostng
_pkgname=TorghostNG
pkgver=119.348f639
pkgrel=1
pkgdesc='TorghostNG is a tool that make all your internet traffic anonymized through Tor network.'
url='https://github.com/GitHackTools/TorghostNG'
arch=('any')
groups=('blackarch' 'blackarch-proxies')
license=('GPL3')
depends=('python' 'tor' 'macchanger' 'privoxy' 'curl' 'net-tools')
makedepends=('git')
source=('git+https://github.com/GitHackTools/TorghostNG.git')
sha512sums=('SKIP')

pkgver(){
  cd $_pkgname
  git describe --long --tags | sed 's/\([^-]*-g\)/r\1/;s/-/./g'
}

package(){
  cd $_pkgname

  install -dm 755 "$pkgdir/usr/bin/"
  install -dm 755 "$pkgdir/usr/share/$pkgname"

  install -Dm 644 -t "$pkgdir/usr/share/doc/$pkgname/" *.md

  rm LICENSE *.md

  cp -a * "$pkgdir/usr/share/$pkgname"
  cat > "$pkgdir/usr/bin/$pkgname" << EOF
#!/bin/sh
cd /usr/share/$pkgname
exec python $pkgname.py "\$@"
EOF
  chmod +x "$pkgdir/usr/bin/$pkgname"
}
