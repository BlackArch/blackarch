# This file is part of BlackArch Linux ( https://www.blackarch.org/ ).
# See COPYING for license details.

pkgbase=python-pypdf2
pkgname=('python2-pypdf2' 'python-pypdf2')
_pkgname=PyPDF2
pkgver=1.27.12
pkgrel=1
pkgdesc='PDF toolkit.'
arch=('any')
url='http://mstamy2.github.com/PyPDF2'
license=('BSD')
makedepends=('python2-setuptools' 'python-setuptools')
source=("https://files.pythonhosted.org/packages/03/ff/d27c15e86f7b39b53d4f33fec4d395cc3b82bd442f863cbe5c5c60ecdb8e/PyPDF2-$pkgver.tar.gz")
sha512sums=('ef28c2caa20c16c0d1fb50ef7317e2a6daef3a9b9c9f08d5349b73837a0e9bd19e205af809845840c6f7af552192a6e263bf552f55093d67ddc82e1e71edf27e')

prepare() {
  cp -a "$_pkgname-$pkgver"{,-2}
}

build() {
  cd "$_pkgname-$pkgver-2"

  python2 setup.py build

  cd "$srcdir/$_pkgname-$pkgver"

  python setup.py build
}

package_python2-pypdf2() {
  depends=('python2')

  cd "$_pkgname-$pkgver-2"

  python2 setup.py install --prefix=/usr --root="$pkgdir" --optimize=1 \
    --skip-build
}

package_python-pypdf2() {
  depends=('python')

  cd "$_pkgname-$pkgver"

  python setup.py install --prefix=/usr --root="$pkgdir" --optimize=1 \
    --skip-build
}

