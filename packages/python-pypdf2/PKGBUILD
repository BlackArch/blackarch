# This file is part of BlackArch Linux ( https://www.blackarch.org/ ).
# See COPYING for license details.

pkgname=python-pypdf2
_pkgname=PyPDF2
pkgver=3.0.1
pkgrel=2
epoch=1
pkgdesc='A pure-python PDF library capable of splitting, merging, cropping, and transforming PDF files.'
arch=('any')
url='https://pypi.org/project/PyPDF2/#files'
license=('BSD-3-Clause')
depends=('python' 'python-typing_extensions')
makedepends=('python-build' 'python-installer' 'python-setuptools'
             'python-wheel' 'python-flit-core')
options=(!emptydirs)
source=("https://files.pythonhosted.org/packages/source/${_pkgname::1}/$_pkgname/$_pkgname-$pkgver.tar.gz")
sha512sums=('8bca23a7aef28ebb724d1a2579a6d2fa7e8ae0530b6410ef54d88b46674aab726c37c2407107c3243ad52a7ebed4e19fdad01b4124145fc8fce12cb3b141d008')

build() {
  cd "$_pkgname-$pkgver"

  python -m build --wheel --no-isolation
}

package() {
  cd "$_pkgname-$pkgver"

  python -m installer --destdir="$pkgdir" dist/*.whl
}

