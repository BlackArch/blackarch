# This file is part of BlackArch Linux ( https://www.blackarch.org/ ).
# See COPYING for license details.

pkgbase=python-pypdf2
pkgname=('python2-pypdf2' 'python-pypdf2')
_pkgname=PyPDF2
pkgver=1.27.8
pkgrel=1
pkgdesc='PDF toolkit.'
arch=('any')
url='http://mstamy2.github.com/PyPDF2'
license=('BSD')
makedepends=('python2-setuptools' 'python-setuptools')
source=("https://files.pythonhosted.org/packages/07/f0/671247a98cf0b357c1b8ac3f857c0ad3434ca202c29c1d568c8a29e65478/PyPDF2-$pkgver.tar.gz")
sha512sums=('5e9bd5eb9462711ed751e4b9d1a4d3aba711ce5cd1b2ebbf1a9cec2dc6ac7f71948a7822c6f98190d9a317916dd80dedd99bfe278551a7da1db9430b580b6158')

prepare() {
  cp -a "$_pkgname-$pkgver"{,-2}
}

build() {
  cd "$_pkgname-$pkgver-2"

  python2 setup.py build

  cd "$srcdir/$_pkgname-$pkgver"

  python setup.py build
}

package_python2-pypdf2() {
  depends=('python2')

  cd "$_pkgname-$pkgver-2"

  python2 setup.py install --prefix=/usr --root="$pkgdir" --optimize=1 \
    --skip-build
}

package_python-pypdf2() {
  depends=('python')

  cd "$_pkgname-$pkgver"

  python setup.py install --prefix=/usr --root="$pkgdir" --optimize=1 \
    --skip-build
}

