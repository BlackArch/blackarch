# This file is part of BlackArch Linux ( https://www.blackarch.org/ ).
# See COPYING for license details.

pkgbase=python-pypdf2
pkgname=('python2-pypdf2' 'python-pypdf2')
_pkgname=PyPDF2
pkgver=2.0.0
pkgrel=1
pkgdesc='PDF toolkit.'
arch=('any')
url='http://mstamy2.github.com/PyPDF2'
license=('BSD')
makedepends=('python2-setuptools' 'python-setuptools')
source=("https://files.pythonhosted.org/packages/40/27/7ac08d83b15c391c3f1e9644bfacaf4e45aef670902af2235eb92b0bf2df/PyPDF2-$pkgver.tar.gz")
sha512sums=('99c19e685093bf3d49606e2a0aaa7b8519472122849111e66678ea50ae5b87f38c76c40b24bfadd98bb9a29b835d6f409a7fc0f48f952c865284f1dfb8e39e19')

prepare() {
  cp -a "$_pkgname-$pkgver"{,-2}
}

build() {
  cd "$_pkgname-$pkgver-2"

  python2 setup.py build

  cd "$srcdir/$_pkgname-$pkgver"

  python setup.py build
}

package_python2-pypdf2() {
  depends=('python2')

  cd "$_pkgname-$pkgver-2"

  python2 setup.py install --prefix=/usr --root="$pkgdir" --optimize=1 \
    --skip-build
}

package_python-pypdf2() {
  depends=('python')

  cd "$_pkgname-$pkgver"

  python setup.py install --prefix=/usr --root="$pkgdir" --optimize=1 \
    --skip-build
}

