# This file is part of BlackArch Linux ( https://www.blackarch.org/ ).
# See COPYING for license details.

pkgbase=python-pypdf2
pkgname=('python2-pypdf2' 'python-pypdf2')
_pkgname=PyPDF2
pkgver=2.10.2
pkgrel=1
pkgdesc='PDF toolkit.'
arch=('any')
url='http://mstamy2.github.com/PyPDF2'
license=('BSD')
makedepends=('python2-setuptools' 'python-setuptools')
source=("https://files.pythonhosted.org/packages/cc/a9/e011f1d3af047e819c63b5e914e3853ac1d9099b2c1e12af506e701e0d67/PyPDF2-$pkgver.tar.gz")
sha512sums=('90ef27a78995ec82cf73f84cb4681fc0510fad6462cc12f66e8e86331ba802a4afb26bb011769f924fafe3df43dfaa9c1e1156b31f938206db1f182b1047a4c3')

prepare() {
  cp -a "$_pkgname-$pkgver"{,-2}
}

build() {
  cd "$_pkgname-$pkgver-2"

  python2 setup.py build

  cd "$srcdir/$_pkgname-$pkgver"

  python setup.py build
}

package_python2-pypdf2() {
  depends=('python2')

  cd "$_pkgname-$pkgver-2"

  python2 setup.py install --prefix=/usr --root="$pkgdir" --optimize=1 \
    --skip-build
}

package_python-pypdf2() {
  depends=('python')

  cd "$_pkgname-$pkgver"

  python setup.py install --prefix=/usr --root="$pkgdir" --optimize=1 \
    --skip-build
}

