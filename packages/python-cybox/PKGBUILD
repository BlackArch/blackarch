# This file is part of BlackArch Linux ( https://www.blackarch.org/ ).
# See COPYING for license details.

pkgbase=python-cybox
pkgname=('python2-cybox' 'python-cybox')
_pkgname=python-cybox
pkgver=2.1.0.17
pkgrel=3
epoch=1
pkgdesc='A Python library for parsing and generating CybOX 2.1.0 content.'
arch=('any')
url='https://github.com/CybOXProject/python-cybox'
makedepends=('python2-setuptools' 'python-setuptools')
license=('BSD')
source=("https://github.com/CybOXProject/python-cybox/archive/v$pkgver.tar.gz")
sha512sums=('e9b6cee6bcd7703c758a55a76424882b8d7e14fe4cdcfa8bdfe64e3613a9b02a26714f529e8a42d99c665f2aec7b5adedfe81467535a44c6bbbf1995ea9c7a9d')

prepare() {
  cp -a "$_pkgname-$pkgver"{,-2}
}

build() {
  cd "$_pkgname-$pkgver-2"

  python2 setup.py build

  cd "$srcdir/$_pkgname-$pkgver"

  python setup.py build
}

package_python2-cybox() {
  depends=('python2')

  cd "$_pkgname-$pkgver-2"

  python2 setup.py install --prefix=/usr --root="$pkgdir" --optimize=1 \
    --skip-build
}

package_python-cybox() {
  depends=('python')

  cd "$_pkgname-$pkgver"

  python setup.py install --prefix=/usr --root="$pkgdir" --optimize=1 \
    --skip-build
}

