# This file is part of BlackArch Linux ( http://blackarch.org ).
# See COPYING for license details.

pkgname=c5scan
pkgver=29.33a500c
pkgrel=2
pkgdesc='Vulnerability scanner and information gatherer for the Concrete5 CMS.'
groups=('blackarch' 'blackarch-webapp' 'blackarch-scan' 'blackarch-recon')
arch=('any')
url='https://github.com/auraltension/c5scan'
license=('GPL2')
depends=('python2' 'python2-argparse' 'python2-lxml' 'python2-requests')
makedepends=('git')
source=('git+https://github.com/auraltension/c5scan')
sha512sums=('SKIP')

pkgver() {
  cd "$srcdir/c5scan"

  echo $(git rev-list --count HEAD).$(git rev-parse --short HEAD)
}

prepare() {
  cd "$srcdir/c5scan"

  sed -i "s|bin/env python|bin/env python2|" c5scan.py
}

package() {
  cd "$srcdir/c5scan"

  install -Dm755 c5scan.py "$pkgdir/usr/bin/c5scan"
  install -Dm644 -t "$pkgdir/usr/share/doc/c5scan/" README.md
  install -Dm644 LICENSE "$pkgdir/usr/share/licenses/c5scan/LICENSE"
}
