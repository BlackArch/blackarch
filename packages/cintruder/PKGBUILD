# This file is part of BlackArch Linux ( http://blackarch.org ).
# See COPYING for license details.

pkgname='cintruder'
pkgver=6.a628c62
pkgrel=1
groups=('blackarch' 'blackarch-cracker' 'blackarch-webapp')
pkgdesc='An automatic pentesting tool to bypass captchas.'
arch=('any')
url='https://github.com/epsylon/cintruder'
license=('GPL3')
depends=('python2' 'python2-pycurl' 'python2-lxml' 'python2-pillow')
makedepends=('git')
source=('git+https://github.com/epsylon/cintruder.git')
sha1sums=('SKIP')

pkgver() {
  cd "$srcdir/cintruder"

  echo $(git rev-list --count HEAD).$(git rev-parse --short HEAD)
}

prepare() {
  cd "$srcdir/cintruder"

  sed -i 's|/usr/bin/env python|/usr/bin/env python2|' cintruder
}

package() {
  cd "$srcdir/cintruder"

  mkdir -p "$pkgdir/usr/bin"
  mkdir -p "$pkgdir/usr/share/cintruder"
  mkdir -p "$pkgdir/usr/share/doc/cintruder"

  install -Dm644 docs/LICENSE "$pkgdir/usr/share/licenses/cintruder/LICENSE"
  rm docs/LICENSE

  install -Dm644 -t "$pkgdir/usr/share/doc/cintruder" docs/*

  rm -rf docs/

  cp -a * "$pkgdir/usr/share/cintruder"

  cat > "$pkgdir/usr/bin/cintruder" << EOF
#!/bin/sh
cd /usr/share/cintruder
exec python2 cintruder "\${@}"
EOF

  chmod +x "$pkgdir/usr/bin/cintruder"
}
