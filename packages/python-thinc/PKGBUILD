# This file is part of BlackArch Linux ( https://www.blackarch.org/ ).
# See COPYING for license details.

pkgname=python-thinc
_pkgname=thinc
pkgver=8.0.7
pkgrel=1
pkgdesc='Practical Machine Learning for NLP.'
arch=('x86_64' 'aarch64')
url='https://pypi.org/project/thinc/'
license=('MIT')
depends=('python' 'python-cymem' 'python-blis' 'python-catalogue'
         'python-hypothesis' 'python-mock' 'python-murmurhash' 'python-numpy'
         'python-pathlib' 'python-plac' 'python-preshed' 'python-pydantic'
         'python-six' 'python-srsly' 'python-tqdm' 'python-wasabi')
makedepends=('python-setuptools')
source=("https://files.pythonhosted.org/packages/source/${_pkgname::1}/$_pkgname/$_pkgname-$pkgver.tar.gz")
sha512sums=('56337e99e9e71f6f5e6f66b57f5d5b017e8223e3e6462c54d13fe7a422ff81cf8941b1f84ad2baa431f9f59ea9400593364370bf294f15842f8968560048806e')

build() {
  cd "$_pkgname-$pkgver"

  python setup.py build
}

package() {
  cd "$_pkgname-$pkgver"

  python setup.py install --root="$pkgdir" --prefix=/usr -O1 --skip-build

  install -Dm 644 LICENSE "$pkgdir/usr/share/licenses/$pkgname/LICENSE"
}

