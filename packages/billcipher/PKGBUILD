# This file is part of BlackArch Linux ( http://blackarch.org ).
# See COPYING for license details.

pkgname=billcipher
pkgver=26.e8c5ea6
pkgrel=1
pkgdesc='Information Gathering tool for a Website or IP address.'
groups=('blackarch' 'blackarch-recon' 'blackarch-scanner')
arch=('any')
url='https://github.com/GitHackTools/BillCipher'
license=('GPL3')
depends=('python' 'python-argparse' 'python-dnspython' 'python-requests'
         'python-urllib3')
makedepends=('git')
source=('git+https://github.com/GitHackTools/BillCipher.git')
sha512sums=('SKIP')

pkgver() {
  cd "$srcdir/BillCipher"

  echo $(git rev-list --count HEAD).$(git rev-parse --short HEAD)
}

package() {
  cd "$srcdir/BillCipher"

  install -dm 755 "$pkgdir/usr/bin"
  install -dm 755 "$pkgdir/usr/share/billcipher"

  install -Dm644 -t "$pkgdir/usr/share/doc/billcipher/" README.md
  install -Dm644 LICENSE "$pkgdir/usr/share/licenses/billcipher/LICENSE"

  rm README.md LICENSE

  cp -a * "$pkgdir/usr/share/billcipher/"

  cat > "$pkgdir/usr/bin/billcipher" << EOF
#!/bin/sh
cd /usr/share/billcipher
exec python billcipher.py "\${@}"
EOF

  chmod a+x "$pkgdir/usr/bin/billcipher"
}
