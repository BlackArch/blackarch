# This file is part of BlackArch Linux ( https://www.blackarch.org/ ).
# See COPYING for license details.

pkgname=billcipher
pkgver=26.e8c5ea6
pkgrel=1
pkgdesc='Information Gathering tool for a Website or IP address.'
groups=('blackarch' 'blackarch-recon' 'blackarch-scanner')
arch=('any')
url='https://github.com/GitHackTools/BillCipher'
license=('GPL3')
depends=('python' 'python-argparse' 'python-dnspython' 'python-requests'
         'python-urllib3')
makedepends=('git')
source=('git+https://github.com/GitHackTools/BillCipher.git')
sha512sums=('SKIP')

pkgver() {
  cd BillCipher

  echo $(git rev-list --count HEAD).$(git rev-parse --short HEAD)
}

package() {
  cd BillCipher

  install -dm 755 "$pkgdir/usr/bin"
  install -dm 755 "$pkgdir/usr/share/$pkgname"

  install -Dm 644 -t "$pkgdir/usr/share/doc/$pkgname/" README.md
  install -Dm 644 LICENSE "$pkgdir/usr/share/licenses/$pkgname/LICENSE"

  rm README.md LICENSE

  cp -a * "$pkgdir/usr/share/$pkgname/"

  cat > "$pkgdir/usr/bin/$pkgname" << EOF
#!/bin/sh
cd /usr/share/$pkgname
exec python $pkgname.py "\$@"
EOF

  chmod +x "$pkgdir/usr/bin/$pkgname"
}

