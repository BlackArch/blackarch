# This file is part of BlackArch Linux ( https://www.blackarch.org/ ).
# See COPYING for license details.

pkgname=python-libsast
_pkgname=libsast
pkgver=1.4.6
pkgrel=1
pkgdesc='A generic SAST core built on top of semgrep and regex.'
arch=('any')
url='https://pypi.org/project/libsast/#files'
license=('LGPL')
depends=('python' 'python-requests' 'python-yaml' 'python-pyaml'
         'python-semgrep')
makedepends=('python-setuptools')
source=("https://files.pythonhosted.org/packages/7c/bd/578a42b52afd5a5dc7b5585256264b7dd97f3d5a03ab8e6a4278dabbccc4/$_pkgname-$pkgver.tar.gz")
sha512sums=('70430ce9d75e2a415ddd2f38b658e443a25deb8816fb9728c31b2c5b4dcc77bf7725d748f8d03f18c810195880309e1e659e78cc61bd849a0fdac7e972e32bfb')

prepare() {
  cd "$_pkgname-$pkgver"

  sed -i 's|semgrep==0.53.0|semgrep|g' setup.py
}

build() {
  cd "$_pkgname-$pkgver"

  python setup.py build
}

package() {
  cd "$_pkgname-$pkgver"

  python setup.py install --root="$pkgdir" --prefix=/usr -O1 --skip-build
}

