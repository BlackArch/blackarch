# This file is part of BlackArch Linux ( https://www.blackarch.org/ ).
# See COPYING for license details.

pkgname=python-libsast
_pkgname=libsast
pkgver=1.5.2
pkgrel=2
pkgdesc='A generic SAST core built on top of semgrep and regex.'
arch=('any')
url='https://pypi.org/project/libsast/#files'
license=('LGPL')
depends=('python' 'python-requests' 'python-yaml' 'python-pyaml' 'semgrep')
makedepends=('python-setuptools')
source=("https://files.pythonhosted.org/packages/source/${_pkgname::1}/$_pkgname/$_pkgname-$pkgver.tar.gz")
sha512sums=('86643d89aefca2a0e80f7516e3270c05e32ddba10a649746cb0fb868a3a3f444e3359097061648b8efc99800a7f48ead9954a8effbaa0258624b3e3d8c7fa809')

prepare() {
  cd "$_pkgname-$pkgver"

  sed -i 's|semgrep==0.80.0|semgrep|g' setup.py
}

build() {
  cd "$_pkgname-$pkgver"

  python setup.py build
}

package() {
  cd "$_pkgname-$pkgver"

  python setup.py install --root="$pkgdir" --prefix=/usr -O1 --skip-build
}

