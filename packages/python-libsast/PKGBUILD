# This file is part of BlackArch Linux ( https://www.blackarch.org/ ).
# See COPYING for license details.

pkgname=python-libsast
_pkgname=libsast
pkgver=2.0.3
pkgrel=1
pkgdesc='A generic SAST core built on top of semgrep and regex.'
arch=('any')
url='https://pypi.org/project/libsast/#files'
license=('LGPL')
depends=('python' 'python-requests' 'python-yaml' 'python-pyaml' 'semgrep')
makedepends=('python-setuptools')
source=("https://files.pythonhosted.org/packages/source/${_pkgname::1}/$_pkgname/$_pkgname-$pkgver.tar.gz")
sha512sums=('66c7580b413c92120e27f0c96c9483008ae18fafe16b733ad3c44adfa89e91264f10c3595425bc70339b399aa5ab847b3dd924ffae8e1323b2e62e8c47d564b2')

prepare() {
  cd "$_pkgname-$pkgver"

  sed -i 's|semgrep==0.80.0|semgrep|g' setup.py
}

build() {
  cd "$_pkgname-$pkgver"

  python setup.py build
}

package() {
  cd "$_pkgname-$pkgver"

  python setup.py install --root="$pkgdir" --prefix=/usr -O1 --skip-build
}

