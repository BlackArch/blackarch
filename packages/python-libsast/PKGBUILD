# This file is part of BlackArch Linux ( https://www.blackarch.org/ ).
# See COPYING for license details.

pkgname=python-libsast
_pkgname=libsast
pkgver=1.4.5
pkgrel=3
pkgdesc='A generic SAST core built on top of semgrep and regex.'
arch=('any')
url='https://pypi.org/project/libsast/#files'
license=('LGPL')
depends=('python' 'python-requests' 'python-yaml' 'python-pyaml'
         'python-semgrep')
makedepends=('python-setuptools')
source=("https://files.pythonhosted.org/packages/11/4c/6e0938661516ba2939ca2d5710d9386dc1f51b96bfc97ea681a7b12d56cd/$_pkgname-$pkgver.tar.gz")
sha512sums=('23d4b60883ead1002d54dba22fb50d4fc483d811abf81472f10d5eda7948ec583b3deff224d64f3c6db2e12a0e5fa0b836c8726857600a93b06bd5d05a112385')

prepare() {
  cd "$_pkgname-$pkgver"

  sed -i 's|semgrep==0.53.0|semgrep|g' setup.py
}

build() {
  cd "$_pkgname-$pkgver"

  python setup.py build
}

package() {
  cd "$_pkgname-$pkgver"

  python setup.py install --root="$pkgdir" --prefix=/usr -O1 --skip-build
}

