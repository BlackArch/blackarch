# This file is part of BlackArch Linux ( https://www.blackarch.org/ ).
# See COPYING for license details.

pkgname=pfring-kmod
_name=PF_RING
pkgver=7.4.0
pkgrel=1
epoch=1
pkgdesc='Linux kernel module for packet capture at wirespeed.'
url='https://github.com/ntop/PF_RING'
arch=('x86_64' 'i686')
license=('LGPL')
depends=('numactl')
makedepends=('binutils' 'bison' 'flex' 'git' 'linux-headers' 'make')
source=("https://github.com/ntop/$_name/archive/$pkgver.tar.gz"
        'pf_ring.c.patch')
sha512sums=('45d8c995389dcdc606471fbde511e2f2483364295dfe1ab4f01af1ce711934838c06ee9cbc74e903d94a95109f92f0e624199b209ff0cbd966585b2db73916aa'
            '2fe2c96ab1c8c883796597515ddda64066b7fc36d8f50d5a71191faee1c9af8765c539ba2a59dfe79a74482ea23287075dd00c01d1b82cfe5a3aafa28f8a6c4a')

prepare() {
  cd "$_name-$pkgver/kernel"

  patch < ../../pf_ring.c.patch
}

build() {
  cd "$_name-$pkgver/kernel"

  ./configure --prefix=/usr
  sed -i "s/INSTDIR\ \:=\ \$(DESTDIR)/INSTDIR := \$(DESTDIR)\/usr/g" Makefile

  make
}

package() {
  cd "$_name-$pkgver/kernel"

  install -dm 755 "$pkgdir/usr/include/linux"

  make DESTDIR="$pkgdir" install
}

