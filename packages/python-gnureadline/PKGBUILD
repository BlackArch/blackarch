# This file is part of BlackArch Linux ( https://www.blackarch.org/ ).
# See COPYING for license details.

pkgbase=python-gnureadline
pkgname=('python2-gnureadline' 'python-gnureadline')
_pkgname=gnureadline
pkgver=8.0.0
pkgrel=3
pkgdesc='The standard Python readline extension statically linked against the GNU readline library.'
arch=('any')
url='https://pypi.org/project/gnureadline/#files'
license=('GPL3')
makedepends=('fakeroot' 'gcc' 'make' 'patch'
             'python2-setuptools' 'python-setuptools')
source=("https://files.pythonhosted.org/packages/b2/e9/0f3c62faa8e471541d77068f440b4f5df8f396a3760fccf1b88697e282a8/gnureadline-8.0.0.tar.gz")
sha512sums=('a28b458bf72eafd70bf952df11f99dc4f80a102074e61c8fcaa123c69c66bce4688b84846c0ddd979cf19efaeb7d898712de416b0c08f6b1275b9a92fe0fc02a')

prepare() {
  cp -a "$_pkgname-$pkgver"{,-2}
}

build() {
  cd "$_pkgname-$pkgver-2"

  python2 setup.py build

  cd "$srcdir/$_pkgname-$pkgver"

  python setup.py build
}

package_python2-gnureadline() {
  depends=('python2')

  cd "$_pkgname-$pkgver-2"

  python2 setup.py install --prefix=/usr --root="$pkgdir" --optimize=1 \
    --skip-build
}

package_python-gnureadline() {
  depends=('python')

  cd "$_pkgname-$pkgver"

  python setup.py install --prefix=/usr --root="$pkgdir" --optimize=1 \
    --skip-build
}

