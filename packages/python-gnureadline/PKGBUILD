# This file is part of BlackArch Linux ( https://www.blackarch.org/ ).
# See COPYING for license details.

pkgbase=python-gnureadline
pkgname=('python2-gnureadline' 'python-gnureadline')
_pkgname=gnureadline
pkgver=8.1.2
pkgrel=1
pkgdesc='The standard Python readline extension statically linked against the GNU readline library.'
arch=('any')
url='https://pypi.org/project/gnureadline/#files'
license=('GPL3')
makedepends=('fakeroot' 'gcc' 'make' 'patch'
             'python2-setuptools' 'python-setuptools')
source=("https://files.pythonhosted.org/packages/source/${_pkgname::1}/$_pkgname/$_pkgname-$pkgver.tar.gz")
sha512sums=('85b29a51431e6f7113897a7ffed45574dd5d1b3cd2761d27c4701466214e093c90e490cfca065aa047d66ecbf2899b88c30483db5504e81b2a8e18ca4f80cbe8')

prepare() {
  cp -a "$_pkgname-$pkgver"{,-2}
}

build() {
  cd "$_pkgname-$pkgver-2"

  python2 setup.py build

  cd "$srcdir/$_pkgname-$pkgver"

  python setup.py build
}

package_python2-gnureadline() {
  depends=('python2')

  cd "$_pkgname-$pkgver-2"

  python2 setup.py install --prefix=/usr --root="$pkgdir" --optimize=1 \
    --skip-build
}

package_python-gnureadline() {
  depends=('python')

  cd "$_pkgname-$pkgver"

  python setup.py install --prefix=/usr --root="$pkgdir" --optimize=1 \
    --skip-build
}

