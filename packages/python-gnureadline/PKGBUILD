# This file is part of BlackArch Linux ( https://www.blackarch.org/ ).
# See COPYING for license details.

pkgname=python-gnureadline
_pkgname=${pkgname#python-}
pkgver=8.3.3
pkgrel=1
pkgdesc='The standard Python readline extension statically linked against the GNU readline library.'
arch=('any')
url='https://pypi.org/project/gnureadline/#files'
license=('GPL3')
depends=('python')
makedepends=('python-build' 'python-pip')
options=(!emptydirs)
source=("https://files.pythonhosted.org/packages/source/${_pkgname::1}/$_pkgname/$_pkgname-$pkgver.tar.gz")
sha512sums=('7ab43c912b332f570de2e4f657c940aba53aab4a2f4847f368a5f340dd72408bee63434d0269d5dea13354ed166ffe069ba2254fbbd6ce344070f44da93ba623')

build() {
  cd "$_pkgname-$pkgver"

  python -m build --wheel --outdir="$startdir/dist"
}

package() {
  cd "$_pkgname-$pkgver"

  pip install \
    --verbose \
    --disable-pip-version-check \
    --no-warn-script-location \
    --ignore-installed \
    --no-compile \
    --no-deps \
    --root="$pkgdir" \
    --prefix=/usr \
    --no-index \
    --find-links="file://$startdir/dist" \
    $_pkgname
}

