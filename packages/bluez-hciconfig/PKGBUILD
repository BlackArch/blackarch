# This file is part of BlackArch Linux ( https://www.blackarch.org/ ).
# See COPYING for license details.
#
# Old Maintainer: Sergej Pupykin <arch+pub@sergej.pp.ru>

pkgname=bluez-hciconfig
_pkgname=bluez
pkgver=5.59
pkgrel=1
pkgdesc='Deprecated hciconfig tool from bluez.'
url='http://www.bluez.org/'
arch=('x86_64' 'aarch64')
license=('GPL2')
depends=('bluez' 'bluez-utils' 'bluez-libs' 'bluez-tools')
source=("https://www.kernel.org/pub/linux/bluetooth/bluez-$pkgver.tar.xz")
sha512sums=('506ce28459a82614e5f6c55235b84e5780a9bfa821904fd40ee2928a503978248368069c57a283db9dd81f48d78f45e00d95a7c52d1538d3f5c858a29424304f')

build() {
  cd "$_pkgname-$pkgver/tools"

  gcc hciconfig.c parser/{csr,parser,lmp}.c ../src/textfile.c -lbluetooth -o \
    hciconfig -DVERSION=\"$pkgver\" -I..
}

package() {
  cd "$_pkgname-$pkgver/tools"

  install -Dm 755 hciconfig "$pkgdir/usr/bin/hciconfig"
  install -Dm 755 hciconfig.1 "$pkgdir/usr/share/man/man1/hciconfig.1"
}

