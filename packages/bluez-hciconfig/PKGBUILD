# This file is part of BlackArch Linux ( https://www.blackarch.org/ ).
# See COPYING for license details.
#
# Old Maintainer: Sergej Pupykin <arch+pub@sergej.pp.ru>

pkgname=bluez-hciconfig
_pkgname=bluez
pkgver=5.86
pkgrel=1
pkgdesc='Deprecated hciconfig tool from bluez.'
url='http://www.bluez.org/'
arch=('x86_64' 'aarch64')
license=('GPL2')
depends=('bluez' 'bluez-utils' 'bluez-libs' 'bluez-tools')
conflicts=('bluez-deprecated-tools')
source=("https://www.kernel.org/pub/linux/bluetooth/bluez-$pkgver.tar.xz")
sha512sums=('d3268be43ad869dabd9d9eabc7a0da699837bb5432ecf61e1756e03f9b867a1d989299db1493bd349d73b495d736f55d7518fbb9ee664de49e5d1cf7c2156df7')

build() {
  cd "$_pkgname-$pkgver/tools"

  gcc hciconfig.c parser/{csr,parser,lmp}.c -lbluetooth -o \
    hciconfig -DVERSION=\"$pkgver\" -I..

  #gcc hciconfig.c parser/{csr,parser,lmp}.c ../src/textfile.c -lbluetooth -o \
  #  hciconfig -DVERSION=\"$pkgver\" -I..
}

package() {
  cd "$_pkgname-$pkgver"

  install -Dm 755 tools/hciconfig "$pkgdir/usr/bin/hciconfig"
  install -Dm 755 doc/hciconfig.1 "$pkgdir/usr/share/man/man1/hciconfig.1"
}

