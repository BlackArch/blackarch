# This file is part of BlackArch Linux ( https://www.blackarch.org/ ).
# See COPYING for license details.

pkgname=buster
pkgver=92.131437e
pkgrel=13
pkgdesc='Find emails of a person and return info associated with them.'
arch=('any')
groups=('blackarch' 'blackarch-social' 'blackarch-recon')
url='https://github.com/sham00n/buster'
license=('GPL-3.0-or-later')
depends=('python' 'python-requests' 'python-beautifulsoup4' 'python-yaml'
         'python-lxml' 'python-grequests' 'python-gevent' 'twint' 'cython'
         'python-certifi')
makedepends=('git' 'python-setuptools' 'python-pip' 'python-build'
             'python-wheel')
source=("git+https://github.com/sham00n/$pkgname.git")
sha512sums=('SKIP')

pkgver() {
  cd $pkgname

  ( set -o pipefail
    git describe --long --tags --abbrev=7 2>/dev/null |
      sed 's/\([^-]*-g\)/r\1/;s/-/./g' ||
    printf "%s.%s" "$(git rev-list --count HEAD)" \
      "$(git rev-parse --short=7 HEAD)"
  )
}

build() {
  cd $pkgname

  python setup.py build
}

package() {
  cd $pkgname

  python setup.py install --root="$pkgdir" --prefix=/usr -O1 --skip-build

  install -Dm 644 -t "$pkgdir/usr/share/doc/$pkgname/" *.md
  install -Dm 644 LICENSE "$pkgdir/usr/share/licenses/$pkgname/LICENSE"
}

