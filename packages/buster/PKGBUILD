# This file is part of BlackArch Linux ( https://www.blackarch.org/ ).
# See COPYING for license details.

pkgname=buster
pkgver=92.131437e
pkgrel=4
pkgdesc='Find emails of a person and return info associated with them.'
groups=('blackarch' 'blackarch-social' 'blackarch-recon')
arch=('any')
url='https://github.com/sham00n/buster'
license=('GPL3')
depends=('python' 'python-requests' 'python-beautifulsoup4' 'python-yaml'
         'python-lxml' 'python-grequests' 'python-gevent' 'twint' 'cython'
         'python-dataclasses')
makedepends=('git' 'python-setuptools')
source=("git+https://github.com/sham00n/$pkgname.git")
sha512sums=('SKIP')

pkgver() {
  cd $pkgname

  echo $(git rev-list --count HEAD).$(git rev-parse --short HEAD)
}

build() {
  cd $pkgname

  python setup.py build
}

package() {
  cd $pkgname

  python setup.py install --root="$pkgdir" --prefix=/usr -O1 --skip-build

  install -Dm 644 -t "$pkgdir/usr/share/doc/$pkgname/" *.md
  install -Dm 644 LICENSE "$pkgdir/usr/share/licenses/$pkgname/LICENSE"
}

