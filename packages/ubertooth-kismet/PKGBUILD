# This file is part of BlackArch Linux ( http://blackarch.org ).
# See COPYING for license details.

pkgname='ubertooth-kismet'
pkgver='2012.10.R1'
_kismet_pkgver=2013-03-R1b
pkgrel=1
groups=('blackarch' 'blackarch-bluetooth')
pkgdesc="A kismet plugin for ubertooth."
url="http://sourceforge.net/projects/ubertooth/"
arch=('i686' 'x86_64' 'armv6h' 'armv7h')
license=('GPL')
depends=('kismet' 'ubertooth')
source=("http://downloads.sourceforge.net/project/ubertooth/ubertooth-${pkgver//./-}.tar.xz"
        "http://www.kismetwireless.net/code/kismet-$_kismet_pkgver.tar.gz")
sha1sums=('c865e639eaec12cdd225576f78a37d187c437de2'
          'c77b92bf5d589b79281271e96724ef96032a162b')

build() {
  cd "$srcdir/kismet-$_kismet_pkgver"

  ./configure --prefix=/usr \
              --sysconfdir=/etc \
              --localstatedir=/var

  cd "$srcdir/ubertooth-${pkgver//./-}/host/kismet/plugin-ubertooth"

  make "KIS_SRC_DIR=$srcdir/kismet-$_kismet_pkgver"
}

package() {
  cd "$srcdir/ubertooth-${pkgver//./-}/host/kismet/plugin-ubertooth"

  # Base directories.
  mkdir -p "$pkgdir/usr/lib"

  make "DESTDIR=$pkgdir/" LIBDIR=/usr/lib install
}
