# This file is part of BlackArch Linux ( https://www.blackarch.org/ ).
# See COPYING for license details.

pkgname=ubertooth-kismet
pkgver=2018.08.R1
_kismet_pkgver=2013-03-R1b
pkgrel=1
groups=('blackarch' 'blackarch-bluetooth')
pkgdesc='A kismet plugin for ubertooth.'
url='http://sourceforge.net/projects/ubertooth/'
arch=('x86_64' 'aarch64')
license=('GPL')
depends=('kismet' 'ubertooth')
source=("https://github.com/greatscottgadgets/ubertooth/archive/${pkgver//./-}.tar.gz"
        "http://www.kismetwireless.net/code/kismet-$_kismet_pkgver.tar.gz")
sha512sums=('59d801718d98fd3c8f08108f3cce715cb5f11b247e3ff13823146ee4f9393086abc0cfbe9182213e4acd562738ea4e429fe28b3a8b5a0b6842f8d3d6275037af'
            '9a6a04f1d6da5ba5aee17806e318b3a7b5ce72c4493e7e4d2f23ed33c4d4190f4bfde97070d58cbde4e4c7c548dc7d6c7cad13d0f15119e2b0f961cdd67d08fd')

build() {
  cd "$srcdir/kismet-$_kismet_pkgver"

  ./configure --prefix=/usr \
              --sysconfdir=/etc \
              --localstatedir=/var

  cd "$srcdir/ubertooth-${pkgver//./-}/host/kismet/plugin-ubertooth"

  make KIS_SRC_DIR="$srcdir/kismet-$_kismet_pkgver"
}

package() {
  cd "ubertooth-${pkgver//./-}/host/kismet/plugin-ubertooth"

  install -dm 755 "$pkgdir/usr/lib"

  make DESTDIR="$pkgdir" LIBDIR=/usr/lib install
}

