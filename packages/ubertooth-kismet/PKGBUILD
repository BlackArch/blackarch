# This file is part of BlackArch Linux ( http://blackarch.org ).
# See COPYING for license details.

pkgname=ubertooth-kismet
pkgver=2015.10.R1
_kismet_pkgver=2013-03-R1b
pkgrel=1
groups=('blackarch' 'blackarch-bluetooth')
pkgdesc='A kismet plugin for ubertooth.'
url='http://sourceforge.net/projects/ubertooth/'
arch=('x86_64' 'armv6h' 'armv7h' 'aarch64')
license=('GPL')
depends=('kismet' 'ubertooth')
source=("https://github.com/greatscottgadgets/ubertooth/archive/${pkgver//./-}.tar.gz"
        "http://www.kismetwireless.net/code/kismet-${_kismet_pkgver}.tar.gz")
sha512sums=('f0d8512136e79ee56010d17af4b0d227a7585822c08896fbaa22ba8e756d6cc561d9c45a54f84b964f4df98d3ce47f0136c6cdc19d077dcca807e189c1cfc05a'
            '9a6a04f1d6da5ba5aee17806e318b3a7b5ce72c4493e7e4d2f23ed33c4d4190f4bfde97070d58cbde4e4c7c548dc7d6c7cad13d0f15119e2b0f961cdd67d08fd')

build() {
  cd "$srcdir/kismet-$_kismet_pkgver"

  ./configure --prefix=/usr \
              --sysconfdir=/etc \
              --localstatedir=/var

  cd "$srcdir/ubertooth-${pkgver//./-}/host/kismet/plugin-ubertooth"

  make KIS_SRC_DIR="$srcdir/kismet-$_kismet_pkgver"
}

package() {
  cd "$srcdir/ubertooth-${pkgver//./-}/host/kismet/plugin-ubertooth"

  install -dm 755 "$pkgdir/usr/lib"

  make DESTDIR="$pkgdir/" LIBDIR=/usr/lib install
}
