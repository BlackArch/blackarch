# This file is part of BlackArch Linux ( https://www.blackarch.org/ ).
# See COPYING for license details.

pkgname=wxhexeditor
pkgver=733.f439d8f
pkgrel=4
pkgdesc='A free hex editor / disk editor for Linux, Windows and MacOSX.'
arch=('x86_64' 'aarch64')
groups=('blackarch' 'blackarch-binary' 'blackarch-misc')
url='http://wxhexeditor.sourceforge.net/'
license=('GPL')
depends=('wxwidgets-gtk3' 'mhash' 'python' 'python2' 'gcc-libs')
makedepends=('git' 'python' 'python2')
source=("$pkgname::git+https://github.com/EUA/wxHexEditor.git")
sha512sums=('SKIP')

pkgver() {
  cd $pkgname

  echo "$(git rev-list --count HEAD)"."$(git rev-parse --short HEAD)"
}

build() {
  cd $pkgname

  export CFLAGS="-O2 -Wall -Wno-error -Wno-implicit-function-declaration -std=gnu89"
  export CXXFLAGS="${CXXFLAGS} -O2 -Wall -Wno-error -Wno-implicit-function-declaration -std=gnu89"
  make -s WXCONFIG='/usr/bin/wx-config' CXXFLAGS="${CXXFLAGS/-fno-rtti/}"
}

package() {
  cd $pkgname

  make DESTDIR="$pkgdir" PREFIX="/usr" install

  install -Dm 644 -t "$pkgdir/usr/share/doc/$pkgname/" docs/*
  install -Dm 644 LICENSE "$pkgdir/usr/share/licenses/$pkgname/LICENSE"
}

