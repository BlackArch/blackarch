# This file is part of BlackArch Linux ( http://blackarch.org ).
# See COPYING for license details.

pkgname='wxhexeditor'
pkgver=637.5103931
pkgrel=1
pkgdesc='A free hex editor / disk editor for Linux, Windows and MacOSX.'
arch=('x86_64' 'armv6h' 'arvm7h')
groups=('blackarch' 'blackarch-binary' 'blackarch-misc')
url='http://wxhexeditor.sourceforge.net/'
license=('GPL')
depends=('wxgtk' 'mhash' 'python' 'python2')
makedepends=('git')
source=('git+https://github.com/EUA/wxHexEditor.git')
sha1sums=('SKIP')

pkgver() {
  cd "$srcdir/wxHexEditor"

  echo "$(git rev-list --count HEAD)"."$(git rev-parse --short HEAD)"
}

build() {
  cd "$srcdir/wxHexEditor"

  make CXXFLAGS="${CXXFLAGS/-fno-rtti/}"
}

package() {
  cd "$srcdir/wxHexEditor"

  make DESTDIR="$pkgdir" PREFIX="/usr" install

  install -Dm644 docs/Change.log "$pkgdir/usr/share/doc/wxhexeditor/Change.log"
  install -Dm644 docs/GPL.txt "$pkgdir/usr/share/licenses/wxhexeditor/GPL.txt"
}
