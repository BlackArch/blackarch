# This file is part of BlackArch Linux ( https://www.blackarch.org/ ).
# See COPYING for license details.

pkgbase=python-ajpy
pkgname=('python2-ajpy' 'python-ajpy')
_pkgname='ajpy'
pkgver=0.0.5
pkgrel=1
pkgdesc='AJP package crafting library.'
arch=('any')
url='https://pypi.org/project/ajpy/'
license=('LGPL')
makedepends=('python2-setuptools' 'python-setuptools')
source=("https://files.pythonhosted.org/packages/d0/51/cf7473848bfe8805e4e899a1df7472e90f79f83fe88ab766a5f6f1c4b2b9/$_pkgname-$pkgver.tar.gz")
sha512sums=('e2bd4ad7aeb2dc69a2aef14a83e9f55bff11a1c232892062d6694af31055ed1a2a9129804ae445cae391913541864bf37cdf0833eff555ad7af88e12fd81d57e')

prepare() {
  cp -a "$_pkgname-$pkgver"{,-2}
}

build() {
  cd "$_pkgname-$pkgver-2"

  python2 setup.py build

  cd "$srcdir/$_pkgname-$pkgver"

  python setup.py build
}

package_python2-ajpy() {
  depends=('python2')

  cd "$_pkgname-$pkgver-2"

  python2 setup.py install --prefix=/usr --root="$pkgdir" --optimize=1 \
    --skip-build
}

package_python-ajpy() {
  depends=('python')

  cd "$_pkgname-$pkgver"

  python setup.py install --prefix=/usr --root="$pkgdir" --optimize=1 \
    --skip-build
}

