# This file is part of BlackArch Linux ( https://www.blackarch.org/ ).
# See COPYING for license details.

pkgbase=python-ajpy
pkgname=('python2-ajpy' 'python-ajpy')
_pkgname='ajpy'
pkgver=0.0.4
pkgrel=4
pkgdesc='AJP package crafting library.'
arch=('any')
url='https://pypi.org/project/ajpy/'
license=('LGPL')
makedepends=('python2-setuptools' 'python-setuptools')
source=("https://files.pythonhosted.org/packages/12/dd/e641d8c0b3b14eed50122a3c090ff9150bd0988fd0790d4819cd8083e83d/$_pkgname-$pkgver.tar.gz")
sha512sums=('07674c7d1e2b2e647dd2f28ab0b442fc58846113013f26655aec708aa88aa4d907cfe593444a8f25496f82f38506883b0384f3c58aed3277b708458f4357bf50')

prepare() {
  cp -a "$_pkgname-$pkgver"{,-2}
}

build() {
  cd "$_pkgname-$pkgver-2"

  python2 setup.py build

  cd "$srcdir/$_pkgname-$pkgver"

  python setup.py build
}

package_python2-ajpy() {
  depends=('python2')

  cd "$_pkgname-$pkgver-2"

  python2 setup.py install --prefix=/usr --root="$pkgdir" --optimize=1 \
    --skip-build
}

package_python-ajpy() {
  depends=('python')

  cd "$_pkgname-$pkgver"

  python setup.py install --prefix=/usr --root="$pkgdir" --optimize=1 \
    --skip-build
}

