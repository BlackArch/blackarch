# This file is part of BlackArch Linux ( https://www.blackarch.org/ ).
# See COPYING for license details.

pkgbase=python-pyasn
pkgname=('python2-pyasn' 'python-pyasn')
_pkgname=pyasn
pkgver=1.6.1
pkgrel=2
pkgdesc='Offline IP address to Autonomous System Number lookup module.'
arch=('x86_64' 'aarch64')
url='https://pypi.org/project/pyasn/#files'
license=('MIT')
makedepends=('python2-setuptools' 'python-setuptools')
source=("https://files.pythonhosted.org/packages/39/5b/08fd0790c2f97cc9148aa611e8df28cc11fe157e279740c992597a983ec3/$_pkgname-$pkgver.tar.gz")
sha512sums=('2ef4cd0bbab3fd0e1b309dcd9b491ea90fbc4d297eee6dbc7250c0c26dd431e6e0901d31e5b29a6adae750dc224d3f0374cac2cd4530f7d17707b0735dcf02e6')

prepare() {
  cp -a "$_pkgname-$pkgver"{,-2}
}

build() {
  cd "$_pkgname-$pkgver-2"

  python2 setup.py build

  cd "$srcdir/$_pkgname-$pkgver"

  python setup.py build
}

package_python2-pyasn() {
  depends=('python2' 'python2-ordereddict')

  cd "$_pkgname-$pkgver-2"

  python2 setup.py install --prefix=/usr --root="$pkgdir" -O1 --skip-build

  for i in "$pkgdir/usr/bin/"*; do
    mv $i "${i}2"
  done

  chmod 644 \
    "$pkgdir/usr/lib/python2.7/site-packages/pyasn-1.6.1-py2.7.egg-info/"*
}

package_python-pyasn() {
  depends=('python')

  cd "$_pkgname-$pkgver"

  python setup.py install --prefix=/usr --root="$pkgdir" -O1 --skip-build

  chmod 644 \
    "$pkgdir/usr/lib/python3.9/site-packages/pyasn-1.6.1-py3.9.egg-info/"*
}

