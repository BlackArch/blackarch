# This file is part of BlackArch Linux ( https://www.blackarch.org/ ).
# See COPYING for license details.

pkgbase=python-pyasn
pkgname=('python2-pyasn' 'python-pyasn')
_pkgname=pyasn
pkgver=1.6.0b1
pkgrel=4
pkgdesc='Offline IP address to Autonomous System Number lookup module.'
arch=('x86_64' 'aarch64')
url='https://pypi.org/project/pyasn/#files'
license=('MIT')
makedepends=('python2-setuptools' 'python-setuptools')
source=("https://files.pythonhosted.org/packages/31/da/c8338545be0ee7a727c977113e75888e4f1f2b2e10f9284fdfa31dab29bc/$_pkgname-$pkgver.tar.gz")
sha512sums=('a5774413b99bd2096c3c4109676a2b107b56de4b5e1a55649dc51554612acaec707267e27a1a4c7f0afca808e870f896903ef87695d5323f3cb5275569256a31')

prepare() {
  cp -a "$_pkgname-$pkgver"{,-2}
}

build() {
  cd "$_pkgname-$pkgver-2"

  python2 setup.py build

  cd "$srcdir/$_pkgname-$pkgver"

  python setup.py build
}

package_python2-pyasn() {
  depends=('python2' 'python2-ordereddict')

  cd "$_pkgname-$pkgver-2"

  python2 setup.py install --prefix=/usr --root="$pkgdir" --optimize=1 \
    --skip-build

  for i in "$pkgdir/usr/bin/"*; do
    mv $i "${i}2"
  done

  chmod 644 \
    "$pkgdir/usr/lib/python2.7/site-packages/pyasn-1.6.0b1-py2.7.egg-info/"*
}

package_python-pyasn() {
  depends=('python')

  cd "$_pkgname-$pkgver"

  python setup.py install --prefix=/usr --root="$pkgdir" --optimize=1 \
    --skip-build

  chmod 644 \
    "$pkgdir/usr/lib/python3.8/site-packages/pyasn-1.6.0b1-py3.8.egg-info/"*
}

