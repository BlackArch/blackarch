# This file is part of BlackArch Linux ( https://www.blackarch.org/ ).
# See COPYING for license details.

pkgname=tbear
pkgver=1.5
pkgrel=4
pkgdesc='Transient Bluetooth Environment Auditor includes an ncurses-based Bluetooth scanner (a bit similar to kismet), a Bluetooth DoS tool, and a Bluetooth hidden device locator.'
arch=('x86_64' 'aarch64')
url='http://freshmeat.net/projects/t-bear'
license=('GPL3')
depends=('bluez-libs' 'bluez' 'ncurses' 'ncurses5-compat-libs')
groups=('blackarch' 'blackarch-bluetooth')
source=("https://dl.packetstormsecurity.net/wireless/$pkgname.tar.gz")
sha512sums=('e0c2227b1721bb91d75f5cc8d8eb663eb694ca3e34cda0d88a74132b5690ffffa948feeb22e4149781b3933c3bc21dc11e0beca8dd8930ac797d5949123e9cca')

build() {
  cd "$pkgname-$pkgver"

  make CFLAGS="$CFLAGS"
}

package(){
  cd "$pkgname-$pkgver"

  for exe in 'tbear' 'tbsearch' 'tanya' ; do
    install -Dm 755 $exe "$pkgdir/usr/bin/$exe"
  done

  install -Dm 644 btoui "$pkgdir/etc/btoui"
  install -Dm 644 -t "$pkgdir/usr/share/$pkgname/" README
}

