pkgname=hydra
pkgver=7.4.2
pkgrel=3
pkgdesc="A very fast network logon cracker which support many different services."
arch=('i686' 'x86_64')
url='http://www.thc.org/thc-hydra/'
license="GPL3"
groups=('blackarch' 'blackarch-password-attacks')
depends=('postgresql-libs' 'subversion' 'libssh' 'flamerobin' 'libmariadbclient' 'libidn')
source=("http://www.thc.org/releases/hydra-${pkgver}.tar.gz"
        "fix-locations.patch")
md5sums=('c38d9daff27ff1a46e7a31f4550fe714'
         '869ca964fac894a670806d27a51ec843')

prepare() {
  cd $srcdir/hydra-$pkgver
  patch -p1 < ../fix-locations.patch
}

build() {
  cd $srcdir/hydra-$pkgver

  ./configure
  make
}

package() {
  cd $srcdir/hydra-$pkgver

  # Needed or else make install will not
  # install sample default password files.
  install -dm755 $pkgdir/usr/etc
  make PREFIX=$pkgdir/usr install

  # Conform to the FHS.
  install -dm755 "$pkgdir/usr/share" "$pkgdir/etc"
  mv "$pkgdir/usr/man" "$pkgdir/usr/share"
  mv "$pkgdir/usr/etc" "$pkgdir/etc/hydra"
}
