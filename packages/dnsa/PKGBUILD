# This file is part of BlackArch Linux ( http://blackarch.org ).
# See COPYING for license details.

pkgname=dnsa
pkgver=0.5
pkgrel=3
pkgdesc='DNSA is a dns security swiss army knife.'
arch=('x86_64' 'armv6h' 'armv7h' 'aarch64')
url='http://packetfactory.openwall.net/projects/dnsa/index.html'
license=('GPL')
depends=('libnet' 'libpcap')
groups=('blackarch' 'blackarch-scanner')
source=("http://packetfactory.openwall.net/projects/dnsa/dnsa-current.tar.gz")
sha512sums=('01457e8bc2ee5ab7a862f57a9278e3d93a0d8b0b1becbef6394a56e6c50ca0f719e9390b9cee33c82751a17cb2735ce4894be907358f55da8650ae64c5711c18')

build() {
  cd "$srcdir/dnsa-$pkgver/sources"

  if [[ "$CARCH" == "i686" || "$CARCH" == "x86_64" ]];
  then
    ./configure --prefix=/usr
  else
    ./configure --build=arm --prefix=/usr
  fi

  make
}

package(){
  cd "$srcdir/dnsa-$pkgver/sources"

  make DESTDIR="$pkgdir" install
}
