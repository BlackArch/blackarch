# This file is part of BlackArch Linux ( https://www.blackarch.org/ ).
# See COPYING for license details.

pkgname=getsploit
pkgver=30.6525826
pkgrel=1
pkgdesc='Command line utility for searching and downloading exploits.'
groups=('blackarch' 'blackarch-exploitation' 'blackarch-misc')
arch=('any')
url='https://github.com/vulnersCom/getsploit'
license=('LGPL3')
depends=('python2')
makedepends=('git')
source=('git+https://github.com/vulnersCom/getsploit.git')
sha512sums=('SKIP')

pkgver() {
  cd $pkgname

  echo $(git rev-list --count HEAD).$(git rev-parse --short HEAD)
}

package() {
  cd $pkgname

  install -dm 755 "$pkgdir/usr/bin"
  install -dm 755 "$pkgdir/usr/share/$pkgname"

  install -Dm 644 -t "$pkgdir/usr/share/doc/$pkgname/" README.md
  install -Dm 644 LICENSE "$pkgdir/usr/share/licenses/$pkgname/LICENSE"

  rm README.md LICENSE

  cp -a * "$pkgdir/usr/share/$pkgname/"

  cat > "$pkgdir/usr/bin/$pkgname" << EOF
#!/bin/sh
cd /usr/share/$pkgname/$pkgname
exec python2 getsploit.py "\$@"
EOF

  chmod +x "$pkgdir/usr/bin/$pkgname"
}

