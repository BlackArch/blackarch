# This file is part of BlackArch Linux ( http://blackarch.org ).
# See COPYING for license details.

pkgname=getsploit
pkgver=25.1db4256
pkgrel=2
pkgdesc='Command line utility for searching and downloading exploits.'
groups=('blackarch' 'blackarch-exploitation' 'blackarch-misc')
arch=('any')
url='https://github.com/vulnersCom/getsploit'
license=('LGPL3')
depends=('python2')
makedepends=('git')
source=('git+https://github.com/vulnersCom/getsploit.git')
sha512sums=('SKIP')

pkgver() {
  cd "$srcdir/getsploit"

  echo $(git rev-list --count HEAD).$(git rev-parse --short HEAD)
}

package() {
  cd "$srcdir/getsploit"

  install -dm 755 "$pkgdir/usr/bin"
  install -dm 755 "$pkgdir/usr/share/getsploit"

  install -Dm644 -t "$pkgdir/usr/share/doc/getsploit/" README.md
  install -Dm644 LICENSE "$pkgdir/usr/share/licenses/getsploit/LICENSE"

  rm README.md LICENSE

  cp -a * "$pkgdir/usr/share/getsploit/"

  cat > "$pkgdir/usr/bin/getsploit" << EOF
#!/bin/sh
cd /usr/share/getsploit/getsploit
exec python2 getsploit.py "\${@}"
EOF

  chmod a+x "$pkgdir/usr/bin/getsploit"
}
