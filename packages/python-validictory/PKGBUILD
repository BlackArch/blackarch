# This file is part of BlackArch Linux ( https://www.blackarch.org/ ).
# See COPYING for license details.

pkgbase=python-validictory
pkgname=('python2-validictory' 'python-validictory')
_pkgname=validictory
pkgver=1.1.2
pkgrel=5
pkgdesc='General purpose Python data validator that allows validation of arbitrary Python data structures.'
arch=('any')
url='https://pypi.org/project/validictory/#files'
license=('MIT')
makedepends=('python2-setuptools' 'python-setuptools')
source=("https://files.pythonhosted.org/packages/c9/c6/59d4273279df9f942f34cf45b9031c109a51d8e5682ca7975a9e1ae71080/$_pkgname-$pkgver.tar.gz")
sha512sums=('e520e034a233e1dbfacfbffed52cb9b25fe1df0992113b12903da0a732e8818cce9b000aa31efff6b7715acc292b3331ea4bcaf4fe853ec1e16833a5261779e7')

prepare() {
  cp -a "$_pkgname-$pkgver"{,-2}
}

build() {
  cd "$_pkgname-$pkgver-2"

  python2 setup.py build

  cd "$srcdir/$_pkgname-$pkgver"

  python setup.py build
}

package_python2-validictory() {
  depends=('python2')

  cd "$_pkgname-$pkgver-2"

  python2 setup.py install --prefix=/usr --root="$pkgdir" --optimize=1 \
    --skip-build
}

package_python-validictory() {
  depends=('python')

  cd "$_pkgname-$pkgver"

  python setup.py install --prefix=/usr --root="$pkgdir" --optimize=1 \
    --skip-build
}

