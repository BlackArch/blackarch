# This file is part of BlackArch Linux ( https://www.blackarch.org/ ).
# See COPYING for license details.

pkgname=beroot
pkgver=1.0.1.r109.g4c0b648
pkgrel=5
pkgdesc='A post exploitation tool to check common misconfigurations to find a way to escalate our privilege.'
arch=('any')
groups=('blackarch' 'blackarch-exploitation')
url='https://github.com/AlessandroZ/BeRoot'
license=('LGPL3')
depends=('python' 'python-dbus')
makedepends=('git')
source=("git+https://github.com/AlessandroZ/$pkgname.git")
sha512sums=('SKIP')

pkgver() {
  cd $pkgname

  ( set -o pipefail
    git describe --long --tags --abbrev=7 2>/dev/null |
      sed 's/\([^-]*-g\)/r\1/;s/-/./g' ||
    printf "%s.%s" "$(git rev-list --count HEAD)" \
      "$(git rev-parse --short=7 HEAD)"
  )
}

package() {
  cd $pkgname

  install -dm 755 "$pkgdir/usr/bin"
  install -dm 755 "$pkgdir/usr/share/$pkgname"
  install -Dm 644 -t "$pkgdir/usr/share/doc/$pkgname" *.md

  rm *.md

  cp -a * "$pkgdir/usr/share/$pkgname/"

  cat > "$pkgdir/usr/bin/$pkgname" << EOF
#!/bin/sh
exec python /usr/share/$pkgname/Linux/beroot.py "\$@"
EOF

  chmod +x "$pkgdir/usr/bin/$pkgname"
}

