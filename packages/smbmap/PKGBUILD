# This file is part of BlackArch Linux ( https://www.blackarch.org/ ).
# See COPYING for license details.

pkgname=smbmap
pkgver=116.07b0b43
pkgrel=2
groups=('blackarch' 'blackarch-scanner' 'blackarch-recon')
pkgdesc='A handy SMB enumeration tool.'
arch=('any')
url='https://github.com/ShawnDEvans/smbmap'
license=('GPL3')
depends=('python' 'impacket' 'python-pycryptodomex' 'python-pyasn1')
makedepends=('git')
source=("git+https://github.com/ShawnDEvans/$pkgname.git")
sha512sums=('SKIP')

pkgver() {
  cd $pkgname

  echo $(git rev-list --count HEAD).$(git rev-parse --short HEAD)
}

package() {
  cd $pkgname

  install -dm 755 "$pkgdir/usr/bin"
  install -dm 755 "$pkgdir/usr/share/$pkgname"

  install -Dm 644 -t "$pkgdir/usr/share/doc/$pkgname/" README.md
  install -Dm 644 -t "$pkgdir/usr/share/$pkgname/" psutils/*
  install -Dm 755 smbmap.py "$pkgdir/usr/share/$pkgname/$pkgname.py"

  #rm -rf *.txt *.md LICENSE
  #cp -a * "$pkgdir/usr/share/$pkgname/"
  
  cat > "$pkgdir/usr/bin/$pkgname" << EOF
#!/bin/sh
cd /usr/share/$pkgname
exec ./$pkgname.py "\$@"
EOF

  chmod a+x "$pkgdir/usr/bin/$pkgname"
}


