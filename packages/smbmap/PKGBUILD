# This file is part of BlackArch Linux ( https://www.blackarch.org/ ).
# See COPYING for license details.

pkgname=smbmap
pkgver=166.73c9907
pkgrel=1
groups=('blackarch' 'blackarch-scanner' 'blackarch-recon')
pkgdesc='A handy SMB enumeration tool.'
arch=('any')
url='https://github.com/ShawnDEvans/smbmap'
license=('GPL3')
depends=('python' 'impacket' 'python-pycryptodomex' 'python-pyasn1'
         'python-termcolor')
makedepends=('python-build' 'python-pip' 'git')
source=("git+https://github.com/ShawnDEvans/$pkgname.git")
sha512sums=('SKIP')

pkgver() {
  cd $pkgname

  echo $(git rev-list --count HEAD).$(git rev-parse --short HEAD)
}

build() {
  cd $pkgname

  python -m build --wheel --outdir="$startdir/dist"
}

package() {
  cd $pkgname

  pip install \
    --verbose \
    --disable-pip-version-check \
    --no-warn-script-location \
    --ignore-installed \
    --no-compile \
    --no-deps \
    --root="$pkgdir" \
    --prefix=/usr \
    --no-index \
    --find-links="file://$startdir/dist" \
    $pkgname
}

