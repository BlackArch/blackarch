# This file is part of BlackArch Linux ( https://www.blackarch.org/ ).
# See COPYING for license details.

pkgbase=python-py3exiv2
pkgname=('python2-py3exiv2' 'python-py3exiv2')
_pkgname=py3exiv2
pkgver=0.7.2
pkgrel=1
pkgdesc='A Python3 binding to the library exiv2'
arch=('x86_64' 'aarch64')
url='https://pypi.org/project/py3exiv2/#files'
license=('GPL3')
makedepends=('python2-setuptools' 'python-setuptools' 'boost')
source=("https://files.pythonhosted.org/packages/70/61/35bf9f40fda6153711d12b45d7d2135e4adc8e266259ce5016b0d3d71ac7/$_pkgname-$pkgver.tar.gz")
sha512sums=('6bd4cc4551fc19492390d3bef5f9ea44256445b30446a527034aefc8910751e80a09e662d5d16e2d45d6063b017e4370bb76f40e302d186c5c1dd6ff52afd77a')

prepare() {
  cp -a "$_pkgname-$pkgver"{,-2}
}

build() {
  cd "$_pkgname-$pkgver-2"

  python2 setup.py build

  cd "$srcdir/$_pkgname-$pkgver"

  python setup.py build
}

package_python2-py3exiv2() {
  depends=('python2')

  cd "$_pkgname-$pkgver-2"

  python2 setup.py install --prefix=/usr --root="$pkgdir" -O1 --skip-build
}

package_python-py3exiv2() {
  depends=('python')

  cd "$_pkgname-$pkgver"

  python setup.py install --prefix=/usr --root="$pkgdir" -O1 --skip-build
}

