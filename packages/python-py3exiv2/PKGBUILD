# This file is part of BlackArch Linux ( https://www.blackarch.org/ ).
# See COPYING for license details.

pkgbase=python-py3exiv2
pkgname=('python2-py3exiv2' 'python-py3exiv2')
_pkgname=py3exiv2
pkgver=0.7.1
pkgrel=1
pkgdesc='A Python3 binding to the library exiv2'
arch=('x86_64' 'armv6h' 'armv7h' 'aarch64')
url='https://pypi.org/project/py3exiv2/#files'
license=('GPL3')
makedepends=('python2-setuptools' 'python-setuptools' 'boost')
source=("https://files.pythonhosted.org/packages/49/e0/f8b22cc5061559345a340747feb6f60369f5ba576155ef73009fe898278d/$_pkgname-$pkgver.tar.gz")
sha512sums=('c9dd66cb968e91f9ae44de97b95f132322e6c5a96c572fc92251378bfd6177fc7528f02321c4d4aa296eb26e5facde72ed0e1eeb904cd36ac22e7c5ece6645c3')

prepare() {
  cp -a "$_pkgname-$pkgver"{,-2}
}

build() {
  cd "$_pkgname-$pkgver-2"

  python2 setup.py build

  cd "$srcdir/$_pkgname-$pkgver"

  python setup.py build
}

package_python2-py3exiv2() {
  depends=('python2')

  cd "$_pkgname-$pkgver-2"

  python2 setup.py install --prefix=/usr --root="$pkgdir" -O1 --skip-build
}

package_python-py3exiv2() {
  depends=('python')

  cd "$_pkgname-$pkgver"

  python setup.py install --prefix=/usr --root="$pkgdir" -O1 --skip-build
}

