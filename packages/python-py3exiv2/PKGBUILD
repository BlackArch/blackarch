# This file is part of BlackArch Linux ( https://www.blackarch.org/ ).
# See COPYING for license details.

pkgbase=python-py3exiv2
pkgname=('python2-py3exiv2' 'python-py3exiv2')
_pkgname=py3exiv2
pkgver=0.8.0
pkgrel=2
pkgdesc='A Python3 binding to the library exiv2'
arch=('x86_64' 'aarch64')
url='https://pypi.org/project/py3exiv2/#files'
license=('GPL3')
makedepends=('python2-setuptools' 'python-setuptools' 'boost')
source=("https://files.pythonhosted.org/packages/57/16/250e47eb4594ba91623a5920ba2c31c734ed705c26454c4430710f13c78b/$_pkgname-$pkgver.tar.gz")
sha512sums=('95922b6e11462a4cb4cd57ba985438e234ba0f5f60404f1a16094700e4051097366d01da1776f54f2935753508ad0bf730e7f836f4558bfe84d3f608ebe7bf02')

prepare() {
  cp -a "$_pkgname-$pkgver"{,-2}
}

build() {
  cd "$_pkgname-$pkgver-2"

  python2 setup.py build

  cd "$srcdir/$_pkgname-$pkgver"

  python setup.py build
}

package_python2-py3exiv2() {
  depends=('python2')

  cd "$_pkgname-$pkgver-2"

  python2 setup.py install --prefix=/usr --root="$pkgdir" -O1 --skip-build
}

package_python-py3exiv2() {
  depends=('python')

  cd "$_pkgname-$pkgver"

  python setup.py install --prefix=/usr --root="$pkgdir" -O1 --skip-build
}

