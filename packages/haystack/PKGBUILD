# This file is part of BlackArch Linux ( http://blackarch.org ).
# See COPYING for license details.

pkgname='haystack'
pkgver=1823.c178b5a
pkgrel=1
pkgdesc='A Python framework for finding C structures from process memory - heap analysis - Memory structures forensics.'
groups=('blackarch' 'blackarch-binary' 'blackarch-forensic')
arch=('any')
url='https://github.com/trolldbois/python-haystack'
license=('GPL3')
depends=('python2' 'python2-levenshtein' 'python2-ptrace' 'python2-networkx')
makedepends=('git' 'python2-setuptools')
source=('git+https://github.com/trolldbois/python-haystack.git')
sha1sums=('SKIP')

pkgver() {
  cd "$srcdir/python-haystack"

  echo $(git rev-list --count HEAD).$(git rev-parse --short HEAD)
}

build() {
  cd "$srcdir/python-haystack"

  python2 setup.py build
}

package() {
  cd "$srcdir/python-haystack"

  install -Dm644 -t "$pkgdir/usr/share/doc/haystack/" README.rst CHANGES.txt
  install -Dm644 LICENSE.txt "$pkgdir/usr/share/licenses/haystack/LICENSE.txt"

  python2 setup.py install --root="$pkgdir" --optimize=1
}
