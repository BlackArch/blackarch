# This file is part of BlackArch Linux ( http://blackarch.org ).
# See COPYING for license details.

pkgname='jsql-injection'
pkgver='0.77'
pkgrel=2
pkgdesc='A Java application for automatic SQL database injection.'
groups=('blackarch' 'blackarch-webapp' 'blackarch-exploitation'
        'blackarch-fuzzer')
arch=('any')
url='https://github.com/ron190/jsql-injection'
license=('GPL2')
depends=('java-environment')
makedepends=('git')
noextract=("jsql-injection-v${pkgver}.jar")
source=("https://github.com/ron190/jsql-injection/releases/download/v0.77/jsql-injection-v${pkgver}.jar")
sha1sums=('7843a30518c2f01532efb308c624ef6eff96a24f')

package() {
  cd "$srcdir"

  mkdir -p "$pkgdir/usr/bin"

  install -Dm644 jsql-injection-v${pkgver}.jar \
    "$pkgdir/usr/share/jsql-injection/jsql-injection-v${pkgver}.jar"

  cat > "$pkgdir/usr/bin/jsql-injection"<< EOF
#!/bin/sh
exec java -jar /usr/share/jsql-injection/jsql-injection-v${pkgver}.jar "\${@}"
EOF

  chmod a+x "$pkgdir/usr/bin/jsql-injection"
}
