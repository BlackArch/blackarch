# This file is part of BlackArch Linux ( https://www.blackarch.org/ ).
# See COPYING for license details.

pkgname=jsql-injection
pkgver=0.114
pkgrel=1
pkgdesc='A Java application for automatic SQL database injection.'
groups=('blackarch' 'blackarch-webapp' 'blackarch-exploitation'
        'blackarch-fuzzer')
arch=('any')
url='https://github.com/ron190/jsql-injection'
license=('GPL-2.0-or-later')
depends=('java-environment')
makedepends=('git')
noextract=("$pkgname-v$pkgver.jar")
source=("https://github.com/ron190/jsql-injection/releases/download/v$pkgver/$pkgname-v$pkgver.jar")
sha512sums=('d258bae659dfac11380d9c77360000eee6ffe90a34e325e9e91825d0b722d67df362d575485386118fe21d0bcbdd94093074a3fbfb908077157d4e176d8c490e')

package() {
  install -dm 755 "$pkgdir/usr/bin"

  install -Dm 644 "$pkgname-v$pkgver.jar" \
    "$pkgdir/usr/share/$pkgname/$pkgname-v$pkgver.jar"

  cat > "$pkgdir/usr/bin/$pkgname"<< EOF
#!/bin/sh
exec java -jar /usr/share/$pkgname/$pkgname-v$pkgver.jar "\$@"
EOF

  chmod +x "$pkgdir/usr/bin/$pkgname"
}

