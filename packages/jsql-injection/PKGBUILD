# This file is part of BlackArch Linux ( https://www.blackarch.org/ ).
# See COPYING for license details.

pkgname=jsql-injection
pkgver=0.110
pkgrel=1
pkgdesc='A Java application for automatic SQL database injection.'
groups=('blackarch' 'blackarch-webapp' 'blackarch-exploitation'
        'blackarch-fuzzer')
arch=('any')
url='https://github.com/ron190/jsql-injection'
license=('GPL2')
depends=('java-environment')
makedepends=('git')
noextract=("$pkgname-v$pkgver.jar")
source=("https://github.com/ron190/jsql-injection/releases/download/v$pkgver/$pkgname-v$pkgver.jar")
sha512sums=('2469387b34d6a90275a129416d4d9739a99b57752d9be3d79b59c35ef4b5ef018a86aa0e72eec113dc9d2fa2db564eda8657307a4b77d440b1cad9fee18c2da7')

package() {
  install -dm 755 "$pkgdir/usr/bin"

  install -Dm 644 "$pkgname-v$pkgver.jar" \
    "$pkgdir/usr/share/$pkgname/$pkgname-v$pkgver.jar"

  cat > "$pkgdir/usr/bin/$pkgname"<< EOF
#!/bin/sh
exec java -jar /usr/share/$pkgname/$pkgname-v$pkgver.jar "\$@"
EOF

  chmod +x "$pkgdir/usr/bin/$pkgname"
}

