# This file is part of BlackArch Linux ( https://www.blackarch.org/ ).
# See COPYING for license details.

pkgname=plasma-disasm
_pkgname=plasma
pkgver=922.ec7df9b
pkgrel=9
groups=('blackarch' 'blackarch-disassembler' 'blackarch-binary'
        'blackarch-debugger')
pkgdesc='An interactive disassembler for x86/ARM/MIPS. It can generates indented pseudo-code with colored syntax.'
arch=('x86_64' 'aarch64')
url='https://github.com/joelpx/plasma'
license=('GPL-3.0-or-later')
depends=('python' 'python-capstone' 'python-pyelftools' 'python-pefile'
         'python-msgpack' 'python-pefile' 'python-pyqt4' 'python-nose')
makedepends=('git' 'python-setuptools' 'python-nose' 'python-pip')
source=("$pkgname::git+https://github.com/joelpx/$_pkgname.git"
        'setup.py.patch'
        'analyzer.c.patch'
        'analyzer-mips.c.patch'
        'analyzer-arm.c.patch')
sha512sums=('SKIP'
            '2e20f77ed97d17da5f68a61f3308f03f1a24817f7cba2b0e4f915b55f2bc1fecea107209c06b863f69653f6e01f510656593d15a8812f0b19a691303af5dc4c9'
            '39e88f5b212871af0ab89079a088cff359b4dd085b04bd1dd6c52d5a2a8dd60598634fcaef0d8ecfb5173347d40b190183cc05c6f32e61ae68c8edfd9666dfc5'
            'ad41509a03102b6c362d345cfefe8e2d882caefa668c73a5201eb18920506538f5d3f4879769fcbada276ed2968a6fb16f48b8dfcbbdba25e135ed3b490bf5b8'
            '0ea6f6ff2a6e88beb236814aa563ddabc797f738de4484e707621929e61746c3247e7879772099f694d52321e73190ea00bd7e4ee0be3e09f6015dd8fbdcf49a')

pkgver() {
  cd $pkgname

  ( set -o pipefail
    git describe --long --tags --abbrev=7 2>/dev/null |
      sed 's/\([^-]*-g\)/r\1/;s/-/./g' ||
    printf "%s.%s" "$(git rev-list --count HEAD)" \
      "$(git rev-parse --short=7 HEAD)"
  )
}

prepare() {
  cd $pkgname

  patch -p0 < "$srcdir/setup.py.patch"
  patch -p2 < "$srcdir/analyzer.c.patch"
  patch -p2 < "$srcdir/analyzer-mips.c.patch"
  patch -p2 < "$srcdir/analyzer-arm.c.patch"

  find . -type f -exec sed -i 's|/bin/python3|/usr/bin/env python3|' {} +

  sed -i 's/msgpack-python>=0.4.6/msgpack/' requirements.txt
}

build() {
  cd $pkgname

  python setup.py build
}

package() {
  cd $pkgname

  python setup.py build_ext --inplace
  python setup.py install --root="$pkgdir" --prefix=/usr -O1

  install -Dm 644 -t "$pkgdir/usr/share/doc/$pkgname" README.md
  install -Dm 644 LICENSE "$pkgdir/usr/share/licenses/$pkgname/LICENSE"

  rm README.md LICENSE

  mv "$pkgdir/usr/bin/$_pkgname" "$pkgdir/usr/bin/$pkgname"
}

