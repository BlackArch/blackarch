# This file is part of BlackArch Linux ( https://www.blackarch.org/ ).
# See COPYING for license details.

pkgname=dftimewolf
pkgver=402.41abc50
pkgrel=1
pkgdesc='Framework for orchestrating forensic collection, processing and data export .'
arch=('any')
groups=('blackarch' 'blackarch-forensic')
url='https://github.com/log2timeline/dftimewolf'
license=('Apache')
depends=('python')
makedepends=('git' 'python-setuptools' 'python-pip')
source=("git+https://github.com/log2timeline/$pkgname.git")
sha512sums=('SKIP')
install="$pkgname.install"

pkgver() {
  cd $pkgname

  echo $(git rev-list --count HEAD).$(git rev-parse --short HEAD)
}

package() {
  cd $pkgname

  install -dm 755 "$pkgdir/usr/bin"
  install -Dm 644 requirements.txt "$pkgdir/usr/share/$pkgname/requirements.txt"
  install -Dm 644 -t "$pkgdir/usr/share/doc/$pkgname/" *.md docs/*.md
  rm -rf LICENSE *.md docs/*.md .gitignore .pylintrc .travis.yml .style.yapf \
    .isort.cfg appveyor.yml run_tests.py

  cp -a * "$pkgdir/usr/share/$pkgname/"

  cat > "$pkgdir/usr/bin/$pkgname" << EOF
#!/bin/sh
source /usr/share/$pkgname/venv/bin/activate
exec python /usr/share/$pkgname/dftimewolf/cli/dftimewolf_recipes.py "\$@"
EOF

  chmod a+x "$pkgdir/usr/bin/$pkgname"
}

