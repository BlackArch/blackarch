# This file is part of BlackArch Linux ( https://www.blackarch.org/ ).
# See COPYING for license details.

pkgname=dftimewolf
pkgver=833.50f869b
pkgrel=1
epoch=1
_pyver=3.14
pkgdesc='Framework for orchestrating forensic collection, processing and data export.'
arch=('any')
groups=('blackarch' 'blackarch-forensic')
url='https://github.com/log2timeline/dftimewolf'
license=('Apache-2.0')
depends=('python' 'python-poetry')
makedepends=('git')
source=("git+https://github.com/log2timeline/$pkgname.git")
sha512sums=('SKIP')
install="$pkgname.install"

pkgver() {
  cd $pkgname

  ( set -o pipefail
    git describe --long --tags --abbrev=7 2>/dev/null |
      sed 's/\([^-]*-g\)/r\1/;s/-/./g' ||
    printf "%s.%s" "$(git rev-list --count HEAD)" \
      "$(git rev-parse --short=7 HEAD)"
  )
}

prepare() {
  cd $pkgname

  # Allow python 3.14
  # waiting for https://github.com/log2timeline/dftimewolf/pull/1042
  sed -i 's|>=3.11,<=3.13|>=3.11,<=3.15|g' pyproject.toml

  # Fix missing dependency
  # waiting for https://github.com/log2timeline/dftimewolf/pull/1043
  sed -i '/openrelik-api-client = "0.2.3"/a \python-dateutil = "^2.9.0.post0"' pyproject.toml

  rm poetry.lock
}

package() {
  cd $pkgname

  install -dm 755 "$pkgdir/usr/bin"
  install -dm 755 "$pkgdir/usr/share/$pkgname"

  install -Dm 644 -t "$pkgdir/usr/share/doc/$pkgname/" *.md docs/*.md

  rm -rf .github/ .vscode/ docker/ docs/ tests/ .gitignore .isort.cfg .pylintrc \
    ACKNOWLEDGEMENTS AUTHORS LICENSE mkdocs.yml mypy.ini

  cp -a * "$pkgdir/usr/share/$pkgname/"

  cat > "$pkgdir/usr/bin/$pkgname" << EOF
#!/bin/sh
poetry config cache-dir "/usr/share/$pkgname"
poetry env use /usr/share/$pkgname/.venv/bin/python$_pyver \\
  --directory /usr/share/$pkgname
poetry run --directory "/usr/share/$pkgname" $pkgname "\$@"

# some try with venv + pip
#source /usr/share/$pkgname/venv/bin/activate
#exec python -m dftimewolf "\$@"
EOF

  chmod +x "$pkgdir/usr/bin/$pkgname"
}

