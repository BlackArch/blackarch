# This file is part of BlackArch Linux ( https://www.blackarch.org/ ).
# See COPYING for license details.

pkgname=dftimewolf
pkgver=576.e8d5e392
pkgrel=2
pkgdesc='Framework for orchestrating forensic collection, processing and data export .'
arch=('any')
groups=('blackarch' 'blackarch-forensic')
url='https://github.com/log2timeline/dftimewolf'
license=('Apache')
depends=('python')
makedepends=('git' 'python-pip')
source=("git+https://github.com/log2timeline/$pkgname.git")
sha512sums=('SKIP')
install="$pkgname.install"

pkgver() {
  cd $pkgname

  echo $(git rev-list --count HEAD).$(git rev-parse --short HEAD)
}

package() {
  cd $pkgname

  install -dm 755 "$pkgdir/usr/bin"
  install -dm 755 "$pkgdir/usr/share/$pkgname"

  install -Dm 644 -t "$pkgdir/usr/share/doc/$pkgname/" *.md docs/*.md

  rm -rf LICENSE *.md docs/*.md .gitignore .pylintrc .travis.yml .style.yapf \
    .isort.cfg appveyor.yml run_tests.py docker tests mkdocs.yml *.ini \
    requirements*.txt

  cp -a * "$pkgdir/usr/share/$pkgname/"

  cat > "$pkgdir/usr/bin/$pkgname" << EOF
#!/bin/sh
cd /usr/share/$pkgname
source venv/bin/activate
exec python $pkgname/cli/${pkgname}_recipes.py "\$@"
EOF

  chmod a+x "$pkgdir/usr/bin/$pkgname"
}

