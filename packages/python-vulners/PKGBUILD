# This file is part of BlackArch Linux ( https://www.blackarch.org/ ).
# See COPYING for license details.

pkgbase=python-vulners
_pkgname=vulners
pkgname=('python2-vulners' 'python-vulners')
pkgver=1.5.10
pkgrel=1
pkgdesc='Python library and command-line utility for Vulners (https://vulners.com).'
arch=('any')
url='https://pypi.org/project/vulners/#files'
makedepends=('python2-setuptools' 'python-setuptools')
license=('GPL3')
source=("https://files.pythonhosted.org/packages/b5/64/0d2e6a8436f0b105afed25dfc6790d4d033535f83d91557066568cdee6df/$_pkgname-$pkgver.tar.gz")
sha512sums=('bfb0573146ac50a60f48a84ffd810c8bb1a088e01686697b110a1e8dafa856f539a03620e6592b9d59c27412490d0395532fe61c00ccee21e4dfb71b7fc96e11')

prepare() {
  cp -a "$_pkgname-$pkgver" "$_pkgname-2-$pkgver"
}

build() {
  cd "$_pkgname-$pkgver"

  python setup.py build

  cd "$srcdir/$_pkgname-2-$pkgver"

  python2 setup.py build
}

package_python2-vulners() {
  depends=('python2-requests' 'python2-six')

  cd "$_pkgname-2-$pkgver"

  python2 setup.py install --root="$pkgdir" --optimize=1

  install -Dm 644 LICENSE "$pkgdir/usr/share/licenses/$pkgname/LICENSE"
}

package_python-vulners() {
  depends=('python-requests' 'python-six')

  cd "$_pkgname-$pkgver"

  python setup.py install --root="$pkgdir" --optimize=1

  install -Dm 644 LICENSE "$pkgdir/usr/share/licenses/$pkgname/LICENSE"
}

