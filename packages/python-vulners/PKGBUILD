# This file is part of BlackArch Linux ( https://www.blackarch.org/ ).
# See COPYING for license details.

pkgbase=python-vulners
_pkgname=vulners
pkgname=('python2-vulners' 'python-vulners')
pkgver=1.5.2
pkgrel=1
pkgdesc='Python library and command-line utility for Vulners (https://vulners.com).'
arch=('any')
url='https://pypi.org/project/vulners/#files'
makedepends=('python2-setuptools' 'python-setuptools')
license=('GPL3')
source=("https://files.pythonhosted.org/packages/3d/76/478fdd94b558ea9d75652307c6c25e641050aca72f088d3c3c4dbb73d8ad/$_pkgname-$pkgver.tar.gz")
sha512sums=('616c86353533d83c9f61772469a575429e0eaefbce1326a7ee85f66cdfadb1d5e192341f16103c9ae88e728c0b4968cdeb46df059250700e2fb30d935af2d4c8')

prepare() {
  cp -a "$_pkgname-$pkgver" "$_pkgname-2-$pkgver"
}

build() {
  cd "$_pkgname-$pkgver"

  python setup.py build

  cd "$srcdir/$_pkgname-2-$pkgver"

  python2 setup.py build
}

package_python2-vulners() {
  depends=('python2-requests' 'python2-six')

  cd "$_pkgname-2-$pkgver"

  python2 setup.py install --root="$pkgdir" --optimize=1

  install -Dm 644 LICENSE "$pkgdir/usr/share/licenses/$pkgname/LICENSE"
}

package_python-vulners() {
  depends=('python-requests' 'python-six')

  cd "$_pkgname-$pkgver"

  python setup.py install --root="$pkgdir" --optimize=1

  install -Dm 644 LICENSE "$pkgdir/usr/share/licenses/$pkgname/LICENSE"
}

