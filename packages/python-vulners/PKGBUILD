# This file is part of BlackArch Linux ( https://www.blackarch.org/ ).
# See COPYING for license details.

pkgbase=python-vulners
_pkgname=vulners
pkgname=('python2-vulners' 'python-vulners')
pkgver=1.5.9
pkgrel=2
pkgdesc='Python library and command-line utility for Vulners (https://vulners.com).'
arch=('any')
url='https://pypi.org/project/vulners/#files'
makedepends=('python2-setuptools' 'python-setuptools')
license=('GPL3')
source=("https://files.pythonhosted.org/packages/eb/e1/1cbb359c0bfbb2c9fab6acac8e8c466308f5f50b2d301138fb12e3fe957c/$_pkgname-$pkgver.tar.gz")
sha512sums=('93b2a69fa34faf8c56e340cd8953cbcfff0ca1d47a20d2deb87744d2f75bf4c16d0c50ef9e6c1d4afa7b3e5af7954896a6b0ab78682ba0cf75dbdee3a6c8e3b0')

prepare() {
  cp -a "$_pkgname-$pkgver" "$_pkgname-2-$pkgver"
}

build() {
  cd "$_pkgname-$pkgver"

  python setup.py build

  cd "$srcdir/$_pkgname-2-$pkgver"

  python2 setup.py build
}

package_python2-vulners() {
  depends=('python2-requests' 'python2-six')

  cd "$_pkgname-2-$pkgver"

  python2 setup.py install --root="$pkgdir" --optimize=1

  install -Dm 644 LICENSE "$pkgdir/usr/share/licenses/$pkgname/LICENSE"
}

package_python-vulners() {
  depends=('python-requests' 'python-six')

  cd "$_pkgname-$pkgver"

  python setup.py install --root="$pkgdir" --optimize=1

  install -Dm 644 LICENSE "$pkgdir/usr/share/licenses/$pkgname/LICENSE"
}

