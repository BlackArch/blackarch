# This file is part of BlackArch Linux ( https://www.blackarch.org/ ).
# See COPYING for license details.

pkgbase=python-vulners
_pkgname=vulners
pkgname=('python2-vulners' 'python-vulners')
pkgver=1.5.8
pkgrel=1
pkgdesc='Python library and command-line utility for Vulners (https://vulners.com).'
arch=('any')
url='https://pypi.org/project/vulners/#files'
makedepends=('python2-setuptools' 'python-setuptools')
license=('GPL3')
source=("https://files.pythonhosted.org/packages/8e/b4/10fc87a53a54818a54ffd4d7771247bd4b567821d9101c4aa54a8b7c55ba/$_pkgname-$pkgver.tar.gz")
sha512sums=('d3431f9bd08923013fcea8a14e08d09578c336ce6ffcb95e3cb916dee0a3b0a0ef9f4b81f57fe1312ef031abb7753047fa9e1b86debcc5997c45058885d69ffb')

prepare() {
  cp -a "$_pkgname-$pkgver" "$_pkgname-2-$pkgver"
}

build() {
  cd "$_pkgname-$pkgver"

  python setup.py build

  cd "$srcdir/$_pkgname-2-$pkgver"

  python2 setup.py build
}

package_python2-vulners() {
  depends=('python2-requests' 'python2-six')

  cd "$_pkgname-2-$pkgver"

  python2 setup.py install --root="$pkgdir" --optimize=1

  install -Dm 644 LICENSE "$pkgdir/usr/share/licenses/$pkgname/LICENSE"
}

package_python-vulners() {
  depends=('python-requests' 'python-six')

  cd "$_pkgname-$pkgver"

  python setup.py install --root="$pkgdir" --optimize=1

  install -Dm 644 LICENSE "$pkgdir/usr/share/licenses/$pkgname/LICENSE"
}

