# This file is part of BlackArch Linux ( https://www.blackarch.org/ ).
# See COPYING for license details.

pkgbase=python-vulners
_pkgname=vulners
pkgname=('python2-vulners' 'python-vulners')
pkgver=1.5.4
pkgrel=1
pkgdesc='Python library and command-line utility for Vulners (https://vulners.com).'
arch=('any')
url='https://pypi.org/project/vulners/#files'
makedepends=('python2-setuptools' 'python-setuptools')
license=('GPL3')
source=("https://files.pythonhosted.org/packages/1e/a6/67b0adbea1efef7cba22da446a6f5da24cda064c47f6a844a7ae5961a59a/$_pkgname-$pkgver.tar.gz")
sha512sums=('490ec29157c76d04d34d9c865d2004df43ed9c0c0d55d96d53f6f2abbdaa6341b84e3d93f0cb6790a8a393d8455fa5e4cfcfc7cd67ed6589548a5ca508d03bec')

prepare() {
  cp -a "$_pkgname-$pkgver" "$_pkgname-2-$pkgver"
}

build() {
  cd "$_pkgname-$pkgver"

  python setup.py build

  cd "$srcdir/$_pkgname-2-$pkgver"

  python2 setup.py build
}

package_python2-vulners() {
  depends=('python2-requests' 'python2-six')

  cd "$_pkgname-2-$pkgver"

  python2 setup.py install --root="$pkgdir" --optimize=1

  install -Dm 644 LICENSE "$pkgdir/usr/share/licenses/$pkgname/LICENSE"
}

package_python-vulners() {
  depends=('python-requests' 'python-six')

  cd "$_pkgname-$pkgver"

  python setup.py install --root="$pkgdir" --optimize=1

  install -Dm 644 LICENSE "$pkgdir/usr/share/licenses/$pkgname/LICENSE"
}

