# This file is part of BlackArch Linux ( https://www.blackarch.org/ ).
# See COPYING for license details.

pkgname=python-vulners
_pkgname=${pkgname#python-}
pkgver=3.1.3
pkgrel=1
pkgdesc='Python library and command-line utility for Vulners (https://vulners.com).'
arch=('any')
url='https://pypi.org/project/vulners/#files'
license=('GPL-3.0-or-later')
depends=('python' 'python-requests' 'python-six' 'python-httpx' 'python-orjson'
         'python-pydantic')
makedepends=('python-build' 'python-installer' 'python-setuptools'
             'python-wheel')
options=(!emptydirs)
source=("https://files.pythonhosted.org/packages/source/${_pkgname::1}/$_pkgname/$_pkgname-$pkgver.tar.gz")
sha512sums=('da85cbca574fa474110e938cdaf97ad039ad8e3f5d140d9ff5f9ad8c3190f3dc2cc999aa8cef53e9ecb2e7386bd7f371713a26c36bb49aadfbd03b6b1b79f367')

build() {
  cd "$_pkgname-$pkgver"

  python -m build --wheel --no-isolation
}

package() {
  cd "$_pkgname-$pkgver"

  python -m installer --destdir="$pkgdir" dist/*.whl
}

