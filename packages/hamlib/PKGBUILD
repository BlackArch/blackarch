# This file is part of BlackArch Linux ( https://www.blackarch.org/ ).
# See COPYING for license details.

pkgname=hamlib
pkgver=3.3
pkgrel=2
pkgdesc='Ham radio equipment control libraries.'
arch=('x86_64' 'armv6h' 'armv7h' 'aarch64')
url='http://hamlib.org/'
license=('GPL' 'LGPL')
depends=('perl' 'python2' 'tcl' 'libxml2' 'libusb')
makedepends=('libtool' 'swig')
source=("http://downloads.sourceforge.net/project/hamlib/hamlib/$pkgver/hamlib-$pkgver.tar.gz")
options=('!emptydirs')
changelog='hamlib.ChangeLog.markdown'
sha512sums=('4cf6c94d0238c8a13aed09413b3f4a027c8ded07f8840cdb2b9d38b39b6395a4a88a8105257015345f6de0658ab8c60292d11a9de3e16a493e153637af630a80')

build() {
	cd "$pkgname-$pkgver"

	export PYTHON=/usr/bin/python2

	./configure --prefix=/usr --sbindir=/usr/bin --with-perl-binding \
    --with-python-binding --enable-tcl-binding

	make
}

package() {
	cd "$pkgname-$pkgver"

	make DESTDIR=$pkgdir install

	# fix perl module location
	cd "$pkgdir/usr/lib/perl5/5.28/site_perl/"
	mkdir current/
	mv auto current
	mv Hamlib.pm current
	rm perltest.pl

	/usr/bin/find $pkgdir -name '.packlist' -delete
	/usr/bin/find $pkgdir -name '*.pod' -delete
}

