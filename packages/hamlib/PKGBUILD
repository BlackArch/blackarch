# This file is part of BlackArch Linux ( http://blackarch.org ).
# See COPYING for license details.

pkgname=hamlib
pkgver=3.2
pkgrel=3
pkgdesc='Ham radio equipment control libraries.'
arch=('x86_64' 'armv6h' 'armv7h' 'aarch64')
url='http://hamlib.org/'
license=('GPL' 'LGPL')
depends=('perl' 'python2' 'tcl' 'libxml2' 'libusb')
makedepends=('libtool' 'swig')
source=("http://downloads.sourceforge.net/project/hamlib/hamlib/${pkgver}/hamlib-${pkgver}.tar.gz")
options=('!emptydirs')
changelog='hamlib.ChangeLog.markdown'
sha512sums=('9aec2d361980abafdedf7b80f5b8bcf5b71f4f0c32bbfecebc0a50e6f74eb263ddc5188cf77283ed7361baabedb8364a2ce94a12fdda1bbdfaa0a030f510f224')

build() {
	cd "$srcdir/hamlib-$pkgver"

	export PYTHON=/usr/bin/python2

	./configure --prefix=/usr --sbindir=/usr/bin --with-perl-binding \
    --with-python-binding --enable-tcl-binding

	make
}

package() {
	cd "$srcdir/hamlib-$pkgver"

	make DESTDIR=$pkgdir install

	# fix perl module location
	cd $pkgdir/usr/lib/perl5/5.26/site_perl/
	mkdir current/
	mv auto current
	mv Hamlib.pm current
	rm perltest.pl

	/usr/bin/find $pkgdir -name '.packlist' -delete
	/usr/bin/find $pkgdir -name '*.pod' -delete
}
