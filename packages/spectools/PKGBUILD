# This file is part of BlackArch Linux ( http://blackarch.org ).
# See COPYING for license details.

pkgname='spectools'
pkgver='2011_08_R1'
pkgrel=1
pkgdesc='Spectrum-Tools is a set of utilities for using the Wi-Spy USB spectrum analyzer hardware. Stable version.'
arch=('x86_64' 'armv6h' 'armv7h' 'aarch64')
url='http://www.kismetwireless.net/spectools/'
license=('GPL2')
depends=('bash' 'libusb' 'gtk2' 'cairo')
groups=('blackarch' 'blackarch-wireless')
source=("http://www.kismetwireless.net/code/spectools-${pkgver//_/-}.tar.gz")
sha1sums=('971af469d05346f78a5499eac74d92e96dcb67db')

build() {
  cd "$srcdir/spectools-${pkgver//_/-}"

  autoreconf --install

  if [[ "$CARCH" == "i686" || "$CARCH" == "x86_64" ]];
  then
    ./configure --prefix=/usr --sysconfdir=/etc
  else
    ./configure --build=arm --prefix=/usr --sysconfdir=/etc
  fi

  make
}

package() {
  cd "$srcdir/spectools-${pkgver//_/-}"

  make DESTDIR="$pkgdir" install
}
