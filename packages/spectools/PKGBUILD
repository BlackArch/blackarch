# This file is part of BlackArch Linux ( https://www.blackarch.org/ ).
# See COPYING for license details.

pkgname=spectools
pkgver=3.0
pkgrel=1
pkgdesc='Spectrum-Tools is a set of utilities for using the Wi-Spy USB spectrum analyzer hardware.'
arch=('x86_64' 'aarch64')
groups=('blackarch' 'blackarch-wireless')
url='https://www.kismetwireless.net/spectools/'
license=('GPL-2.0-or-later')
depends=('libusb' 'gtk4' 'cairo' 'ncurses')
source=("https://github.com/Obsidian-Covenant/$pkgname/archive/refs/tags/$pkgver.tar.gz")
sha512sums=('7cbadf66300fbccbc78d4cd86cd90c452e7e38dd4196063f7ede51f6de5ae036a7a0079470fe3d500911def7d8e893627a78999d30a6686c03624f895ac0e413')

build() {
  cd "$pkgname-$pkgver"

  autoreconf -i

  if [[ "$CARCH" == "i686" || "$CARCH" == "x86_64" ]]; then
    ./configure --enable-gtk --enable-curses --prefix=/usr --sysconfdir=/etc
  else
    ./configure --enable-gtk --enable-curses --build=arm --prefix=/usr --sysconfdir=/etc
  fi

  make
}

package() {
  cd "$pkgname-$pkgver"

  make DESTDIR="$pkgdir" install
}

