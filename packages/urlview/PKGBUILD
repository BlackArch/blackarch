# This file is part of BlackArch Linux ( http://blackarch.org ).
# See COPYING for license details.

pkgname=urlview
pkgver=0.9
pkgrel=13
_patchlevel=20
pkgdesc='A curses URL parser for text files.'
arch=('x86_64' 'armv6h' 'armv7h' 'aarch64')
groups=('blackarch' 'blackarch-misc')
url="http://packages.qa.debian.org/u/urlview.html"
license=('GPL')
depends=('bash' 'ncurses')
makedepends=('patch' 'automake-1.11' 'autoconf')
source=("http://ftp.debian.org/debian/pool/main/u/urlview/urlview_${pkgver}.orig.tar.gz"
        "http://ftp.debian.org/debian/pool/main/u/urlview/urlview_${pkgver}-${_patchlevel}.diff.gz")
sha512sums=('68eb2140d93f60974fb6094131d48b6b2bf116ea904d53c6cba54a172933789849c3226cbf5d5565ae10a7717eb0c539390d4474f0617e9b85c3008be767c4fa'
            '1887d3cdea0660a0799ec37d7ff27d8e600234c1145b49d31c2a7659d137ec5546ec8edb39f9b30d4e1eaf9a6eb63de4dd5244a5473d8c90d42684feac98e004')

prepare() {
  cd "$srcdir/urlview-${pkgver}.orig"

  patch -Np1 -i "$srcdir/urlview_${pkgver}-$_patchlevel.diff"

  aclocal-1.11
}

build() {
  cd "$srcdir/urlview-${pkgver}.orig"

  ./configure --prefix=/usr

  make
}

package() {
  cd "$srcdir/urlview-${pkgver}.orig"

  install -dm 755 "$pkgdir/usr/bin"
  install -dm 755 "$pkgdir/etc/urlview"
  install -dm 755 "$pkgdir/usr/share/man/man1"

  make prefix="$pkgdir/usr" mandir="$pkgdir/usr/share/man" install

  install -Dm755 url_handler.sh "$pkgdir/etc/urlview/url_handler.sh"
  install -Dm644 sample.urlview "$pkgdir/etc/urlview/system.urlview"

  ln -fs /etc/urlview/url_handler.sh "$pkgdir/usr/bin/url_handler.sh"
}
