# This file is part of BlackArch Linux ( http://blackarch.org ).
# See COPYING for license details.

pkgname='urlview'
pkgver='0.9'
pkgrel=13
_patchlevel=20
pkgdesc='A curses URL parser for text files.'
arch=('x86_64' 'armv6h' 'armv7h' 'aarch64')
groups=('blackarch' 'blackarch-misc')
url="http://packages.qa.debian.org/u/urlview.html"
license=('GPL')
depends=('bash' 'ncurses')
makedepends=('patch' 'automake-1.11' 'autoconf')
source=("http://ftp.debian.org/debian/pool/main/u/urlview/urlview_${pkgver}.orig.tar.gz"
        "http://ftp.debian.org/debian/pool/main/u/urlview/urlview_${pkgver}-${_patchlevel}.diff.gz")
sha1sums=('323af9ba30ba87ec600531629f5dd84c720984b6'
          '897e2eb78adbfb0f6afbc9044b98a2b5491092e2')

prepare() {
  cd "$srcdir/urlview-${pkgver}.orig"

  patch -Np1 -i "$srcdir/urlview_${pkgver}-$_patchlevel.diff"

  aclocal-1.11
}

build() {
  cd "$srcdir/urlview-${pkgver}.orig"

  ./configure --prefix=/usr

  make
}

package() {
  cd "$srcdir/urlview-${pkgver}.orig"

  mkdir -p "$pkgdir/usr/bin"
  mkdir -p "$pkgdir/etc/urlview"
  mkdir -p "$pkgdir/usr/share/man/man1"

  make prefix="$pkgdir/usr" mandir="$pkgdir/usr/share/man" install

  install -Dm755 url_handler.sh "$pkgdir/etc/urlview/url_handler.sh"
  install -Dm644 sample.urlview "$pkgdir/etc/urlview/system.urlview"

  ln -fs /etc/urlview/url_handler.sh "$pkgdir/usr/bin/url_handler.sh"
}
