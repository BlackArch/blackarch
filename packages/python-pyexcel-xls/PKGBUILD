# This file is part of BlackArch Linux ( https://www.blackarch.org/ ).
# See COPYING for license details.

pkgname=python-pyexcel-xls
_pkgname=pyexcel-xls
pkgver=0.5.9
pkgrel=1
pkgdesc='A wrapper library to read, manipulate and write data in xls format. It reads xlsx and xlsm format.'
arch=('any')
url='https://pypi.org/project/pyexcel-xls/#files'
license=('BSD')
depends=('python' 'python-pyexcel-io' 'python-xlrd' 'python-xlwt')
makedepends=('python-setuptools')
source=("https://files.pythonhosted.org/packages/d9/77/47864a6b9e68343a922ca21722e0ddf8f2aa08505786efb42a785b7b98eb/$_pkgname-$pkgver.tar.gz")
sha512sums=('9255341194eb336ddd748d958548ce8a8280d94fdf235d228f04189f45a29ffb2da204777fb09402234760c630035973d8cc96024ad00108e73414b252094722')

prepare() {
  cd "$_pkgname-$pkgver"

  sed -i 's/pyexcel-io>=0.5.3/pyexcel-io/g' setup.py
}

build() {
  cd "$_pkgname-$pkgver"

  python setup.py build
}

package() {
  cd "$_pkgname-$pkgver"

  python setup.py install --root="$pkgdir" --prefix=/usr -O1 --skip-build
}

