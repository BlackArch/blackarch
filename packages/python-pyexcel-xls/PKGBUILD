# This file is part of BlackArch Linux ( https://www.blackarch.org/ ).
# See COPYING for license details.

pkgname=python-pyexcel-xls
_pkgname=pyexcel-xls
pkgver=0.6.2
pkgrel=1
pkgdesc='A wrapper library to read, manipulate and write data in xls format. It reads xlsx and xlsm format.'
arch=('any')
url='https://pypi.org/project/pyexcel-xls/#files'
license=('BSD')
depends=('python' 'python-pyexcel-io' 'python-xlrd' 'python-xlwt')
makedepends=('python-setuptools')
source=("https://files.pythonhosted.org/packages/9b/b5/9c0c1aaae80a0e500bc643e97a66f7a891964c152bbcb1f98534eb53098c/$_pkgname-$pkgver.tar.gz")
sha512sums=('21256407f4b8e087f50cae31fd28f04cd7f813964efb30708fb85bfc70a5f10aada94246690853a1f38b020841bda5cfebff73a28985e069c4ce61336c4c9e02')

prepare() {
  cd "$_pkgname-$pkgver"

  sed -i 's/pyexcel-io>=0.5.3/pyexcel-io/g' setup.py
}

build() {
  cd "$_pkgname-$pkgver"

  python setup.py build
}

package() {
  cd "$_pkgname-$pkgver"

  python setup.py install --root="$pkgdir" --prefix=/usr -O1 --skip-build
}

