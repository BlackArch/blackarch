# This file is part of BlackArch Linux ( http://blackarch.org ).
# See COPYING for license details.

pkgname='depant'
pkgver='0.3a'
pkgrel=8
pkgdesc='Check network for services with default passwords.'
arch=('any')
url='http://midnightresearch.com/projects/depant/'
license=('GPL')
groups=('blackarch' 'blackarch-cracker')
depends=('python2-ipcalc' 'hydra' 'nmap' 'python2-setuptools')
source=("http://web.archive.org/web/20140531100324/http://midnightresearch.com/local/packages/depant/depant-${pkgver}.tgz")
#source=("http://midnightresearch.com/local/packages/depant/depant-${pkgver}.tgz")
sha1sums=('96aef6bb863b5c34efafe877d3f6ce9bab41e8a0')

build() {
  cd "$srcdir/depant"

  python2 setup.py build
}

package() {
  cd "$srcdir/depant"

  install -D -m755 "build/lib/depant/depant.py" \
    "$pkgdir/usr/share/depant/depant.py"
  install -D -m644 "build/lib/depant/hydra.py" \
    "$pkgdir/usr/share/depant/hydra.py"
  install -D -m644 "build/lib/depant/nmap.py" "$pkgdir/usr/share/depant/nmap.py"
  install -D -m644 "README" "$pkgdir/usr/share/doc/depant/README"
  install -D -m644 "LICENSE" "$pkgdir/usr/share/licenses/depant/LICENSE"

  mkdir -p "$pkgdir/usr/bin"

  cat > "$pkgdir/usr/bin/depant"  << EOF
#!/bin/sh
cd /usr/share/depant
exec python2 depant.py "\$@"
EOF
  chmod 0755 "$pkgdir/usr/bin/depant"
}
