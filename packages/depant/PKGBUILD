# This file is part of BlackArch Linux ( http://blackarch.org ).
# See COPYING for license details.

pkgname=depant
pkgver=0.3a
pkgrel=8
pkgdesc='Check network for services with default passwords.'
arch=('any')
url='http://midnightresearch.com/projects/depant/'
license=('GPL')
groups=('blackarch' 'blackarch-cracker')
depends=('python2-ipcalc' 'hydra' 'nmap' 'python2-setuptools')
source=("http://web.archive.org/web/20140531100324/http://midnightresearch.com/local/packages/depant/depant-${pkgver}.tgz")
#source=("http://midnightresearch.com/local/packages/depant/depant-${pkgver}.tgz")
sha512sums=('d41681ac9a41bfcbb7314f3bbe08018a71869a7f5d04e86d89a109eb36d433e9d18dfcd3b5dff96b2753a55fd24b1a1d6b2f9bca465e5fb082cbf00d76c1b289')

build() {
  cd "$srcdir/depant"

  python2 setup.py build
}

package() {
  cd "$srcdir/depant"

  install -D -m755 "build/lib/depant/depant.py" \
    "$pkgdir/usr/share/depant/depant.py"
  install -D -m644 "build/lib/depant/hydra.py" \
    "$pkgdir/usr/share/depant/hydra.py"
  install -D -m644 "build/lib/depant/nmap.py" "$pkgdir/usr/share/depant/nmap.py"
  install -D -m644 "README" "$pkgdir/usr/share/doc/depant/README"
  install -D -m644 "LICENSE" "$pkgdir/usr/share/licenses/depant/LICENSE"

  install -dm 755 "$pkgdir/usr/bin"

  cat > "$pkgdir/usr/bin/depant"  << EOF
#!/bin/sh
cd /usr/share/depant
exec python2 depant.py "\$@"
EOF
  chmod 0755 "$pkgdir/usr/bin/depant"
}
