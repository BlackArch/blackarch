# This file is part of BlackArch Linux ( http://blackarch.org ).
# See COPYING for license details.

pkgname='gpredict'
pkgver='2.2.1'
pkgrel=1
pkgdesc='A real-time satellite tracking and orbit prediction application.'
arch=('x86_64' 'armv6h' 'armv7h' 'aarch64')
groups=('blackarch' 'blackarch-radio' 'blackarch-scanner')
license=('GPL')
url='http://gpredict.oz9aec.net/'
depends=('curl' 'gtk3' 'glib2' 'grig' 'goocanvas' 'hamlib')
makedepends=('git' 'intltool')
source=("https://github.com/csete/gpredict/archive/v${pkgver}.tar.gz")
sha1sums=('bc28b92a9974b07d624a93afa91cbc8093074c0a')

build() {
	cd "$srcdir/gpredict-$pkgver"

  ./autogen.sh

  ./configure --prefix=/usr

  make
}

package() {
	cd "$srcdir/gpredict-$pkgver"

  install -Dm644 -t "$pkgdir/usr/share/doc/gpredict" README NEWS AUTHORS \
    ChangeLog

  install -Dm644 doc/man/gpredict.1 "$pkgdir/usr/share/man/man1/gpredict.1"

  make DESTDIR="$pkgdir" install

  rm -rf "$pkgdir"/usr/share/{gpredict,applications}
}
