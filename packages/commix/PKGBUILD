# This file is part of BlackArch Linux ( http://blackarch.org ).
# See COPYING for license details.

pkgname=commix
pkgver=1191.456ed65
pkgrel=1
groups=('blackarch' 'blackarch-webapp' 'blackarch-automation'
        'blackarch-exploitation')
pkgdesc='Automated All-in-One OS Command Injection and Exploitation Tool.'
arch=('any')
url='https://github.com/stasinopoulos/commix'
license=('custom:unknown' 'GAGPL')
depends=('python2')
makedepends=('git')
source=('git+https://github.com/stasinopoulos/commix.git')
sha512sums=('SKIP')

pkgver() {
  cd "$srcdir/commix"

  echo $(git rev-list --count HEAD).$(git rev-parse --short HEAD)
}

prepare() {
  cd "$srcdir"

  sed -i '20 i\from os.path import expanduser' commix/src/utils/settings.py
  sed -i \
    's|OUTPUT_DIR = ".output/"|OUTPUT_DIR = expanduser("~") + "/commix-output/"|' \
    commix/src/utils/settings.py

  #patch -p1 < settings.py.patch
}

package() {
  cd "$srcdir/commix"

  install -dm 755 "$pkgdir/usr/bin"
  install -dm 755 "$pkgdir/usr/share/commix"

  install -Dm644 -t "$pkgdir/usr/share/doc/commix/" README.md readme/AUTHOR \
    readme/CHANGELOG.md
  install -Dm644 LICENSE.txt "$pkgdir/usr/share/licenses/commix/LICENSE.txt"

  rm -rf README.md readme/

  cp -a * "$pkgdir/usr/share/commix"

  cat > "$pkgdir/usr/bin/commix" << EOF
#!/bin/sh
cd /usr/share/commix
exec python2 commix.py "\${@}"
EOF

  chmod +x "$pkgdir/usr/bin/commix"
}
