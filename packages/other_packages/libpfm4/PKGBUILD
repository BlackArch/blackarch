# This file is part of BlackArch Linux ( https://www.blackarch.org/ ).
# See COPYING for license details.

pkgname=libpfm4
pkgver=4.10.1
conflicts=('libpfm3')
pkgrel=1
pkgdesc='Library to encode performance events for use by perf tool.'
arch=('x86_64' 'aarch64')
url='http://perfmon2.sourceforge.net/'
license=('MIT')
depends=('glibc')
source=("http://downloads.sourceforge.net/project/perfmon2/$pkgname/libpfm-$pkgver.tar.gz")
sha512sums=('33d99824216b4d83784e0db9f1aae3b39f84c2ba42dee64dde0b3c43cc5ee3a18c206d9044e75bf10867768add7cb9967c6318f7aa196f178f334271fa05e1aa')

build() {
  cd "libpfm-$pkgver"

  sed -i 's|PREFIX=/usr/local|PREFIX=/usr|g' config.mk
  sed -i 's|LDCONFIG=ldconfig|LDCONFIG=true|g' config.mk

  make
}

package() {
  cd "libpfm-$pkgver"

  make DESTDIR="$pkgdir" install
  make DESTDIR="$pkgdir" install_examples

  install -Dm 644 COPYING "$pkgdir/usr/share/licenses/$pkgname/LICENSE"
}

