# This file is part of BlackArch Linux ( https://www.blackarch.org/ ).
# See COPYING for license details.

pkgbase=oletools
pkgname=('python2-oletools' 'python-oletools')
pkgver=0.55.1
pkgrel=2
epoch=1
groups=('blackarch' 'blackarch-binary' 'blackarch-forensic')
pkgdesc='Tools to analyze Microsoft OLE2 files.'
arch=('any')
url='https://pypi.org/project/oletools/'
license=('GPL2')
makedepends=('python2-setuptools' 'python-setuptools')
source=("https://files.pythonhosted.org/packages/a4/56/1fdf589a15d9cc666666205d0ae4fbb6c55ca57362aa824eb5d5c4dd3753/$pkgbase-$pkgver.zip")
sha512sums=('e575510baeab50dcc3da26195d22e5e19fdc1c37ff732d07cf0a2390cc600f24709d84dbd446e22f20f05faa162bf9fa323c0a63394614da679b57764d46ba3f')

prepare() {
  cp -a "$pkgbase-$pkgver"{,-2}
}

build() {
  cd "$pkgbase-$pkgver-2"

  python2 setup.py build

  cd "$srcdir/$pkgbase-$pkgver"

  python setup.py build
}

package_python2-oletools() {
  depends=('python2' 'python2-colorclass' 'python2-pyparsing' 'python2-olefile'
           'python2-easygui' 'python2-pcodedmp' 'python2-msoffcrypto-tool')

  cd "$pkgbase-$pkgver-2"

  python2 setup.py install --root="$pkgdir" --prefix=/usr -O1 --skip-build

  install -Dm 644 -t "$pkgdir/usr/share/doc/${pkgname[0]}/" README.md
  install -Dm 644 oletools/LICENSE.txt \
    "$pkgdir/usr/share/licenses/${pkgname[0]}/LICENSE"

  # fixing shebang line
  cd "$pkgdir/usr/lib/python2.7/site-packages/$pkgbase/"
  for file in *.py; do
    if [ "${file}" != "__init__.py" ]; then
      sed -i '1s/python/python2/' "${file}"
    fi
  done

  for t in "$pkgdir/usr/bin/"* ; do
    mv $t "${t}2"
  done
}

package_python-oletools() {
  depends=('python' 'python-colorclass' 'python-pyparsing' 'python-olefile'
           'python-easygui' 'python-pcodedmp' 'python-msoffcrypto-tool')

  cd "$pkgbase-$pkgver"

  python setup.py install --root="$pkgdir" --prefix=/usr -O1 --skip-build

  install -Dm 644 -t "$pkgdir/usr/share/doc/$pkgbase/" README.md
  install -Dm 644 oletools/LICENSE.txt \
    "$pkgdir/usr/share/licenses/$pkgbase/LICENSE"

  cp oletools/doc/* "$pkgdir/usr/share/doc/$pkgbase"
}

