# This file is part of BlackArch Linux ( https://www.blackarch.org/ ).
# See COPYING for license details.

pkgname=libowfat
pkgver=0.32
pkgrel=1
pkgdesc='GPL reimplementation of libdjb.'
arch=('x86_64' 'aarch64')
url='http://www.fefe.de/libowfat/'
depends=('dietlibc')
license=('GPL')
options=('staticlibs')
source=("http://www.fefe.de/libowfat/libowfat-$pkgver.tar.xz")
sha512sums=('c762a1aa27dfb30e2f6da67d1ad16da03d301b2e3cce33c83b69103183a6689a494c8cf8d7d4e26ad5b22130e4e0560d5f3ef6f44b14d706f10e2300ce11ff3b')
install="$pkgname.install"

build() {
  cd "$pkgname-$pkgver"

  DIET= make
}

package() {
  cd "$pkgname-$pkgver"

  DIET= make prefix="$pkgdir/usr/" LIBDIR="$pkgdir/usr/lib" \
    MAN3DIR="$pkgdir/usr/share/man/man3" install

  cd "$pkgdir/usr/share/man/man3"

  mv buffer.3 buffer-libowfat.3

  # Fix a conflict with extra/tinycdb.
  rm "$pkgdir/usr/include/cdb.h"
}

