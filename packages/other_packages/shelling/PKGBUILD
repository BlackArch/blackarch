# This file is part of BlackArch Linux ( https://www.blackarch.org/ ).
# See COPYING for license details.

pkgname=shelling
pkgver=227.0a6c135
pkgrel=1
pkgdesc='An offensive approach to the anatomy of improperly written OS command injection sanitisers.'
groups=('blackarch' 'blackarch-misc')
arch=('any')
url='https://github.com/ewilded/shelling'
license=('custom:unknown')
depends=('java-environment')
makedepends=('git')
source=("git+https://github.com/ewilded/$pkgname.git")
sha512sums=('SKIP')

pkgver() {
  cd $pkgname

  echo $(git rev-list --count HEAD).$(git rev-parse --short HEAD)
}

package() {
  cd $pkgname

  install -dm 755 "$pkgdir/usr/bin"
  install -dm 755 "$pkgdir/usr/share/$pkgname"

  install -Dm 644 -t "$pkgdir/usr/share/doc/$pkgname/" README.md

  rm README.md

  cp -a * "$pkgdir/usr/share/$pkgname/"

  cat > "$pkgdir/usr/bin/$pkgname" << EOF
#!/bin/sh
cd /usr/share/$pkgname
exec java -jar SHELLING.jar "\$@"
EOF

  chmod +x "$pkgdir/usr/bin/$pkgname"
}

