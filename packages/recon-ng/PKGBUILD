# This file is part of BlackArch Linux ( https://www.blackarch.org/ ).
# See COPYING for license details.

pkgname=recon-ng
pkgver=5.0.1
pkgrel=1
epoch=1
groups=('blackarch' 'blackarch-recon')
pkgdesc='A full-featured Web Reconnaissance framework written in Python.'
arch=('any')
url='https://github.com/lanmaster53/recon-ng'
license=('GPL3')
depends=('python' 'python-dicttoxml' 'python-dnspython' 'python-unicodecsv'
         'python-jsonrpclib-pelix' 'python-lxml' 'python-mechanize'
         'python-slowaes' 'python-xlsxwriter' 'python-olefile' 'python-pypdf2'
         'python-flask' 'python-pyaml')
makedepends=('git')
source=("https://github.com/lanmaster53/recon-ng/archive/v$pkgver.tar.gz")
sha512sums=('8b336d8904b499093d3478136304ff35e2041fde986d0f0dad04a552ac8bf0e37d845c86dff2d02a67d25aafd42a42472c051a1b6970ec70fa37c392ff9293c4')

package() {
  cd "$pkgname-$pkgver"

  _bins='recon-ng recon-cli recon-rpc recon-web'

  install -dm 755 "$pkgdir/usr/bin"
  install -dm 755 "$pkgdir/usr/share/$pkgname"

  install -Dm 644 -t "$pkgdir/usr/share/doc/$pkgname" README.md REQUIREMENTS \
    VERSION
  install -Dm 644 LICENSE "$pkgdir/usr/share/licenses/$pkgname/LICENSE"

  rm LICENSE README.md

  cp -a * "$pkgdir/usr/share/$pkgname/"

  for bin in $_bins; do
    cat >> "$pkgdir/usr/bin/$bin" << EOF
#!/bin/sh
cd /usr/share/$pkgname
exec python $bin "\$@"
EOF
    chmod +x "$pkgdir/usr/bin/$bin"
  done
}

