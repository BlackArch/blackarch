# This file is part of BlackArch Linux ( https://www.blackarch.org/ ).
# See COPYING for license details.

pkgname=recon-ng
pkgver=4.9.6
pkgrel=1
epoch=1
groups=('blackarch' 'blackarch-recon')
pkgdesc='A full-featured Web Reconnaissance framework written in Python.'
arch=('any')
url='https://bitbucket.org/LaNMaSteR53/recon-ng'
license=('GPL3')
depends=('python2' 'python2-dicttoxml' 'python2-dnspython'
         'python2-jsonrpclib-pelix'
         'python2-lxml' 'python2-mechanize' 'python2-slowaes'
         'python2-xlsxwriter' 'python2-olefile' 'python2-pypdf2'
         'python2-flask' 'python2-unicodecsv')
makedepends=('git')
source=("https://bitbucket.org/LaNMaSteR53/$pkgname/get/v$pkgver.tar.bz2")
sha512sums=('a6c36f84eca6a51b3db9e9f53bef45f7b1eb39cbc59af201bfe2eda709dc28638db5ba298cf0664ec63819a82ad37593e2e3c8ed6b8b5c1dd1411df1f3a392a9')

package() {
  cd "LaNMaSteR53-$pkgname-1a691939a77f"

  _bins='recon-ng recon-cli recon-rpc recon-web'

  install -dm 755 "$pkgdir/usr/bin"
  install -dm 755 "$pkgdir/usr/share/$pkgname"

  install -Dm 644 -t "$pkgdir/usr/share/doc/$pkgname" README.md REQUIREMENTS \
    VERSION
  install -Dm 644 LICENSE "$pkgdir/usr/share/licenses/$pkgname/LICENSE"

  rm LICENSE README.md

  cp -a * "$pkgdir/usr/share/$pkgname/"

  for bin in $_bins
  do
    cat >> "$pkgdir/usr/bin/$bin" << EOF
#!/bin/sh
cd /usr/share/$pkgname
exec python2 $bin "\$@"
EOF
    chmod +x "$pkgdir/usr/bin/$bin"
  done
}

