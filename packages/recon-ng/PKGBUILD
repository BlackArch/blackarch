# This file is part of BlackArch Linux ( http://blackarch.org ).
# See COPYING for license details.

pkgname='recon-ng'
pkgver=870.4f2b9f6
pkgrel=1
groups=('blackarch' 'blackarch-recon')
pkgdesc='A full-featured Web Reconnaissance framework written in Python.'
arch=('any')
url='https://bitbucket.org/LaNMaSteR53/recon-ng'
license=('GPL3')
depends=('python2')
makedepends=('git')
source=("git+https://bitbucket.org/LaNMaSteR53/recon-ng.git")
sha1sums=('SKIP')

pkgver() {
  cd "$srcdir/recon-ng"

  echo $(git rev-list --count HEAD).$(git rev-parse --short HEAD)
}

prepare() {
  cd "$srcdir/recon-ng"

  sed -i '1s/env python/env python2/' "recon-ng"
}

package() {
  cd "$srcdir/recon-ng"

  local _bins='recon-ng recon-cli recon-rpc'

  mkdir -p "$pkgdir/usr/bin"
  mkdir -p "$pkgdir/usr/share/recon-ng"

  cp -a * "$pkgdir/usr/share/recon-ng"

  for bin in $_bins
  do
    cat >> "$pkgdir/usr/bin/$bin" << EOF
#!/bin/sh
cd /usr/share/$bin
exec python2 $bin
EOF
    chmod +x "$pkgdir/usr/bin/$bin"
  done
}
