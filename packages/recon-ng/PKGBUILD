# This file is part of BlackArch Linux ( http://blackarch.org ).
# See COPYING for license details.

pkgname='recon-ng'
pkgver='4.7.1'
pkgrel=2
epoch=1
groups=('blackarch' 'blackarch-recon')
pkgdesc='A full-featured Web Reconnaissance framework written in Python.'
arch=('any')
url='https://bitbucket.org/LaNMaSteR53/recon-ng'
license=('GPL3')
depends=('python2' 'python2-xlsxwriter' 'python2-dict2xml' 'python2-pypdf2')
makedepends=('git')
source=("https://bitbucket.org/LaNMaSteR53/recon-ng/get/v${pkgver}.tar.bz2")
sha1sums=('77dc1f17cafbd84f5a824a1a8f50c7b8916b0093')

prepare() {
  cd "$srcdir/LaNMaSteR53-recon-ng-131c609abc42"

  sed -i '1s/env python/env python2/' "recon-ng"
}

package() {
  cd "$srcdir/LaNMaSteR53-recon-ng-131c609abc42"

  local _bins='recon-ng recon-cli recon-rpc'

  mkdir -p "$pkgdir/usr/bin"
  mkdir -p "$pkgdir/usr/share/recon-ng"

  install -Dm644 -t "$pkgdir/usr/share/doc/recon-ng" README.md REQUIREMENTS \
    VERSION
  install -Dm644 LICENSE "$pkgdir/usr/share/licenses/recon-ng/LICENSE"

  rm LICENSE README.md

  cp -a * "$pkgdir/usr/share/recon-ng"

  for bin in $_bins
  do
    cat >> "$pkgdir/usr/bin/$bin" << EOF
#!/bin/sh
cd /usr/share/$bin
exec python2 $bin
EOF
    chmod +x "$pkgdir/usr/bin/$bin"
  done
}
