# This file is part of BlackArch Linux ( https://www.blackarch.org/ ).
# See COPYING for license details.

pkgbase=python-virustotal-api
pkgname=('python2-virustotal-api' 'python-virustotal-api')
_pkgname=virustotal-api
pkgver=1.1.11
pkgrel=3
pkgdesc='Virus Total Public/Private/Intel API.'
arch=('any')
url='https://pypi.org/project/virustotal-api/#files'
license=('MIT')
makedepends=('python2-setuptools' 'python-setuptools')
source=("https://files.pythonhosted.org/packages/2a/44/2780b13218eb0b417ee3bf57b24df82496853c14d30524b43a89b8cc9222/$_pkgname-$pkgver.tar.gz")
sha512sums=('af29d4f5600753fc7d07a7a0b2846463dc484c4068151844fdbdc8a9c949556b1f4849175f7a4d0bce20cfd37a419f7f000aa5235003716a31dd9cf1899e2991')

prepare() {
  cp -a "$_pkgname-$pkgver"{,-2}
}

build() {
  cd "$_pkgname-$pkgver-2"

  python2 setup.py build

  cd "$srcdir/$_pkgname-$pkgver"

  python setup.py build
}

package_python2-virustotal-api() {
  depends=('python2' 'python2-requests')

  cd "$_pkgname-$pkgver-2"

  python2 setup.py install --prefix=/usr --root="$pkgdir" --optimize=1 \
    --skip-build
}

package_python-virustotal-api() {
  depends=('python' 'python-requests')

  cd "$_pkgname-$pkgver"

  python setup.py install --prefix=/usr --root="$pkgdir" --optimize=1 \
    --skip-build
}

