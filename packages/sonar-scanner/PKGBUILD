# This file is part of BlackArch Linux ( https://www.blackarch.org/ ).
# See COPYING for license details.
#
# This PKGBUILD was extracted from AUR and adapted to our style.
# AUR Maintainer: Marcell Meszaros < marcell.meszaros AT runbox.eu >
# AUR Contributor: max-k <max-kATpostDOTcom>

pkgname=sonar-scanner
pkgver=6.2.1.4610
pkgrel=1
pkgdesc='Generic CLI tool to launch project analysis on SonarQube servers.'
groups=('blackarch' 'blackarch-code-audit')
arch=('any')
url='https://docs.sonarqube.org/latest/analysis/scan/sonarscanner/'
license=('LGPL3')
depends=('java-runtime' 'sh')
backup=("opt/$pkgname/conf/$pkgname.properties")
source=("https://binaries.sonarsource.com/Distribution/$pkgname-cli/$pkgname-cli-$pkgver.zip"
        "$pkgname.sh"
        "$pkgname.install")
sha512sums=('4da4447df630270738ee0a803bfd1d081e0c29a0783572eebada186dd808f2fd26b60de29df9cb6d284279cad215a708d3678aee8dbcbbe1f8f924ac2a267372'
            'd31374e9cf8a7e814655b1e4dca0496e90f973b87df056fa84756b4824cd59dabe0dcf4419f0ff6428e3cb43707d6586be07ec7de31d9c39a828bb18808d0fcd'
            '5f54133c386fe7d93618df04abe12105d846ca24f9f0706ea02055013f9623641281d8e70507afd6f04a77c98e7af11ed9ed3ef346ed4abe728b069e4f1a57f4')

package() {
    install -Dm755 --target-directory="$pkgdir/etc/profile.d/" "$pkgname.sh"

    cd "$pkgname-$pkgver"
    install -Dm755 --target-directory="$pkgdir/opt/$pkgname/bin/" "bin/$pkgname"
    install -Dm644 --target-directory="$pkgdir/opt/$pkgname/lib/" "lib/$pkgname-cli-$pkgver.jar"
    install -Dm644 --target-directory="$pkgdir/opt/$pkgname/conf/" "conf/sonar-scanner.properties"
    ln -sf "/opt/$pkgname/conf" "$pkgdir/etc/$pkgname"
}

