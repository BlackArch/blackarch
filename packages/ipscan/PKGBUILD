# This file is part of BlackArch Linux ( http://blackarch.org ).
# See COPYING for license details.

pkgname='ipscan'
pkgver='3.4.2'
pkgrel=1
groups=('blackarch' 'blackarch-scanner')
pkgdesc='Angry IP scanner is a very fast IP address and port scanner.'
arch=('i686' 'x86_64')
license=('GPL2')
url='http://www.angryziber.com/'
depends=('java-environment')
if [[ $CARCH == 'x86_64' ]]; then
  source=("https://github.com/angryziber/ipscan/releases/download/$pkgver/ipscan-linux64-${pkgver}.jar")
  sha1sums=('662b9c7ea9633455d7a790ce2e2c48424a50928e')
else
  source=("https://github.com/angryziber/ipscan/releases/download/$pkgver/ipscan-linux-${pkgver}.jar")
  sha1sums=('063bd27872c680ce1ebf89c8d95c47617010288e')
fi

package() {
  cd "$srcdir"

  mkdir -p "$pkgdir/usr/bin"

  if [[ $CARCH == 'i686' ]];
  then
    install -Dm644 ipscan-linux-${pkgver}.jar \
      "$pkgdir/usr/share/ipscan/ipscan-linux-${pkgver}.jar"
  else
    install -Dm644 ipscan-linux64-${pkgver}.jar \
      "$pkgdir/usr/share/ipscan/ipscan-linux-${pkgver}.jar"
  fi

  cat > "$pkgdir/usr/bin/ipscan" << EOF
#!/bin/sh
exec java -jar /usr/share/ipscan/ipscan-linux-${pkgver}.jar
EOF

  chmod +x "$pkgdir/usr/bin/ipscan"
}
