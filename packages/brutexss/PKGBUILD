# This file is part of BlackArch Linux ( http://blackarch.org ).
# See COPYING for license details.

pkgname='brutexss'
pkgver=54.ba753df
pkgrel=1
pkgdesc='Cross-Site Scripting Bruteforcer.'
groups=('blackarch' 'blackarch-webapp' 'blackarch-fuzzer')
arch=('any')
url='https://github.com/shawarkhanethicalhacker/BruteXSS'
license=('GPL3')
depends=('python2' 'python2-lxml')
makedepends=('git')
source=('git+https://github.com/shawarkhanethicalhacker/BruteXSS.git')
sha1sums=('SKIP')

pkgver() {
  cd "$srcdir/BruteXSS"

  echo $(git rev-list --count HEAD).$(git rev-parse --short HEAD)
}

package() {
  cd "$srcdir/BruteXSS"

  mkdir -p "$pkgdir/usr/bin"
  mkdir -p "$pkgdir/usr/share/brutexss"

  install -Dm644 -t "$pkgdir/usr/share/doc/brutexss/" README.md
  install -Dm644 License.txt "$pkgdir/usr/share/licenses/brutexss/License.txt"

  rm README.md License.txt

  cp -a * "$pkgdir/usr/share/brutexss"

  cat > "$pkgdir/usr/bin/brutexss" << EOF
#!/bin/sh
cd /usr/share/brutexss
exec python2 brutexss.py "\${@}"
EOF

  chmod a+x "$pkgdir/usr/bin/brutexss"
}
