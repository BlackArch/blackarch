# This file is part of BlackArch Linux ( https://www.blackarch.org/ ).
# See COPYING for license details.

pkgname=mybff
pkgver=94.6547c51
pkgrel=1
pkgdesc='A Brute Force Framework.'
groups=('blackarch' 'blackarch-cracker')
arch=('any')
url='https://github.com/MooseDojo/myBFF'
license=('BSD')
depends=('python2' 'python2-requests' 'python2-lxml' 'python2-selenium'
         'python2-beautifulsoup4')
makedepends=('git')
source=("$pkgname::git+https://github.com/MooseDojo/myBFF.git")
sha512sums=('SKIP')

pkgver() {
  cd $pkgname

  echo $(git rev-list --count HEAD).$(git rev-parse --short HEAD)
}

package() {
  cd $pkgname

  install -dm 755 "$pkgdir/usr/bin"
  install -dm 755 "$pkgdir/usr/share/$pkgname"

  install -Dm 644 -t "$pkgdir/usr/share/doc/$pkgname/" README.md
  install -Dm 644 LICENSE.txt "$pkgdir/usr/share/licenses/$pkgname/LICENSE"

  rm *.md

  cp -a * "$pkgdir/usr/share/$pkgname"

  cat > "$pkgdir/usr/bin/$pkgname" << EOF
#!/bin/sh
cd /usr/share/$pkgname
exec python2 myBFF.py "\$@"
EOF

  chmod +x "$pkgdir/usr/bin/$pkgname"
}

