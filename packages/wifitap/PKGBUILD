# This file is part of BlackArch Linux ( http://blackarch.org ).
# See COPYING for license details.

_gitname="GDSSecurity"
pkgname='wifitap'
pkgver='2b16088'
pkgrel=6
groups=('blackarch' 'blackarch-wireless')
pkgdesc='WiFi injection tool through tun/tap device.'
arch=('any')
url='https://github.com/GDSSecurity/wifitap'
license=('GPL2')
depends=('python2' 'scapy')
optdepends=('pysco2: JIT optimization')
makedepends=('git')
source=('git+https://github.com/GDSSecurity/wifitap.git')
sha1sums=('SKIP')

pkgver() {
  cd "$srcdir/wifitap"

  git describe --always | sed 's|-|.|g'
}

prepare() {
  cd "$srcdir/wifitap"

  # Fix python shebangs.
  sed -i 's|env python|env python2|' *.py

  # Fix imports.
  sed -i 's|import IP,ICMP|import *|' wifiping.py
  sed -i 's|import ARP|import *|' wifiarp.py
}

package() {
  cd "$srcdir/wifitap"

  # Base directories.
  mkdir -p "$pkgdir/usr/bin"
  mkdir -p "$pkgdir/usr/share/wifitap"

  # Bin.
  for file in *.py ; do
    install -m755 "$file" "$pkgdir/usr/bin/${file%.*}"
  done

  # Docs.
  install -m644 README TODO BUGS "$pkgdir/usr/share/wifitap"
}
