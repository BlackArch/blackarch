# This file is part of BlackArch Linux ( http://blackarch.org ).
# See COPYING for license details.

_gitname="GDSSecurity"
pkgname=wifitap
pkgver=2b16088
pkgrel=9
groups=('blackarch' 'blackarch-wireless')
pkgdesc='WiFi injection tool through tun/tap device.'
arch=('any')
url='https://github.com/GDSSecurity/wifitap'
license=('GPL2')
depends=('python2' 'python2-scapy')
optdepends=('pysco2: JIT optimization')
makedepends=('git')
source=('git+https://github.com/GDSSecurity/wifitap.git')
sha512sums=('SKIP')

pkgver() {
  cd "$srcdir/wifitap"

  git describe --always | sed 's|-|.|g'
}

prepare() {
  cd "$srcdir/wifitap"

  sed -i 's|env python|env python2|' *.py
  sed -i 's|import IP,ICMP|import *|' wifiping.py
  sed -i 's|import ARP|import *|' wifiarp.py
}

package() {
  cd "$srcdir/wifitap"

  install -dm 755 "$pkgdir/usr/bin"
  install -dm 755 "$pkgdir/usr/share/wifitap"

  for file in *.py ; do
    install -m755 "$file" "$pkgdir/usr/bin/${file%.*}"
  done

  install -Dm644 -t "$pkgdir/usr/share/wifitap" README TODO BUGS
}
