# This file is part of BlackArch Linux ( https://www.blackarch.org/ ).
# See COPYING for license details.

pkgname=ddosify
pkgver=v0.13.1.r0.g0c3ff28
pkgrel=1
pkgdesc='High-performance load testing tool, written in Golang.'
arch=('x86_64' 'aarch64')
groups=('blackarch' 'blackarch-dos' 'blackarch-misc')
url='https://github.com/ddosify/ddosify'
license=('AGPL')
depends=('glibc')
makedepends=('git' 'go')
source=("git+https://github.com/ddosify/$pkgname.git")
sha256sums=('SKIP')

pkgver() {
  cd $pkgname

  git describe --long --tags | sed 's/\([^-]*-g\)/r\1/;s/-/./g'
}

build() {
  cd $pkgname

  GOPATH="$srcdir" go mod download
  GOPATH="$srcdir" go build \
    -trimpath \
    -buildmode=pie \
    -mod=readonly \
    -modcacherw \
    -ldflags "-s -w" \
    -o $pkgname .

}

package() {
  cd $pkgname

  install -Dm 755 $pkgname "$pkgdir/usr/bin/$pkgname"
  install -Dm 644 -t "$pkgdir/usr/share/doc/$pkgname/" *.md

  install -Dm 644 LICENSE "$pkgdir/usr/share/licenses/$pkgname/LICENSE"
}

