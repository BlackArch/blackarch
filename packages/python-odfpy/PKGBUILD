# This file is part of BlackArch Linux ( https://www.blackarch.org/ ).
# See COPYING for license details.

pkgbase=python-odfpy
pkgname=('python2-odfpy' 'python-odfpy')
_pkgname=odfpy
pkgver=1.4.0
pkgrel=2
pkgdesc='A complete API for OpenDocument in Python. Python 2.x support.'
arch=('any')
url='https://pypi.org/project/odfpy/'
license=('Apache')
makedepends=('python2-setuptools' 'python-setuptools')
source=("https://files.pythonhosted.org/packages/85/7d/8f6d1f2a4683be362b101c00232b4c3839e4e4a90e0945d8d43ec6aa671d/$_pkgname-$pkgver.tar.gz")
sha512sums=('10b001588ad4ae9779740f6a3ff77073f6abf500eb2db7975d50f8026b2ab12d9df89d4ca87434a6f73dc13dfe4661bff7007dfa265783ecc410816e73f482d2')

prepare() {
  cp -a "$_pkgname-$pkgver" "$_pkgname-$pkgver-2"
}

build() {
  cd "$_pkgname-$pkgver-2"

  python2 setup.py build

  cd "$srcdir/$_pkgname-$pkgver"

  python setup.py build
}

package_python2-odfpy() {
  depends=('python2')

  cd "$_pkgname-$pkgver-2"

  python2 setup.py install --prefix=/usr --root="$pkgdir" --optimize=1 \
    --skip-build
}

package_python-odfpy() {
  depends=('python')

  cd "$_pkgname-$pkgver"

  python setup.py install --prefix=/usr --root="$pkgdir" --optimize=1 \
    --skip-build
}

