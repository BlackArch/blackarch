# This file is part of BlackArch Linux ( https://www.blackarch.org/ ).
# See COPYING for license details.

pkgname=pin
pkgver=3.11.r97998
_pkgver=3.11-97998-g7ecce2dac
pkgrel=1
pkgdesc='A dynamic binary instrumentation tool.'
groups=('blackarch' 'blackarch-automation' 'blackarch-binary'
        'blackarch-reversing')
arch=('x86_64')
url="https://software.intel.com/en-us/articles/pin-a-binary-instrumentation-tool-downloads"
license=('custom')
depends=('gcc' 'lib32-gcc-libs')
makedepends=('gcc' 'lib32-gcc-libs' 'make')
options=('!strip' 'staticlibs')
source=("https://software.intel.com/sites/landingpage/pintool/downloads/$pkgname-$_pkgver-gcc-linux.tar.gz")
sha512sums=('74caa0b9c30de3d351e8e0f961cf96f293ca038ebe4d8be21757a82f88963c80658435df534e068a982117f1141659198039e26c7dd6b04092d1558fbbd3a7e6')

build() {
  cd "$pkgname-$_pkgver-gcc-linux/source/tools/ManualExamples"

  TARGET=intel64 make

  TARGET=ia32 make
}

package() {
  cd "$pkgname-$_pkgver-gcc-linux"

  install -dm 755 "$pkgdir/usr/bin"
  install -dm 755 "$pkgdir/usr/share/$pkgname"

  cp -a --no-preserve=ownership * "$pkgdir/usr/share/$pkgname/"

  ln -fs "/usr/share/$pkgname/$pkgname" "$pkgdir/usr/bin/$pkgname"
}

