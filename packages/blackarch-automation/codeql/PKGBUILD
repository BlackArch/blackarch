# This file is part of BlackArch Linux ( https://www.blackarch.org/ ).
# See COPYING for license details.

pkgname=codeql
pkgver=2.2.5
pkgrel=1
epoch=1
groups=('blackarch' 'blackarch-automation' 'blackarch-code-audit')
pkgdesc='The CLI tool for GitHub CodeQL'
arch=('x86_64')
url='https://github.com/codeql'
license=('custom')
depends=('java-runtime-headless' 'bash')
source=("$pkgname-v$pkgver.zip::https://github.com/github/codeql-cli-binaries/releases/download/v$pkgver/$pkgname-linux64.zip")
sha512sums=('528fe094a9c56de8f68c1d2cbda65308a09cbde38cb2edd754da62908e7dee1eba1cd1e47dd8b564c0d5903dd6a4bdd34d35be45becd4ea4edc60b207465d14e')

prepare() {
  ln -sf "$pkgname/tools/linux64/lib64trace.so" \
    "$pkgname/tools/linux64/libtrace.so"
}

package() {
  install -dm 755 "$pkgdir/usr/bin"
  install -dm 755 "$pkgdir/usr/share/$pkgname"

  install -Dm 644 "$pkgname/LICENSE.md" \
    "$pkgdir/usr/share/licenses/$pkgname/LICENSE"

  cp -a $pkgname "$pkgdir/usr/share/"

  cat > "$pkgdir/usr/bin/$pkgname" << EOF
#!/bin/sh
exec /usr/share/$pkgname/$pkgname "\$@"
EOF

  chmod +x "$pkgdir/usr/bin/$pkgname"
}

