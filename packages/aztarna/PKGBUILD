# This file is part of BlackArch Linux ( http://blackarch.org ).
# See COPYING for license details.

pkgname=aztarna
pkgver=1.0
pkgrel=1
pkgdesc='A footprinting tool for ROS and SROS systems.'
arch=('any')
url='https://github.com/aliasrobotics/aztarna'
groups=('blackarch' 'blackarch-recon' 'blackarch-fingerprint')
license=('GPL3')
depends=('python' 'python-aiohttp' 'python-aiohttp-xmlrpc' 'python-argcomplete'
         'python-asn1crypto' 'python-yarl' 'python-async-timeout' 'python-attrs'
         'python-babel' 'python-certifi' 'python-cffi' 'python-chardet'
         'python-click' 'python-click-plugins' 'python-colorama' 'python-idna'
         'python-cryptography' 'python-dnspython' 'python-docutils'
         'python-idna_ssl' 'python-imagesize' 'python-ipwhois' 'python-jinja'
         'python-lxml' 'python-markupsafe' 'python-multidict' 'python-packaging'
         'python-pycparser' 'python-pygments' 'python-pyparsing' 'python-pytz'
         'python-requests' 'python-scapy' 'python-shodan' 'python-six'
         'python-snowballstemmer' 'python-sphinx' 'python-urllib3'
         'python-sphinxcontrib-websupport' 'python-urllib3' 'python-uvloop'
         'python-xlsxwriter')
makedepends=('python-setuptools')
source=("https://files.pythonhosted.org/packages/d4/48/cc7f5a54291524518debcf4785e2ad7523e1eb9e2607124774f0a42eda4e/$pkgname-$pkgver.tar.gz"
        'setup.patch')
sha512sums=('2dfa7d67a079e5fba3534c77116c52bcc0d206bcc4477d0ed9b215c5a1096fc71532f083e81631955f5807a093e42c4d6ed2e416d5aa0c42010bb79f07c6b152'
            '83d38860398c853c98e9821ba9c90fc8f07b779f08e42b65acdbdc4cc3cbd8e74bdd9ddc92bde6e66e46d24dab7229c0f6963fe542a3ba6217f1afc5de0e4566')

prepare() {
  cd "$pkgname-$pkgver"

  patch setup.py < "$srcdir/setup.patch"
}

build() {
  cd "$pkgname-$pkgver"

  python setup.py build
}

package() {
  cd "$pkgname-$pkgver"

  python setup.py install --root="$pkgdir" -O1 --prefix=/usr --skip-build
}

