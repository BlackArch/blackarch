# This file is part of BlackArch Linux ( https://www.blackarch.org/ ).
# See COPYING for license details.

pkgname=tempomail
pkgver=16.298fe33
pkgrel=1
groups=('blackarch' 'blackarch-misc')
pkgdesc='Tool to create a temporary email address in 1 Second and receive emails. '
arch=('x86_64' 'aarch64')
url='https://github.com/kavishgr/tempomail'
license=('MIT')
depends=('glibc')
makedepends=('git' 'go-pie')
source=("git+https://github.com/kavishgr/$pkgname.git")
sha512sums=('SKIP')

pkgver() {
  cd $pkgname

  echo $(git rev-list --count HEAD).$(git rev-parse --short HEAD)
}

build() {
  cd $pkgname

  GOPATH="$srcdir" go get -d -t .
  GOPATH="$srcdir" go build -v .
}

package() {
  cd $pkgname

  install -Dm 755 $pkgname "$pkgdir/usr/bin/$pkgname"
  install -Dm 644 -t "$pkgdir/usr/share/doc/$pkgname/" README.md
}

