# This file is part of BlackArch Linux ( https://www.blackarch.org/ ).
# See COPYING for license details.

pkgname=zdns
pkgver=237.8c53210
pkgrel=2
groups=('blackarch' 'blackarch-networking')
pkgdesc='Fast CLI DNS Lookup Tool.'
arch=('x86_64' 'aarch64')
url='https://github.com/zmap/zdns'
license=('Apache')
makedepends=('git' 'go')
source=("git+https://github.com/zmap/$pkgname.git")
sha512sums=('SKIP')

pkgver() {
  cd $pkgname

  echo $(git rev-list --count HEAD).$(git rev-parse --short HEAD)
}

build() {
  cd $pkgname

  GOPATH="$srcdir" go mod download
  GOPATH="$srcdir" go build \
    -trimpath \
    -buildmode=pie \
    -mod=readonly \
    -modcacherw \
    -ldflags "-s -w" \
    -o $pkgname-bin ./$pkgname
}

package() {
  cd $pkgname

  install -Dm 755 "$pkgname-bin" "$pkgdir/usr/bin/$pkgname"
  install -Dm 644 -t "$pkgdir/usr/share/doc/$pkgname" README.md
  install -Dm 644 -t "$pkgdir/usr/share/licenses/$pkgname" LICENSE
}

