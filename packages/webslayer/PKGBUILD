# This file is part of BlackArch Linux ( http://blackarch.org ).
# See COPYING for license details.

pkgname=webslayer
pkgver=5
pkgrel=7
groups=('blackarch' 'blackarch-webapp')
pkgdesc='A tool designed for brute forcing Web Applications.'
arch=('any')
url='https://code.google.com/p/webslayer/'
license=('GPL')
depends=('python2-pycurl' 'python2-pyqt4')
makedepends=('subversion')
source=("svn+http://webslayer.googlecode.com/svn/trunk/")
sha512sums=('SKIP')

prepare() {
  cd "$srcdir/trunk"

  sed -i 's|bin/python$|bin/python2|' *.py
}

pkgver() {
  cd "$srcdir/trunk"

  svnversion | tr -d [A-z]
}

package() {
  cd "$srcdir/trunk"

  install -dm 755 "$pkgdir/usr/bin"
  install -dm 755 "$pkgdir/usr/share/webslayer"
  install -dm 755 "$pkgdir/usr/share/doc/webslayer"

  cp -R --no-preserve=ownership * "$pkgdir/usr/share/webslayer"

  rm "$pkgdir/usr/share/webslayer"/{COPYING,LICENSES}

  mv "$pkgdir/usr/share/webslayer/README" "$pkgdir/usr/share/doc/webslayer"

  cat > "$pkgdir/usr/bin/webslayer" << EOF
#!/bin/sh
cd /usr/share/webslayer
exec ./WebSlayer.py "$@"
EOF

  chmod +x "$pkgdir/usr/bin/webslayer"
}
