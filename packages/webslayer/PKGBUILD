# This file is part of BlackArch Linux ( http://blackarch.org ).
# See COPYING for license details.

pkgname='webslayer'
pkgver='5'
pkgrel=5
groups=('blackarch' 'blackarch-webapp')
pkgdesc="A tool designed for brute forcing Web Applications"
arch=('any')
url="https://code.google.com/p/webslayer/"
license=('GPL')
depends=('python2-pycurl' 'python2-pyqt4')
source=("svn+http://webslayer.googlecode.com/svn/trunk/")
sha1sums=('SKIP')

prepare() {
  cd "$srcdir/trunk"

  # Fix shebangs.
  sed -i 's|bin/python$|bin/python2|' *.py
}

pkgver() {
  cd trunk
  svnversion | tr -d [A-z]
}

package() {
  cd "$srcdir/trunk"

  # Base directories.
  mkdir -p "$pkgdir/usr/bin"
  mkdir -p "$pkgdir/usr/share/webslayer"
  mkdir -p "$pkgdir/usr/share/doc/webslayer"

  cp -R --no-preserve=ownership * "$pkgdir/usr/share/webslayer"

  # These are redundant.
  rm "$pkgdir/usr/share/webslayer"/{COPYING,LICENSES}

  mv "$pkgdir/usr/share/webslayer/README" "$pkgdir/usr/share/doc/webslayer"

  # Joint script.
  cat > "$pkgdir/usr/bin/webslayer" <<\EOF
#!/bin/sh
cd /usr/share/webslayer
./WebSlayer.py "$@"
EOF
  chmod +x "$pkgdir/usr/bin/webslayer"
}
