# This file is part of BlackArch Linux ( https://www.blackarch.org/ ).
# See COPYING for license details.

pkgbase=python-macholib
pkgname=('python2-macholib' 'python-macholib')
_pkgname=macholib
pkgver=1.15
pkgrel=1
pkgdesc='Mach-O header analysis and editing.'
arch=('any')
url='https://pypi.org/project/macholib/#files'
license=('MIT')
makedepends=('python2-setuptools' 'python-setuptools')
source=("https://files.pythonhosted.org/packages/5f/cd/045e6e025d7484eef8c534a0ffe98792fd1ea19aadc8ac048a5ed9272e9d/$_pkgname-$pkgver.tar.gz")
sha512sums=('66a4660752a7d25649ac179dc11415b6859758746e3825a0678929f70fe14f7ba79760a8b5901061e0de8d60fd6bd911667ce0e489e153395c1a669d62bf580a')

prepare() {
  cp -a "$_pkgname-$pkgver"{,-2}
}

build() {
  cd "$_pkgname-$pkgver-2"

  python2 setup.py build

  cd "$srcdir/$_pkgname-$pkgver"

  python setup.py build
}

package_python2-macholib() {
  depends=('python2')

  cd "$_pkgname-$pkgver-2"

  python2 setup.py install --prefix=/usr --root="$pkgdir" --optimize=1 \
    --skip-build

  for tool in 'macho_dump' 'macho_find' 'macho_standalone'; do
    mv "$pkgdir/usr/bin/$tool" "$pkgdir/usr/bin/${tool}-2"
  done
}

package_python-macholib() {
  depends=('python')

  cd "$_pkgname-$pkgver"

  python setup.py install --prefix=/usr --root="$pkgdir" --optimize=1 \
    --skip-build
}

