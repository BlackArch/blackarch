# This file is part of BlackArch Linux ( https://www.blackarch.org/ ).
# See COPYING for license details.

pkgbase=python-macholib
pkgname=('python2-macholib' 'python-macholib')
_pkgname=macholib
pkgver=1.15.1
pkgrel=1
pkgdesc='Mach-O header analysis and editing.'
arch=('any')
url='https://pypi.org/project/macholib/#files'
license=('MIT')
makedepends=('python2-setuptools' 'python-setuptools')
source=("https://files.pythonhosted.org/packages/aa/0b/9087a2206f4441504711d8df8d08f49afa20a431a92ab14175f3c3c80f3c/$_pkgname-$pkgver.tar.gz")
sha512sums=('4c1511c9784cd004efd2b249b09ee283a38b4bbca7608da311b61e4edbca620a721b968aca03131c2d6bcef84bc840815c887b624c936f05082ef666af4029c0')

prepare() {
  cp -a "$_pkgname-$pkgver"{,-2}
}

build() {
  cd "$_pkgname-$pkgver-2"

  python2 setup.py build

  cd "$srcdir/$_pkgname-$pkgver"

  python setup.py build
}

package_python2-macholib() {
  depends=('python2')

  cd "$_pkgname-$pkgver-2"

  python2 setup.py install --prefix=/usr --root="$pkgdir" --optimize=1 \
    --skip-build

  for tool in 'macho_dump' 'macho_find' 'macho_standalone'; do
    mv "$pkgdir/usr/bin/$tool" "$pkgdir/usr/bin/${tool}-2"
  done
}

package_python-macholib() {
  depends=('python')

  cd "$_pkgname-$pkgver"

  python setup.py install --prefix=/usr --root="$pkgdir" --optimize=1 \
    --skip-build
}

