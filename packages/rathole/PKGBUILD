# This file is part of BlackArch Linux ( https://www.blackarch.org/ ).
# See COPYING for license details.

pkgname=rathole
pkgver=v0.3.9.r17.gfcfb0bc
pkgrel=1
pkgdesc='A lightweight and high-performance reverse proxy for NAT traversal, written in Rust. An alternative to frp and ngrok.'
arch=('x86_64' 'aarch64')
groups=('blackarch' 'blackarch-proxy' 'blackarch-tunnel' 'blackarch-networking')
url='https://github.com/rapiz1/rathole'
license=('Apache')
depends=()
makedepends=('git' 'cargo')
source=("git+https://github.com/rapiz1/$pkgname.git")
sha256sums=('SKIP')

pkgver() {
  cd $pkgname

  git describe --long --tags | sed 's/\([^-]*-g\)/r\1/;s/-/./g'
}

build() {
  cd $pkgname

  cargo build --release --locked
}

package() {
  cd $pkgname

  install -Dm 755 "target/release/$pkgname" "$pkgdir/usr/bin/$pkgname"
  install -Dm 644 -t "$pkgdir/usr/share/doc/$pkgname/" README.md docs/*.md
  cp -a docs/img "$pkgdir/usr/share/doc/$pkgname/"
  install -Dm 644 LICENSE "$pkgdir/usr/share/licenses/$pkgname/LICENSE"
}

