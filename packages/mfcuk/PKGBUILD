# This file is part of BlackArch Linux ( https://www.blackarch.org/ ).
# See COPYING for license details.

pkgname=mfcuk
pkgver=0.3.8
pkgrel=7
groups=('blackarch' 'blackarch-wireless')
pkgdesc='MIFARE Classic Universal toolKit.'
arch=('x86_64' 'armv6h' 'armv7h' 'aarch64')
url='http://code.google.com/p/mfcuk/'
license=('GPL2')
depends=('libnfc>=1.7.0')
source=("https://github.com/nfc-tools/mfcuk/archive/$pkgname-$pkgver.tar.gz")
sha512sums=('3b70a82af1de7df425c801c542f12d77d083aeccf5abf28c96840e58e91543726a38eee533ca16c420d80ab22845901f88db2982cd2bb16b382aaf82dcdeed0a')

prepare() {
  cd "$pkgname-$pkgname-$pkgver"

  sed -i 's/"\.\/data\//"\/usr\/share\/mfcuk\/data\//g' src/mfcuk_finger.c
}

build() {
  cd "$pkgname-$pkgname-$pkgver"

  ./configure --prefix=/usr

  make
}

package() {
  cd "$pkgname-$pkgname-$pkgver"

  make DESTDIR="$pkgdir" install
}

