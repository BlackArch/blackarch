# This file is part of BlackArch Linux ( https://www.blackarch.org/ ).
# See COPYING for license details.

pkgname=gsocket
pkgver=400.5a79f85
pkgrel=1
pkgdesc='Global Socket. Moving data from here to there. Securely, Fast and trough NAT/Firewalls.'
arch=('x86_64' 'aarch64')
groups=('blackarch' 'blackarch-networking')
url='https://github.com/hackerschoice/gsocket'
license=('BSD')
makedepends=('git')
source=("git+https://github.com/hackerschoice/$pkgname.git")
sha512sums=('SKIP')

pkgver() {
  cd $pkgname

  echo $(git rev-list --count HEAD).$(git rev-parse --short HEAD)
}

prepare() {
  cd $pkgname

  ./bootstrap
  ./configure --prefix=/usr
}

build() {
  cd $pkgname

  make
}

package() {
  cd $pkgname

  make DESTDIR="$pkgdir" install

  install -Dm 644 -t "$pkgdir/usr/share/doc/$pkgname/" *.md
  install -Dm 644 LICENSE "$pkgdir/usr/share/licenses/$pkgname/LICENSE"
}

