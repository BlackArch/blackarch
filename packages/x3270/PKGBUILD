# This file is part of BlackArch Linux ( https://www.blackarch.org/ ).
# See COPYING for license details.

pkgname=x3270
pkgver=3.6ga8
pkgrel=1
epoch=1
pkgdesc='An IBM 3270 terminal emulator for the X Window System.'
arch=('x86_64' 'aarch64')
url='http://x3270.bgp.nu/'
license=('BSD' 'MIT')
depends=('openssl' 'libxaw')
makedepends=('imake' 'openssl' 'libx11' 'libxaw' 'libxt' 'xbitmaps'
             'xorg-mkfontdir' 'xorg-bdftopcf')
backup=('etc/x3270/ibm_hosts')
source=("http://x3270.bgp.nu/download/03.06/suite3270-$pkgver-src.tgz")
#source=("http://x3270.bgp.nu/download/03.06/suite3270-$pkgver-src.tgz")
sha512sums=('88a00bec19ae7be92e5bc0edd6ae1e27bef7fbbf8c3f419efda3ba2b3a0cb5e72693916cd44d50166d5745ce0f9d8497641481cedbb0bb3149567b6f51b1a987')
install="$pkgname.install"

build() {
  cd "suite3270-${pkgver:0:3}"

  if [[ "$CARCH" == "aarch64" ]]; then
    ./configure --prefix=/usr --bindir=/usr/bin --sbindir=/usr/bin \
      --sysconfdir=/etc --with-fontdir=/usr/share/fonts/3270 --build=arm
  else
    ./configure --prefix=/usr --bindir=/usr/bin --sbindir=/usr/bin \
      --sysconfdir=/etc --with-fontdir=/usr/share/fonts/3270
  fi

  make
}

package() {
  cd "suite3270-${pkgver:0:3}"

  install -dm 755 "$pkgdir/usr/share/fonts/3270/fonts.dir"

  make DESTDIR="$pkgdir" install

  install -Dm 644 x3270/x3270-icon2.xpm \
    "$pkgdir/usr/share/pixmaps/x3270-icon2.xpm"
  #install -Dm 644 LICENSE "$pkgdir/usr/share/licenses/$pkgname/LICENSE"

  #chmod 644 "$pkgdir/etc/x3270/ibm_hosts"
}

