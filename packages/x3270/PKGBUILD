# This file is part of BlackArch Linux ( https://www.blackarch.org/ ).
# See COPYING for license details.

pkgname=x3270
pkgver=3.6beta2
pkgrel=1
pkgdesc='An IBM 3270 terminal emulator for the X Window System.'
arch=('x86_64' 'aarch64')
url='http://x3270.bgp.nu/'
license=('BSD' 'MIT')
depends=('openssl' 'libxaw')
makedepends=('imake' 'openssl' 'libx11' 'libxaw' 'libxt' 'xbitmaps'
             'xorg-mkfontdir')
backup=('etc/x3270/ibm_hosts')
install="$pkgname.install"
source=("http://x3270.bgp.nu/download/03.06/suite3270-$pkgver-src.tgz")
sha512sums=('7c04846ecd7cfeeff0da780c5cbf1cc85528f0fd0e94bfe07fe508d36657a5cd7e099a8acdf05b01654fddbe3a91a71015f560c613689908e03f64e63c462134')

build() {
  cd "suite3270-${pkgver:0:3}"

  ./configure --prefix=/usr --bindir=/usr/bin --sbindir=/usr/bin \
    --sysconfdir=/etc --with-fontdir=/usr/share/fonts/3270

  make
}

package() {
  cd "suite3270-${pkgver:0:3}"

  install -dm 755 "$pkgdir/usr/share/fonts/3270/fonts.dir"

  make DESTDIR="$pkgdir" install

  install -Dm 644 x3270/x3270-icon2.xpm \
    "$pkgdir/usr/share/pixmaps/x3270-icon2.xpm"
  #install -Dm 644 LICENSE "$pkgdir/usr/share/licenses/$pkgname/LICENSE"

  #chmod 644 "$pkgdir/etc/x3270/ibm_hosts"
}

