# This file is part of BlackArch Linux ( https://www.blackarch.org/ ).
# See COPYING for license details.

pkgname=websockify
pkgver=857.108f993
pkgrel=1
pkgdesc='WebSocket to TCP proxy/bridge.'
groups=('blackarch' 'blackarch-networking')
license=('LGPL3')
arch=('any')
url='http://github.com/kanaka/websockify'
depends=('python2' 'python2-numpy' 'python2-mox3')
makedepends=('git' 'python2-setuptools')
source=("git+https://github.com/kanaka/$pkgname.git")
sha512sums=('SKIP')

pkgver() {
  cd $pkgname

  echo $(git rev-list --count HEAD).$(git rev-parse --short HEAD)
}

build() {
  cd $pkgname

  python2 setup.py build
}

package() {
  cd $pkgname

  python2 setup.py install --root="$pkgdir" -O1 --skip-build

  sed -i 's|env python$|env python2|' \
    "$pkgdir/usr/lib/python2.7/site-packages/$pkgname/websocket.py"

  sed -i 's|env python$|env python2|' \
    "$pkgdir/usr/lib/python2.7/site-packages/$pkgname/websocketproxy.py"
}

