# This file is part of BlackArch Linux ( https://www.blackarch.org/ ).
# See COPYING for license details.

pkgname=websockify
pkgver=885.eca301c
pkgrel=1
pkgdesc='WebSocket to TCP proxy/bridge.'
groups=('blackarch' 'blackarch-networking')
license=('LGPL3')
arch=('any')
url='http://github.com/kanaka/websockify'
depends=('python' 'python-numpy' 'python-mox3' 'python-urllib3')
makedepends=('git' 'python-setuptools')
source=("git+https://github.com/kanaka/$pkgname.git")
sha512sums=('SKIP')

pkgver() {
  cd $pkgname

  echo $(git rev-list --count HEAD).$(git rev-parse --short HEAD)
}

build() {
  cd $pkgname

  python setup.py build
}

package() {
  cd $pkgname

  python setup.py install --root="$pkgdir" -O1 --skip-build
}

