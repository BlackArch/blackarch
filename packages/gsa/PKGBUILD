# This file is part of BlackArch Linux ( https://www.blackarch.org/ ).
# See COPYING for license details.

pkgname=gsa
pkgver=22.4.0
pkgrel=2
pkgdesc='The Greenbone Security Assistant.'
arch=('x86_64')
groups=('blackarch' 'blakcarch-scanner')
url='https://github.com/greenbone/'
license=('GPL')
depends=('nodejs')
makedepends=('yarn' 'nodejs' 'npm')
source=("${pkgname}-${pkgver}.tar.gz::https://github.com/greenbone/gsa/archive/refs/tags/v$pkgver.tar.gz")
sha512sums=('e404c4f281c11c7565e9f697cb67e0ed123b2da7a8c4ecea0c83fa10120489c64a1cf1e17c52da4909738136b1ffc0aaad18db040a67c43134e836a140852399')

build() {
  cd "${pkgname}-${pkgver}"

  rm -rf build

  export NODE_OPTIONS=--max-old-space-size=2048
  yarn && yarn build
}

package() {
  cd "${pkgname}-${pkgver}"

  install -Dm 644 -t "$pkgdir/usr/share/doc/$pkgname/" *.md
  install -Dm 644 LICENSE "$pkgdir/usr/share/licenses/$pkgname/LICENSE"
  install -dm 755 "$pkgdir/usr/local/share/gvm/gsad/web"

  rm -rf docs *.md LICENSE

  cp -r build/* "$pkgdir/usr/local/share/gvm/gsad/web/"
}

