# This file is part of BlackArch Linux ( http://blackarch.org ).
# See COPYING for license details.

pkgname='bed'
pkgver='0.5'
pkgrel=10
groups=('blackarch' 'blackarch-exploitation')
pkgdesc="Collection of scripts to test for buffer overflows, format string vulnerabilities."
arch=('any')
url='http://www.aldeid.com/wiki/Bed'
license=('GPL')
depends=('perl')
source=("http://web.archive.org/web/20101229024258/http://www.remote-exploit.org/wp-content/uploads/2010/01/bed-$pkgver.tar.gz")
sha1sums=('22a56f64d49df3032f656d687544943018bb68e9')

package() {
  cd "$srcdir/bed-$pkgver"

  mkdir -p "$pkgdir/usr/share/doc/bed"
  mkdir -p "$pkgdir/usr/share/bed"
  mkdir -p "$pkgdir/usr/bin"

  install -m755 bed.pl  "$pkgdir/usr/share/bed"

  cp --no-preserve=ownership -a bedmod "$pkgdir/usr/share/bed"
  cp --no-preserve=ownership -a docs/* "$pkgdir/usr/share/doc/bed"

  # Joint script.
  cat >> "$pkgdir/usr/bin/bed" <<EOF
#!/bin/sh
cd /usr/share/bed
exec ./bed.pl "\$@"
EOF
  chmod +x "$pkgdir/usr/bin/bed"
}
