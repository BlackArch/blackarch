# This file is part of BlackArch Linux ( http://blackarch.org ).
# See COPYING for license details.

pkgname=bed
pkgver=0.5
pkgrel=10
groups=('blackarch' 'blackarch-exploitation')
pkgdesc="Collection of scripts to test for buffer overflows, format string vulnerabilities."
arch=('any')
url='http://www.aldeid.com/wiki/Bed'
license=('GPL')
depends=('perl')
source=("http://web.archive.org/web/20101229024258/http://www.remote-exploit.org/wp-content/uploads/2010/01/bed-$pkgver.tar.gz")
sha512sums=('17a2debe4516c13dd677529b8ebe34bee777ce554668c1b5b43f94b05688611423392e04bbb5b4b52218ec18bf3bc77857ce493683384f73206b93a8b0c97aef')

package() {
  cd "$srcdir/bed-$pkgver"

  install -dm 755 "$pkgdir/usr/share/doc/bed"
  install -dm 755 "$pkgdir/usr/share/bed"
  install -dm 755 "$pkgdir/usr/bin"

  install -m755 bed.pl  "$pkgdir/usr/share/bed"

  cp --no-preserve=ownership -a bedmod "$pkgdir/usr/share/bed"
  cp --no-preserve=ownership -a docs/* "$pkgdir/usr/share/doc/bed"

  # Joint script.
  cat >> "$pkgdir/usr/bin/bed" <<EOF
#!/bin/sh
cd /usr/share/bed
exec ./bed.pl "\$@"
EOF
  chmod +x "$pkgdir/usr/bin/bed"
}
