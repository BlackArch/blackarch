# This file is part of BlackArch Linux ( https://www.blackarch.org/ ).
# See COPYING for license details.

pkgname=xsscrapy
pkgver=139.06ad0aa
pkgrel=2
pkgdesc='XSS spider - 66/66 wavsep XSS detected.'
groups=('blackarch' 'blackarch-webapp')
arch=('any')
url='https://github.com/DanMcInerney/xsscrapy'
license=('custom:unknown')
depends=('python2' 'python2-lxml' 'python2-requests' 'python2-pybloom'
         'scrapy')
makedepends=('git')
source=('git+https://github.com/DanMcInerney/xsscrapy.git')
sha512sums=('SKIP')

pkgver() {
  cd $pkgname

  echo $(git rev-list --count HEAD).$(git rev-parse --short HEAD)
}

package() {
  cd $pkgname

  install -dm 755 "$pkgdir/usr/bin"
  install -dm 755 "$pkgdir/usr/share/$pkgname"

  install -Dm 644 -t "$pkgdir/usr/share/doc/$pkgname/" README.md

  rm README.md

  cp -a * "$pkgdir/usr/share/$pkgname/"

  cat > "$pkgdir/usr/bin/$pkgname" << EOF
#!/bin/sh
cd /usr/share/$pkgname
exec python2 $pkgname.py "\$@"
EOF

  chmod +x "$pkgdir/usr/bin/$pkgname"
}

