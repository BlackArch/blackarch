# This file is part of BlackArch Linux ( https://www.blackarch.org/ ).
# See COPYING for license details.

pkgbase=python-skpy
pkgname=('python2-skpy' 'python-skpy')
_pkgname=SkPy
pkgver=0.10
pkgrel=1
pkgdesc='An unofficial Python library for interacting with the Skype HTTP API.'
arch=('any')
url='https://pypi.org/project/skpy/#files'
license=('custom:unknown')
makedepends=('python2-setuptools' 'python-setuptools')
source=("https://files.pythonhosted.org/packages/32/14/0b18c889e28d091a71a785f376fc9156839b4067108b91e20ddc73258668/SkPy-$pkgver.tar.gz")
sha512sums=('383a9a49c3a6e6c93987b22b006aefd317ba6bf8fc0831a19392ee7eea849af0b9432a0e21a199dd20c45de9c74730947767c3438b78201ce329b846a9a0c584')

prepare() {
  cp -a "$_pkgname-$pkgver"{,-2}
}

build() {
  cd "$_pkgname-$pkgver-2"

  python2 setup.py build

  cd "$srcdir/$_pkgname-$pkgver"

  python setup.py build
}

package_python2-skpy() {
  depends=('python2' 'python2-beautifulsoup4' 'python2-requests')

  cd "$_pkgname-$pkgver-2"

  python2 setup.py install --prefix=/usr --root="$pkgdir" --optimize=1 \
    --skip-build
}

package_python-skpy() {
  depends=('python' 'python-beautifulsoup4' 'python-requests')

  cd "$_pkgname-$pkgver"

  python setup.py install --prefix=/usr --root="$pkgdir" --optimize=1 \
    --skip-build
}

