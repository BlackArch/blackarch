# This file is part of BlackArch Linux ( https://www.blackarch.org/ ).
# See COPYING for license details.

pkgbase=python-skpy
pkgname=('python2-skpy' 'python-skpy')
_pkgname=SkPy
pkgver=0.10.3
pkgrel=1
pkgdesc='An unofficial Python library for interacting with the Skype HTTP API.'
arch=('any')
url='https://pypi.org/project/skpy/#files'
license=('custom:unknown')
makedepends=('python2-setuptools' 'python-setuptools')
source=("https://files.pythonhosted.org/packages/6c/e2/11c6fb8207db107bb041b169aecf205f14cb6892cdf993b883d72266fef2/SkPy-$pkgver.tar.gz")
sha512sums=('efa2ba6ad617a1774bf4ade0ee643a8dc82e737d83ec469c7e60afa52634eb2d5aa805ba481ab0c52495f937b864fc232ed282b668e012f57526c8e198719ce2')

prepare() {
  cp -a "$_pkgname-$pkgver"{,-2}
}

build() {
  cd "$_pkgname-$pkgver-2"

  python2 setup.py build

  cd "$srcdir/$_pkgname-$pkgver"

  python setup.py build
}

package_python2-skpy() {
  depends=('python2' 'python2-beautifulsoup4' 'python2-requests')

  cd "$_pkgname-$pkgver-2"

  python2 setup.py install --prefix=/usr --root="$pkgdir" --optimize=1 \
    --skip-build
}

package_python-skpy() {
  depends=('python' 'python-beautifulsoup4' 'python-requests')

  cd "$_pkgname-$pkgver"

  python setup.py install --prefix=/usr --root="$pkgdir" --optimize=1 \
    --skip-build
}

