# This file is part of BlackArch Linux ( https://www.blackarch.org/ ).
# See COPYING for license details.

pkgbase=python-skpy
pkgname=('python2-skpy' 'python-skpy')
_pkgname=SkPy
pkgver=0.10.2
pkgrel=1
pkgdesc='An unofficial Python library for interacting with the Skype HTTP API.'
arch=('any')
url='https://pypi.org/project/skpy/#files'
license=('custom:unknown')
makedepends=('python2-setuptools' 'python-setuptools')
source=("https://files.pythonhosted.org/packages/b9/a2/caf37b7939824580ae6d8a65560a3e53eb94c20fbf64871d6554d8a3bdee/SkPy-$pkgver.tar.gz")
sha512sums=('21366ffc38b4c2fc85e448af138255f85c0e3efe5f322c6a2750aa07853674fa8eb9deea4acdf9d5c770ce8fdcd704eb20798de996438dccfb528aa74ec467e4')

prepare() {
  cp -a "$_pkgname-$pkgver"{,-2}
}

build() {
  cd "$_pkgname-$pkgver-2"

  python2 setup.py build

  cd "$srcdir/$_pkgname-$pkgver"

  python setup.py build
}

package_python2-skpy() {
  depends=('python2' 'python2-beautifulsoup4' 'python2-requests')

  cd "$_pkgname-$pkgver-2"

  python2 setup.py install --prefix=/usr --root="$pkgdir" --optimize=1 \
    --skip-build
}

package_python-skpy() {
  depends=('python' 'python-beautifulsoup4' 'python-requests')

  cd "$_pkgname-$pkgver"

  python setup.py install --prefix=/usr --root="$pkgdir" --optimize=1 \
    --skip-build
}

