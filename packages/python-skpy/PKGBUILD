# This file is part of BlackArch Linux ( https://www.blackarch.org/ ).
# See COPYING for license details.

pkgbase=python-skpy
pkgname=('python2-skpy' 'python-skpy')
_pkgname=SkPy
pkgver=0.10.1
pkgrel=2
pkgdesc='An unofficial Python library for interacting with the Skype HTTP API.'
arch=('any')
url='https://pypi.org/project/skpy/#files'
license=('custom:unknown')
makedepends=('python2-setuptools' 'python-setuptools')
source=("https://files.pythonhosted.org/packages/c4/99/3346b4e4483cd769df027efd9233e51036a635d01baee3c5df5fc4351e27/SkPy-$pkgver.tar.gz")
sha512sums=('2375671f302b9e5ad43a2fa9a3ec64816df9766c6adce66236e48d3a8f08d5a735bbc57c521549661f1e09905f8e812a5c92f6855445b5f2ec4f3879515b417a')

prepare() {
  cp -a "$_pkgname-$pkgver"{,-2}
}

build() {
  cd "$_pkgname-$pkgver-2"

  python2 setup.py build

  cd "$srcdir/$_pkgname-$pkgver"

  python setup.py build
}

package_python2-skpy() {
  depends=('python2' 'python2-beautifulsoup4' 'python2-requests')

  cd "$_pkgname-$pkgver-2"

  python2 setup.py install --prefix=/usr --root="$pkgdir" --optimize=1 \
    --skip-build
}

package_python-skpy() {
  depends=('python' 'python-beautifulsoup4' 'python-requests')

  cd "$_pkgname-$pkgver"

  python setup.py install --prefix=/usr --root="$pkgdir" --optimize=1 \
    --skip-build
}

