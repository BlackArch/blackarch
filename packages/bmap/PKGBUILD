# This file is part of BlackArch Linux ( http://blackarch.org ).
# See COPYING for license details.

pkgname='bmap'
pkgver='1.0.17'
pkgrel=1
groups=('blackarch' 'blackarch-forensic')
pkgdesc="A tool that can store data in slackspace on any filesystem. It can detect used slackspace and recover the data."
url='http://www.securityfocus.com/tools/1359'
arch=('x86_64')
license=('GPL2')
makedepends=('linuxdoc-tools')
source=("http://www.hacker-soft.net/tools/Miscellaneous/bmap-$pkgver.tar.gz")
sha1sums=('962ec6d193adea7e2fae79cf87d65d09a8ea24f0')

prepare() {
  cd "$srcdir/bmap-$pkgver"

  sed -i 's|BINDIR = "[^"]*"|BINDIR = $(DESTDIR)/usr/bin|;
          s|MANDIR = "[^"]*"|MANDIR = $(DESTDIR)/usr/share/man|' Makefile
}

build() {
  cd "$srcdir/bmap-$pkgver"

  make
}

package() {
  cd "$srcdir/bmap-$pkgver"

  mkdir -p "$pkgdir/usr/bin" "$pkgdir/usr/share/man/man1"
  make "DESTDIR=$pkgdir" install
}
