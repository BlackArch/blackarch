pkgname='bmap'
pkgver='1.0.17'
pkgrel=1
groups=('blackarch' 'blackarch-forensic')
pkgdesc="A tool that can store data in slackspace on any filesystem. It can detect used slackspace and recover the data."
url='http://www.securityfocus.com/tools/1359'
arch=('x86' 'x86_64')
license=('GPL2')
makedepends=('linuxdoc-tools')
source=("http://www.hacker-soft.net/tools/Miscellaneous/bmap-$pkgver.tar.gz")
md5sums=('8b96a7f41b2de1a076dd4c00f32daee0')

prepare() {
  cd "$srcdir/bmap-$pkgver"

  sed -i 's|BINDIR = "[^"]*"|BINDIR = $(DESTDIR)/usr/bin|;
          s|MANDIR = "[^"]*"|MANDIR = $(DESTDIR)/usr/share/man|' Makefile
}

build() {
  cd "$srcdir/bmap-$pkgver"

  make
}

package() {
  cd "$srcdir/bmap-$pkgver"

  install -d -m755 "$pkgdir/usr/bin" "$pkgdir/usr/share/man/man1"
  make "DESTDIR=$pkgdir" install
}
