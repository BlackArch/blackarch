# This file is part of BlackArch Linux ( https://www.blackarch.org/ ).
# See COPYING for license details.

pkgname=python-pefile
pkgver=2018.8.8
pkgrel=2
pkgdesc='This is a Python library for PE file support and investigation.'
arch=('any')
url='https://pypi.org/project/pefile/#files'
license=('MIT')
depends=('python' 'python-future')
makedepends=('python-setuptools')
source=("https://files.pythonhosted.org/packages/ed/cc/157f20038a80b6a9988abc06c11a4959be8305a0d33b6d21a134127092d4/pefile-$pkgver.tar.gz"
        'UserDB.TXT'
        'userdb.txt')
sha512sums=('44e8ed40f03e795badcc17cc6567165b3430aa80034f1b02b35fc15c94ce6b544ac195dedcea5858664c169fc4241e999df09a36f1a8a6635ce536af40284d29'
            '3db3041768285f664eb42319e4c7a18e4a5a1c0731d806a8e7a4070d086395727ad5cba292f81db5ba9f6de7f244f34594b7379d32234fa01380083966874809'
            '852fed6728f84203bb39d4538699217446156d8e4bf60e8498bdaa97a853fd1b6605864bf3bba3535fbcbdc54ebee5624500356cf2bd46015de3ff31c9a51e19')

build() {
  cd "pefile-$pkgver"

  python setup.py build
}

package() {
  cd "pefile-$pkgver"

  install -dm 755 "$pkgdir/usr/share/pefile"

  python setup.py install --prefix=/usr --root="$pkgdir" --optimize=1 \
    --skip-build

  install -Dm 644 LICENSE "$pkgdir/usr/share/licenses/$pkgname/LICENSE"
  install -Dm 644 ../UserDB.TXT "$pkgdir/usr/share/pefile/UserDB.TXT"
  install -Dm 644 ../userdb.txt "$pkgdir/usr/share/pefile/userdb.txt"
}

