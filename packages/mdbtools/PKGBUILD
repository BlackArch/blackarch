# This file is part of BlackArch Linux ( https://www.blackarch.org/ ).
# See COPYING for license details.

pkgname=mdbtools
pkgver=700.d6f5745
pkgrel=1
groups=('blackarch' 'blackarch-forensic')
pkgdesc="Utilities for viewing data and exporting schema from Microsoft Access Database files"
arch=('x86_64' 'armv6h' 'armv7h' 'aarch64')
url="http://sourceforge.net/projects/mdbtools/"
license=('LGPL' 'GPL')
depends=('libgnomeui' 'unixodbc')
makedepends=('git' 'bison' 'flex' 'txt2man' 'autoconf' 'rarian')
options=('!libtool' '!makeflags')
source=('git+https://github.com/brianb/mdbtools.git'
        'gmdb2.desktop')
sha512sums=('SKIP'
            'f7f75936ce0fd385884cc4d15766b70a08f45a9fa1ed79553c898856f144b28839c6c3c0b7aab8227c996222ff5281287fda5c7039e6b47c48104e46e0232d50')

pkgver() {
  cd $pkgname

  echo $(git rev-list --count HEAD).$(git rev-parse --short HEAD)
}

build() {
  cd $pkgname

  autoreconf -i -f

  ./configure --prefix=/usr --sysconfdir=/etc --mandir=/usr/share/man \
    --with-unixodbc=/usr

  make
}

package() {
  cd $pkgname

  make DESTDIR="$pkgdir" install

  rmdir "$pkgdir/usr/share/man/mant"

  install -Dm 644 ../gmdb2.desktop "$pkgdir/usr/share/applications/gmdb2.desktop"
}

