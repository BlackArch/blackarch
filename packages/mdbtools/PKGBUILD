# This file is part of BlackArch Linux ( http://blackarch.org ).
# See COPYING for license details.

pkgname=mdbtools
pkgver=0.7.1
pkgrel=6
groups=('blackarch' 'blackarch-forensic')
pkgdesc="Utilities for viewing data and exporting schema from Microsoft Access Database files"
arch=('x86_64' 'armv6h' 'armv7h' 'aarch64')
url="http://sourceforge.net/projects/mdbtools/"
license=('LGPL' 'GPL')
depends=('libgnomeui' 'unixodbc')
makedepends=('bison' 'flex' 'txt2man' 'autoconf' 'rarian')
options=('!libtool' '!makeflags')
source=("https://github.com/brianb/mdbtools/archive/${pkgver}.tar.gz"
        'gmdb2.desktop')
sha512sums=('bbad415c59a3f54c11bcd04971220b1370046c5dfd7300d1f23e597615a1df6af850c6f767e78033676d486d87a11c72dfdb141abbee8378337c3fda9a450b75'
            'f7f75936ce0fd385884cc4d15766b70a08f45a9fa1ed79553c898856f144b28839c6c3c0b7aab8227c996222ff5281287fda5c7039e6b47c48104e46e0232d50')

build() {
  cd "$srcdir/mdbtools-$pkgver"

  autoreconf -i -f

  ./configure --prefix=/usr --sysconfdir=/etc --mandir=/usr/share/man \
    --with-unixodbc=/usr

  make
}

package() {
  cd "$srcdir/mdbtools-$pkgver"

  make DESTDIR="$pkgdir" install

  rmdir "$pkgdir/usr/share/man/mant"

  install -Dm644 ../gmdb2.desktop "$pkgdir/usr/share/applications/gmdb2.desktop"
}
