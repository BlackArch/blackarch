pkgname=mdbtools
pkgver=0.7
groups=(blackarch blackarch-analysis)
pkgrel=3
pkgdesc='Utilities for viewing data and exporting schema from Microsoft Access Database files'
arch=('i686' 'x86_64')
url="http://sourceforge.net/projects/mdbtools/"
license=('LGPL' 'GPL')
depends=('libgnomeui' 'unixodbc')
makedepends=('bison' 'flex' 'txt2man' 'automake')
options=('!libtool')
source=(https://github.com/brianb/mdbtools/tarball/0.7
        gmdb2.desktop
        autogen.sh.patch)
md5sums=('234241dc00bb15e70ca5bc7addf184da'
         '289f236ca91d73510e842922b5776999'
         'a686eba6fde82a0fb06a5b886dcb89e5')

prepare() {
  cd "$srcdir/brianb-mdbtools-004cc9f"

  patch -i ../autogen.sh.patch
}

build() {
  cd "$srcdir/brianb-mdbtools-004cc9f"

  ./autogen.sh
  ./configure --prefix=/usr --sysconfdir=/etc --mandir=/usr/share/man --with-unixodbc=/usr
  make
}

package() {
  cd "$srcdir/brianb-mdbtools-004cc9f"

  make DESTDIR="$pkgdir" install
  rmdir "$pkgdir/usr/share/man/mant"
  install -Dm644 ../gmdb2.desktop "$pkgdir/usr/share/applications/gmdb2.desktop"
}
