# This file is part of BlackArch Linux ( https://blackarch.org )
# See COPYING for license details.

pkgbase=python-flask-apscheduler
pkgname=('python2-flask-apscheduler' 'python-flask-apscheduler')
_pkgname=Flask-APScheduler
pkgver=1.11.0
pkgrel=4
pkgdesc='Adds APScheduler support to Flask.'
arch=('any')
url='https://pypi.org/project/Flask-apscheduler/#files'
makedepends=('python2-setuptools' 'python-setuptools')
license=('Apache')
source=("https://files.pythonhosted.org/packages/85/cd/7a3c34d970b741a24dbbcda49cd892144b5ddcf24acf2e22803d3870cebd/$_pkgname-$pkgver.tar.gz")
sha512sums=('11dcb79ec64dfb3820194c4d04912e1fae3d53b6fdcf91a86b24c0f22559d5ae7d77b86aa65f88718cdd8ee37964b325ec495f31ef025240b53c680e9a6c5ea7')

prepare() {
  cp -a "$_pkgname-$pkgver"{,-2}
}

build() {
  cd "$_pkgname-$pkgver-2"

  python2 setup.py build

  cd "$srcdir/$_pkgname-$pkgver"

  python setup.py build
}

package_python2-flask-apscheduler() {
  depends=('python2' 'python2-flask' 'python2-dateutil' 'python2-apscheduler')

  install -dm 755 "$pkgdir/usr/share/licenses/${pkgname[0]}"

  cd "$_pkgname-$pkgver-2"

  python2 setup.py install --prefix=/usr --root="$pkgdir" -O1 --skip-build

  mv "$pkgdir/usr/LICENSE" "$pkgdir/usr/share/licenses/${pkgname[0]}"
}

package_python-flask-apscheduler() {
  depends=('python' 'python-flask' 'python-dateutil' 'python-apscheduler')

  install -dm 755 "$pkgdir/usr/share/licenses/${pkgname[1]}"

  cd "$_pkgname-$pkgver"

  python setup.py install --prefix=/usr --root="$pkgdir" -O1 --skip-build

  mv "$pkgdir/usr/LICENSE" "$pkgdir/usr/share/licenses/${pkgname[1]}"
}

