# This file is part of BlackArch Linux ( https://blackarch.org )
# See COPYING for license details.

pkgbase=python-flask-apscheduler
pkgname=('python2-flask-apscheduler' 'python-flask-apscheduler')
_pkgname=Flask-APScheduler
pkgver=1.12.2
pkgrel=1
pkgdesc='Adds APScheduler support to Flask.'
arch=('any')
url='https://pypi.org/project/Flask-apscheduler/#files'
makedepends=('python2-setuptools' 'python-setuptools')
license=('Apache')
source=("https://files.pythonhosted.org/packages/47/b6/9008c514ae1f9d8d449e81f8f9748ab622bee7fe97cc86eb94363af885ff/Flask-APScheduler-$pkgver.tar.gz")
sha512sums=('bb2a03a45118001e414b9fe40904d9cd4f02e2af5f930d12708706d108ca167a33c425a2f63b134b42ec1b49f4ddae255ce9f65baaa4dcf642918bec858bf903')

prepare() {
  cp -a "$_pkgname-$pkgver"{,-2}
}

build() {
  cd "$_pkgname-$pkgver-2"

  python2 setup.py build

  cd "$srcdir/$_pkgname-$pkgver"

  python setup.py build
}

package_python2-flask-apscheduler() {
  depends=('python2' 'python2-flask' 'python2-dateutil' 'python2-apscheduler')

  install -dm 755 "$pkgdir/usr/share/licenses/${pkgname[0]}"

  cd "$_pkgname-$pkgver-2"

  python2 setup.py install --prefix=/usr --root="$pkgdir" -O1 --skip-build

  mv "$pkgdir/usr/LICENSE" "$pkgdir/usr/share/licenses/${pkgname[0]}"
}

package_python-flask-apscheduler() {
  depends=('python' 'python-flask' 'python-dateutil' 'python-apscheduler')

  install -dm 755 "$pkgdir/usr/share/licenses/${pkgname[1]}"

  cd "$_pkgname-$pkgver"

  python setup.py install --prefix=/usr --root="$pkgdir" -O1 --skip-build

  mv "$pkgdir/usr/LICENSE" "$pkgdir/usr/share/licenses/${pkgname[1]}"
}

