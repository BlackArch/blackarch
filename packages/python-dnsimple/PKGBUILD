# This file is part of BlackArch Linux ( https://www.blackarch.org/ ).
# See COPYING for license details.

pkgname=python-dnsimple
_pkgname=dnsimple
pkgdesc='DNSimple API service for python.'
pkgver=2.15.0
pkgrel=1
arch=('any')
url='https://pypi.org/project/dnsimple/'
license=('MIT')
depends=('python' 'python-requests' 'python-omitempty')
makedepends=('python-build' 'python-pip')
source=("https://files.pythonhosted.org/packages/source/${_pkgname::1}/$_pkgname/$_pkgname-$pkgver.tar.gz")
sha512sums=('dc3bddf6a61f73aff683461a7dbe3c343c944487f8ff1075462b76cd0b3e74079ac5b38fee997e15729946a4ba1453cd36e782cbb6eadf3d5b69d2f668ecb8b3')

build() {
  cd "$_pkgname-$pkgver"

  python -m build --wheel --outdir="$startdir/dist"
}

package() {
  cd "$_pkgname-$pkgver"

  pip install \
    --verbose \
    --disable-pip-version-check \
    --no-warn-script-location \
    --ignore-installed \
    --no-compile \
    --no-deps \
    --root="$pkgdir" \
    --prefix=/usr \
    --no-index \
    --find-links="file://$startdir/dist" \
    $_pkgname
}

