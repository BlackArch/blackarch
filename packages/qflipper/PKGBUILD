# This file is part of BlackArch Linux ( https://www.blackarch.org/ ).
# See COPYING for license details.

# Initial PKGBUILD from AUR.
# Old Maintainer: L. Bradley LaBoon <me@bradleylaboon.com>

## links
# https://flipperzero.one/update
# https://github.com/flipperdevices/qFlipper

_pkgname="qflipper"
pkgname="$_pkgname"
pkgver=1.3.3
pkgrel=2
pkgdesc="Desktop application for updating Flipper Zero firmware via PC."
url="https://flipperzero.one/update"
license=('GPL3')
arch=('x86_64')
groups=('blackarch' 'blackarch-firmware')
depends=(
  'libusb'
  'qt6-5compat'
  'qt6-declarative'
  'qt6-serialport'
  'qt6-svg'
)
makedepends=(
  'git'
  'qt6-tools'
)
_pkgsrc="$_pkgname"
source=("$_pkgsrc"::"git+https://github.com/flipperdevices/qFlipper.git#tag=$pkgver")
sha512sums=('SKIP')
source+=(
  "libwdi"::"git+https://github.com/pbatard/libwdi"
  "nanopb"::"git+https://github.com/nanopb/nanopb"
)
sha512sums+=(
  'SKIP'
  'SKIP'
)

prepare() {
  cd "$_pkgsrc"
  git submodule init
  git config submodule.driver-tool/libwdi.url "$srcdir/libwdi"
  git config submodule.3rdparty/nanopb.url "$srcdir/nanopb"
  git -c protocol.file.allow=always submodule update

  # Use uucp group instead of dialout for udev rules
  sed -i 's/dialout/uucp/g' installer-assets/udev/42-flipperzero.rules
}

build() {
  local _qmake_options=(
    ../qFlipper.pro
    -spec linux-g++
    CONFIG+=qtquickcompiler
    DEFINES+=DISABLE_APPLICATION_UPDATES
    PREFIX=/usr
  )

  mkdir -p "$_pkgsrc/build"
  cd "$_pkgsrc/build"

  qmake6 "${_qmake_options[@]}"

  make qmake_all
  make
}

package() {
  make -C "$_pkgsrc/build" INSTALL_ROOT="$pkgdir" install
}

