# This file is part of BlackArch Linux ( https://www.blackarch.org/ ).
# See COPYING for license details.

pkgname=python-spacy
_pkgname=spacy
pkgver=3.1.4
pkgrel=1
pkgdesc='Industrial-strength Natural Language Processing (NLP) in Python.'
arch=('x86_64' 'aarch64')
url='https://pypi.org/project/spacy/#files'
license=('MIT')
depends=('python-catalogue' 'python-cymem' 'python-jinja' 'python-murmurhash'
         'python-numpy' 'python-pathlib' 'python-plac' 'python-preshed'
         'python-regex' 'python-requests' 'python-srsly' 'python-thinc'
         'python-tqdm' 'python-typer' 'python-ujson' 'python-wasabi')
makedepends=('python-pip' 'python-wheel' 'cython')
source=("https://files.pythonhosted.org/packages/e4/a9/9628234a2e4892919c3c807de6759097ac0d525391d18df1d98da0512e77/$_pkgname-$pkgver.tar.gz")
sha512sums=('99e9fdffd88ede0b2d4afbc67dc0862f1d1dfff8fcef7db88f01098d1338b67e6b33266486b5c1163dbb2c7f928229a2a844eab0b4438729c6e334728179bf68')

build() {
	cd "$_pkgname-$pkgver"

	python setup.py build
}

package() {
	cd "$_pkgname-$pkgver"

	python setup.py install --root="$pkgdir" --prefix=/usr -O1 --skip-build
}

