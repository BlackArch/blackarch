# This file is part of BlackArch Linux ( https://www.blackarch.org/ ).
# See COPYING for license details.

pkgname=python-spacy
_pkgname=spacy
pkgver=3.7.4
pkgrel=1
pkgdesc='Industrial-strength Natural Language Processing (NLP) in Python.'
arch=('x86_64' 'aarch64')
url='https://pypi.org/project/spacy/#files'
license=('MIT')
depends=('python-catalogue' 'python-cymem' 'python-jinja' 'python-murmurhash'
         'python-numpy' 'python-pathlib' 'python-plac' 'python-preshed'
         'python-regex' 'python-requests' 'python-srsly' 'python-thinc'
         'python-tqdm' 'python-typer' 'python-ujson' 'python-wasabi')
makedepends=('python-pip' 'python-build' 'python-wheel' 'cython')
source=("https://files.pythonhosted.org/packages/source/${_pkgname::1}/$_pkgname/$_pkgname-$pkgver.tar.gz")
sha512sums=('7a76a22d1486eb914c1638e0b29d0cb376790b85a033c1f75b4e4f874fce2e47c39b173d3fd90e6e5f59a873f2163abf245cbc75932cb08f538537980c587ef5')

build() {
  cd "$_pkgname-$pkgver"

  python -m build --wheel --outdir="$startdir/dist"
}

package() {
  cd "$_pkgname-$pkgver"

  pip install \
    --verbose \
    --disable-pip-version-check \
    --no-warn-script-location \
    --ignore-installed \
    --no-compile \
    --no-deps \
    --root="$pkgdir" \
    --prefix=/usr \
    --no-index \
    --find-links="file://$startdir/dist" \
    $_pkgname
}

