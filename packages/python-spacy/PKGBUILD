# This file is part of BlackArch Linux ( https://www.blackarch.org/ ).
# See COPYING for license details.

pkgname=python-spacy
_pkgname=spacy
pkgver=3.7.1
pkgrel=1
pkgdesc='Industrial-strength Natural Language Processing (NLP) in Python.'
arch=('x86_64' 'aarch64')
url='https://pypi.org/project/spacy/#files'
license=('MIT')
depends=('python-catalogue' 'python-cymem' 'python-jinja' 'python-murmurhash'
         'python-numpy' 'python-pathlib' 'python-plac' 'python-preshed'
         'python-regex' 'python-requests' 'python-srsly' 'python-thinc'
         'python-tqdm' 'python-typer' 'python-ujson' 'python-wasabi')
makedepends=('python-pip' 'python-build' 'python-wheel' 'cython')
source=("https://files.pythonhosted.org/packages/source/${_pkgname::1}/$_pkgname/$_pkgname-$pkgver.tar.gz")
sha512sums=('c4294598d55dfdb1e5aa09397ec3961c615f7b65eb51aa53a6692c69cc898e401e6af1c702a9aa0576e50505684fa6fc8298598763633f3aab13323a45a9778e')

build() {
  cd "$_pkgname-$pkgver"

  python -m build --wheel --outdir="$startdir/dist"
}

package() {
  cd "$_pkgname-$pkgver"

  pip install \
    --verbose \
    --disable-pip-version-check \
    --no-warn-script-location \
    --ignore-installed \
    --no-compile \
    --no-deps \
    --root="$pkgdir" \
    --prefix=/usr \
    --no-index \
    --find-links="file://$startdir/dist" \
    $_pkgname
}

