# This file is part of BlackArch Linux ( https://www.blackarch.org/ ).
# See COPYING for license details.

pkgname=python-spacy
_pkgname=spacy
pkgver=3.5.1
pkgrel=1
pkgdesc='Industrial-strength Natural Language Processing (NLP) in Python.'
arch=('x86_64' 'aarch64')
url='https://pypi.org/project/spacy/#files'
license=('MIT')
depends=('python-catalogue' 'python-cymem' 'python-jinja' 'python-murmurhash'
         'python-numpy' 'python-pathlib' 'python-plac' 'python-preshed'
         'python-regex' 'python-requests' 'python-srsly' 'python-thinc'
         'python-tqdm' 'python-typer' 'python-ujson' 'python-wasabi')
makedepends=('python-pip' 'python-wheel' 'cython' 'python-setuptools')
source=("https://files.pythonhosted.org/packages/source/${_pkgname::1}/$_pkgname/$_pkgname-$pkgver.tar.gz")
sha512sums=('38bbfef99a70fe13769d9ac03156a5826fa58f5d842ce5abfaafb3016b7df13febb503ddfa26f64062be8cc6d6d7d6f5e44a3579eb9fb8ff61560cd305ccb16f')

build() {
	cd "$_pkgname-$pkgver"

	python setup.py build
}

package() {
	cd "$_pkgname-$pkgver"

	python setup.py install --root="$pkgdir" --prefix=/usr -O1 --skip-build
}

