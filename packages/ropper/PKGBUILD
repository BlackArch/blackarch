# This file is part of BlackArch Linux ( http://blackarch.org ).
# See COPYING for license details.

pkgname='ropper'
pkgver=270.c7ee950
pkgrel=1
groups=('blackarch' 'blackarch-exploitation')
pkgdesc='It can show information about files in different file formats and you can find gadgets to build rop chains for different architectures. For disassembly ropper uses the awesome Capstone Framework.'
arch=('any')
url='https://github.com/sashs/Ropper'
license=('GPL2')
depends=('python2' 'python2-capstone')
makedepends=('git' 'python2-setuptools')
source=('git+https://github.com/sashs/Ropper.git')
sha1sums=('SKIP')

pkgver() {
  cd "$srcdir/Ropper"

  echo $(git rev-list --count HEAD).$(git rev-parse --short HEAD)
}

build() {
  cd "$srcdir/Ropper"

  python2 setup.py build
}

package() {
  cd "$srcdir/Ropper"

  install -Dm644 README.md "$pkgdir/usr/share/doc/ropper/README.md"
  install -Dm644 COPYING "$pkgdir/usr/share/licenses/ropper/COPYING"

  python2 setup.py install --root="$pkgdir" --optimize=1
}
