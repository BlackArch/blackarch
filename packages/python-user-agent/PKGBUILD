# This file is part of BlackArch Linux ( https://www.blackarch.org/ ).
# See COPYING for license details.

pkgbase=python-user-agent
pkgname=('python2-user-agent' 'python-user-agent')
_pkgname=user-agent
__pkgname=user_agent
pkgver=0.1.14
pkgrel=2
pkgdesc='User-Agent generator.'
arch=('any')
url='https://pypi.org/project/user_agent/#files'
license=('MIT')
makedepends=('python2-setuptools' 'python-setuptools')
source=("https://files.pythonhosted.org/packages/source/${_pkgname::1}/$_pkgname/user_agent-$pkgver.tar.gz")
sha512sums=('421a7c17b28fb449dce2f39dc3db466515b6bce48777c24eb547fd7f867c4bbe2d1dbb3b3bcd930c33f7e82fe8b39a9c308b6b34f887f3ae5dcdfce73d674d0a')

prepare() {
  cp -a "$__pkgname-$pkgver"{,-2}
}

build() {
  cd "$__pkgname-$pkgver-2"

  python2 setup.py build

  cd "$srcdir/$__pkgname-$pkgver"

  python setup.py build
}

package_python2-user-agent() {
  depends=('python2-six')

  cd "$__pkgname-$pkgver-2"

  python2 setup.py install --root="$pkgdir" --prefix=/usr -O1 --skip-build
}

package_python-user-agent() {
  depends=('python-six')

  cd "$__pkgname-$pkgver"

  python setup.py install --root="$pkgdir" --prefix=/usr -O1 --skip-build
}

