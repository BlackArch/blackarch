# This file is part of BlackArch Linux ( https://www.blackarch.org/ ).
# See COPYING for license details.

pkgbase=python-user-agent
pkgname=('python2-user-agent' 'python-user-agent')
pkgver=0.1.9
pkgrel=3
pkgdesc='User-Agent generator.'
arch=('any')
url='https://pypi.org/project/user_agent/#files'
license=('MIT')
makedepends=('python2-setuptools' 'python-setuptools')
source=("https://files.pythonhosted.org/packages/c3/ca/15546284f62edfec7666ecb6403a6e77f5db850def37cd36f140d99cce02/user_agent-$pkgver.tar.gz")
sha512sums=('54d273ba1eec260591f3f1fd04cd9894713129d017f2910be04aa0342287bd17fcc75dc44c627c54bf19dc872db55ab9c4965eaa1a3976e6fca91ebcc7c99a48')

prepare() {
  cp -a "user_agent-$pkgver" "user_agent2-$pkgver"
}

build() {
  cd "user_agent2-$pkgver"

  python2 setup.py build

  cd "$srcdir/user_agent-$pkgver"

  python setup.py build
}

package_python2-user-agent() {
  depends=('python2-six')

  cd "user_agent2-$pkgver"

  python2 setup.py install --prefix=/usr --root="$pkgdir" --optimize=1 \
    --skip-build
}

package_python-user-agent() {
  depends=('python-six')

  cd "user_agent-$pkgver"

  python setup.py install --prefix=/usr --root="$pkgdir" --optimize=1 \
    --skip-build
}

