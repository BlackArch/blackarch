# This file is part of BlackArch Linux ( https://www.blackarch.org/ ).
# See COPYING for license details.

pkgname=python2-python-libnmap
_pkgname=python-libnmap
pkgver=0.7.3
pkgrel=1
pkgdesc='Python NMAP library enabling you to start async nmap tasks, parse and compare/diff scan results.'
arch=('any')
# https://github.com/savon-noir/python-libnmap
url='https://pypi.org/project/python-libnmap/#files'
license=('custom:unknown')
depends=('python2')
makedepends=('python2-setuptools')
source=("https://files.pythonhosted.org/packages/source/${_pkgname::1}/$_pkgname/$_pkgname-$pkgver.tar.gz")
sha512sums=('aa9e7dc26f6391698e9b27ffc6092859ba63174f89f7fe6d6e0c1f08f67aea25f2a40c3454460a45783218fa5c44d3959e21700ba9c16f6d28bc358965079bd3')

prepare() {
  cd "$_pkgname-$pkgver"

  # issue: https://github.com/savon-noir/python-libnmap/issues/138
  # PR: https://github.com/savon-noir/python-libnmap/pull/139
  sed -i 's/, encoding="utf-8"//' setup.py
}

build() {
  cd "$_pkgname-$pkgver"

  python2 setup.py build
}

package() {
  cd "$_pkgname-$pkgver"

  python2 setup.py install --root="$pkgdir" --prefix=/usr -O1 --skip-build
}

