# This file is part of BlackArch Linux ( https://www.blackarch.org/ ).
# See COPYING for license details.

pkgbase=python-libolecf-python
pkgname=('python2-libolecf-python' 'python-libolecf-python')
pkgver=20181231
pkgrel=2
pkgdesc='Library and tools to access the OLE 2 Compound File (OLECF) format.'
url='https://github.com/libyal/libolecf'
arch=('any')
license=('LGPLv3')
makedepends=('python2-setuptools' 'python-setuptools')
source=("https://pypi.python.org/packages/source/l/libolecf-python/libolecf-python-$pkgver.tar.gz")
sha512sums=('1796b26198eaacd02a9620b0e1eae8cb30f7d742a5894dd79838fec42b3db6e204c0dd6f8690b1f327f8e1511a05f51cafdc0490ed6107e15470f9072cd4a57d')

check(){
  cd "libolecf-$pkgver/tests"

  chmod +x ./test_runner.sh
  ./test_runner.sh
}

package_python2-libolecf-python() {
  depends=('python2')
  cd "libolecf-$pkgver"

  python2 setup.py install --root="$pkgdir" --optimize=1
}

package_python-libolecf-python() {
  depends=('python')
  cd "libolecf-$pkgver"

  python setup.py install --root="$pkgdir" --optimize=1
}

