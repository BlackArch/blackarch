# This file is part of BlackArch Linux ( https://www.blackarch.org/ ).
# See COPYING for license details.

pkgname=cupp
pkgver=59.d640d61
pkgrel=1
pkgdesc='Common User Password Profiler'
url='http://www.remote-exploit.org/?page_id=418'
groups=('blackarch' 'blackarch-cracker')
license=('GPL3')
depends=('python2')
makedepends=('git')
arch=('any')
sha512sums=('SKIP')
source=('git+https://github.com/Mebus/cupp.git')

pkgver() {
  cd $pkgname

  echo $(git rev-list --count HEAD).$(git rev-parse --short HEAD)
}

package() {
  cd $pkgname

  install -dm 755 "$pkgdir/usr/bin"
  install -dm 755 "$pkgdir/usr/share/$pkgname"

  install -Dm 644 -t "$pkgdir/usr/share/doc/$pkgname/" README.md CHANGELOG.md
  install -Dm 644 LICENSE "$pkgdir/usr/share/licenses/$pkgname/LICENSE"

  rm *.md LICENSE

  cp -a * "$pkgdir/usr/share/$pkgname/"

  cat > "$pkgdir/usr/bin/$pkgname" << EOF
#!/bin/sh
cd /usr/share/$pkgname
exec python2 ./cupp.py "\$@"
EOF

  chmod +x "$pkgdir/usr/bin/$pkgname"

  cat > "$pkgdir/usr/bin/cupp3" << EOF
#!/bin/sh
cd /usr/share/$pkgname
exec python3 ./cupp3.py "\$@"
EOF

  chmod +x "$pkgdir/usr/bin/cupp3"
}

