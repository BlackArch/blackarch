# This file is part of BlackArch Linux ( http://blackarch.org ).
# See COPYING for license details.

pkgname=cupp
pkgver=32.9628c13
pkgrel=1
pkgdesc='Common User Password Profiler'
url='http://www.remote-exploit.org/?page_id=418'
groups=('blackarch' 'blackarch-cracker')
license=('GPL3')
depends=('python2')
makedepends=('git')
arch=('any')
sha512sums=('SKIP')
source=('git+https://github.com/Mebus/cupp.git')

pkgver() {
  cd "$srcdir/cupp"

  echo $(git rev-list --count HEAD).$(git rev-parse --short HEAD)
}

package() {
  cd "$srcdir/cupp"

  install -dm 755 "$pkgdir/usr/bin"
  install -dm 755 "$pkgdir/usr/share/cupp"

  install -Dm644 -t "$pkgdir/usr/share/doc/cupp/" README.md CHANGELOG.md
  install -Dm644 LICENSE "$pkgdir/usr/share/licenses/cupp/LICENSE"

  rm *.md LICENSE

  cp -a * "$pkgdir/usr/share/cupp"

  cat > "$pkgdir/usr/bin/cupp" << EOF
#!/bin/sh
cd /usr/share/cupp
exec python2 ./cupp.py "\${@}"
EOF

  chmod +x "$pkgdir/usr/bin/cupp"

  cat > "$pkgdir/usr/bin/cupp3" << EOF
#!/bin/sh
cd /usr/share/cupp
exec python3 ./cupp3.py "\${@}"
EOF

  chmod +x "$pkgdir/usr/bin/cupp3"
}
