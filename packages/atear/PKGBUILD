# This file is part of BlackArch Linux ( http://blackarch.org ).
# See COPYING for license details.

pkgname='atear'
pkgver=139.245ec8d
pkgrel=1
pkgdesc='Wireless Hacking, WiFi Security, Vulnerability Analyzer, Pentestration.'
groups=('blackarch' 'blackarch-wireless' 'blackarch-recon' 'blackarch-scanner')
arch=('any')
url='https://github.com/NORMA-Inc/AtEar'
license=('Apache2')
depends=('nmap' 'wireshark-cli' 'aircrack-ng' 'hostapd' 'scapy' 'python2'
         'python2-requests' 'python2-flask' 'python2-paramiko'
         'python2-smartencoding' 'python2-python-libnmap')
makedepends=('git')
source=('git+https://github.com/NORMA-Inc/AtEar.git')
sha1sums=('SKIP')

pkgver() {
  cd "$srcdir/AtEar"

  echo $(git rev-list --count HEAD).$(git rev-parse --short HEAD)
}

package() {
  cd "$srcdir/AtEar"

  mkdir -p "$pkgdir/usr/bin"
  mkdir -p "$pkgdir/usr/share/atear"

  install -Dm644 -t "$pkgdir/usr/share/doc/atear/" README.md
  install -Dm644 LICENSE "$pkgdir/usr/share/licenses/atear/LICENSE"

  rm README.md LICENSE

  cp -a * "$pkgdir/usr/share/atear"

  cat > "$pkgdir/usr/bin/atear" << EOF
#!/bin/sh
cd /usr/share/atear
exec python2 run.py "\${@}"
EOF

  chmod a+x "$pkgdir/usr/bin/atear"
}
