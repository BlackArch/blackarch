# This file is part of BlackArch Linux ( https://www.blackarch.org/ ).
# See COPYING for license details.

pkgbase=python-google-search
pkgname=('python2-google-search' 'python-google-search')
pkgver=1.0.2
pkgrel=1
pkgdesc='Library for scraping google search results.'
arch=('any')
url='https://pypi.org/project/google-search/#files'
license=('MIT')
makedepends=('python2-setuptools' 'python-setuptools')
source=("https://files.pythonhosted.org/packages/f5/bd/095d622d731f963505a52b9f27fb6ccdd1b13bf7789afeae10e2711809b6/google-search-$pkgver.tar.gz")
sha512sums=('636c287e557213fe84a32ee4148d1c62a5269416dc3583db774b83ed3206dd6a53c1358a5269437277bec564afb5447ef5aec9b0aa3b85ae498395cde86fff47')

prepare() {
  cp -a "google-search-$pkgver" "google-search2-$pkgver"
}

build() {
  cd "google-search2-$pkgver"

  python2 setup.py build

  cd "$srcdir/google-search-$pkgver"

  python setup.py build
}

package_python2-google-search() {
  depends=('python2-beautifulsoup4' 'python2-lxml')

  cd "google-search2-$pkgver"

  python2 setup.py install --prefix=/usr --root="$pkgdir" --optimize=1 \
    --skip-build
}

package_python-google-search() {
  depends=('python-beautifulsoup4' 'python-lxml')

  cd "google-search-$pkgver"

  python setup.py install --prefix=/usr --root="$pkgdir" --optimize=1 \
    --skip-build
}

