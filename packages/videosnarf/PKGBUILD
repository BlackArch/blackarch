# This file is part of BlackArch Linux ( https://www.blackarch.org/ ).
# See COPYING for license details.

pkgname=videosnarf
pkgver=0.63
pkgrel=6
groups=('blackarch' 'blackarch-scanner')
pkgdesc='A new security assessment tool for pcap analysis.'
arch=('x86_64' 'aarch64')
url='http://ucsniff.sourceforge.net/videosnarf.html'
license=('GPL')
depends=('libpcap' 'libnet')
source=("http://downloads.sourceforge.net/project/ucsniff/videosnarf/videosnarf-$pkgver.tar.gz"
        'videosnarf-0.63-fix-linking.patch')

sha512sums=('d9d033bcea5c0cd6fed784fc4a9ec3796c75432e90d2d479dfc6989031270f9eaf7654a79fad866319d0c3f1cdc0aab325fd1cebeadfcf9ee0a8d875f314a6de'
            '1be90f258f3f92e05cdfe58c0677b92be895bf2e742d080cfe077b75295f008dbac6d51830f961197f8606668b510dc9c8fb2beb18a18682dbc9c00206165830')

build() {
  cd "$pkgname-$pkgver"

  patch -Np1 -i ../videosnarf-0.63-fix-linking.patch

  autoreconf -fi

  ./configure --prefix=/usr

  make
}

package() {
  cd "$pkgname-$pkgver"

  make DESTDIR="$pkgdir" install
}

