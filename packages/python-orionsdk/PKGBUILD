# This file is part of BlackArch Linux ( https://www.blackarch.org/ ).
# See COPYING for license details.

pkgbase=python-orionsdk
pkgname=('python2-orionsdk' 'python-orionsdk')
_pkgname=orionsdk
pkgver=0.1.0
pkgrel=1
pkgdesc='Nanosecond resolution temporal types.'
arch=('any')
url='https://pypi.org/project/orionsdk/#files'
license=('APACHE')
makedepends=('python2-setuptools' 'python-setuptools')
source=("https://files.pythonhosted.org/packages/11/86/2dfa0044f474cede4bf34ca9a17ac84ec22278a43e364ef40392ceeab8b5/$_pkgname-$pkgver.tar.gz")
sha512sums=('22322075289509d6dd129416dc449bdea972568779a6645b3ca6eee3139bd0e435febedd4bcbfa85625ec9f340eeaa332e9ce6ac02272bee30f96c6c651de96c')

prepare() {
  cp -a "$_pkgname-$pkgver"{,-2}
}

build() {
  cd "$_pkgname-$pkgver-2"

  python2 setup.py build

  cd "$srcdir/$_pkgname-$pkgver"

  python setup.py build
}

package_python2-orionsdk() {
  depends=('python2' 'python2-requests' 'python2-six')

  cd "$_pkgname-$pkgver-2"

  python2 setup.py install --prefix=/usr --root="$pkgdir" --optimize=1 \
    --skip-build
}

package_python-orionsdk() {
  depends=('python' 'python-requests' 'python-six')

  cd "$_pkgname-$pkgver"

  python setup.py install --prefix=/usr --root="$pkgdir" --optimize=1 \
    --skip-build
}

