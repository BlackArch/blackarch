# This file is part of BlackArch Linux ( https://www.blackarch.org/ ).
# See COPYING for license details.

pkgbase=python-orionsdk
pkgname=('python2-orionsdk' 'python-orionsdk')
_pkgname=orionsdk
pkgver=0.0.6
pkgrel=1
pkgdesc='Nanosecond resolution temporal types.'
arch=('any')
url='https://pypi.org/project/orionsdk/#files'
license=('APACHE')
makedepends=('python2-setuptools' 'python-setuptools')
source=("https://files.pythonhosted.org/packages/30/38/5e7e67207ae8dad99da52de38529d729c5748c770385af6f813d44fd5434/$_pkgname-$pkgver.zip")
sha512sums=('9a109348a4c575642eab6f783bd0824c9a60f7c981388c7bfcfc1f513658d6d63794cfbcf66ff77f6ef7cf6d78a5691dc2882b2b8adc71f6482731433142ecac')

prepare() {
  cp -a "$_pkgname-$pkgver"{,-2}
}

build() {
  cd "$_pkgname-$pkgver-2"

  python2 setup.py build

  cd "$srcdir/$_pkgname-$pkgver"

  python setup.py build
}

package_python2-orionsdk() {
  depends=('python2' 'python2-requests' 'python2-six')

  cd "$_pkgname-$pkgver-2"

  python2 setup.py install --prefix=/usr --root="$pkgdir" --optimize=1 \
    --skip-build
}

package_python-orionsdk() {
  depends=('python' 'python-requests' 'python-six')

  cd "$_pkgname-$pkgver"

  python setup.py install --prefix=/usr --root="$pkgdir" --optimize=1 \
    --skip-build
}

