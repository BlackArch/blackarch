# This file is part of BlackArch Linux ( http://blackarch.org ).
# See COPYING for license details.

pkgname='dotdotpwn'
pkgver='3.0.2'
pkgrel=1
groups=('blackarch' 'blackarch-exploitation' 'blackarch-fuzzer')
pkgdesc="The Transversal Directory Fuzzer"
url="http://dotdotpwn.blogspot.com"
arch=('any')
license=('GPL2')
depends=('perl' 'perl-http-lite' 'perl-tftp' 'perl-switch' 'perl-io-socket'
         'perl-io-socket-ssl')
install=dotdotpwn.install
source=("https://github.com/wireghoul/dotdotpwn/archive/${pkgver}.tar.gz")
sha1sums=('595a6bbabdbb1468d1f8e2bf40c292e346b63b85')

package() {
  cd "$srcdir/dotdotpwn-$pkgver"

  mkdir -p "$pkgdir/usr/bin"
  mkdir -p "$pkgdir/usr/share/dotdotpwn"

  cp -a * "$pkgdir/usr/share/dotdotpwn"

  cat > "$pkgdir/usr/bin/dotdotpwn" <<EOF
#!/bin/sh
cd /usr/share/dotdotpwn
exec perl dotdotpwn.pl "\${@}"
EOF
  chmod +x "$pkgdir/usr/bin/dotdotpwn"
}
