# This file is part of BlackArch Linux ( http://blackarch.org ).
# See COPYING for license details.

pkgname=dotdotpwn
pkgver=3.0.2
pkgrel=1
groups=('blackarch' 'blackarch-exploitation' 'blackarch-fuzzer')
pkgdesc="The Transversal Directory Fuzzer"
url="http://dotdotpwn.blogspot.com"
arch=('any')
license=('GPL2')
depends=('perl' 'perl-http-lite' 'perl-tftp' 'perl-switch' 'perl-io-socket'
         'perl-io-socket-ssl')
install=dotdotpwn.install
source=("https://github.com/wireghoul/dotdotpwn/archive/${pkgver}.tar.gz")
sha512sums=('2621e6fd45b2ace56d83d634ee702fce4ec0dec5fd8819bd33ecfa2b5fa286249744377931a512c574f907426fdfdf4a1069f46b07a83b73a1bc00f1045cc973')

package() {
  cd "$srcdir/dotdotpwn-$pkgver"

  install -dm 755 "$pkgdir/usr/bin"
  install -dm 755 "$pkgdir/usr/share/dotdotpwn"

  cp -a * "$pkgdir/usr/share/dotdotpwn"

  cat > "$pkgdir/usr/bin/dotdotpwn" <<EOF
#!/bin/sh
cd /usr/share/dotdotpwn
exec perl dotdotpwn.pl "\${@}"
EOF
  chmod +x "$pkgdir/usr/bin/dotdotpwn"
}
