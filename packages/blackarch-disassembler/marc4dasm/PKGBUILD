# This file is part of BlackArch Linux ( https://www.blackarch.org/ ).
# See COPYING for license details.

pkgname=marc4dasm
pkgver=6.f11860f
pkgrel=3
groups=('blackarch' 'blackarch-disassembler' 'blackarch-hardware'
        'blackarch-reversing')
pkgdesc='A disassembler for the Atmel MARC4 (a 4 bit Harvard micro).'
arch=('any')
url='https://github.com/ApertureLabsLtd/marc4dasm'
license=('custom:unknown')
depends=('python2')
makedepends=('git')
source=("git+https://github.com/ApertureLabsLtd/$pkgname.git")
sha512sums=('SKIP')

pkgver() {
  cd $pkgname

  echo $(git rev-list --count HEAD).$(git rev-parse --short HEAD)
}

prepare() {
  cd $pkgname

  sed -i 's|/usr/bin/env python|/usr/bin/env python2|' "$pkgname.py"
}

package() {
  cd $pkgname

  install -Dm 755 "$pkgname.py" "$pkgdir/usr/bin/$pkgname"
  install -Dm 644 -t "$pkgdir/usr/share/doc/$pkgname/" README.md
}

