# This file is part of BlackArch Linux ( https://www.blackarch.org/ ).
# See COPYING for license details.

pkgname=rdwatool
pkgver=1.2.r12.g6c47a21
pkgrel=1
pkgdesc='Python script to extract information from a Microsoft Remote Desktop Web Access (RDWA) application'
arch=('any')
groups=('blackarch' 'blackarch-windows')
url='https://github.com/p0dalirius/rdwatool'
license=('GPL-2.0-only')
depends=('python' 'python-beautifulsoup4' 'python-requests' 'python-urllib3' 'python-xlsxwriter')
makedepends=('git' 'python-build' 'python-installer' 'python-setuptools' 'python-wheel')
source=("git+$url.git")
sha512sums=('SKIP')

pkgver() {
  cd $pkgname

  ( set -o pipefail
    git describe --long --tags --abbrev=7 2>/dev/null |
      sed 's/\([^-]*-g\)/r\1/;s/-/./g' ||
    printf "%s.%s" "$(git rev-list --count HEAD)" \
      "$(git rev-parse --short=7 HEAD)"
  )
}

build() {
  cd $pkgname

  python -m build --wheel --no-isolation
}

package() {
  cd $pkgname

  install -Dm 644 -t "$pkgdir/usr/share/doc/$pkgname/" README.md

  python -m installer --destdir="$pkgdir" dist/*.whl
}

