# This file is part of BlackArch Linux ( https://www.blackarch.org/ ).
# See COPYING for license details.

pkgbase=python-pyghmi
pkgname=('python2-pyghmi' 'python-pyghmi')
_pkgname=pyghmi
pkgver=1.5.17
pkgrel=1
pkgdesc='Python General Hardware Management Initiative (IPMI and others).'
arch=('any')
url='https://pypi.org/project/pyghmi/#files'
license=('Apache')
makedepends=('python2-setuptools' 'python-setuptools')
source=("https://files.pythonhosted.org/packages/6b/71/f7f31fcd0cba1ac01f0b05215f63c900a8ebdc2eed11ddfb6849e357fc80/$_pkgname-$pkgver.tar.gz")
sha512sums=('cb007e4d9cbf52afa7cb1b4465baa761c611032bc2e4f47241ce1a6ca25cbcb908bfe23888cdfa78289be29f70b4afe3ee63e24806e94126b0cec05c3ef994c1')

prepare() {
  cp -a "$_pkgname-$pkgver"{,-2}
}

build() {
  cd "$_pkgname-$pkgver-2"

  python2 setup.py build

  cd "$srcdir/$_pkgname-$pkgver"

  python setup.py build
}

package_python2-pyghmi() {
  depends=('python2' 'python2-pbr')

  cd "$_pkgname-$pkgver-2"

  python2 setup.py install --prefix=/usr --root="$pkgdir" --optimize=1 \
    --skip-build

  for i in "$pkgdir/usr/bin/"* ; do
    mv "$i" "${i}2"
  done
}

package_python-pyghmi() {
  depends=('python' 'python-pbr')

  cd "$_pkgname-$pkgver"

  python setup.py install --prefix=/usr --root="$pkgdir" --optimize=1 \
    --skip-build
}

