# This file is part of BlackArch Linux ( https://www.blackarch.org/ ).
# See COPYING for license details.

pkgbase=python-pyghmi
pkgname=('python2-pyghmi' 'python-pyghmi')
_pkgname=pyghmi
pkgver=1.5.14
pkgrel=1
pkgdesc='Python General Hardware Management Initiative (IPMI and others).'
arch=('any')
url='https://pypi.org/project/pyghmi/#files'
license=('Apache')
makedepends=('python2-setuptools' 'python-setuptools')
source=("https://files.pythonhosted.org/packages/c9/f1/13bf7e4e24ae3077a32409ad8d64e94d1acf394f75bc532158509d0be05d/$_pkgname-$pkgver.tar.gz")
sha512sums=('b2dfa2be53e0936a7dfa91ef7b5aff3c44f848c8dbe818780886f6ee31fc1cdcdd66b106dd76ba7fa6b9ef5690d016064b0a092b173a868e12c3139a1e229acb')

prepare() {
  cp -a "$_pkgname-$pkgver"{,-2}
}

build() {
  cd "$_pkgname-$pkgver-2"

  python2 setup.py build

  cd "$srcdir/$_pkgname-$pkgver"

  python setup.py build
}

package_python2-pyghmi() {
  depends=('python2' 'python2-pbr')

  cd "$_pkgname-$pkgver-2"

  python2 setup.py install --prefix=/usr --root="$pkgdir" --optimize=1 \
    --skip-build

  for i in "$pkgdir/usr/bin/"* ; do
    mv "$i" "${i}2"
  done
}

package_python-pyghmi() {
  depends=('python' 'python-pbr')

  cd "$_pkgname-$pkgver"

  python setup.py install --prefix=/usr --root="$pkgdir" --optimize=1 \
    --skip-build
}

