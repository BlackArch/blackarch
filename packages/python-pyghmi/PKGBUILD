# This file is part of BlackArch Linux ( https://www.blackarch.org/ ).
# See COPYING for license details.

pkgbase=python-pyghmi
pkgname=('python2-pyghmi' 'python-pyghmi')
_pkgname=pyghmi
pkgver=1.5.29
pkgrel=2
pkgdesc='Python General Hardware Management Initiative (IPMI and others).'
arch=('any')
url='https://pypi.org/project/pyghmi/#files'
license=('Apache')
makedepends=('python2-setuptools' 'python-setuptools')
source=("https://files.pythonhosted.org/packages/5a/6a/006df0753674adbd931dbc542e4abe8f0a2e2f13e8026d4b35da63c5af8b/$_pkgname-$pkgver.tar.gz")
sha512sums=('3e3ecbf4efa179b872d8cc24774e81f6946fa122bb5f3b5de3598e17b448eb5010d6bf6f783b7c27422b71e4739e8b34127ff1c3b5dfec341382f49b2b691c29')

prepare() {
  cp -a "$_pkgname-$pkgver"{,-2}
}

build() {
  cd "$_pkgname-$pkgver-2"

  python2 setup.py build

  cd "$srcdir/$_pkgname-$pkgver"

  python setup.py build
}

package_python2-pyghmi() {
  depends=('python2' 'python2-pbr')

  cd "$_pkgname-$pkgver-2"

  python2 setup.py install --root="$pkgdir" --prefix=/usr -O1 --skip-build

  for i in "$pkgdir/usr/bin/"* ; do
    mv "$i" "${i}2"
  done
}

package_python-pyghmi() {
  depends=('python' 'python-pbr')

  cd "$_pkgname-$pkgver"

  python setup.py install --root="$pkgdir" --prefix=/usr -O1 --skip-build
}

