# This file is part of BlackArch Linux ( https://www.blackarch.org/ ).
# See COPYING for license details.

pkgbase=python-pyghmi
pkgname=('python2-pyghmi' 'python-pyghmi')
_pkgname=pyghmi
pkgver=1.5.25
pkgrel=1
pkgdesc='Python General Hardware Management Initiative (IPMI and others).'
arch=('any')
url='https://pypi.org/project/pyghmi/#files'
license=('Apache')
makedepends=('python2-setuptools' 'python-setuptools')
source=("https://files.pythonhosted.org/packages/e9/d5/8aa17aa6360954f91c9c1ab945c777316ec546759c9fbaeb9d96ce12ad13/$_pkgname-$pkgver.tar.gz")
sha512sums=('eba687c10c3bfc33bd6b933511b6e2111effc2cab5a12c3972e039198c50e2b0691d00e28c03b774001ae9babff37ed06a10a214241dc28a0cfce78ad87c7635')

prepare() {
  cp -a "$_pkgname-$pkgver"{,-2}
}

build() {
  cd "$_pkgname-$pkgver-2"

  python2 setup.py build

  cd "$srcdir/$_pkgname-$pkgver"

  python setup.py build
}

package_python2-pyghmi() {
  depends=('python2' 'python2-pbr')

  cd "$_pkgname-$pkgver-2"

  python2 setup.py install --prefix=/usr --root="$pkgdir" --optimize=1 \
    --skip-build

  for i in "$pkgdir/usr/bin/"* ; do
    mv "$i" "${i}2"
  done
}

package_python-pyghmi() {
  depends=('python' 'python-pbr')

  cd "$_pkgname-$pkgver"

  python setup.py install --prefix=/usr --root="$pkgdir" --optimize=1 \
    --skip-build
}

