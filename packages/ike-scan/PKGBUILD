# This file is part of BlackArch Linux ( https://www.blackarch.org/ ).
# See COPYING for license details.

pkgname=ike-scan
pkgver=1.9.5
pkgrel=2
groups=('blackarch' 'blackarch-scanner')
pkgdesc='A tool that uses IKE protocol to discover, fingerprint and test IPSec VPN servers.'
arch=('x86_64' 'aarch64')
url='http://www.nta-monitor.com/tools/ike-scan/'
license=('GPL')
depends=('openssl')
makedepends=('make' 'gcc')
source=("https://github.com/royhills/ike-scan/archive/refs/tags/$pkgver.tar.gz")
sha512sums=('5eb98e41c639bc30996041f4e759a6ada7f4baa5b8102992ac2313ed8ff51032d877b116bc24972e0ecd652290849aa67a63523d92ee63af6577112350ee5590')

prepare() {
  cd "$pkgname-$pkgver"

  aclocal
  autoheader
  automake --add-missing
  autoconf
}

build() {
  cd "$pkgname-$pkgver"

  if [[ "$CARCH" == "x86_64" ]]; then
    ./configure --prefix=/usr --mandir=/usr/share/man --with-openssl
  else
    ./configure --prefix=/usr --mandir=/usr/share/man --with-openssl \
      --build=arm
  fi
}

package() {
  cd "$pkgname-$pkgver"

  make DESTDIR="$pkgdir" install
}

