# This file is part of BlackArch Linux ( http://blackarch.org ).
# See COPYING for license details.

pkgname=hsecscan
pkgver=63.996d127
pkgrel=1
pkgdesc='A security scanner for HTTP response headers.'
groups=('blackarch' 'blackarch-scanner')
arch=('any')
url='https://github.com/riramar/hsecscan'
license=('GPL2')
depends=('python2' 'python2-simplejson')
makedepends=('git')
source=('git+https://github.com/riramar/hsecscan.git')
sha512sums=('SKIP')

pkgver() {
	cd "$srcdir/hsecscan"

  echo $(git rev-list --count HEAD).$(git rev-parse --short HEAD)
}

package() {
	cd "$srcdir/hsecscan"

  install -dm 755 "$pkgdir/usr/bin"
  install -dm 755 "$pkgdir/usr/share/hsecscan"

  install -Dm644 -t "$pkgdir/usr/share/doc/hsecscan" README.md
  install -Dm644 LICENSE "$pkgdir/usr/share/licenses/hsecscan/LICENSE"

  rm README.md LICENSE

  cp -a * "$pkgdir/usr/share/hsecscan"

  cat > "$pkgdir/usr/bin/hsecscan" << EOF
#!/bin/sh
cd /usr/share/hsecscan
exec python2 hsecscan.py "\$@"
EOF

  chmod a+x "$pkgdir/usr/bin/hsecscan"
}
