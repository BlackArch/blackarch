# This file is part of BlackArch Linux ( https://www.blackarch.org/ ).
# See COPYING for license details.

pkgname=autosploit
pkgver=256.5f5954a
pkgrel=1
groups=('blackarch' 'blackarch-exploitation' 'blackarch-automation')
pkgdesc='Automate the exploitation of remote hosts.'
arch=('any')
url='https://github.com/NullArray/AutoSploit'
license=('GPL3')
depends=('python2' 'python2-requests' 'python2-psutil' 'python2-blessings'
         'shodan' 'metasploit' 'sudo')
makedepends=('git')
source=('git+https://github.com/NullArray/AutoSploit.git')
sha512sums=('SKIP')

pkgver() {
  cd AutoSploit

  echo "$(git rev-list --count HEAD)"."$(git rev-parse --short HEAD)"
}

prepare() {
  cd AutoSploit

  sed -i 's/sudo service {} start/sudo systemctl start {}/g' autosploit/main.py
}

package() {
  cd AutoSploit

  install -dm 755 "$pkgdir/usr/bin"
  install -dm 755 "$pkgdir/usr/share/$pkgname/Docker"

  install -Dm 644 -t "$pkgdir/usr/share/doc/$pkgname" README.md CONTRIBUTING.md
  install -Dm 644 LICENSE "$pkgdir/usr/share/licenses/autosploit/LICENSE"

  rm *.md LICENSE

  cp -a * "$pkgdir/usr/share/$pkgname/"

  cat > "$pkgdir/usr/bin/$pkgname" << EOF
#!/bin/sh
cd /usr/share/$pkgname
exec python2 ./autosploit.py "\$@"
EOF

  chmod +x "$pkgdir/usr/bin/$pkgname"
}

