# This file is part of BlackArch Linux ( https://www.blackarch.org/ ).
# See COPYING for license details.

pkgname=python-viewstate
_pkgname=${pkgname#python-}
pkgver=v0.7.0.r0.g0bb00b3
pkgrel=1
pkgdesc='Python 3 library for decoding .NET viewstate.'
arch=('any')
url='https://github.com/yuvadm/viewstate'
license=('MIT')
depends=('python')
makedepends=('git' 'python-build' 'python-pip')
options=(!emptydirs)
source=("git+https://github.com/yuvadm/$_pkgname.git")
sha512sums=('SKIP')

pkgver() {
  cd $_pkgname

  ( set -o pipefail
    git describe --long --tags --abbrev=7 2>/dev/null |
      sed 's/\([^-]*-g\)/r\1/;s/-/./g' ||
    printf "%s.%s" "$(git rev-list --count HEAD)" \
      "$(git rev-parse --short=7 HEAD)"
  )
}

build() {
  cd $_pkgname

  python -m build --wheel --outdir="$startdir/dist"
}

package() {
  cd $_pkgname

  pip install \
    --verbose \
    --disable-pip-version-check \
    --no-warn-script-location \
    --ignore-installed \
    --no-compile \
    --no-deps \
    --root="$pkgdir" \
    --prefix=/usr \
    --no-index \
    --find-links="file://$startdir/dist" \
    $_pkgname
}

