# This file is part of BlackArch Linux ( https://www.blackarch.org/ ).
# See COPYING for license details.

pkgname=brutespray
pkgver=409.e1cbab8
pkgrel=1
pkgdesc='Brute-Forcing from Nmap output - Automatically attempts default creds on found services.'
groups=('blackarch' 'blackarch-automation' 'blackarch-cracker')
arch=('x86_64' 'aarch64')
url='https://github.com/x90skysn3k/brutespray'
license=('MIT')
depends=()
makedepends=('git' 'go')
source=("git+https://github.com/x90skysn3k/$pkgname.git")
sha512sums=('SKIP')

pkgver() {
  cd $pkgname

  echo $(git rev-list --count HEAD).$(git rev-parse --short HEAD)
}

build() {
  cd $pkgname

  GOPATH="$srcdir" go mod download
  GOPATH="$srcdir" go build \
    -trimpath \
    -buildmode=pie \
    -mod=readonly \
    -modcacherw \
    -ldflags "-s -w" \
    -o $pkgname .
}

package() {
  cd $pkgname

  install -Dm 755 "$pkgname/$pkgname" "$pkgdir/usr/bin/$pkgname"
  install -Dm 644 -t "$pkgdir/usr/share/doc/$pkgname/" README.md
  # Only install license if required according to
  # https://wiki.archlinux.org/index.php/PKGBUILD#license
  install -Dm 644 LICENSE "$pkgdir/usr/share/licenses/$pkgname/LICENSE"
}

