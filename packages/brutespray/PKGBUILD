# This file is part of BlackArch Linux ( https://www.blackarch.org/ ).
# See COPYING for license details.

pkgname=brutespray
pkgver=148.340af6d
pkgrel=1
pkgdesc='Brute-Forcing from Nmap output - Automatically attempts default creds on found services.'
groups=('blackarch' 'blackarch-automation' 'blackarch-cracker')
arch=('any')
url='https://github.com/x90skysn3k/brutespray'
license=('MIT')
depends=('python2' 'python2-argparse' 'python2-lxml' 'python2-argcomplete'
         'medusa')
makedepends=('git')
source=("git+https://github.com/x90skysn3k/$pkgname.git")
sha512sums=('SKIP')

pkgver() {
  cd $pkgname

  echo $(git rev-list --count HEAD).$(git rev-parse --short HEAD)
}

package() {
  cd $pkgname

  install -dm 755 "$pkgdir/usr/bin"
  install -dm 755 "$pkgdir/usr/share/$pkgname"

  install -Dm 644 -t "$pkgdir/usr/share/doc/$pkgname/" README.md
  install -Dm 644 LICENSE.md "$pkgdir/usr/share/licenses/$pkgname/LICENSE.md"

  rm README.md LICENSE.md

  cp -a * "$pkgdir/usr/share/$pkgname/"

  cat > "$pkgdir/usr/bin/$pkgname" << EOF
#!/bin/sh
cd /usr/share/$pkgname
exec python2 $pkgname.py "\$@"
EOF

  chmod +x "$pkgdir/usr/bin/$pkgname"
}

