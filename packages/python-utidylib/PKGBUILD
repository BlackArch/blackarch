# This file is part of BlackArch Linux ( https://www.blackarch.org/ ).
# See COPYING for license details.

pkgname=python-utidylib
_pkgname=uTidylib
pkgver=0.8
pkgrel=2
groups=('blackarch' 'blackarch-misc')
pkgdesc='Python bindings for Tidy HTML parser/cleaner.'
arch=('any')
url='http://utidylib.berlios.de'
license=('MIT')
depends=('tidyhtml')
makedepends=('python2>=2.5')
source=("https://files.pythonhosted.org/packages/source/${_pkgname::1}/$_pkgname/uTidylib-$pkgver.tar.gz")
sha512sums=('2b8d2040b909b75809a563392e69c1c27218d7b0958af54844d0d0456d9401712e5ad56a0903b5c278effe97158080fa39c711b2b4d5277a7f7f9ba3ef09051f')

build() {
  cd "$_pkgname-$pkgver"

  python2 setup.py build
}

package() {
  cd "$_pkgname-$pkgver"

  python2 setup.py install --prefix=/usr --root="$pkgdir" --optimize=1 \
    --skip-build

  install -Dm 644 LICENSE "$pkgdir/usr/share/licenses/python-utidylib/LICENSE"
}

