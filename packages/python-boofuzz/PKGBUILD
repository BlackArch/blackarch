# This file is part of BlackArch Linux ( https://www.blackarch.org/ ).
# See COPYING for license details.

pkgname=python-boofuzz
_pkgname=boofuzz
pkgver=0.4.0
pkgrel=2
pkgdesc='Boofuzz is a fork of Sulley fuzzing framework and aims for extensibility. The goal: fuzz everything.'
arch=('any')
url='https://pypi.org/project/boofuzz/'
license=('GPL2')
depends=( 'python' 'python-attrs' 'python-backports.shutil-get-terminal-size'
          'python-click' 'python-flask' 'python-future' 'impacket'
          'python-psutil' 'python-pyserial' 'python-pydot' 'python-six'
          'python-tornado')
makedepends=('python-setuptools')
source=("https://files.pythonhosted.org/packages/74/df/9241e37dfc75c14535a0e3fdc6bc5b8cb9ccff4954aca72c0305bd870de8/$_pkgname-$pkgver.tar.gz")
sha512sums=('f76cc3e7aeeb5e776488d7d2efc33e682981f713720596a81cfa475ab72e3a5f8350b0f94b3ad685acba225dc684f3985545f6c8a8fb1d4d241d4cf7d22ff331')

build() {
  cd "$_pkgname-$pkgver"

  python setup.py build
}

package() {
  cd "$_pkgname-$pkgver"

  install -Dm 644 -t "$pkgdir/usr/share/doc/$pkgname/" README.rst
  install -Dm 644 -t "$pkgdir/usr/share/licenses/$pkgname/" LICENSE.txt

  python setup.py install --prefix=/usr --root="$pkgdir" -O1 --skip-build
}

