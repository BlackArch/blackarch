# This file is part of BlackArch Linux ( https://www.blackarch.org/ ).
# See COPYING for license details.

pkgname=python-boofuzz
_pkgname=boofuzz
pkgver=0.4.1
pkgrel=1
pkgdesc='Boofuzz is a fork of Sulley fuzzing framework and aims for extensibility. The goal: fuzz everything.'
arch=('any')
url='https://pypi.org/project/boofuzz/'
license=('GPL2')
depends=( 'python' 'python-attrs' 'python-backports.shutil-get-terminal-size'
          'python-click' 'python-flask' 'python-future' 'impacket'
          'python-psutil' 'python-pyserial' 'python-pydot' 'python-six'
          'python-tornado')
makedepends=('python-setuptools')
source=("https://files.pythonhosted.org/packages/1e/df/00db41f5d1400d3cc3dd97a6ea9e911cf3ace8cd4153aa2d89a88e36a57a/$_pkgname-$pkgver.tar.gz")
sha512sums=('f9a7698253e5bb111cd7e2f580684f8b70574a71fa3273a23263b004995e622598eb3cc55704f32502bb21eb6ba93e368ac95cf0458eb557b705ce43b3625ede')

build() {
  cd "$_pkgname-$pkgver"

  python setup.py build
}

package() {
  cd "$_pkgname-$pkgver"

  install -Dm 644 -t "$pkgdir/usr/share/doc/$pkgname/" README.rst
  install -Dm 644 -t "$pkgdir/usr/share/licenses/$pkgname/" LICENSE.txt

  python setup.py install --prefix=/usr --root="$pkgdir" -O1 --skip-build
}

