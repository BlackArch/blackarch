# This file is part of BlackArch Linux ( https://www.blackarch.org/ ).
# See COPYING for license details.

pkgname=python-boofuzz
_pkgname=boofuzz
pkgver=0.2.0
pkgrel=1
pkgdesc='Boofuzz is a fork of Sulley fuzzing framework and aims for extensibility. The goal: fuzz everything.'
arch=('any')
url='https://pypi.org/project/boofuzz/'
license=('GPL2')
depends=( 'python' 'python-attrs' 'python-backports.shutil-get-terminal-size'
          'python-click' 'python-flask' 'python-future' 'impacket'
          'python-psutil' 'python-pyserial' 'python-pydot' 'python-six'
          'python-tornado')
makedepends=('python-setuptools')
source=("https://files.pythonhosted.org/packages/ae/27/1e1885d09b4a4c359a08455de9a2d5665f79150058371897b08e4b0d7163/$_pkgname-$pkgver.tar.gz")
sha512sums=('f1f1efb699427f0254bb1fb4505647d1bea7722f1eb042e5b3b4184d3dbdc6e3e6e7668c2a0561ebfdc49c5c57ac682b3b5e966b42e0c401c8530fe6f0ec9bcf')

build() {
  cd "$_pkgname-$pkgver"

  python setup.py build
}

package() {
  cd "$_pkgname-$pkgver"

  install -Dm 644 -t "$pkgdir/usr/share/doc/$pkgname/" README.rst
  install -Dm 644 -t "$pkgdir/usr/share/licenses/$pkgname/" LICENSE.txt

  python setup.py install --prefix=/usr --root="$pkgdir" -O1 --skip-build
}

