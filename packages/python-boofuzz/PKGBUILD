# This file is part of BlackArch Linux ( https://www.blackarch.org/ ).
# See COPYING for license details.

pkgname=python-boofuzz
_pkgname=boofuzz
pkgver=0.2.1
pkgrel=2
pkgdesc='Boofuzz is a fork of Sulley fuzzing framework and aims for extensibility. The goal: fuzz everything.'
arch=('any')
url='https://pypi.org/project/boofuzz/'
license=('GPL2')
depends=( 'python' 'python-attrs' 'python-backports.shutil-get-terminal-size'
          'python-click' 'python-flask' 'python-future' 'impacket'
          'python-psutil' 'python-pyserial' 'python-pydot' 'python-six'
          'python-tornado')
makedepends=('python-setuptools')
source=("https://files.pythonhosted.org/packages/96/b4/9e2156d6c01a0177f79e12beea8702b0949d7fa85aa9efb0029c6a89ffa1/$_pkgname-$pkgver.tar.gz")
sha512sums=('c80aa6ee870f1d6f18384cba09aaec3cda4ea69da321a99540a2b6559f28fc9e39e1891199f326360bf2f130afba5ae45f292c1b3684476a2a125f9bc5ba4c56')

build() {
  cd "$_pkgname-$pkgver"

  python setup.py build
}

package() {
  cd "$_pkgname-$pkgver"

  install -Dm 644 -t "$pkgdir/usr/share/doc/$pkgname/" README.rst
  install -Dm 644 -t "$pkgdir/usr/share/licenses/$pkgname/" LICENSE.txt

  python setup.py install --prefix=/usr --root="$pkgdir" -O1 --skip-build
}

