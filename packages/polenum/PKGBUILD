# This file is part of BlackArch Linux ( http://blackarch.org ).
# See COPYING for license details.

pkgname='polenum'
pkgver='0.2'
pkgrel=4
pkgdesc="a python script to extract the password policy information from a windows machine."
arch=('any')
url="http://labs.portcullis.co.uk/application/polenum/"
license=('custom')
depends=('impacket' 'python2')
source=("http://labs.portcullis.co.uk/download/polenum-$pkgver.tar.bz2")
sha1sums=('1591a8635de714d4f9bff799cf7dc7e6bb5dfa69')

prepare() {
  cd "$srcdir/polenum-$pkgver"

  sed -i '1s/python$/&2.7/' polenum.py
}

package() {
  cd "$srcdir/polenum-$pkgver"

  # Base directories.
  install -dm755 "$pkgdir/usr/bin"
  install -dm755 "$pkgdir/usr/share/licenses/polenum"

  # Bin.
  install -m755 polenum.py "$pkgdir/usr/bin/"

  # License.
  install -m644 LICENSE "$pkgdir/usr/share/licenses/polenum"
}
