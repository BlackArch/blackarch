# This file is part of BlackArch Linux ( https://www.blackarch.org/ ).
# See COPYING for license details.

pkgname=polenum
pkgver=12.b1a3bdc
pkgrel=1
pkgdesc='A python script to extract the password policy information from a windows machine.'
arch=('any')
url='https://github.com/Wh1t3Fox/polenum/'
license=('custom')
depends=('impacket' 'python2')
source=('git+https://github.com/Wh1t3Fox/polenum.git')
sha512sums=('SKIP')

pkgver() {
  cd $pkgname

  echo $(git rev-list --count HEAD).$(git rev-parse --short HEAD)
}

prepare() {
  cd "$pkgname"

  sed -i '1s/python$/&2.7/' polenum.py
}

package() {
  cd "$pkgname"

  install -Dm 755 polenum.py "$pkgdir/usr/bin/$pkgname"
  install -Dm 644 -t "$pkgdir/usr/share/doc/$pkgname/" README.md
  install -Dm 644 LICENSE "$pkgdir/usr/share/licenses/$pkgname/LICENSE"
}

