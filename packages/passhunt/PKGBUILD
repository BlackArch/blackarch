# This file is part of BlackArch Linux ( http://blackarch.org ).
# See COPYING for license details.

pkgname=passhunt
pkgver=5.332f374
pkgrel=2
groups=('blackarch' 'blackarch-scanner')
pkgdesc='Search drives for documents containing passwords.'
arch=('any')
url='https://github.com/Dionach/PassHunt'
license=('GPL3')
depends=('python2' 'python2-colorama' 'python2-progressbar')
makedepends=('git')
source=('git+https://github.com/Dionach/PassHunt.git')
sha512sums=('SKIP')

pkgver() {
  cd "$srcdir/PassHunt"

  echo $(git rev-list --count HEAD).$(git rev-parse --short HEAD)
}

package() {
  cd "$srcdir/PassHunt"

  install -dm 755 "$pkgdir/usr/bin"
  install -dm 755 "$pkgdir/usr/share/passhunt"

  install -Dm644 -t "$pkgdir/usr/share/doc/passhunt" README.md
  install -Dm644 LICENSE "$pkgdir/usr/share/licenses/passhunt/LICENSE"

  rm README.md LICENSE

  cp -a * "$pkgdir/usr/share/passhunt"

  cat > "$pkgdir/usr/bin/passhunt" << EOF
#!/bin/sh
cd /usr/share/passhunt
exec python2 passhunt.py "\$@"
EOF

  chmod a+x "$pkgdir/usr/bin/passhunt"
}
