# This file is part of BlackArch Linux ( https://www.blackarch.org/ ).
# See COPYING for license details.

pkgname=usb-canary
pkgver=31.bb23552
pkgrel=2
pkgdesc='A Linux or OSX tool that uses psutil to monitor devices while your computer is locked.'
groups=('blackarch' 'blackarch-defensive')
arch=('any')
url='https://github.com/errbufferoverfl/usb-canary'
license=('GPL3')
depends=('python2' 'python2-psutil' 'python2-python-pushover' 'python2-twilio'
         'python2-slackclient' 'python2-sander-daemon')
makedepends=('git')
conflicts=('pshitt')
source=("git+https://github.com/errbufferoverfl/$pkgname.git")
sha512sums=('SKIP')

pkgver() {
  cd $pkgname

  echo $(git rev-list --count HEAD).$(git rev-parse --short HEAD)
}

package() {
  cd $pkgname

  install -dm 755 "$pkgdir/usr/bin"
  install -dm 755 "$pkgdir/usr/share/$pkgname"

  install -Dm 644 -t "$pkgdir/usr/share/doc/$pkgname/" *.md
  install -Dm 644 LICENSE.txt "$pkgdir/usr/share/licenses/$pkgname/LICENSE"

  rm README.md LICENSE.txt

  cp -a * "$pkgdir/usr/share/$pkgname/"

  cat > "$pkgdir/usr/bin/$pkgname" << EOF
#!/bin/sh
cd /usr/share/$pkgname
exec python2 usbcanary.py "\$@"
EOF

  chmod +x "$pkgdir/usr/bin/$pkgname"
}

