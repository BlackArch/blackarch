# This file is part of BlackArch Linux ( http://blackarch.org ).
# See COPYING for license details.

pkgname='multiscanner'
pkgver=403.7dc6ffe
pkgrel=1
pkgdesc='Modular file scanning/analysis framework.'
groups=('blackarch' 'blackarch-scanner' 'blackarch-automation')
arch=('any')
url='https://github.com/mitre/multiscanner'
license=('Mozilla')
depends=('python2' 'python2-requests' 'python2-magic' 'python2-flask'
         'python2-configparser' 'python2-bitstring' 'python2-paramiko'
         'python2-pefile' 'python2-ssdeep' 'python2-sqlalchemy'
         'python2-sqlalchemy-utils' 'python2-pymongo' 'python2-pyclamd'
         'python2-elasticsearch')
makedepends=('git')
source=('git+https://github.com/mitre/multiscanner.git')
sha1sums=('SKIP')

pkgver() {
  cd "$srcdir/multiscanner"

  echo $(git rev-list --count HEAD).$(git rev-parse --short HEAD)
}

build() {
  cd "$srcdir/multiscanner"

  python2 setup.py build
}

package() {
  cd "$srcdir/multiscanner"

  mkdir -p "$pkgdir/usr/bin"
  mkdir -p "$pkgdir/usr/share/multiscanner"

  install -Dm644 -t "$pkgdir/usr/share/doc/multiscanner/" *.md AUTHORS
  install -Dm644 LICENSE "$pkgdir/usr/share/licenses/multiscanner/LICENSE"

  rm *.md AUTHORS LICENSE

  cp -a * "$pkgdir/usr/share/multiscanner"

  python2 setup.py install --root="$pkgdir" --prefix=/usr --optimize=1

  cat > "$pkgdir/usr/bin/multiscanner" << EOF
#!/bin/sh
cd /usr/share/multiscanner
exec python2 multiscanner.py "\${@}"
EOF

  chmod a+x "$pkgdir/usr/bin/multiscanner"
}
