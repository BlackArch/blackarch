# This file is part of BlackArch Linux ( https://www.blackarch.org/ ).
# See COPYING for license details.

pkgname=darkscrape
pkgver=63.4c225f3
pkgrel=2
pkgdesc='OSINT Tool For Scraping Dark Websites.'
groups=('blackarch' 'blackarch-webapp' 'blackarch-scanner' 'blackarch-recon')
arch=('any')
url='https://github.com/itsmehacker/DarkScrape'
license=('MIT')
depends=('python' 'python-xlrd' 'python-beautifulsoup4' 'python-requests'
         'python-face-recognition' 'tor')
makedepends=('git')
source=("$pkgname::git+https://github.com/itsmehacker/DarkScrape.git")
sha512sums=('SKIP')

pkgver() {
  cd $pkgname

  echo $(git rev-list --count HEAD).$(git rev-parse --short HEAD)
}

package() {
  cd $pkgname

  install -dm 755 "$pkgdir/usr/bin"
  install -dm 755 "$pkgdir/usr/share/$pkgname"

  install -Dm 644 -t "$pkgdir/usr/share/doc/$pkgname/" README.md
  install -Dm 644 LICENSE "$pkgdir/usr/share/licenses/$pkgname/LICENSE"

  rm README.md LICENSE

  cp -a * "$pkgdir/usr/share/$pkgname/"

  cat > "$pkgdir/usr/bin/$pkgname" << EOF
#!/bin/sh
cd /usr/share/$pkgname
exec python $pkgname.py "\$@"
EOF

  chmod a+x "$pkgdir/usr/bin/$pkgname"
}

