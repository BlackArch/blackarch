# This file is part of BlackArch Linux ( https://www.blackarch.org/ ).
# See COPYING for license details.

pkgbase=python-tracer
pkgname=('python2-tracer' 'python-tracer')
_pkgname=tracer
pkgver=0.3.2
pkgrel=8
pkgdesc='Centralized sys.settrace management.'
arch=('any')
url='https://pypi.python.org/pypi/tracer/'
license=('GPL3')
makedepends=('python2-setuptools' 'python-setuptools')
source=("https://pypi.python.org/packages/41/7c/73012494a7aa0d2e693ef0dc29304ea7968ac86a034ce8c0b0acd83640e3/$_pkgname-$pkgver.tar.gz")
sha512sums=('ba931ef11dea16aed58674825be04cd9e7ba68a3ad165b27c8be0f79ac40730d49da883fbf6de9b97707f5cdb4b34fa376b8fed59a7e9ca89cbc37f7ac1d4301')

prepare() {
  cp -a "$_pkgname-$pkgver"{,-2}
}

build() {
  cd "$_pkgname-$pkgver-2"

  python2 setup.py build

  cd "$srcdir/$_pkgname-$pkgver"

  python setup.py build
}

package_python2-tracer() {
  depends=('python2')

  cd "$_pkgname-$pkgver-2"

  python2 setup.py install --prefix=/usr --root="$pkgdir" --optimize=1 \
    --skip-build
}

package_python-tracer() {
  depends=('python')

  cd "$_pkgname-$pkgver"

  python setup.py install --prefix=/usr --root="$pkgdir" --optimize=1 \
    --skip-build
}

