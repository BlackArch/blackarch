# This file is part of BlackArch Linux ( https://www.blackarch.org/ ).
# See COPYING for license details.

pkgname=jwt-tool
_pkgname=jwt_tool
pkgver=47.c9230a8
pkgrel=1
pkgdesc='Toolkit for validating, forging and cracking JWTs (JSON Web Tokens).'
arch=('any')
groups=('blackarch' 'blackarch-cracker')
url='https://github.com/ticarpi/jwt_tool'
license=('GPL3')
depends=('python' 'python-pycryptodomex')
makedepends=('git')
source=("git+https://github.com/ticarpi/$_pkgname.git")
sha512sums=('SKIP')

pkgver() {
  cd $_pkgname

  echo $(git rev-list --count HEAD).$(git rev-parse --short HEAD)
}

package() {
  cd $_pkgname

  install -Dm 755 "$_pkgname.py" "$pkgdir/usr/bin/$pkgname"
  install -Dm 644 -t "$pkgdir/usr/share/doc/$pkgname/" README.md
}

