# This file is part of BlackArch Linux ( http://blackarch.org ).
# See COPYING for license details.

pkgname='wfuzz'
pkgver=518.e13cea0
pkgrel=2
groups=('blackarch' 'blackarch-fuzzer' 'blackarch-webapp')
pkgdesc='Utility to bruteforce web applications to find their not linked resources.'
url='https://github.com/xmendez/wfuzz'
arch=('any')
license=('GPL2')
depends=('python2' 'python2-pysqlite-legacy' 'python2-lxml' 'python2-pyxml'
         'python2-pycurl')
makedepends=('git')
source=('git+https://github.com/xmendez/wfuzz.git')
sha1sums=('SKIP')

pkgver() {
  cd "$srcdir/wfuzz"

  echo $(git rev-list --count HEAD).$(git rev-parse --short HEAD)
}

package() {
  cd "$srcdir/wfuzz"

  tools="wfencode wfuzz wfpayload wxfuzz"

  mkdir -p "$pkgdir/usr/bin"
  mkdir -p "$pkgdir/usr/share/wfuzz"

  install -Dm644 -t "$pkgdir/usr/share/doc/wfuzz/" README.md
  install -Dm644 LICENSE "$pkgdir/usr/share/licenses/wfuzz/LICENSE"

  rm LICENSE README.md setup.py

  cp -a * "$pkgdir/usr/share/wfuzz"

  for t in ${tools}
  do
    if [ "${t}" = "wfuzz" ]
    then
      cat > "$pkgdir/usr/bin/${t}" << EOF
#!/bin/sh
cd /usr/share/wfuzz
exec python2 src/${t}-cli.py "\${@}"
EOF
    else
      cat > "$pkgdir/usr/bin/${t}" << EOF
#!/bin/sh
cd /usr/share/wfuzz
exec python2 src/${t}.py "\${@}"
EOF
    fi
    chmod a+x "$pkgdir/usr/bin/${t}"
  done
}
