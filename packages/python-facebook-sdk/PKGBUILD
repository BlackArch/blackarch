# This file is part of BlackArch Linux ( https://www.blackarch.org/ ).
# See COPYING for license details.

pkgbase=python-facebook-sdk
pkgname=('python2-facebook-sdk' 'python-facebook-sdk')
_pkgname=facebook-sdk
pkgver=3.1.0
pkgrel=2
pkgdesc='Facebook Platform Python SDK.'
arch=('any')
url='https://pypi.org/project/facebook-sdk/#files'
license=('Apache')
makedepends=('python2-setuptools' 'python-setuptools')
source=("https://files.pythonhosted.org/packages/80/c7/e9b1677116e6a850b5f22630959b87c0d2605d3753d8f52eebd544f85662/$_pkgname-$pkgver.tar.gz")
sha512sums=('a77a33499347b032a44be7751563bbe6d9974d2eb24bfe66612e776c1ac9ea150b3be62f7dc19c0160ae6c06a8d52557365af5ecb5294f66917b729fe57c1737')

prepare() {
  cp -a "$_pkgname-$pkgver"{,-2}
}

build() {
  cd "$_pkgname-$pkgver-2"

  python2 setup.py build

  cd "$srcdir/$_pkgname-$pkgver"

  python setup.py build
}

package_python2-facebook-sdk() {
  depends=('python2' 'python2-requests' 'python2-tornado' 'python2-flask')

  cd "$_pkgname-$pkgver-2"

  python2 setup.py install --prefix=/usr --root="$pkgdir" --optimize=1 \
    --skip-build
}

package_python-facebook-sdk() {
  depends=('python' 'python-requests' 'python-tornado' 'python-flask')

  cd "$_pkgname-$pkgver"

  python setup.py install --prefix=/usr --root="$pkgdir" --optimize=1 \
    --skip-build
}

