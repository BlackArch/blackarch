# This file is part of BlackArch Linux ( http://blackarch.org ).
# See COPYING for license details.

pkgname=netsniff-ng
pkgver=0.6.5
pkgrel=1
groups=('blackarch' 'blackarch-sniffer')
pkgdesc='A high performance Linux network sniffer for packet inspection.'
arch=('x86_64' 'armv6h' 'armv7h' 'aarch64')
url='http://netsniff-ng.org/'
license=('GPL2')
depends=('libpcap' 'ncurses' 'geoip' 'libsodium' 'libnetfilter_conntrack'
        'liburcu' 'libnl' 'libcli' 'libnet')
makedepends=('cmake' 'bison' 'flex')
source=("http://pub.netsniff-ng.org/netsniff-ng/netsniff-ng-$pkgver.tar.gz")
sha512sums=('75c5cb2ecaac892d0e394d5dc364cd866c02427d50ead4669e58b719dabf1e4fc4237b190e8e8661329cbd580976b572d534286f484d1d00140dc2bf623cdc92')

build() {
  cd "$pkgname-$pkgver"

  ./configure --prefix=/usr

  make PREFIX="/usr"
}

package() {
  cd "$pkgname-$pkgver"

  install -dm 755 "$pkgdir/usr/share/doc/$pkgname"

  install -Dm 644 -t "$pkgdir/usr/share/doc/$pkgname" README INSTALL AUTHORS \
    REPORTING-BUGS

  make PREFIX="/usr" DESTDIR="$pkgdir" install

  mv "$pkgdir/usr/sbin" "$pkgdir/usr/bin"
}

