# This file is part of BlackArch Linux ( http://blackarch.org ).
# See COPYING for license details.

pkgname=netsniff-ng
pkgver=0.6.4
pkgrel=1
groups=('blackarch' 'blackarch-sniffer')
pkgdesc='A high performance Linux network sniffer for packet inspection.'
arch=('x86_64' 'armv6h' 'armv7h' 'aarch64')
url='http://netsniff-ng.org/'
license=('GPL2')
depends=('libpcap' 'ncurses' 'geoip' 'libsodium' 'libnetfilter_conntrack'
        'liburcu' 'libnl' 'libcli' 'libnet')
makedepends=('cmake' 'bison' 'flex')
source=("http://pub.netsniff-ng.org/netsniff-ng/netsniff-ng-${pkgver}.tar.gz")
sha512sums=('3a92413f8abf00ed4b55def0bc0d66b8cda3881bce820f4957d1578ca1d4bf63e4f0e4d807e7cdda5ec3318cb18ed102af0643e32de76c3e52ce78b1f7b4e439')

build() {
  cd "$srcdir/netsniff-ng-$pkgver"

  ./configure --prefix=/usr

  make PREFIX="/usr"
}

package() {
  cd "$srcdir/netsniff-ng-$pkgver"

  install -dm 755 "$pkgdir/usr/share/doc/netsniff-ng"

  install -Dm644 -t "$pkgdir/usr/share/doc/netsniff-ng" README INSTALL \
    AUTHORS REPORTING-BUGS

  make PREFIX="/usr" DESTDIR="$pkgdir" install

  mv "$pkgdir/usr/sbin" "$pkgdir/usr/bin"
}
