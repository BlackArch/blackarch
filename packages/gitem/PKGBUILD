# This file is part of BlackArch Linux ( https://www.blackarch.org/ ).
# See COPYING for license details.

pkgname=gitem
pkgver=96.20e8763
pkgrel=1
groups=('blackarch' 'blackarch-recon')
pkgdesc='A Github organization reconnaissance tool.'
arch=('any')
url='https://github.com/mschwager/gitem'
depends=('python' 'python-requests')
makedepends=('git' 'python-setuptools')
license=('GPL3')
source=("git+https://github.com/mschwager/$pkgname.git")
sha512sums=('SKIP')

pkgver() {
  cd $pkgname

  echo $(git rev-list --count HEAD).$(git rev-parse --short HEAD)
}

build() {
  cd $pkgname

  python setup.py build
}

package() {
  cd $pkgname

  python setup.py install --root="$pkgdir" --skip-build

  install -Dm 644 -t "$pkgdir/usr/share/$pkgname/" README.md
  install -Dm 644 LICENSE "$pkgdir/usr/share/$pkgname/LICENSE"
}

