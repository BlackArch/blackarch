# This file is part of BlackArch Linux ( https://www.blackarch.org/ ).
# See COPYING for license details.

pkgname=bro
pkgver=2.6.3
pkgrel=1
groups=('blackarch' 'blackarch-networking' 'blackarch-defensive'
        'blackarch-sniffer')
pkgdesc='A powerful network analysis framework that is much different from the typical IDS you may know.'
arch=('x86_64' 'aarch64')
url='https://www.bro.org/download/index.html'
license=('custom:unknown')
depends=('libpcap' 'openssl-1.0' 'bash' 'python' 'swig' 'ruby' 'perl' 'crypto++')
makedepends=('cmake' 'flex' 'bison' 'swig' 'zlib')
source=("https://www.bro.org/downloads/$pkgname-$pkgver.tar.gz")
sha512sums=('03291905de01db7f60bf204391640a0e76830a8931bb6997083d944ea573882c519be91dbf3fbf7a1ccd32b7642028406528fc284d71c0f1142875df527ddeec')

build() {
  install -dm 755 "$srcdir/bro-$pkgver/build"

  cd "$pkgname-$pkgver/build"

  cmake .. -DOPENSSL_CRYPTO_LIBRARY=/usr/lib/openssl-1.0/libcrypto.so \
           -DOPENSSL_INCLUDE_DIR=/usr/include/openssl-1.0 \
           -DOPENSSL_ROOT_DIR=/usr/lib/openssl-1.0 \
           -DOPENSSL_SSL_LIBRARY=/usr/lib/openssl-1.0/libssl.so \
           -DCMAKE_INSTALL_PREFIX=/usr
  make
}

package() {
  cd "$pkgname-$pkgver/build"

  make DESTDIR="$pkgdir" install

  cd "$srcdir/bro-$pkgver"

  install -Dm 644 -t "$pkgdir/usr/share/doc/$pkgname" README NEWS VERSION
  install -Dm 644 "man/$pkgname.8" "$pkgdir/usr/share/man/man8/$pkgname.8"
  install -Dm 644 COPYING "$pkgdir/usr/share/licenses/$pkgname/COPYING"
}

