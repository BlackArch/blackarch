# This file is part of BlackArch Linux ( http://blackarch.org ).
# See COPYING for license details.

pkgname='bro'
pkgver='2.5.1'
pkgrel=1
groups=('blackarch' 'blackarch-networking' 'blackarch-defensive'
        'blackarch-sniffer')
pkgdesc='A powerful network analysis framework that is much different from the typical IDS you may know.'
arch=('i686' 'x86_64' 'armv6h' 'armv7h' 'aarch64')
url='https://www.bro.org/download/index.html'
license=('custom:unknown')
depends=('libpcap' 'openssl-1.0' 'bash' 'python' 'swig' 'ruby' 'perl' 'crypto++')
makedepends=('cmake' 'flex' 'bison' 'swig' 'zlib')
source=("https://www.bro.org/downloads/bro-${pkgver}.tar.gz")
sha1sums=('9c133dd3a075be1084f9bf53d79c42ddcf23633c')

build() {
  mkdir -p "$srcdir/bro-${pkgver}/build"
  cd "$srcdir/bro-${pkgver}/build"
  cmake .. -DOPENSSL_CRYPTO_LIBRARY=/usr/lib/openssl-1.0/libcrypto.so \
           -DOPENSSL_INCLUDE_DIR=/usr/include/openssl-1.0 \
           -DOPENSSL_ROOT_DIR=/usr/lib/openssl-1.0 \
           -DOPENSSL_SSL_LIBRARY=/usr/lib/openssl-1.0/libssl.so \
           -DCMAKE_INSTALL_PREFIX=/usr
  make
}

package() {
  cd "$srcdir/bro-${pkgver}/build"
  make DESTDIR="$pkgdir" install
}
