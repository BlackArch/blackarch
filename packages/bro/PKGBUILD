# This file is part of BlackArch Linux ( https://www.blackarch.org/ ).
# See COPYING for license details.

pkgname=bro
pkgver=2.6.1
pkgrel=1
groups=('blackarch' 'blackarch-networking' 'blackarch-defensive'
        'blackarch-sniffer')
pkgdesc='A powerful network analysis framework that is much different from the typical IDS you may know.'
arch=('x86_64' 'armv6h' 'armv7h' 'aarch64')
url='https://www.bro.org/download/index.html'
license=('custom:unknown')
depends=('libpcap' 'openssl-1.0' 'bash' 'python' 'swig' 'ruby' 'perl' 'crypto++')
makedepends=('cmake' 'flex' 'bison' 'swig' 'zlib')
source=("https://www.bro.org/downloads/$pkgname-$pkgver.tar.gz")
sha512sums=('18ce8efd12226b4aea962034b68f14202b03f64d2cd0c2feab354ae84160d36fd2ec94343c0f816e25aa7903e7670cf9f69cac48db75f645ee641d58af2b2f28')

build() {
  install -dm 755 "$srcdir/bro-$pkgver/build"

  cd "$pkgname-$pkgver/build"

  cmake .. -DOPENSSL_CRYPTO_LIBRARY=/usr/lib/openssl-1.0/libcrypto.so \
           -DOPENSSL_INCLUDE_DIR=/usr/include/openssl-1.0 \
           -DOPENSSL_ROOT_DIR=/usr/lib/openssl-1.0 \
           -DOPENSSL_SSL_LIBRARY=/usr/lib/openssl-1.0/libssl.so \
           -DCMAKE_INSTALL_PREFIX=/usr
  make
}

package() {
  cd "$pkgname-$pkgver/build"

  make DESTDIR="$pkgdir" install

  cd "$srcdir/bro-$pkgver"

  install -Dm 644 -t "$pkgdir/usr/share/doc/$pkgname" README NEWS VERSION
  install -Dm 644 man/bro.8 "$pkgdir/usr/share/man/man8/bro.8"
  install -Dm 644 COPYING "$pkgdir/usr/share/licenses/$pkgname/COPYING"
}

