# This file is part of BlackArch Linux ( http://blackarch.org ).
# See COPYING for license details.

pkgname='bro'
pkgver='2.5.1'
pkgrel=1
groups=('blackarch' 'blackarch-networking' 'blackarch-defensive'
        'blackarch-sniffer')
pkgdesc='A powerful network analysis framework that is much different from the typical IDS you may know.'
arch=('i686' 'x86_64' 'armv6h' 'armv7h' 'aarch64')
url='https://www.bro.org/download/index.html'
license=('custom:unknown')
depends=('libpcap' 'openssl' 'bash' 'python' 'swig' 'ruby' 'perl' 'crypto++')
makedepends=('cmake' 'flex' 'bison' 'swig' 'zlib')
source=("https://www.bro.org/downloads/beta/bro-${pkgver}-beta.tar.gz")
sha1sums=('b1ce31948263f504ff9d97275bfab92643341034')

build() {
  cd "$srcdir/bro-$pkgver-beta"

  # tmp solution. needs to be fixed for 'broccoli' and 'broctl'
  ./configure --prefix=/usr --with-openssl=/usr/lib \
    --disable-broccoli --disable-broctl

  make
}

package() {
  cd "$srcdir/bro-$pkgver-beta"

  make DESTDIR="$pkgdir" install

  mv "$pkgdir/usr/bin/bro" "$pkgdir/usr/bin/bro-sniffer"
}
