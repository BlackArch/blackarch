# This file is part of BlackArch Linux ( http://blackarch.org ).
# See COPYING for license details.

pkgname=wsfuzzer
pkgver=1.9.5
pkgrel=8
groups=('blackarch' 'blackarch-fuzzer' 'blackarch-webapp')
pkgdesc='A Python tool written to automate SOAP pentesting of web services.'
url='https://www.owasp.org/index.php/Category:OWASP_WSFuzzer_Project'
arch=('any')
license=('LGPL')
depends=('python2' 'python2-pyxml' 'java-environment')
source=("http://downloads.sourceforge.net/project/wsfuzzer/wsfuzzer/version1.9.5/wsfuzzer-${pkgver}.tar.gz")
sha512sums=('1e7551293a0421ff982473f1940e6b4c6c73aeb427336509ed3fb06200fdacafc90c452ee00d3e30c9c21e484dcaf7c849a045c9162765b604440f576daa0075')

prepare() {
  cd "$srcdir"

  sed -i '1i\#!/usr/bin/env python2' *.py
}

package() {
  cd "$srcdir"

  install -dm 755 "$pkgdir/usr/bin"
  install -dm 755 "$pkgdir/usr/share/wsfuzzer"

  cp -La --no-preserve=ownership * "$pkgdir/usr/share/wsfuzzer"

  find "$pkgdir" -name .svn -exec rm -rf {} +

  ln -s /usr/share/wsfuzzer/WSFuzzer.py "$pkgdir/usr/bin/wsfuzzer"
}
