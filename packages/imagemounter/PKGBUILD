# This file is part of BlackArch Linux ( http://blackarch.org ).
# See COPYING for license details.

pkgname='imagemounter'
pkgver=352.27538e6
pkgrel=1
pkgdesc='Command line utility and Python package to ease the (un)mounting of forensic disk images.'
groups=('blackarch' 'blackarch-forensic' 'blackarch-misc')
arch=('any')
url='https://github.com/ralphje/imagemounter'
license=('MIT')
depends=('python' 'python-termcolor' 'python-argparse')
makedepends=('git' 'python-setuptools')
source=('git+https://github.com/ralphje/imagemounter.git')
sha1sums=('SKIP')

pkgver() {
  cd "$srcdir/imagemounter"

  echo $(git rev-list --count HEAD).$(git rev-parse --short HEAD)
}

build() {
  cd "$srcdir/imagemounter"

  python setup.py build
}

package() {
  cd "$srcdir/imagemounter"

  python setup.py install --root="$pkgdir" --prefix=/usr --optimize=1

  install -Dm644 -t "$pkgdir/usr/share/doc/imagemounter/" README.rst docs/*
  install -Dm644 LICENSE "$pkgdir/usr/share/licenses/imagemounter/LICENSE"
}
