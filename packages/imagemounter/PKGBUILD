# This file is part of BlackArch Linux ( https://www.blackarch.org/ ).
# See COPYING for license details.

pkgname=imagemounter
pkgver=413.383b30b
pkgrel=8
pkgdesc='Command line utility and Python package to ease the (un)mounting of forensic disk images.'
groups=('blackarch' 'blackarch-forensic' 'blackarch-misc')
arch=('any')
url='https://github.com/ralphje/imagemounter'
license=('MIT')
depends=('python' 'python-termcolor')
makedepends=('git' 'python-setuptools')
source=("git+https://github.com/ralphje/$pkgname.git")
sha512sums=('SKIP')

pkgver() {
  cd $pkgname

  ( set -o pipefail
    git describe --long --tags --abbrev=7 2>/dev/null |
      sed 's/\([^-]*-g\)/r\1/;s/-/./g' ||
    printf "%s.%s" "$(git rev-list --count HEAD)" \
      "$(git rev-parse --short=7 HEAD)"
  )
}

build() {
  cd $pkgname

  python setup.py build
}

package() {
  cd $pkgname

  python setup.py install --root="$pkgdir" --prefix=/usr -O1 --skip-build

  install -Dm 644 -t "$pkgdir/usr/share/doc/$pkgname/" README.rst docs/*
  install -Dm 644 LICENSE "$pkgdir/usr/share/licenses/$pkgname/LICENSE"
}

