# This file is part of BlackArch Linux ( https://www.blackarch.org/ ).
# See COPYING for license details.

pkgname=python2-cheroot
_pkgname=cheroot
pkgdesc='Highly-optimized, pure-python HTTP server.'
pkgver=8.5.2
pkgrel=6
arch=('any')
url='https://pypi.org/project/cheroot/#files'
license=('MIT')
depends=('python' 'python2-six' 'python2-jaraco')
makedepends=('git' 'python2-six' 'python2-setuptools-scm' 'python2-tox'
             'python2-jaraco')
source=("https://files.pythonhosted.org/packages/0e/77/0f823e39f78d97706b11cefc4b95829a2ca237a3021a37a6b7ec361b2113/$_pkgname-$pkgver.tar.gz"
        'LICENSE')
sha512sums=('245c4157f5e11741d94375099878ef5b31945412a2b02961502e762b9770fd968f4f977fe2d74744aa6596c668079d7f07b6f129fa11438783a95d3a5ee088fa'
            '1106afed483b7258e4ae89c5d9459c3834412b31aac90169725ed62d2ab44f61f6f79e894d4c9e4d8bd99e14530ab778df2187784f0b25eaab86d312fad68944')

prepare() {
  cd "$_pkgname-$pkgver"

  sed -i '/setuptools_scm_git_archive/d' setup.cfg
}

build() {
  cd "$_pkgname-$pkgver"

  export SETUPTOOLS_SCM_PRETEND_VERSION="$pkgver"
  python2 setup.py build
}

package() {
  cd "$_pkgname-$pkgver"

  python2 setup.py install --root="$pkgdir" --prefix=/usr -O1 --skip-build

  install -Dm 644 "$srcdir/LICENSE" \
    "$pkgdir/usr/share/licenses/$pkgname/LICENSE"

  mv "$pkgdir/usr/bin/$_pkgname" "$pkgdir/usr/bin/${_pkgname}2"
}

