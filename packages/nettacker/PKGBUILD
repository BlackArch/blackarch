# This file is part of BlackArch Linux ( https://www.blackarch.org/ ).
# See COPYING for license details.

pkgname=nettacker
_pkgname=Nettacker
pkgver=0.0.2.r0.g92239ce
pkgrel=1
pkgdesc='automate information gathering, vulnerability scanning and eventually generating a report for networks, including services, bugs, vulnerabilities, misconfigurations, and other information.'
arch=('any')
url='https://github.com/OWASP/Nettacker'
license=('Apache')
depends=('flake8' 'python-backoff' 'python-argparse' 'python-netaddr' 'python-ipaddr' 'python-requests' 'python-paramiko' 'python-texttable' 'python-pysocks' 'python-win_inet_pton' 'python-pyopenssl' 'python-flask' 'python-lockfile' 'python-bs4' 'python-pyspf' 'python-sqlalchemy' 'canari' 'python-py3dns' 'python-censys' 'python-six' 'python-shodan' 'python-pycurl' 'python-xmltodict' 'python-rule' 'python-mechanize' 'scapy' 'python-psycopg2')
makedepends=('python-setuptools' 'git')
source=("git+https://github.com/OWASP/$_pkgname.git")
sha512sums=('SKIP')

prepare() {
  cd $_pkgname

  sed -i '9d;10d' setup.py
}

pkgver() {
  cd $_pkgname

  git describe --long --tags | sed 's/\([^-]*-g\)/r\1/;s/-/./g'
}

build() {
  cd $_pkgname

  python setup.py build
}

package() {
  cd $_pkgname

  python setup.py install --root="$pkgdir" --prefix=/usr -O1 --skip-build

  install -Dm 644 LICENSE "$pkgdir/usr/share/licenses/$pkgname/LICENSE"
}

