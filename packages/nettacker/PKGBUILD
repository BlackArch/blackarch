# This file is part of BlackArch Linux ( https://www.blackarch.org/ ).
# See COPYING for license details.

pkgname=nettacker
pkgver=0.4.0.r163.gb759d79
pkgrel=1
pkgdesc='Automated Penetration Testing Framework.'
arch=('any')
groups=('blackarch' 'blackarch-automation' 'blackarch-scanner'
        'blackarch-recon')
url='https://github.com/OWASP/Nettacker'
license=('Apache-2.0')
depends=('flake8' 'python-backoff' 'python-netaddr' 'python-ipaddr'
         'python-requests' 'python-paramiko' 'python-texttable' 'python-pysocks'
         'python-win_inet_pton' 'python-pyopenssl' 'python-flask'
         'python-lockfile' 'python-bs4' 'python-pyspf' 'python-sqlalchemy'
         'canari' 'python-py3dns' 'python-censys' 'python-six' 'python-shodan'
         'python-pycurl' 'python-xmltodict' 'scapy' 'python-rule'
         'python-mechanize' 'python-psycopg2' 'python-numpy'
         'python-multiprocess' 'python-terminable-thread' 'python-aiohttp')
makedepends=('git' 'python-build' 'python-installer' 'python-setuptools'
             'python-wheel' 'python-poetry')
source=("git+https://github.com/OWASP/$pkgname.git")
sha512sums=('SKIP')

pkgver() {
  cd $pkgname

  ( set -o pipefail
    git describe --long --tags --abbrev=7 2>/dev/null |
      sed 's/\([^-]*-g\)/r\1/;s/-/./g' ||
    printf "%s.%s" "$(git rev-list --count HEAD)" \
      "$(git rev-parse --short=7 HEAD)"
  )
}

build() {
  cd $pkgname

  python -m build --wheel --no-isolation
}

package() {
  cd $pkgname

  python -m installer --destdir="$pkgdir" dist/*.whl
}

