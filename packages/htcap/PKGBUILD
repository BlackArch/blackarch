# This file is part of BlackArch Linux ( https://www.blackarch.org/ ).
# See COPYING for license details.

pkgname=htcap
pkgver=93.c1ae0ea
pkgrel=1
epoch=1
groups=('blackarch' 'blackarch-webapp' 'blackarch-scanner')
pkgdesc='A web application analysis tool for detecting communications between javascript and the server.'
arch=('any')
url='https://github.com/segment-srl/htcap'
license=('GPL3')
depends=('python2' 'python2-simplejson' 'phantomjs')
makedepends=('git')
source=('git+https://github.com/segment-srl/htcap.git')
sha512sums=('SKIP')

pkgver() {
  cd $pkgname

  echo $(git rev-list --count HEAD).$(git rev-parse --short HEAD)
}

package() {
  cd $pkgname

  install -dm 755 "$pkgdir/usr/bin"
  install -dm 755 "$pkgdir/usr/share/$pkgname"

  install -Dm 644 README.md "$pkgdir/usr/share/doc/$pkgname/README.md"

  rm README.md

  cp -a * "$pkgdir/usr/share/$pkgname/"

  cat > "$pkgdir/usr/bin/$pkgname" << EOF
#!/bin/sh
cd /usr/share/$pkgname
exec python2 htcap.py "\$@"
EOF

  chmod +x "$pkgdir/usr/bin/$pkgname"
}

