# This file is part of BlackArch Linux ( https://www.blackarch.org/ ).
# See COPYING for license details.

pkgname=waybackpack
pkgver=114.c5507a0
pkgrel=1
pkgdesc='Download the entire Wayback Machine archive for a given URL.'
groups=('blackarch' 'blackarch-webapp' 'blackarch-recon')
arch=('any')
url='https://github.com/jsvine/waybackpack'
license=('MIT')
depends=('python' 'python-requests' 'python-argparse')
makedepends=('git' 'python-setuptools')
source=('git+https://github.com/jsvine/waybackpack.git')
sha512sums=('SKIP')

pkgver() {
  cd $pkgname

  echo $(git rev-list --count HEAD).$(git rev-parse --short HEAD)
}

build() {
  cd $pkgname

  python setup.py build
}

package() {
  cd $pkgname

  python setup.py install --root="$pkgdir" --prefix=/usr --skip-build

  install -Dm 644 -t "$pkgdir/usr/share/doc/$pkgname/" README.md
  install -Dm 644 LICENSE.txt "$pkgdir/usr/share/licenses/$pkgname/LICENSE"
}

