# This file is part of BlackArch Linux ( http://blackarch.org ).
# See COPYING for license details.

pkgname='host-extract'
pkgver=8.0134ad7
pkgrel=4
epoch=1
groups=('blackarch' 'blackarch-scanner' 'blackarch-webapp')
pkgdesc='Ruby script tries to extract all IP/Host patterns in page response of a given URL and JavaScript/CSS files of that URL.'
arch=('any')
url='https://code.google.com/p/host-extract/'
license=('GPL3')
depends=('ruby' 'ruby-mechanize' 'ruby-net-http-digest_auth'
         'ruby-http-cookie' 'ruby-ntlm-http' 'ruby-webrobots')
makedepends=('git')
source=('git+https://github.com/jeffrbeyer/host-extract.git')
sha1sums=('SKIP')

pkgver() {
  cd "$srcdir/host-extract"

  echo $(git rev-list --count HEAD).$(git rev-parse --short HEAD)
}

package() {
  cd "$srcdir/host-extract"

  mkdir -p "$pkgdir/usr/bin"
  mkdir -p "$pkgdir/usr/share/host-extract"

  install -Dm644 README "$pkgdir/usr/share/doc/host-extract/README"

  rm README

  cp -a * "$pkgdir/usr/share/host-extract"

  cat > "$pkgdir/usr/bin/host-extract" << EOF
#!/bin/sh
exec ruby /usr/share/host-extract/host-extract.rb "\${@}"
EOF

  chmod a+x "$pkgdir/usr/bin/host-extract"
}
