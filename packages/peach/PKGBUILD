# This file is part of BlackArch Linux ( http://blackarch.org ).
# See COPYING for license details.

pkgname='peach'
pkgver='3.0.202'
pkgrel=4
groups=('blackarch' 'blackarch-fuzzer')
pkgdesc='A SmartFuzzer that is capable of performing both generation and mutation based fuzzing.'
arch=('i686' 'x86_64' 'armv6h' 'armv7h')
url=('http://peachfuzzer.com/')
license=('MIT')
depends=('mono' 'python2' 'giflib' 'gcc-libs')
source=("http://downloads.sourceforge.net/project/peachfuzz/Peach/${pkgver%.*}/peach-$pkgver-source.zip")
sha1sums=('df97187ec7561828dd0d828f0be7b7f499cb5dc7')

prepare() {
  cd "$srcdir/peach-$pkgver-source"

  sed -i "s|'win32', 'linux', 'darwin'|'linux'|" \
    "$srcdir/peach-$pkgver-source/build/config/doc.py"

  if [ "$CARCH" == i686 ]; then
    sed -i "s|'x86', 'x86_64'|'x86'|" \
      "$srcdir/peach-$pkgver-source/build/config/linux.py"
  else
    sed -i "s|'x86', 'x86_64'|'x86_64'|" \
      "$srcdir/peach-$pkgver-source/build/config/linux.py"
  fi
}

package() {
  cd "$srcdir/peach-$pkgver-source"

  python2 ./waf configure
  python2 ./waf build --variant=release
  python2 ./waf install --variant=release --destdir="$pkgdir/usr/share/peach"

  mkdir -p "$pkgdir/usr/bin"
  mkdir -p "$pkgdir/usr/share/peach"

  if [ "$CARCH" == i686 ]; then
    mv "$pkgdir/usr/share/peach/output/linux_x86_release/bin"/* \
      "$pkgdir/usr/share/peach/"
  else
    mv "$pkgdir/usr/share/peach/output/linux_x86_64_release/bin"/* \
      "$pkgdir/usr/share/peach/"
  fi

  rm -rf "$pkgdir/usr/share/peach/output"

cat > "$pkgdir/usr/bin/peach" << EOF
#!/bin/sh
  exec mono /usr/share/peach/peach.exe "\$@"
EOF

  chmod a+x "$pkgdir/usr/bin/peach"
}
