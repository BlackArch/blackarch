pkgname=peach
pkgver=3.0.202
pkgrel=1
groups=('blackarch-analysis' 'blackarch')
pkgdesc="A SmartFuzzer that is capable of performing both generation and mutation based fuzzing"
arch=('i686' 'x86_64')
url=('http://peachfuzzer.com/')
license=('MIT')
depends=('mono' 'python2' 'giflib' 'gcc-libs')
source=("http://downloads.sourceforge.net/project/peachfuzz/Peach/${pkgver%.*}/peach-$pkgver-source.zip")
md5sums=('dfe208dca3ca777968f16e49f2e7e2f5')

prepare() {
  cd "$srcdir/peach-$pkgver-source"

  sed -i "s|'win32', 'linux', 'darwin'|'linux'|" "$srcdir/peach-$pkgver-source/build/config/doc.py"

  if [ "$CARCH" == i686 ]; then
    sed -i "s|'x86', 'x86_64'|'x86'|" "$srcdir/peach-$pkgver-source/build/config/linux.py"
  else
    sed -i "s|'x86', 'x86_64'|'x86_64'|" "$srcdir/peach-$pkgver-source/build/config/linux.py"
  fi
}

package() {
  cd "$srcdir/peach-$pkgver-source"
  python2 ./waf configure
  python2 ./waf build --variant=release
  python2 ./waf install --variant=release --destdir="$pkgdir/usr/share/peach"

  # Base directories.
  install -dm755 "$pkgdir/usr/bin"
  install -dm755 "$pkgdir/usr/share/peach"

  if [ "$CARCH" == i686 ]; then
    mv "$pkgdir/usr/share/peach/output/linux_x86_release/bin"/* "$pkgdir/usr/share/peach/"
  else
    mv "$pkgdir/usr/share/peach/output/linux_x86_64_release/bin"/* "$pkgdir/usr/share/peach/"
  fi

  rm -rf "$pkgdir/usr/share/peach/output"

  ln -s /usr/share/peach/peach "$pkgdir/usr/bin/peach"
}
