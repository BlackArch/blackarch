# This file is part of BlackArch Linux ( http://blackarch.org ).
# See COPYING for license details.

pkgname='maligno'
pkgver='2.5'
pkgrel=3
groups=('blackarch' 'blackarch-scanner' 'blackarch-fuzzer' 'blackarch-exploitation')
pkgdesc='An open source penetration testing tool written in python, that serves Metasploit payloads. It generates shellcode with msfvenom and transmits it over HTTP or HTTPS.'
arch=('any')
url='http://www.encripto.no/tools/'
license=('FreeBSD')
depends=('python2' 'python2-lxml' 'python2-crypto' 'metasploit'
         'python2-ipcalc')
source=("http://www.encripto.no/tools/maligno-${pkgver}.tar.gz")
sha1sums=('1ea66fe6ce91599cfaa891644158a77b1c0f9058')

prepare() {
  cd "$srcdir/maligno-$pkgver"

  sed -i 's|/usr/bin/python|/usr/bin/python2|g' maligno_srv.py
  sed -i 's|/usr/bin/python|/usr/bin/python2|g' clientgen.py
}

package() {
  cd "$srcdir/maligno-$pkgver"

  local tools="maligno_srv clientgen"

  mkdir -p "$pkgdir/usr/bin"
  mkdir -p "$pkgdir/usr/share/maligno"

  install -Dm644 -t "$pkgdir/usr/share/doc/maligno/" docs/*

  rm -rf docs

  cp -a * "$pkgdir/usr/share/maligno"

  cat > "$pkgdir/usr/bin/maligno-srv" << EOF
#!/bin/sh
cd /usr/share/maligno
exec python2 maligno_srv.py "\$@"
EOF

  chmod +x "$pkgdir/usr/bin/maligno-srv"

  cat > "$pkgdir/usr/bin/maligno-clientgen" << EOF
#!/bin/sh
cd /usr/share/maligno
exec python2 clientgen.py "\$@"
EOF

  chmod +x "$pkgdir/usr/bin/maligno-clientgen"
}
