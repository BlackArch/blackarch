# This file is part of BlackArch Linux ( http://blackarch.org ).
# See COPYING for license details.

pkgname='jadx'
pkgver='0.6.0'
pkgrel=1
pkgdesc='Dex to Java command line and GUI decompiler tool that produces Java source code from Android Dex and APK files.'
groups=('blackarch' 'blackarch-decompiler' 'blackarch-reversing')
arch=('any')
url='https://github.com/skylot/jadx'
license=('Apache')
depends=('java-environment' 'bash')
source=("https://github.com/skylot/jadx/releases/download/v${pkgver}/jadx-${pkgver}.zip")
sha1sums=('e05ff9005ea709059af9b6acc22fc506ca98fde7')

package() {
  cd "$srcdir"

  mkdir -p "$pkgdir/usr/bin"
  mkdir -p "$pkgdir/usr/share/jadx/lib"

  install -Dm755 bin/jadx "$pkgdir/usr/share/jadx/bin/jadx"
  install -Dm755 bin/jadx-gui "$pkgdir/usr/share/jadx/bin/jadx-gui"
  install -Dm644 lib/* "$pkgdir/usr/share/jadx/lib/"
  install -Dm644 -t "$pkgdir/usr/share/doc/jadx/" README.md NOTICE

  ln -s /usr/share/jadx/bin/jadx "$pkgdir/usr/bin/jadx"
  ln -s /usr/share/jadx/bin/jadx-gui "$pkgdir/usr/bin/jadx-gui"
}
