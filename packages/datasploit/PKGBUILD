# This file is part of BlackArch Linux ( http://blackarch.org ).
# See COPYING for license details.

pkgname='datasploit'
pkgver=358.602efc6
pkgrel=1
epoch=1
pkgdesc='Performs automated OSINT and more.'
groups=('blackarch' 'blackarch-recon' 'blackarch-scanner')
arch=('any')
url='https://github.com/upgoingstar/datasploit'
license=('GPL3')
depends=('python2' 'python2-requests' 'python2-tweepy' 'python2-dnspython'
         'python2-pymongo' 'python2-python-whois' 'python2-python-wappalyzer'
         'python2-celery' 'python2-beautifulsoup4' 'python2-django' 'shodan'
         'python2-termcolor' 'python2-ipwhois' 'python2-clearbit'
         'python2-json2html' 'python2-future')
makedepends=('git')
source=('git+https://github.com/DataSploit/datasploit')
sha1sums=('SKIP')

pkgver() {
  cd "$srcdir/datasploit"

  echo $(git rev-list --count HEAD).$(git rev-parse --short HEAD)
}

package() {
  cd "$srcdir/datasploit"

  mkdir -p "$pkgdir/usr/bin"
  mkdir -p "$pkgdir/usr/share/datasploit"

  install -Dm644 -t "$pkgdir/usr/share/doc/datasploit/" README.md docs/*
  install -Dm644 LICENSE "$pkgdir/usr/share/licenses/datasploit/LICENSE"

  rm -rf docs README.md LICENSE

  cp -a * "$pkgdir/usr/share/datasploit/"

  cat > "$pkgdir/usr/bin/datasploit" << EOF
#!/bin/sh
cd /usr/share/datasploit
exec python2 domainOsint.py "\${@}"
EOF

  chmod a+x "$pkgdir/usr/bin/datasploit"
}
