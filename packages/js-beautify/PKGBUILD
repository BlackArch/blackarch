# This file is part of BlackArch Linux ( http://blackarch.org ).
# See COPYING for license details.

pkgname='js-beautify'
pkgver='1.5.10'
pkgrel=1
groups=('blackarch' 'blackarch-reversing' 'blackarch-unpacker')
pkgdesc='This little beautifier will reformat and reindent bookmarklets, ugly JavaScript, unpack scripts packed by Dean Edward?s popular packer, as well as deobfuscate scripts processed by javascriptobfuscator.com.'
arch=('any')
url='https://github.com/einars/js-beautify'
license=('MIT')
depends=('gcc' 'nodejs')
makedepends=('npm')
source=("https://github.com/einars/js-beautify/archive/v${pkgver}.tar.gz")
sha1sums=('071cb35e447ec730f3ff25274d90b206e4bc13ef')

prepare(){
  cd "$srcdir/js-beautify-$pkgver/python/"

  sed -i 's|env python|env python2|' js-beautify
  sed -i 's|env python|env python2|' *.py

  npm install
}

package() {
  cd "$srcdir/js-beautify-$pkgver"

  mkdir -p "$pkgdir/usr/bin/"
  mkdir -p "$pkgdir/usr/share/js-beautify/"

  install -Dm644 package.json "$pkgdir/usr/share/js-beautify/package.json"
  install -Dm644 -t "$pkgdir/usr/share/doc/js-beautify" README.md \
    CONTRIBUTING.md
  install -Dm644 LICENSE "$pkgdir/usr/share/licenses/js-beautify/LICENSE"
  cp -r --no-preserve=ownership node_modules "$pkgdir/usr/share/js-beautify/"
  cp -r --no-preserve=ownership js "$pkgdir/usr/share/js-beautify/"
  cp -a python/* "$pkgdir/usr/share/js-beautify/"

  rm -rf "$pkgdir/usr/share/js-beautify/js/test"

  ln -s "/usr/share/js-beautify/js/bin/js-beautify.js" \
    "$pkgdir/usr/bin/js-beautify"
  ln -s "/usr/share/js-beautify/js/bin/css-beautify.js" \
    "$pkgdir/usr/bin/css-beautify"
  ln -s "/usr/share/js-beautify/js/bin/html-beautify.js" \
    "$pkgdir/usr/bin/html-beautify"
}
