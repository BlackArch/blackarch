# This file is part of BlackArch Linux ( https://www.blackarch.org/ ).
# See COPYING for license details.

pkgname=awsbucketdump
pkgver=82.4684670
pkgrel=8
pkgdesc='A tool to quickly enumerate AWS S3 buckets to look for loot.'
groups=('blackarch' 'blackarch-automation' 'blackarch-scanner')
arch=('any')
url='https://github.com/jordanpotti/AWSBucketDump'
license=('MIT')
depends=('python' 'python-xmltodict' 'python-requests')
makedepends=('git')
source=("$pkgname::git+https://github.com/jordanpotti/AWSBucketDump.git")
sha512sums=('SKIP')

pkgver() {
  cd $pkgname

  ( set -o pipefail
    git describe --long --tags --abbrev=7 2>/dev/null |
      sed 's/\([^-]*-g\)/r\1/;s/-/./g' ||
    printf "%s.%s" "$(git rev-list --count HEAD)" \
      "$(git rev-parse --short=7 HEAD)"
  )
}

package() {
  cd $pkgname

  install -dm 755 "$pkgdir/usr/share/wordlists/$pkgname"

  install -Dm 755 AWSBucketDump.py "$pkgdir/usr/bin/$pkgname"
  install -Dm 644 -t "$pkgdir/usr/share/wordlists/$pkgname/" BucketNames.txt \
    interesting_Keywords.txt
}

