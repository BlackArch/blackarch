# This file is part of BlackArch Linux ( http://blackarch.org ).
# See COPYING for license details.

pkgname='doona'
pkgver=142.710584f
pkgrel=1
groups=('blackarch' 'blackarch-fuzzer' 'blackarch-exploitation')
pkgdesc='A fork of the Bruteforce Exploit Detector Tool (BED).'
arch=('any')
url='https://github.com/wireghoul/doona'
license=('GPL2')
depends=('perl')
makedepends=('git')
source=('git+https://github.com/wireghoul/doona.git')
sha1sums=('SKIP')

pkgver() {
  cd "$srcdir/doona"

  echo $(git rev-list --count HEAD).$(git rev-parse --short HEAD)
}

package() {
  cd "$srcdir/doona"

  mkdir -p "$pkgdir/usr/bin"
  mkdir -p "$pkgdir/usr/share/doona"

  install -Dm644 README "$pkgdir/usr/share/doc/doona/README"
  install -m644 Docs/CHANGES "$pkgdir/usr/share/doc/doona/CHANGES"
  install -m644 Docs/FAQ "$pkgdir/usr/share/doc/doona/FAQ"
  install -m644 Docs/HOWTO "$pkgdir/usr/share/doc/doona/HOWTO"
  install -m644 Docs/TODO "$pkgdir/usr/share/doc/doona/TODO"

  cd Docs
  rm ../README CHANGES FAQ HOWTO TODO
  cd ..

  cp -a * "$pkgdir/usr/share/doona"

  cat > "$pkgdir/usr/bin/doona" << EOF
#!/bin/sh
cd /usr/share/doona
exec perl doona.pl "\$@"
EOF

  chmod +x "$pkgdir/usr/bin/doona"
}
