pkgname=wifite
pkgver=2
pkgrel=3
groups=('blackarch' 'blackarch-wireless' 'blackarch-cracker')
pkgdesc="A tool to attack multiple WEP and WPA encrypted networks at the same time"
arch=('any')
url='http://code.google.com/p/wifite/'
license=('GPL')
depends=(python2 aircrack-ng reaver)
optdepends=('wireshark-cli: Handshake observation'
            'pyrit: WPA/WPA2-PSK attacking with gpu and cluster'
            'cowpatty: Handshake cracking')
source=("http://wifite.googlecode.com/svn/trunk/wifite.py")
md5sums=('732a9bc37d7eb95844dedd895efdc5e1')

prepare() {
  cd "$srcdir"
  sed -i "s#/usr/bin/python#/usr/bin/python2#g" "wifite.py"
}

package() {
  cd "$srcdir"
  install -Dm755 "wifite.py" "$pkgdir/usr/bin/wifite"
}
