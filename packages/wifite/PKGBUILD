# This file is part of BlackArch Linux ( http://blackarch.org ).
# See COPYING for license details.

pkgname='wifite'
pkgver=54.293c05a
pkgrel=1
epoch=1
groups=('blackarch' 'blackarch-cracker' 'blackarch-wireless')
pkgdesc='A tool to attack multiple WEP and WPA encrypted networks at the same time.'
arch=('any')
url='http://code.google.com/p/wifite/'
license=('GPL')
depends=('python2' 'aircrack-ng' 'reaver' 'wireshark-cli' 'pyrit' 'cowpatty')
makedepends=('git')
source=('git+https://github.com/derv82/wifite2.git')
sha1sums=('SKIP')

pkgver() {
  cd "$srcdir/wifite2"

  echo $(git rev-list --count HEAD).$(git rev-parse --short HEAD)
}

package() {
  cd "$srcdir/wifite2"

  mkdir -p "$pkgdir/usr/bin"
  mkdir -p "$pkgdir/usr/share/wifite"

  cp -a * "$pkgdir/usr/share/wifite"

  cat > "$pkgdir/usr/bin/wifite" << EOF
#!/bin/sh
cd /usr/share/wifite
exec python2 Wifite.py "\${@}"
EOF

  chmod +x "$pkgdir/usr/bin/wifite"
}
