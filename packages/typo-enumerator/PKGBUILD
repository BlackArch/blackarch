# This file is part of BlackArch Linux ( https://www.blackarch.org/ ).
# See COPYING for license details.

pkgname=typo-enumerator
pkgver=81.b01084b
pkgrel=3
pkgdesc='Enumerate Typo3 version and extensions.'
groups=('blackarch' 'blackarch-webapp' 'blackarch-scanner')
arch=('any')
url='https://github.com/whoot/Typo-Enumerator'
license=('GPL2')
depends=('python3' 'python-requests' 'python-colorama' 'python-lxml')
makedepends=('git')
source=('git+https://github.com/whoot/Typo-Enumerator.git')
sha512sums=('SKIP')

pkgver() {
  cd Typo-Enumerator

  echo $(git rev-list --count HEAD).$(git rev-parse --short HEAD)
}

package() {
  cd Typo-Enumerator

  install -dm 755 "$pkgdir/usr/bin"
  install -dm 755 "$pkgdir/usr/share/$pkgname"

  install -Dm 644 -t "$pkgdir/usr/share/doc/$pkgname/" README.md doc/*
  install -Dm 644 LICENSE.txt "$pkgdir/usr/share/licenses/$pkgname/LICENSE"

  rm -rf doc README.md LICENSE.txt

  cp -a * "$pkgdir/usr/share/$pkgname/"

  cat > "$pkgdir/usr/bin/$pkgname" << EOF
#!/bin/sh
cd /usr/share/$pkgname
exec python3 typo3_enumerator.py "\$@"
EOF

  chmod +x "$pkgdir/usr/bin/$pkgname"
}

