# This file is part of BlackArch Linux ( https://www.blackarch.org/ ).
# See COPYING for license details.

pkgname=typo-enumerator
pkgver=117.7fb4fa0
pkgrel=1
pkgdesc='Enumerate Typo3 version and extensions.'
groups=('blackarch' 'blackarch-webapp' 'blackarch-scanner')
arch=('any')
url='https://github.com/whoot/Typo-Enumerator'
license=('GPL2')
depends=('python' 'python-requests' 'python-colorama' 'python-lxml'
         'python-progressbar')
makedepends=('git')
source=("$pkgname::git+https://github.com/whoot/Typo-Enumerator.git")
sha512sums=('SKIP')

pkgver() {
  cd $pkgname

  echo $(git rev-list --count HEAD).$(git rev-parse --short HEAD)
}

package() {
  cd $pkgname

  install -dm 755 "$pkgdir/usr/bin"
  install -dm 755 "$pkgdir/usr/share/$pkgname"

  install -Dm 644 -t "$pkgdir/usr/share/doc/$pkgname/" README.md doc/*
  install -Dm 644 LICENSE.txt "$pkgdir/usr/share/licenses/$pkgname/LICENSE"

  rm -rf doc README.md LICENSE.txt

  cp -a * "$pkgdir/usr/share/$pkgname/"

  cat > "$pkgdir/usr/bin/$pkgname" << EOF
#!/bin/sh
cd /usr/share/$pkgname
exec python3 typo3scan.py "\$@"
EOF

  chmod +x "$pkgdir/usr/bin/$pkgname"

  rm -rf "$pkgdir/usr/share/typo-enumerator/lib/__pycache__/"
}

