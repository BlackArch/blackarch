# This file is part of BlackArch Linux ( https://www.blackarch.org/ ).
# See COPYING for license details.

pkgname=corstest
pkgver=5.b203683
pkgrel=2
pkgdesc='A simple CORS misconfigurations checker.'
groups=('blackarch' 'blackarch-scanner' 'blackarch-webapp')
arch=('any')
license=('GPL2')
url='https://github.com/RUB-NDS/CORStest'
depends=('python' 'python-argparse')
makedepends=('git')
source=('git+https://github.com/RUB-NDS/CORStest')
sha512sums=('SKIP')

pkgver() {
  cd CORStest

  echo $(git rev-list --count HEAD).$(git rev-parse --short HEAD)
}

package() {
  cd CORStest

  install -dm 755 "$pkgdir/usr/bin"
  install -dm 755 "$pkgdir/usr/share/$pkgname"

  install -Dm644 -t "$pkgdir/usr/share/doc/$pkgname" README.md
  install -Dm644 LICENSE "$pkgdir/usr/share/licenses/$pkgname/LICENSE"

  rm README.md LICENSE

  cp -a * "$pkgdir/usr/share/$pkgname/"

  cat > "$pkgdir/usr/bin/$pkgname" << EOF
#!/bin/sh
cd /usr/share/$pkgname
exec python2 corstest.py "\$@"
EOF

  chmod +x "$pkgdir/usr/bin/$pkgname"
}

