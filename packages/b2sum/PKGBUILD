# This file is part of BlackArch Linux ( http://blackarch.org ).
# See COPYING for license details.

pkgname='b2sum'
pkgver=20140114
pkgrel=1
pkgdesc='BLAKE2 file hash sum check. Computes the BLAKE2 (BLAKE2b or -s, -bp, -sp) cryptographic hash of a given file.'
arch=('i686' 'x86_64')
groups=('blackarch' 'blackarch-crypto')
url='https://blake2.net/'
license=('custom')
makedepends=('unzip' 'gnuplot')
source=("https://blake2.net/blake2_code_${pkgver}.zip")
sha512sums=('9a906bfdc38d4a032323f9ce1dd6d59186d602a0b2a58c5b0236a7eaea8840aa2dd4d5128dc0ad1b721e58f594698424b69b295a21748efd19c2f0a042c35e59')

prepare() {
  cd "$srcdir/blake2_code_${pkgver}/b2sum"

  sed -i "s# -fopenmp##" makefile

  make clean
}

build() {
  cd "$srcdir/blake2_code_${pkgver}/b2sum"

  make all
}

package() {
  cd "$srcdir/blake2_code_${pkgver}/b2sum"

  install -Dm755 b2sum "$pkgdir/usr/bin/b2sum"
  install -Dm644 ../COPYING "$pkgdir/usr/share/licenses/b2sum/COPYING"
  install -Dm644 ../README "$pkgdir/usr/share/b2sum/README"
}
