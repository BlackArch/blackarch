# This file is part of BlackArch Linux ( http://blackarch.org ).
# See COPYING for license details.

pkgname='b2sum'
pkgver=27.1c2b66c
pkgrel=1
epoch=1
pkgdesc='BLAKE2 file hash sum check. Computes the BLAKE2 (BLAKE2b or -s, -bp, -sp) cryptographic hash of a given file.'
arch=('i686' 'x86_64')
groups=('blackarch' 'blackarch-crypto')
url='https://blake2.net/'
license=('custom')
makedepends=('git' 'unzip' 'gnuplot')
source=('git+https://github.com/BLAKE2/BLAKE2.git')
sha1sums=('SKIP')

pkgver() {
  cd "$srcdir/BLAKE2"

  echo $(git rev-list --count HEAD).$(git rev-parse --short HEAD)
}

prepare() {
  cd "$srcdir/BLAKE2/b2sum"

  sed -i "s# -fopenmp##" makefile

  make clean
}

build() {
  cd "$srcdir/BLAKE2/b2sum"

  make all
}

package() {
  cd "$srcdir/BLAKE2/b2sum"

  install -Dm755 b2sum "$pkgdir/usr/bin/b2sum"
  install -Dm644 ../README.md "$pkgdir/usr/share/b2sum/README"
  install -Dm644 ../COPYING "$pkgdir/usr/share/licenses/b2sum/COPYING"
}
