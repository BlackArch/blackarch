# This file is part of BlackArch Linux ( http://blackarch.org ).
# See COPYING for license details.

pkgname=simplyemail
pkgver=1.4.10.r7.6a42d37
pkgrel=1
epoch=1
pkgdesc='Email recon made fast and easy, with a framework to build on CyberSyndicates'
groups=('blackarch' 'blackarch-recon')
url='https://github.com/killswitch-GUI/SimplyEmail'
license=('GPL2')
arch=('any')
depends=('python2' 'python2-requests' 'python2-pdfminer' 'python2-docx2txt'
         'python2-beautifulsoup4' 'python2-xlsx2csv' 'python2-lxml'
         'python2-mechanize' 'python2-beautifulsoup3' 'python2-fake-useragent'
         'python2-magic' 'python2-configparser' 'antiword' 'odt2txt' 'wget')
makedepends=('git')
source=('git+https://github.com/killswitch-GUI/SimplyEmail.git')
sha512sums=('SKIP')

pkgver() {
  cd SimplyEmail

  git describe --long --tags |
  sed 's/^v//;s/\([^-]*-\)g/r\1/;s/-/./g;s/\.rc./rc/g;s/Version\.//g'
}

package() {
  cd SimplyEmail

  install -dm 755 "$pkgdir"/usr/bin
  install -dm 755 "$pkgdir"/usr/share/simplyemail

  install -Dm 644 README.md "$pkgdir"/usr/share/doc/simplyemail/README

  cp -a * "$pkgdir"/usr/share/simplyemail/

  cat > "$pkgdir"/usr/bin/simplyemail << EOF
#!/bin/sh
cd /usr/share/simplyemail
exec python2 SimplyEmail.py "\${@}"
EOF

  chmod 755 "$pkgdir"/usr/bin/simplyemail
}

