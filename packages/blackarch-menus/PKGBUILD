# This file is part of BlackArch Linux ( https://www.blackarch.org/ ).
# See COPYING for license details.

pkgname=blackarch-menus
pkgver=2.1d2cbdc
pkgrel=1
groups=('blackarch')
pkgdesc='BlackArch specific XDG-compliant menu'
arch=('any')
url='https://github.com/BlackArch/blackarch-menus'
license=('custom:unknown')
install='blackarch-menus.install'
depends=('xdg-utils')
source=("git+https://github.com/BlackArch/$pkgname.git")
sha512sums=('SKIP')

pkgver() {
  cd $pkgname

  echo $(git rev-list --count HEAD).$(git rev-parse --short HEAD)
}

package() {
  cd $pkgname

  install -dm 755 "$pkgdir/etc/xdg/menus/applications-merged"
  install -dm 755 "$pkgdir/usr/share/$pkgname"
  install -dm 755 "$pkgdir/usr/share/desktop-directories"
  install -dm 755 "$pkgdir/usr/share/icons/hicolor/32x32/apps"

  install -m 644 README.md "$pkgdir/usr/share/$pkgname"
  install -m 644 menus/X-BlackArch.menu "$pkgdir/etc/xdg/menus/applications-merged/"
  install -m 644 icons/*.png "$pkgdir/usr/share/icons/hicolor/32x32/apps/"
  install -m 644 desktop-directories/*.directory "$pkgdir/usr/share/desktop-directories/"
}

