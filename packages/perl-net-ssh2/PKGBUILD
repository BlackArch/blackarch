# This file is part of BlackArch Linux ( https://www.blackarch.org/ ).
# See COPYING for license details.

# taken from AUR
# Old Maintainer: Manifest0
# Old Contributor: John D Jones III AKA jnbek <jnbek1972 -_AT_- g m a i l -_Dot_- com>
# Generator  : CPANPLUS::Dist::Arch 1.32

pkgname=perl-net-ssh2
_pkgname=Net-SSH2
pkgver=0.72
pkgrel=3
pkgdesc='Support for the SSH 2 protocol via libssh2.'
arch=('x86_64' 'aarch64')
url='https://metacpan.org/release/Net-SSH2'
license=('PerlArtistic' 'GPL')
options=('!emptydirs')
depends=('perl' 'libssh2' 'openssl' 'openssl' 'zlib')
source=("https://cpan.metacpan.org/authors/id/S/SA/SALVA/$_pkgname-$pkgver.tar.gz")
sha512sums=('bd0453fa3c971111114f17f3972c0d98726020230ef4d5a21ccd102158d427b46ba3b508f95cf9119f2f207b7c2592adb06184db9cb0d7c69de854e7aa7c6851')
_distdir="$_pkgname-$pkgver"

build() {
  ( export PERL_MM_USE_DEFAULT=1 PERL5LIB=""                 \
      PERL_AUTOINSTALL=--skipdeps                            \
      PERL_MM_OPT="INSTALLDIRS=vendor DESTDIR='$pkgdir'"     \
      PERL_MB_OPT="--installdirs vendor --destdir '$pkgdir'" \
      MODULEBUILDRC=/dev/null

    cd $_distdir
    /usr/bin/perl Makefile.PL
    make
  )
}

package() {
  cd $_distdir

  make install

  find "$pkgdir" -name .packlist -o -name perllocal.pod -delete
}

