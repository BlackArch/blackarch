# This file is part of BlackArch Linux ( https://www.blackarch.org/ ).
# See COPYING for license details.

pkgbase=python-flask-bcrypt
pkgname=('python2-flask-bcrypt' 'python-flask-bcrypt')
_pkgname='Flask-Bcrypt'
pkgver=0.7.1
pkgrel=3
pkgdesc='Bcrypt hashing for Flask.'
arch=('any')
url='https://pypi.org/project/Flask-Bcrypt/'
license=('BSD')
makedepends=('python2-setuptools' 'python-setuptools')
source=("https://files.pythonhosted.org/packages/1d/c0/6d4c04d007d72b355de24e7a223978d1a95732245f9e9becbf45d3024bf8/Flask-Bcrypt-$pkgver.tar.gz")
sha512sums=('05184a6a29f0a45943c435bf025f3f9522a357e88fcf7949f72083f0696b4ca16cb2c8cdb54e7cb266068bb04b1b41b44456ae97641bda930e00d20f3a60e5f3')

prepare() {
  cp -a "$_pkgname-$pkgver"{,-2}
}

build() {
  cd "$_pkgname-$pkgver-2"

  python2 setup.py build

  cd "$srcdir/$_pkgname-$pkgver"

  python setup.py build
}

package_python2-flask-bcrypt() {
  depends=('python2' 'python2-flask' 'python2-bcrypt')

  cd "$_pkgname-$pkgver-2"

  python2 setup.py install --root="$pkgdir" --optimize=1 --skip-build

  install -Dm 644 LICENSE "$pkgdir/usr/share/licenses/$pkgname/LICENSE"
}

package_python-flask-bcrypt() {
  depends=('python' 'python-flask' 'python-bcrypt')

  cd "$_pkgname-$pkgver"

  python setup.py install --root="$pkgdir" --optimize=1 --skip-build

  install -Dm 644 LICENSE "$pkgdir/usr/share/licenses/$pkgname/LICENSE"
}

