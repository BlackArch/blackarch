pkgname=paketto
pkgver=1.10
pkgrel=3
pkgdesc='Advanced TCP/IP Toolkit.'
url='http://www.doxpara.com/paketto'
license=('GPL')
groups=('blackarch' 'blackarch-intel')
arch=('i686' 'x86_64')
depends=('glibc')
source=("http://ftp.oregonstate.edu/pub/gentoo/distfiles/paketto-$pkgver.tar.gz"
        'paketto.patch'
        'libnet.h.patch'
        'configure.patch'
        'gcc4-tomcrypt.diff')
md5sums=('7fd82af09a6493f24c8681f7bbf03898'
         '6311927674ef6e91cf8ea335188ee5ff'
         'e5f89a48b1e0e5f7455a82badb567871'
         '5f15c42c90d3cd6bc1acac4778cc0298'
         '2fe6703e7d8ad74f2fbc0e5b670b5176')

prepare() {
  cd "$srcdir/paketto-$pkgver"

  patch -p1 < "$srcdir/paketto.patch"
  patch -p1 < "$srcdir/configure.patch"
}

build() {
  cd "$srcdir/paketto-$pkgver"

  rm -rf libtomcrypt
  ./configure --prefix=/usr --mandir=/usr/share/man --sbindir=/usr/bin

  cd libtomcrypt/

  patch -p0 < "$srcdir/gcc4-tomcrypt.diff"
  make

  cd ..
  patch -p1 < "$srcdir/libnet.h.patch"

  make
}

package() {
  cd "$srcdir/paketto-$pkgver"

  make "DESTDIR=$pkgdir" install

  # Fix conflicts with extra/mono.
  mv "$pkgdir/usr/bin/lc" "$pkgdir/usr/bin/paketto-lc"
  mv "$pkgdir/usr/share/man/man1/lc.1" "$pkgdir/usr/share/man/man1/paketto-lc.1"
}
