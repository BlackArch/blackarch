# This file is part of BlackArch Linux ( https://www.blackarch.org/ ).
# See COPYING for license details.

pkgname=paketto
pkgver=3.0
pkgrel=1
pkgdesc='Advanced TCP/IP Toolkit.'
arch=('x86_64' 'aarch64')
groups=('blackarch' 'blackarch-scanner')
url='http://www.doxpara.com/paketto/'
license=('BSD-3-Clause')
depends=('glibc' 'libghthash' 'libnet' 'libnetfilter_queue' 'libpcap'
         'libtomcrypt')
source=("https://github.com/Obsidian-Covenant/$pkgname/archive/refs/tags/$pkgver.tar.gz")
sha512sums=('c8fc9a74c17d2d0599f077a4c0b270cd6ceabfeae71ba389dd2b25eda24fd51374c6a9267321109e1a931a6b88059dd70095f1499f99ca90a461c9701ba4e01e')

build() {
  cd "$pkgname-$pkgver"

  autoreconf -i

  if [[ "$CARCH" == "i686" || "$CARCH" == "x86_64" ]]; then
    ./configure --prefix=/usr --mandir=/usr/share/man --sbindir=/usr/bin
  else
    ./configure --build=arm --prefix=/usr --mandir=/usr/share/man \
      --sbindir=/usr/bin
  fi

  make
}

package() {
  cd "$pkgname-$pkgver"

  make DESTDIR="$pkgdir" install
}

