# This file is part of BlackArch Linux ( https://www.blackarch.org/ ).
# See COPYING for license details.

pkgname=rusthound
pkgver=v1.1.69.r4.g241751c
pkgrel=1
epoch=1
pkgdesc='Active Directory data collector for BloodHound legacy (v4).'
arch=('x86_64' 'aarch64')
groups=('blackarch' 'blackarch-recon' 'blackarch-windows')
url='https://github.com/OPENCYBER-FR/RustHound'
license=('MIT')
depends=()
makedepends=('git' 'cargo' 'clang' 'krb5')
source=("git+https://github.com/OPENCYBER-FR/$pkgname.git")
sha512sums=('SKIP')

pkgver() {
  cd $pkgname

  ( set -o pipefail
    git describe --long --tags --abbrev=7 2>/dev/null |
      sed 's/\([^-]*-g\)/r\1/;s/-/./g' ||
    printf "%s.%s" "$(git rev-list --count HEAD)" \
      "$(git rev-parse --short=7 HEAD)"
  )
}

build() {
  cd $pkgname

  export CFLAGS+=' -ffat-lto-objects'
  cargo build --release --locked
}

package() {
  cd $pkgname

  install -Dm 755 "target/release/$pkgname" "$pkgdir/usr/bin/$pkgname"
  install -Dm 644 README.md -t "$pkgdir/usr/share/doc/$pkgname"

  install -Dm 644 LICENSE "$pkgdir/usr/share/licenses/$pkgname/LICENSE"
}

