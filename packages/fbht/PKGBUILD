# This file is part of BlackArch Linux ( http://blackarch.org ).
# See COPYING for license details.
# Original: https://aur.archlinux.org/packages/fb/fbht-git/PKGBUILD

pkgname='fbht'
pkgver=70.d75ae93
pkgrel=2
epoch=1
pkgdesc='A Facebook Hacking Tool'
arch=('any')
url='https://github.com/chinoogawa/fbht-linux'
groups=('blackarch' 'blackarch-webapp')
license=('custom')
depends=('python2' 'python2-mechanize' 'python2-networkx' 'python2-matplotlib'
         'python2-numpy' 'python2-simplejson')
makedepends=('git')
provides=('fbht')
source=('git+https://github.com/chinoogawa/fbht.git')
sha1sums=('SKIP')

pkgver() {
  cd "$srcdir/fbht"

  echo $(git rev-list --count HEAD).$(git rev-parse --short HEAD)
}

prepare(){
  cd "$srcdir/fbht"

  grep -iRl 'python' "$srcdir/fbht" |
  xargs sed -i 's|#!.*/usr/bin/python|#!/usr/bin/python2|;
  s|#!.*/usr/bin/env python$|#!/usr/bin/env python2|'
}

package() {
  cd "$srcdir/fbht"

  mkdir -p "$pkgdir/usr/bin"
  mkdir -p "$pkgdir/usr/share/fbht"

  install -Dm644 "README.md" "$pkgdir/usr/share/doc/fbht/README.md"
  install -Dm644 License "$pkgdir/usr/share/licenses/fbht/License"

  cp -r dumps "$pkgdir/usr/share/fbht/dumps"
  cp -r logs "$pkgdir/usr/share/fbht/logs"

  chmod 666 "$pkgdir/usr/share/fbht/logs/error.log"

  install -m755 {MyParser,community,database,handlers,main,mainFunc,mainLib}.py \
    "$pkgdir/usr/share/fbht/"
  install -m644 fb_db.db "$pkgdir/usr/share/fbht/"

  cat > "$pkgdir/usr/bin/fbht" << EOF
#!/bin/sh
cd /usr/share/fbht
exec python2 main.py "\$@"
EOF

  chmod +x "$pkgdir/usr/bin/fbht"
}
