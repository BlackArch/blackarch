# This file is part of BlackArch Linux ( https://www.blackarch.org/ ).
# See COPYING for license details

pkgname=('python-django-rest-framework' 'python2-django-rest-framework')
pkgver=3.8.2
pkgrel=1
pkgdesc='Awesome web-browsable Web APIs.'
arch=('any')
url='https://github.com/tomchristie/django-rest-framework'
license=('MIT')
options=(!emptydirs)
source=("https://github.com/tomchristie/django-rest-framework/archive/$pkgver.tar.gz")
sha512sums=('603c86a4741a37a22a2be273136eb4d1eaa0a07aa24ba97b67f9f8d4e7fa254fe9ca7d1fdd44ee9237e86ef4cfe6bdbc8f48954a92f91873c9ef0761813a43d2')
makedepends=('python-setuptools' 'python2-setuptools')

package_python-django-rest-framework() {

  depends=('python-django')
  optdepends=(
    'python-pygments: Pygments support for API docs'
    'python-markdown: Markdown support for the browsable API'
    'python-django-crispy-forms: Improved HTML display for filtering'
    'python-coreapi: Schema generation support'
  )

  cd django-rest-framework-$pkgver
  install -Dm 644 LICENSE.md "$pkgdir/usr/share/licenses/$pkgname/LICENSE"
  python setup.py install --root="$pkgdir" --optimize=1
}

package_python2-django-rest-framework() {

  depends=('python2-django')
  optdepends=(
    'python2-pygments: Pygments support for API docs'
    'python2-markdown: Markdown support for the browsable API'
    'python2-django-crispy-forms: Improved HTML display for filtering'
    'python2-coreapi: Schema generation support'
  )

  cd django-rest-framework-$pkgver
  install -Dm 644 LICENSE.md "$pkgdir/usr/share/licenses/$pkgname/LICENSE"
  python2 setup.py install --root="$pkgdir" --optimize=1
}

