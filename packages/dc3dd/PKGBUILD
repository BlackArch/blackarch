# This file is part of BlackArch Linux ( http://blackarch.org ).
# See COPYING for license details.

pkgname='dc3dd'
pkgver='7.2.646'
pkgrel=1
groups=('blackarch' 'blackarch-forensic')
pkgdesc='A patched version of dd that includes a number of features useful for computer forensics.'
url='http://sourceforge.net/projects/dc3dd'
license=('GPL3')
arch=('x86_64' 'armv6h' 'armv7h' 'aarch64')
depends=('perl-locale-gettext')
source=("http://downloads.sf.net/sourceforge/dc3dd/dc3dd-${pkgver}.zip")
sha1sums=('3d3d9ba5f3b18fbf4798e1f3554f2d34d0ce5e2c')

prepare() {
  cd "$srcdir/dc3dd-$pkgver"

  chmod a+x configure
}

build() {
  cd "$srcdir/dc3dd-$pkgver"

  if [[ "$CARCH" == "i686" || "$CARCH" == "x86_64" ]];
  then
    ./configure --prefix=/usr
  else
    ./configure --build=arm --prefix=/usr
  fi

  make
}

package() {
  cd "$srcdir/dc3dd-$pkgver"

  make DESTDIR="$pkgdir" install
}
