# This file is part of BlackArch Linux ( https://www.blackarch.org/ ).
# See COPYING for license details.

pkgname=dc3dd
pkgver=7.2.646
pkgrel=1
groups=('blackarch' 'blackarch-forensic')
pkgdesc='A patched version of dd that includes a number of features useful for computer forensics.'
url='http://sourceforge.net/projects/dc3dd'
license=('GPL3')
arch=('x86_64' 'aarch64')
depends=('perl-locale-gettext')
source=("http://downloads.sf.net/sourceforge/$pkgname/$pkgname-$pkgver.zip")
sha512sums=('5e2d3cceab8e555e5b3b0363098221da4c772f6c2ca1c98545c8925412ea60360ba6d9febe9a83b92465f0e7b231b3344dfe4f73777644bbf107fab6264c66bc')

prepare() {
  cd "$pkgname-$pkgver"

  chmod +x configure
}

build() {
  cd "$pkgname-$pkgver"

  if [[ "$CARCH" == "i686" || "$CARCH" == "x86_64" ]];
  then
    ./configure --prefix=/usr
  else
    ./configure --build=arm --prefix=/usr
  fi

  make
}

package() {
  cd "$pkgname-$pkgver"

  make DESTDIR="$pkgdir" install
}

