# This file is part of BlackArch Linux ( https://www.blackarch.org/ ).
# See COPYING for license details.

pkgname=ldeep
pkgver=1.0.86.r8.g0968a2c
pkgrel=1
epoch=1
pkgdesc='In-depth ldap enumeration utility.'
groups=('blackarch' 'blackarch-recon')
arch=('any')
url='https://github.com/franc-pentest/ldeep'
license=('MIT')
depends=('python' 'python-dnspython' 'python-cryptography' 'python-ldap3'
         'python-tqdm' 'python-termcolor' 'python-commandparse'
         'python-pycryptodomex' 'python-six' 'python-gssapi' 'python-oscrypto'
         'python-pycryptodome')
makedepends=('git' 'python-build' 'python-pip')
source=("git+https://github.com/franc-pentest/$pkgname.git")
sha512sums=('SKIP')

pkgver() {
  cd $pkgname

  git describe --long --tags | sed 's/\([^-]*-g\)/r\1/;s/-/./g'
}

build() {
  cd $pkgname

  python -m build --wheel --outdir="$startdir/dist"
}

package() {
  cd $pkgname

  pip install \
    --verbose \
    --disable-pip-version-check \
    --no-warn-script-location \
    --ignore-installed \
    --no-compile \
    --no-deps \
    --root="$pkgdir" \
    --prefix=/usr \
    --no-index \
    --find-links="file://$startdir/dist" \
    $pkgname

  install -Dm 644 -t "$pkgdir/usr/share/doc/$pkgname/" README.rst VERSION
  install -Dm 644 LICENSE "$pkgdir/usr/share/licenses/$pkgname/LICENSE"
}

