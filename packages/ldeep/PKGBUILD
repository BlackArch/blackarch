# This file is part of BlackArch Linux ( https://www.blackarch.org/ ).
# See COPYING for license details.

pkgname=ldeep
pkgver=111.b413c5d
pkgrel=1
pkgdesc='In-depth ldap enumeration utility.'
groups=('blackarch' 'blackarch-recon')
arch=('any')
url='https://github.com/franc-pentest/ldeep'
license=('MIT')
depends=('python' 'python-dnspython' 'python-tqdm' 'python-termcolor'
         'python-ldap3' 'python-commandparse')
makedepends=('git' 'python-setuptools')
source=("git+https://github.com/franc-pentest/$pkgname.git")
sha512sums=('SKIP')

pkgver() {
  cd $pkgname

  echo $(git rev-list --count HEAD).$(git rev-parse --short HEAD)
}

build() {
  cd $pkgname

  python setup.py build
}

package() {
  cd $pkgname

  python setup.py install --root="$pkgdir" --prefix=/usr --skip-build

  install -Dm 644 -t "$pkgdir/usr/share/doc/$pkgname/" README.rst VERSION
  install -Dm 644 LICENSE "$pkgdir/usr/share/licenses/$pkgname/LICENSE"
}

