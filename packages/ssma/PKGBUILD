# This file is part of BlackArch Linux ( http://blackarch.org ).
# See COPYING for license details.

pkgname=ssma
pkgver=203.e8f3e94
pkgrel=4
groups=('blackarch' 'blackarch-malware')
arch=('any')
pkgdesc='Simple Static Malware Analyzer.'
url='https://github.com/secrary/SSMA'
license=('GPL3')
depends=('python' 'python-pefile' 'python-magic' 'python-yara-rednaga' 'ssdeep'
         'python-elasticsearch' 'python-gitpython' 'python-virustotal-api')
makedepends=('git')
source=('git+https://github.com/secrary/SSMA.git')
sha512sums=('SKIP')

pkgver() {
  cd SSMA

  echo $(git rev-list --count HEAD).$(git rev-parse --short HEAD)
}

prepare() {
  cd SSMA

  touch src/__init__.py
}

package() {
  cd SSMA

  install -dm 755 "$pkgdir/usr/bin"
  install -dm 755 "$pkgdir/usr/share/$pkgname"

  install -Dm644 -t "$pkgdir/usr/share/doc/$pkgnae" README.md

  rm README.md

  cp -a --no-preserve=ownership * "$pkgdir/usr/share/$pkgname"

  chmod 755 "$pkgdir/usr/share/$pkgname/ssma.py"

  cat > "${pkgdir}/usr/bin/ssma" << EOF
#!/usr/bin/env bash
cd /usr/share/ssma
exec python3 ssma.py "\${@}"
EOF

  chmod a+x "$pkgdir/usr/bin/ssma"
}

