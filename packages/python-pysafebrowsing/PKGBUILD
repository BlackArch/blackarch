# This file is part of BlackArch Linux ( https://www.blackarch.org/ ).
# See COPYING for license details.

pkgbase=python-pysafebrowsing
pkgname=('python2-pysafebrowsing' 'python-pysafebrowsing')
_pkgname=pysafebrowsing
pkgver=0.1.1
pkgrel=3
pkgdesc='Google Safe Browsing API python wrapper.'
arch=('any')
url='https://pypi.org/project/pysafebrowsing/#files'
license=('MIT')
makedepends=('python2-setuptools' 'python-setuptools')
source=("https://files.pythonhosted.org/packages/1e/52/434e92a6b74df683a866082bb34fc008ed8e234fc621904434c3ec85b525/$_pkgname-$pkgver.tar.gz")
sha512sums=('a5a44b69a74582320a4bbe433d5bb44febecc7d806436f52b8a25d344b184ed973a715f5394b4064bc53772466710bda02834008c839222db6401a11a97abeb3')

prepare() {
  cp -a "$_pkgname-$pkgver"{,-2}
}

build() {
  cd "$_pkgname-$pkgver-2"

  python2 setup.py build

  cd "$srcdir/$_pkgname-$pkgver"

  python setup.py build
}

package_python2-pysafebrowsing() {
  depends=('python2' 'python2-configparser' 'python2-requests')

  cd "$_pkgname-$pkgver-2"

  python2 setup.py install --prefix=/usr --root="$pkgdir" --optimize=1 \
    --skip-build

  mv "$pkgdir/usr/bin/safebrowsing" "$pkgdir/usr/bin/safebrowsing2"
}

package_python-pysafebrowsing() {
  depends=('python' 'python-requests')

  cd "$_pkgname-$pkgver"

  python setup.py install --prefix=/usr --root="$pkgdir" --optimize=1 \
    --skip-build
}

