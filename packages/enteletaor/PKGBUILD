# This file is part of BlackArch Linux ( https://www.blackarch.org/ ).
# See COPYING for license details.

pkgname=enteletaor
pkgver=65.d1fbda5
pkgrel=3
groups=('blackarch' 'blackarch-exploitation' 'blackarch-scanner'
        'blackarch-recon')
pkgdesc='Message Queue & Broker Injection tool that implements attacks to Redis, RabbitMQ and ZeroMQ.'
arch=('any')
url='https://github.com/cr0hn/enteletaor'
license=('BSD')
depends=('python' 'python-six' 'python-lxml' 'python-wtforms' 'python-eventlet'
         'python-colorlog' 'python-requests' 'python-netaddr' 'python-amqp'
         'python-redis' 'python-kombu' 'python-celery' 'python-pyzmq')
makedepends=('git' 'python-setuptools')
source=("git+https://github.com/cr0hn/$pkgname.git")
sha512sums=('SKIP')

pkgver() {
  cd $pkgname

  echo $(git rev-list --count HEAD).$(git rev-parse --short HEAD)
}

build() {
  cd $pkgname

  python setup.py build
}

package() {
  cd $pkgname

  python setup.py install --root="$pkgdir" --optimize=1 --skip-build

  install -Dm 644 -t "$pkgdir/usr/share/doc/$pkgname/" README.rst CHANGELOG
  install -Dm 644 LICENSE "$pkgdir/usr/share/licenses/$pkgname/LICENSE"
}

