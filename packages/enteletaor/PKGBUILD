# This file is part of BlackArch Linux ( http://blackarch.org ).
# See COPYING for license details.

pkgname='enteletaor'
pkgver=64.399d107
pkgrel=2
groups=('blackarch' 'blackarch-exploitation' 'blackarch-scanner'
        'blackarch-recon')
pkgdesc='Message Queue & Broker Injection tool that implements attacks to Redis, RabbitMQ and ZeroMQ.'
arch=('any')
url='https://github.com/cr0hn/enteletaor'
license=('BSD')
depends=('python' 'python-six' 'python-lxml' 'python-wtforms' 'python-eventlet'
         'python-colorlog' 'python-ipaddress' 'python-requests' 'python-netaddr'
         'python-amqp' 'python-redis' 'python-kombu' 'python-celery'
         'python-pyzmq')
makedepends=('git' 'python-setuptools')
source=('git+https://github.com/cr0hn/enteletaor.git')
sha1sums=('SKIP')

pkgver() {
  cd "$srcdir/enteletaor"

  echo $(git rev-list --count HEAD).$(git rev-parse --short HEAD)
}

package() {
  cd "$srcdir/enteletaor"

  python setup.py install --root="$pkgdir" --optimize=1

  install -Dm644 -t "$pkgdir/usr/share/doc/enteletaor/" README.rst CHANGELOG
  install -Dm644 LICENSE "$pkgdir/usr/share/licenses/enteletaor/LICENSE"
}
