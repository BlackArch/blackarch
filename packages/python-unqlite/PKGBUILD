# This file is part of BlackArch Linux ( https://www.blackarch.org/ ).
# See COPYING for license details.

pkgbase=python-unqlite
pkgname=('python2-unqlite' 'python-unqlite')
_pkgname=unqlite
pkgver=0.9.3
pkgrel=1
pkgdesc='Fast Python bindings for the UnQLite embedded NoSQL database.'
arch=('x86_64' 'aarch64')
url='https://pypi.org/project/unqlite/#files'
license=('MIT')
makedepends=('python2-setuptools' 'python-setuptools' 'cython2' 'cython')
source=("https://files.pythonhosted.org/packages/58/00/c6f82a9c24b53eb5aac5fa11eb210197609b8644421198eb404c202c9c43/$_pkgname-$pkgver.tar.gz")
sha512sums=('79d9ec98054a368ca5226577acf416bc4470b4166e4876cdb6f9b2ded660c76480f7e8bcea5c1708925b2d2758bb648f0e09ad5b589ee76ef7b36532f67a3806')

prepare() {
  cp -a "$_pkgname-$pkgver"{,-2}
}

build() {
  cd "$_pkgname-$pkgver-2"

  python2 setup.py build

  cd "$srcdir/$_pkgname-$pkgver"

  python setup.py build
}

package_python2-unqlite() {
  depends=('python2' 'cython2')

  cd "$_pkgname-$pkgver-2"

  python2 setup.py install --root="$pkgdir" --prefix=/usr -O1 --skip-build
}

package_python-unqlite() {
  depends=('python' 'cython')

  cd "$_pkgname-$pkgver"

  python setup.py install --root="$pkgdir" --prefix=/usr -O1 --skip-build
}

