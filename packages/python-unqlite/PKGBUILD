# This file is part of BlackArch Linux ( https://www.blackarch.org/ ).
# See COPYING for license details.

pkgbase=python-unqlite
pkgname=('python2-unqlite' 'python-unqlite')
_pkgname=unqlite
pkgver=1.0.0
pkgrel=1
pkgdesc='Fast Python bindings for the UnQLite embedded NoSQL database.'
arch=('x86_64' 'aarch64')
url='https://pypi.org/project/unqlite/#files'
license=('MIT')
makedepends=('python2-setuptools' 'python-setuptools' 'cython2' 'cython')
source=("https://files.pythonhosted.org/packages/source/${_pkgname::1}/$_pkgname/$_pkgname-$pkgver.tar.gz")
sha512sums=('b0ab487262ae11570122b96a36581713fb47871feb816c02a7cd27aada4404e022502eb053da155e1106533388774e2bd82604c5604c2a9d7b23eb512a223198')

prepare() {
  cp -a "$_pkgname-$pkgver"{,-2}
}

build() {
  cd "$_pkgname-$pkgver-2"

  python2 setup.py build

  cd "$srcdir/$_pkgname-$pkgver"

  python setup.py build
}

package_python2-unqlite() {
  depends=('python2' 'cython2')

  cd "$_pkgname-$pkgver-2"

  python2 setup.py install --root="$pkgdir" --prefix=/usr -O1 --skip-build
}

package_python-unqlite() {
  depends=('python' 'cython')

  cd "$_pkgname-$pkgver"

  python setup.py install --root="$pkgdir" --prefix=/usr -O1 --skip-build
}

