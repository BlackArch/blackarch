# This file is part of BlackArch Linux ( https://www.blackarch.org/ ).
# See COPYING for license details.

pkgbase=python-unqlite
pkgname=('python2-unqlite' 'python-unqlite')
_pkgname=unqlite
pkgver=0.7.1
pkgrel=3
pkgdesc='Fast Python bindings for the UnQLite embedded NoSQL database.'
arch=('x86_64' 'armv6h' 'armv7h' 'aarch64')
url='https://pypi.org/project/unqlite/#files'
license=('MIT')
makedepends=('python2-setuptools' 'python-setuptools' 'cython2' 'cython')
source=("https://files.pythonhosted.org/packages/ea/6b/d872a8ec53d284f88cb55f6245f8df77481863974212bc8bff4fc13eb687/$_pkgname-$pkgver.tar.gz")
sha512sums=('c97a0a91404fbd515649e2188a7a99f8f79aee82d66248c6d28ae0e589826e266c17bf65a4102a4c3d3e9fcf703667e90a5f75297d55cf330af33c057fd85342')

prepare() {
  cp -a "$_pkgname-$pkgver" "$_pkgname-$pkgver-2"
}

build() {
  cd "$_pkgname-$pkgver-2"

  python2 setup.py build

  cd "$srcdir/$_pkgname-$pkgver"

  python setup.py build
}

package_python2-unqlite() {
  depends=('python2' 'cython2')

  cd "$_pkgname-$pkgver-2"

  python2 setup.py install --prefix=/usr --root="$pkgdir" --optimize=1 \
    --skip-build
}

package_python-unqlite() {
  depends=('python' 'cython')

  cd "$_pkgname-$pkgver"

  python setup.py install --prefix=/usr --root="$pkgdir" --optimize=1 \
    --skip-build
}

