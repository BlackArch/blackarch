# This file is part of BlackArch Linux ( https://www.blackarch.org/ ).
# See COPYING for license details.

pkgname=sinfp
pkgver=1.24
pkgrel=1
pkgdesc='A full operating system stack fingerprinting suite.'
arch=('x86_64' 'armv6h' 'armv7h' 'aarch64')
url='http://www.networecon.com/tools/sinfp/'
groups=('blackarch' 'blackarch-fingerprint')
license=('PerlArtistic')
depends=('perl' 'perl-class-gomor' 'perl-data-dumper' 'perl-dbd-sqlite'
        'perl-dbi' 'perl-digest-perl-md5' 'perl-net-frame' 'perl-net-libdnet6'
        'perl-net-frame-device' 'perl-net-libdnet' 'perl-net-netmask'
        'perl-net-write' 'perl-parallel-forkmanager' 'perl-net-ipv4addr'
        'perl-net-frame-dump' 'perl-net-frame-layer-ipv6'
        'perl-net-frame-layer-icmp6' 'perl-net-frame-simple'
        'perl-net-frame-layer-sinfp3' 'perl-io-socket-inet6'
        'perl-time-interval')
source=("http://search.cpan.org/CPAN/authors/id/G/GO/GOMOR/Net-SinFP3-$pkgver.tar.gz")
sha512sums=('223936980612a19d8b8286ca49a80e6522f43fead23927cf45481cb3455e112666e5d62ce941443c4ba78efe15146fcdb27a813046d9e97b66bdf9cb68a25c0a')

prepare() {
  cd "Net-SinFP3-$pkgver"

  perl Makefile.PL
}

build() {
  cd "Net-SinFP3-$pkgver"

  make
}

package() {
  cd "Net-SinFP3-$pkgver"

  make DESTDIR="$pkgdir" install

  install -Dm 755 bin/sinfp3.pl "$pkgdir/usr/share/sinfp3/sinfp3.pl"

  ln -s /usr/share/sinfp3/sinfp3.pl "$pkgdir/usr/bin/$pkgname"

  install -D -m644 "$pkgdir/usr/bin/site_perl/sinfp3.db" \
    "$pkgdir/usr/share/sinfp3/sinfp3.db"

  rm -rf "$pkgdir/usr/bin/site_perl"
}

