# This file is part of BlackArch Linux ( https://www.blackarch.org/ ).
# See COPYING for license details.

pkgname=oledump
pkgver=0.0.55
pkgrel=1
groups=('blackarch' 'blackarch-binary' 'blackarch-malware')
pkgdesc='Analyze OLE files (Compound File Binary Format). These files contain streams of data. This tool allows you to analyze these streams.'
arch=('any')
url='http://blog.didierstevens.com/programs/oledump-py/'
license=('custom:unknown')
depends=('python' 'python-lxml' 'python-yara' 'python-olefile')
source=("https://didierstevens.com/files/software/${pkgname}_V${pkgver//./_}.zip")
sha512sums=('b1e63bcbb4aa649437aa9bb0e798cd429e371e638bc635288651f0bd9b84e5b6202dca39da1bee94b17ecf08ce70138b9add8eb7bca9315088a59091c9b42a63')

prepare() {
  find . -type f -name '*.py' -exec sed -i 's|env python|env python|' {} +
}

package() {
  install -dm 755 "$pkgdir/usr/bin"
  install -dm 755 "$pkgdir/usr/share/$pkgname"

  rm "${pkgname}_V${pkgver//./_}.zip"

  cp -a * "$pkgdir/usr/share/$pkgname/"

  cat > "$pkgdir/usr/bin/$pkgname" << EOF
#!/bin/sh
cd /usr/share/$pkgname
exec python $pkgname.py "\$@"
EOF

  chmod +x "$pkgdir/usr/bin/$pkgname"
}

