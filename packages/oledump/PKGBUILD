# This file is part of BlackArch Linux ( https://www.blackarch.org/ ).
# See COPYING for license details.

pkgname=oledump
pkgver=0.0.42
pkgrel=1
groups=('blackarch' 'blackarch-binary' 'blackarch-malware')
pkgdesc='Analyze OLE files (Compound File Binary Format). These files contain streams of data. This tool allows you to analyze these streams.'
arch=('any')
url='http://blog.didierstevens.com/programs/oledump-py/'
license=('custom:unknown')
depends=('python2' 'python2-lxml' 'python2-yara')
source=("https://didierstevens.com/files/software/oledump_V${pkgver//./_}.zip")
sha512sums=('cabf500ad07c5068e617b56171cd4fea230349d5e6114c7ca024c9df17f45702c0ac56f6217f43546ac224965f12a624b7a6288c35d9374b1201dd1b80b0fa96')

prepare() {
  find . -type f -name '*.py' -exec sed -i 's|env python|env python2|' {} +
}

package() {
  install -dm 755 "$pkgdir/usr/bin"
  install -dm 755 "$pkgdir/usr/share/$pkgname"

  rm oledump_V${pkgver//./_}.zip

  cp -a * "$pkgdir/usr/share/$pkgname/"

  cat > "$pkgdir/usr/bin/$pkgname" << EOF
#!/bin/sh
cd /usr/share/$pkgname
exec python2 oledump.py "\$@"
EOF

  chmod +x "$pkgdir/usr/bin/$pkgname"
}

