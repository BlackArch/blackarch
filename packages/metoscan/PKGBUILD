# This file is part of BlackArch Linux ( https://www.blackarch.org/ ).
# See COPYING for license details.

pkgname=metoscan
pkgver=05
pkgrel=5
pkgdesc='Tool for scanning the HTTP methods supported by a webserver.'
arch=('x86_64' 'aarch64')
groups=('blackarch' 'blackarch-webapp')
url='http://www.open-labs.org/'
license=('custom')
depends=('curl')
source=("https://web.archive.org/web/20110902174832/http://open-labs.org/$pkgname$pkgver.tar.gz")
sha512sums=('734403caf956b73e2e09a01e058a7deb8636b93f557f7af28ff317d62a49636d4c3d1aef778673ba1709c6708114e088fd546fed87129a5a347554ab3a1ceae2')

prepare() {
  cd "${pkgname}${pkgver}"

  sed -i '/CURLOPT_MUTE/d' get_url.c
  sed -i "s:gcc :gcc $CFLAGS -fcommon :" Makefile
  sed -i  "s|#include <curl/curl.h>|#include <curl/curl.h>\n#include <stdlib.h>\n#include <string.h>|g" metoscan.h
  sed -i  "s|main(|int main(|g" metoscan.c
}

build() {
  cd "${pkgname}${pkgver}"

  make
}

package() {
  cd "${pkgname}${pkgver}"

  install -Dm 755 $pkgname "$pkgdir/usr/bin/$pkgname"
  install -Dm 644 -t "$pkgdir/usr/share/doc/$pkgname/" README.txt
}

