# This file is part of BlackArch Linux ( http://blackarch.org ).
# See COPYING for license details.

pkgname=metoscan
pkgver=05
pkgrel=3
groups=('blackarch' 'blackarch-webapp')
pkgdesc='Tool for scanning the HTTP methods supported by a webserver. It works
by testing a URL and checking the responses for the different requests.'
url='http://www.open-labs.org/'
license=('custom')
depends=('curl')
makedepends=('gcc' 'make')
arch=('x86_64' 'armv6h' 'armv7h' 'aarch64')
source=("http://www.open-labs.org/metoscan$pkgver.tar.gz")
sha512sums=('ba4d823ded4597ea5f843284db91a30d182d3820458f9cd88255751fcdc0d067afa2369c09e21f1c2d101b86cd547c844a05f1121acc41502ccad0a5b7b1fc5d')

prepare() {
  cd "$srcdir/metoscan$pkgver"

  sed -i '/CURLOPT_MUTE/d' get_url.c
  sed -i "s:gcc :gcc $CFLAGS :" Makefile
}

build() {
  cd "$srcdir/metoscan$pkgver"

  make
}

package() {
  cd "$srcdir/metoscan$pkgver"

  install -Dm755 metoscan "$pkgdir/usr/bin/metoscan"
  install -Dm644 README.txt "$pkgdir/usr/share/doc/metoscan/README.txt"
}
