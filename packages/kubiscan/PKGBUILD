# This file is part of BlackArch Linux ( https://www.blackarch.org/ ).
# See COPYING for license details.

pkgname=kubiscan
_pkgname=KubiScan
pkgver=v1.5.r60.gb851b37
pkgrel=1
pkgdesc='A tool for scanning a Kubernetes cluster for risky rbac permissions.'
arch=('any')
groups=('blackarch' 'blackarch-scanner')
url='https://github.com/cyberark/KubiScan'
license=('GPL3')
depends=('python' 'python-virtualenv')
makedepends=('git' 'python-pip')
source=("git+https://github.com/cyberark/$_pkgname.git")
sha512sums=('SKIP')
install="$pkgname.install"

pkgver() {
  cd $_pkgname

  git describe --long --tags | sed 's/\([^-]*-g\)/r\1/;s/-/./g'
}

package() {
  cd $_pkgname

  install -dm 755 "$pkgdir/usr/bin"
  install -Dm 644 requirements.txt "$pkgdir/usr/share/$pkgname/requirements.txt"
  install -Dm 644 -t "$pkgdir/usr/share/doc/$pkgname/" *.md

  install -Dm 644 LICENSE "$pkgdir/usr/share/licenses/$pkgname/LICENSE"

  rm -rf LICENSE *.md

  cp -a * "$pkgdir/usr/share/$pkgname/"

  cat > "$pkgdir/usr/bin/$pkgname" << EOF
#!/bin/sh
source /usr/share/$pkgname/venv/bin/activate
exec python /usr/share/$pkgname/$_pkgname.py "\$@"
EOF

  chmod a+x "$pkgdir/usr/bin/$pkgname"
}

