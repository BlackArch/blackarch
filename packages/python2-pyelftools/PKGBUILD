# This file is part of BlackArch Linux ( http://blackarch.org ).
# See COPYING for license details.

pkgname='python2-pyelftools'
pkgver='0.23'
pkgrel=1
pkgdesc='Python library for analyzing ELF files and DWARF debugging information'
url='https://github.com/eliben/pyelftools'
arch=('any')
license=('custom:Public Domain')
depends=('python2')
source=("https://github.com/eliben/pyelftools/archive/v${pkgver}.tar.gz")
sha1sums=('729b558605e5b033010ecd60705e0d5a61bc94ea')

prepare() {
  cd "pyelftools-${pkgver}"

  mv scripts/readelf.py scripts/readelf.py2

  sed -ri 's/readelf.py/\02/g' setup.py
}

build() {
  cd "pyelftools-${pkgver}"

  python2 setup.py build
}

package() {
  cd "pyelftools-${pkgver}"

  python2 setup.py install --root="$pkgdir/" --optimize=1

  install -Dm644 LICENSE "$pkgdir/usr/share/licenses/pyelftools/LICENSE"
  install -Dm644 README.rst "$pkgdir/usr/share/doc/pyelftools/README.rst"

  cp -r examples "$pkgdir/usr/share/doc/pyelftools"
}
