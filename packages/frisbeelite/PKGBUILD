# This file is part of BlackArch Linux ( https://www.blackarch.org/ ).
# See COPYING for license details.

pkgname=frisbeelite
pkgver=1.2
pkgrel=3
groups=('blackarch' 'blackarch-fuzzer')
pkgdesc='A GUI-based USB device fuzzer.'
arch=('any')
url='https://github.com/nccgroup/FrisbeeLite'
license=('GPL3')
depends=('python2' 'libusb' 'python2-pyusb' 'wxgtk')
makedepends=('git')
source=('git+https://github.com/nccgroup/FrisbeeLite.git')
sha512sums=('SKIP')

pkgver() {
  cd FrisbeeLite

  ls FrisbeeLite_v*.py | cut -d 'v' -f 2 |  sed 's/\.py//'
}

prepare() {
  cd FrisbeeLite

  mv "FrisbeeLite_v$pkgver.py" frisbeelite.py

  sed -i '/#!\/usr\/bin\/python/d' frisbeelite.py
  sed -i '1i\#!/usr/bin/python2' frisbeelite.py
  sed -i 's/.$//g' frisbeelite.py
}

package() {
  cd FrisbeeLite

  install -dm 755 "$pkgdir/usr/bin/"
  install -dm 755 "$pkgdir/usr/share/$pkgname"

  cp -a frisbeelite.py images dependencies "$pkgdir/usr/share/$pkgname"

  install -Dm644 Fuzzing_USB_devices_with_FrisbeeLite.pdf \
    "$pkgdir/usr/share/doc/$pkgname/Fuzzing_USB_devices_with_FrisbeeLite.pdf"
  install -Dm644 README.md "$pkgdir/usr/share/doc/$pkgname/README.md"

  cat > "$pkgdir/usr/bin/$pkgname" << EOF
#!/bin/sh
cd /usr/share/$pkgname
exec python2 frisbeelite.py
EOF

  chmod +x "$pkgdir/usr/bin/$pkgname"
}

