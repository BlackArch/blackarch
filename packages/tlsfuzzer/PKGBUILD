# This file is part of BlackArch Linux ( https://www.blackarch.org/ ).
# See COPYING for license details.

pkgname=tlsfuzzer
pkgver=1247.5dcf52b
pkgrel=2
pkgdesc='SSL and TLS protocol test suite and fuzzer.'
groups=('blackarch' 'blackarch-crypto' 'blackarch-fuzzer')
arch=('any')
url='https://github.com/tomato42/tlsfuzzer'
license=('GPL2')
depends=('python' 'python-tlslite-ng' 'python-ecdsa' 'python-six')
makedepends=('git' 'python-setuptools' 'python-pyaml' 'python-requests'
             'python-coverage' 'python-coveralls' 'python-pylint')
source=("git+https://github.com/tomato42/$pkgname.git")
sha512sums=('SKIP')

pkgver() {
  cd $pkgname

  echo $(git rev-list --count HEAD).$(git rev-parse --short HEAD)
}

build() {
  cd $pkgname

  python setup.py build
}

package() {
  cd $pkgname

  python setup.py install --root="$pkgdir" --prefix=/usr -O1 --skip-build
}

