# This file is part of BlackArch Linux ( https://www.blackarch.org/ ).
# See COPYING for license details.

pkgbase=python-poyo
pkgname=('python2-poyo' 'python-poyo')
_pkgname=poyo
pkgver=0.4.2
pkgrel=1
pkgdesc='A lightweight YAML Parser for Python.'
arch=('any')
url='https://pypi.org/project/poyo/#files'
license=('MIT')
makedepends=('python2-setuptools' 'python-setuptools')
source=("https://files.pythonhosted.org/packages/e2/74/3cca4e9a3d11f4f55eb7ed44d1cb09d403b7491cb03daa037f8338a09ed1/$_pkgname-$pkgver.tar.gz")
sha512sums=('8c34fa816e7c4a919f7e2b221df2983458a0e7dc70d73f5184bab9cd6add42581b48ca9b7eb83188b529559787a54e1a779fce55717346b3a0fa8d3866a8028f')

prepare() {
  cp -a "$_pkgname-$pkgver"{,-2}
}

build() {
  cd "$_pkgname-$pkgver-2"

  python2 setup.py build

  cd "$srcdir/$_pkgname-$pkgver"

  python setup.py  build
}

package_python2-poyo() {
  depends=('python2')

  cd "$_pkgname-$pkgver-2"

  python2 setup.py install --prefix=/usr --root="$pkgdir" --optimize=1 \
    --skip-build
}

package_python-poyo() {
  depends=('python')

  cd "$_pkgname-$pkgver"

  python setup.py install --prefix=/usr --root="$pkgdir" --optimize=1 \
    --skip-build
}

