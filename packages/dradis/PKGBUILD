# This file is part of BlackArch Linux ( http://blackarch.org ).
# See COPYING for license details.

pkgname=dradis
pkgver=3.0.0.rc1
pkgrel=3
pkgdesc='An open source framework to enable effective information sharing.'
groups=('blackarch' 'blackarch-recon' 'blackarch-misc')
url='http://dradisframework.org/'
license=('GPL')
depends=('ruby2.3')
arch=('any')
source=("https://github.com/dradis/dradis-legacy/archive/v${pkgver}.tar.gz")
install='dradis.install'
sha512sums=('d20a0e5884186afbc6e611e8999673023e099edccd67a9dd2fe9994aacd01d7e26017978a4f11a3e4d7f8f2400df69c4e3631268368a07cd94335f4c8ec36006')

package() {
  cd "$srcdir/dradis-legacy-$pkgver"

  install -dm 755 "$pkgdir/usr/bin"
  install -dm 755 "$pkgdir/usr/share/dradis"

  install -Dm644 -t "$pkgdir/usr/share/doc/dradis/" README.md CHANGELOG.md \
    CONTRIBUTING.md
  cp -a * "$pkgdir/usr/share/dradis"

  cat > "$pkgdir/usr/bin/dradis" << EOF
#!/bin/sh
cd /usr/share/dradis
exec ruby-2.3 dradis "\${@}"
EOF

  chmod +x "$pkgdir/usr/bin/dradis"
}
