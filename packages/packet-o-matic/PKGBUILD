# This file is part of BlackArch Linux ( https://www.blackarch.org/ ).
# See COPYING for license details.

pkgname=packet-o-matic
pkgver=v0.0.19.r12.g2fe05e7
pkgrel=1
epoch=1
pkgdesc='A real time packet processor. Reads the packet from an input module, match the packet using rules and connection tracking information and then send it to a target module.'
arch=('x86_64' 'aarch64')
groups=('blackarch' 'blackarch-networking' 'blackarch-tunnel')
url='http://www.packet-o-matic.org/'
license=('GPL-2.0-or-later')
depends=('libpcap' 'libxml2' 'libnet' 'uthash' 'libmicrohttpd' 'lua'
         'libexif')
makedepends=('git' 'xmlrpc-c' 'libtool' 'pkg-config')
source=("$pkgname::git+https://github.com/gmsoft-tuxicoman/pom-ng.git")
sha512sums=('SKIP')

pkgver() {
  cd $pkgname

  ( set -o pipefail
    git describe --long --tags --abbrev=7 2>/dev/null |
      sed 's/\([^-]*-g\)/r\1/;s/-/./g' ||
    printf "%s.%s" "$(git rev-list --count HEAD)" \
      "$(git rev-parse --short=7 HEAD)"
  )
}

build() {
  cd $pkgname

  autoreconf -f -i
  ./configure --prefix=/usr

  make
}

package() {
  cd $pkgname

  make DESTDIR="$pkgdir" install
}

