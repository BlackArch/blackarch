# This file is part of BlackArch Linux ( https://www.blackarch.org/ ).
# See COPYING for license details.

pkgname=packet-o-matic
pkgver=0.0.19
pkgrel=1
epoch=1
pkgdesc='A real time packet processor. Reads the packet from an input module, match the packet using rules and connection tracking information and then send it to a target module.'
arch=('x86_64' 'armv6h' 'armv7h' 'aarch64')
url='http://www.packet-o-matic.org/'
license=('GPL')
groups=('blackarch' 'blackarch-networking' 'blackarch-tunnel')
depends=('libpcap' 'libxml2' 'libnet' 'uthash' 'libmicrohttpd' 'lua51'
         'libexif')
makedepends=('git' 'xmlrpc-c' 'libtool' 'pkg-config')
source=("https://github.com/gmsoft-tuxicoman/pom-ng/archive/v$pkgver.tar.gz")
sha512sums=('652117e65148f8fa827ca7ca3c283b427551bf3844fd15d124a8c398236255b8b1761d6e7cba3172610eb2c62e2256ccc56235396f00f94997cf53de6ae23b8a')

build() {
  cd "pom-ng-$pkgver"

  export lua_CFLAGS="-llua5.1"
  export lua_LIBS="-llua5.1"

  autoreconf -f -i
  ./configure --prefix=/usr

  make
}

package() {
  cd "pom-ng-$pkgver"

  make DESTDIR="$pkgdir" install
}

