# This file is part of BlackArch Linux ( https://www.blackarch.org/ ).
# See COPYING for license details.

pkgbase=python-itanium-demangler
pkgname=('python2-itanium-demangler' 'python-itanium-demangler')
_pkgname=itanium_demangler
pkgver=1.0
pkgrel=2
pkgdesc='Nanosecond resolution temporal types.'
arch=('any')
url='https://pypi.org/project/itanium_demangler/#files'
license=('BSD')
makedepends=('python2-setuptools' 'python-setuptools')
source=("https://files.pythonhosted.org/packages/25/98/bd3496af11643dc9d74dc62ea3f5323b7874a08bd3aeac61927f0c029453/$_pkgname-$pkgver.tar.gz")
sha512sums=('93e2349d6e447033205231f30402a03a838f1929359359907fb3d07572524dc5753e061f9ad585884431951d6e9202ca689c6923834402552bcd650f260155ba')

prepare() {
  cp -a "$_pkgname-$pkgver"{,-2}
}

build() {
  cd "$_pkgname-$pkgver-2"

  python2 setup.py build

  cd "$srcdir/$_pkgname-$pkgver"

  python setup.py build
}

package_python2-itanium-demangler() {
  depends=('python2')

  cd "$_pkgname-$pkgver-2"

  python2 setup.py install --prefix=/usr --root="$pkgdir" -O1 --skip-build
}

package_python-itanium-demangler() {
  depends=('python')

  cd "$_pkgname-$pkgver"

  python setup.py install --prefix=/usr --root="$pkgdir" -O1 --skip-build
}

