# This file is part of BlackArch Linux ( http://blackarch.org ).
# See COPYING for license details.

pkgname='junkie'
pkgver=1365.70a83d6
pkgrel=1
groups=('blackarch' 'blackarch-sniffer' 'blackarch-networking')
pkgdesc='A modular packet sniffer and analyzer.'
arch=('x86_64' 'armv6h' 'armv7h' 'aarch64')
url='https://github.com/securactive/junkie'
license=('GPL3')
depends=('rlwrap' 'doxygen' 'openssl' 'libpcap' 'guile' 'p0f')
makedepends=('git')
source=('git+https://github.com/securactive/junkie.git')
sha1sums=('SKIP')

pkgver() {
  cd "$srcdir/junkie"

  echo $(git rev-list --count HEAD).$(git rev-parse --short HEAD)
}

build() {
  cd "$srcdir/junkie"

  export LIBS="-ldl"

  sh autogen.sh

  ./configure --prefix=/usr --sbindir=/usr/bin

  make
}

package() {
  cd "$srcdir/junkie"

  mkdir -p "$pkgdir/etc"

  make install DESTDIR="$pkgdir/"

  mv "$pkgdir/usr/etc/junkie" "$pkgdir/etc/junkie"
}
