# This file is part of BlackArch Linux ( https://www.blackarch.org/ ).
# See COPYING for license details.

pkgbase=python-dataset
pkgname=('python2-dataset' 'python-dataset')
_pkgname=dataset
pkgver=1.2.2
pkgrel=1
pkgdesc='Toolkit for Python-based data processing.'
arch=('any')
url='https://pypi.org/project/dataset/'
license=('MIT')
makedepends=('python2-setuptools' 'python-setuptools')
source=("https://files.pythonhosted.org/packages/2d/83/3d9bdffc3f6749aff574dbffc99c58ac8b4a742ddce98f38e41557f1e3db/$_pkgname-$pkgver.tar.gz")
sha512sums=('e5183d235fc9c95f4c7d1fc3070c539917a7f702174803e7810d20d31b9eb3262794b2d33abee8cadc0cf1291538e8c4b0d1661da58ea1885169d7ea50bbfccc')

prepare() {
  cp -a "$_pkgname-$pkgver"{,-2}
}

build() {
  cd "$_pkgname-$pkgver-2"

  python2 setup.py build

  cd "$srcdir/$_pkgname-$pkgver"

  python setup.py build
}

package_python2-dataset() {
  depends=('python2' 'python2-six' 'python2-sqlalchemy' 'python2-alembic'
           'python2-normality')

  cd "$_pkgname-$pkgver-2"

  python2 setup.py install --prefix=/usr --root="$pkgdir" --optimize=1 \
    --skip-build
}

package_python-dataset() {
  depends=('python' 'python-six' 'python-sqlalchemy' 'python-alembic'
           'python-normality')

  cd "$_pkgname-$pkgver"

  python setup.py install --prefix=/usr --root="$pkgdir" --optimize=1 \
    --skip-build
}

