# This file is part of BlackArch Linux ( https://www.blackarch.org/ ).
# See COPYING for license details.

pkgbase=python-dataset
pkgname=('python2-dataset' 'python-dataset')
_pkgname=dataset
pkgver=1.3.1
pkgrel=1
pkgdesc='Toolkit for Python-based data processing.'
arch=('any')
url='https://pypi.org/project/dataset/'
license=('MIT')
makedepends=('python2-setuptools' 'python-setuptools')
source=("https://files.pythonhosted.org/packages/b9/f1/90bca05c56194c9210d679cfbd5bc9d74df393ed45c3cebd734226e7b4f6/$_pkgname-$pkgver.tar.gz")
sha512sums=('2372239e254b8552e93573f4d88a429319fd230a1405f711958187da246a23fa014df79257dea4d9fc935f3e80f10eec26be8cf7632683abbb06d864c7f2bb57')

prepare() {
  cp -a "$_pkgname-$pkgver"{,-2}
}

build() {
  cd "$_pkgname-$pkgver-2"

  python2 setup.py build

  cd "$srcdir/$_pkgname-$pkgver"

  python setup.py build
}

package_python2-dataset() {
  depends=('python2' 'python2-six' 'python2-sqlalchemy' 'python2-alembic'
           'python2-normality')

  cd "$_pkgname-$pkgver-2"

  python2 setup.py install --prefix=/usr --root="$pkgdir" --optimize=1 \
    --skip-build
}

package_python-dataset() {
  depends=('python' 'python-six' 'python-sqlalchemy' 'python-alembic'
           'python-normality')

  cd "$_pkgname-$pkgver"

  python setup.py install --prefix=/usr --root="$pkgdir" --optimize=1 \
    --skip-build
}

