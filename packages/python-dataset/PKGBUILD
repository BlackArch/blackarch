# This file is part of BlackArch Linux ( https://www.blackarch.org/ ).
# See COPYING for license details.

pkgbase=python-dataset
pkgname=('python2-dataset' 'python-dataset')
_pkgname=dataset
pkgver=1.2.0
pkgrel=1
pkgdesc='Toolkit for Python-based data processing.'
arch=('any')
url='https://pypi.org/project/dataset/'
license=('MIT')
makedepends=('python2-setuptools' 'python-setuptools')
source=("https://files.pythonhosted.org/packages/dc/1f/2ae0c3d097e92a8a0364a0c0f790e7c9726cf30a379a363c362e8547552b/$_pkgname-$pkgver.tar.gz")
sha512sums=('92b3c9fb09b905aef9e4ee48153c9f21d7b18a345f5a682280c20f645507ca5bf31ae38aed4c573c85df91ce3c3e52779bf19a1d98a7b01d9813463c4128c16d')

prepare() {
  cp -a "$_pkgname-$pkgver"{,-2}
}

build() {
  cd "$_pkgname-$pkgver-2"

  python2 setup.py build

  cd "$srcdir/$_pkgname-$pkgver"

  python setup.py build
}

package_python2-dataset() {
  depends=('python2' 'python2-six' 'python2-sqlalchemy' 'python2-alembic'
           'python2-normality')

  cd "$_pkgname-$pkgver-2"

  python2 setup.py install --prefix=/usr --root="$pkgdir" --optimize=1 \
    --skip-build
}

package_python-dataset() {
  depends=('python' 'python-six' 'python-sqlalchemy' 'python-alembic'
           'python-normality')

  cd "$_pkgname-$pkgver"

  python setup.py install --prefix=/usr --root="$pkgdir" --optimize=1 \
    --skip-build
}

