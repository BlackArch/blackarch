# This file is part of BlackArch Linux ( https://www.blackarch.org/ ).
# See COPYING for license details.

pkgbase=python-dataset
pkgname=('python2-dataset' 'python-dataset')
_pkgname=dataset
pkgver=1.3.2
pkgrel=1
pkgdesc='Toolkit for Python-based data processing.'
arch=('any')
url='https://pypi.org/project/dataset/'
license=('MIT')
makedepends=('python2-setuptools' 'python-setuptools')
source=("https://files.pythonhosted.org/packages/75/be/d733e443300f1e04629a78fb8d1ba9ae44a5422eae821781e5dc0f74348b/$_pkgname-$pkgver.tar.gz")
sha512sums=('4f7ddf2cedb6b10cb0f9f3b85a4469af08a54915be8bb9ffda5f313c0786a740b8592202c6b044b05dea4b98a0edcd5c20f261c8d8430483b89953fff8771386')

prepare() {
  cp -a "$_pkgname-$pkgver"{,-2}
}

build() {
  cd "$_pkgname-$pkgver-2"

  python2 setup.py build

  cd "$srcdir/$_pkgname-$pkgver"

  python setup.py build
}

package_python2-dataset() {
  depends=('python2' 'python2-six' 'python2-sqlalchemy' 'python2-alembic'
           'python2-normality')

  cd "$_pkgname-$pkgver-2"

  python2 setup.py install --prefix=/usr --root="$pkgdir" --optimize=1 \
    --skip-build
}

package_python-dataset() {
  depends=('python' 'python-six' 'python-sqlalchemy' 'python-alembic'
           'python-normality')

  cd "$_pkgname-$pkgver"

  python setup.py install --prefix=/usr --root="$pkgdir" --optimize=1 \
    --skip-build
}

