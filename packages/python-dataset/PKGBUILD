# This file is part of BlackArch Linux ( https://www.blackarch.org/ ).
# See COPYING for license details.

pkgbase=python-dataset
pkgname=('python2-dataset' 'python-dataset')
_pkgname=dataset
pkgver=1.3.0
pkgrel=1
pkgdesc='Toolkit for Python-based data processing.'
arch=('any')
url='https://pypi.org/project/dataset/'
license=('MIT')
makedepends=('python2-setuptools' 'python-setuptools')
source=("https://files.pythonhosted.org/packages/4b/fb/2806b0d20f6b71fbecdec32581bb130ce636d5ca494e2a5869f60bee125f/$_pkgname-$pkgver.tar.gz")
sha512sums=('54bf082505ad68b0be20116d5334f009fda998a67b09b53075992a53479e2b2ba805c4c11b50dd16430e2a2d6b7ccb0bf33e47a2e9a451fa6ba058268020abe4')

prepare() {
  cp -a "$_pkgname-$pkgver"{,-2}
}

build() {
  cd "$_pkgname-$pkgver-2"

  python2 setup.py build

  cd "$srcdir/$_pkgname-$pkgver"

  python setup.py build
}

package_python2-dataset() {
  depends=('python2' 'python2-six' 'python2-sqlalchemy' 'python2-alembic'
           'python2-normality')

  cd "$_pkgname-$pkgver-2"

  python2 setup.py install --prefix=/usr --root="$pkgdir" --optimize=1 \
    --skip-build
}

package_python-dataset() {
  depends=('python' 'python-six' 'python-sqlalchemy' 'python-alembic'
           'python-normality')

  cd "$_pkgname-$pkgver"

  python setup.py install --prefix=/usr --root="$pkgdir" --optimize=1 \
    --skip-build
}

