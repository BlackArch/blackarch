# This file is part of BlackArch Linux ( http://blackarch.org ).
# See COPYING for license details.

pkgname='wifi-honey'
pkgver='1.0'
pkgrel=4
groups=('blackarch' 'blackarch-honeypot' 'blackarch-wireless')
pkgdesc="A management tool for wifi honeypots"
arch=('any')
url="http://www.digininja.org/projects/wifi_honey.php"
license=('CCPL')
depends=('screen' 'aircrack-ng')
source=("http://www.digininja.org/files/wifi_honey_$pkgver.tar.bz2")
sha1sums=('83ed96c586be3d20b02e6e972cb777d3eb019947')

prepare() {
  cd "$srcdir/wifi_honey"

  # Conform to the FHS.
  sed -i 's|wifi_honey_template\.rc|/etc/wifi-honey/wifi_honey_template\.rc|' wifi_honey.sh
}

package() {
  cd "$srcdir/wifi_honey"

  # Base directories.
  mkdir -p "$pkgdir/usr/bin"
  mkdir -p "$pkgdir/usr/share/doc/wifi-honey"
  mkdir -p "$pkgdir/etc/wifi-honey"

  # Bin.
  install -m755 wifi_honey.sh "$pkgdir/usr/bin/wifi-honey"

  # Docs.
  install -m644 README "$pkgdir/usr/share/doc/wifi-honey"

  # Config.
  install -m644 wifi_honey_template.rc "$pkgdir/etc/wifi-honey"
}
