pkgname=wifi-honey
pkgver=1.0
pkgrel=1
pkgdesc="A management tool for wifi honeypots"
arch=('x86_64' 'i686')
url="http://www.digininja.org/projects/wifi_honey.php"
license=('CCPL')
groups=('blackarch' 'blackarch-wireless')
depends=('screen' 'aircrack-ng')
source=("http://www.digininja.org/files/wifi_honey_$pkgver.tar.bz2")
md5sums=('512dad43ddddce5cebff3fd7861efa7e')

prepare() {
  cd "$srcdir/wifi_honey"

  # Conform to the FHS.
  sed -i 's|wifi_honey_template\.rc|/etc/wifi-honey/wifi_honey_template\.rc|' wifi_honey.sh
}

package() {
  cd "$srcdir/wifi_honey"

  # Base directories.
  install -dm755 "$pkgdir/usr/bin"
  install -dm755 "$pkgdir/usr/share/doc/wifi-honey"
  install -dm755 "$pkgdir/etc/wifi-honey"

  # Bin.
  install -m755 wifi_honey.sh "$pkgdir/usr/bin/wifi-honey"

  # Docs.
  install -m644 README "$pkgdir/usr/share/doc/wifi-honey"

  # Config.
  install -m644 wifi_honey_template.rc "$pkgdir/etc/wifi-honey"
}
