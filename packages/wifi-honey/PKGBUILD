# This file is part of BlackArch Linux ( https://www.blackarch.org/ ).
# See COPYING for license details.

pkgname=wifi-honey
pkgver=1.0
pkgrel=7
groups=('blackarch' 'blackarch-honeypot' 'blackarch-wireless')
pkgdesc='A management tool for wifi honeypots.'
arch=('any')
url='http://www.digininja.org/projects/wifi_honey.php'
license=('CCPL')
depends=('screen' 'aircrack-ng')
source=("http://www.digininja.org/files/wifi_honey_$pkgver.tar.bz2")
sha512sums=('c27a12dd6326e5953b0bef0b4d1ae5b602f3e0e0fc1ba3b67660c3748f85a0dc1e5b96bf38ba429b47786585a08d7b5d2f3f45ca17d535d739a824fbe46ba44d')

prepare() {
  cd wifi_honey

  # Conform to the FHS.
  sed -i 's|wifi_honey_template\.rc|/etc/wifi-honey/wifi_honey_template\.rc|' \
    wifi_honey.sh
}

package() {
  cd wifi_honey

  install -Dm 755 wifi_honey.sh "$pkgdir/usr/bin/$pkgname"
  install -Dm 644 -t "$pkgdir/usr/share/doc/$pkgname/" README
  install -Dm 644 wifi_honey_template.rc \
    "$pkgdir/etc/$pkgname/wifi_honey_template.rc"
}

