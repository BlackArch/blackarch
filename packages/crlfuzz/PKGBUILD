# This file is part of BlackArch Linux ( https://www.blackarch.org/ ).
# See COPYING for license details.

pkgname=crlfuzz
pkgver=v1.4.1.r4.gc960f98
pkgrel=1
pkgdesc='A fast tool to scan CRLF vulnerability written in Go.'
url='https://github.com/dwisiswant0/crlfuzz'
groups=('blackarch' 'blackarch-webapp')
arch=('x86_64' 'aarch64')
source=("git+https://github.com/dwisiswant0/$pkgname.git")
makedepends=('go' 'git')
license=('MIT')
sha512sums=('SKIP')

pkgver(){
  cd $pkgname
  
  git describe --long --tags | sed 's/\([^-]*-g\)/r\1/;s/-/./g'
}

build(){
  cd $pkgname

  GOPATH="$srcdir" go mod download
  GOPATH="$srcdir" go build \
    -trimpath \
    -buildmode=pie \
    -mod=readonly \
    -modcacherw \
    -ldflags "-s -w" \
    -o $pkgname ./cmd/$pkgname
}

package(){
  cd $pkgname

  install -dm 755 "$pkgdir/usr/share/$pkgname/"
  install -Dm 755 "$pkgname" "$pkgdir/usr/bin/$pkgname"

  install -Dm 644 LICENSE "$pkgdir/usr/share/license/$pkgname/LICENSE"
  install -Dm 644 -t "$pkgdir/usr/share/doc/$pkgname/" *.md

  rm LICENSE *.md

  cp -a * "$pkgdir/usr/share/$pkgname/"
}
