# This file is part of BlackArch Linux ( http://blackarch.org ).
# See COPYING for license details.

pkgname=firmwalker
pkgver=97.0535a48
pkgrel=1
pkgdesc='Script for searching the extracted firmware file system for goodies.'
groups=('blackarch' 'blackarch-firmware')
arch=('any')
url='https://github.com/craigz28/firmwalker'
license=('GPL3')
depends=('bash')
makedepends=('git')
source=('git+https://github.com/craigz28/firmwalker.git')
sha512sums=('SKIP')

pkgver() {
  cd "$srcdir/firmwalker"

  echo $(git rev-list --count HEAD).$(git rev-parse --short HEAD)
}

package() {
  cd "$srcdir/firmwalker"

  install -dm 755 "$pkgdir/usr/bin"
  install -dm 755 "$pkgdir/usr/share/firmwalker"

  install -Dm644 README.md "$pkgdir/usr/share/doc/firmwalker/README.md"
  install -Dm644 license "$pkgdir/usr/share/licenses/firmwalker/license"

  rm README.md license

  cp -a * "$pkgdir/usr/share/firmwalker"

  ln -s "/usr/share/firmwalker/firmwalker.sh" \
    "$pkgdir/usr/bin/firmwalker"
}
