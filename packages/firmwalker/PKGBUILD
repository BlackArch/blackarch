# This file is part of BlackArch Linux ( https://www.blackarch.org/ ).
# See COPYING for license details.

pkgname=firmwalker
pkgver=101.23ff299
pkgrel=1
pkgdesc='Script for searching the extracted firmware file system for goodies.'
groups=('blackarch' 'blackarch-firmware')
arch=('any')
url='https://github.com/craigz28/firmwalker'
license=('GPL3')
depends=('bash')
makedepends=('git')
source=("git+https://github.com/craigz28/$pkgname.git")
sha512sums=('SKIP')

pkgver() {
  cd $pkgname

  echo $(git rev-list --count HEAD).$(git rev-parse --short HEAD)
}

package() {
  cd $pkgname

  install -dm 755 "$pkgdir/usr/bin"
  install -dm 755 "$pkgdir/usr/share/$pkgname"

  install -Dm 644 -t "$pkgdir/usr/share/doc/$pkgname/" README.md
  install -Dm 644 license "$pkgdir/usr/share/licenses/$pkgname/LICENSE"

  rm README.md license

  cp -a * "$pkgdir/usr/share/$pkgname/"

  cat > "$pkgdir/usr/bin/$pkgname" << EOF
#!/bin/sh
cd /usr/share/$pkgname
exec ./$pkgname.sh "\$@"
EOF

  chmod +x "$pkgdir/usr/bin/$pkgname"
}

