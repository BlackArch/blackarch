# This file is part of BlackArch Linux ( https://www.blackarch.org/ ).
# See COPYING for license details.

pkgbase=python-pgspecial
pkgname=('python2-pgspecial' 'python-pgspecial')
_pkgname=pgspecial
pkgver=1.13.0
pkgrel=2
pkgdesc='Meta-commands handler for Postgres Database.'
arch=('any')
url='https://pypi.org/project/pgspecial/#files'
license=('BSD')
makedepends=('python2-setuptools' 'python-setuptools')
source=("https://files.pythonhosted.org/packages/8c/04/1f872b69366cf9ef6cd46b02897b21cd9d03435a6b7ca73c618ec1870bb5/$_pkgname-$pkgver.tar.gz")
sha512sums=('3e78969c328a7dd3d5e9aec3a02fa25446b32025b1aa6986a1ce7348b51f687613329af07b99ad84bd065708ef6c1d6ea686f6f63a788a7ed131601a03247a80')

prepare() {
  cp -a "$_pkgname-$pkgver"{,-2}
}

build() {
  cd "$_pkgname-$pkgver-2"

  python2 setup.py build

  cd "$srcdir/$_pkgname-$pkgver"

  python setup.py build
}

package_python2-pgspecial() {
  depends=('python2' 'python2-click' 'python2-sqlparse')

  cd "$_pkgname-$pkgver-2"

  python2 setup.py install --prefix=/usr --root="$pkgdir" --optimize=1 \
    --skip-build
}

package_python-pgspecial() {
  depends=('python' 'python-click' 'python-sqlparse')

  cd "$_pkgname-$pkgver"

  python setup.py install --prefix=/usr --root="$pkgdir" --optimize=1 \
    --skip-build
}

