# This file is part of BlackArch Linux ( https://www.blackarch.org/ ).
# See COPYING for license details.

pkgbase=python-pgspecial
pkgname=('python2-pgspecial' 'python-pgspecial')
_pkgname=pgspecial
pkgver=1.11.10
pkgrel=2
pkgdesc='Meta-commands handler for Postgres Database.'
arch=('any')
url='https://pypi.org/project/pgspecial/#files'
license=('BSD')
makedepends=('python2-setuptools' 'python-setuptools')
source=("https://files.pythonhosted.org/packages/7a/0a/1bc722f782ebb0c126dabe97d58f902b82703e8eda3477a60ddbbb51bb70/$_pkgname-$pkgver.tar.gz")
sha512sums=('04ef511a26a92bc21a99d256e8f71ce6b12b8ee66a50dd2e2b92e647a0163a23e49f4451eaed0d11c7fbf6ffb421444f48d1991b66c9c3b8a3aa8a00fafbeb08')

prepare() {
  cp -a "$_pkgname-$pkgver"{,-2}
}

build() {
  cd "$_pkgname-$pkgver-2"

  python2 setup.py build

  cd "$srcdir/$_pkgname-$pkgver"

  python setup.py build
}

package_python2-pgspecial() {
  depends=('python2' 'python2-click' 'python2-sqlparse')

  cd "$_pkgname-$pkgver-2"

  python2 setup.py install --prefix=/usr --root="$pkgdir" --optimize=1 \
    --skip-build
}

package_python-pgspecial() {
  depends=('python' 'python-click' 'python-sqlparse')

  cd "$_pkgname-$pkgver"

  python setup.py install --prefix=/usr --root="$pkgdir" --optimize=1 \
    --skip-build
}

