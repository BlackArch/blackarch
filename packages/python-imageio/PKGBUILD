# This file is part of BlackArch Linux ( https://www.blackarch.org/ ).
# See COPYING for license details.

pkgbase=python-imageio
pkgname=('python2-imageio' 'python-imageio')
_pkgname=imageio
pkgver=2.6.1
pkgrel=1
pkgdesc='Python library for reading and writing image data.'
arch=('any')
url='https://pypi.org/project/imageio/#files'
license=('BSD')
makedepends=('python2-setuptools' 'python-setuptools')
source=("https://files.pythonhosted.org/packages/0d/07/4d4c7768f3b88d90a999d81ca6852b9c2d38e87d47aab6d64de432293973/$_pkgname-$pkgver.tar.gz")
sha512sums=('0bbf8c0c06c01eb3d7953791ffaaae75fbfb94edc2f50d286434bb4397e4317e07e646392abf7c1030cf1a568202d4bda17dd6420284719591c10e7c98a914eb')

prepare() {
  cp -a "$_pkgname-$pkgver"{,-2}
}

build() {
  cd "$_pkgname-$pkgver-2"

  python2 setup.py build

  cd "$srcdir/$_pkgname-$pkgver"

  python setup.py build
}

package_python2-imageio() {
  depends=('python2-numpy' 'python2-pillow' 'python2-enum34' 'python2-futures'
           'python2-pathlib')

  cd "$_pkgname-$pkgver-2"

  python2 setup.py install --prefix=/usr --root="$pkgdir" --optimize=1 \
    --skip-build

  mv "$pkgdir/usr/bin/${_pkgname}_download_bin" \
    "$pkgdir/usr/bin/${_pkgname}_download_bin2"
  mv "$pkgdir/usr/bin/${_pkgname}_remove_bin" \
    "$pkgdir/usr/bin/${_pkgname}_remove_bin2"
}

package_python-imageio() {
  depends=('python' 'python-numpy' 'python-pillow')

  cd "$_pkgname-$pkgver"

  python setup.py install --prefix=/usr --root="$pkgdir" --optimize=1 \
    --skip-build
}

