# This file is part of BlackArch Linux ( https://www.blackarch.org/ ).
# See COPYING for license details.

pkgbase=python-imageio
pkgname=('python2-imageio' 'python-imageio')
_pkgname=imageio
pkgver=2.19.3
pkgrel=1
pkgdesc='Python library for reading and writing image data.'
arch=('any')
url='https://pypi.org/project/imageio/#files'
license=('BSD')
makedepends=('python2-setuptools' 'python-setuptools')
source=("https://files.pythonhosted.org/packages/84/4f/c26bb91ad39ed2264518c189832f875acc58d6fcfe4d28e1527500b74894/$_pkgname-$pkgver.tar.gz")
sha512sums=('0676ed19e356646c91bf25e4472a85b44ee57c9fee7b9267b33612f272da545e824cbbf08b75f9b5aae516e7bcc5db0d9f391b7138729948033024733321bf82')

prepare() {
  cp -a "$_pkgname-$pkgver"{,-2}
}

build() {
  cd "$_pkgname-$pkgver-2"

  python2 setup.py build

  cd "$srcdir/$_pkgname-$pkgver"

  python setup.py build
}

package_python2-imageio() {
  depends=('python2-numpy' 'python2-pillow6' 'python2-enum34' 'python2-futures'
           'python2-pathlib')

  cd "$_pkgname-$pkgver-2"

  python2 setup.py install --root="$pkgdir" --prefix=/usr -O1 --skip-build

  mv "$pkgdir/usr/bin/${_pkgname}_download_bin" \
    "$pkgdir/usr/bin/${_pkgname}_download_bin2"
  mv "$pkgdir/usr/bin/${_pkgname}_remove_bin" \
    "$pkgdir/usr/bin/${_pkgname}_remove_bin2"
}

package_python-imageio() {
  depends=('python' 'python-numpy' 'python-pillow6')

  cd "$_pkgname-$pkgver"

  python setup.py install --root="$pkgdir" --prefix=/usr -O1 --skip-build
}

