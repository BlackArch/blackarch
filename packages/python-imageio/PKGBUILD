# This file is part of BlackArch Linux ( https://www.blackarch.org/ ).
# See COPYING for license details.

pkgbase=python-imageio
pkgname=('python2-imageio' 'python-imageio')
_pkgname=imageio
pkgver=2.17.0
pkgrel=1
pkgdesc='Python library for reading and writing image data.'
arch=('any')
url='https://pypi.org/project/imageio/#files'
license=('BSD')
makedepends=('python2-setuptools' 'python-setuptools')
source=("https://files.pythonhosted.org/packages/de/55/11cd9f3171e30b5bcec2aeb679bbfc82d581a3f41874e8e9e47963224a01/$_pkgname-$pkgver.tar.gz")
sha512sums=('d500991f253e9f5629a5884cb09c059d16c29e5301697d47978b261ff58d0d483a370e1562f333b00ec8e1739127bc5584ecdd0cf0a1d3ebce0bb32cd0bdb3ad')

prepare() {
  cp -a "$_pkgname-$pkgver"{,-2}
}

build() {
  cd "$_pkgname-$pkgver-2"

  python2 setup.py build

  cd "$srcdir/$_pkgname-$pkgver"

  python setup.py build
}

package_python2-imageio() {
  depends=('python2-numpy' 'python2-pillow6' 'python2-enum34' 'python2-futures'
           'python2-pathlib')

  cd "$_pkgname-$pkgver-2"

  python2 setup.py install --root="$pkgdir" --prefix=/usr -O1 --skip-build

  mv "$pkgdir/usr/bin/${_pkgname}_download_bin" \
    "$pkgdir/usr/bin/${_pkgname}_download_bin2"
  mv "$pkgdir/usr/bin/${_pkgname}_remove_bin" \
    "$pkgdir/usr/bin/${_pkgname}_remove_bin2"
}

package_python-imageio() {
  depends=('python' 'python-numpy' 'python-pillow6')

  cd "$_pkgname-$pkgver"

  python setup.py install --root="$pkgdir" --prefix=/usr -O1 --skip-build
}

