# This file is part of BlackArch Linux ( https://www.blackarch.org/ ).
# See COPYING for license details.

pkgbase=python-imageio
pkgname=('python2-imageio' 'python-imageio')
_pkgname=imageio
pkgver=2.10.1
pkgrel=1
pkgdesc='Python library for reading and writing image data.'
arch=('any')
url='https://pypi.org/project/imageio/#files'
license=('BSD')
makedepends=('python2-setuptools' 'python-setuptools')
source=("https://files.pythonhosted.org/packages/cb/87/d406597e3cbcbb4d4a5ca30c822152f8f7556b0ca65824ad8fb4c169b712/$_pkgname-$pkgver.tar.gz")
sha512sums=('fd16fcfc399f08588db2cc3e08e45a8d475654da8ba5d62288ea0f6c9518d3b7b13f4605d1276e424b8eebbff4a9113f7c868f0bda942110039613257d1e0c44')

prepare() {
  cp -a "$_pkgname-$pkgver"{,-2}
}

build() {
  cd "$_pkgname-$pkgver-2"

  python2 setup.py build

  cd "$srcdir/$_pkgname-$pkgver"

  python setup.py build
}

package_python2-imageio() {
  depends=('python2-numpy' 'python2-pillow6' 'python2-enum34' 'python2-futures'
           'python2-pathlib')

  cd "$_pkgname-$pkgver-2"

  python2 setup.py install --root="$pkgdir" --prefix=/usr -O1 --skip-build

  mv "$pkgdir/usr/bin/${_pkgname}_download_bin" \
    "$pkgdir/usr/bin/${_pkgname}_download_bin2"
  mv "$pkgdir/usr/bin/${_pkgname}_remove_bin" \
    "$pkgdir/usr/bin/${_pkgname}_remove_bin2"
}

package_python-imageio() {
  depends=('python' 'python-numpy' 'python-pillow6')

  cd "$_pkgname-$pkgver"

  python setup.py install --root="$pkgdir" --prefix=/usr -O1 --skip-build
}

