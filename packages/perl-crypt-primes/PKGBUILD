# This file is part of BlackArch Linux ( http://blackarch.org ).
# See COPYING for license details.

_author='VIPUL'
_perlmod='Crypt-Primes'
pkgname='perl-crypt-primes'
pkgver='0.50'
pkgrel=8
pkgdesc='Provable Prime Number Generator suitable for Cryptographic Applications.'
arch=('any')
url='http://search.cpan.org/~VIPUL/Crypt-Primes-0.50/'
license=('GPL' 'PerlArtistic')
depends=('perl' 'gmp')
options=(!emptydirs)
source=("http://cpan.perl.org/modules/by-authors/id/V/VI/$_author/${_perlmod}-${pkgver}.tar.gz")
sha1sums=('1b61c6693b7d703514082ddb5821cdfd45274e81')

build() {
  cd "$srcdir/$_perlmod-$pkgver"

  PERL_MM_USE_DEFAULT=1 perl Makefile.PL INSTALLDIRS=vendor

  make
}

package() {
  cd "$srcdir/$_perlmod-$pkgver"

  make install DESTDIR="$pkgdir/"
}
