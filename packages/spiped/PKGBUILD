# This file is part of BlackArch Linux ( https://www.blackarch.org/ ).
# See COPYING for license details.

pkgname=spiped
pkgver=1.6.0
pkgrel=1
groups=('blackarch' 'blackarch-networking' 'blackarch-crypto')
pkgdesc='A utility for creating symmetrically encrypted and authenticated pipes between socket addresses.'
url='https://www.tarsnap.com/spiped.html'
arch=('x86_64' 'aarch64')
license=('custom:unknown')
makedepends=('gcc')
depends=('openssl')
source=("https://www.tarsnap.com/$pkgname/$pkgname-$pkgver.tgz")
sha512sums=('9bca8e7da31b86f956a653279f9346d8270a7e28a07d30b4a7104b21fbfbd1a81410e6a7dc0827127331a8f10226d5b8d2ddaacb94c9f76ecc6d15d728ead27a')

build() {
  cd "$pkgname-$pkgver"

  make
}

package() {
  cd "$pkgname-$pkgver"

  install -dm 755 "$pkgdir/usr/bin"
  install -dm 755 "$pkgdir/usr/share/man/man1"

  install -Dm 644 COPYRIGHT "$pkgdir/usr/share/licenses/$pkgname/COPYRIGHT"
  install -Dm 644 -t "$pkgdir/usr/share/doc/$pkgname/" BUILDING CHANGELOG \
    README.md STYLE

  make BINDIR="$pkgdir/usr/bin" MAN1DIR="$pkgdir/usr/share/man/man1/" install
}

