# This file is part of BlackArch Linux ( https://www.blackarch.org/ ).
# See COPYING for license details.

pkgname=python-js8py
_pkgname=${pkgname#python-}
pkgver=0.1.2.r6.gf7e394b
pkgrel=2
pkgdesc='Python module for parsing messages from the "js8" command line decoder.'
arch=('any')
url="https://github.com/jketterl/js8py"
license=('GPL-3.0')
depends=('python')
makedepends=('git' 'python-build' 'python-pip')
options=(!emptydirs)
provides=('js8py')
source=("git+https://github.com/jketterl/$_pkgname.git")
sha512sums=('SKIP')

pkgver() {
  cd $_pkgname

  ( set -o pipefail
    git describe --long --tags --abbrev=7 2>/dev/null |
      sed 's/\([^-]*-g\)/r\1/;s/-/./g' ||
    printf "%s.%s" "$(git rev-list --count HEAD)" \
      "$(git rev-parse --short=7 HEAD)"
  )
}

build() {
  cd $_pkgname

  python -m build --wheel --outdir="$startdir/dist"
}

package() {
  cd $_pkgname

  pip install \
    --verbose \
    --disable-pip-version-check \
    --no-warn-script-location \
    --ignore-installed \
    --no-compile \
    --no-deps \
    --root="$pkgdir" \
    --prefix=/usr \
    --no-index \
    --find-links="file://$startdir/dist" \
    $_pkgname
}

