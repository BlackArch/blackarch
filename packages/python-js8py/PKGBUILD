# This file is part of BlackArch Linux ( https://www.blackarch.org/ ).
# See COPYING for license details.

pkgname=python-js8py
_pkgname=${pkgname#python-}
pkgver=0.1.2.r6.gf7e394b
pkgrel=1
pkgdesc='Python module for parsing messages from the "js8" command line decoder.'
arch=('x86_64')
url="https://github.com/jketterl/js8py"
license=('GPL-3.0')
makedepends=('git' 'python-setuptools' 'python-nose')
depends=('python')
provides=('js8py')
source=("git+https://github.com/jketterl/$_pkgname.git")
sha512sums=('SKIP')

pkgver() {
  cd $_pkgname

  ( set -o pipefail
    git describe --long --tags --abbrev=7 2>/dev/null |
      sed 's/\([^-]*-g\)/r\1/;s/-/./g' ||
    printf "%s.%s" "$(git rev-list --count HEAD)" \
      "$(git rev-parse --short=7 HEAD)"
  )
}

check(){
  cd $_pkgname

  nosetests
}

build() {
  cd $_pkgname

  python setup.py build
}

package() {
  cd $_pkgname

  python setup.py install --root="$pkgdir" --prefix=/usr -O1 --skip-build
}

