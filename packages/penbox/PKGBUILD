# This file is part of BlackArch Linux ( http://blackarch.org ).
# See COPYING for license details.

pkgname='penbox'
pkgver=81.3b77c69
pkgrel=3
groups=('blackarch' 'blackarch-automation')
arch=('any')
pkgdesc='A Penetration Testing Framework - The Tool With All The Tools.'
url='https://github.com/x3omdax/PenBox'
license=('MIT')
depends=('python2' 'python2-requests' 'python2-lxml' 'git' 'python3' 'perl'
         'ruby')
makedepends=('git')
source=('git+https://github.com/x3omdax/PenBox.git')
sha1sums=('SKIP')

pkgver() {
  cd "$srcdir/PenBox"

  echo $(git rev-list --count HEAD).$(git rev-parse --short HEAD)
}

package() {
  cd "$srcdir/PenBox"

  mkdir -p "$pkgdir/usr/bin"
  mkdir -p "$pkgdir/usr/share/penbox"

  install -Dm644 -t "$pkgdir/usr/share/doc/penbox/" README.md
  install -Dm644 LICENSE.md "$pkgdir/usr/share/licenses/penbox/LICENSE.md"

  rm README.md LICENSE.md

  cp -a * "$pkgdir/usr/share/penbox"

  cat > "$pkgdir/usr/bin/penbox" << EOF
#!/bin/sh
exec python2 /usr/share/penbox/penbox.py "\${@}"
EOF

  chmod a+x "$pkgdir/usr/bin/penbox"
}
