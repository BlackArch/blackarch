# This file is part of BlackArch Linux ( https://www.blackarch.org/ ).
# See COPYING for license details.

pkgname=python-pyqt5-webengine
_pkgname=PyQtWebEngine
pkgdesc='Python bindings for QtWebEngine.'
pkgver=5.15.7
pkgrel=4
arch=('x86_64' 'aarch64')
url='https://pypi.org/project/PyQtWebEngine/'
license=('GPL-3.0-only')
depends=('python-pyqt5' 'qt5-webengine')
makedepends=('sip' 'pyqt-builder')
conflicts=('pyqtwebengine-common' 'python-pyqtwebwengine')
provides=('python-pyqtwebengine')
replaces=('python-pyqtwebengine')
source=("https://pypi.python.org/packages/source/P/PyQtWebEngine/PyQtWebEngine-$pkgver.tar.gz")
sha512sums=('0eede365bd0fa6eb60aa09443298a7afdb94497960ae31bf050311aec919c02a17ea13e57abe37e55f74d458acf34a6c3ab7f5895914742adc90e90a8c0eb442')

build() {
  cd "$_pkgname-$pkgver"

  sip-build \
    --no-make \
    --api-dir /usr/share/qt/qsci/api/python

  cd build

  make
}

package() {
  cd "$_pkgname-$pkgver/build"

  make INSTALL_ROOT="$pkgdir" install
}

