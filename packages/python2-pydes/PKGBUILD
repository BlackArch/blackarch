# This file is part of BlackArch Linux ( http://blackarch.org ).
# See COPYING for license details.

pkgname='python2-pydes'
pkgver='2.0.1'
pkgrel=2
pkgdesc='Pure python implementation of DES and TRIPLE DES encryption algorithm.'
arch=('any')
url='http://twhiteman.netfirms.com/des.html'
license=('custom:Public Domain')
depends=('python2')
makedepends=('python2-setuptools')
source=("http://twhiteman.netfirms.com/pyDES/pyDes-${pkgver}.zip"
        'LICENSE')
sha1sums=('1cd92750ab36326aa08d525f79e29ed6b80a85f1'
          'c0c8dd5c8209db4129e884c1d6c0226e7c7cfaa4')

prepare() {
	cd "$srcdir/pyDes-$pkgver"

  find "$srcdir" -type f -name '*.py' |
  xargs sed -i 's|#!/usr/bin/python|#!/usr/bin/python2|'
}

build() {
  cd "$srcdir/pyDes-$pkgver"

  python2 setup.py build
}

package() {
  cd "$srcdir/pyDes-$pkgver"

  python2 setup.py install --root="$pkgdir" --optimize=1

  install -Dm644 "$srcdir/LICENSE" \
    "$pkgdir/usr/share/licenses/python2-pydes/LICENSE"
}
