# This file is part of BlackArch Linux ( http://blackarch.org ).
# See COPYING for license details.

pkgname=python2-pydes
pkgver=2.0.1
pkgrel=3
pkgdesc='Pure python implementation of DES and TRIPLE DES encryption algorithm.'
arch=('any')
url='http://twhiteman.netfirms.com/des.html'
license=('custom:Public Domain')
depends=('python2')
makedepends=('python2-setuptools')
source=("http://twhiteman.netfirms.com/pyDES/pyDes-${pkgver}.zip"
        'LICENSE')
sha512sums=('d9107d532801ff2eb096eeb606c1b81fd68a76fccffdf095a6a2a79bd19815b4c366549989b6f4508c99a2f9afb063eceb7b0702e504ebb4d73dc8175057cb28'
            '5c9755f31433e5ee540049124fc7b6bdbb575eb682966247658424d0cc842cda92c9e8bc500e96462177ebeee71db993bbf9da75e3899eee1f00e48346d6c342')

prepare() {
	cd "$srcdir/pyDes-$pkgver"

  find "$srcdir" -type f -name '*.py' |
  xargs sed -i 's|#!/usr/bin/python|#!/usr/bin/python2|'
}

build() {
  cd "$srcdir/pyDes-$pkgver"

  python2 setup.py build
}

package() {
  cd "$srcdir/pyDes-$pkgver"

  python2 setup.py install --root="$pkgdir" --optimize=1

  install -Dm644 "$srcdir/LICENSE" \
    "$pkgdir/usr/share/licenses/python2-pydes/LICENSE"
}
