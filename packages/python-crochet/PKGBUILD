# This file is part of BlackArch Linux ( https://www.blackarch.org/ ).
# See COPYING for license details.

pkgbase=python-crochet
pkgname=('python2-crochet' 'python-crochet')
_pkgname=crochet
pkgver=2.0.0
pkgrel=2
pkgdesc='Use Twisted anywhere! - An MIT-licensed library that makes it easier to use Twisted from regular blocking code.'
url='https://pypi.org/project/crochet/'
license=('MIT')
arch=('any')
makedepends=('python2-setuptools' 'python-setuptools')
source=("https://files.pythonhosted.org/packages/ca/c0/76c9e9031a33ed2ebb80c06a4c6cbb5cee637a23d68f15b207842fb411a9/$_pkgname-$pkgver.tar.gz")
sha512sums=('fc905bcfbd24b47f4f6955a5abdc179811b9dceaadd557bce43eb57857737ee8ae9ec02adfe2cc4cf113c943f546887111a42a4dfefb78a23edb64b69d8b7905')

prepare() {
  cp -a "$_pkgname-$pkgver"{,-2}
}

build() {
  cd "$_pkgname-$pkgver-2"

  python2 setup.py build

  cd "$srcdir/$_pkgname-$pkgver"

  python setup.py build
}

package_python2-crochet() {
  depends=('python2' 'python2-twistedx' 'python2-wrapt')

  cd "$_pkgname-$pkgver-2"

  python2 setup.py install --root="$pkgdir" --prefix=/usr -O1 --skip-build
}

package_python-crochet() {
  depends=('python' 'python-twisted' 'python-wrapt')

  cd "$_pkgname-$pkgver"

  python setup.py install --root="$pkgdir" --prefix=/usr -O1 --skip-build
}

