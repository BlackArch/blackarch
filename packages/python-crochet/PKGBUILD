# This file is part of BlackArch Linux ( https://www.blackarch.org/ ).
# See COPYING for license details.

pkgbase=python-crochet
pkgname=('python2-crochet' 'python-crochet')
_pkgname=crochet
pkgver=1.10.0
pkgrel=2
pkgdesc='Use Twisted anywhere! - An MIT-licensed library that makes it easier to use Twisted from regular blocking code.'
url='https://pypi.org/project/crochet/'
license=('MIT')
arch=('any')
makedepends=('python2-setuptools' 'python-setuptools')
source=("https://files.pythonhosted.org/packages/fe/53/a5da4485840f83164b582b24a96ec5ebe7497f36db382170f22d4f765af1/$_pkgname-$pkgver.tar.gz")
sha512sums=('d80d974747f9e475494521998612d03d3d8c350e0d3fc9c2428bebeaaf24f35fd9a0bc3562b009c700feecf1ce36dca8ac6c37de4697bd94b5e132310b6c7114')

prepare() {
  cp -a "$_pkgname-$pkgver"{,-2}
}

build() {
  cd "$_pkgname-$pkgver-2"

  python2 setup.py build

  cd "$srcdir/$_pkgname-$pkgver"

  python setup.py build
}

package_python2-crochet() {
  depends=('python2' 'python2-twisted' 'python2-wrapt')

  cd "$_pkgname-$pkgver-2"

  python2 setup.py install --prefix=/usr --root="$pkgdir" --optimize=1 \
    --skip-build
}

package_python-crochet() {
  depends=('python' 'python-twisted' 'python-wrapt')

  cd "$_pkgname-$pkgver"

  python setup.py install --prefix=/usr --root="$pkgdir" --optimize=1 \
    --skip-build
}

