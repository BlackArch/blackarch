# This file is part of BlackArch Linux ( https://www.blackarch.org/ ).
# See COPYING for license details.

pkgbase=python-rpi-gpio
pkgname=('python2-rpi-gpio' 'python-rpi-gpio')
_pkgname=RPi.GPIO
pkgver=0.6.5
pkgrel=3
pkgdesc='A module to control Raspberry Pi GPIO channels.'
arch=('x86_64' 'armv6h' 'armv7h' 'aarch64')
url='https://pypi.org/project/rpi-gpio/#files'
license=('MIT')
makedepends=('python2-setuptools' 'python-setuptools')
source=("https://files.pythonhosted.org/packages/af/2f/407b6e4cc8a0bdf434825a160bba1807991886b63cce16a5f1a6e1f24cdf/$_pkgname-$pkgver.tar.gz")
sha512sums=('1393f49715b9c2d693743f962f75c4129ed229c83f49f31b913af8eaddb94280884127b8815d5c1e8451c3764c86962763a611b19ebc4afe72ac5fea3b61817b')

prepare() {
  cp -a "$_pkgname-$pkgver" "$_pkgname-$pkgver-2"
}

build() {
  cd "$_pkgname-$pkgver-2"

  python2 setup.py build

  cd "$srcdir/$_pkgname-$pkgver"

  python setup.py build
}

package_python2-rpi-gpio() {
  depends=('python2')

  cd "$_pkgname-$pkgver-2"

  python2 setup.py install --prefix=/usr --root="$pkgdir" --optimize=1 \
    --skip-build
}

package_python-rpi-gpio() {
  depends=('python')

  cd "$_pkgname-$pkgver"

  python setup.py install --prefix=/usr --root="$pkgdir" --optimize=1 \
    --skip-build
}

