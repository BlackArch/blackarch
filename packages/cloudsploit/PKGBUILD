# This file is part of BlackArch Linux ( https://www.blackarch.org/ ).
# See COPYING for license details.

pkgname=cloudsploit
pkgver=299.6f44b45
pkgrel=1
pkgdesc='AWS security scanning checks.'
arch=('any')
groups=('blackarch' 'blackarch-scanner' 'blackarch-automation')
url='https://github.com/cloudsploit/scans'
license=('GPL3')
depends=('nodejs')
makedepends=('git' 'npm')
source=("$pkgname::git+https://github.com/$pkgname/scans.git"
        'cloudsploit.install')
install='cloudsploit.install'
sha512sums=('SKIP'
            '9bb76f7e3fb14ca69b49f6ec385e9d13dd1abcd7a012669042e8ca5456e0b51fca6faea248fa65c8029f809828381cffd2b35155bb1b8313fdb461ba2032c523')

pkgver() {
  cd $pkgname

  echo $(git rev-list --count HEAD).$(git rev-parse --short HEAD)
}

prepare() {
  cd $pkgname

  npm install
}

package() {
  cd $pkgname

  install -dm 755 "$pkgdir/usr/bin"
  install -dm 755 "$pkgdir/usr/share/$pkgname"

  install -Dm 644 -t "$pkgdir/usr/share/doc/$pkgname/" README.md
  install -Dm 644 LICENSE "$pkgdir/usr/share/licenses/$pkgname/LICENSE"

  cp -a --no-preserve=ownership * "$pkgdir/usr/share/$pkgname/"

  cat > "$pkgdir/usr/bin/$pkgname" << EOF
#!/bin/sh
cd /usr/share/$pkgname
exec node index.js
EOF

  chmod +x "$pkgdir/usr/bin/$pkgname"
}

