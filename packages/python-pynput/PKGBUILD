# This file is part of BlackArch Linux ( https://www.blackarch.org/ ).
# See COPYING for license details.

pkgbase=python-pynput
pkgname=('python2-pynput' 'python-pynput')
_pkgname=pynput
pkgver=1.6.3
pkgrel=1
pkgdesc='Monitor and control user input devices.'
arch=('any')
url='https://pypi.org/project/pynput/#files'
license=('GPL')
makedepends=('python2-setuptools' 'python-setuptools' 'python2-sphinx'
             'python-sphinx')
options=(!emptydirs)
source=("https://files.pythonhosted.org/packages/c8/6a/11eaea1ce5aafcae0f28578248682cba55fffaaf093ff43e24c0f048edfc/$_pkgname-$pkgver.tar.gz")
sha512sums=('2fce17f871acb89c95d855ca04756e672bd622a10de41a9ba5801f26aed3f2b0ca81a2663f7c5fd17a9f9d45a2cc92826410c0884c8a78e77244283d19d39e11')

prepare() {
  cp -a "$_pkgname-$pkgver"{,-2}
}

build() {
  cd "$_pkgname-$pkgver-2"

  python2 setup.py build

  cd "$srcdir/$_pkgname-$pkgver"

  python setup.py build
}

package_python2-pynput() {
  depends=('python2' 'python2-six')

  cd "$_pkgname-$pkgver-2"

  python2 setup.py install --root="$pkgdir" --prefix=/usr -O1 --skip-build
}

package_python-pynput() {
  depends=('python' 'python-six')

  cd "$_pkgname-$pkgver"

  python setup.py install --root="$pkgdir" --prefix=/usr -O1 --skip-build
}

