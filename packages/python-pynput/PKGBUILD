# This file is part of BlackArch Linux ( https://www.blackarch.org/ ).
# See COPYING for license details.

pkgbase=python-pynput
pkgname=('python2-pynput' 'python-pynput')
_pkgname=pynput
pkgver=1.6.0
pkgrel=1
pkgdesc='Monitor and control user input devices.'
arch=('any')
url='https://pypi.org/project/pynput/#files'
license=('GPL')
makedepends=('python2-setuptools' 'python-setuptools' 'python-sphinx'
             'python2-sphinx')
options=(!emptydirs)
source=("https://files.pythonhosted.org/packages/87/a1/a1c03a688c81686d1bd22a7e8e4c076a6acd4ba89e25df94032686489c39/$_pkgname-$pkgver.tar.gz")
sha512sums=('e7baca04b3d7b03523e3985baa073b6002c3f074a416284e343b498b5628d76fd78951c6a1e45352114b44df5198251e5172405d813f3ef0b9b2f1f509280183')

prepare() {
  cp -a "$_pkgname-$pkgver"{,-2}
}

build() {
  cd "$_pkgname-$pkgver-2"

  python2 setup.py build

  cd "$srcdir/$_pkgname-$pkgver"

  python setup.py build
}

package_python2-pynput() {
  depends=('python2' 'python2-six')

  cd "$_pkgname-$pkgver-2"

  python2 setup.py install --root="$pkgdir" --prefix=/usr -O1 --skip-build
}

package_python-pynput() {
  depends=('python' 'python-six')

  cd "$_pkgname-$pkgver"

  python setup.py install --root="$pkgdir" --prefix=/usr -O1 --skip-build
}

