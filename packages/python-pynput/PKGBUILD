# This file is part of BlackArch Linux ( https://www.blackarch.org/ ).
# See COPYING for license details.

pkgbase=python-pynput
pkgname=('python2-pynput' 'python-pynput')
_pkgname=pynput
pkgver=1.5.2
pkgrel=1
pkgdesc='Monitor and control user input devices.'
arch=('any')
url='https://pypi.org/project/pynput/#files'
license=('GPL')
makedepends=('python2-setuptools' 'python-setuptools' 'python-sphinx'
             'python2-sphinx')
options=(!emptydirs)
source=("https://files.pythonhosted.org/packages/9b/fc/e71d4db926b9ceca05190f95077e6106165d9696e8469a62ae36b052674d/$_pkgname-$pkgver.tar.gz")
sha512sums=('b1039c218eb294bb848a5e86fa67ceef0b10b1e0a581dca3624def3b61101341edd3eb8acc3268ab8600dd8b045871ff06fbbe3a37cf50422231f1fa0084a031')

prepare() {
  cp -a "$_pkgname-$pkgver"{,-2}
}

build() {
  cd "$_pkgname-$pkgver-2"

  python2 setup.py build

  cd "$srcdir/$_pkgname-$pkgver"

  python setup.py build
}

package_python2-pynput() {
  depends=('python2' 'python2-six')

  cd "$_pkgname-$pkgver-2"

  python2 setup.py install --root="$pkgdir" --prefix=/usr -O1 --skip-build
}

package_python-pynput() {
  depends=('python' 'python-six')

  cd "$_pkgname-$pkgver"

  python setup.py install --root="$pkgdir" --prefix=/usr -O1 --skip-build
}

