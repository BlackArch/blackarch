# This file is part of BlackArch Linux ( http://blackarch.org ).
# See COPYING for license details.

pkgname=perl-digest-md4
pkgver=1.9
pkgrel=6
pkgdesc='Perl interface to the MD4 Algorithm.'
arch=('x86_64' 'armv6h' 'armv7h' 'aarch64')
url='http://search.cpan.org/dist/Digest-MD4'
license=('GPL' 'PerlArtistic')
depends=('perl')
options=(!emptydirs)
source=("http://search.cpan.org/CPAN/authors/id/M/MI/MIKEM/DigestMD4/Digest-MD4-${pkgver}.tar.gz")
sha512sums=('618ac83de37c697e0ea1354383d755cbc4b097b2156a8ae0e83d9463822bc860733f2b064eb6b614b4ef6889460001a4aed2e1607e38d409e75dc9d1772b059b')

build() {
  cd "$srcdir/Digest-MD4-$pkgver"

  PERL_MM_USE_DEFAULT=1 perl Makefile.PL INSTALLDIRS=vendor

  make
}

package() {
  cd "$srcdir/Digest-MD4-$pkgver"

  make install DESTDIR="$pkgdir/"

  find "$pkgdir" -name perllocal.pod -delete
  find "$pkgdir" -name .packlist -delete
}
