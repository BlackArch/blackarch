# This file is part of BlackArch Linux ( https://www.blackarch.org/ ).
# See COPYING for license details.

pkgname=icmptx
pkgver=15.33e0b82
pkgrel=1
pkgdesc='IP over ICMP tunnel.'
groups=('blackarch' 'blackarch-tunnel' 'blackarch-networking')
arch=('x86_64' 'armv6h' 'armv7h' 'aarch64')
url='http://thomer.com/icmptx/'
license=('GPL')
depends=('glibc')
makedepends=('git' 'make' 'gcc')
source=("git+https://github.com/jakkarth/$pkgname.git")
sha512sums=('SKIP')

pkgver() {
  cd $pkgname

  echo $(git rev-list --count HEAD).$(git rev-parse --short HEAD)
}

build() {
  cd $pkgname

  make flags="$CFLAGS"
}

package(){
  cd $pkgname

  install -Dm 755 icmptx "$pkgdir/usr/bin/$pkgname"
  install -Dm 644 README "$pkgdir/usr/share/doc/$pkgname/README"
  install -Dm 644 COPYING "$pkgdir/usr/share/licenses/$pkgname/COPYING"
}

