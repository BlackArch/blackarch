# This file is part of BlackArch Linux ( https://www.blackarch.org/ ).
# See COPYING for license details.

pkgbase=shodan
pkgname=('python2-shodan' 'python-shodan')
pkgver=1.27.0
pkgrel=1
pkgdesc='Python library and command-line utility for Shodan (https://developer.shodan.io).'
groups=('blackarch' 'blackarch-recon' 'blackarch-scanner'
        'blackarch-automation')
arch=('any')
url='https://pypi.org/project/shodan/#files'
makedepends=('python-setuptools' 'python2-setuptools')
license=('MIT')
source=("https://files.pythonhosted.org/packages/0a/3d/4c5aa163102d6b1991cfa26b662df0920643fc7694d5fbafa03b298adb6f/$pkgbase-$pkgver.tar.gz")
sha512sums=('0394a1d94bacc3c70035dc7ef37a34f4fe1ebed0791c7bd62b0f3b1981ff9844463766589cfa899e6485d7881378da11e3c3d4dfc9391902ca794b0efa85ff7f')

prepare() {
  cp -a "$pkgbase-$pkgver"{,-2}
}

build_python-shodan() {
  cd "$pkgbase-$pkgver-2"

  python2 setup.py build

  cd "$srcdir/$pkgbase-$pkgver"

  python2 setup.py build
}

package_python2-shodan() {
  depends=('python2' 'python2-requests' 'python2-simplejson' 'python2-click'
           'python2-click-plugins' 'python2-xlsxwriter' 'python2-colorama')

  cd "$pkgbase-$pkgver-2"

  python2 setup.py install --root="$pkgdir" --prefix=/usr -O1

  install -Dm 644 LICENSE "$pkgdir/usr/share/licenses/${pkgname[0]}/LICENSE"

  mv "$pkgdir/usr/bin/$pkgbase" "$pkgdir/usr/bin/${pkgbase}2"
}

package_python-shodan() {
  depends=('python' 'python-requests' 'python-simplejson' 'python-click'
           'python-click-plugins' 'python-xlsxwriter' 'python-colorama')

  cd "$pkgbase-$pkgver"

  python setup.py install --root="$pkgdir" --prefix=/usr -O1

  install -Dm 644 LICENSE "$pkgdir/usr/share/licenses/${pkgname[1]}/LICENSE"
}

