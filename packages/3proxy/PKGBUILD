# This file is part of BlackArch Linux ( http://blackarch.org ).
# See COPYING for license details.

pkgname=3proxy
pkgver=0.8.12
pkgrel=1
groups=('blackarch' 'blackarch-proxy')
pkgdesc='Tiny free proxy server.'
url='http://3proxy.ru/'
license=('GPL')
arch=('x86_64' 'armv6h' 'armv7h' 'aarch64')
options=('!makeflags')
depends=('openssl')
source=("https://github.com/z3APA3A/3proxy/archive/${pkgver}.tar.gz")
sha512sums=('33412e89991055598e06210dbcd7b2f9ee6810753c8f40ada02f222def0a49047a90ff7f2efa6895bc4f8ad99193866c980de2e79464310a77cc8e63c8b9813d')

prepare() {
  cd "$srcdir/3proxy-$pkgver"

  sed -i "s|/usr/bin/install|/bin/install|g" Makefile.Linux
  sed -i "s|/usr/local|/usr|g" Makefile.Linux
  sed -i "s|-s ||g" Makefile.Linux
  sed -i "s|-Wall -g -O2|$CFLAGS|g" Makefile.Linux
  sed -i "s|\$(prefix)/etc/|/etc/|g" Makefile.Linux
}

build() {
  cd "$srcdir/3proxy-$pkgver"

  make -f Makefile.Linux prefix=/usr
}

package() {
  cd "$srcdir/3proxy-$pkgver"

  make -f Makefile.Linux install "DESTDIR=$pkgdir/"

  # Fix conflict with extra/libproxy.
  mv "$pkgdir/usr/bin/proxy" "$pkgdir/usr/bin/3proxy"
}
