# This file is part of BlackArch Linux ( https://www.blackarch.org/ ).
# See COPYING for license details.

pkgname=3proxy
pkgver=496.5ee553e
pkgrel=1
groups=('blackarch' 'blackarch-proxy')
pkgdesc='Tiny free proxy server.'
url='http://3proxy.ru/'
license=('GPL')
arch=('x86_64' 'armv6h' 'armv7h' 'aarch64')
options=('!makeflags')
depends=('openssl')
source=('git+https://github.com/z3APA3A/3proxy.git')
sha512sums=('SKIP')

pkgver() {
  cd $pkgname

  echo $(git rev-list --count HEAD).$(git rev-parse --short HEAD)
}

prepare() {
  cd "$srcdir/3proxy"

  sed -i "s|/usr/bin/install|/bin/install|g" Makefile.Linux
  sed -i "s|/usr/local|/usr|g" Makefile.Linux
  sed -i "s|-s ||g" Makefile.Linux
  sed -i "s|-Wall -g -O2|$CFLAGS|g" Makefile.Linux
  sed -i "s|\$(prefix)/etc/|/etc/|g" Makefile.Linux
}

build() {
  cd "$srcdir/3proxy"

  make -f Makefile.Linux prefix=/usr
}

package() {
  cd "$srcdir/3proxy"

  make -f Makefile.Linux install "DESTDIR=$pkgdir/"

  # Fix conflict with extra/libproxy.
  mv "$pkgdir/usr/bin/proxy" "$pkgdir/usr/bin/3proxy"
}
