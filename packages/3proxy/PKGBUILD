# This file is part of BlackArch Linux ( http://blackarch.org ).
# See COPYING for license details.

pkgname='3proxy'
pkgver='0.8.11'
pkgrel=1
groups=('blackarch' 'blackarch-proxy')
pkgdesc='Tiny free proxy server.'
url='http://3proxy.ru/'
license=('GPL')
arch=('x86_64' 'armv6h' 'armv7h' 'aarch64')
options=('!makeflags')
depends=('openssl')
source=("http://3proxy.ru/${pkgver}/3proxy-${pkgver}.tgz")
sha1sums=('0bef5462e558db2d86cce92f8dc916b494dd7bde')

prepare() {
  cd "$srcdir/3proxy"

  sed -i "s|/usr/bin/install|/bin/install|g" Makefile.Linux
  sed -i "s|/usr/local|/usr|g" Makefile.Linux
  sed -i "s|-s ||g" Makefile.Linux
  sed -i "s|-Wall -g -O2|$CFLAGS|g" Makefile.Linux
  sed -i "s|\$(prefix)/etc/|/etc/|g" Makefile.Linux
}

build() {
  cd "$srcdir/3proxy"

  make -f Makefile.Linux prefix=/usr
}

package() {
  cd "$srcdir/3proxy"

  make -f Makefile.Linux install "DESTDIR=$pkgdir/"

  # Fix conflict with extra/libproxy.
  mv "$pkgdir/usr/bin/proxy" "$pkgdir/usr/bin/3proxy"
}
