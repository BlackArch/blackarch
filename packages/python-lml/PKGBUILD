# This file is part of BlackArch Linux ( https://www.blackarch.org/ ).
# See COPYING for license details.

pkgbase=python-lml
pkgname=('python2-lml' 'python-lml')
_pkgname=lml
pkgver=0.0.9
pkgrel=3
pkgdesc='Load me later. A lazy loading plugin management system.'
arch=('any')
url='https://github.com/chfw/lml/releases'
license=('BSD')
makedepends=('python2-setuptools' 'python-setuptools')
source=("https://files.pythonhosted.org/packages/65/f3/3718c8b7b066731e8e60eaf42b1fd0d48d3539a6e7dbbdd4828be13c14ee/$_pkgname-$pkgver.tar.gz")
sha512sums=('8eea12968501f4cbb8c4c26684d89cc5b423964c660ccfe268200843e25e88ad8989b933caae8d2579e6206b2bd12157b48c26d86b8a369c7827b4d109826a1e')

prepare() {
  cp -a "$_pkgname-$pkgver"{,-2}
}

build() {
  cd "$_pkgname-$pkgver-2"

  python2 setup.py build

  cd "$srcdir/$_pkgname-$pkgver"

  python setup.py build
}

package_python2-lml() {
  depends=('python2')

  cd "$_pkgname-$pkgver-2"

  python2 setup.py install --prefix=/usr --root="$pkgdir" --optimize=1 \
    --skip-build
}

package_python-lml() {
  depends=('python')

  cd "$_pkgname-$pkgver"

  python setup.py install --prefix=/usr --root="$pkgdir" --optimize=1 \
    --skip-build
}

