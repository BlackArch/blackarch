# This file is part of BlackArch Linux ( https://www.blackarch.org/ ).
# See COPYING for license details.

pkgname=ruby1.8
_pkgname='ruby'
pkgver=1.8.7_p370
_pkgver="${pkgver/_/-}"
pkgrel=5
pkgdesc='An object-oriented language for quick and easy programming'
arch=('x86_64' 'aarch64')
license=('custom')
url='http://www.ruby-lang.org/en/'
options=('!emptydirs')
makedepends=('tk')
depends=('gdbm' 'db' 'openssl' 'zlib' 'readline')
optdepends=('tk: for Ruby/TK')
source=("ftp://ftp.ruby-lang.org/pub/ruby/1.8/$_pkgname-$_pkgver.tar.bz2" "fix.patch")
sha512sums=('92770a8159cd9049ffc5bc3ce4777b701eb19d7b'
          '3ed3c86ff3f534e27750604c6e781a2bec79efc2')

prepare() {
  cd "$_pkgname-$_pkgver"

  patch -Np1 < "$srcdir/fix.patch"
}

build() {
  cd "$_pkgname-$_pkgver"

if [[ $CARCH == arm* ]] ; then
  ./configure \
    --prefix=/usr/share/ruby1.8 \
    --disable-rpath \
    --enable-shared \
    --disable-install-doc \
    --enable-pthread \
    --build=arm
else
  ./configure \
    --prefix=/usr/share/ruby1.8 \
    --disable-rpath \
    --enable-shared \
    --enable-pthread \
    CFLAGS="-O2 -fno-tree-dce -fno-optimize-sibling-calls"
fi
  make
}

package() {
  cd "$_pkgname-$_pkgver"

  make DESTDIR="$pkgdir" install

  install -dm 755 "$pkgdir/usr/bin"
  install -dm 755 "$pkgdir/usr/lib"

  install -Dm644 COPYING "$pkgdir/usr/share/licenses/$pkgname/COPYING"
  # Bin.
  for i in erb irb rdoc ri ruby testrb
  do
    ln -s "/usr/share/ruby1.8/bin/$i" "$pkgdir/usr/bin/$i-1.8"
  done

  ln -s /usr/share/ruby1.8/lib/libruby.so.1.8 "$pkgdir/usr/lib/libruby.so.1.8"
}

