# This file is part of BlackArch Linux ( http://blackarch.org ).
# See COPYING for license details.

pkgname='ffdec'
pkgver='10.0.0'
pkgrel=1
pkgdesc='Open source Flash SWF decompiler and editor.'
groups=('blackarch' 'blackarch-decompiler' 'blackarch-binary'
        'blackarch-misc')
arch=('any')
url='https://www.free-decompiler.com/flash/'
license=('GPL3')
depends=('java-runtime=8')
conflicts=('jpexs-decompiler-git')
source=("https://github.com/jindrapetrik/jpexs-decompiler/releases/download/version${pkgver}/ffdec_${pkgver}.zip"
        'ffdec'
        'ffdec.desktop'
        'ffdec.png')
sha1sums=('c69e943d8e1beb1178efd7d317ad51cf239e931d'
          '50827d8a8aa8d527f8f6e4b315322bba3ed83eed'
          '19ad3b88c640c2303965f8dce384cc4dddd5a08d'
          '6f748d60a076315e953cdfa4332de830662d742b')

package() {
  cd "$srcdir"

  install -Ddm755 "${pkgdir}/usr/share/java/ffdec/lib"
  install -Ddm755 "${pkgdir}/usr/share/licenses/ffdec"
  install -Dm755 "ffdec" "${pkgdir}/usr/bin/ffdec"
  install -Dm644 "ffdec.jar" "${pkgdir}/usr/share/java/ffdec/ffdec.jar"
  install -Dm644 "license.txt" "${pkgdir}/usr/share/licenses/ffdec/license.txt"
  install -Dm644 "ffdec.desktop" "${pkgdir}/usr/share/applications/ffdec.desktop"
  install -Dm644 "ffdec.png" "${pkgdir}/usr/share/pixmaps/ffdec.png"

  cd "$srcdir/lib"

  for f in *.license.txt;
  do
      cp -d --no-preserve=ownership "$f" "${pkgdir}/usr/share/licenses/ffdec/$f"
  done

  for f in *.jar;
  do
      cp -d --no-preserve=ownership "$f" "${pkgdir}/usr/share/java/ffdec/lib/$f"
  done
}
