# This file is part of BlackArch Linux ( http://blackarch.org ).
# See COPYING for license details.

pkgname='sublist3r'
pkgver=115.832d544
pkgrel=1
arch=('any')
groups=('blackarch' 'blackarch-recon' 'blackarch-scanner')
pkgdesc='A Fast subdomains enumeration tool for penetration testers.'
license=('GPL2')
depends=('python2' 'python2-argparse' 'python2-dnspython' 'python2-requests')
makedepends=('git')
source=('git+https://github.com/aboul3la/Sublist3r.git')
sha1sums=('SKIP')

pkgver() {
  cd "$srcdir/Sublist3r"

  echo $(git rev-list --count HEAD).$(git rev-parse --short HEAD)
}

package() {
  cd "$srcdir/Sublist3r"

  mkdir -p "$pkgdir/usr/bin"
  mkdir -p "$pkgdir/usr/share/sublist3r"

  install -Dm644 -t "$pkgdir/usr/share/doc/sublist3r" README.md
  install -Dm644 LICENSE "$pkgdir/usr/share/licenses/sublist3r/LICENSE"

  rm README.md LICENSE

  cp -a --no-preserve=ownership * "$pkgdir/usr/share/sublist3r/"

  cat > "$pkgdir/usr/bin/sublist3r" << EOF
#!/bin/sh
cd /usr/share/$pkgname
exec python2 sublist3r.py "\${@}"
EOF

  chmod a+x "$pkgdir/usr/bin/sublist3r"
}
