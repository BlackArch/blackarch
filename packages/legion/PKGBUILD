# This file is part of BlackArch Linux ( https://www.blackarch.org/ ).
# See COPYING for license details.

pkgname=legion
pkgver=52.036730a
pkgrel=2
pkgdesc='Automatic Enumeration Tool based in Open Source tools.'
groups=('blackarch' 'blackarch-recon' 'blackarch-automation')
arch=('any')
url='https://github.com/carlospolop/legion'
license=('MIT')
# NOTE: 'odat' is missing. we need to add it
depends=('python' 'python-termcolor' 'dirhunt' 'arjun' 'dirsearch' 'impacket'
         'rpcbind')
makedepends=('git')
source=("git+https://github.com/carlospolop/$pkgname.git")
sha512sums=('SKIP')

pkgver() {
  cd $pkgname

  echo $(git rev-list --count HEAD).$(git rev-parse --short HEAD)
}

package() {
  cd $pkgname

  install -dm 755 "$pkgdir/usr/bin"
  install -dm 755 "$pkgdir/usr/share/$pkgname"

  install -Dm 644 -t "$pkgdir/usr/share/doc/$pkgname/" README.md
  install -Dm 644 LICENSE "$pkgdir/usr/share/licenses/$pkgname/LICENSE"

  rm README.md LICENSE

  cp -a * "$pkgdir/usr/share/$pkgname/"

  cat > "$pkgdir/usr/bin/$pkgname" << EOF
#!/bin/sh
cd /usr/share/$pkgname
exec python $pkgname.py "\$@"
EOF

  chmod +x "$pkgdir/usr/bin/$pkgname"
}

