# This file is part of BlackArch Linux ( http://blackarch.org ).
# See COPYING for license details.

pkgname='dnmap'
pkgver='0.6'
pkgrel=6
groups=('blackarch' 'blackarch-scanner')
pkgdesc="The distributed nmap framework"
url="http://sourceforge.net/projects/dnmap/"
license=('GPL3')
arch=('any')
depends=('python2-twisted' 'python2-pyopenssl' 'python2')
install='dnmap.install'
source=("http://downloads.sourceforge.net/project/dnmap/dnmap_v$pkgver.tgz")
sha1sums=('f80dd7aff9a8ba83e329f40b330fc0ae33ec7bd7')

prepare() {
  cd "$srcdir/dnmap_v$pkgver"

  sed -i 's/env python/env python2/' *.py
}

package() {
  cd "$srcdir/dnmap_v$pkgver"

  # Base directories.
  mkdir -p "$pkgdir/usr/bin"
  mkdir -p "$pkgdir/usr/share/dnmap"
  mkdir -p "$pkgdir/usr/share/doc/dnmap"

  # Docs.
  install -m644 README "$pkgdir/usr/share/doc/dnmap"

  install -m644 server.pem "$pkgdir/usr/share/dnmap"

  # Bin.
  install -m755 dnmap_client.py "$pkgdir/usr/bin/dnmap_client"
  install -m755 dnmap_server.py "$pkgdir/usr/bin/dnmap_server"
}
