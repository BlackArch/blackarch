# This file is part of BlackArch Linux ( http://blackarch.org ).
# See COPYING for license details.

pkgname='make-pdf'
pkgver='0.1.7'
pkgrel=1
pkgdesc='This tool will embed javascript inside a PDF document.'
arch=('any')
url='http://blog.didierstevens.com/programs/pdf-tools/'
groups=('blackarch' 'blackarch-forensic')
depends=('python2' 'dos2unix')
license=('GPL')
source=("http://www.didierstevens.com/files/software/make-pdf_V${pkgver//./_}.zip")
sha1sums=('c95947781ca1d157df1387dd4cb00c2eaec4bb0c')

prepare(){
  cd "$srcdir"

  grep -iRl 'python' "$srcdir" |
    xargs sed -i 's|#!.*/usr/bin/python|#!/usr/bin/python2|;s|#!.*/usr/bin/env python$|#!/usr/bin/env python2|'

  dos2unix *.py
}

package() {
  cd "$srcdir"

  install -Dm644 mPDF.py "$pkgdir/usr/lib/python2.7/site-packages/mPDF.py"
  install -Dm755 make-pdf-javascript.py "$pkgdir/usr/bin/make-pdf-javascript"
}
