# This file is part of BlackArch Linux ( https://www.blackarch.org/ ).
# See COPYING for license details.

pkgname=aws-inventory
pkgver=19.9a2fa8e
pkgrel=1
pkgdesc='Discover resources created in an AWS account.'
groups=('blackarch' 'blackarch-recon')
arch=('any')
url='https://github.com/nccgroup/aws-inventory'
license=('AGPL')
depends=('python2' 'python2-argparse' 'python2-botocore' 'python2-opinel' 'tk')
makedepends=('git')
source=("git+https://github.com/nccgroup/$pkgname.git")
sha512sums=('SKIP')

pkgver() {
  cd $pkgname

  echo $(git rev-list --count HEAD).$(git rev-parse --short HEAD)
}

package() {
  cd $pkgname

  install -dm 755 "$pkgdir/usr/bin"
  install -dm 755 "$pkgdir/usr/share/$pkgname"

  install -Dm 644 -t "$pkgdir/usr/share/doc/$pkgname/" *.md
  install -Dm 644 LICENSE "$pkgdir/usr/share/licenses/$pkgname/LICENSE"

  rm *.md LICENSE

  cp -a * "$pkgdir/usr/share/$pkgname/"

  cat > "$pkgdir/usr/bin/$pkgname" << EOF
#!/bin/sh
cd /usr/share/$pkgname
exec python2 aws_inventory.py "\$@"
EOF

  chmod +x "$pkgdir/usr/bin/$pkgname"
}

