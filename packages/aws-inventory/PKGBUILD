# This file is part of BlackArch Linux ( http://blackarch.org ).
# See COPYING for license details.

pkgname=aws-inventory
pkgver=16.d987097
pkgrel=1
pkgdesc='Discover resources created in an AWS account.'
groups=('blackarch' 'blackarch-recon')
arch=('any')
url='https://github.com/nccgroup/aws-inventory'
license=('GAGPL')
depends=('python2' 'python2-argparse' 'python2-botocore' 'python2-opinel')
makedepends=('git')
source=('git+https://github.com/nccgroup/aws-inventory.git')
sha512sums=('SKIP')

pkgver() {
  cd "$srcdir/aws-inventory"

  echo $(git rev-list --count HEAD).$(git rev-parse --short HEAD)
}

package() {
  cd "$srcdir/aws-inventory"

  install -dm 755 "$pkgdir/usr/bin"
  install -dm 755 "$pkgdir/usr/share/aws-inventory"

  install -Dm644 -t "$pkgdir/usr/share/doc/aws-inventory/" *.md
  install -Dm644 LICENSE "$pkgdir/usr/share/licenses/aws-inventory/LICENSE"

  rm *.md LICENSE

  cp -a * "$pkgdir/usr/share/aws-inventory/"

  cat > "$pkgdir/usr/bin/aws-inventory" << EOF
#!/bin/sh
cd /usr/share/aws-inventory
exec python2 aws_inventory.py "\${@}"
EOF

  chmod a+x "$pkgdir/usr/bin/aws-inventory"
}
