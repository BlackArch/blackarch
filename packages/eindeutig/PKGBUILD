# This file is part of BlackArch Linux ( http://blackarch.org ).
# See COPYING for license details.

pkgname=eindeutig
pkgver=20050628_1
pkgrel=4
groups=('blackarch' 'blackarch-forensic')
pkgdesc="Examine the contents of Outlook Express DBX email repository files (forensic purposes)"
arch=('x86_64' 'armv6h' 'armv7h' 'aarch64')
url="http://www.jonesdykstra.com/"
license=('GPL')
source=("http://downloads.sourceforge.net/project/fast/Eindeutig/Eindeutig%20v20050628_1/eindeutig_$pkgver.zip")
sha512sums=('1263acb7d67514821f3186f47da9796e043fe8334369675078024435739dba3838ffdf0a75cb5e1d97119960e43f61e5a0a7ab86f2f724e5240ac4277114a8e4')

prepare(){
  cd "$srcdir/eindeutig_$pkgver/src"
  mv Eindeutig.c eindeutig.c
}

build() {
  cd "$srcdir/eindeutig_$pkgver/src"
  make
}

package() {
  cd "$srcdir/eindeutig_$pkgver/src"

  # Base directories.
  install -dm 755 "$pkgdir/usr/bin/"

  # Bin.
  # Renaming to dbxparse as the usage is for dbxparse not eindeutig.
  install -m755 ../bin/eindeutig "$pkgdir/usr/bin/dbxparse"
}
