# This file is part of BlackArch Linux ( http://blackarch.org ).
# See COPYING for license details.

pkgname='hasher'
pkgver='29.0441700'
pkgrel=1
groups=('blackarch' 'blackarch-cracker' 'blackarch-crypto')
pkgdesc='A tool that allows you to quickly hash plaintext strings, or compare hashed values with a plaintext locally.'
arch=('any')
url='https://github.com/ChrisTruncer/Hasher'
depends=('python2' 'python2-passlib' 'python2-py-bcrypt')
makedepends=('git')
license=('GPL3')
source=('git+https://github.com/ChrisTruncer/Hasher.git')
sha1sums=('SKIP')

pkgver() {
  cd "$srcdir/Hasher"

  echo $(git rev-list --count HEAD).$(git rev-parse --short HEAD)
}

prepare() {
  cd "$srcdir/Hasher"

  sed -i 's|/usr/bin/env python|/usr/bin/env python2|' Hasher.py
}

package() {
  cd "$srcdir/Hasher"

  install -Dm755 Hasher.py "$pkgdir/usr/bin/hasher"
  install -Dm644 README.md "$pkgdir/usr/share/doc/hasher/README.md"
  install -m644 CHANGELOG "$pkgdir/usr/share/doc/hasher/CHANGELOG"
  install -Dm644 LICENSE "$pkgdir/usr/share/licenses/hasher/LICENSE"
}
