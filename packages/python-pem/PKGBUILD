# This file is part of BlackArch Linux ( https://www.blackarch.org/ ).
# See COPYING for license details.

pkgbase=python-pem
pkgname=('python2-pem' 'python-pem')
_pkgname=pem
pkgver=21.2.0
pkgrel=1
pkgdesc='Easy PEM file parsing in Python.'
arch=('any')
url='https://pypi.org/project/pem/#files'
license=('MIT')
makedepends=('python2-setuptools' 'python-setuptools')
source=("https://files.pythonhosted.org/packages/48/57/78c8edb8a241fe2ae8f19bc7d5fe73c2f23e648fd38408e821fb6929f784/$_pkgname-$pkgver.tar.gz")
sha512sums=('a3d2b9b42a8c016f26c9a85b191bbb737ee65bacf2d812abb9b7a0f4fc360e6e3cbd3bc5a52284dd597fbbdf4a7454acdc240f69a3cb70ce87952c10de2038a8')

prepare() {
  cp -a "$_pkgname-$pkgver"{,-2}
}

build() {
  cd "$_pkgname-$pkgver-2"

  python2 setup.py build

  cd "$srcdir/$_pkgname-$pkgver"

  python setup.py build
}

package_python2-pem() {
  depends=('python2')

  cd "$_pkgname-$pkgver-2"

  python2 setup.py install --prefix=/usr --root="$pkgdir" -O1 --skip-build
}

package_python-pem() {
  depends=('python')

  cd "$_pkgname-$pkgver"

  python setup.py install --prefix=/usr --root="$pkgdir" -O1 --skip-build
}

