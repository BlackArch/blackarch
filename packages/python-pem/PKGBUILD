# This file is part of BlackArch Linux ( https://www.blackarch.org/ ).
# See COPYING for license details.

pkgbase=python-pem
pkgname=('python2-pem' 'python-pem')
_pkgname=pem
pkgver=19.3.0
pkgrel=1
pkgdesc='Easy PEM file parsing in Python.'
arch=('any')
url='https://pypi.org/project/pem/#files'
license=('MIT')
makedepends=('python2-setuptools' 'python-setuptools')
source=("https://files.pythonhosted.org/packages/56/86/96e340a690a8ee4e95fa5e9d056db698c70372a608b91dfa8725a3587134/$_pkgname-$pkgver.tar.gz")
sha512sums=('b37b79ed86f7f736611b5d7275e834afb933450294f3ce7a2a1fef4926a7445cba1627c2c995add54d556febea5a477fc1bc013d9a01cd5453e957df05e033e1')

prepare() {
  cp -a "$_pkgname-$pkgver"{,-2}
}

build() {
  cd "$_pkgname-$pkgver-2"

  python2 setup.py build

  cd "$srcdir/$_pkgname-$pkgver"

  python setup.py build
}

package_python2-pem() {
  depends=('python2')

  cd "$_pkgname-$pkgver-2"

  python2 setup.py install --prefix=/usr --root="$pkgdir" -O1 --skip-build
}

package_python-pem() {
  depends=('python')

  cd "$_pkgname-$pkgver"

  python setup.py install --prefix=/usr --root="$pkgdir" -O1 --skip-build
}

