# This file is part of BlackArch Linux ( https://www.blackarch.org/ ).
# See COPYING for license details.

pkgbase=python-pem
pkgname=('python2-pem' 'python-pem')
_pkgname=pem
pkgver=21.1.0
pkgrel=1
pkgdesc='Easy PEM file parsing in Python.'
arch=('any')
url='https://pypi.org/project/pem/#files'
license=('MIT')
makedepends=('python2-setuptools' 'python-setuptools')
source=("https://files.pythonhosted.org/packages/ae/f8/43fd26ac249cf81350d4d5a68b18fa51e158b93476f7fa5e2f653865ecf4/$_pkgname-$pkgver.tar.gz")
sha512sums=('af80fa0df76bcbc70c7c46cc3b32a0bbae1415f15e118fbc15a4bc5db23f88e89c29dc2a6b09b833339eca8ed9fb358fb1db781938a9f403a9f0a90297c893f3')

prepare() {
  cp -a "$_pkgname-$pkgver"{,-2}
}

build() {
  cd "$_pkgname-$pkgver-2"

  python2 setup.py build

  cd "$srcdir/$_pkgname-$pkgver"

  python setup.py build
}

package_python2-pem() {
  depends=('python2')

  cd "$_pkgname-$pkgver-2"

  python2 setup.py install --prefix=/usr --root="$pkgdir" -O1 --skip-build
}

package_python-pem() {
  depends=('python')

  cd "$_pkgname-$pkgver"

  python setup.py install --prefix=/usr --root="$pkgdir" -O1 --skip-build
}

