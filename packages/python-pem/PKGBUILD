# This file is part of BlackArch Linux ( https://www.blackarch.org/ ).
# See COPYING for license details.

pkgbase=python-pem
pkgname=('python2-pem' 'python-pem')
_pkgname=pem
pkgver=19.2.0
pkgrel=1
pkgdesc='Easy PEM file parsing in Python.'
arch=('any')
url='https://pypi.org/project/pem/#files'
license=('MIT')
makedepends=('python2-setuptools' 'python-setuptools')
source=("https://files.pythonhosted.org/packages/88/86/2481ceeeadc6b7d2221729c83a1316cfc90ce828a4bc3e1022a9e06085c9/$_pkgname-$pkgver.tar.gz")
sha512sums=('cf9cdce93594fabe2e75c47414899c0724f258eafcdf85e3e5fca6bb01c8e9b59a5bf2a5f10fd948d27a7a0f00e23000924f144dd7d9c54ea6c1992305c2b6c8')

prepare() {
  cp -a "$_pkgname-$pkgver"{,-2}
}

build() {
  cd "$_pkgname-$pkgver-2"

  python2 setup.py build

  cd "$srcdir/$_pkgname-$pkgver"

  python setup.py build
}

package_python2-pem() {
  depends=('python2')

  cd "$_pkgname-$pkgver-2"

  python2 setup.py install --prefix=/usr --root="$pkgdir" -O1 --skip-build
}

package_python-pem() {
  depends=('python')

  cd "$_pkgname-$pkgver"

  python setup.py install --prefix=/usr --root="$pkgdir" -O1 --skip-build
}

