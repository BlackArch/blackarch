# This file is part of BlackArch Linux ( https://www.blackarch.org/ ).
# See COPYING for license details.

pkgname=python-dissect.target
_pkgname=${pkgname#python-}
pkgver=3.20.1.r13.gf2f50a5
pkgrel=1
pkgdesc='The Dissect module tying all other Dissect modules together. It provides a programming API and command line tools which allow easy access to various data sources inside disk images or file collections (a.k.a. targets).'
arch=('any')
groups=('blackarch' 'blackarch-forensic')
url='https://github.com/fox-it/dissect.target'
license=('AGPL-3.0-or-later')
depends=('python' 'python-asn1crypto' 'python-defusedxml'
         'python-dissect.archive' 'python-dissect.btrfs' 'python-dissect.cim'
         'python-dissect.clfs' 'python-dissect.cstruct' 'python-dissect.esedb'
         'python-dissect.etl' 'python-dissect.eventlog'
         'python-dissect.evidence' 'python-dissect.extfs' 'python-dissect.fat'
         'python-dissect.ffs' 'python-dissect.fve' 'python-dissect.hypervisor'
         'python-dissect.jffs' 'python-dissect.ntfs' 'python-dissect.ole'
         'python-dissect.regf' 'python-dissect.shellitem'
         'python-dissect.squashfs' 'python-dissect.sql'
         'python-dissect.thumbcache' 'python-dissect.util'
         'python-dissect.vmfs' 'python-dissect.volume' 'python-dissect.xfs'
         'python-flow.record' 'python-fusepy' 'python-pycryptodome'
         'python-ruamel-yaml' 'python-structlog' 'python-zstandard' 'ipython')
makedepends=('git' 'python-pip' 'python-build' 'python-installer'
             'python-wheel' 'python-setuptools' 'python-setuptools-scm')
options=(!emptydirs)
source=("git+https://github.com/fox-it/$_pkgname.git")
sha512sums=('SKIP')

pkgver() {
  cd $_pkgname

  ( set -o pipefail
    git describe --long --tags --abbrev=7 2>/dev/null |
      sed 's/\([^-]*-g\)/r\1/;s/-/./g' ||
    printf "%s.%s" "$(git rev-list --count HEAD)" \
      "$(git rev-parse --short=7 HEAD)"
  )
}

prepare() {
  git -C "$srcdir/$_pkgname" clean -dfx
}

build() {
  cd $_pkgname

  python -m build --wheel --no-isolation
}

package() {
  cd $_pkgname

  python -m installer --destdir="$pkgdir" dist/*.whl
}

