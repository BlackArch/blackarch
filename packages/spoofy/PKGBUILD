# This file is part of BlackArch Linux ( https://www.blackarch.org/ ).
# See COPYING for license details.

pkgname=spoofy
pkgver=v1.0.2.r1.g4dc2163
pkgrel=1
pkgdesc='Check if a list of domains can be spoofed based on SPF and DMARC records.'
arch=('any')
groups=('blackarch' 'blackarch-spoof')
url='https://github.com/MattKeeley/Spoofy'
license=('CC0-1.0')
depends=('python' 'python-colorama' 'python-dnspython' 'python-tldextract' 'python-pandas' 'python-openpyxl')
makedepends=('git')
source=("$pkgname::git+https://github.com/MattKeeley/$pkgname.git")
sha512sums=('SKIP')

pkgver() {
  cd $pkgname

  ( set -o pipefail
    git describe --long --tags --abbrev=7 2>/dev/null |
      sed 's/\([^-]*-g\)/r\1/;s/-/./g' ||
    printf "%s.%s" "$(git rev-list --count HEAD)" \
      "$(git rev-parse --short=7 HEAD)"
  )
}

package() {
  cd $pkgname

  install -dm 755 "$pkgdir/usr/bin"
  install -Dm 644 requirements.txt "$pkgdir/usr/share/$pkgname/requirements.txt"
  install -Dm 644 -t "$pkgdir/usr/share/doc/$pkgname/" *.md

  rm -rf *.md .gitignore

  cp -a * "$pkgdir/usr/share/$pkgname/"

  cat > "$pkgdir/usr/bin/$pkgname" << EOF
#!/bin/sh
exec python /usr/share/$pkgname/spoofy.py "\$@"
EOF

  chmod a+x "$pkgdir/usr/bin/$pkgname"
}

