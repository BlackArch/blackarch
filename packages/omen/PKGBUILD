# This file is part of BlackArch Linux ( http://blackarch.org ).
# See COPYING for license details.

pkgname='omen'
pkgver=15.78ce868
pkgrel=1
pkgdesc='Ordered Markov ENumerator - Password Guesser.'
groups=('blackarch' 'blackarch-cracker' 'blackarch-crypto')
arch=('x86_64' 'armv6h' 'armv7h' 'aarch64')
url='https://github.com/RUB-SysSec/OMEN'
license=('MIT')
depends=()
makedepends=('git')
source=('git+https://github.com/RUB-SysSec/OMEN.git')
sha1sums=('SKIP')

pkgver() {
  cd "$srcdir/OMEN"

  echo $(git rev-list --count HEAD).$(git rev-parse --short HEAD)
}

build() {
  cd "$srcdir/OMEN"

  make
}

package() {
  cd "$srcdir/OMEN"

  mkdir -p "$pkgdir/usr/share/omen"

  install -Dm755 alphabetCreator "$pkgdir/usr/bin/omen-alphabetcreator"
  install -Dm755 createNG "$pkgdir/usr/bin/omen-createng"
  install -Dm755 enumNG "$pkgdir/usr/bin/omen-enumng"
  install -Dm755 evalPW "$pkgdir/usr/bin/omen-evalpw"
  install -Dm644 -t "$pkgdir/usr/share/doc/omen/" README.md docs/*.md
  install -Dm644 docs/LICENSE "$pkgdir/usr/share/licenses/omen/LICENSE"

  rm README.md docs/*.md docs/LICENSE

  cp -a * "$pkgdir/usr/share/omen/"
}
