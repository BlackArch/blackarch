# This file is part of BlackArch Linux ( https://www.blackarch.org/ ).
# See COPYING for license details.

pkgname=swarm
pkgver=41.1713c1e
pkgrel=4
epoch=1
pkgdesc='A distributed penetration testing tool.'
groups=('blackarch' 'blackarch-scanner' 'blackarch-recon' 'blackarch-cracker'
        'blackarch-exploitation' 'blackarch-webapp')
arch=('any')
url='https://github.com/Arvin-X/swarm'
license=('GPL3')
depends=('python2' 'python2-ipy' 'python2-pymongo')
makedepends=('git')
source=("git+https://github.com/Arvin-X/$pkgname.git")
sha512sums=('SKIP')

pkgver() {
  cd $pkgname

  echo $(git rev-list --count HEAD).$(git rev-parse --short HEAD)
}

package() {
  cd $pkgname

  install -dm 755 "$pkgdir/usr/bin"
  install -dm 755 "$pkgdir/usr/share/$pkgname"

  install -Dm 644 -t "$pkgdir/usr/share/doc/$pkgname/" README.md docs/*
  install -Dm 644 LICENSE "$pkgdir/usr/share/licenses/$pkgname/LICENSE"

  rm -rf docs README.md LICENSE

  cp -a * "$pkgdir/usr/share/$pkgname/"

  cat > "$pkgdir/usr/bin/$pkgname" << EOF
#!/bin/sh
cd /usr/share/$pkgname
exec python2 $pkgname.py "\$@"
EOF

  chmod +x "$pkgdir/usr/bin/$pkgname"
}

