# This file is part of BlackArch Linux ( http://blackarch.org ).
# See COPYING for license details.

pkgname=brakeman
pkgver=4.3.1
pkgrel=1
pkgdesc='A static analysis security vulnerability scanner for Ruby on Rails applications'
groups=('blackarch' 'blackarch-code-audit' 'blackarch-exploitation' 'blackarch-scanner')
arch=('any')
url='https://brakemanscanner.org/'
license=('MIT')
depends=('ruby' 'ruby-bundler')
source=("https://github.com/presidentbeef/$pkgname/archive/v$pkgver.tar.gz")
install='brakeman.install'
sha512sums=('f7c59b53c64c090840d119971610f0996130a0a2eec0d277db56d4f23018db8b4b27d997b599b085e6d51ab4e63cb62419a5c30a7b4868dbef816fce5c7c56b8')

package() {
  cd "$pkgname-$pkgver"

  install -dm 755 "$pkgdir/usr/bin"
  install -dm 755 "$pkgdir/usr/share/$pkgname"
  install -dm 755 "$pkgdir/opt/$pkgname"

  install -Dm 644 MIT-LICENSE "$pkgdir/usr/share/licenses/$pkgname/LICENSE"
  install -Dm 644 README.md "$pkgdir/usr/share/docs/$pkgname"

  cp -a --no-preserve=ownership * "$pkgdir/opt/$pkgname"

  cat > "$pkgdir/usr/bin/$pkgname" << EOF
#!/bin/sh
cd /opt/$pkgname
exec ./bin/brakeman "\${@}"
EOF

  chmod a+x "$pkgdir/usr/bin/$pkgname"
}

