# This file is part of BlackArch Linux ( http://blackarch.org ).
# See COPYING for license details.

pkgname='appmon'
pkgver=139.dc25baf
pkgrel=1
pkgdesc='A runtime security testing & profiling framework for native apps on macOS, iOS & android and it is built using Frida.'
groups=('blackarch' 'blackarch-mobile' 'blackarch-scanner')
arch=('any')
url='https://github.com/dpnishant/appmon'
license=('GPL3')
depends=('python2' 'python2-flask' 'python2-lxml' 'python2-termcolor'
         'python2-frida' 'python2-dataset' 'python2-normality' 'python2-yaml')
makedepends=('git')
source=('git+https://github.com/dpnishant/appmon.git')
sha1sums=('SKIP')

pkgver() {
  cd "$srcdir/appmon"

  echo $(git rev-list --count HEAD).$(git rev-parse --short HEAD)
}

package() {
  cd "$srcdir/appmon"

  mkdir -p "$pkgdir/usr/bin"
  mkdir -p "$pkgdir/usr/share/appmon"

  install -Dm644 README.md "$pkgdir/usr/share/doc/appmon/README.md"
  install -Dm644 LICENSE "$pkgdir/usr/share/licenses/appmon/LICENSE"

  rm README.md LICENSE

  cp -a * "$pkgdir/usr/share/appmon"

  cat > "$pkgdir/usr/bin/appmon" << EOF
#!/bin/sh
cd /usr/share/appmon
exec python2 appmon.py "\${@}"
EOF

  chmod +x "$pkgdir/usr/bin/appmon"
}
