# This file is part of BlackArch Linux ( https://www.blackarch.org/ ).
# See COPYING for license details.

pkgname=appmon
pkgver=164.974418b
pkgrel=1
pkgdesc='A runtime security testing & profiling framework for native apps on macOS, iOS & android and it is built using Frida.'
groups=('blackarch' 'blackarch-mobile' 'blackarch-scanner')
arch=('any')
url='https://github.com/dpnishant/appmon'
license=('GPL3')
depends=('python2' 'python2-flask' 'python2-lxml' 'python2-termcolor'
         'python2-frida' 'python2-dataset' 'python2-normality' 'python2-yaml'
         'python2-htmlentities' 'python2-werkzeug' 'python2-sqlalchemy'
         'python2-six' 'python2-mako' 'python2-normality' 'python2-jinja'
         'python2-markupsafe' 'python2-click' 'python2-chardet'
         'python2-dateutil' 'python2-alembic' 'python2-banal' 'python2-editor'
         'python2-itsdangerous')
makedepends=('git')
source=("git+https://github.com/dpnishant/$pkgname.git")
sha512sums=('SKIP')

pkgver() {
  cd $pkgname

  echo $(git rev-list --count HEAD).$(git rev-parse --short HEAD)
}

package() {
  cd $pkgname

  install -dm 755 "$pkgdir/usr/bin"
  install -dm 755 "$pkgdir/usr/share/$pkgname"

  install -Dm 644 -t "$pkgdir/usr/share/doc/$pkgname/" README.md
  install -Dm 644 LICENSE "$pkgdir/usr/share/licenses/$pkgname/LICENSE"

  rm README.md LICENSE

  cp -a * "$pkgdir/usr/share/$pkgname"

  cat > "$pkgdir/usr/bin/$pkgname" << EOF
#!/bin/sh
cd /usr/share/$pkgname
exec python2 $pkgname.py "\$@"
EOF

  chmod +x "$pkgdir/usr/bin/$pkgname"
}

