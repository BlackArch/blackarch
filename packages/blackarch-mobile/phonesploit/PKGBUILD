# This file is part of BlackArch Linux ( https://www.blackarch.org/ ).
# See COPYING for license details.

pkgname=phonesploit
pkgver=51.0193f9e
pkgrel=3
pkgdesc='Adb exploiting tools.'
arch=('any')
groups=('blackarch' 'blackarch-mobile')
url='https://github.com/metachar/PhoneSploit'
license=('custom:unknown')
depends=('python2' 'python2-colorama' 'android-tools')
makedepends=('git')
source=("git+https://github.com/metachar/$pkgname.git")
sha512sums=('SKIP')

prepare() {
  cd $pkgname

  rm adb.rar
}


pkgver() {
  cd $pkgname

  echo $(git rev-list --count HEAD).$(git rev-parse --short HEAD)
}

package() {
  cd $pkgname

  install -dm 755 "$pkgdir/usr/bin"
  install -dm 755 "$pkgdir/usr/share/$pkgname"

  install -Dm 644 -t "$pkgdir/usr/share/doc/$pkgname/" README.md

  rm README.md

  cp -a * "$pkgdir/usr/share/$pkgname/"

  cat > "$pkgdir/usr/bin/$pkgname" << EOF
#!/bin/sh
exec python2 /usr/share/$pkgname/main_linux.py "\$@"
EOF

  chmod +x "$pkgdir/usr/bin/$pkgname"
}

