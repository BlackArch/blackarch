# This file is part of BlackArch Linux ( http://blackarch.org ).
# See COPYING for license details.

pkgname=python2-libbde-python
pkgver=242.9a14e73
pkgrel=1
pkgdesc='Library and tools to access the BitLocker Drive Encryption (BDE) encrypted volumes.'
arch=('x86_64' 'armv6h' 'armv7h' 'aarch64')
url='https://github.com/libyal/libbde'
license=('LGPL3')
depends=('python2')
makedepends=('git')
source=('git+https://github.com/libyal/libbde.git')
sha512sums=('SKIP')

pkgver() {
  cd "$srcdir/libbde"

  echo $(git rev-list --count HEAD).$(git rev-parse --short HEAD)
}

build() {
  cd "$srcdir/libbde"

  ./synclibs.sh --use-head && ./autogen.sh

  python2 setup.py build
}

package() {
  cd "$srcdir/libbde"

  python2 setup.py install --root="$pkgdir" --prefix=/usr --optimize=1
}
