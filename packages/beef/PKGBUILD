pkgname=beef
pkgver=0.4.4.7.43.g41bfb8e
pkgrel=1
groups=(blackarch blackarch-exploitation)
pkgdesc="BeEF, the Browser Exploitation Framework - it is a penetration testing tool that focuses on the web browser"
arch=('i686' 'x86_64')
url=('http://beefproject.com/')
license=('Apache')
makedepends=('git')
depends=('ruby' 'ruby-sqlite3' 'ruby-bundler>=1.3-1' 'ruby-thin>=1.5.1-1' 'ruby-sinatra>=1.4.4-1' 'ruby-ansi' 'ruby-term-ansicolor' 'ruby-dm-core>=1.2.1-1' 'ruby-json' 'ruby-data_objects>=0.10.13-1' 'ruby-dm-sqlite-adapter' 'ruby-parseconfig' 'ruby-erubis' 'ruby-dm-migrations' 'ruby-msfrpc-client' 'ruby-eventmachine>=1.0.3-1' 'ruby-em-websocket>=0.3.8-1' 'ruby-addressable' 'ruby-rack-protection>=1.5.1-1' 'ruby-dm-do-adapter' 'ruby-do_sqlite3>=0.10.13-1' 'ruby-librex' 'ruby-jsmin' 'ruby-msgpack' 'ruby-twitter' 'ruby-uglifier>=2.3.0-1' 'ruby-libv8>=3.16.14.3-1' 'ruby-therubyracer>=0.11.4' 'ruby-rubyzip>=1.0.0-1' 'ruby-daemons>=1.1.9-1' 'ruby-tilt' 'ruby-simple_oauth>=0.2.0-1' 'ruby-faraday' 'ruby-multi_json' 'ruby-railties' 'ruby-execjs' 'ruby-actionpack' 'ruby-activesupport' 'ruby-rack-ssl' 'ruby-thor')
_gitname=('beef')
source=("$_gitname"::'git://github.com/beefproject/beef.git')
md5sums=('SKIP')

pkgver() {
  cd ${_gitname}
  git describe | sed 's/^v//;s/-/./g;s/_/./g;s/^beef.//;'
}

package() {
  cd ${_gitname}

  install -d -m755 ${pkgdir}/usr/bin/
  install -d -m755 ${pkgdir}/usr/share/${_gitname}

  git clone ${srcdir}/${_gitname} ${pkgdir}/usr/share/${_gitname}

  ## blackarch fixes applied to the beef Gemfile
  # we're CHANGING => gem "eventmachine", "0.12.10"
  # TO             => gem "eventmachine", ">=0.12.10"
  sed -i 's|0.12.10|>=0.12.10|' ${pkgdir}/usr/share/${_gitname}/Gemfile
  # changing the sinatra requirement from 1.4.2 to >=1.4.4
  sed -i 's|1.4.2|>=1.4.4|' ${pkgdir}/usr/share/${_gitname}/Gemfile
  # changing uglifier gem requirement from ~> 2.2.1 TO >=1.2.4
  sed -i 's|~> 2.2.1|>=2.3.0|' ${pkgdir}/usr/share/${_gitname}/Gemfile
  # adding a 'ruby-rack-protection' gem requirement >=1.5.1
  sed -i '/gem "rack"/agem "rack-protection", ">=1.5.1"' ${pkgdir}/usr/share/${_gitname}/Gemfile

  ## NOTE: to build archlinux package 'ruby-therubyracer' v0.12.0 you need to have a working/functional ruby-libv8 3.16.14.3-1

  # Make joint script.
  cat > "$pkgdir/usr/bin/beef" <<EOF
#!/bin/sh
cd /usr/share/beef
ruby beef "\$@"
cd -
EOF
  chmod +x "$pkgdir/usr/bin/beef"
}
