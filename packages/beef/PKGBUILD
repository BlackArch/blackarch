# This file is part of BlackArch Linux ( http://blackarch.org ).
# See COPYING for license details.

pkgname='beef'
pkgver=3126.445cbb02
pkgrel=1
epoch=1
groups=('blackarch' 'blackarch-exploitation')
pkgdesc='The Browser Exploitation Framework that focuses on the web browser'
arch=('any')
url='http://beefproject.com/'
install='beef.install'
license=('Apache')
makedepends=('git')
depends=('ruby' 'ruby-bundler' 'sqlite' 'python2' 'libxslt')
source=('git+http://github.com/beefproject/beef.git')
sha1sums=('SKIP')

pkgver() {
  cd "$srcdir/beef"

  echo $(git rev-list --count HEAD).$(git rev-parse --short HEAD)
}

package() {
  cd "$srcdir/beef"

  mkdir -p "$pkgdir/usr/bin"
  mkdir -p "$pkgdir/usr/share/beef"

  install -Dm644 -t "$pkgdir/usr/share/doc/beef" README.md VERSION INSTALL.txt
  install -Dm644 doc/COPYING "$pkgdir/usr/share/licenses/beef/COPYING"

  rm -rf doc README.md VERSION INSTALL.txt install

  cp -a * "$pkgdir/usr/share/beef"

  cat > "$pkgdir/usr/bin/beef" << EOF
#!/bin/sh
cd /usr/share/beef
exec ruby beef "\${@}"
EOF

  chmod +x "$pkgdir/usr/bin/beef"
}
