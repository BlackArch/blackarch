# This file is part of BlackArch Linux ( http://blackarch.org ).
# See COPYING for license details.

pkgname='beef'
pkgver=2822.7e8e22a9
pkgrel=1
epoch=1
groups=('blackarch' 'blackarch-exploitation')
pkgdesc='The Browser Exploitation Framework that focuses on the web browser'
arch=('any')
url='http://beefproject.com/'
install='beef.install'
license=('Apache')
makedepends=('git')
depends=('ruby2.3' 'ruby2.3-bundler' 'sqlite' 'python2')
source=('git+http://github.com/beefproject/beef.git')
sha1sums=('SKIP')

pkgver() {
  cd "$srcdir/beef"

  echo $(git rev-list --count HEAD).$(git rev-parse --short HEAD)
  #git describe | sed 's/^v//;s/-/./g;s/_/./g;s/^beef.//'
}

package() {
  cd "$srcdir/beef"

  mkdir -p "$pkgdir/usr/bin"

  # The .git/ overhead is tiny and this allows users to update with git.
  git clone "$srcdir/beef" "$pkgdir/usr/share/beef"

  rm "$pkgdir/usr/share/beef/install"

  cat > "$pkgdir/usr/bin/beef" << EOF
#!/bin/sh
cd /usr/share/beef
exec ruby-2.3 beef "\${@}"
EOF

  chmod +x "$pkgdir/usr/bin/beef"
}
