# This file is part of BlackArch Linux ( https://www.blackarch.org/ ).
# See COPYING for license details.

pkgname=semgrep
pkgver=1.55.2
_pyver=3.11
_py=cp38.cp39.cp310.cp311.py37.py38.py39.py310.py311
pkgrel=1
pkgdesc='Lightweight static analysis for many languages.'
arch=('x86_64')
groups=('blackarch' 'blackarch-code-audit')
url='https://pypi.org/project/semgrep/#files'
license=('LGPL')
depends=('python' 'python-attrs' 'python-boltons' 'python-colorama'
         'python-click' 'python-click-option-group' 'python-glom'
         'python-requests' 'python-ruamel-yaml' 'python-tqdm' 'python-packaging'
         'python-jsonschema' 'python-wcmatch' 'python-peewee'
         'python-defusedxml' 'python-urllib3' 'python-typing_extensions'
         'python-lsp-jsonrpc' 'python-tomli' 'python-rich' 'python-exceptiongroup')
makedepends=('python-build' 'python-pip' 'python-wheel')
replaces=('python-semgrep')
source=("https://files.pythonhosted.org/packages/source/${pkgname::1}/$pkgname/$pkgname-$pkgver.tar.gz"
        "https://files.pythonhosted.org/packages/$_py/${pkgname::1}/$pkgname/${pkgname//-/_}-$pkgver-$_py-none-any.whl")
sha512sums=('fd3ff2fc846de44dfbab04530d07a513673f08114b6c79269f5277bd3724adecc8a68060f6c3375f9dbe14dca69b94706d5943ebb7bf13892e4e66ed9b2ab8b4'
            'f33dc6369717c190ffe1170ff85b5a6494e1d86e99f992e2c4ed53cca5047e29f604e081a656df2233ca1155a55eb4c4dfc80a2827d6aea8d2562cdab52836a1')

build() {
  cd "$pkgname-$pkgver"

  export SEMGREP_CORE_BIN="$srcdir/$pkgname-$pkgver.data/purelib/$pkgname/bin/$pkgname-core"

  python -m build --wheel --outdir="$startdir/dist"
}

package() {
  cd "$pkgname-$pkgver"

  pip install \
    --verbose \
    --disable-pip-version-check \
    --no-warn-script-location \
    --ignore-installed \
    --no-compile \
    --no-deps \
    --root="$pkgdir" \
    --prefix=/usr \
    --no-index \
    --find-links="file://$startdir/dist" \
    "$pkgname"

  install -Dm 555 \
    "$srcdir/$pkgname-$pkgver.data/purelib/$pkgname/bin/$pkgname-core" \
    "$pkgdir/usr/lib/python$_pyver/site-packages/$pkgname/bin/$pkgname-core"
  strip "$pkgdir/usr/lib/python$_pyver/site-packages/$pkgname/bin/$pkgname-core"

  # semgrep sg binary conflicts with shadow sg binary
  # https://github.com/semgrep/semgrep/issues/9571
  mv "$pkgdir/usr/bin/sg" "$pkgdir/usr/bin/$pkgname-sg"
}

