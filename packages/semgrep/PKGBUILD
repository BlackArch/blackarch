# This file is part of BlackArch Linux ( https://www.blackarch.org/ ).
# See COPYING for license details.

pkgname=semgrep
# Don't bump version to 1.38.3 until https://github.com/returntocorp/semgrep/issues/8641 has been fixed
pkgver=1.38.2
_pyver=3.11
_py=cp37.cp38.cp39.cp310.cp311.py37.py38.py39.py310.py311
pkgrel=1
pkgdesc='Lightweight static analysis for many languages.'
arch=('x86_64')
groups=('blackarch' 'blackarch-code-audit')
url='https://pypi.org/project/semgrep/#files'
license=('LGPL')
depends=('python' 'python-attrs' 'python-boltons' 'python-colorama'
         'python-click' 'python-click-option-group' 'python-glom'
         'python-requests' 'python-ruamel-yaml' 'python-tqdm' 'python-packaging'
         'python-jsonschema' 'python-wcmatch' 'python-peewee'
         'python-defusedxml' 'python-urllib3' 'python-typing_extensions'
         'python-lsp-jsonrpc' 'python-tomli' 'python-rich')
makedepends=('python-build' 'python-pip' 'python-wheel')
replaces=('python-semgrep')
source=("https://files.pythonhosted.org/packages/source/${pkgname::1}/$pkgname/$pkgname-$pkgver.tar.gz"
        "https://files.pythonhosted.org/packages/$_py/${pkgname::1}/$pkgname/${pkgname//-/_}-$pkgver-$_py-none-any.whl")
sha512sums=('8978f01b66d1f743f86bd5810092f30a11183c54c26e831e6e5a115cc52a54cd524ece42dce2d246c49b1b04710ddc1e14afd544ecdce883a557f3e5f4ce1aa9'
            '4f00e548defd1b33630c496e947b7b49b637039b35be29dae83cfd9ceaa1d7ac86ab958393dc0a5a4060987680c7a39fba83b630234d395a781ff335fb6ef3f5')

build() {
  cd "$pkgname-$pkgver"

  export SEMGREP_CORE_BIN="$srcdir/$pkgname-$pkgver.data/purelib/semgrep/bin/semgrep-core"

  python -m build --wheel --outdir="$startdir/dist"
}

package() {
  cd "$pkgname-$pkgver"

  pip install \
    --verbose \
    --disable-pip-version-check \
    --no-warn-script-location \
    --ignore-installed \
    --no-compile \
    --no-deps \
    --root="$pkgdir" \
    --prefix=/usr \
    --no-index \
    --find-links="file://$startdir/dist" \
    "$pkgname"

  install -Dm 555 \
    "$srcdir/$pkgname-$pkgver.data/purelib/semgrep/bin/semgrep-core" \
    "$pkgdir/usr/lib/python$_pyver/site-packages/$pkgname/bin/semgrep-core"
  strip "$pkgdir/usr/lib/python$_pyver/site-packages/$pkgname/bin/semgrep-core"
}

