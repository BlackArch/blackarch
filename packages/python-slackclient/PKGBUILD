# This file is part of BlackArch Linux ( https://www.blackarch.org/ ).
# See COPYING for license details.

pkgbase=python-slackclient
pkgname=('python2-slackclient' 'python-slackclient')
_pkgname=slackclient
pkgver=1.0.9
pkgrel=1
epoch=1
pkgdesc='Slack API clients for Web API and RTM API.'
arch=('any')
url='https://pypi.org/project/slackclient/#files'
license=('MIT')
makedepends=('python2-setuptools' 'python-setuptools')
source=("https://files.pythonhosted.org/packages/74/ba/f462c817ecc3966252d9320f1bda2b4b81d2b3e2a8e845d8d3229d013d81/$_pkgname-$pkgver.tar.gz")
sha512sums=('8363f15d01aab0b1e4e02ea8103c124461b18f9e7007444fa7c6b6433ad85f81d1148e07d525f877d79f8b47ca09cf48bfa7f9b359bcae15996dc35e7e2ae2c5')

prepare() {
  cp -a "$_pkgname-$pkgver"{,-2}
}

build() {
  cd "$_pkgname-$pkgver-2"

  python2 setup.py build

  cd "$srcdir/$_pkgname-$pkgver"

  python setup.py build
}

package_python2-slackclient() {
  depends=('python2' 'python2-six' 'python2-requests'
           'python2-websocket-client')

  cd "$_pkgname-$pkgver-2"

  python2 setup.py install --prefix=/usr --root="$pkgdir" --optimize=1 \
    --skip-build
}

package_python-slackclient() {
  depends=('python' 'python-six' 'python-requests' 'python-websocket-client')

  cd "$_pkgname-$pkgver"

  python setup.py install --prefix=/usr --root="$pkgdir" --optimize=1 \
    --skip-build
}

