# This file is part of BlackArch Linux ( https://www.blackarch.org/ ).
# See COPYING for license details.

pkgbase=python-maclookup
pkgname=('python2-maclookup' 'python-maclookup')
_pkgname=maclookup
pkgver=1.0.3
pkgrel=3
pkgdesc='Python client library for macaddress.io API.'
arch=('any')
url='https://pypi.org/project/maclookup/#files'
license=('MIT')
makedepends=('python2-setuptools' 'python-setuptools')
source=("https://files.pythonhosted.org/packages/d1/59/59bb372710df70d63ccf9e30f44640eec2bd2da4e6d0952daca14713f02f/$_pkgname-$pkgver.tar.gz")
sha512sums=('20dfc1f9ac926f0cc50a8abb49fa5a2665e7deb20109ff082be761a55390e72cc3a2281549900a1641ed2986bed27e5d5957e6d693d701dc969ddd8a04cc80e3')

prepare() {
  cp -a "$_pkgname-$pkgver"{,-2}
}

build() {
  cd "$_pkgname-$pkgver-2"

  python2 setup.py build

  cd "$srcdir/$_pkgname-$pkgver"

  python setup.py build
}

package_python2-maclookup() {
  depends=('python2' 'python2-future' 'python2-dateutil')

  cd "$_pkgname-$pkgver-2"

  python2 setup.py install --prefix=/usr --root="$pkgdir" --optimize=1 \
    --skip-build
}

package_python-maclookup() {
  depends=('python' 'python-future' 'python-dateutil')

  cd "$_pkgname-$pkgver"

  python setup.py install --prefix=/usr --root="$pkgdir" --optimize=1 \
    --skip-build
}

