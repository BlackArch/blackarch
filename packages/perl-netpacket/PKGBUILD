pkgname=perl-netpacket
pkgver=1.2.0
pkgrel=1
pkgdesc="Assemble/disassemble network packets at the protocol level"
arch=('i686' 'x86_64')
url='http://search.cpan.org/dist/NetPacket/'
license=('artistic2')
makedepends=('perl-module-build>=0.360.100')
provides=('perl-netpacket-arp=0.43.1' 'perl-netpacket-ethernet=0.43.1'
          'perl-netpacket-icmp=0.43.1' 'perl-netpacket-icmpv6=0.43.1'
          'perl-netpacket-igmp=0.43.1' 'perl-netpacket-ip=0.43.1'
          'perl-netpacket-ipv6=0.43.1' 'perl-netpacket-pflog=0.43.1'
          'perl-netpacket-tcp=0.43.1' 'perl-netpacket-udp=0.43.1')
options=(!emptydirs)
source=('http://search.cpan.org/CPAN/authors/id/Y/YA/YANICK/NetPacket-1.2.0.tar.gz')
md5sums=('c3a09b48bc55cf366b594e30860d0a82')

build() {
  cd "$srcdir/NetPacket-$pkgver"

  PERL_MM_USE_DEFAULT=1 perl Build.PL INSTALLDIRS=vendor
  ./Build
}

package() {
  cd "$srcdir/NetPacket-$pkgver"

  ./Build install destdir=${pkgdir}

  # Remove perllocal.pod and .packlist.
  find ${pkgdir} -name perllocal.pod -delete
  find ${pkgdir} -name .packlist -delete
}
