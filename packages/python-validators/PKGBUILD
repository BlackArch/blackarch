# This file is part of BlackArch Linux ( https://www.blackarch.org/ ).
# See COPYING for license details.

pkgbase=python-validators
pkgname=('python2-validators' 'python-validators')
pkgver=0.12.6
pkgrel=1
pkgdesc='Python Data Validation for Humans.'
arch=('any')
url='https://pypi.org/project/validators/#files'
license=('BSD')
makedepends=('python2-setuptools' 'python-setuptools')
source=("https://files.pythonhosted.org/packages/8f/cd/1571ece4bf93e02143449417d244daa89dfc46288110b096b81e84aa6ddd/validators-0.12.6.tar.gz")
sha512sums=('db2fb96848a28790a56a8d329fb22b048d464ccb506db30c34193baf5657e65f0acc896215263427fd2df6f0e25dd4c468528ae0a56d9ca465f6fcf6c28951cf')

prepare() {
  cp -a "validators-$pkgver" "validators2-$pkgver"
}

build() {
  cd "validators2-$pkgver"

  python2 setup.py build

  cd "$srcdir/validators-$pkgver"

  python setup.py build
}

package_python2-validators() {
  depends=('python2-six' 'python2-decorator')

  cd "validators2-$pkgver"

  python2 setup.py install --prefix=/usr --root="$pkgdir" --optimize=1 \
    --skip-build
}

package_python-validators() {
  depends=('python-six' 'python-decorator')

  cd "validators-$pkgver"

  python setup.py install --prefix=/usr --root="$pkgdir" --optimize=1 \
    --skip-build
}

