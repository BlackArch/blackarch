# This file is part of BlackArch Linux ( https://www.blackarch.org/ ).
# See COPYING for license details.

pkgbase=python-validators
pkgname=('python2-validators' 'python-validators')
_pkgname=validators
pkgver=0.21.2
pkgrel=1
pkgdesc='Python Data Validation for Humans.'
arch=('any')
url='https://pypi.org/project/validators/#files'
license=('BSD')
makedepends=('python2-setuptools' 'python-setuptools')
source=("https://files.pythonhosted.org/packages/source/${_pkgname::1}/$_pkgname/$_pkgname-$pkgver.tar.gz")
sha512sums=('558630849f6c81b13f7043f5713c160f0cdcc94c7d90c9e13c97ab972c1bf74df9a9d889b50238f7239c6885a34549a8a209b3a325d62291d93073b9cad18d08')

prepare() {
  cp -a "$_pkgname-$pkgver"{,-2}
}

build() {
  cd "$_pkgname-$pkgver-2"

  python2 setup.py build

  cd "$srcdir/$_pkgname-$pkgver"

  python setup.py build
}

package_python2-validators() {
  depends=('python2' 'python2-six' 'python2-decorator')

  cd "$_pkgname-$pkgver-2"

  python2 setup.py install --prefix=/usr --root="$pkgdir" --optimize=1 \
    --skip-build
}

package_python-validators() {
  depends=('python' 'python-six' 'python-decorator')

  cd "$_pkgname-$pkgver"

  python setup.py install --prefix=/usr --root="$pkgdir" --optimize=1 \
    --skip-build
}

