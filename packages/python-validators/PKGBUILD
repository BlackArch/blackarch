# This file is part of BlackArch Linux ( https://www.blackarch.org/ ).
# See COPYING for license details.

pkgbase=python-validators
pkgname=('python2-validators' 'python-validators')
_pkgname=validators
pkgver=0.16.0
pkgrel=1
pkgdesc='Python Data Validation for Humans.'
arch=('any')
url='https://pypi.org/project/validators/#files'
license=('BSD')
makedepends=('python2-setuptools' 'python-setuptools')
source=("https://files.pythonhosted.org/packages/c0/74/66419c1dad0c4e66b0c79f28d83f1259ed8648f35d8de10de8acff474f0f/$_pkgname-$pkgver.tar.gz")
sha512sums=('06b82bbe91abad7524e400872b396e5bf51e6edbc8373d7de773bbde6e8d4646b215adacf45b0aa17881070e6490e67d7832971f820d365dabbec9d1f7b6068c')

prepare() {
  cp -a "$_pkgname-$pkgver"{,-2}
}

build() {
  cd "$_pkgname-$pkgver-2"

  python2 setup.py build

  cd "$srcdir/$_pkgname-$pkgver"

  python setup.py build
}

package_python2-validators() {
  depends=('python2' 'python2-six' 'python2-decorator')

  cd "$_pkgname-$pkgver-2"

  python2 setup.py install --prefix=/usr --root="$pkgdir" --optimize=1 \
    --skip-build
}

package_python-validators() {
  depends=('python' 'python-six' 'python-decorator')

  cd "$_pkgname-$pkgver"

  python setup.py install --prefix=/usr --root="$pkgdir" --optimize=1 \
    --skip-build
}

