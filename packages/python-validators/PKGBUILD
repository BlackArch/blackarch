# This file is part of BlackArch Linux ( https://www.blackarch.org/ ).
# See COPYING for license details.

pkgbase=python-validators
pkgname=('python2-validators' 'python-validators')
pkgver=0.12.5
pkgrel=3
pkgdesc='Python Data Validation for Humans.'
arch=('any')
url='https://pypi.org/project/validators/#files'
license=('BSD')
makedepends=('python2-setuptools' 'python-setuptools')
source=("https://files.pythonhosted.org/packages/36/90/afec6009db4d7dedf1e3d921f09bc8b2314c7ce464d84e381e218e279cb3/validators-0.12.5.tar.gz")
sha512sums=('d2320cf985d9b655d7c8537ea5555d2d0134d668dcccf6c5e878dbc0ac47bcea1a5975c970a36d64dfb669194ec9de1bb9f601779dc0feb073cc1eed55088211')

prepare() {
  cp -a "validators-$pkgver" "validators2-$pkgver"
}

build() {
  cd "validators2-$pkgver"

  python2 setup.py build

  cd "$srcdir/validators-$pkgver"

  python setup.py build
}

package_python2-validators() {
  depends=('python2-six' 'python2-decorator')

  cd "validators2-$pkgver"

  python2 setup.py install --prefix=/usr --root="$pkgdir" --optimize=1 \
    --skip-build
}

package_python-validators() {
  depends=('python-six' 'python-decoratorr')

  cd "validators-$pkgver"

  python setup.py install --prefix=/usr --root="$pkgdir" --optimize=1 \
    --skip-build
}

