# This file is part of BlackArch Linux ( https://www.blackarch.org/ ).
# See COPYING for license details.

pkgbase=python-validators
pkgname=('python2-validators' 'python-validators')
_pkgname=validators
pkgver=0.18.0
pkgrel=1
pkgdesc='Python Data Validation for Humans.'
arch=('any')
url='https://pypi.org/project/validators/#files'
license=('BSD')
makedepends=('python2-setuptools' 'python-setuptools')
source=("https://files.pythonhosted.org/packages/8a/29/8ca6f7a2f354265b133653c6fbdce1865d4fabcc2d3503f10af4704efd33/$_pkgname-$pkgver.tar.gz")
sha512sums=('f735798a81b817bc1277acdc694d8259dc518fec3dea5f8edbe35bc7f0e919f595ab3757bc07eddf01523217ad2e5039b7fe5833b2306dbba90261e46d8cc3e0')

prepare() {
  cp -a "$_pkgname-$pkgver"{,-2}
}

build() {
  cd "$_pkgname-$pkgver-2"

  python2 setup.py build

  cd "$srcdir/$_pkgname-$pkgver"

  python setup.py build
}

package_python2-validators() {
  depends=('python2' 'python2-six' 'python2-decorator')

  cd "$_pkgname-$pkgver-2"

  python2 setup.py install --prefix=/usr --root="$pkgdir" --optimize=1 \
    --skip-build
}

package_python-validators() {
  depends=('python' 'python-six' 'python-decorator')

  cd "$_pkgname-$pkgver"

  python setup.py install --prefix=/usr --root="$pkgdir" --optimize=1 \
    --skip-build
}

