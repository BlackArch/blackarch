# This file is part of BlackArch Linux ( https://www.blackarch.org/ ).
# See COPYING for license details.

pkgbase=python-validators
pkgname=('python2-validators' 'python-validators')
_pkgname=validators
pkgver=0.14.0
pkgrel=1
pkgdesc='Python Data Validation for Humans.'
arch=('any')
url='https://pypi.org/project/validators/#files'
license=('BSD')
makedepends=('python2-setuptools' 'python-setuptools')
source=("https://files.pythonhosted.org/packages/b2/b7/5fc43b7dba2a35362b2f5ce39dbee835c48898e2458910c3af810240fbaa/$_pkgname-$pkgver.tar.gz")
sha512sums=('ec9153cb817f25e40221502484cdbdb9cf1566fe8445f7b822d4a80325154aca5a665be452f6251351682d36167c5a6bb02c3259d6067f4f51fa1d01aaa44a4e')

prepare() {
  cp -a "$_pkgname-$pkgver"{,-2}
}

build() {
  cd "$_pkgname-$pkgver-2"

  python2 setup.py build

  cd "$srcdir/$_pkgname-$pkgver"

  python setup.py build
}

package_python2-validators() {
  depends=('python2' 'python2-six' 'python2-decorator')

  cd "$_pkgname-$pkgver-2"

  python2 setup.py install --prefix=/usr --root="$pkgdir" --optimize=1 \
    --skip-build
}

package_python-validators() {
  depends=('python' 'python-six' 'python-decorator')

  cd "$_pkgname-$pkgver"

  python setup.py install --prefix=/usr --root="$pkgdir" --optimize=1 \
    --skip-build
}

