pkgname=crunch
pkgver=3.4
groups=('blackarch' 'blackarch-password-attacks')
pkgrel=2
pkgdesc="A wordlist generator for all combinations/permutations of a given character set"
arch=('i686' 'x86_64')
url='http://sourceforge.net/projects/crunch-wordlist/'
license=('GPL')
depends=('glibc')
source=("http://downloads.sourceforge.net/crunch-wordlist/$pkgname-$pkgver.tgz"
        "http://downloads.sourceforge.net/crunch-wordlist/readme.txt")
md5sums=('3f658a60d8227025df12cfb4ff4b9e15'
         'd448c4d06ac8879ae5a0ad546dd322ce')

build() {
  cd "$srcdir/$pkgname-$pkgver"
  make
}

package() {
  cd "$srcdir/$pkgname-$pkgver"

  # Bin.
  install -Dm755  $pkgname \
    "$pkgdir/usr/bin/$pkgname"

  # Data.
  install -Dm644 charset.lst \
    "$pkgdir/usr/share/$pkgname/charset.lst"

  # Docs.
  install -Dm644 ../readme.txt \
    "$pkgdir/usr/share/doc/$pkgname/readme.txt"

  # Man.
  install -Dm644  $pkgname.1 \
    "$pkgdir/usr/share/man/man1/$pkgname.1"
}
