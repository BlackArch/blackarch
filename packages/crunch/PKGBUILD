pkgname=crunch
pkgver=3.4
pkgrel=3
groups=('blackarchlinux' 'blackarchlinux-automation' 'blackarchlinux-misc')
pkgdesc="A wordlist generator for all combinations/permutations of a given character set"
arch=('i686' 'x86_64' 'armv6h' 'armv7h')
url='http://sourceforge.net/projects/crunch-wordlist/'
license=('GPL')
depends=('glibc')
source=("http://downloads.sourceforge.net/crunch-wordlist/crunch-$pkgver.tgz")
md5sums=('3f658a60d8227025df12cfb4ff4b9e15')

build() {
  cd "$srcdir/crunch-$pkgver"
  make
}

package() {
  cd "$srcdir/crunch-$pkgver"

  # Base directories.
  install -dm755 "$pkgdir/usr/bin"
  install -dm755 "$pkgdir/usr/share/crunch"
  install -dm755 "$pkgdir/usr/share/man/man1"

  # Bin.
  install -m755 crunch "$pkgdir/usr/bin/crunch"

  # Data.
  install -m644 charset.lst "$pkgdir/usr/share/crunch/charset.lst"

  # Man.
  install -m644 crunch.1 "$pkgdir/usr/share/man/man1/crunch.1"
}
