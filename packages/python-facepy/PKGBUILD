# This file is part of BlackArch Linux ( https://www.blackarch.org/ ).
# See COPYING for license details.

pkgbase=python-facepy
pkgname=('python2-facepy' 'python-facepy')
_pkgname=facepy
pkgver=1.0.11
pkgrel=1
pkgdesc="Makes it really easy to interact with Facebook's Graph API."
arch=('any')
url='https://github.com/jgorset/facepy'
license=('MIT')
makedepends=('python2-setuptools' 'python-setuptools')
source=("https://files.pythonhosted.org/packages/cc/c9/4460ac369df2c43ca50d8b72536005db9156e3018749fe10bde75586765d/$_pkgname-$pkgver.tar.gz")
sha512sums=('dd0d15097c7990a89dad6503c1c9aae65389cae40448f645c0bedd3092d4de5e41b2e99b3076ac7408418b846124cf939d79c9ca096e5ca4f184fba536fc3708')

prepare() {
  cp -a "$_pkgname-$pkgver"{,-2}
}

build() {
  cd "$_pkgname-$pkgver-2"

  python2 setup.py build

  cd "$srcdir/$_pkgname-$pkgver"

  python setup.py build
}

package_python2-facepy() {
  depends=('python2' 'python2-requests' 'python2-six')

  cd "$_pkgname-$pkgver-2"

  python2 setup.py install --prefix=/usr --root="$pkgdir" --optimize=1 \
    --skip-build
}

package_python-facepy() {
  depends=('python' 'python-requests' 'python-six')

  cd "$_pkgname-$pkgver"

  python setup.py install --prefix=/usr --root="$pkgdir" --optimize=1 \
    --skip-build
}

