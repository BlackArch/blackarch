# This file is part of BlackArch Linux ( https://www.blackarch.org/ ).
# See COPYING for license details.

pkgbase=python-facepy
pkgname=('python2-facepy' 'python-facepy')
_pkgname=facepy
pkgver=1.0.10
pkgrel=3
pkgdesc="Makes it really easy to interact with Facebook's Graph API."
arch=('any')
url='https://github.com/jgorset/facepy'
license=('MIT')
makedepends=('python2-setuptools' 'python-setuptools')
source=("https://files.pythonhosted.org/packages/63/3b/365a752d3fdfd7a580d9cbee85a7cecdc0d403d470c0852c47b3ee95c3e1/$_pkgname-$pkgver.tar.gz")
sha512sums=('532a3b897b1eb8fa7242d0056dfadba8b449a7eb335a68a28b6f3f4a5f7fa0a32c9428eb525493a335f5400f34f7e5a4c5136bcf57921aef0bbe0192b15c1378')

prepare() {
  cp -a "$_pkgname-$pkgver"{,-2}
}

build() {
  cd "$_pkgname-$pkgver-2"

  python2 setup.py build

  cd "$srcdir/$_pkgname-$pkgver"

  python setup.py build
}

package_python2-facepy() {
  depends=('python2' 'python2-requests' 'python2-six')

  cd "$_pkgname-$pkgver-2"

  python2 setup.py install --prefix=/usr --root="$pkgdir" --optimize=1 \
    --skip-build
}

package_python-facepy() {
  depends=('python' 'python-requests' 'python-six')

  cd "$_pkgname-$pkgver"

  python setup.py install --prefix=/usr --root="$pkgdir" --optimize=1 \
    --skip-build
}

