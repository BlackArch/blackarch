# This file is part of BlackArch Linux ( https://www.blackarch.org/ ).
# See COPYING for license details.

pkgname=neo4j-community
pkgver=4.4.4
pkgrel=2
pkgdesc='A fully transactional graph database implemented in Java.'
arch=('any')
url='https://neo4j.org/'
license=('custom')
depends=('jre11-openjdk-headless')
conflicts=('neo4j-enterprise')
backup=('etc/neo4j/neo4j.conf')
options=(!strip)
install='neo4j.install'
source=("http://dist.neo4j.org/$pkgname-$pkgver-unix.tar.gz"
        'bin.patch'
        'neo4j.conf'
        'neo4j.install'
        'neo4j.service'
        'neo4j-tmpfile.conf'
        'neo4j-sysuser.conf')
sha512sums=('92ea66fd15ac495aec36c0542d5ab3e525a2c385f41bc910859d90bad5b860010bfc82e4d8b0554febccfd515e5f053458063ed627573732e56e2594a76337d9'
            '58a825817f3453162106fd27a0eb9b4d74129700de4d8284ba0f8cb24351d5c80c7e2f6225e28ecbd99307d1c73a46e6e082e45d08ccd1a173b2b376ac7bd495'
            'a80c547ee7f04c03502ee2a375b989195aabe6616ef9fc0be68bf865d4d6ee83915b97984e4cf2ec8ea43b1599e1423b2c443689d95e03f8df643347e1478e0b'
            '90388a53a53fba56a5fce51680f74a26108203ab2ee4cfa3652f4dc1baa4f898283c00d4448ef9077b3f9ab6274cd087101b8325163d63a8adadcb3a93d1713e'
            'a831855c4297417baadf6138dd7740087931f094de5dd43640c3e5d35d29c6d3c2efafc3b94cd36c53f70beadc50073a5695587be595f50da8340d2e35977b25'
            '86c6233d7bc6575a4af1e9756173871da8b62b8aaa25e0550ae21d9cec5b96fbc48f397d6d87bb12f440c7407aa5fc82db64e72e6bc0b35fc7d4593db0902bd9'
            'b82b8936629ccdb10a5a57ab94c38fe287e6affd1eaf9e1098e3d76b84e7583d29446a38fb6d9046bb78807d3468aaf042d81b879d346702affe2382e0ed61a9')

prepare() {
  cd "$pkgname-$pkgver"

  patch -Np1 -i ../bin.patch
}

package() {
  cd "$pkgname-$pkgver"

  # Config files
  CONFIG_DIR=etc/neo4j
  install -dm 755 "$pkgdir/$CONFIG_DIR"
  install -dm 700 "$pkgdir/$CONFIG_DIR/certificates"
  install -Dm 644 "$srcdir/neo4j.conf" "$pkgdir/etc/neo4j/neo4j.conf"

  # Data, import and log files
  DATA_DIR=var/lib/neo4j/data
  install -dm 755 "$pkgdir/$DATA_DIR"
  [[ $(ls -A data/* 2>/dev/null) ]] && cp -r data/* "$pkgdir/$DATA_DIR"

  IMPORT_DIR=var/lib/neo4j/import
  install -dm755 "$pkgdir/$IMPORT_DIR"
  [[ $(ls -A import/* 2>/dev/null) ]] && cp -r import/* "$pkgdir/$IMPORT_DIR"

  LOG_DIR=var/log/neo4j
  install -dm 755 "$pkgdir/$LOG_DIR"
  [[ $(ls -A logs/* 2>/dev/null) ]] && cp -r logs/* "$pkgdir/$LOG_DIR"

  # Copy JARs in lib and plugins
  LIB_DIR=usr/share/neo4j/lib
  install -dm 755 "$pkgdir/$LIB_DIR"
  [[ $(ls -A lib/* 2>/dev/null) ]] && cp -r lib/* "$pkgdir/$LIB_DIR"

  PLUGINS_DIR=usr/share/neo4j/plugins
  install -dm 755 "$pkgdir/$PLUGINS_DIR"
  [[ $(ls -A plugins/* 2>/dev/null) ]] && cp -r plugins/* "$pkgdir/$PLUGINS_DIR"

  # Executable files
  BIN_DIR=usr/share/neo4j/bin
  install -dm 755 "$pkgdir/$BIN_DIR"
  [[ $(ls -A bin/* 2>/dev/null) ]] && cp -r bin/* "$pkgdir/$BIN_DIR"

  SYSTEM_BIN_DIR=usr/bin
  install -dm 755 "$pkgdir/$SYSTEM_BIN_DIR"
  for file in $(find "$pkgdir/$BIN_DIR" -maxdepth 1 -type f); do
    b_file=$(basename $file)
    ln -s "/$BIN_DIR/$b_file" "$pkgdir/$SYSTEM_BIN_DIR/$b_file";
  done

  # Documentation
  DOC_DIR=usr/share/doc/neo4j
  install -dm 755 "$pkgdir/$DOC_DIR"
  cp README.txt UPGRADE.txt "$pkgdir/$DOC_DIR"

  # License files
  LICENSES_DIR=usr/share/licenses/neo4j
  install -dm 755 "$pkgdir/$LICENSES_DIR"
  cp LICENSE.txt LICENSES.txt NOTICE.txt "$pkgdir/$LICENSES_DIR"

  # Service definition files
  install -Dm 644 "$srcdir/neo4j.service" \
    "$pkgdir"/usr/lib/systemd/system/neo4j.service

  # Runtime files
  install -Dm 644 "$srcdir/neo4j-tmpfile.conf" \
    "$pkgdir"/usr/lib/tmpfiles.d/neo4j.conf
  install -Dm 644 "$srcdir/neo4j-sysuser.conf" \
    "$pkgdir"/usr/lib/sysusers.d/neo4j.conf
}

