# This file is part of BlackArch Linux ( https://www.blackarch.org/ ).
# See COPYING for license details.

pkgname=neo4j-community
pkgver=3.5.14
pkgrel=1
pkgdesc='A fully transactional graph database implemented in Java.'
arch=('any')
url='http://neo4j.org/'
license=('custom')
depends=('java-runtime-headless>=8')
conflicts=('neo4j-enterprise')
backup=('etc/neo4j/neo4j-wrapper.conf'
        'etc/neo4j/neo4j.conf')
options=(!strip)
install='neo4j.install'
source=("http://dist.neo4j.org/$pkgname-$pkgver-unix.tar.gz"
        'bin.patch'
        'neo4j.conf'
        'neo4j.install'
        'neo4j.service'
        'neo4j-tmpfile.conf')
sha512sums=('6b277b88930c1679328c145e3c7978ce5880dc02974a59d3802eda7e4dd519553ac0411dbaa3aa602a21f49dc32f1577bc38e24d87b7194bfe382b4c58711ed4'
            '3e2ad8bba304a05ce6132f47f8d1823f4314a755c8c9b701fc2a5c460d5eaf47f5e15d84eb850236454a2e3f52fd16f00a56ff243035321732707e3e0d417027'
            '2e04f99f51c3d13197ab520002346236f49c8de543dc0a54fd10076e5a14d00abbbcebe843b6a71c6e544e3004f8dcef9f303d304be5c338cc3f339e19cec26a'
            '90388a53a53fba56a5fce51680f74a26108203ab2ee4cfa3652f4dc1baa4f898283c00d4448ef9077b3f9ab6274cd087101b8325163d63a8adadcb3a93d1713e'
            '8f95947a370bcc843a7877b96ff4b07c2c5f4ea67a96f5958321a83677554f1b2fa7e72edbfe3bb3990d0a24baf60b782c3b69c95ba52c22ff43db1a66fd0cf9'
            '86c6233d7bc6575a4af1e9756173871da8b62b8aaa25e0550ae21d9cec5b96fbc48f397d6d87bb12f440c7407aa5fc82db64e72e6bc0b35fc7d4593db0902bd9')
prepare() {
  cd "$pkgname-$pkgver"

  patch -Np1 -i ../bin.patch
}

package() {
  cd "$pkgname-$pkgver"

  # Config files
  CONFIG_DIR=etc/neo4j
  install -dm 755 "$pkgdir/$CONFIG_DIR"
  install -dm 700 "$pkgdir/$CONFIG_DIR/certificates"
  install -Dm 644 "$srcdir/neo4j.conf" "$pkgdir/etc/neo4j/neo4j.conf"

  # Data, import and log files
  DATA_DIR=var/lib/neo4j/data
  install -dm 755 "$pkgdir/$DATA_DIR"
  [[ $(ls -A data/* 2>/dev/null) ]] && cp -r data/* "$pkgdir/$DATA_DIR"

  IMPORT_DIR=var/lib/neo4j/import
  install -dm755 "$pkgdir/$IMPORT_DIR"
  [[ $(ls -A import/* 2>/dev/null) ]] && cp -r import/* "$pkgdir/$IMPORT_DIR"

  LOG_DIR=var/log/neo4j
  install -dm 755 "$pkgdir/$LOG_DIR"
  [[ $(ls -A logs/* 2>/dev/null) ]] && cp -r logs/* "$pkgdir/$LOG_DIR"

  # Copy JARs in lib and plugins
  LIB_DIR=usr/share/java/neo4j/lib
  install -dm 755 "$pkgdir/$LIB_DIR"
  [[ $(ls -A lib/* 2>/dev/null) ]] && cp -r lib/* "$pkgdir/$LIB_DIR"

  PLUGINS_DIR=usr/share/java/neo4j/plugins
  install -dm 755 "$pkgdir/$PLUGINS_DIR"
  [[ $(ls -A plugins/* 2>/dev/null) ]] && cp -r plugins/* "$pkgdir/$PLUGINS_DIR"

  # Executable files
  BIN_DIR=usr/share/neo4j/bin
  install -dm 755 "$pkgdir/$BIN_DIR"
  [[ $(ls -A bin/* 2>/dev/null) ]] && cp -r bin/* "$pkgdir/$BIN_DIR"

  SYSTEM_BIN_DIR=usr/bin
  install -dm 755 "$pkgdir/$SYSTEM_BIN_DIR"
  for file in $(find "$pkgdir/$BIN_DIR" -maxdepth 1 -type f); do
    b_file=$(basename $file)
    ln -s "/$BIN_DIR/$b_file" "$pkgdir/$SYSTEM_BIN_DIR/$b_file";
  done

  # Documentation
  DOC_DIR=usr/share/doc/neo4j
  install -dm 755 "$pkgdir/$DOC_DIR"
  cp README.txt UPGRADE.txt "$pkgdir/$DOC_DIR"

  # License files
  LICENSES_DIR=usr/share/licenses/neo4j
  install -dm 755 "$pkgdir/$LICENSES_DIR"
  cp LICENSE.txt LICENSES.txt NOTICE.txt "$pkgdir/$LICENSES_DIR"

  # Service definition files
  install -Dm 644 "$srcdir/neo4j.service" \
    "$pkgdir"/usr/lib/systemd/system/neo4j.service

  # Runtime files
  install -Dm 644 "$srcdir/neo4j-tmpfile.conf" \
    "$pkgdir"/usr/lib/tmpfiles.d/neo4j.conf
}

