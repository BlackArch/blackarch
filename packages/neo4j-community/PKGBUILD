# This file is part of BlackArch Linux ( https://www.blackarch.org/ ).
# See COPYING for license details.

pkgname=neo4j-community
_pkgname=neo4j
pkgver=5.24.0
pkgrel=1
pkgdesc='A fully transactional graph database implemented in Java.'
arch=('any')
url='https://neo4j.com/'
license=('GPL-3.0-only')
_java_version=17
depends=("java-runtime=$_java_version")
makedepends=("java-environment=$_java_version" 'maven')
conflicts=('neo4j-enterprise')
backup=('etc/neo4j/neo4j-admin.conf'
        'etc/neo4j/neo4j.conf'
        'etc/neo4j/server-logs.xml'
        'etc/neo4j/user-logs.xml')
options=(!strip)
# Crashes at runtim when using the pre-built distribution "https://dist.neo4j.org/$pkgname-$pkgver-unix.tar.gz"
source=("https://github.com/$_pkgname/$_pkgname/archive/$pkgver.tar.gz"
        'neo4j.executable-template'
        'neo4j.service'
        'neo4j.sysusers'
        'neo4j.tmpfiles')
sha512sums=('cedb13ce99e9f20efa042f6115701232bff78a4c062ffc4df437d3eda1bbf54d251cb8dc1b1e9def9a6fcba1591e278bfc6d2f316456347dafbb093cc6211cb1'
            '1aee5f3f977720fd1a31a4404403a3241467257380dd4db8c6a3f949a7e2d16d127c02eb64b2d9801decbef034e91a3de98977101b7511f23197f7fb02b3b989'
            '3b4ec61ce42df80dc42f16cd7c2e2d1f54b2e71325b6a21060bd9961ddf277aa451fe8d94194f631114f82ff44c5802c28deff802c639b265fe5cf76ec770c97'
            '1a8f4fd2cc64c3e3e472448512d7d5ed4b123628a39fb440b29523a5fc849667a1ef54dfdd0fb1328f2398b2adcad149e5ea5fed0e5105c5a81fa2ea135b5413'
            'b2bcfaded870902124e80637a81f9c0f90963a70ba46fdf3dbc1a530463b2a1355d06ac6edb41bde19c2771b2e377a3c88e742b4c880cce6691b3fb9ce1abd8f')

prepare() {
  cd "$_pkgname-$pkgver"

  # Set version
  mvn versions:set -DnewVersion="$pkgver"

  # Download dependencies
  mvn \
    -Dmaven.repo.local="$srcdir/repo" \
    dependency:go-offline

  mkdir -p bin
  # shellcheck disable=SC2002
  {
    cat "$srcdir/neo4j.executable-template" \
      | sed "s/%APP_NAME%/neo4j/" \
      | sed "s/%CLASS_NAME%/org.neo4j.server.startup.Neo4jBoot/" \
        > bin/neo4j
    cat "$srcdir/neo4j.executable-template" \
      | sed "s/%APP_NAME%/neo4j-admin/" \
      | sed "s/%CLASS_NAME%/org.neo4j.server.startup.Neo4jAdminBoot/" \
        > bin/neo4j-admin
    cat "$srcdir/neo4j.executable-template" \
      | sed "s/%APP_NAME%/cypher-shell/" \
      | sed "s/%CLASS_NAME%/org.neo4j.shell.startup.CypherShellBoot/" \
        > bin/cypher-shell
  }
}

build() {
  cd "$_pkgname-$pkgver"

  export PATH="/usr/lib/jvm/java-$_java_version-openjdk/bin:$PATH"
  mvn \
    -Dmaven.repo.local="$srcdir/repo" \
    package -DskipTests
}

package() {
  cd "$_pkgname-$pkgver"

  # Create necessary folders
  install -dm 755 "$pkgdir/var/log/$_pkgname"

  # Man pages
  install -Dm644 -t "$pkgdir/usr/share/man/man1" \
    community/cypher-shell/packaging/src/common/manpages/cypher-shell.1

  # Executable files
  install -Dm755 -t "$pkgdir/usr/bin" \
    bin/cypher-shell \
    bin/neo4j \
    bin/neo4j-admin

  # Locally built distribution
  tar -xf "packaging/standalone/target/neo4j-community-$pkgver-unix.tar.gz"
  local bin_archive="neo4j-community-$pkgver"

  # Config files
  install -Dm644 -t "$pkgdir/etc/neo4j" \
    "$bin_archive/conf/neo4j-admin.conf" \
    "$bin_archive/conf/neo4j.conf" \
    "$bin_archive/conf/server-logs.xml" \
    "$bin_archive/conf/user-logs.xml"

  sed -i 's|=/usr/share/neo4j/lib|=/usr/share/java/neo4j|' \
    "$pkgdir/etc/neo4j/neo4j.conf"

  # Bash completion
  install -Dm644 "$bin_archive/bin/completion/neo4j-admin_completion" \
    "$pkgdir/usr/share/bash-completion/completions/neo4j-admin"
  install -Dm644 "$bin_archive/bin/completion/neo4j_completion" \
    "$pkgdir/usr/share/bash-completion/completions/neo4j"

  # Install JARs
  install -Dm644 -t "$pkgdir/usr/share/java/neo4j" "$bin_archive/lib/"*.jar

  # Documentation
  install -Dm644 -t "$pkgdir/usr/share/doc/neo4j" \
    "$bin_archive/README.txt" \
    "$bin_archive/UPGRADE.txt"

  # License files
  install -Dm644 -t "$pkgdir/usr/share/licenses/$pkgname" \
    "$bin_archive/LICENSE.txt" \
    "$bin_archive/LICENSES.txt" \
    "$bin_archive/NOTICE.txt"

  # Systemd files
  install -Dm644 -t "$pkgdir/usr/lib/systemd/system" "$srcdir/neo4j.service"
  install -Dm644 "$srcdir/neo4j.sysusers" "$pkgdir/usr/lib/sysusers.d/neo4j.conf"
  install -Dm644 "$srcdir/neo4j.tmpfiles" "$pkgdir/usr/lib/tmpfiles.d/neo4j.conf"
}

