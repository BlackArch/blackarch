# This file is part of BlackArch Linux ( https://www.blackarch.org/ ).
# See COPYING for license details.

pkgname=rcracki-mt
pkgver=0.7.0
pkgrel=8
groups=('blackarch' 'blackarch-cracker')
pkgdesc='A tool to perform rainbow table attacks on password hashes. It is intended for indexed/perfected rainbow tables, mainly generated by the distributed project www.freerainbowtables.com.'
arch=('x86_64' 'aarch64')
url='https://rcracki.sourceforge.net/'
depends=('openssl')
license=('GPL-2.0-or-later')
replaces=('rcracki_mt')
conflicts=('rcracki_mt')
provides=('rcracki_mt')
source=("https://downloads.sourceforge.net/project/rcracki/rcracki_mt/rcracki_mt_$pkgver/rcracki_mt_${pkgver}_src.7z"
        "fix-variable-name.patch"
        "no-install-executable-as-root.patch")
sha512sums=('f93dc1d7ca1f18c63a9612b053d86ad639589e2534361c2fb3d3c064c162002798082bc1219737ae9567a23daa6ef7a0f7a52e7debbeb8ceceb2e1724c71bf0d'
            'f6572b7ef40d5f942e398d2898361b1e2497e5793581af10a3692a2f9115d45530cdfc2106afa25dbb97df9d22c2d3e9fa27531fb2daa14c6217f135374e7bbe'
            'f462f754a8dcec5eb77cfefd53a4cb885dbd9a687dd54a45cc8710c35189fc873779c1ad02dd81142fc0c64fe4b905c9e8ff53e4b7afd390819749fd4f0b8f18')

prepare() {
  cd "rcracki_mt_${pkgver}_src"

  patch -p1 < ../no-install-executable-as-root.patch
  patch -p1 < ../fix-variable-name.patch
}

build() {
  cd "rcracki_mt_${pkgver}_src/rcracki_mt"

  make
}

package() {
  cd "rcracki_mt_${pkgver}_src/rcracki_mt"

  make DESTDIR="$pkgdir" install
}

