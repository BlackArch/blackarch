# This file is part of BlackArch Linux ( https://www.blackarch.org/ ).
# See COPYING for license details.

pkgname=python2-portend
_pkgname=portend
pkgver=2.6
pkgrel=3
pkgdesc='TCP port monitoring and discovery.'
arch=('any')
url='https://pypi.org/project/portend/#files'
license=('MIT')
depends=('python2' 'python2-tempora')
makedepends=('python2' 'python2-setuptools' 'python2-setuptools-scm'
             'python2-tox' 'python2-tempora')
source=("https://files.pythonhosted.org/packages/04/98/997f8668b11292f13d3e69fc626232c497228306c764523c5a3a3b59c775/$_pkgname-$pkgver.tar.gz"
        'LICENSE')
sha512sums=('9e53c668ba9d0ddb2e92d9b4c97d2b8a4d858a6fc44123414ee3c8ac433f51f50a4773ee08f85bb134e49aeace2a17baffd32962d36df0b19b505a2c6634bf7e'
            '1106afed483b7258e4ae89c5d9459c3834412b31aac90169725ed62d2ab44f61f6f79e894d4c9e4d8bd99e14530ab778df2187784f0b25eaab86d312fad68944')

build() {
  export SETUPTOOLS_SCM_PRETEND_VERSION="$pkgver"

  cd "$_pkgname-$pkgver"

  python2 setup.py build
}

package() {
  cd "$_pkgname-$pkgver"

  python2 setup.py install --root="$pkgdir" --prefix=/usr -O1 --skip-build

  install -Dm 644 "$srcdir/LICENSE" \
    "$pkgdir/usr/share/licenses/$pkgname/LICENSE"
}
