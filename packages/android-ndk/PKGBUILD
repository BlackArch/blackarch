# This file is part of BlackArch Linux ( https://www.blackarch.org/ ).
# See COPYING for license details.

pkgname=android-ndk
pkgver=r22
pkgrel=1
epoch=2
pkgdesc='Android C/C++ developer kit'
arch=('x86_64')
groups=('blackarch' 'blackarch-mobile')
url='https://developer.android.com/sdk/ndk/index.html'
license=('GPL' 'LGPL' 'custom')
depends=('android-sdk' 'glibc')
optdepends=(
  'ncurses5-compat-libs: for using gdb'
  'python2: various helper scripts'
  'python: various helper scripts'
)
options=('!strip' 'staticlibs')
install="$pkgname.install"
source=('android-ndk.sh'
        "https://dl.google.com/android/repository/$pkgname-${pkgver/./}-linux-x86_64.zip")
sha512sums=('3b1a83792d314a5ff1a35db0be30ff436447e9b8c2ec9337c729ed5defd5c7ab99d2a79af65b7727255b48affaa1dca81e92c6b3b1a5ff79f518e428a96c5cb7'
            '6f7e52ef68b2ab465d43e0392e21fb0eecc33c5bea29ee8bdfbf47754bb90f9abf9aaf48eee0bb94b763852055586756238398ba0d0bbf2a2380cc60840adec7')

package() {
  install -dm 755 "$pkgdir/opt"
  cp -a --no-preserve=ownership "android-ndk-${pkgver/./}" "$pkgdir/opt/$pkgname"

  install -Dm 644 "$pkgname.sh" -t "$pkgdir/etc/profile.d/"
}

