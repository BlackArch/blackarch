# This file is part of BlackArch Linux ( https://www.blackarch.org/ ).
# See COPYING for license details.

pkgname=dsss
pkgver=116.6d14edb
pkgrel=2
groups=('blackarch' 'blackarch-webapp' 'blackarch-scanner')
pkgdesc='A fully functional SQL injection vulnerability scanner (supporting GET and POST parameters) written in under 100 lines of code.'
arch=('any')
url='https://github.com/stamparm/DSSS'
license=('custom:unknown')
depends=('python2')
makedepends=('git')
source=('git+https://github.com/stamparm/DSSS.git')
sha512sums=('SKIP')

pkgver() {
  cd DSSS

  echo $(git rev-list --count HEAD).$(git rev-parse --short HEAD)
}

prepare() {
  cd DSSS

  sed -i 's|bin/env python|bin/env python2|' dsss.py
}

package() {
  cd DSSS

  install -Dm 755 dsss.py "$pkgdir/usr/bin/$pkgname"
  install -Dm 644 -t "$pkgdir/usr/share/doc/$pkgname/" README.md
}

