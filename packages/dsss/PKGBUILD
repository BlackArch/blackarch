# This file is part of BlackArch Linux ( http://blackarch.org ).
# See COPYING for license details.

pkgname='dsss'
pkgver=116.6d14edb
pkgrel=2
groups=('blackarch' 'blackarch-webapp' 'blackarch-scanner')
pkgdesc='A fully functional SQL injection vulnerability scanner (supporting GET and POST parameters) written in under 100 lines of code.'
arch=('any')
url='https://github.com/stamparm/DSSS'
license=('custom:unknown')
depends=('python2')
makedepends=('git')
source=('git+https://github.com/stamparm/DSSS.git')
sha1sums=('SKIP')

pkgver() {
  cd "$srcdir/DSSS"

  echo $(git rev-list --count HEAD).$(git rev-parse --short HEAD)
}

prepare() {
  cd "$srcdir/DSSS"

  sed -i 's|bin/env python|bin/env python2|' dsss.py
}

package() {
  cd "$srcdir/DSSS"

  install -Dm755 dsss.py "$pkgdir/usr/bin/dsss"
  install -Dm644 -t "$pkgdir/usr/share/doc/dsss/" README.md
}
