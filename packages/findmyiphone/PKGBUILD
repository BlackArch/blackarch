# This file is part of BlackArch Linux ( http://blackarch.org ).
# See COPYING for license details.

pkgname=findmyiphone
pkgver=19.aef3ac8
pkgrel=1
pkgdesc='Locates all devices associated with an iCloud account'
groups=('blackarch' 'blackarch-mobile' 'blackarch-recon')
arch=('any')
url='https://github.com/manwhoami/findmyiphone'
license=('custom:unknown')
depends=('python2')
makedepends=('git')
source=('git+https://github.com/manwhoami/FindMyiPhone.git')
sha512sums=('SKIP')

pkgver() {
  cd FindMyiPhone

  echo $(git rev-list --count HEAD).$(git rev-parse --short HEAD)
}

prepare() {
  cd FindMyiPhone

  sed -i '1 i\#!/usr/bin/python2' FMIP.py
}

package() {
  cd FindMyiPhone

  install -Dm 755 FMIP.py "$pkgdir/usr/bin/$pkgname"
  install -Dm 644 README.md "$pkgdir/usr/share/doc/$pkgname/README.md"
}

