# This file is part of BlackArch Linux ( http://blackarch.org ).
# See COPYING for license details.

pkgname='fssb'
pkgver=73.51d2ac2
pkgrel=1
pkgdesc='A low-level filesystem sandbox for Linux using syscall intercepts.'
groups=('blackarch' 'blackarch-defensive')
arch=('x86_64' 'armv6h' 'armv7h' 'aarch64')
url='https://github.com/adtac/fssb'
license=('GPL3')
depends=('openssl')
makedepends=('git')
source=('git+https://github.com/adtac/fssb.git')
sha1sums=('SKIP')

pkgver() {
  cd "$srcdir/fssb"

  echo $(git rev-list --count HEAD).$(git rev-parse --short HEAD)
}

build() {
  cd "$srcdir/fssb"

  make
}

package() {
  cd "$srcdir/fssb"

  mkdir -p "$pkgdir/usr/share/fssb/tests"

  install -Dm755 fssb "$pkgdir/usr/bin/fssb"
  install -Dm644 -t "$pkgdir/usr/share/fssb/tests" tests/*
  install -Dm644 -t "$pkgdir/usr/share/doc/fssb/" README.md
  install -Dm644 LICENSE "$pkgdir/usr/share/licenses/fssb/LICENSE"
}
