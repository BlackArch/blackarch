# This file is part of BlackArch Linux ( http://blackarch.org ).
# See COPYING for license details.

pkgname='tinfoleak'
pkgver=3.6469eb3
pkgrel=3
groups=('blackarch' 'blackarch-recon' 'blackarch-social' 'blackarch-webapp')
pkgdesc='Get detailed information about a Twitter user activity.'
arch=('any')
url='https://github.com/technoskald/tinfoleak/'
license=('custom:unknown')
depends=('python2' 'python2-tweepy')
makedepends=('git')
source=('git+https://github.com/technoskald/tinfoleak.git')
sha1sums=('SKIP')

pkgver() {
  cd "$srcdir/tinfoleak"

  echo $(git rev-list --count HEAD).$(git rev-parse --short HEAD)
}

prepare() {
  cd "$srcdir/tinfoleak"

  sed -i 's|/usr/bin/python|/usr/bin/python2|' tinfoleak.py
}

package() {
  cd "$srcdir/tinfoleak"

  install -Dm755 tinfoleak.py "$pkgdir/usr/bin/tinfoleak"
  install -Dm644 README.md "$pkgdir/usr/share/doc/tinfoleak/README.md"
}
