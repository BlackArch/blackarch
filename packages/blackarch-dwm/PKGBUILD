pkgname=blackarch-dwm
pkgver=6.0
pkgrel=5
pkgdesc="Dynamic window manager for X with BlackArch colors."
arch=('i686' 'x86_64')
url="http://dwm.suckless.org"
license=('GPL')
depends=('dmenu' 'xorg-server' 'xorg-xsetroot' 'libx11' 'libxinerama')
conflicts=('dwm')
provides=('dwm')
source=("http://git.suckless.org/dwm/snapshot/dwm-$pkgver.tar.bz2"
        'config.h'
        'blackarch-dwm')
md5sums=('b6521aa6bd9d58839a1cc759f693c52c'
         'ac3a2bdeec75b3cc43d81958ac7772e9'
         '0123431da1444d94bc525af0d747d889')

prepare(){
  cd "$srcdir/dwm-$pkgver"

  cp -f "$srcdir/config.h" .

  sed -i 's/CPPFLAGS =/CPPFLAGS +=/g' config.mk
  sed -i 's/^CFLAGS = -g/#CFLAGS += -g/g' config.mk
  sed -i 's/^#CFLAGS = -std/CFLAGS += -std/g' config.mk
  sed -i 's/^LDFLAGS = -g/#LDFLAGS += -g/g' config.mk
  sed -i 's/^#LDFLAGS = -s/LDFLAGS += -s/g' config.mk
}

build() {
  cd "$srcdir/dwm-$pkgver"

  make X11INC=/usr/include/X11 X11LIB=/usr/lib/X11
}

package() {
  cd "$srcdir/dwm-$pkgver"

  make PREFIX=/usr DESTDIR="$pkgdir" install

  cp "$srcdir/blackarch-dwm" "$pkgdir/usr/bin/"
}
