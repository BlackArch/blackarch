# This file is part of BlackArch Linux ( http://blackarch.org ).
# See COPYING for license details.

pkgname=blackarch-dwm
pkgver=6.1
pkgrel=1
pkgdesc='Dynamic window manager for X with BlackArch colors.'
arch=('x86_64' 'armv6h' 'armv7h' 'aarch64')
url='https://dl.suckless.org/dwm/'
license=('GPL')
depends=('dmenu' 'xorg-server' 'xorg-xsetroot' 'libx11' 'libxinerama')
conflicts=('dwm')
provides=('dwm')
source=("https://dl.suckless.org/dwm/dwm-${pkgver}.tar.gz"
        'config.h'
        'blackarch-dwm')
sha512sums=('875644bbc246088f2c87e50debbadd4bc179444b396b1b327149087e8bc5303325ebc9103a7e3c864266ee1ac683ea3cd8f0a08285c8e46fc4389d5b83a555f1'
            '6b079086c1c08c4680d1e166d182677c568f5735c7dcbb0059d6477bd3a39cd64de63d3a7ae0de62a8617535ed375737164065dabe2638a4ca32fe8d776e8061'
            '414ec8da178907fdba5a2c04481cc4afb077d15c00fbb67138962b82cda33cad3a901bb88b5811a71a585c8f052ad6368f43e9cf1982ef3923837453599f03e1')

prepare(){
  cd "dwm-$pkgver"

  cp -f "$srcdir/config.h" .

  sed -i 's/CPPFLAGS =/CPPFLAGS +=/g' config.mk
  sed -i 's/^CFLAGS = -g/#CFLAGS += -g/g' config.mk
  sed -i 's/^#CFLAGS = -std/CFLAGS += -std/g' config.mk
  sed -i 's/^LDFLAGS = -g/#LDFLAGS += -g/g' config.mk
  sed -i 's/^#LDFLAGS = -s/LDFLAGS += -s/g' config.mk
}

build() {
  cd "dwm-$pkgver"

  make X11INC=/usr/include/X11 X11LIB=/usr/lib/X11
}

package() {
  cd "dwm-$pkgver"

  make PREFIX=/usr DESTDIR="$pkgdir" install

  install -m755 "$srcdir/blackarch-dwm" "$pkgdir/usr/bin/"
}

