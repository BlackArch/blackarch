# This file is part of BlackArch Linux ( https://www.blackarch.org/ ).
# See COPYING for license details.

pkgname=aflplusplus
pkgver=3.12c.r162.gd0225c2c
pkgrel=1
pkgdesc='American Fuzzing Lop fuzzer with community patches and additional features.'
arch=('x86_64' 'aarch64')
groups=('blackarch' 'blackarch-fuzzer')
url='https://aflplus.plus/'
license=('Apache')
depends=('bash' 'python')
optdepends=('clang: clang instrumentation support'
            'llvm: clang-fast instrumentation support'
            'gcc: gcc instrumentation support'
            'gnuplot: graph plotting support')
makedepends=('clang' 'llvm' 'git' 'lib32-glibc')
provides=('afl' 'american-fuzzy-lop')
conflicts=('afl')
source=("git+https://github.com/AFLplusplus/$pkgname.git")
sha512sums=('SKIP')

pkgver() {
  cd $pkgname

  git describe --long --tags | sed 's/\([^-]*-g\)/r\1/;s/-/./g'
}

build() {
  cd $pkgname

  make source-only
  make man
}

package() {
  cd $pkgname

  make DESTDIR="$pkgdir" PREFIX=/usr install
}

