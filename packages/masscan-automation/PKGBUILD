# This file is part of BlackArch Linux ( http://blackarch.org ).
# See COPYING for license details.

pkgname=masscan-automation
pkgver=24.2df3467
pkgrel=2
groups=('blackarch' 'blackarch-automation')
pkgdesc='Masscan integrated with Shodan API.'
arch=('any')
url='https://github.com/trevordavenport/MasscanAutomation'
license=('custom:unknown')
depends=('python2')
makedepends=('git' 'python2-beautifulsoup4' 'masscan' 'shodan')
source=('git+https://github.com/trevordavenport/MasscanAutomation.git'
        'masscan-automation.patch')
sha512sums=('SKIP'
            'ce38e93b986ffdf7ed59b8159ad67ca36378c6f4dfefe96e5f26e3107f82de55f3e534af14df0b5f6d96e8b1d5b16c2adc255db3398fc68d2a5ae835fbaa8329')

pkgver() {
  cd "$srcdir/MasscanAutomation"

  echo $(git rev-list --count HEAD).$(git rev-parse --short HEAD)
}

prepare() {
  cd "$srcdir/MasscanAutomation"

  patch -p2 < ../masscan-automation.patch
}

package() {
  cd "$srcdir/MasscanAutomation"

  install -Dm755 masscan_automation.py "$pkgdir/usr/bin/masscan-automation"
  install -Dm644 README.md "$pkgdir/usr/share/doc/masscan-automation/README.md"
}
