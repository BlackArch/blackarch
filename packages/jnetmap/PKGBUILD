# This file is part of BlackArch Linux ( http://blackarch.org ).
# See COPYING for license details.

pkgname='jnetmap'
pkgver='0.5.3'
pkgrel=5
groups=('blackarch' 'blackarch-networking')
pkgdesc="A network monitor of sorts"
arch=('any')
url='http://www.rakudave.ch/jnetmap/?file=introduction'
license=('GPL3')
depends=('java-environment')
source=("http://downloads.sourceforge.net/project/jnetmap/jNetMap%200.5.3/jNetMap-$pkgver.tar.gz")
sha1sums=('66432fba3f58804eb269e98cbced2cd3a6f27ea1')

package() {
  cd "$srcdir/jNetMap-$pkgver-591"

  # Base directories.
  install -dm755 "$pkgdir/usr/bin"
  install -dm755 "$pkgdir/usr/share/jnetmap"
  install -dm755 "$pkgdir/usr/share/applications"

  # Icons and desktop files.
  install -m644 jnetmap.png "$pkgdir/usr/share/jnetmap"
  install -m644 jnetmap.desktop "$pkgdir/usr/share/applications"

  # Bin.
  install -m755 jNetMap.jar "$pkgdir/usr/share/jnetmap"
  install -m755 jnetmap "$pkgdir/usr/bin/jnetmap"
}
