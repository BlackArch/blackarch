# This file is part of BlackArch Linux ( https://www.blackarch.org/ ).
# See COPYING for license details.

pkgname=python-updog
_pkgname=updog
pkgver=1.4
pkgrel=1
pkgdesc="A replacement for Python's SimpleHTTPServer."
arch=('any')
url='https://pypi.org/project/updog/#files'
license=('MIT')
depends=('python' 'python-colorama' 'python-flask' 'python-flask-httpauth'
         'python-werkzeug' 'python-pyopenssl')
makedepends=('python-setuptools')
source=("https://files.pythonhosted.org/packages/source/${_pkgname::1}/$_pkgname/$_pkgname-$pkgver.tar.gz")
sha512sums=('3f000f12594dc57a74bf70ff050b8a02425f5fea4ba5984228b93f39d7ac523c5708e370fd1be9631eacf2a2ca1e10994ea4beec23658fef6f093fc12088e240')

build() {
  cd "$_pkgname-$pkgver"

  python setup.py build
}

package() {
  cd "$_pkgname-$pkgver"

  python setup.py install --root="$pkgdir" --prefix=/usr -O1 --skip-build
}

