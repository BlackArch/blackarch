# This file is part of BlackArch Linux ( https://www.blackarch.org/ ).
# See COPYING for license details.

pkgname=maigret
pkgver=1279.b577477
pkgrel=2
pkgdesc='Collect a dossier on a person by username from a huge number of sites.'
arch=('any')
groups=('blackarch' 'blackarch-recon')
url='https://git.rip/soxoj/maigret'
license=('MIT')
depends=('python' 'python-virtualenv')
makedepends=('git' 'python-pip')
source=("git+https://git.rip/Soxoj/$pkgname.git")
sha512sums=('SKIP')
install="$pkgname.install"

pkgver() {
  cd $pkgname

  echo $(git rev-list --count HEAD).$(git rev-parse --short HEAD)
}

package() {
  cd $pkgname

  install -dm 755 "$pkgdir/usr/bin"
  install -dm 755 "$pkgdir/usr/share/$pkgname"

  install -Dm 644 -t "$pkgdir/usr/share/doc/$pkgname/" README.md
  install -Dm 644 LICENSE "$pkgdir/usr/share/licenses/$pkgname/LICENSE"

  cp -a --no-preserve=ownership * "$pkgdir/usr/share/$pkgname/"

    cat > "$pkgdir/usr/bin/$pkgname" << EOF
#!/bin/sh
cd /usr/share/$pkgname
source venv/bin/activate
exec python maigret/maigret.py "\$@"
EOF

chmod a+x "$pkgdir/usr/bin/$pkgname"
}

