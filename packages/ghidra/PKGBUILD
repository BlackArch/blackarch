# This file is part of BlackArch Linux ( https://www.blackarch.org/ ).
# See COPYING for license details.

pkgname=ghidra
pkgver=9.0.2
_releasedate=20190403
pkgrel=2
pkgdesc="A software reverse engineering (SRE) suite of tools developed by NSA's Research Directorate in support of the Cybersecurity mission."
arch=('x86_64')
groups=('blackarch' 'blackarch-reversing' 'blackarch-disassembler'
        'blackarch-debugger')
url='https://www.ghidra-sre.org/'
license=('Apache')
depends=('java-environment>=11' 'bash')
source=("https://www.ghidra-sre.org/${pkgname}_${pkgver}_PUBLIC_${_releasedate}.zip")
sha512sums=('865eb93029ca56936e444c76d442d14f71d7c3f59c2a19c67175759c918b8500e175e62ec7338e22e364b2a2ee42827f886c70a487f44b18dddeda60bf6a7ac7')

package() {
  cd "${pkgname}_${pkgver}"

  install -dm 755 "$pkgdir/usr/bin/"
  install -dm 755 "$pkgdir/usr/share/$pkgname/"

  cp -a --no-preserve=ownership * "$pkgdir/usr/share/$pkgname/"

  ln -sf "/usr/share/$pkgname/ghidraRun" "$pkgdir/usr/bin/$pkgname"
  ln -sf "/usr/share/$pkgname/support/ghidraDebug" \
    "$pkgdir/usr/bin/$pkgname-debug"
}

