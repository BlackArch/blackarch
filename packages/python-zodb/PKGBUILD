# This file is part of BlackArch Linux ( https://www.blackarch.org/ ).
# See COPYING for license details.

pkgname=python-zodb
_pkgname=${pkgname#python-}
pkgver=6.2
pkgrel=1
pkgdesc='A native object database for Python.'
arch=('any')
url='https://pypi.org/project/ZODB/'
license=('custom:unknown')
depends=('python' 'python-persistent' 'python-zodbpickle' 'python-transaction'
         'python-btrees')
makedepends=('python-build' 'python-installer' 'python-setuptools'
             'python-wheel')
options=(!emptydirs)
source=("https://files.pythonhosted.org/packages/source/${_pkgname::1}/$_pkgname/$_pkgname-$pkgver.tar.gz")
sha512sums=('575518d20a2e6351ded1ea96b7d5338b403e1e573d8a1dfac5f46d0e0586b5af60e9b39b7d103786246aab96a892faccb6840af057a24324dee1298d642d6a9e')

prepare() {
  cd "$_pkgname-$pkgver"

  sed -i 's|setuptools == 78.1.1|setuptools|g' pyproject.toml
}

build() {
  cd "$_pkgname-$pkgver"

  python -m build --wheel --no-isolation
}

package() {
  cd "$_pkgname-$pkgver"

  python -m installer --destdir="$pkgdir" dist/*.whl
}

