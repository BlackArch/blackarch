# This file is part of BlackArch Linux ( https://www.blackarch.org/ ).
# See COPYING for license details.

pkgbase=python-aiosqlite
pkgname=('python2-aiosqlite' 'python-aiosqlite')
_pkgname=aiosqlite
pkgver=0.14.1
pkgrel=1
pkgdesc='Asyncio bridge to the standard sqlite3 module.'
arch=('any')
url='https://pypi.org/project/aiosqlite/#files'
license=('APACHE')
makedepends=('python2-setuptools' 'python-setuptools')
source=("https://files.pythonhosted.org/packages/81/71/02ae04e69ba6d116faf65e8d179f9f55a02b0732283504e62e1e54f4619f/$_pkgname-$pkgver.tar.gz")
sha512sums=('7a68670e29a7c450b58ab7c5cd5e4063068fe302a0055df96460e58969ad478e7e5d8652df3c31d5fe40c3cda573189dbc8239526c8ca613654b9a689b514148')

prepare() {
  cp -a "$_pkgname-$pkgver"{,-2}
}

build() {
  cd "$_pkgname-$pkgver-2"

  python2 setup.py build

  cd "$srcdir/$_pkgname-$pkgver"

  python setup.py build
}

package_python2-aiosqlite() {
  depends=('python2')

  cd "$_pkgname-$pkgver-2"

  python2 setup.py install --prefix=/usr --root="$pkgdir" -O1 --skip-build
}

package_python-aiosqlite() {
  depends=('python')

  cd "$_pkgname-$pkgver"

  python setup.py install --prefix=/usr --root="$pkgdir" -O1 --skip-build
}

