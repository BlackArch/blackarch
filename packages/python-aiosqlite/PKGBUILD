# This file is part of BlackArch Linux ( https://www.blackarch.org/ ).
# See COPYING for license details.

pkgbase=python-aiosqlite
pkgname=('python2-aiosqlite' 'python-aiosqlite')
_pkgname=aiosqlite
pkgver=0.11.0
pkgrel=1
pkgdesc='Asyncio bridge to the standard sqlite3 module.'
arch=('any')
url='https://pypi.org/project/aiosqlite/#files'
license=('APACHE')
makedepends=('python2-setuptools' 'python-setuptools')
source=("https://files.pythonhosted.org/packages/61/61/4082af155aa2d58971e94a8f5798a430c3b2c1b4fb8a977264c8a360f54a/$_pkgname-$pkgver.tar.gz")
sha512sums=('7570434e40ba61b6568d9f16ad0fec8a246de12b604a514d7de8e113aa5ed3060a5e183f803bc6e8719f0bbdf45957bb43633b9cba73866d53b7a52bcf185580')

prepare() {
  cp -a "$_pkgname-$pkgver"{,-2}
}

build() {
  cd "$_pkgname-$pkgver-2"

  python2 setup.py build

  cd "$srcdir/$_pkgname-$pkgver"

  python setup.py build
}

package_python2-aiosqlite() {
  depends=('python2')

  cd "$_pkgname-$pkgver-2"

  python2 setup.py install --prefix=/usr --root="$pkgdir" -O1 --skip-build
}

package_python-aiosqlite() {
  depends=('python')

  cd "$_pkgname-$pkgver"

  python setup.py install --prefix=/usr --root="$pkgdir" -O1 --skip-build
}

