# This file is part of BlackArch Linux ( https://www.blackarch.org/ ).
# See COPYING for license details.

pkgbase=python-aiosqlite
pkgname=('python2-aiosqlite' 'python-aiosqlite')
_pkgname=aiosqlite
pkgver=0.16.1
pkgrel=1
pkgdesc='Asyncio bridge to the standard sqlite3 module.'
arch=('any')
url='https://pypi.org/project/aiosqlite/#files'
license=('APACHE')
makedepends=('python2-setuptools' 'python-setuptools')
source=("https://files.pythonhosted.org/packages/9a/d8/a5ebbfe225ab58a4a2b89067b9908b7af961f324a0cce08956926b3a5ad3/$_pkgname-$pkgver.tar.gz")
sha512sums=('6d2a63eeed12303cbc0b42478fa4c18582c8962817d8b927b52d03900c5ab5f70e4c6cc7ff3547d28cca8d70f618e43a6765f5e26393f8ed7a740a7f55895c86')

prepare() {
  cp -a "$_pkgname-$pkgver"{,-2}
}

build() {
  cd "$_pkgname-$pkgver-2"

  python2 setup.py build

  cd "$srcdir/$_pkgname-$pkgver"

  python setup.py build
}

package_python2-aiosqlite() {
  depends=('python2')

  cd "$_pkgname-$pkgver-2"

  python2 setup.py install --prefix=/usr --root="$pkgdir" -O1 --skip-build
}

package_python-aiosqlite() {
  depends=('python')

  cd "$_pkgname-$pkgver"

  python setup.py install --prefix=/usr --root="$pkgdir" -O1 --skip-build
}

