# This file is part of BlackArch Linux ( https://www.blackarch.org/ ).
# See COPYING for license details.

pkgbase=python-aiosqlite
pkgname=('python2-aiosqlite' 'python-aiosqlite')
_pkgname=aiosqlite
pkgver=0.13.0
pkgrel=1
pkgdesc='Asyncio bridge to the standard sqlite3 module.'
arch=('any')
url='https://pypi.org/project/aiosqlite/#files'
license=('APACHE')
makedepends=('python2-setuptools' 'python-setuptools')
source=("https://files.pythonhosted.org/packages/6f/11/65eb49c0c0001167e97edba2bf801a9e37dc49ff41d1f7d88c311421dc80/$_pkgname-$pkgver.tar.gz")
sha512sums=('f6304803f9533c8caded7e28feabe18367ee436af8a1a138344104708bd46eb7bbf0e0cad347a4e604b821463c4039b0de4ad3e72c6a39cb104d78e7d393fa62')

prepare() {
  cp -a "$_pkgname-$pkgver"{,-2}
}

build() {
  cd "$_pkgname-$pkgver-2"

  python2 setup.py build

  cd "$srcdir/$_pkgname-$pkgver"

  python setup.py build
}

package_python2-aiosqlite() {
  depends=('python2')

  cd "$_pkgname-$pkgver-2"

  python2 setup.py install --prefix=/usr --root="$pkgdir" -O1 --skip-build
}

package_python-aiosqlite() {
  depends=('python')

  cd "$_pkgname-$pkgver"

  python setup.py install --prefix=/usr --root="$pkgdir" -O1 --skip-build
}

