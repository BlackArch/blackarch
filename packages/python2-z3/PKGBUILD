# This file is part of BlackArch Linux ( https://www.blackarch.org/ ).
# See COPYING for license details.

pkgname=python2-z3
pkgver=0.2.0
pkgrel=1
pkgdesc='High-performance theorem prover.'
url='https://github.com/Z3Prover/z3'
arch=('x86_64' 'armv6h' 'armv7h' 'aarch64')
license=('MIT')
depends=('python2' 'libz3')
makedepends=('cmake' 'python2' 'python2-setuptools' 'gcc-libs' 'gmp')
source=("https://files.pythonhosted.org/packages/bc/00/c1f1dfb34f5975c0d5f03e108b0669246026b83512b755e9bc725638219d/z3-0.2.0.tar.gz")
sha512sums=('4668d77ab33c2d7338edaf0097ace7d4c1c2872dbfcc8b1c8005a626ae729348a6525f8d811faf0114242bf17167b0b95ec62104f1abcb07c67d6db0dceec86f')

prepare() {
  cd "z3-z3-$pkgver"

  mkdir -p build
}

build() {
  cd "z3-z3-$pkgver/build"

  cmake .. -DCMAKE_BUILD_TYPE=Release -DCMAKE_INSTALL_PREFIX=/usr \
    -DCMAKE_INSTALL_LIBDIR=lib -DBUILD_PYTHON_BINDINGS=1 \
    -DPYTHON_EXECUTABLE=/usr/bin/python2 -DUSE_LIB_GMP=1
  make
}

package() {
  cd "z3-z3-$pkgver/build"

  make DESTDIR="$pkgdir" install

  install -Dm 644 ../LICENSE.txt -t "$pkgdir/usr/share/licenses/$pkgname/"

  cd $pkgdir

  rm -r usr/include usr/bin usr/lib/cmake usr/lib/lib*

  python2 -m compileall .
  python2 -O -m compileall .
}

