# Maintainer: lsfxz (https://github.com/lsfxz/quarry)
# This file is part of BlackArch Linux ( http://blackarch.org ).
# See COPYING for license details.

pkgname='ruby-twitter'
_gemname='twitter'
pkgver='6.1.0'
pkgrel=2
pkgdesc="A Ruby interface to the Twitter API."
arch=('any')
url='http://sferik.github.com/twitter/'
license=("MIT")
depends=('ruby' 'ruby-addressable' 'ruby-buftok' 'ruby-equalizer' 'ruby-faraday-0.11' 'ruby-http-2' 'ruby-http_parser.rb' 'ruby-memoizable' 'ruby-naught' 'ruby-simple_oauth')
options=('!emptydirs')
source=("https://rubygems.org/downloads/$_gemname-$pkgver.gem" )
noextract=("$_gemname-$pkgver.gem")
sha1sums=('93c1a0d373dbe61e7418ddff93971dfa71645478' )


package() {
  local _gemdir="$(ruby -rubygems -e 'puts Gem.default_dir')"
  gem install --ignore-dependencies --no-document --no-user-install -i "$pkgdir/$_gemdir" -n "$pkgdir"/usr/bin $_gemname-$pkgver.gem 
  rm "$pkgdir/$_gemdir/cache/$_gemname-$pkgver.gem"
  install -D -m644 "$pkgdir/$_gemdir/gems/$_gemname-$pkgver/LICENSE.md" "$pkgdir/usr/share/licenses/$pkgname/LICENSE.md"

  # Preserve library and other required directories. Remove junk that gem installs.
  local _copydir=`mktemp -d -t $_gemname.XXX`
  chmod 755 $_copydir
  (cd "$pkgdir/$_gemdir/gems/$_gemname-$pkgver"/ && cp -r --parents lib $_copydir)
  rm -rf "$pkgdir/$_gemdir/gems/$_gemname-$pkgver"
  mv $_copydir "$pkgdir/$_gemdir/gems/$_gemname-$pkgver"

}
