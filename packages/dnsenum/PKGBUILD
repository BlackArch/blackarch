# This file is part of BlackArch Linux ( http://blackarch.org ).
# See COPYING for license details.

pkgname=dnsenum
pkgver=1.2.3
pkgrel=1
groups=('blackarch' 'blackarch-recon' 'blackarch-scanner')
pkgdesc="Perl script that enumerates DNS information from a domain, attempts zone transfers, performs a brute force dictionary style attack, and then performs reverse look-ups on the results"
arch=('i686' 'x86_64' 'armv6h' 'armv7h')
url='http://www2.packetstormsecurity.org/cgi-bin/search/search.cgi?searchvalue=dnsenum'
license=('GPL')
depends=('perl' 'perl-net-dns' 'perl-net-ip' 'perl-net-netmask'
         'perl-www-mechanize' 'perl-net-whois-ip' 'perl-xml-writer')
source=("http://packetstorm.foofus.com/UNIX/scanners/dnsenum-$pkgver.tar.gz")
md5sums=('55bbe85fa07d304498a91206efa8f993')

package() {
  cd "$srcdir"

  # Base directories.
  install -dm755 "$pkgdir/usr/bin"
  install -dm755 "$pkgdir/usr/share/doc/dnsenum"

  # Docs.
  install -Dm644 README.txt "$pkgdir/usr/share/doc/dnsenum/README.txt"

  # Script.
  install -Dm755 dnsenum.pl "$pkgdir/usr/share/dnsenum/dnsenum.pl"

  # Brute-force list.
  install -Dm644 dns.txt "$pkgdir/usr/share/dnsenum/dns.txt"

  # Link.
  ln -sf ../share/dnsenum/dnsenum.pl "$pkgdir/usr/bin/dnsenum"
}
