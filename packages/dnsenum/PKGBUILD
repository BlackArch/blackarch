# This file is part of BlackArch Linux ( https://www.blackarch.org/ ).
# See COPYING for license details.

pkgname=dnsenum
pkgver=18.e3336c5
pkgrel=1
groups=('blackarch' 'blackarch-recon' 'blackarch-scanner')
pkgdesc='Script that enumerates DNS information from a domain, attempts zone transfers, performs a brute force dictionary style attack, and then performs reverse look-ups on the results.'
arch=('any')
url='http://www2.packetstormsecurity.org/cgi-bin/search/search.cgi?searchvalue=dnsenum'
license=('GPL')
depends=('perl' 'perl-net-dns' 'perl-net-ip' 'perl-net-netmask'
         'perl-www-mechanize' 'perl-net-whois-ip' 'perl-xml-writer'
         'perl-string-random')
source=('git+https://github.com/fwaeytens/dnsenum.git')
sha512sums=('SKIP')

pkgver() {
  cd $pkgname

  echo $(git rev-list --count HEAD).$(git rev-parse --short HEAD)
}

package() {
  cd "$pkgname"

  install -Dm 755 dnsenum.pl "$pkgdir/usr/bin/$pkgname"
  install -Dm 644 dns.txt "$pkgdir/usr/share/$pkgname/dns.txt"
  install -Dm 644 README.md "$pkgdir/usr/share/doc/$pkgname/README.md"
  install -m 644 INSTALL.txt "$pkgdir/usr/share/doc/$pkgname/INSTALL.txt"
}

