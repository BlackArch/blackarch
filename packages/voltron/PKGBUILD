# This file is part of BlackArch Linux ( http://blackarch.org ).
# See COPYING for license details.

pkgname=voltron
pkgver=596.4ee3cbe
pkgrel=2
pkgdesc="UI for GDB, LLDB and Vivisect's VDB."
groups=('blackarch' 'blackarch-debugger')
arch=('any')
url='https://github.com/snare/voltron'
license=('MIT')
depends=('python' 'python-pygments' 'python-blessed' 'python-flask'
         'python-flask-restful' 'python-requests-unixsocket' 'python-argparse'
         'python-scruffy' 'python-pysigset' 'python-pexpect' 'python-requests'
         'python-werkzeug' 'python-six' 'python-mock' 'python-nose')
makedepends=('git' 'python-setuptools')
optdepends=('gdb: GDB' 'lldb: LLDB' 'vivisect: VDB')
source=('git+https://github.com/snare/voltron.git'
        'git+https://github.com/snare/voltron.wiki')
sha512sums=('SKIP'
            'SKIP')

pkgver() {
  cd "$srcdir/voltron"

  echo $(git rev-list --count HEAD).$(git rev-parse --short HEAD)
}

prepare() {
  cd "$srcdir/voltron"

  python setup.py build
}

package() {
  cd "$srcdir/voltron"

  python setup.py install --root="$pkgdir" --optimize=1

  install -Dm644 -t "$pkgdir/usr/share/doc/voltron/" README.md
  cp -a "$srcdir/voltron.wiki" "$pkgdir/usr/share/doc/voltron/"
  install -Dm644 LICENSE "$pkgdir/usr/share/licenses/voltron/LICENSE"
}
