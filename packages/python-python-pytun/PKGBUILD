# This file is part of BlackArch Linux ( https://www.blackarch.org/ ).
# See COPYING for license details.

pkgbase=python-python-pytun
pkgname=('python2-python-pytun' 'python-python-pytun')
_pkgname=python-pytun
pkgver=2.3.0
pkgrel=5
pkgdesc='A Python module which let you create TUN/TAP device very easily.'
arch=('x86_64' 'aarch64')
url='https://pypi.org/project/python-pytun/#files'
license=('MIT')
makedepends=('python2-setuptools' 'python-setuptools')
replaces=('python2-pytun' 'python-pytun')
source=("https://files.pythonhosted.org/packages/52/a4/a062106c739eac79c8160fcf5779ebc84afc1c38b016ab216ed1e6da69b6/$_pkgname-$pkgver.tar.gz")
sha512sums=('6627f67052fc87c1f68be8c9b649786319a5661284352ea42a9c51ae38295773c2903452da58950a883cad13cdf2e2ee71c50f7e0368a22e68305f2e50dd0202')

prepare() {
  cp -a "$_pkgname-$pkgver"{,-2}
}

build() {
  cd "$_pkgname-$pkgver-2"

  python2 setup.py build

  cd "$srcdir/$_pkgname-$pkgver"

  python setup.py build
}

package_python2-python-pytun() {
  depends=('python2')

  cd "$_pkgname-$pkgver-2"

  python2 setup.py install --prefix=/usr --root="$pkgdir" --optimize=1 \
    --skip-build
}

package_python-python-pytun() {
  depends=('python')

  cd "$_pkgname-$pkgver"

  python setup.py install --prefix=/usr --root="$pkgdir" --optimize=1 \
    --skip-build
}

