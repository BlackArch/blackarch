# This file is part of BlackArch Linux ( http://blackarch.org ).
# See COPYING for license details.

pkgname='ropgadget'
pkgver='5.4'
pkgrel=5
groups=('blackarch' 'blackarch-exploitation' 'blackarch-binary')
pkgdesc='Lets you search your gadgets on your binaries (ELF format) to facilitate your ROP exploitation.'
url='https://github.com/JonathanSalwan/ROPgadget'
arch=('any')
license=('GPL')
depends=('gcc' 'python' 'python-capstone' 'python2' 'python2-capstone')
makedepends=('python-setuptools' 'python2-setuptools')
source=("https://github.com/JonathanSalwan/ROPgadget/archive/v${pkgver}.tar.gz")
sha1sums=('10eb7169f01b78968517b9f6220215c5d72e0bd6')

build() {
  cd "$srcdir/ROPgadget-$pkgver"

  python2 setup.py build
  python3 setup.py build
}

package() {
  cd "$srcdir/ROPgadget-$pkgver"

  mkdir -p "$pkgdir/usr/share/doc/ropgadget"

  python2 setup.py install --root="$pkgdir" --optimize=1
  python3 setup.py install --root="$pkgdir" --optimize=1

  mv "$pkgdir/usr/bin/ROPgadget" "$pkgdir/usr/bin/ropgadget"

  cp -r --no-preserve=ownership AUTHORS COPYING README.md \
    "$pkgdir/usr/share/doc/ropgadget"
}
