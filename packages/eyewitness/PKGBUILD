# This file is part of BlackArch Linux ( https://www.blackarch.org/ ).
# See COPYING for license details.

pkgname=eyewitness
pkgver=780.03280e2
pkgrel=1
groups=('blackarch' 'blackarch-webapp' 'blackarch-recon' 'blackarch-misc')
pkgdesc='Designed to take screenshots of websites, provide some server header info, and identify default credentials if possible.'
arch=('any')
url='https://github.com/ChrisTruncer/EyeWitness'
license=('GPL3')
depends=('python2' 'python2-pyqt4' 'python2-netaddr' 'ghost-py' 'python2-rdpy'
         'python2-selenium' 'python2-rsa' 'xorg-server-xvfb'
         'python2-beautifulsoup4' 'python2-pyvirtualdisplay'
         'python2-fuzzywuzzy' 'python2-pillow' 'geckodriver' 'firefox'
         'python2-easyprocess' 'python2-pytesseract')
makedepends=('git')
source=("$pkgname::git+https://github.com/FortyNorthSecurity/EyeWitness.git")
sha512sums=('SKIP')

pkgver() {
  cd $pkgname

  echo $(git rev-list --count HEAD).$(git rev-parse --short HEAD)
}

prepare() {
  cd $pkgname

  grep -iRl 'python' * | xargs sed -i 's|python$|python2|'
}

package() {
  cd $pkgname

  install -dm 755 "$pkgdir/usr/bin"
  install -dm 755 "$pkgdir/usr/share/$pkgname"

  install -Dm 644 -t "$pkgdir/usr/share/doc/$pkgname/" README.md CHANGELOG
  install -Dm 644 LICENSE "$pkgdir/usr/share/licenses/$pkgname/LICENSE"

  rm README.md CHANGELOG LICENSE

  cp -a * "$pkgdir/usr/share/$pkgname/"

  cat > "$pkgdir/usr/bin/$pkgname" << EOF
#!/bin/sh
cd /usr/share/$pkgname
exec python2 EyeWitness.py "\$@"
EOF

  chmod +x "$pkgdir/usr/bin/$pkgname"
}

