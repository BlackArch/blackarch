# This file is part of BlackArch Linux ( http://blackarch.org ).
# See COPYING for license details.

pkgname='eyewitness'
pkgver=677.4005a20
pkgrel=1
groups=('blackarch' 'blackarch-webapp' 'blackarch-recon' 'blackarch-misc')
pkgdesc='Designed to take screenshots of websites, provide some server header info, and identify default credentials if possible.'
arch=('any')
url='https://github.com/ChrisTruncer/EyeWitness'
license=('GPL3')
depends=('python2' 'python2-pyqt4' 'python2-netaddr' 'ghost-py' 'python2-rdpy'
         'python2-selenium' 'python2-rsa' 'xorg-server-xvfb'
         'python2-pyvirtualdisplay' 'python2-easyprocess')
makedepends=('git')
source=('git+https://github.com/ChrisTruncer/EyeWitness.git')
sha1sums=('SKIP')

pkgver() {
  cd "$srcdir/EyeWitness"

  echo $(git rev-list --count HEAD).$(git rev-parse --short HEAD)
}

package() {
  cd "$srcdir/EyeWitness"

  mkdir -p "$pkgdir/usr/bin"
  mkdir -p "$pkgdir/usr/share/eyewitness"

  install -Dm644 -t "$pkgdir/usr/share/doc/eyewitness/" README.md CHANGELOG
  install -Dm644 LICENSE "$pkgdir/usr/share/licenses/eyewitness/LICENSE"

  rm README.md CHANGELOG LICENSE

  cp -a * "$pkgdir/usr/share/eyewitness"

  cat > "$pkgdir/usr/bin/eyewitness" << EOF
#!/bin/sh
cd /usr/share/eyewitness/
exec python2 EyeWitness.py "\$@"
EOF
  chmod +x "$pkgdir/usr/bin/eyewitness"
}
