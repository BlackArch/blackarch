# This file is part of BlackArch Linux ( https://www.blackarch.org/ ).
# See COPYING for license details.

pkgbase=python-r2pipe
pkgname=('python2-r2pipe' 'python-r2pipe')
_pkgname=r2pipe
pkgver=1.4.1
pkgrel=2
pkgdesc='Pipe interface for radare2.'
arch=('any')
url='https://pypi.org/project/r2pipe/#files'
license=('MIT')
makedepends=('python2-setuptools' 'python-setuptools')
source=("https://files.pythonhosted.org/packages/43/11/10bc2eab6f27f553e9afe3bdd6380a4f3874187493106b7887d1c4b29e0e/$_pkgname-$pkgver.tar.gz")
sha512sums=('01e114f3aa1abdb50c459b15d67c4cd406caf3d85e94989184fcb5532c677d3aa43fecffb44e945da8fb533cc103fa7cf8aa15026e7e1fe48406a3a75f2bc114')

prepare() {
  cp -a "$_pkgname-$pkgver"{,-2}
}

build() {
  cd "$_pkgname-$pkgver-2"

  python2 setup.py build

  cd "$srcdir/$_pkgname-$pkgver"

  python setup.py build
}

package_python2-r2pipe() {
  depends=('python2')

  cd "$_pkgname-$pkgver-2"

  python2 setup.py install --prefix=/usr --root="$pkgdir" --optimize=1 \
    --skip-build
}

package_python-r2pipe() {
  depends=('python')

  cd "$_pkgname-$pkgver"

  python setup.py install --prefix=/usr --root="$pkgdir" --optimize=1 \
    --skip-build
}

