# This file is part of BlackArch Linux ( https://www.blackarch.org/ ).
# See COPYING for license details.

pkgbase=python-r2pipe
pkgname=('python2-r2pipe' 'python-r2pipe')
_pkgname=r2pipe
pkgver=1.5.3
pkgrel=2
pkgdesc='Pipe interface for radare2.'
arch=('any')
url='https://pypi.org/project/r2pipe/#files'
license=('MIT')
makedepends=('python2-setuptools' 'python-setuptools')
source=("https://files.pythonhosted.org/packages/fe/2a/7efc950272dcb3093416c5e7cef6181d169f2c1b600e65ec68e1451876ba/$_pkgname-$pkgver.tar.gz")
sha512sums=('2f65581dc89c65a9574ce89f22900b8104b3d2c90e79fa81969347babf2ccc5994b6ab3001b7f617775ab3c6172634a3d777797a4561d1cc5a5521ad900033f6')

prepare() {
  cp -a "$_pkgname-$pkgver"{,-2}
}

build() {
  cd "$_pkgname-$pkgver-2"

  python2 setup.py build

  cd "$srcdir/$_pkgname-$pkgver"

  python setup.py build
}

package_python2-r2pipe() {
  depends=('python2')

  cd "$_pkgname-$pkgver-2"

  python2 setup.py install --prefix=/usr --root="$pkgdir" --optimize=1 \
    --skip-build
}

package_python-r2pipe() {
  depends=('python')

  cd "$_pkgname-$pkgver"

  python setup.py install --prefix=/usr --root="$pkgdir" --optimize=1 \
    --skip-build
}

