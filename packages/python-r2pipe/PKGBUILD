# This file is part of BlackArch Linux ( https://www.blackarch.org/ ).
# See COPYING for license details.

pkgbase=python-r2pipe
pkgname=('python2-r2pipe' 'python-r2pipe')
_pkgname=r2pipe
pkgver=1.5.0
pkgrel=1
pkgdesc='Pipe interface for radare2.'
arch=('any')
url='https://pypi.org/project/r2pipe/#files'
license=('MIT')
makedepends=('python2-setuptools' 'python-setuptools')
source=("https://files.pythonhosted.org/packages/1d/08/c7f0a5e494ee2bf9c33bdd7e28f5a2a34ec0bd0466396fd2d27814517685/$_pkgname-$pkgver.tar.gz")
sha512sums=('90d3bb6bfc9cb5adc8afd5c86d21b7be173847b5aad422c0a9145bfe09ee0bca1a94eab377b620f168723e91a01dc0da4cf60fab2076e1924d6a32414f676c49')

prepare() {
  cp -a "$_pkgname-$pkgver"{,-2}
}

build() {
  cd "$_pkgname-$pkgver-2"

  python2 setup.py build

  cd "$srcdir/$_pkgname-$pkgver"

  python setup.py build
}

package_python2-r2pipe() {
  depends=('python2')

  cd "$_pkgname-$pkgver-2"

  python2 setup.py install --prefix=/usr --root="$pkgdir" --optimize=1 \
    --skip-build
}

package_python-r2pipe() {
  depends=('python')

  cd "$_pkgname-$pkgver"

  python setup.py install --prefix=/usr --root="$pkgdir" --optimize=1 \
    --skip-build
}

