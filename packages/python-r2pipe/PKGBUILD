# This file is part of BlackArch Linux ( https://www.blackarch.org/ ).
# See COPYING for license details.

pkgbase=python-r2pipe
pkgname=('python2-r2pipe' 'python-r2pipe')
_pkgname=r2pipe
pkgver=1.7.1
pkgrel=1
pkgdesc='Pipe interface for radare2.'
arch=('any')
url='https://pypi.org/project/r2pipe/#files'
license=('MIT')
makedepends=('python2-setuptools' 'python-setuptools')
source=("https://files.pythonhosted.org/packages/07/f7/c141cc5aeabeab5866495ac5c1b36a14dacce245d2df9fd18ec87b47708f/$_pkgname-$pkgver.tar.gz")
sha512sums=('ca73105a83c4604069e53a5712cfe803c21f3482126a150e4459c7a68dfd2f3eaed9aecbe449ca1e8de5eccfa419afdcf4f2ab6cde9c7604a618484b132e7e77')

prepare() {
  cp -a "$_pkgname-$pkgver"{,-2}
}

build() {
  cd "$_pkgname-$pkgver-2"

  python2 setup.py build

  cd "$srcdir/$_pkgname-$pkgver"

  python setup.py build
}

package_python2-r2pipe() {
  depends=('python2')

  cd "$_pkgname-$pkgver-2"

  python2 setup.py install --prefix=/usr --root="$pkgdir" --optimize=1 \
    --skip-build
}

package_python-r2pipe() {
  depends=('python')

  cd "$_pkgname-$pkgver"

  python setup.py install --prefix=/usr --root="$pkgdir" --optimize=1 \
    --skip-build
}

