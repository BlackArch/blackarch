# This file is part of BlackArch Linux ( http://blackarch.org ).
# See COPYING for license details.

pkgname=binaryninja-demo
pkgver=1.1.1259
pkgrel=1
pkgdesc='A new kind of reversing platform (demo version).'
groups=('blackarch' 'blackarch-reversing' 'blackarch-binary')
arch=('x86_64')
url='http://binary.ninja/demo.html'
license=('commercial' 'evaluation')
source=('https://cdn.binary.ninja/installers/BinaryNinja-demo.zip'
        'LICENSE')
sha512sums=('e524279b54bcdd185975541a6ed67e19f3e90f3d43656faeffdbc769042e8a8ff76075823bf376833dbaac2f98e040ff8aee13ff9425ad1f94c6be4a0a3cbbb0'
            '34c2406eb4cd7f80291008753ee72d06e746d95a2c0c63a336b72ccecc4e0eba2449f5e19490d1b44836db6356a90f175f953305962c345d223063ded5abf14a')
install='binaryninja-demo.install'

package() {
  cd "$srcdir/binaryninja"

  install -dm 755 "$pkgdir/usr/bin"
  install -dm 755 "$pkgdir/usr/share/binaryninja-demo"

  install -Dm644 $srcdir/LICENSE \
    "$pkgdir/usr/share/licenses/binaryninja-demo/LICENSE"

  cp -a * "$pkgdir/usr/share/binaryninja-demo"

  cat > "$pkgdir/usr/bin/binaryninja-demo" << EOF
#!/bin/sh
cd /usr/share/binaryninja-demo
exec ./binaryninja "\${@}"
EOF

  chmod a+x "$pkgdir/usr/bin/binaryninja-demo"
}
