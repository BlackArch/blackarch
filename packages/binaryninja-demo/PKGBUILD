# This file is part of BlackArch Linux ( https://www.blackarch.org/ ).
# See COPYING for license details.

pkgname=binaryninja-demo
pkgver=3.2.3814
pkgrel=1
pkgdesc='A new kind of reversing platform (demo version).'
groups=('blackarch' 'blackarch-reversing' 'blackarch-binary')
arch=('x86_64')
url='http://binary.ninja/demo.html'
license=('custom:commercial' 'evaluation')
depends=('libglvnd' 'libxkbcommon-x11')
source=('https://cdn.binary.ninja/installers/BinaryNinja-demo.zip'
        'LICENSE')
sha512sums=('e46569917eceb8eeacf36ff911c79be0db292c43bfd68e26c95bebf4aff00bf31b763a9a1dcbabf3f1ccc39cda864e5fb328847465327e8ca5fcc2337a45e9ea'
            '34c2406eb4cd7f80291008753ee72d06e746d95a2c0c63a336b72ccecc4e0eba2449f5e19490d1b44836db6356a90f175f953305962c345d223063ded5abf14a')
install="$pkgname.install"

package() {
  cd binaryninja

  install -dm 755 "$pkgdir/usr/bin"
  install -dm 755 "$pkgdir/usr/share/$pkgname"

  install -Dm 644 $srcdir/LICENSE \
    "$pkgdir/usr/share/licenses/$pkgname/LICENSE"

  cp -a * "$pkgdir/usr/share/$pkgname"

  cat > "$pkgdir/usr/bin/$pkgname" << EOF
#!/bin/sh
cd /usr/share/$pkgname
exec ./binaryninja "\$@"
EOF

  chmod +x "$pkgdir/usr/bin/$pkgname"
}

