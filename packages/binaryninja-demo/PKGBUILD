# This file is part of BlackArch Linux ( http://blackarch.org ).
# See COPYING for license details.

pkgname='binaryninja-demo'
pkgver='1.1.922'
pkgrel=1
pkgdesc='A new kind of reversing platform (demo version).'
groups=('blackarch' 'blackarch-reversing' 'blackarch-binary')
arch=('x86_64')
url='http://binary.ninja/demo.html'
license=('commercial' 'evaluation')
source=('https://cdn.binary.ninja/installers/BinaryNinja-demo.zip'
        'LICENSE')
sha1sums=('502f10c6ae5a3b2b9b80c4c26e5c80303232731a'
          '40523205c9b45c720a147be950f40c1e0afe0153')
install='binaryninja-demo.install'

package() {
  cd "$srcdir/binaryninja"

  mkdir -p "$pkgdir/usr/bin"
  mkdir -p "$pkgdir/usr/share/binaryninja-demo"

  install -Dm644 $srcdir/LICENSE \
    "$pkgdir/usr/share/licenses/binaryninja-demo/LICENSE"

  cp -a * "$pkgdir/usr/share/binaryninja-demo"

  cat > "$pkgdir/usr/bin/binaryninja-demo" << EOF
#!/bin/sh
cd /usr/share/binaryninja-demo
exec ./binaryninja "\${@}"
EOF

  chmod a+x "$pkgdir/usr/bin/binaryninja-demo"
}
