# This file is part of BlackArch Linux ( http://blackarch.org ).
# See COPYING for license details.

pkgname=maryam
pkgver=10.26af6d2
pkgrel=2
epoch=1
pkgdesc='Tool to scan Web application and networks and easily and complete the information gathering process.'
groups=('blackarch' 'blackarch-scanner' 'blackarch-webapp' 'blackarch-recon')
arch=('any')
url='https://github.com/black-brain/maryam'
license=('MIT')
depends=('python2' 'python2-python-whois')
makedepends=('git')
source=('git+https://github.com/saeeddhqan/maryam')
sha512sums=('SKIP')

pkgver() {
  cd "$srcdir/maryam"

  echo $(git rev-list --count HEAD).$(git rev-parse --short HEAD)
}

package() {
  cd "$srcdir/maryam"

  install -dm 755 "$pkgdir/usr/bin"
  install -dm 755 "$pkgdir/usr/share/maryam"

  install -Dm644 -t "$pkgdir/usr/share/doc/maryam/" README.md
  install -Dm644 LICENSE "$pkgdir/usr/share/licenses/maryam/LICENSE"

  rm README.md LICENSE

  cp -a * "$pkgdir/usr/share/maryam/"

  cat > "$pkgdir/usr/bin/maryam" << EOF
#!/bin/sh
cd /usr/share/maryam
exec python2 maryam.py "\${@}"
EOF

  chmod a+x "$pkgdir/usr/bin/maryam"
}
