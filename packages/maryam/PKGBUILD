# This file is part of BlackArch Linux ( https://www.blackarch.org/ ).
# See COPYING for license details.

pkgname=maryam
pkgver=10.26af6d2
pkgrel=2
epoch=1
pkgdesc='Tool to scan Web application and networks and easily and complete the information gathering process.'
groups=('blackarch' 'blackarch-scanner' 'blackarch-webapp' 'blackarch-recon')
arch=('any')
url='https://github.com/black-brain/maryam'
license=('MIT')
depends=('python2' 'python2-python-whois')
makedepends=('git')
source=('git+https://github.com/saeeddhqan/maryam')
sha512sums=('SKIP')

pkgver() {
  cd $pkgname

  echo $(git rev-list --count HEAD).$(git rev-parse --short HEAD)
}

package() {
  cd $pkgname

  install -dm 755 "$pkgdir/usr/bin"
  install -dm 755 "$pkgdir/usr/share/$pkgname"

  install -Dm 644 -t "$pkgdir/usr/share/doc/$pkgname/" README.md
  install -Dm 644 LICENSE "$pkgdir/usr/share/licenses/$pkgname/LICENSE"

  rm README.md LICENSE

  cp -a * "$pkgdir/usr/share/$pkgname/"

  cat > "$pkgdir/usr/bin/$pkgname" << EOF
#!/bin/sh
cd /usr/share/$pkgname
exec python2 maryam.py "\$@"
EOF

  chmod a+x "$pkgdir/usr/bin/$pkgname"
}

