# This file is part of BlackArch Linux ( https://www.blackarch.org/ ).
# See COPYING for license details.

pkgname=g72x++
pkgver=1
pkgrel=3
pkgdesc='Decoder for the g72x++ codec.'
arch=('x86_64' 'aarch64')
groups=('blackarch' 'blackarch-wireless')
url='https://www.ps-auxw.de/'
license=('GPL-2.0-or-later')
source=("https://www.ps-auxw.de/g72x++.tar.bz2"
        "fix-build.patch")
sha512sums=('03f13794a643a390c7ecb3f7f6f3158f782ca28d24fe6135b618adf0bae17271e8c72ef96a714c5277c65f1f601a429651b46102accd803d44d22a511b9ea407'
            '781b094fbf4c157a1ff8aa4e9a70a781890262eac1cacd3338b66640cd50e4ce39882ee488735d56b22db3c7acdf97e10ed1055ba399e9afd202550b7ff70b70')

prepare() {
  cd g72x

  patch -p1 < ../fix-build.patch
}

build() {
  cd g72x

  gcc $CFLAGS -o decode-g72x decode.c g*.c bitstream.c
}

package() {
  cd g72x

  install -Dm 755 decode-g72x "$pkgdir/usr/bin/decode-g72x"
  install -Dm 644 -t "$pkgdir/usr/share/doc/decode-g72x/" README
  install -Dm 644 COPYING "$pkgdir/usr/share/licenses/decode-g72x/COPYING"
}

