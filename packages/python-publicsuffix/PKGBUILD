# This file is part of BlackArch Linux ( https://www.blackarch.org/ ).
# See COPYING for license details.

pkgbase=python-publicsuffix
pkgname=('python2-publicsuffix' 'python-publicsuffix')
_pkgname=publicsuffix
pkgver=1.1.0
pkgrel=4
pkgdesc='Get a public suffix for a domain name using the Public Suffix List.'
arch=('any')
url='https://pypi.python.org/pypi/publicsuffix'
license=('MIT')
makedepends=('python2-setuptools' 'python-setuptools')
source=("https://files.pythonhosted.org/packages/76/8e/2be900ba8397bafe88c9d17fab456faddab7af22d7105df18699d3dd97de/$_pkgname-$pkgver.tar.gz")
sha512sums=('d006f14624f84531606659675ed49a3c96f184c30bbb4ac7b512141d70e101a9cf14a91c8e7d57757b5cb0064551bbc996af17e88c0fdc762c3032b3cc8f17ef')

prepare() {
  cp -a "$_pkgname-$pkgver" "$_pkgname-$pkgver-2"
}

build() {
  cd "$_pkgname-$pkgver-2"

  python2 setup.py build

  cd "$srcdir/$_pkgname-$pkgver"

  python setup.py build
}

package_python2-publicsuffix() {
  depends=('python2')

  cd "$_pkgname-$pkgver-2"

  python2 setup.py install --prefix=/usr --root="$pkgdir" --optimize=1 \
    --skip-build
}

package_python-publicsuffix() {
  depends=('python')

  cd "$_pkgname-$pkgver"

  python setup.py install --prefix=/usr --root="$pkgdir" --optimize=1 \
    --skip-build
}

