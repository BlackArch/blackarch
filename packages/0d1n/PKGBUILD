# This file is part of BlackArch Linux ( http://blackarch.org ).
# See COPYING for license details.

pkgname='0d1n'
pkgver=207.ccaf57d
pkgrel=1
groups=('blackarch' 'blackarch-webapp' 'blackarch-fuzzer' 'blackarch-scanner')
pkgdesc='Web security tool to make fuzzing at HTTP inputs, made in C with libCurl.'
arch=('x86_64' 'armv6h' 'armv7h' 'aarch64')
url='https://github.com/CoolerVoid/0d1n'
license=('GPL3')
depends=('curl')
makedepends=('git')
source=('git+https://github.com/CoolerVoid/0d1n.git')
sha1sums=('SKIP')

pkgver() {
  cd "$srcdir/0d1n"

  echo $(git rev-list --count HEAD).$(git rev-parse --short HEAD)
}

build() {
  cd "$srcdir/0d1n"

  make
}

package() {
  cd "$srcdir/0d1n"

  mkdir -p "$pkgdir/usr/share/0d1n"

  install -Dm755 0d1n "$pkgdir/usr/bin/0d1n"

  cp -a payloads response2find response_sources tables "$pkgdir/usr/share/0d1n"

  install -Dm644 -t "$pkgdir/usr/share/doc/0d1n/" README.md README.txt

  cp -a doc/* "$pkgdir/usr/share/doc/0d1n/"
}
