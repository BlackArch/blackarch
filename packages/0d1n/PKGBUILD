# This file is part of BlackArch Linux ( http://blackarch.org ).
# See COPYING for license details.

pkgname=0d1n
pkgver=2.5
pkgrel=1
epoch=1
groups=('blackarch' 'blackarch-webapp' 'blackarch-fuzzer' 'blackarch-scanner')
pkgdesc='Web security tool to make fuzzing at HTTP inputs, made in C with libCurl.'
arch=('x86_64' 'armv6h' 'armv7h' 'aarch64')
url='https://github.com/CoolerVoid/0d1n'
license=('GPL3')
depends=('make')
source=("https://github.com/CoolerVoid/0d1n/archive/$pkgname$pkgver.zip")
sha512sums=('c94eb4831515ed4859740068ee1a88e3fea0228a17e2a9f28f10c17930c8a34076907bb629ad9488eee25a6fc6f81829efd5c1271a1dc577121f82cc2166b22b')

build() {
  cd "$pkgname-$pkgname$pkgver"

  make
}

package() {
  cd "$pkgname-$pkgname$pkgver"

  install -dm 755 "$pkgdir/usr/share/$pkgname"

  install -Dm 755 0d1n "$pkgdir/usr/bin/$pkgname"

  cp -a --no-preserve=ownership payloads response2find response_sources tables "$pkgdir/usr/share/$pkgname"

  install -Dm 644 -t "$pkgdir/usr/share/doc/$pkgname/" README.md README.txt

  cp -a doc/* "$pkgdir/usr/share/doc/$pkgname/"
}
