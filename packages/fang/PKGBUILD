pkgname=fang
pkgver=1.2
pkgrel=4
groups=('blackarchlinux' 'blackarchlinux-cracker')
pkgdesc="A multi service threaded MD5 cracker"
arch=('any')
url='https://github.com/evilsocket/fang'
license=('GPL2')
makedepends=('git')
depends=('python2')
source=('git+https://github.com/evilsocket/fang')
md5sums=('SKIP')
backup=('etc/fang.conf')

prepare() {
  cd "$srcdir/fang"

  # Fix file location.
  sed -i 's|fang\.conf|/etc/fang.conf|' fang.py

  # Fix shebang.
  sed -i 's|python$|python2|' fang.py
}

package() {
  cd "$srcdir/fang"

  # Base directories.
  install -dm755 "$pkgdir/etc"
  install -dm755 "$pkgdir/usr/bin"

  install -m755 fang.py "$pkgdir/usr/bin/fang"
  install -m644 fang.conf "$pkgdir/etc/fang.conf"
}
