# This file is part of BlackArch Linux ( http://blackarch.org ).
# See COPYING for license details.

pkgname='fang'
pkgver=22.4f94552
pkgrel=2
groups=('blackarch' 'blackarch-cracker')
pkgdesc='A multi service threaded MD5 cracker.'
arch=('any')
url='https://github.com/evilsocket/fang'
license=('GPL2')
makedepends=('git')
depends=('python2')
source=('git+https://github.com/evilsocket/fang')
sha1sums=('SKIP')
backup=('etc/fang.conf')

pkgver() {
  cd "$srcdir/fang"

  echo $(git rev-list --count HEAD).$(git rev-parse --short HEAD)
}

prepare() {
  cd "$srcdir/fang"

  # Fix file location.
  sed -i 's|fang\.conf|/etc/fang.conf|' fang.py

  # Fix shebang.
  sed -i 's|python$|python2|' fang.py
}

package() {
  cd "$srcdir/fang"

  install -Dm755 fang.py "$pkgdir/usr/bin/fang"
  install -Dm644 fang.conf "$pkgdir/etc/fang.conf"
}
