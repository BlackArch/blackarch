# This file is part of BlackArch Linux ( https://www.blackarch.org/ ).
# See COPYING for license details.

pkgname=shellinabox
pkgver=431.4f0ecc3
pkgrel=1
pkgdesc='Implements a web server that can export arbitrary command line tools to a web based terminal emulator.'
groups=('blackarch' 'blackarch-backdoor' 'blackarch-webapp')
arch=('x86_64' 'aarch64')
url='https://github.com/shellinabox/shellinabox'
license=('GPL2')
depends=('openssl' 'zlib')
makedepends=('git' 'autoconf')
source=("git+https://github.com/shellinabox/$pkgname.git")
sha512sums=('SKIP')

pkgver() {
  cd $pkgname

  echo $(git rev-list --count HEAD).$(git rev-parse --short HEAD)
}

build() {
  cd $pkgname

  unset CPPFLAGS
  autoreconf -i
  ./configure --prefix=/usr

  make
}

package() {
  cd $pkgname

  make DESTDIR="$pkgdir" install
}

