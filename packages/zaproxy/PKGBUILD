# This file is part of BlackArch Linux ( http://blackarch.org ).
# See COPYING for license details.

pkgname=zaproxy
pkgver=2.2.2
pkgrel=2
groups=('blackarch' 'blackarch-fuzzer' 'blackarch-webapp')
pkgdesc="A local intercepting proxy with integrated penetration testing tool for finding vulnerabilities in web applications"
arch=('any')
url="http://code.google.com/p/zaproxy/"
license=('Apache')
depends=('java-environment' 'bash')
source=("http://zaproxy.googlecode.com/files/ZAP_${pkgver}_Linux.tar.gz"
	'patchfile')
md5sums=('479c3e4ba3fa713c4e007cda585166a6'
	 'efdc294928281e2589bcb4fd1c9c2e3a')

package() {
  install -dm755 "$pkgdir/usr/bin"
  install -dm755 "$pkgdir/usr/share/zaproxy"

  cp -r "$srcdir/ZAP_$pkgver"/* "$pkgdir/usr/share/zaproxy"

  # workaround for version parse error when _JAVA_OPTIONS is set
  # NOTE: this will be fixed in next stable release
  cd "$pkgdir/usr/share/zaproxy"
  patch -p0 < "$srcdir/patchfile"

  cat > "$pkgdir/usr/bin/zaproxy" <<EOF
#!/bin/sh
exec /usr/share/zaproxy/zap.sh
EOF
  chmod +x "$pkgdir/usr/bin/zaproxy"
}
