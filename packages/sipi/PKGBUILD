# This file is part of BlackArch Linux ( https://www.blackarch.org/ ).
# See COPYING for license details.

pkgname=sipi
pkgver=13.58f0dcc
pkgrel=1
pkgdesc='Simple IP Information Tools for Reputation Data Analysis.'
groups=('blackarch' 'blackarch-recon' 'blackarch-misc')
arch=('any')
url='https://github.com/ST2Labs/SIPI'
license=('GPL2')
depends=('python2' 'python2-requests' 'shodan')
makedepends=('git')
source=('git+https://github.com/ST2Labs/SIPI.git')
sha512sums=('SKIP')

pkgver() {
  cd SIPI

  echo $(git rev-list --count HEAD).$(git rev-parse --short HEAD)
}

package() {
  cd SIPI

  install -dm 755 "$pkgdir/usr/bin"
  install -dm 755 "$pkgdir/usr/share/$pkgname"

  install -Dm 644 README.md "$pkgdir/usr/share/doc/$pkgname/README.md"

  rm *.md

  cp -a * "$pkgdir/usr/share/$pkgname/"

  cat > "$pkgdir/usr/bin/$pkgname" << EOF
#!/bin/sh
cd /usr/share/$pkgname
exec python2 sipi.py "\$@"
EOF

  chmod +x "$pkgdir/usr/bin/$pkgname"
}

