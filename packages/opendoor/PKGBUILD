# This file is part of BlackArch Linux ( http://blackarch.org ).
# See COPYING for license details.

pkgname='opendoor'
pkgver=314.a5b230a
pkgrel=1
pkgdesc='OWASP WEB Directory Scanner.'
groups=('blackarch' 'blackarch-webapp' 'blackarch-scanner')
arch=('any')
url='https://github.com/stanislav-web/OpenDoor'
license=('GPL3')
depends=('python2' 'python2-urllib3' 'python2-json2html' 'python2-tabulate'
         'python2-backport_collections')
makedepends=('git' 'python2-setuptools')
source=('git+https://github.com/stanislav-web/OpenDoor.git')
sha1sums=('SKIP')

pkgver() {
  cd "$srcdir/OpenDoor"

  echo $(git rev-list --count HEAD).$(git rev-parse --short HEAD)
}

build() {
  cd "$srcdir/OpenDoor"

  python2 setup.py build
}

package() {
  cd "$srcdir/OpenDoor"

  python2 setup.py install --root="$pkgdir"

  install -Dm644 -t "$pkgdir/usr/share/doc/opendoor/" README.md CHANGELOG.md
  install -Dm644 LICENSE "$pkgdir/usr/share/licenses/opendoor/LICENSE"

  mv "$pkgdir/usr/bin/opendoor.py" "$pkgdir/usr/bin/opendoor"

  rm "$pkgdir/usr/lib/python2.7/site-packages/tests/__init__."{py,pyc}
}
