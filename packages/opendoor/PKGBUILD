# This file is part of BlackArch Linux ( http://blackarch.org ).
# See COPYING for license details.

pkgname='opendoor'
pkgver=73.74c1017
pkgrel=1
pkgdesc='OWASP Directory Access scanner.'
groups=('blackarch' 'blackarch-webapp' 'blackarch-scanner')
arch=('any')
url='https://github.com/stanislav-web/OpenDoor'
license=('GPL2')
depends=('python2' 'python2-logging' 'python2-verboselogs' 'python2-urllib3'
         'python2-threadpool' 'python2-linereader' 'python2-coloredlogs'
         'python2-termcolor' 'python2-tabulate' 'python2-colorama')
makedepends=('python2' 'python2-logging' 'python2-verboselogs' 'python2-urllib3'
             'python2-threadpool' 'python2-linereader' 'python2-coloredlogs'
             'python2-termcolor' 'python2-tabulate' 'git' 'python2-setuptools'
             'python2-colorama')
source=('git+https://github.com/stanislav-web/OpenDoor.git')
sha1sums=('SKIP')

pkgver() {
  cd "$srcdir/OpenDoor"

  echo $(git rev-list --count HEAD).$(git rev-parse --short HEAD)
}

build() {
  cd "$srcdir/OpenDoor"

  python2 setup.py build
}

package() {
  cd "$srcdir/OpenDoor"

  python2 setup.py install --root="$pkgdir"

  install -Dm644 -t "$pkgdir/usr/share/doc/opendoor/" README.rst CHANGELOG.rst
  install -Dm644 LICENSE "$pkgdir/usr/share/licenses/opendoor/LICENSE"

  mv "$pkgdir/usr/bin/opendoor.py" "$pkgdir/usr/bin/opendoor"

  rm "$pkgdir/usr/lib/python2.7/site-packages/tests/__init__."{py,pyc}
}
