# This file is part of BlackArch Linux ( https://www.blackarch.org/ ).
# See COPYING for license details.

pkgname=python-m2crypto
_pkgname=${pkgname#python-}
pkgver=0.45.1.r4.ge384b91
pkgrel=1
pkgdesc='A Python crypto and SSL toolkit.'
arch=('x86_64' 'aarch64')
url='https://gitlab.com/m2crypto/m2crypto'
license=('MIT')
depends=('python' 'openssl')
makedepends=('python-build' 'python-pip' 'python-wheel' 'swig')
options=(!emptydirs)
source=("git+https://gitlab.com/m2crypto/$_pkgname.git")
sha512sums=('SKIP')

pkgver() {
  cd $_pkgname

  ( set -o pipefail
    git describe --long --tags --abbrev=7 2>/dev/null |
      sed 's/\([^-]*-g\)/r\1/;s/-/./g' ||
    printf "%s.%s" "$(git rev-list --count HEAD)" \
      "$(git rev-parse --short=7 HEAD)"
  )
}

build() {
  cd $_pkgname

  python -m build --wheel --outdir="$startdir/dist"
}

package() {
  cd $_pkgname

  pip install \
    --verbose \
    --disable-pip-version-check \
    --no-warn-script-location \
    --ignore-installed \
    --no-compile \
    --no-deps \
    --root="$pkgdir" \
    --prefix=/usr \
    --no-index \
    --find-links="file://$startdir/dist" \
    $_pkgname
}

