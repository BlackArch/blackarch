# This file is part of BlackArch Linux ( https://www.blackarch.org/ ).
# See COPYING for license details.

pkgbase=python-xdot
pkgname=('python2-xdot' 'python-xdot')
_pkgname=xdot
pkgver=1.2
pkgrel=2
pkgdesc='Interactive viewer for Graphviz dot files.'
arch=('any')
url='https://pypi.org/project/xdot/#files'
license=('LGPL')
makedepends=('python2-setuptools' 'python-setuptools')
conflicts=('xdot')
provides=('xdot')
source=("https://files.pythonhosted.org/packages/8b/f5/f5282a470a1c0f16b6600edae18ffdc3715cdd6ac8753205df034650cebe/$_pkgname-$pkgver.tar.gz")
sha512sums=('4e42f2e97356b0c829c120984dd9a0298c8c4aa0e2e4f0de6087bc8de735db1d16e85fd0e154a208d9e0492b250184875bbf4edb8f7971b0b00379a1b8586586')

prepare() {
  cp -a "$_pkgname-$pkgver"{,-2}
}

build() {
  cd "$_pkgname-$pkgver-2"

  python2 setup.py build

  cd "$srcdir/$_pkgname-$pkgver"

  python setup.py build
}

package_python2-xdot() {
  depends=('python2' 'graphviz' 'pygtk')

  cd "$_pkgname-$pkgver-2"

  python2 setup.py install --prefix=/usr --root="$pkgdir" --optimize=1 \
    --skip-build

  find "$pkgdir/usr/lib/python2.7/site-packages/" -type f -exec chmod 644 {} \;

  mv "$pkgdir/usr/bin/$_pkgname" "$pkgdir/usr/bin/${_pkgname}2"
}

package_python-xdot() {
  depends=('python' 'graphviz' 'pygtk')

  cd "$_pkgname-$pkgver"

  python setup.py install --prefix=/usr --root="$pkgdir" --optimize=1 \
    --skip-build

  find "$pkgdir/usr/lib/python3.9/site-packages/" -type f -exec chmod 644 {} \;
}

