# This file is part of BlackArch Linux ( https://www.blackarch.org/ ).
# See COPYING for license details.

pkgbase=python-xdot
pkgname=('python2-xdot' 'python-xdot')
_pkgname=xdot
pkgver=1.3
_pyver=3.12
pkgrel=2
pkgdesc='Interactive viewer for Graphviz dot files.'
arch=('any')
url='https://pypi.org/project/xdot/#files'
license=('LGPL')
makedepends=('python2-setuptools' 'python-setuptools')
conflicts=('xdot')
provides=('xdot')
source=("https://files.pythonhosted.org/packages/source/${_pkgname::1}/$_pkgname/$_pkgname-$pkgver.tar.gz")
sha512sums=('d1e95ddabc6b659f036f3a84bd8c322cbfbdc63c1bd06a195ad61e363ceb9d57b5203dd8f701330ff8fd992367f7338b48d9038df676a9ca0a759dd211fe7c90')

prepare() {
  cp -a "$_pkgname-$pkgver"{,-2}
}

build() {
  cd "$_pkgname-$pkgver-2"

  python2 setup.py build

  cd "$srcdir/$_pkgname-$pkgver"

  python setup.py build
}

package_python2-xdot() {
  depends=('python2' 'graphviz' 'pygtk')

  cd "$_pkgname-$pkgver-2"

  python2 setup.py install --root="$pkgdir" --prefix=/usr -O1 --skip-build

  find "$pkgdir/usr/lib/python2.7/site-packages/" -type f -exec chmod 644 {} \;

  mv "$pkgdir/usr/bin/$_pkgname" "$pkgdir/usr/bin/${_pkgname}2"
}

package_python-xdot() {
  depends=('python' 'graphviz' 'pygtk')

  cd "$_pkgname-$pkgver"

  python setup.py install --root="$pkgdir" --prefix=/usr -O1 --skip-build

  find "$pkgdir/usr/lib/python$_pyver/site-packages/" -type f \
    -exec chmod 644 {} \;
}

