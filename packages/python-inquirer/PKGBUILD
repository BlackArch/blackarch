# This file is part of BlackArch Linux ( https://www.blackarch.org/ ).
# See COPYING for license details.

pkgname=python-inquirer
_pkgname=inquirer
pkgver=2.9.1
_pyver=3.10
pkgrel=1
pkgdesc='Collection of common interactive command line user interfaces, based on Inquirer.js.'
arch=('any')
url='https://pypi.org/project/inquirer/#files'
license=('MIT')
depends=('python')
makedepends=('python-setuptools')
source=("https://files.pythonhosted.org/packages/03/26/5931d7a568080942ec82989e911b01bae0d179c385d6aff0459158ae83bd/$_pkgname-$pkgver.tar.gz")
sha512sums=('462b1e6099ee942d9603d417bb2d3793659b92b2b36b6d1db895495461e1225618a8b2a85f9c64508dc572f9dadbca27a21bd6b6a9ef58f38a74a0da8479acc5')

prepare() {
  cd "$_pkgname-$pkgver"

  # psst, temp fix!
  touch requirements.txt
  sed -i 's|long_description=read_description()|long_description=""|' setup.py
}

build() {
  cd "$_pkgname-$pkgver"

  python setup.py build
}

package() {
  cd "$_pkgname-$pkgver"

  python setup.py install --root="$pkgdir" --prefix=/usr -O1 --skip-build

  rm -rf "$pkgdir/usr/lib/python$_pyver/site-packages/tests/"
}

