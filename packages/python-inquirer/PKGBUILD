# This file is part of BlackArch Linux ( https://www.blackarch.org/ ).
# See COPYING for license details.

pkgname=python-inquirer
_pkgname=inquirer
pkgver=2.8.0
pkgrel=1
pkgdesc='Collection of common interactive command line user interfaces, based on Inquirer.js.'
arch=('any')
url='https://github.com/magmax/python-inquirer'
license=('MIT')
depends=('python')
makedepends=('python-setuptools')
source=("https://files.pythonhosted.org/packages/fd/f5/f0f9aa6de31b096030f623dc142d4c12a002eb34a924b7df24d795bf3b18/$_pkgname-$pkgver.tar.gz")
sha512sums=('547c9abfc647e2d61362fa04174fb295cb76dd948e6dacbb6a07ae46fd3a518137878a1a56e7316ab3acf86a17b1f364d5cbc4793d3869d11b30ec1ce42a740f')

prepare() {
  cd "$_pkgname-$pkgver"

  # psst, temp fix!
  touch requirements.txt
  sed -i 's|long_description=read_description()|long_description=""|' setup.py
}

build() {
  cd "$_pkgname-$pkgver"

  python setup.py build
}

package() {
  cd "$_pkgname-$pkgver"

  python setup.py install --root="$pkgdir" --prefix=/usr -O1 --skip-build

  rm -rf "$pkgdir/usr/lib/python3.9/site-packages/tests/"
}

