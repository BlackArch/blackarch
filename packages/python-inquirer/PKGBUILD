# This file is part of BlackArch Linux ( https://www.blackarch.org/ ).
# See COPYING for license details.

pkgname=python-inquirer
_pkgname=inquirer
pkgver=2.10.0
_pyver=3.10
pkgrel=1
pkgdesc='Collection of common interactive command line user interfaces, based on Inquirer.js.'
arch=('any')
url='https://pypi.org/project/inquirer/#files'
license=('MIT')
depends=('python')
makedepends=('python-setuptools')
source=("https://files.pythonhosted.org/packages/e5/e3/cd77784d0cca9ab8b5be5a6cd4f72ffec407486207ce4f6edec3fc4b8ece/$_pkgname-$pkgver.tar.gz")
sha512sums=('c8b6d925af51268d21c39d387a4cec233f6cbd4a6e7f9d32eb55957724e3bae2b18d8e484d52af37c8ae1419b65d02439e3460d8805065c332c080397cb0f7e7')

prepare() {
  cd "$_pkgname-$pkgver"

  # psst, temp fix!
  touch requirements.txt
  sed -i 's|long_description=read_description()|long_description=""|' setup.py
}

build() {
  cd "$_pkgname-$pkgver"

  python setup.py build
}

package() {
  cd "$_pkgname-$pkgver"

  python setup.py install --root="$pkgdir" --prefix=/usr -O1 --skip-build

  rm -rf "$pkgdir/usr/lib/python$_pyver/site-packages/tests/"
}

