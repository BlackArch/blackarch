# This file is part of BlackArch Linux ( https://www.blackarch.org/ ).
# See COPYING for license details.

pkgname=python-inquirer
_pkgname=inquirer
pkgver=2.6.3
pkgrel=1
pkgdesc='Collection of common interactive command line user interfaces, based on Inquirer.js.'
arch=('any')
url='https://github.com/magmax/python-inquirer'
license=('MIT')
depends=('python')
makedepends=('python-setuptools')
source=("https://files.pythonhosted.org/packages/e3/80/c8eaa2ba143f017c71879dc2421f1ff2322dfbd72c3814e8089fc4b68f02/$_pkgname-$pkgver.tar.gz")
sha512sums=('7693ca4ac8a942e07470154321fbfcdafdb9cdb56e0721af0edf55542f23d11f486809d15eb3c6c65249aaefdb4048a4663578f314cf7342b4352f4ab561c657')

prepare() {
  cd "$_pkgname-$pkgver"

  # psst, temp fix!
  sed -i 's|long_description=read_description()|long_description=""|' setup.py
}

build() {
  cd "$_pkgname-$pkgver"

  python setup.py build
}

package() {
  cd "$_pkgname-$pkgver"

  python setup.py install --root="$pkgdir" --prefix=/usr -O1 --skip-build

  rm -rf "$pkgdir/usr/lib/python3.8/site-packages/tests/"
}

