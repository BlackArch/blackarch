# This file is part of BlackArch Linux ( https://www.blackarch.org/ ).
# See COPYING for license details.

pkgname=python-inquirer
_pkgname=inquirer
pkgver=3.1.2
_pyver=3.10
pkgrel=1
pkgdesc='Collection of common interactive command line user interfaces, based on Inquirer.js.'
arch=('any')
url='https://pypi.org/project/inquirer/#files'
license=('MIT')
depends=('python')
makedepends=('python-setuptools')
source=("https://files.pythonhosted.org/packages/source/${_pkgname::1}/$_pkgname/$_pkgname-$pkgver.tar.gz")
sha512sums=('d00d23142ec9bd08344b0b11c3fd948901d6587d0afcf15f647a3f3f740dce512d9b5c7456c93a2f233029d274c15de494c15015dafe9676a91bfd8d84a5b83c')

prepare() {
  cd "$_pkgname-$pkgver"

  # psst, temp fix!
  touch requirements.txt
  sed -i 's|long_description=read_description()|long_description=""|' setup.py
}

build() {
  cd "$_pkgname-$pkgver"

  python setup.py build
}

package() {
  cd "$_pkgname-$pkgver"

  python setup.py install --root="$pkgdir" --prefix=/usr -O1 --skip-build

  rm -rf "$pkgdir/usr/lib/python$_pyver/site-packages/tests/"
}

