# This file is part of BlackArch Linux ( http://blackarch.org ).
# See COPYING for license details.

pkgname=python-inquirer
pkgver=2.2.0
pkgrel=2
pkgdesc='Collection of common interactive command line user interfaces, based on Inquirer.js.'
arch=('any')
url='https://pypi.python.org/pypi/inquirer/'
license=('MIT')
depends=('python')
makedepends=('python-setuptools')
source=("https://pypi.python.org/packages/1d/f0/0b109010458a1cef35a391f3748720e801c2e1e71899b5807a47aff5e5da/inquirer-${pkgver}.tar.gz")
sha512sums=('1658a0d72bf12fcd6ea8c1ece14b841b6673111e32c0f52fa74f7cecafdd381d443e3a96012fb0be96b1b46e54197da353ec5817cfbddede4fc683c06faf5076')

prepare() {
  cd "$srcdir/inquirer-$pkgver"

 # psst, temp fix!
 sed -i 's|long_description=read_description()|long_description=""|' setup.py
}

build() {
  cd "$srcdir/inquirer-$pkgver"

  python setup.py build
}

package() {
  cd "$srcdir/inquirer-$pkgver"

  python setup.py install --prefix=/usr --root="$pkgdir" --optimize=1
}
