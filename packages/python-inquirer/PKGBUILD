# This file is part of BlackArch Linux ( https://www.blackarch.org/ ).
# See COPYING for license details.

pkgname=python-inquirer
_pkgname=inquirer
pkgver=3.1.3
_pyver=3.10
pkgrel=1
pkgdesc='Collection of common interactive command line user interfaces, based on Inquirer.js.'
arch=('any')
url='https://pypi.org/project/inquirer/#files'
license=('MIT')
depends=('python')
makedepends=('python-setuptools')
source=("https://files.pythonhosted.org/packages/source/${_pkgname::1}/$_pkgname/$_pkgname-$pkgver.tar.gz")
sha512sums=('13e21976fabf07a1c8ae27741eb70f124a4e37e225da786f36fb4ae196dfee9cef4e44cd25352c3acddcd107fd9beebaf4f230d384d99960f34c144e7686fec1')

prepare() {
  cd "$_pkgname-$pkgver"

  # psst, temp fix!
  touch requirements.txt
  sed -i 's|long_description=read_description()|long_description=""|' setup.py
}

build() {
  cd "$_pkgname-$pkgver"

  python setup.py build
}

package() {
  cd "$_pkgname-$pkgver"

  python setup.py install --root="$pkgdir" --prefix=/usr -O1 --skip-build

  rm -rf "$pkgdir/usr/lib/python$_pyver/site-packages/tests/"
}

