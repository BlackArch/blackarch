# This file is part of BlackArch Linux ( http://blackarch.org ).
# See COPYING for license details.

pkgname='sandsifter'
pkgver=dff6324
pkgrel=1
pkgdesc='The x86 processor fuzzer.'
groups=('blackarch' 'blackarch-fuzzer')
arch=('x86_64')
url='https://github.com/xoreaxeaxeax/sandsifter'
license=('unknown')
depends=('python2' 'capstone')
makedepends=('git' 'make' 'sed')
source=('git+https://github.com/xoreaxeaxeax/sandsifter.git')
md5sums=('SKIP')

pkgver() {
  cd "${srcdir}/${pkgname}"

  echo $(git rev-list --count HEAD).$(git rev-parse --short HEAD)
}

build () {
  cd "${srcdir}/${pkgname}"

  sed -i "s/\/usr\/bin\/python/\/usr\/bin\/python2/" *.py
  sed -i "s/INJECTOR = \".\/injector\"/INJECTOR = \"\/opt\/sandsifter\/injector\"/" sifter.py
  make
}

package() {
  cd "${srcdir}/${pkgname}"

  mkdir -p "${pkgdir}/opt/${pkgname}"
  mkdir -p "${pkgdir}/usr/bin"
  mkdir -p "${pkgdir}/usr/share/docs/${pkgname}"

  install -Dm 644 README.md "${pkgdir}/usr/share/docs/${pkgname}/"

  cp -rft "${pkgdir}/opt/${pkgname}/" \
    disas/ gui/ injector mutator.py pyutil/ sifter.py summarize.py
    
  ln -sf "/opt/${pkgname}/sifter.py" "${pkgdir}/usr/bin/sifter"
  ln -sf "/opt/${pkgname}/summarize.py" "${pkgdir}/usr/bin/summarize"
}
