# This file is part of BlackArch Linux ( https://www.blackarch.org/ ).
# See COPYING for license details.

pkgname=android-apktool
_pkgname=Apktool
pkgver=2.5.0
pkgrel=2
pkgdesc='A tool for reverse engineering Android apk files.'
arch=('x86_64' 'aarch64')
groups=('blackarch' 'blackarch-reversing' 'blackarch-disassembler')
url='https://ibotpeaches.github.io/Apktool/'
license=('Apache')
depends=('java-environment')
makedepends=('git')
source=("$pkgname-$pkgver.tar.gz::https://github.com/iBotPeaches/$_pkgname/archive/v$pkgver.tar.gz"
        'apktool')
sha512sums=('30a355fe8f9c1dc28a246d6c43689cdbf9cd29ca35cb07eb8b635427b5a5036de7b73c760791cadc4108ebb93864f971185c638fec9ef3cc7605c5f9989acf2a'
            'aa30257ea9bb6102893edab8f5926c7a4abbe94601e7b7e6273ddf89150969f99d27601a83d0e17a6163c3d5822df3479f9c1e84ab0e8bf6018e1e076fc275d4')

prepare() {
  sed -i 's|libdir=.*progdir|libdir="/usr/share/android-apktool|' apktool
}

build() {
  cd "$_pkgname-$pkgver"

  ./gradlew build shadowJar
}

package() {
  cd "$_pkgname-$pkgver"

  install -Dm 755 "$srcdir/apktool" "$pkgdir/usr/bin/apktool"
  install -Dm 755 brut.apktool/apktool-cli/build/libs/apktool-cli-all.jar \
    "$pkgdir/usr/share/$pkgname/apktool.jar"
}

