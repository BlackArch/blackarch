# This file is part of BlackArch Linux ( http://blackarch.org ).
# See COPYING for license details.

pkgname='android-apktool'
pkgver='2.3.3'
pkgrel=1
pkgdesc='A tool for reverse engineering Android apk files.'
arch=('x86_64' 'armv6h' 'armv7h' 'aarch64')
groups=('blackarch' 'blackarch-reversing' 'blackarch-disassembler')
url='https://ibotpeaches.github.io/Apktool/'
license=('Apache')
depends=('java-environment' 'android-sdk-build-tools')
source=("https://github.com/iBotPeaches/Apktool/releases/download/v${pkgver}/apktool_${pkgver}.jar"
        'apktool')
sha1sums=('261a8e68c72b0ebf70894c40b3c35176a66d86fe'
          '002c185bdfe6016a6a77ce0e81a2c89cafc675ef')

prepare() {
  cd "$srcdir"

  sed -i 's|libdir=.*progdir|libdir="/usr/share/android-apktool|' apktool
}

package() {
  cd "$srcdir"

  install -Dm755 apktool "$pkgdir/usr/bin/apktool"
  install -Dm644 apktool_${pkgver}.jar \
    "$pkgdir/usr/share/android-apktool/apktool.jar"
}

