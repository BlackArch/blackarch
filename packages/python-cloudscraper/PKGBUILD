# This file is part of BlackArch Linux ( https://www.blackarch.org/ ).
# See COPYING for license details.

pkgname=python-cloudscraper
_pkgname=cloudscraper
pkgver=1.2.66
pkgrel=1
pkgdesc="A Python module to bypass Cloudflare's anti-bot page."
arch=('any')
url='https://pypi.org/project/cloudscraper/'
license=('MIT')
depends=('python-pyparsing' 'python-requests' 'python-requests-toolbelt')
optdepends=('python-js2py: alternative interpreter/solver'
            'nodejs: alternative interpreter/solver')
makedepends=('python-build' 'python-installer' 'python-setuptools'
             'python-wheel')
source=("https://files.pythonhosted.org/packages/source/${_pkgname::1}/$_pkgname/$_pkgname-$pkgver.tar.gz")
sha512sums=('1bdca0834717fea8144bdc687315977cda5de0d4f7c09742ebd9b6e98efcb7734b5996194358fb593b996119e8f31f4cef4063c19a1aeb78dc1c4951f7cb93cb')

build(){
  cd "$_pkgname-$pkgver"

  python -m build --wheel --no-isolation
}

package() {
  cd "$_pkgname-$pkgver"

  python -m installer --destdir="$pkgdir" dist/*.whl

  install -Dm 644 LICENSE "$pkgdir/usr/share/licenses/$pkgname/LICENSE"
}

