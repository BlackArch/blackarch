# This file is part of BlackArch Linux ( https://www.blackarch.org/ ).
# See COPYING for license details.

pkgname=python-cloudscraper
_pkgname=cloudscraper
pkgver=1.2.64
pkgrel=1
pkgdesc="A Python module to bypass Cloudflare's anti-bot page."
arch=('any')
url='https://pypi.org/project/cloudscraper/'
license=('MIT')
depends=('python-pyparsing' 'python-requests' 'python-requests-toolbelt')
optdepends=('python-js2py: alternative interpreter/solver'
            'nodejs: alternative interpreter/solver')
makedepends=('python-build' 'python-installer' 'python-setuptools'
             'python-wheel')
source=("https://files.pythonhosted.org/packages/37/63/e787c9f41a7669a40f4f8b9e5d74fb70f75afe2013b9bf0d6586ef39483b/$_pkgname-$pkgver.tar.gz")
sha512sums=('a7465e37ba28f029af18ad8268aca7e7c54015ff5a5813d580613db7ce1e955fc87159fe392ec401372885730720d7b3af9fa8b0c585af72be27f8dcac06bf5c')

build(){
  cd "$_pkgname-$pkgver"

  python -m build --wheel --no-isolation
}

package() {
  cd "$_pkgname-$pkgver"

  python -m installer --destdir="$pkgdir" dist/*.whl

  install -Dm 644 LICENSE "$pkgdir/usr/share/licenses/$pkgname/LICENSE"
}

