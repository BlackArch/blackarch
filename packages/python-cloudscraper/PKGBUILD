# This file is part of BlackArch Linux ( https://www.blackarch.org/ ).
# See COPYING for license details.

pkgname=python-cloudscraper
_pkgname=cloudscraper
pkgver=1.2.69
pkgrel=2
pkgdesc="A Python module to bypass Cloudflare's anti-bot page."
arch=('any')
url='https://pypi.org/project/cloudscraper/'
license=('MIT')
depends=('python-pyparsing' 'python-requests' 'python-requests-toolbelt')
optdepends=('python-js2py: alternative interpreter/solver'
            'nodejs: alternative interpreter/solver')
makedepends=('python-build' 'python-installer' 'python-setuptools'
             'python-wheel')
source=("https://files.pythonhosted.org/packages/source/${_pkgname::1}/$_pkgname/$_pkgname-$pkgver.tar.gz")
sha512sums=('3ec27486f5fe7de6941a358b1d2bd63a147763c8b0a2612a8c3d2895e1a7b46993f90d62ff497a38ec45d5e4066367dc6b5e91df0735af742ef9dafcc1ffc047')

build(){
  cd "$_pkgname-$pkgver"

  python -m build --wheel --no-isolation
}

package() {
  cd "$_pkgname-$pkgver"

  python -m installer --destdir="$pkgdir" dist/*.whl

  install -Dm 644 LICENSE "$pkgdir/usr/share/licenses/$pkgname/LICENSE"
}

