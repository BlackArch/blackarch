# This file is part of BlackArch Linux ( https://www.blackarch.org/ ).
# See COPYING for license details.

pkgname=webxploiter
pkgver=56.c03fe6b
pkgrel=4
groups=('blackarch' 'blackarch-webapp' 'blackarch-exploitation'
        'blackarch-fuzzer' 'blackarch-scanner')
pkgdesc='An OWASP Top 10 Security scanner.'
arch=('any')
url='https://github.com/xionsec/WebXploiter'
license=('GPL3')
depends=('python' 'python-requests' 'python-termcolor' 'libffi' 'openssl'
         'ghost-py' 'python-ndg-httpsclient')
makedepends=('git')
source=("$pkgname::git+https://github.com/xionsec/WebXploiter.git")
sha512sums=('SKIP')

pkgver() {
  cd $pkgname

  echo $(git rev-list --count HEAD).$(git rev-parse --short HEAD)
}

package() {
  cd $pkgname

  install -dm 755 "$pkgdir/usr/bin"
  install -dm 755 "$pkgdir/usr/share/$pkgname"

  install -Dm 644 -t "$pkgdir/usr/share/doc/$pkgname/" README.md
  install -Dm 644 LICENSE "$pkgdir/usr/share/licenses/$pkgname/LICENSE"

  cp -a * "$pkgdir/usr/share/$pkgname/"

  cat > "$pkgdir/usr/bin/$pkgname" << EOF
#!/bin/sh
cd /usr/share/$pkgname
exec python WebXploiter.py "\$@"
EOF

  chmod +x "$pkgdir/usr/bin/$pkgname"
}

