# This file is part of BlackArch Linux ( https://www.blackarch.org/ ).
# See COPYING for license details.

pkgbase=python-pushover
pkgname=('python2-pushover' 'python-pushover')
_pkgname=pushover
pkgver=0.5
pkgrel=1
pkgdesc='Send messages via https://pushover.net/ to phones.'
arch=('any')
url='https://pypi.org/project/pushover/#files'
license=('BSD')
makedepends=('python2-setuptools' 'python-setuptools')
source=("https://files.pythonhosted.org/packages/73/f0/c241c0c24ab7c610e3a7d4d66ad9c5d60e7aa90bcaa8fc4be5b6d2b82af6/$_pkgname-$pkgver.tar.gz")
sha512sums=('3d9ca921b685f98d8295f0e7a8453dbf4fc16f1de7a1d73dde6ca5199d446068fa7b140c4d4f86d4cab54fb9ed1bd444d55a67573046983cc883684ca4d1d3a0')

prepare() {
  cp -a "$_pkgname-$pkgver" "$_pkgname-$pkgver-2"
}

build() {
  cd "$_pkgname-$pkgver-2"

  python2 setup.py build

  cd "$srcdir/$_pkgname-$pkgver"

  python setup.py build
}

package_python2-pushover() {
  depends=('python2' 'python2-requests')

  cd "$_pkgname-$pkgver-2"

  python2 setup.py install --prefix=/usr --root="$pkgdir" --optimize=1 \
    --skip-build

  mv "$pkgdir/usr/bin/$_pkgname" "$pkgdir/usr/bin/${_pkgname}2"
}

package_python-pushover() {
  depends=('python' 'python-requests')

  cd "$_pkgname-$pkgver"

  python setup.py install --prefix=/usr --root="$pkgdir" --optimize=1 \
    --skip-build
}

