# This file is part of BlackArch Linux ( http://blackarch.org ).
# See COPYING for license details.

pkgname=quicksand-lite
pkgver=28.857210e
pkgrel=1
groups=('blackarch' 'blackarch-defensive' )
pkgdesc='Command line tool for scanning streams within office documents plus xor db attack.'
arch=('x86_64' 'armv6h' 'armv7h' 'aarch64')
url='https://github.com/tylabs/quicksand_lite'
license=('MPL2')
depends=('zlib' 'libzip' 'yara')
makedepends=('git')
source=('git+https://github.com/tylabs/quicksand_lite.git')
sha512sums=('SKIP')

pkgver() {
  cd quicksand_lite

  echo $(git rev-list --count HEAD).$(git rev-parse --short HEAD)
}

build() {
  cd quicksand_lite

  gcc -g -o quicksand quicksand.c -L/usr/lib -lyara -lzip -lz
}

package() {
  cd quicksand_lite

  install -dm 755 "$pkgdir/usr/bin"
  install -dm 755 "$pkgdir/usr/share/$pkgname"

  install -Dm644 -t "$pkgdir/usr/share/doc/$pkgname" INSTALL.txt
  install -Dm644 LICENSE "$pkgdir/usr/share/licenses/$pkgname/LICENSE"

  rm INSTALL.txt LICENSE

  cp -a --no-preserve=ownership * "$pkgdir/usr/share/$pkgname/"

  cat > "$pkgdir/usr/bin/quicksand" << EOF
#!/bin/sh
export QS=/usr/share/$pkgname
exec /usr/share/$pkgname/quicksand "\$@"
EOF

  chmod a+x "$pkgdir/usr/bin/quicksand"
}

