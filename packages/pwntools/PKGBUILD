# This file is part of BlackArch Linux ( https://www.blackarch.org/ ).
# See COPYING for license details.

pkgname=pwntools
pkgver=3.13.0
pkgrel=2
pkgdesc='CTF framework and exploit development library.'
arch=('any')
url='https://github.com/Gallopsled/pwntools'
license=('MIT' 'GPL2' 'BSD')
groups=('blackarch' 'blackarch-disassembler' 'blackarch-reversing')
depends=('python2' 'python2-paramiko' 'python2-mako' 'python2-pyelftools'
         'python2-capstone' 'python2-pyserial' 'python2-requests' 'python-isort'
         'python2-pip' 'python2-tox' 'python2-pygments' 'python2-pysocks'
         'python2-dateutil' 'python2-psutil' 'python2-pypandoc' 'python2-sphinx'
         'python2-intervaltree' 'python2-pysqlite' 'python2-ropgadget'
         'python2-packaging' 'python2-sortedcontainers' 'python2-doc2dash'
         'python2-coveralls' 'python2-unicorn')
makedepends=('python2-setuptools')
source=("https://github.com/Gallopsled/pwntools/archive/$pkgver.tar.gz"
        'setup.py.patch')
install="$pkgname.install"
sha512sums=('6480fef7a2ed2b9dcee15fb8cddbbb4f33fbb9e61648faad2e6736e15218022187d22bb41d832148c76ebae07f512e46784c08994a040fd330856dfb810a033d'
            '686cd97fb2b9c5e557ac45d6ae7eb797e548f96c163ad767a8bb7885ec6e93bd0e860b352122b495199e612cfbd2ce5b7ac0641ea7be51029ce0a7214701cc94')

prepare() {
  patch -p1 < setup.py.patch

  cd "$pkgname-$pkgver"

  sed -i 's|python|python2|' pwnlib/elf/datatypes.py
}

build() {
  cd "$pkgname-$pkgver"

  python2 setup.py build
}

package() {
  cd "$pkgname-$pkgver"

  _tools="asm common constgrep checksec cyclic disasm errno elfdiff elfpatch"
  _tools="$_tools debug disablenx hex main phd pwn pwnstrip scramble shellcraft"
  _tools="$_tools unhex update template"

  install -Dm 644 LICENSE-pwntools.txt \
    "$pkgdir/usr/share/licenses/$pkgname/LICENSE"

  python2 setup.py install --root="$pkgdir" --optimize=1

  for t in $_tools; do
    mv "$pkgdir/usr/bin/$t" "$pkgdir/usr/bin/$pkgname-$t"
  done
}

