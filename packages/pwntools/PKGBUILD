# This file is part of BlackArch Linux ( http://blackarch.org ).
# See COPYING for license details.

pkgname='pwntools'
pkgver='3.11.0'
pkgrel=1
pkgdesc='CTF framework and exploit development library.'
arch=('any')
url='https://github.com/Gallopsled/pwntools'
license=('MIT' 'GPL2' 'BSD')
groups=('blackarch' 'blackarch-disassembler' 'blackarch-reversing')
depends=('python2' 'python2-paramiko' 'python2-mako' 'python2-pyelftools'
         'python2-capstone' 'python2-pyserial' 'python2-requests'
         'python2-pip' 'python2-tox' 'python2-pygments' 'python2-pysocks'
         'python2-dateutil' 'python2-psutil' 'ropgadget' 'python2-pypandoc'
         'python2-intervaltree' 'python2-sphinx' 'python2-pysqlite'
         'python2-packaging' 'python2-sortedcontainers')
makedepends=('python2-setuptools')
source=("https://github.com/Gallopsled/pwntools/archive/${pkgver}.tar.gz")
sha1sums=('8ddabdc9472e4d4023796daf3ab1b6cd24bc0023')

prepare() {
  cd "$srcdir/pwntools-$pkgver"

  sed -i 's|python|python2|' pwnlib/elf/datatypes.py
}

build() {
  cd "$srcdir/pwntools-$pkgver"

  python2 setup.py build
}

package() {
  cd "$srcdir/pwntools-$pkgver"

  tools="asm common constgrep checksec cyclic disasm errno elfdiff elfpatch hex"
  tools="${tools} main phd pwn pwnstrip scramble shellcraft unhex update"

  install -Dm644 LICENSE-pwntools.txt \
    "$pkgdir/usr/share/licenses/$pkgname/LICENSE-pwntools.txt"

  python2 setup.py install --root="$pkgdir" --optimize=1

  for t in $tools
  do
    mv "$pkgdir/usr/bin/${t}" "$pkgdir/usr/bin/pwntools-${t}"
  done
}
