# This file is part of BlackArch Linux ( http://blackarch.org ).
# See COPYING for license details.

pkgname='pwntools'
pkgver='2.1.3'
pkgrel=1
pkgdesc='The CTF framework used by #Gallopsled in every CTF.'
arch=('any')
url='https://github.com/Gallopsled/pwntools'
license=('MIT')
groups=('blackarch' 'blackarch-disassembler' 'blackarch-reversing')
depends=('python2' 'python2-paramiko' 'python2-mako')
makedepends=('python2-setuptools')
source=("https://github.com/Gallopsled/pwntools/archive/${pkgver}.tar.gz")
sha1sums=('a96d9626b5142cd140fc25cac47099e6256cec2d')

prepare() {
  cd "$srcdir/pwntools-$pkgver/bin"

  mv disasm disasm-pwntools
}

build() {
  cd "$srcdir/pwntools-$pkgver"

  python2 setup.py build
}

package() {
  cd "$srcdir/pwntools-$pkgver"

  install -Dm644 LICENSE-pwntools.txt \
    "$pkgdir/usr/share/licenses/$pkgname/LICENSE-pwntools.txt"

  python2 setup.py install --root="$pkgdir" --optimize=1
}
