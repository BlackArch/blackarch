# This file is part of BlackArch Linux ( https://www.blackarch.org/ ).
# See COPYING for license details.

pkgname=pwntools
pkgver=4.3.1
pkgrel=2
pkgdesc='CTF framework and exploit development library.'
arch=('any')
url='https://github.com/Gallopsled/pwntools'
license=('MIT' 'GPL2' 'BSD')
groups=('blackarch' 'blackarch-disassembler' 'blackarch-reversing')
depends=('python' 'python-paramiko' 'python-mako' 'python-pyelftools'
         'python-capstone' 'python-pyserial' 'python-requests' 'python-isort'
         'python-pip' 'python-tox' 'python-pygments' 'python-pysocks'
         'python-dateutil' 'python-psutil' 'python-pypandoc' 'python-sphinx'
         'python-intervaltree' 'ropgadget' 'python-packaging' 'python-six'
         'python-sortedcontainers' 'python-doc2dash' 'python-coveralls'
         'python-unicorn')
makedepends=('python-setuptools')
source=("https://github.com/Gallopsled/pwntools/archive/$pkgver.tar.gz"
        'setup.py.patch')
sha512sums=('ccff05330122fe15f2e417f1719af5e3b078633a717097e365e86534d3010fe486581645df21e60523c5b721e8851e9e23932e96309ab5a487feac9ecfba444f'
            'c0554f0f6eec4de4b92b1980a960d570f540bf107a2378887b8abef64734fd299ea02cce77b586abc54b88ea27d5f22e1f1334ee3bf9401259cff6f823338636')

prepare() {
  patch -p1 < setup.py.patch

  cd "$pkgname-$pkgver"

  sed -i 's|python2|python|' pwnlib/elf/datatypes.py
}

build() {
  cd "$pkgname-$pkgver"

  python setup.py build
}

package() {
  cd "$pkgname-$pkgver"

  _tools="asm common constgrep checksec cyclic disasm errno elfdiff elfpatch"
  _tools="$_tools debug disablenx hex main phd pwn pwnstrip scramble shellcraft"
  _tools="$_tools unhex update template"

  install -Dm 644 LICENSE-pwntools.txt \
    "$pkgdir/usr/share/licenses/$pkgname/LICENSE"

  python setup.py install --root="$pkgdir" --prefix=/usr -O1

  for t in $_tools; do
    mv "$pkgdir/usr/bin/$t" "$pkgdir/usr/bin/$pkgname-$t"
  done
}

