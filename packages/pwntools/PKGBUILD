# This file is part of BlackArch Linux ( https://www.blackarch.org/ ).
# See COPYING for license details.

pkgname=pwntools
pkgver=3.12.2
pkgrel=3
pkgdesc='CTF framework and exploit development library.'
arch=('any')
url='https://github.com/Gallopsled/pwntools'
license=('MIT' 'GPL2' 'BSD')
groups=('blackarch' 'blackarch-disassembler' 'blackarch-reversing')
depends=('python2' 'python2-paramiko' 'python2-mako' 'python2-pyelftools'
         'python2-capstone' 'python2-pyserial' 'python2-requests' 'python-isort'
         'python2-pip' 'python2-tox' 'python2-pygments' 'python2-pysocks'
         'python2-dateutil' 'python2-psutil' 'ropgadget2' 'python2-pypandoc'
         'python2-intervaltree' 'python2-sphinx' 'python2-pysqlite'
         'python2-packaging' 'python2-sortedcontainers' 'python2-doc2dash'
         'python2-coveralls' 'python2-unicorn')
makedepends=('python2-setuptools')
source=("https://github.com/Gallopsled/pwntools/archive/$pkgver.tar.gz"
        'setup.py.patch')
install='pwntools.install'
sha512sums=('6dcb3a83e668b2ab9ac191e66430702b9e62813f6d7872f8ba08a6b1753235c542810eaeef8bed94f4286309db4094e66ee9ff9895bbc049983be8d4c67a01f8'
            '10cdda20a11d9b7dd57c6608ca1dbf4f708dc3b5118129c2a84dcb753592d4d1b1711eebed644c8d529b59f347c012b47e6a74fde344c680cc0e483afb04a231')

prepare() {
  patch -p1 < setup.py.patch

  cd "$pkgname-$pkgver"

  sed -i 's|python|python2|' pwnlib/elf/datatypes.py
}

build() {
  cd "$pkgname-$pkgver"

  python2 setup.py build
}

package() {
  cd "$pkgname-$pkgver"

  _tools="asm common constgrep checksec cyclic disasm errno elfdiff elfpatch"
  _tools="$_tools debug disablenx hex main phd pwn pwnstrip scramble shellcraft"
  _tools="$_tools unhex update template"

  install -Dm 644 LICENSE-pwntools.txt \
    "$pkgdir/usr/share/licenses/$pkgname/LICENSE"

  python2 setup.py install --root="$pkgdir" --optimize=1

  for t in $_tools; do
    mv "$pkgdir/usr/bin/$t" "$pkgdir/usr/bin/$pkgname-$t"
  done
}

