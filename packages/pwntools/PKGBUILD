# This file is part of BlackArch Linux ( https://www.blackarch.org/ ).
# See COPYING for license details.

pkgname=pwntools
pkgver=4.1.3
pkgrel=1
pkgdesc='CTF framework and exploit development library.'
arch=('any')
url='https://github.com/Gallopsled/pwntools'
license=('MIT' 'GPL2' 'BSD')
groups=('blackarch' 'blackarch-disassembler' 'blackarch-reversing')
depends=('python' 'python-paramiko' 'python-mako' 'python-pyelftools'
         'python-capstone' 'python-pyserial' 'python-requests' 'python-isort'
         'python-pip' 'python-tox' 'python-pygments' 'python-pysocks'
         'python-dateutil' 'python-psutil' 'python-pypandoc' 'python-sphinx'
         'python-intervaltree' 'ropgadget' 'python-packaging'
         'python-sortedcontainers' 'python-doc2dash' 'python-coveralls'
         'python-unicorn')
makedepends=('python-setuptools')
source=("https://github.com/Gallopsled/pwntools/archive/$pkgver.tar.gz"
        'setup.py.patch')
sha512sums=('dd0d883990f3eca586a161834f9503e1ed18ab828946f7d9053667cc42f44be20e20bc10e2470bdd0de7eca115445853d81241daad0b7b84f9f2c2cb6900fafc'
            'bdaa75fb4d4c389206b9a620fde88edbebe6950fefea5f0cb37aa1fa653e4a2962eceb003b252bb9a7c0a4715870ceabc15060b0499867601b84a62e0ef97f89')

prepare() {
  patch -p1 < setup.py.patch

  cd "$pkgname-$pkgver"

  sed -i 's|python2|python|' pwnlib/elf/datatypes.py
}

build() {
  cd "$pkgname-$pkgver"

  python setup.py build
}

package() {
  cd "$pkgname-$pkgver"

  _tools="asm common constgrep checksec cyclic disasm errno elfdiff elfpatch"
  _tools="$_tools debug disablenx hex main phd pwn pwnstrip scramble shellcraft"
  _tools="$_tools unhex update template"

  install -Dm 644 LICENSE-pwntools.txt \
    "$pkgdir/usr/share/licenses/$pkgname/LICENSE"

  python setup.py install --root="$pkgdir" --prefix=/usr -O1

  for t in $_tools; do
    mv "$pkgdir/usr/bin/$t" "$pkgdir/usr/bin/$pkgname-$t"
  done
}

