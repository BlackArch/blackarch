# This file is part of BlackArch Linux ( https://www.blackarch.org/ ).
# See COPYING for license details.

pkgname=pwntools
pkgver=4.0.1
pkgrel=2
pkgdesc='CTF framework and exploit development library.'
arch=('any')
url='https://github.com/Gallopsled/pwntools'
license=('MIT' 'GPL2' 'BSD')
groups=('blackarch' 'blackarch-disassembler' 'blackarch-reversing')
depends=('python' 'python-paramiko' 'python-mako' 'python-pyelftools'
         'python-capstone' 'python-pyserial' 'python-requests' 'python-isort'
         'python-pip' 'python-tox' 'python-pygments' 'python-pysocks'
         'python-dateutil' 'python-psutil' 'python-pypandoc' 'python-sphinx'
         'python-intervaltree' 'ropgadget' 'python-packaging'
         'python-sortedcontainers' 'python-doc2dash' 'python-coveralls'
         'python-unicorn')
makedepends=('python-setuptools')
source=("https://github.com/Gallopsled/pwntools/archive/$pkgver.tar.gz"
        'setup.py.patch')
install="$pkgname.install"
sha512sums=('43a116a13c73b5ef3c7b3b0d65bb706d53cce687928f445f35038677835e75ed121f4fe1d298c991b86e82a364b3dc6f2332c0f38c8685fc07950e68bf53c540'
            'b1746bb707d36725e6ca443288ab0d4358bfa3873e5ed57e5fcbbe96977d95f94c2c835441ae386145b12730152f0e977701efc0a79b0e96e31819ac98949a41')

prepare() {
  patch -p1 < setup.py.patch

  cd "$pkgname-$pkgver"

  sed -i 's|python2|python|' pwnlib/elf/datatypes.py
}

build() {
  cd "$pkgname-$pkgver"

  python setup.py build
}

package() {
  cd "$pkgname-$pkgver"

  _tools="asm common constgrep checksec cyclic disasm errno elfdiff elfpatch"
  _tools="$_tools debug disablenx hex main phd pwn pwnstrip scramble shellcraft"
  _tools="$_tools unhex update template"

  install -Dm 644 LICENSE-pwntools.txt \
    "$pkgdir/usr/share/licenses/$pkgname/LICENSE"

  python setup.py install --root="$pkgdir" --prefix=/usr -O1

  for t in $_tools; do
    mv "$pkgdir/usr/bin/$t" "$pkgdir/usr/bin/$pkgname-$t"
  done
}

