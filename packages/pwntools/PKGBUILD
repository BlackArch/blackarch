# This file is part of BlackArch Linux ( https://www.blackarch.org/ ).
# See COPYING for license details.

pkgname=pwntools
pkgver=4.5.1
pkgrel=1
pkgdesc='CTF framework and exploit development library.'
arch=('any')
url='https://github.com/Gallopsled/pwntools'
license=('MIT' 'GPL2' 'BSD')
groups=('blackarch' 'blackarch-disassembler' 'blackarch-reversing')
depends=('python' 'python-paramiko' 'python-mako' 'python-pyelftools'
         'python-capstone' 'python-pyserial' 'python-requests' 'python-isort'
         'python-pip' 'python-tox' 'python-pygments' 'python-pysocks'
         'python-dateutil' 'python-psutil' 'python-pypandoc' 'python-sphinx'
         'python-intervaltree' 'ropgadget' 'python-packaging' 'python-six'
         'python-sortedcontainers' 'python-doc2dash' 'python-coveralls'
         'python-unicorn' 'python-colored-traceback' 'python-rpyc')
makedepends=('python-setuptools')
source=("https://github.com/Gallopsled/pwntools/archive/$pkgver.tar.gz"
        'setup.py.patch')
sha512sums=('6c681fc5e80405764a109466fd7ea51a54c0290a1eb857f1c69e43b0b9af71647e97607f17ed9c9c050be0e2ac65e2bcaed2aa66178fd3c42e4ad02abe1bf6a2'
            'fb2f6343df7021fb76a5b3bc448e076deba34be43104e8fab04947f96345f669bdea8dcadef52614eadb486c780aad65263a07c53b6f973d73a49895c8fb290b')

prepare() {
  patch -p1 < setup.py.patch

  cd "$pkgname-$pkgver"

  sed -i 's|python2|python|' pwnlib/elf/datatypes.py
}

build() {
  cd "$pkgname-$pkgver"

  python setup.py build
}

package() {
  cd "$pkgname-$pkgver"

  _tools="asm common constgrep checksec cyclic disasm errno elfdiff elfpatch"
  _tools="$_tools debug disablenx hex main phd pwn pwnstrip scramble shellcraft"
  _tools="$_tools unhex update template"

  install -Dm 644 LICENSE-pwntools.txt \
    "$pkgdir/usr/share/licenses/$pkgname/LICENSE"

  python setup.py install --root="$pkgdir" --prefix=/usr -O1

  for t in $_tools; do
    mv "$pkgdir/usr/bin/$t" "$pkgdir/usr/bin/$pkgname-$t"
  done
}

