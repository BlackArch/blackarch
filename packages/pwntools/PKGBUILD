# This file is part of BlackArch Linux ( https://www.blackarch.org/ ).
# See COPYING for license details.

pkgname=pwntools
pkgver=4.1.2
pkgrel=1
pkgdesc='CTF framework and exploit development library.'
arch=('any')
url='https://github.com/Gallopsled/pwntools'
license=('MIT' 'GPL2' 'BSD')
groups=('blackarch' 'blackarch-disassembler' 'blackarch-reversing')
depends=('python' 'python-paramiko' 'python-mako' 'python-pyelftools'
         'python-capstone' 'python-pyserial' 'python-requests' 'python-isort'
         'python-pip' 'python-tox' 'python-pygments' 'python-pysocks'
         'python-dateutil' 'python-psutil' 'python-pypandoc' 'python-sphinx'
         'python-intervaltree' 'ropgadget' 'python-packaging'
         'python-sortedcontainers' 'python-doc2dash' 'python-coveralls'
         'python-unicorn')
makedepends=('python-setuptools')
source=("https://github.com/Gallopsled/pwntools/archive/$pkgver.tar.gz"
        'setup.py.patch')
sha512sums=('3cc662f5bd01b9dcd775b82071193bd6ffe3d5148176d17bb6ed29b693d15a3728bab1f126441bbb295514a712cf2f6a9b34de3c1646f6711f1f36bfe4edc4c4'
            '1319e1fd2856ed9d041d9e3ac90c8992ca156f4953a9fdb8f061d4cd5b99d4024195cfe64d02fdfb457e748416b4347c127f8b58a1475f5d6c1083486bc45ff0')

prepare() {
  patch -p1 < setup.py.patch

  cd "$pkgname-$pkgver"

  sed -i 's|python2|python|' pwnlib/elf/datatypes.py
}

build() {
  cd "$pkgname-$pkgver"

  python setup.py build
}

package() {
  cd "$pkgname-$pkgver"

  _tools="asm common constgrep checksec cyclic disasm errno elfdiff elfpatch"
  _tools="$_tools debug disablenx hex main phd pwn pwnstrip scramble shellcraft"
  _tools="$_tools unhex update template"

  install -Dm 644 LICENSE-pwntools.txt \
    "$pkgdir/usr/share/licenses/$pkgname/LICENSE"

  python setup.py install --root="$pkgdir" --prefix=/usr -O1

  for t in $_tools; do
    mv "$pkgdir/usr/bin/$t" "$pkgdir/usr/bin/$pkgname-$t"
  done
}

