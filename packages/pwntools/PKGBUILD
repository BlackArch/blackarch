# This file is part of BlackArch Linux ( http://blackarch.org ).
# See COPYING for license details.

pkgname=pwntools
pkgver=3.12.1
pkgrel=5
pkgdesc='CTF framework and exploit development library.'
arch=('any')
url='https://github.com/Gallopsled/pwntools'
license=('MIT' 'GPL2' 'BSD')
groups=('blackarch' 'blackarch-disassembler' 'blackarch-reversing')
depends=('python2' 'python2-paramiko' 'python2-mako' 'python2-pyelftools'
         'python2-capstone' 'python2-pyserial' 'python2-requests' 'python-isort'
         'python2-pip' 'python2-tox' 'python2-pygments' 'python2-pysocks'
         'python2-dateutil' 'python2-psutil' 'ropgadget2' 'python2-pypandoc'
         'python2-intervaltree' 'python2-sphinx' 'python2-pysqlite'
         'python2-packaging' 'python2-sortedcontainers' 'python2-doc2dash'
         'python2-coveralls')
makedepends=('python2-setuptools')
source=("https://github.com/Gallopsled/pwntools/archive/${pkgver}.tar.gz"
        'setup.py.patch')
install='pwntools.install'
sha512sums=('860355fab575179a1d35b647211ab5c3a24b9e062366dc7e95741ab68f873fe61032aee0fb9ead624ad4b6f623a020547d4cfa25f1cfafeb0623b7ea38720fa7'
            '400157889cb7cd592b49fa668edcdfd9c00f5c293ea14ec7b619ce502c44dbaf6e0e21dfc3dc32d5ab465f9daf62d898ad46f96236a76bc90ab607ea4faa1528')

prepare() {
  patch -p1 < setup.py.patch

  cd "pwntools-${pkgver}"
  sed -i 's|python|python2|' pwnlib/elf/datatypes.py

}

build() {
  cd "pwntools-${pkgver}"

  python2 setup.py build
}

package() {
  cd "pwntools-${pkgver}"

  tools="asm common constgrep checksec cyclic disasm errno elfdiff elfpatch hex"
  tools="${tools} main phd pwn pwnstrip scramble shellcraft unhex update"

  install -Dm644 LICENSE-pwntools.txt \
    "$pkgdir/usr/share/licenses/${pkgname}/LICENSE-pwntools.txt"

  python2 setup.py install --root="${pkgdir}" --optimize=1

  for t in $tools
  do
    mv "$pkgdir/usr/bin/${t}" "$pkgdir/usr/bin/pwntools-${t}"
  done
}

