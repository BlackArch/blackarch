# This file is part of BlackArch Linux ( http://blackarch.org ).
# See COPYING for license details.

pkgname='pwntools'
pkgver='3.0.2'
pkgrel=1
pkgdesc='CTF framework and exploit development library.'
arch=('any')
url='https://github.com/Gallopsled/pwntools'
license=('MIT' 'GPL2' 'BSD')
groups=('blackarch' 'blackarch-disassembler' 'blackarch-reversing')
depends=('python2' 'python2-paramiko' 'python2-mako' 'python2-pyelftools'
         'python2-capstone' 'python2-pyserial' 'python2-requests'
         'python2-pip' 'python2-tox' 'python2-pygments' 'python2-pysocks'
         'python2-dateutil' 'python2-psutil' 'ropgadget')
makedepends=('python2-setuptools')
source=("https://github.com/Gallopsled/pwntools/archive/${pkgver}.tar.gz")
sha1sums=('858d2bf6fa03e10cee03d046e13b084c5dc9b76c')

prepare() {
  cd "$srcdir/pwntools-$pkgver"

  sed -i 's|python|python2|' pwnlib/elf/datatypes.py
}

build() {
  cd "$srcdir/pwntools-$pkgver"

  python2 setup.py build
}

package() {
  cd "$srcdir/pwntools-$pkgver"

  install -Dm644 LICENSE-pwntools.txt \
    "$pkgdir/usr/share/licenses/$pkgname/LICENSE-pwntools.txt"

  python2 setup.py install --root="$pkgdir" --optimize=1

  mv "$pkgdir/usr/bin/asm" "$pkgdir/usr/bin/pwntools-asm"
  mv "$pkgdir/usr/bin/disasm" "$pkgdir/usr/bin/pwntools-disasm"
  mv "$pkgdir/usr/bin/checksec" "$pkgdir/usr/bin/pwntools-checksec"
}
