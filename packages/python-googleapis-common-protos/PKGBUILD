# This file is part of BlackArch Linux ( https://www.blackarch.org/ ).
# See COPYING for license details.

pkgname=python-googleapis-common-protos
_pkgname='googleapis-common-protos'
pkgver=1.57.0
_pyver=3.10
pkgrel=1
pkgdesc='Common protobufs used in Google APIs.'
arch=('any')
url='https://pypi.org/project/googleapis-common-protos/#files'
license=('Apache')
depends=('python')
makedepends=('python-setuptools')
source=("https://files.pythonhosted.org/packages/source/${_pkgname::1}/$_pkgname/$_pkgname-$pkgver.tar.gz")
sha512sums=('254f31631508770754a3bcc72a31705fc80aca87186291db73c20dd85ee209c71d97110dc2c9c742243ccb2121d38d77d3e1dc8a05eb4fc77ce07a2f32f7ef31')

build() {
  cd "$_pkgname-$pkgver"

  python setup.py build
}

package() {
  cd "$_pkgname-$pkgver"

  python setup.py install --prefix=/usr --root="$pkgdir" -O1 --skip-build

  # fix permissions
  chmod 644 \
    "$pkgdir/usr/lib/python$_pyver/site-packages/googleapis_common_protos-1.57.0-py$_pyver.egg-info/"*
}

