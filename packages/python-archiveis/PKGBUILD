# This file is part of BlackArch Linux ( https://www.blackarch.org/ ).
# See COPYING for license details.

pkgbase=python-archiveis
pkgname=('python2-archiveis' 'python-archiveis')
_pkgname=archiveis
pkgver=0.0.8
pkgrel=1
pkgdesc='A simple Python wrapper for the archive.is capturing service.'
arch=('any')
url='https://pypi.org/project/archiveis/#files'
license=('MIT')
makedepends=('python2-setuptools' 'python-setuptools')
source=("https://files.pythonhosted.org/packages/9f/3e/7d5baeac42680434d700cb1c1f38546f0eb45810f20825bc5a1fb91a4e08/$_pkgname-$pkgver.tar.gz")
sha512sums=('1f5a476dd84f46ce9561d51870f8754c16fbef1b9414af2b08fd2e0a983e82da7e158bb259d5281cc66a7e781c4bc15790df0dfe5e4df3a9a1364d4f19137bc9')

prepare() {
  cp -a "$_pkgname-$pkgver"{,-2}
}

build() {
  cd "$_pkgname-$pkgver-2"

  python2 setup.py build

  cd "$srcdir/$_pkgname-$pkgver"

  python setup.py build
}

package_python2-archiveis() {
  depends=('python2' 'python2-click' 'python2-requests' 'python2-six')

  cd "$_pkgname-$pkgver-2"

  python2 setup.py install --prefix=/usr --root="$pkgdir" --optimize=1 \
    --skip-build

  mv "$pkgdir/usr/bin/$_pkgname" "$pkgdir/usr/bin/${_pkgname}2"
}

package_python-archiveis() {
  depends=('python' 'python-click' 'python-requests' 'python-six')

  cd "$_pkgname-$pkgver"

  python setup.py install --prefix=/usr --root="$pkgdir" --optimize=1 \
    --skip-build
}

