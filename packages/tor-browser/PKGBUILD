# This file is part of BlackArch Linux ( https://www.blackarch.org/ ).
# See COPYING for license details.

pkgname=tor-browser
pkgver=12.5.2
pkgrel=1
pkgdesc='Tor Browser Bundle: anonymous browsing using Firefox and Tor.'
url='https://www.torproject.org/projects/torbrowser.html.en'
arch=('x86_64')
groups=('blackarch' 'blackarch-defensive')
license=('GPL')
depends=('libxt' 'startup-notification' 'mime-types' 'dbus-glib' 'alsa-lib'
         'desktop-file-utils' 'hicolor-icon-theme' 'libvpx' 'icu' 'libevent'
         'nss' 'hunspell' 'sqlite' 'zenity')
optdepends=('kdialog: KDE dialog boxes'
            'gst-plugins-good: H.264 video'
            'gst-libav: H.264 video'
            'libpulse: PulseAudio audio driver'
            'libnotify: Gnome dialog boxes')
#source=("https://dist.torproject.org/torbrowser/$pkgver/tor-browser-linux64-${pkgver}_en-US.tar.xz"
source=("https://www.torproject.org/dist/torbrowser/$pkgver/tor-browser-linux64-${pkgver}_ALL.tar.xz"
        "$pkgname.desktop"
        "$pkgname.png"
        "$pkgname.sh"
        "$pkgname.svg")
sha512sums=('256e1b442eeef3718af519d1306e74493603de9bac45714d6ff4e59b7d5ad549fe25fde350c253438acaa5fdcd6c1b7916d48d6a3f8f7a22a6a054579c09c593'
            '1318a652f7b65e30cdb0c607faf4391035288bcfcaffc50f7713de8eeb7d9151c115269fad1225fccd71d4d38537a804f05fba03b3df516afbce8a79d8988d7c'
            'e334c1f226235c30011e62602ec0c0bd549bfe411e37125a5ee47f2a2d187045bc261efcaf2a9490d01dc687878ecfe01827996adc62e76df39ee8146d76713e'
            '88c5d3035df46aa23cd98657e19b907a68ac233f92307556ea44b6a90bac2d04dfb5b23c7efaa3addd48a5def3daacf0c8731acb59b15a7390c4c3435427674a'
            '662b800e849f2fa369496974d0e22c68cb5f5666cb8d3f768d236daae40c693880fb051f860aacf347a0fc8491764a315053dc9bbcc149f820267d1466bd995a')
noextract=("tor-browser-linux64-${pkgver}_en-US.tar.xz")
install="$pkgname.install"

prepare() {
  sed -i "s/REPL_NAME/$pkgname/g" "$pkgname.sh"
  sed -i "s/REPL_VERSION/$pkgver/g" "$pkgname.sh"
  sed -i "s/REPL_LANGUAGE/en-US/g" "$pkgname.sh"
  sed -i "s/REPL_NAME/$pkgname/g" "$pkgname.desktop"
  sed -i "s/REPL_LANGUAGE/en-US/g" "$pkgname.desktop"
  sed -i "s/REPL_COMMENT/$pkgdesc/g" "$pkgname.desktop"
}

package() {
  install -Dm 755 "$pkgname.sh" "$pkgdir/usr/bin/$pkgname"
  install -Dm 644 "tor-browser-linux64-${pkgver}_ALL.tar.xz" \
    "$pkgdir/opt/$pkgname/tor-browser-linux64-${pkgver}_ALL.tar.xz"
  install -Dm 644 "$pkgname.desktop" \
    "$pkgdir/usr/share/applications/$pkgname.desktop"
  install -Dm 644 "$pkgname.png" "$pkgdir/usr/share/pixmaps/$pkgname.png"
  install -Dm 644 "$pkgname.png" \
    "$pkgdir/usr/share/icons/hicolor/128x128/apps/$pkgname.png"
  install -Dm 644 "$pkgname.svg" \
    "$pkgdir/usr/share/icons/hicolor/scalable/apps/$pkgname.svg"
}

