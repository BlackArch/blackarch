# This file is part of BlackArch Linux ( http://blackarch.org ).
# See COPYING for license details.

pkgname='responder'
pkgver=201.0bdc183
pkgrel=1
epoch=2
pkgdesc='A LLMNR and NBT-NS poisoner, with built-in HTTP/SMB/MSSQL/FTP/LDAP rogue authentication server supporting NTLMv1/NTLMv2/LMv2, Extended Security NTLMSSP and Basic HTTP authentication.'
groups=('blackarch' 'blackarch-scanner' 'blackarch-fuzzer' 'blackarch-spoof'
        'blackarch-networking')
arch=('any')
depends=('python2')
makedepends=('git')
url='https://github.com/SpiderLabs/Responder/'
license=('GPL3')
source=('git+https://github.com/SpiderLabs/Responder.git')
sha1sums=('SKIP')

pkgver() {
  cd "$srcdir/Responder"

  echo $(git rev-list --count HEAD).$(git rev-parse --short HEAD)
}

package() {
  cd "$srcdir/Responder"

  mkdir -p "$pkgdir/usr/bin"
  mkdir -p "$pkgdir/usr/share/responder"
  mkdir -p "$pkgdir/usr/share/doc/responder"

  install -Dm644 -t "$pkgdir/usr/share/doc/responder" README.md

  rm README.md

  cp -a * "$pkgdir/usr/share/responder"

  cat > "$pkgdir/usr/bin/responder" << EOF
#!/bin/sh
cd /usr/share/responder
exec python2 Responder.py "\${@}"
EOF

  chmod +x "$pkgdir/usr/bin/responder"

  cat > "$pkgdir/usr/bin/dhcp-responder" << EOF
#!/bin/sh
cd /usr/share/responder
exec python2 DHCP.py "\${@}"
EOF

  chmod +x "$pkgdir/usr/bin/dhcp-responder"
}
