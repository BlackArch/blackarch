# This file is part of BlackArch Linux ( https://www.blackarch.org/ ).
# See COPYING for license details.

pkgname=drupwn
pkgver=42.9acf030
pkgrel=1
pkgdesc='Drupal enumeration & exploitation tool.'
groups=('blackarch' 'blackarch-webapp' 'blackarch-exploitation' 'blackarch-scanner')
arch=('any')
url='https://github.com/immunIT/drupwn'
license=('GPL3')
depends=('python' 'python-argparse' 'python-requests' 'python-prompt_toolkit'
         'python-veryprettytable')
makedepends=('git')
source=('git+https://github.com/immunIT/drupwn.git')
sha512sums=('SKIP')

pkgver() {
  cd $pkgname

  echo $(git rev-list --count HEAD).$(git rev-parse --short HEAD)
}

package() {
  cd $pkgname

  install -dm 755 "$pkgdir/usr/bin"
  install -dm 755 "$pkgdir/usr/share/$pkgname"

  install -Dm 644 -t "$pkgdir/usr/share/doc/$pkgname/" *.md
  install -Dm 644 LICENSE "$pkgdir/usr/share/licenses/$pkgname/LICENSE"

  rm *.md LICENSE

  cp -a * "$pkgdir/usr/share/$pkgname/"

  cat > "$pkgdir/usr/bin/$pkgname" << EOF
#!/bin/sh
cd /usr/share/$pkgname
exec python drupwn "\$@"
EOF

  chmod +x "$pkgdir/usr/bin/$pkgname"
}

