# This file is part of BlackArch Linux ( http://blackarch.org ).
# See COPYING for license details.

pkgname=drupwn
pkgver=36.54669ad
pkgrel=1
pkgdesc='Drupal enumeration & exploitation tool.'
groups=('blackarch' 'blackarch-webapp' 'blackarch-exploitation' 'blackarch-scanner')
arch=('any')
url='https://github.com/immunIT/drupwn'
license=('GPL3')
depends=('python' 'python-argparse' 'python-requests' 'python-prompt_toolkit'
         'python-veryprettytable')
makedepends=('git')
source=('git+https://github.com/immunIT/drupwn.git')
sha512sums=('SKIP')

pkgver() {
  cd "$srcdir/drupwn"

  echo $(git rev-list --count HEAD).$(git rev-parse --short HEAD)
}

package() {
  cd "$srcdir/drupwn"

  install -dm 755 "$pkgdir/usr/bin"
  install -dm 755 "$pkgdir/usr/share/drupwn"

  install -Dm644 -t "$pkgdir/usr/share/doc/drupwn/" *.md
  install -Dm644 LICENSE "$pkgdir/usr/share/licenses/drupwn/LICENSE"

  rm *.md LICENSE

  cp -a * "$pkgdir/usr/share/drupwn/"

  cat > "$pkgdir/usr/bin/drupwn" << EOF
#!/bin/sh
cd /usr/share/drupwn
exec python3 drupwn.py "\${@}"
EOF

  chmod a+x "$pkgdir/usr/bin/drupwn"
}
