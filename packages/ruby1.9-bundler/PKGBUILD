# This file is part of BlackArch Linux ( https://www.blackarch.org/ ).
# See COPYING for license details.

pkgname=ruby1.9-bundler
_gemname=${pkgname#ruby1.9-}
pkgver=1.17.1
pkgrel=1
pkgdesc="Manages an application's dependencies through its entire life, across many machines, systematically and repeatably."
arch=('any')
url="http://gembundler.com"
license=('MIT')
depends=('ruby1.9')
source=("http://rubygems.org/downloads/$_gemname-$pkgver.gem")
noextract=("$_gemname-$pkgver.gem")
sha512sums=('6adf044096b7716c8b0dae5f559506c65dcb8fd8a8a57428ac25331ca3a85b7cb399fb40b79aeccb0908d172686df9213b86f69286c3e383373ff5ee6f3b414d')

package() {
  install -dm 755 "$pkgdir/usr/bin"

  _gemdir="$(ruby-1.9 -rubygems -e'puts Gem.default_dir')"

  HOME=/tmp GEM_HOME="$_gemdir" GEM_PATH="$_gemdir" gem-1.9 install \
    --ignore-dependencies --no-user-install --no-rdoc --no-ri \
    -i "${pkgdir}${_gemdir}" ${_gemname}-${pkgver}.gem

  ln -s "$_gemdir/bin/bundle" "$pkgdir/usr/bin/bundle-1.9"
}

