# This file is part of BlackArch Linux ( https://www.blackarch.org/ ).
# See COPYING for license details.

pkgbase=python-bitarray
pkgname=('python2-bitarray' 'python-bitarray')
_pkgname=bitarray
pkgver=1.5.3
pkgrel=1
pkgdesc='Efficient arrays of booleans for Python.'
arch=('x86_64' 'aarch64')
url='https://pypi.org/project/bitarray/#files'
license=('PSF')
makedepends=('python2-setuptools' 'python-setuptools')
options=(!emptydirs)
source=("https://files.pythonhosted.org/packages/f7/b5/fc3f21da9286e527cc872ea6d30fd10adc6d2c35b803675d782d1431662d/$_pkgname-$pkgver.tar.gz")
sha512sums=('9f9cb2f0b8e7b66f062877079c043e1ded484a42e49b6926bc5cb019eda386c666ec1f1047a39d73306b23c3d5620373c9a7a79b1e67427ec668721eec432796')

prepare() {
  cp -a "$_pkgname-$pkgver"{,-2}
}

build() {
  cd "$_pkgname-$pkgver-2"

  python2 setup.py build

  cd "$srcdir/$_pkgname-$pkgver"

  python setup.py build
}

package_python2-bitarray() {
  depends=('python2')

  cd "$_pkgname-$pkgver"

  python2 setup.py install --prefix=/usr --root="$pkgdir" --optimize=1 \
    --skip-build
}

package_python-bitarray() {
  depends=('python')

  cd "$_pkgname-$pkgver"

  python setup.py install --prefix=/usr --root="$pkgdir" --optimize=1 \
    --skip-build
}

