# This file is part of BlackArch Linux ( https://www.blackarch.org/ ).
# See COPYING for license details.

pkgbase=python-bitarray
pkgname=('python2-bitarray' 'python-bitarray')
_pkgname=bitarray
pkgver=1.7.1
pkgrel=1
pkgdesc='Efficient arrays of booleans for Python.'
arch=('x86_64' 'aarch64')
url='https://pypi.org/project/bitarray/#files'
license=('PSF')
makedepends=('python2-setuptools' 'python-setuptools')
options=(!emptydirs)
source=("https://files.pythonhosted.org/packages/e7/cb/eb2ca6f6956ce7e6743ce463b5690f55a4fc3fc179aade9fde76d81f6b70/$_pkgname-$pkgver.tar.gz")
sha512sums=('f45461699f5e48b97001c64c514cdafe208fe31f7cf6c211aa0fb2f32d46028717fcbeb38b812bc8f349226537a3ec6dfec97bc7ecf7253d484332307504abd0')

prepare() {
  cp -a "$_pkgname-$pkgver"{,-2}
}

build() {
  cd "$_pkgname-$pkgver-2"

  python2 setup.py build

  cd "$srcdir/$_pkgname-$pkgver"

  python setup.py build
}

package_python2-bitarray() {
  depends=('python2')

  cd "$_pkgname-$pkgver"

  python2 setup.py install --prefix=/usr --root="$pkgdir" --optimize=1 \
    --skip-build
}

package_python-bitarray() {
  depends=('python')

  cd "$_pkgname-$pkgver"

  python setup.py install --prefix=/usr --root="$pkgdir" --optimize=1 \
    --skip-build
}

