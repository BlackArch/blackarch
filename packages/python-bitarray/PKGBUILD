# This file is part of BlackArch Linux ( https://www.blackarch.org/ ).
# See COPYING for license details.

pkgbase=python-bitarray
pkgname=('python2-bitarray' 'python-bitarray')
_pkgname=bitarray
pkgver=2.2.1
pkgrel=1
pkgdesc='Efficient arrays of booleans for Python.'
arch=('x86_64' 'aarch64')
url='https://pypi.org/project/bitarray/#files'
license=('PSF')
makedepends=('python2-setuptools' 'python-setuptools')
options=(!emptydirs)
source=("https://files.pythonhosted.org/packages/06/b8/948c5ec12a1a9ebe56b5f38e18f51efc14f02b7223f7a953c3ce026b634a/$_pkgname-$pkgver.tar.gz")
sha512sums=('7eb25359d405e0be63ca7faab50fd7ab652d8f838f09587b4faa94adc78952582e7c7c0b605820e5feafd18d35edf050f70f65bce1123b883ad47bd1e54244d3')

prepare() {
  cp -a "$_pkgname-$pkgver"{,-2}
}

build() {
  cd "$_pkgname-$pkgver-2"

  python2 setup.py build

  cd "$srcdir/$_pkgname-$pkgver"

  python setup.py build
}

package_python2-bitarray() {
  depends=('python2')

  cd "$_pkgname-$pkgver"

  python2 setup.py install --root="$pkgdir" --prefix=/usr -O1 --skip-build
}

package_python-bitarray() {
  depends=('python')

  cd "$_pkgname-$pkgver"

  python setup.py install --root="$pkgdir" --prefix=/usr -O1 --skip-build
}

