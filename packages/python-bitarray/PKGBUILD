# This file is part of BlackArch Linux ( https://www.blackarch.org/ ).
# See COPYING for license details.

pkgbase=python-bitarray
pkgname=('python2-bitarray' 'python-bitarray')
_pkgname=bitarray
pkgver=1.2.0
pkgrel=1
pkgdesc='Efficient arrays of booleans for Python.'
arch=('x86_64' 'aarch64')
url='https://pypi.org/project/bitarray/#files'
license=('PSF')
makedepends=('python2-setuptools' 'python-setuptools')
options=(!emptydirs)
source=("https://files.pythonhosted.org/packages/eb/fd/bf67cfea810305148ab8a1e8fbdcc5179f56979c73907fec9a36ebd4a58a/$_pkgname-$pkgver.tar.gz")
sha512sums=('58c2bb315cbb02ad19336a2071eef99814a25c8ae5ddae3850e7f23d3e84d3ebee163abbacdee8cb04bba4a8e43f9bc7cca06991450430ec787d84f8e0cdc5f6')

prepare() {
  cp -a "$_pkgname-$pkgver"{,-2}
}

build() {
  cd "$_pkgname-$pkgver-2"

  python2 setup.py build

  cd "$srcdir/$_pkgname-$pkgver"

  python setup.py build
}

package_python2-bitarray() {
  depends=('python2')

  cd "$_pkgname-$pkgver"

  python2 setup.py install --prefix=/usr --root="$pkgdir" --optimize=1 \
    --skip-build
}

package_python-bitarray() {
  depends=('python')

  cd "$_pkgname-$pkgver"

  python setup.py install --prefix=/usr --root="$pkgdir" --optimize=1 \
    --skip-build
}

