# This file is part of BlackArch Linux ( https://www.blackarch.org/ ).
# See COPYING for license details.

pkgbase=python-imageio-ffmpeg
pkgname=('python2-imageio-ffmpeg' 'python-imageio-ffmpeg')
_pkgname=imageio-ffmpeg
pkgver=0.4.4
pkgrel=1
pkgdesc='FFMPEG wrapper for Python.'
arch=('any')
url='https://pypi.org/project/imageio-ffmpeg/#files'
license=('BSD')
makedepends=('python2-setuptools' 'python-setuptools')
source=("https://files.pythonhosted.org/packages/a4/6e/cb996841e065235dd07e619f281537b9ac976e8a54d7966cf8cd19ee810a/$_pkgname-$pkgver.tar.gz")
sha512sums=('747fc91202e28a16eb3bb5a9b4449534da288eb30c187b06186c8d34424be49a2a30077309bb9b1f23914cdd2ab51e04317374c6cca8b234826b8978558c9c77')

prepare() {
  cp -a "$_pkgname-$pkgver"{,-2}
}

build() {
  cd "$_pkgname-$pkgver-2"

  python2 setup.py build

  cd "$srcdir/$_pkgname-$pkgver"

  python setup.py build
}

package_python2-imageio-ffmpeg() {
  depends=('python2')

  cd "$_pkgname-$pkgver-2"

  python2 setup.py install --prefix=/usr --root="$pkgdir" --optimize=1 \
    --skip-build
}

package_python-imageio-ffmpeg() {
  depends=('python')

  cd "$_pkgname-$pkgver"

  python setup.py install --prefix=/usr --root="$pkgdir" --optimize=1 \
    --skip-build
}

