# This file is part of BlackArch Linux ( https://www.blackarch.org/ ).
# See COPYING for license details.

pkgbase=python-imageio-ffmpeg
pkgname=('python2-imageio-ffmpeg' 'python-imageio-ffmpeg')
_pkgname=imageio-ffmpeg
pkgver=0.4.0
pkgrel=1
pkgdesc='FFMPEG wrapper for Python.'
arch=('any')
url='https://pypi.org/project/imageio-ffmpeg/#files'
license=('BSD')
makedepends=('python2-setuptools' 'python-setuptools')
source=("https://files.pythonhosted.org/packages/70/10/aff65fda4367038769538761120a693444376641454dc5db22e5a333eb6b/$_pkgname-$pkgver.tar.gz")
sha512sums=('dc9098b23793669ea57dc861910d7ed1b0decfe9820a52ee079249f7378bb86ce4c28638bb6b94670a03eb517082eb5aec1252ee46c288623f67a2bbb19b888c')

prepare() {
  cp -a "$_pkgname-$pkgver"{,-2}
}

build() {
  cd "$_pkgname-$pkgver-2"

  python2 setup.py build

  cd "$srcdir/$_pkgname-$pkgver"

  python setup.py build
}

package_python2-imageio-ffmpeg() {
  depends=('python2')

  cd "$_pkgname-$pkgver-2"

  python2 setup.py install --prefix=/usr --root="$pkgdir" --optimize=1 \
    --skip-build
}

package_python-imageio-ffmpeg() {
  depends=('python')

  cd "$_pkgname-$pkgver"

  python setup.py install --prefix=/usr --root="$pkgdir" --optimize=1 \
    --skip-build
}

