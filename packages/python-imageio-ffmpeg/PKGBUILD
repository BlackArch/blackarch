# This file is part of BlackArch Linux ( https://www.blackarch.org/ ).
# See COPYING for license details.

pkgbase=python-imageio-ffmpeg
pkgname=('python2-imageio-ffmpeg' 'python-imageio-ffmpeg')
_pkgname=imageio-ffmpeg
pkgver=0.4.1
pkgrel=1
pkgdesc='FFMPEG wrapper for Python.'
arch=('any')
url='https://pypi.org/project/imageio-ffmpeg/#files'
license=('BSD')
makedepends=('python2-setuptools' 'python-setuptools')
source=("https://files.pythonhosted.org/packages/da/8a/d3f887e268371ba0428e2aa0072cf70ec221a524ac14692e61df565fb9c1/$_pkgname-$pkgver.tar.gz")
sha512sums=('3f12cb59153d8f54c3e1b78cf037fabc10985f36a4a20719fbdcdfbe129b288da91344c07285ecbd79e0185aeca39854a1dd2afc044f8b6d80c51d373e2455ad')

prepare() {
  cp -a "$_pkgname-$pkgver"{,-2}
}

build() {
  cd "$_pkgname-$pkgver-2"

  python2 setup.py build

  cd "$srcdir/$_pkgname-$pkgver"

  python setup.py build
}

package_python2-imageio-ffmpeg() {
  depends=('python2')

  cd "$_pkgname-$pkgver-2"

  python2 setup.py install --prefix=/usr --root="$pkgdir" --optimize=1 \
    --skip-build
}

package_python-imageio-ffmpeg() {
  depends=('python')

  cd "$_pkgname-$pkgver"

  python setup.py install --prefix=/usr --root="$pkgdir" --optimize=1 \
    --skip-build
}

