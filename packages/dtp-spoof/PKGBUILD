# This file is part of BlackArch Linux ( http://blackarch.org ).
# See COPYING for license details.

pkgname='dtp-spoof'
pkgver=4.4b2df1b
pkgrel=1
pkgdesc='Python script/security tool to test Dynamic Trunking Protocol configuration on a switch.'
groups=('blackarch' 'blackarch-networking')
arch=('any')
url='https://github.com/fleetcaptain/dtp-spoof'
license=('custom:unknown')
depends=('python2' 'scapy')
makedepends=('git')
source=('git+https://github.com/fleetcaptain/dtp-spoof.git')
sha1sums=('SKIP')

pkgver() {
  cd "$srcdir/dtp-spoof"

  echo $(git rev-list --count HEAD).$(git rev-parse --short HEAD)
}

prepare() {
  cd "$srcdir/dtp-spoof"

  sed -i '1i\#!/usr/bin/python2' py/network/dtp-spoof.py
}

package() {
  cd "$srcdir/dtp-spoof"

  install -Dm755 py/network/dtp-spoof.py "$pkgdir/usr/bin/dtp-spoof"
  install -Dm644 -t "$pkgdir/usr/share/doc/dtp-spoof/" README.md
}
