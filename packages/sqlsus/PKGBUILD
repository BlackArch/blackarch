pkgname=sqlsus
pkgver=0.7.2
pkgrel=5
groups=('blackarch' 'blackarch-exploitation' 'blackarch-webapp')
pkgdesc="An open source MySQL injection and takeover tool, written in perl"
arch=('i686' 'x86_64' 'armv6h' 'armv7h')
url='http://sqlsus.sourceforge.net/'
license=('GPL2')
depends=('perl' 'perl-libwww' 'perl-dbd-sqlite' 'perl-www-mechanize'
         'perl-lwp-protocol-https' 'perl-switch' 'perl-html-linkextractor')
optdepends=('perl-lwp-protocol-socks: SOCKS proxy support'
            'perl-term-readline-gnu: better readline support with AddHistory')
source=(http://downloads.sourceforge.net/sourceforge/sqlsus/sqlsus-${pkgver}.tgz
        sqlsus)
md5sums=('20daed2e349949edb702238ccf7eefbb'
         '041ca20393f25934740dc7dd3145e3d3')

package() {
  cd $srcdir/sqlsus-$pkgver

  # Base directories.
  install -dm755 $pkgdir/usr/bin
  install -dm755 $pkgdir/usr/share/sqlsus/doc

  # Docs.
  install -m644 CHANGELOG README -t $pkgdir/usr/share/sqlsus/doc

  # Shared.
  cp --no-preserve=ownership -R * $pkgdir/usr/share/sqlsus

  # Bin.
  install -Dm755 $srcdir/sqlsus $pkgdir/usr/bin/sqlsus
}
