# This file is part of BlackArch Linux ( https://www.blackarch.org/ ).
# See COPYING for license details.

pkgname=bashfuscator
pkgver=338.7487348
pkgrel=2
pkgdesc='Fully configurable and extendable Bash obfuscation framework.'
groups=('blackarch' 'blackarch-automation')
arch=('any')
url='https://github.com/Bashfuscator/Bashfuscator'
license=('MIT')
depends=('python' 'python-argcomplete' 'python-pyperclip')
makedepends=('git' 'python-setuptools')
source=("$pkgname::git+https://github.com/Bashfuscator/Bashfuscator.git")
sha512sums=('SKIP')

pkgver() {
  cd $pkgname

  echo $(git rev-list --count HEAD).$(git rev-parse --short HEAD)
}

build() {
  cd $pkgname

  python setup.py build
}

package() {
  cd $pkgname

  python setup.py install --prefix=/usr --root="$pkgdir" -O1 --skip-build
}

