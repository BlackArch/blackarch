# This file is part of BlackArch Linux ( https://www.blackarch.org/ ).
# See COPYING for license details.

pkgbase=python-json2html
pkgname=('python2-json2html' 'python-json2html')
_pkgname=json2html
pkgver=1.2.1
pkgrel=3
pkgdesc='JSON to HTML Table Representation.'
arch=('any')
url='https://pypi.org/project/json2html/#files'
license=('MIT')
makedepends=('python2-setuptools' 'python-setuptools')
source=("https://pypi.python.org/packages/23/33/e6367858c5da6795d3277e9ea4e58a70a7fd55f97f4bcc90b1ecec1c1410/$_pkgname-$pkgver.tar.gz")
sha512sums=('6130c2907ecf59a672c2c48bae694bbb1242112d554c60fa6193f0f7d2a1e222aced90820f96fc12401d2bef3254a64dd3bad2f53506ec171cac495dcb7efcda')

prepare() {
  cp -a "$_pkgname-$pkgver"{,-2}
}

build() {
  cd "$_pkgname-$pkgver-2"

  python2 setup.py build

  cd "$srcdir/$_pkgname-$pkgver"

  python setup.py build
}

package_python2-json2html() {
  depends=('python2' 'python2-simplejson' 'python2-ordereddict')

  cd "$_pkgname-$pkgver-2"

  python2 setup.py install --prefix=/usr --root="$pkgdir" --optimize=1 \
    --skip-build
}

package_python-json2html() {
  depends=('python' 'python-simplejson' 'python-ordereddict')

  cd "$_pkgname-$pkgver"

  python setup.py install --prefix=/usr --root="$pkgdir" --optimize=1 \
    --skip-build
}

