# This file is part of BlackArch Linux ( https://www.blackarch.org/ ).
# See COPYING for license details.

pkgbase=python-tinydb
pkgname=('python2-tinydb' 'python-tinydb')
_pkgname=tinydb
pkgver=4.8.0
pkgrel=1
pkgdesc='Lightweight document oriented database optimized for your happiness.'
arch=('any')
url='https://pypi.org/project/tinydb/#files'
license=('MIT')
makedepends=('python2-setuptools' 'python-setuptools')
source=("https://files.pythonhosted.org/packages/source/${_pkgname::1}/$_pkgname/$_pkgname-$pkgver.tar.gz")
sha512sums=('7f1932b766aba34f3721f0fbb7d45b8f39036caf8c41767e21232d4f329f00e440eb93a7284daa7b6df3a6700a5b4fcbdd6a7b79601ffd95480a86065cf7907c')

prepare() {
  cp -a "$_pkgname-$pkgver"{,-2}
}

build() {
  cd "$_pkgname-$pkgver-2"

  python2 setup.py build

  cd "$srcdir/$_pkgname-$pkgver"

  python setup.py build
}

package_python2-tinydb() {
  _pyver=2.7
  depends=('python2' 'python2-pytest-cov' 'python2-yaml')

  cd "$_pkgname-$pkgver-2"

  python2 setup.py install --prefix=/usr --root="$pkgdir" -O1 --skip-build

  rm -rf "$pkgdir/usr/lib/python$_pyver/site-packages/tests"
}

package_python-tinydb() {
  _pyver=3.11
  depends=('python' 'python-pytest-cov' 'python-yaml')

  cd "$_pkgname-$pkgver"

  python setup.py install --prefix=/usr --root="$pkgdir" -O1 --skip-build

  rm -rf "$pkgdir/usr/lib/python$_pyver/site-packages/tests"
}

