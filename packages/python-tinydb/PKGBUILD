# This file is part of BlackArch Linux ( https://www.blackarch.org/ ).
# See COPYING for license details.

pkgbase=python-tinydb
pkgname=('python2-tinydb' 'python-tinydb')
_pkgname=tinydb
pkgver=3.14.1
pkgrel=1
pkgdesc='Lightweight document oriented database optimized for your happiness.'
arch=('any')
url='https://pypi.org/project/tinydb/#files'
license=('MIT')
makedepends=('python2-setuptools' 'python-setuptools')
source=("https://files.pythonhosted.org/packages/12/65/e9f14f3b1e08add15910f4ad9e347bbe4c8f29a52e97e91df942b36a94b2/$_pkgname-$pkgver.tar.gz")
sha512sums=('b1048da4c3445b9ac3ed6017b29c65ca436aeb609f696ae39bd0a20c3b62e7cb768e56e6031dadabe173d8b0c107f96cd5d1f158d662e976aedb03eddab8abf3')

prepare() {
  cp -a "$_pkgname-$pkgver"{,-2}
}

build() {
  cd "$_pkgname-$pkgver-2"

  python2 setup.py build

  cd "$srcdir/$_pkgname-$pkgver"

  python setup.py build
}

package_python2-tinydb() {
  depends=('python2' 'python2-pytest-cov' 'python2-yaml')

  cd "$_pkgname-$pkgver-2"

  python2 setup.py install --prefix=/usr --root="$pkgdir" --optimize=1 \
    --skip-build
}

package_python-tinydb() {
  depends=('python' 'python-pytest-cov' 'python-yaml')

  cd "$_pkgname-$pkgver"

  python setup.py install --prefix=/usr --root="$pkgdir" --optimize=1 \
    --skip-build
}

