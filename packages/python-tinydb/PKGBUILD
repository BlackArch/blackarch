# This file is part of BlackArch Linux ( https://www.blackarch.org/ ).
# See COPYING for license details.

pkgbase=python-tinydb
pkgname=('python2-tinydb' 'python-tinydb')
_pkgname=tinydb
pkgver=3.15.1
pkgrel=2
pkgdesc='Lightweight document oriented database optimized for your happiness.'
arch=('any')
url='https://pypi.org/project/tinydb/#files'
license=('MIT')
makedepends=('python2-setuptools' 'python-setuptools')
source=("https://files.pythonhosted.org/packages/ff/4f/4380ad7d0a42d2412447885234dbab7449958dc8ed5b0440597d261fcc83/$_pkgname-$pkgver.tar.gz")
sha512sums=('e4975f11d1570b97bbf7a48e11fec60ccc9bdd51bbfa99736ed7354ed7a416d001df04b643052463b8361e6d8199fdf9eb371d9cd8ab57c812b779a8a4efd9f2')

prepare() {
  cp -a "$_pkgname-$pkgver"{,-2}
}

build() {
  cd "$_pkgname-$pkgver-2"

  python2 setup.py build

  cd "$srcdir/$_pkgname-$pkgver"

  python setup.py build
}

package_python2-tinydb() {
  depends=('python2' 'python2-pytest-cov' 'python2-yaml')

  cd "$_pkgname-$pkgver-2"

  python2 setup.py install --prefix=/usr --root="$pkgdir" --optimize=1 \
    --skip-build
}

package_python-tinydb() {
  depends=('python' 'python-pytest-cov' 'python-yaml')

  cd "$_pkgname-$pkgver"

  python setup.py install --prefix=/usr --root="$pkgdir" --optimize=1 \
    --skip-build
}

