# This file is part of BlackArch Linux ( https://www.blackarch.org/ ).
# See COPYING for license details.

pkgbase=python-tinydb
pkgname=('python2-tinydb' 'python-tinydb')
_pkgname=tinydb
pkgver=4.5.1
pkgrel=1
pkgdesc='Lightweight document oriented database optimized for your happiness.'
arch=('any')
url='https://pypi.org/project/tinydb/#files'
license=('MIT')
makedepends=('python2-setuptools' 'python-setuptools')
source=("https://files.pythonhosted.org/packages/42/17/5c81cdaea6afd396cef72776b7e31c26bc1482086979f758dd25cb6a79f0/$_pkgname-$pkgver.tar.gz")
sha512sums=('4aec88f49bf04ff83d63891ca22dacb7f5139bc0a2c54ac18f2151fcf84f329271b9548dd3f228f57c91a44dc95a8677f0744bced83b0b58ce2bb558b8f81092')

prepare() {
  cp -a "$_pkgname-$pkgver"{,-2}
}

build() {
  cd "$_pkgname-$pkgver-2"

  python2 setup.py build

  cd "$srcdir/$_pkgname-$pkgver"

  python setup.py build
}

package_python2-tinydb() {
  depends=('python2' 'python2-pytest-cov' 'python2-yaml')

  cd "$_pkgname-$pkgver-2"

  python2 setup.py install --prefix=/usr --root="$pkgdir" --optimize=1 \
    --skip-build
}

package_python-tinydb() {
  depends=('python' 'python-pytest-cov' 'python-yaml')

  cd "$_pkgname-$pkgver"

  python setup.py install --prefix=/usr --root="$pkgdir" --optimize=1 \
    --skip-build
}

