# This file is part of BlackArch Linux ( https://www.blackarch.org/ ).
# See COPYING for license details.

pkgbase=python-tinydb
pkgname=('python2-tinydb' 'python-tinydb')
_pkgname=tinydb
pkgver=4.7.1
pkgrel=2
pkgdesc='Lightweight document oriented database optimized for your happiness.'
arch=('any')
url='https://pypi.org/project/tinydb/#files'
license=('MIT')
makedepends=('python2-setuptools' 'python-setuptools')
source=("https://files.pythonhosted.org/packages/source/${_pkgname::1}/$_pkgname/$_pkgname-$pkgver.tar.gz")
sha512sums=('8bdaffb9bc0e8cf07cea63c4bce76b4cbdc888bb31d11c386c4bc5bf930fc9ccad59ca678bea818cada025b862e3c64493301ec4f84e7b44984d7ef28cb86f31')

prepare() {
  cp -a "$_pkgname-$pkgver"{,-2}
}

build() {
  cd "$_pkgname-$pkgver-2"

  python2 setup.py build

  cd "$srcdir/$_pkgname-$pkgver"

  python setup.py build
}

package_python2-tinydb() {
  _pyver=2.7
  depends=('python2' 'python2-pytest-cov' 'python2-yaml')

  cd "$_pkgname-$pkgver-2"

  python2 setup.py install --prefix=/usr --root="$pkgdir" -O1 --skip-build

  rm -rf "$pkgdir/usr/lib/python$_pyver/site-packages/tests"
}

package_python-tinydb() {
  _pyver=3.10
  depends=('python' 'python-pytest-cov' 'python-yaml')

  cd "$_pkgname-$pkgver"

  python setup.py install --prefix=/usr --root="$pkgdir" -O1 --skip-build

  rm -rf "$pkgdir/usr/lib/python$_pyver/site-packages/tests"
}

