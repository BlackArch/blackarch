# This file is part of BlackArch Linux ( https://www.blackarch.org/ ).
# See COPYING for license details.

pkgbase=python-python-evtx
pkgname=('python2-python-evtx' 'python-python-evtx')
_pkgname=python-evtx
pkgver=0.7.4
pkgrel=1
pkgdesc='Pure Python parser for recent Windows event log files (.evtx).'
arch=('any')
url='https://pypi.org/project/python-evtx/#files'
license=('APACHE')
makedepends=('python2-setuptools' 'python-setuptools')
source=("https://files.pythonhosted.org/packages/7a/0f/02e95a23a9f9a68429af28b8bf90f067a02db65480144e25615ad3b147a7/$_pkgname-$pkgver.tar.gz")
sha512sums=('44c79299b33060bdc4d29184da82fdacfde9d0ba6624cbe31de614c3b08a594ecd2ec1ce3cb8ecdfd004342649ca59102059f7226b4f843d160a9706b8f5ceeb')

prepare() {
  cp -a "$_pkgname-$pkgver"{,-2}
}

build() {
  cd "$_pkgname-$pkgver-2"

  python2 setup.py build

  cd "$srcdir/$_pkgname-$pkgver"

  python setup.py build
}

package_python2-python-evtx() {
  depends=('python2' 'python2-six' 'python2-hexdump' 'python2-pytest'
           'python2-cov')

  cd "$_pkgname-$pkgver-2"

  python2 setup.py install --prefix=/usr --root="$pkgdir" --optimize=1 \
    --skip-build

  for i in "$pkgdir/usr/bin/"* ; do
    mv $i "${i}2"
  done
}

package_python-python-evtx() {
  depends=('python' 'python-six' 'python-hexdump' 'python-pytest'
           'python-pytest-cov')

  cd "$_pkgname-$pkgver"

  python setup.py install --prefix=/usr --root="$pkgdir" --optimize=1 \
    --skip-build
}

