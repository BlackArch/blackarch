# This file is part of BlackArch Linux ( https://www.blackarch.org/ ).
# See COPYING for license details.

pkgbase=python-python-evtx
pkgname=('python2-python-evtx' 'python-python-evtx')
_pkgname=python-evtx
pkgver=0.7.2
pkgrel=1
pkgdesc='Pure Python parser for recent Windows event log files (.evtx).'
arch=('any')
url='https://pypi.org/project/python-evtx/#files'
license=('APACHE')
makedepends=('python2-setuptools' 'python-setuptools')
source=("https://files.pythonhosted.org/packages/29/7e/419b9165ac3dbd2c703344f23478aeec438928b9334026415aec4ea61863/$_pkgname-$pkgver.tar.gz")
sha512sums=('b2697fb5fd568a10e6ada070f25ed8443bb0568f3d594188531340b51cfe1a137ee585a0c438fc6978d3c7c39d9444367ff43e50c7ff0eb22a267f4b90f8ed1b')

prepare() {
  cp -a "$_pkgname-$pkgver"{,-2}
}

build() {
  cd "$_pkgname-$pkgver-2"

  python2 setup.py build

  cd "$srcdir/$_pkgname-$pkgver"

  python setup.py build
}

package_python2-python-evtx() {
  depends=('python2' 'python2-six' 'python2-hexdump' 'python2-pytest'
           'python2-cov')

  cd "$_pkgname-$pkgver-2"

  python2 setup.py install --prefix=/usr --root="$pkgdir" --optimize=1 \
    --skip-build

  for i in "$pkgdir/usr/bin/"* ; do
    mv $i "${i}2"
  done
}

package_python-python-evtx() {
  depends=('python' 'python-six' 'python-hexdump' 'python-pytest'
           'python-pytest-cov')

  cd "$_pkgname-$pkgver"

  python setup.py install --prefix=/usr --root="$pkgdir" --optimize=1 \
    --skip-build
}

