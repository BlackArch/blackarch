# This file is part of BlackArch Linux ( https://www.blackarch.org/ ).
# See COPYING for license details.

pkgname=perl-math-pari
pkgver=2.030507
pkgrel=2
epoch=4
pkgdesc='Perl interface to PARI.'
arch=('x86_64' 'aarch64')
url='http://search.cpan.org/~ilyaz/Math-Pari-2.01080605/Pari.pm'
license=('GPL Artistic')
depends=('perl')
options=(!emptydirs)
source=("https://cpan.metacpan.org/authors/id/I/IL/ILYAZ/modules/Math-Pari-$pkgver.zip")
sha512sums=('a2bba0bf6bbaf14d25b512e35932b3927d3185ef586566244692650e41457a831a53853c008471b28a2e29fb364bb28c43e83043d7c832a7af3873cb39352466')
_distdir="Math-Pari-$pkgver"

build() {
  ( export PERL_MM_USE_DEFAULT=1 PERL5LIB=""                 \
    PERL_AUTOINSTALL=--skipdeps                            \
    PERL_MM_OPT="INSTALLDIRS=vendor DESTDIR='$pkgdir'"     \
    PERL_MB_OPT="--installdirs vendor --destdir '$pkgdir'" \
    MODULEBUILDRC=/dev/null

    cd $_distdir

    /usr/bin/perl Makefile.PL

    make
  )
}

package() {
  cd $_distdir

  make install

  find "$pkgdir" -name .packlist -o -name perllocal.pod -delete
}

