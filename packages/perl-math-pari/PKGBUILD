# This file is part of BlackArch Linux ( http://blackarch.org ).
# See COPYING for license details.

pkgname=perl-math-pari
pkgver=2.01080900
pkgrel=5
epoch=3
pkgdesc='Perl interface to PARI.'
arch=('x86_64' 'armv6h' 'armv7h' 'aarch64')
url='http://search.cpan.org/~ilyaz/Math-Pari-2.01080605/Pari.pm'
license=('GPL Artistic')
depends=('perl')
options=(!emptydirs)
source=("http://search.cpan.org/CPAN/authors/id/I/IL/ILYAZ/modules/Math-Pari-${pkgver}.zip")
sha512sums=('844324472632081e16d8c70e2f3b14a04cc125dc65ac1e4e75b495f7b83393cadc637c6b81d2e6657380b153a6d91eba4a437087481ffbd4c6c58c5da0e4d05b')
_distdir="Math-Pari-$pkgver"

build() {
  ( export PERL_MM_USE_DEFAULT=1 PERL5LIB=""                 \
    PERL_AUTOINSTALL=--skipdeps                            \
    PERL_MM_OPT="INSTALLDIRS=vendor DESTDIR='$pkgdir'"     \
    PERL_MB_OPT="--installdirs vendor --destdir '$pkgdir'" \
    MODULEBUILDRC=/dev/null

    cd "$srcdir/$_distdir"

    /usr/bin/perl Makefile.PL

    make
  )
}

package() {
  cd "$srcdir/$_distdir"

  make install

  find "$pkgdir" -name .packlist -o -name perllocal.pod -delete
}
