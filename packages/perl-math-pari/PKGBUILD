pkgname=perl-math-pari
pkgver=2.01080605
pkgrel=3
pkgdesc="Perl interface to PARI"
arch=('i686' 'x86_64')
url='http://search.cpan.org/~ilyaz/Math-Pari-2.01080605/Pari.pm'
license=('GPL Artistic')
depends=('perl>=5.14.0')
options=(!emptydirs)
source=("http://search.cpan.org/CPAN/authors/id/I/IL/ILYAZ/modules/Math-Pari-$pkgver.tar.gz")
md5sums=('ccb3da2bdce184a5df3f52cfa8b43a85')

build() {
  cd "$srcdir/Math-Pari-$pkgver"

  echo -e "y\n" | perl Makefile.PL installdirs=vendor
  make
}

check() {
  cd "$srcdir/Math-Pari-$pkgver"
  make test
}

package() {
  cd "$srcdir/Math-Pari-$pkgver"

  make install "DESTDIR=$pkgdir"

  find "$pkgdir" -name '.packlist' -delete
  find "$pkgdir" -name '*.pod' -delete
}
