# This file is part of BlackArch Linux ( https://www.blackarch.org/ ).
# See COPYING for license details.

pkgname=infoga
pkgver=19.1608064
pkgrel=1
epoch=3
pkgdesc='Tool for gathering e-mail accounts information from different public sources (search engines, pgp key servers).'
groups=('blackarch' 'blackarch-recon')
arch=('any')
url='https://github.com/m4ll0k/infoga'
license=('MIT')
depends=('python' 'python-requests' 'python-urllib3' 'python-colorama')
makedepends=('git')
source=("git+https://github.com/m4ll0k/$pkgname.git")
sha512sums=('SKIP')

pkgver() {
  cd $pkgname

  echo $(git rev-list --count HEAD).$(git rev-parse --short HEAD)
}

prepare() {
  cd $pkgname

  # vollhorst...
  touch lib/__init__.py
}

package() {
  cd $pkgname

  install -dm 755 "$pkgdir/usr/bin"
  install -dm 755 "$pkgdir/usr/share/$pkgname"

  install -Dm 644 -t "$pkgdir/usr/share/doc/$pkgname/" README.md

  rm README.md

  cp -a * "$pkgdir/usr/share/$pkgname/"

  cat > "$pkgdir/usr/bin/$pkgname" << EOF
#!/bin/sh
cd /usr/share/$pkgname
exec python3 $pkgname.py "\$@"
EOF

  chmod +x "$pkgdir/usr/bin/$pkgname"
}

