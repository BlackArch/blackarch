# This file is part of BlackArch Linux ( http://blackarch.org ).
# See COPYING for license details.

pkgname='infoga'
pkgver=7.aae2b60
pkgrel=1
epoch=1
pkgdesc='Tool for gathering e-mail accounts information from different public sources (search engines, pgp key servers).'
groups=('blackarch' 'blackarch-recon')
arch=('any')
url='https://github.com/m4ll0k/infoga'
license=('MIT')
depends=('python2')
makedepends=('git')
source=('git+https://github.com/m4ll0k/infoga.git')
sha1sums=('SKIP')

pkgver() {
  cd "$srcdir/infoga"

  echo $(git rev-list --count HEAD).$(git rev-parse --short HEAD)
}

package() {
  cd "$srcdir/infoga"

  mkdir -p "$pkgdir/usr/bin"
  mkdir -p "$pkgdir/usr/share/infoga"

  install -Dm644 -t "$pkgdir/usr/share/doc/infoga/" README.md

  rm README.md

  cp -a * "$pkgdir/usr/share/infoga/"

  cat > "$pkgdir/usr/bin/infoga" << EOF
#!/bin/sh
cd /usr/share/infoga
exec python2 infoga.py "\${@}"
EOF

  chmod a+x "$pkgdir/usr/bin/infoga"
}
