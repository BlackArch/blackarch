# This file is part of BlackArch Linux ( https://www.blackarch.org/ ).
# See COPYING for license details.

pkgname=netexec
_pyver=3.12
pkgver=v1.3.0.r118.g95b1af5a
pkgrel=1
groups=('blackarch' 'blackarch-scanner' 'blackarch-exploitation'
        'blackarch-windows')
pkgdesc='A Windows / Active Directory environments pentest tool.'
arch=('any')
url='https://netexec.wiki/'
license=('BSD')
# need rust for aardwolf, poetry can't install system dependencies
# https://github.com/Pennyw0rth/NetExec/issues/233#issuecomment-2274444462
depends=('python' 'python-poetry' 'rust')
makedepends=('git')
source=("$pkgname::git+https://github.com/Pennyw0rth/NetExec.git")
sha512sums=('SKIP')
install="$pkgname.install"

pkgver() {
  cd $pkgname

  git describe --long --tags | sed 's/\([^-]*-g\)/r\1/;s/-/./g'
}

package() {
  cd $pkgname

  install -dm 755 "$pkgdir/usr/bin"
  install -dm 755 "$pkgdir/usr/share/$pkgname"

  install -Dm 644 LICENSE "$pkgdir/usr/share/licenses/$pkgname/LICENSE"

  rm -rf LICENSE .github .git* .dockerignore Dockerfile Makefile *.nix

  cp -a * "$pkgdir/usr/share/$pkgname/"

  cat > "$pkgdir/usr/bin/$pkgname" << EOF
#!/bin/sh
poetry config cache-dir "/usr/share/$pkgname"
poetry env use /usr/share/$pkgname/virtualenvs/*/bin/python$_pyver \\
  --directory /usr/share/netexec
poetry run --directory "/usr/share/$pkgname" $pkgname "\$@"
EOF

  cat > "$pkgdir/usr/bin/nxc" << EOF
#!/bin/sh
poetry config cache-dir "/usr/share/$pkgname"
poetry env use /usr/share/$pkgname/virtualenvs/*/bin/python$_pyver \\
  --directory /usr/share/netexec
poetry run --directory "/usr/share/$pkgname" nxc "\$@"
EOF

  cat > "$pkgdir/usr/bin/nxcdb" << EOF
#!/bin/sh
poetry config cache-dir "/usr/share/$pkgname"
poetry env use /usr/share/$pkgname/virtualenvs/*/bin/python$_pyver \\
  --directory /usr/share/netexec
poetry run --directory "/usr/share/$pkgname" nxcdb "\$@"
EOF

  chmod +x "$pkgdir/usr/bin/$pkgname" "$pkgdir/usr/bin/nxc" \
    "$pkgdir/usr/bin/nxcdb"
}

