# This file is part of BlackArch Linux ( https://www.blackarch.org/ ).
# See COPYING for license details.

pkgbase=python-crysp
pkgname=('python2-crysp' 'python-crysp')
_pkgname=crysp
pkgver=0.99
pkgrel=3
pkgdesc='Crypto Stuff in Python.'
arch=('any')
url='https://pypi.python.org/pypi/crysp/'
license=('GPL2')
makedepends=('python2-setuptools' 'python-setuptools')
source=("https://files.pythonhosted.org/packages/f8/6f/b4acaaf6be424a0509cb064f7288f066fbc3056a66ab1bd586e5ad94104a/$_pkgname-$pkgver.tar.gz")
sha512sums=('722a6e7c1f689f5845cf7c087ec37b168a6e57f845738abddeb9f175eea57c78228d12fdd9c6783dd77ed15d51c9949cf7f9bef68c8a0c1c3c6080dc411ae3f0')

prepare() {
  cp -a "$_pkgname-$pkgver" "$_pkgname-$pkgver-2"
}

build() {
  cd "$_pkgname-$pkgver-2"

  python2 setup.py build

  cd "$srcdir/$_pkgname-$pkgver"

  python setup.py build
}

package_python2-crysp() {
  depends=('python2' 'python2-future')

  cd "$_pkgname-$pkgver-2"

  python2 setup.py install --prefix=/usr --root="$pkgdir" --optimize=1 \
    --skip-build
}

package_python-crysp() {
  depends=('python' 'python-future')

  cd "$_pkgname-$pkgver"

  python setup.py install --prefix=/usr --root="$pkgdir" --optimize=1 \
    --skip-build
}

