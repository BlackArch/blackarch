# This file is part of BlackArch Linux ( https://www.blackarch.org/ ).
# See COPYING for license details.

pkgbase=python-crysp
pkgname=('python2-crysp' 'python-crysp')
_pkgname=crysp
pkgver=1.1
pkgrel=2
pkgdesc='Crypto Stuff in Python.'
arch=('any')
url='https://pypi.python.org/pypi/crysp/'
license=('GPL2')
makedepends=('python2-setuptools' 'python-setuptools')
source=("https://github.com/bdcht/$_pkgname/archive/v$pkgver.tar.gz")
sha512sums=('b75c49e29d3be5000cdb128b92ca970d6d3bb0a06c27a3106e8f37e7d23d3bf4e027f5c9e0464ff660300ef46395abc1a94533871be2f62afa844df961411804')

prepare() {
  cp -a "$_pkgname-$pkgver"{,-2}
}

build() {
  cd "$_pkgname-$pkgver-2"

  python2 setup.py build

  cd "$srcdir/$_pkgname-$pkgver"

  python setup.py build
}

package_python2-crysp() {
  depends=('python2' 'python2-future')

  cd "$_pkgname-$pkgver-2"

  python2 setup.py install --root="$pkgdir" --prefix=/usr -O1 --skip-build
}

package_python-crysp() {
  depends=('python' 'python-future')

  cd "$_pkgname-$pkgver"

  python setup.py install --root="$pkgdir" --prefix=/usr -O1 --skip-build
}

