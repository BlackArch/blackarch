# This file is part of BlackArch Linux ( http://blackarch.org ).
# See COPYING for license details.

pkgname='recstudio'
pkgver='4.0_20130717'
pkgrel=7
groups=('blackarch' 'blackarch-decompiler' 'blackarch-reversing')
pkgdesc="Cross platform interactive decompiler"
arch=('x86_64' 'armv6h' 'armv7h' 'aarch64')
url='http://www.backerstreet.com/rec/rec.htm'
license=('custom')
depends=('gtk2')
[[ $CARCH == x86_64 ]] && depends=('lib32-gtk2')
source=('http://www.backerstreet.com/rec/RecStudioLinux.tgz'
        'LICENSE')
sha1sums=('d2aef715b7db46ec35122d06076e2cbacfedd866'
          'e8a6336f63f14fa1abf8f8f8a15a44dc3fd8d372')
options=('!strip')

package() {
  cd "$srcdir"

  # Base directories.
  mkdir -p "$pkgdir/opt/recstudio/bin"
  mkdir -p "$pkgdir/opt/recstudio/support"
  mkdir -p "$pkgdir/opt/recstudio/support/Prototypes"
  mkdir -p "$pkgdir/opt/recstudio/support/win32api"
  mkdir -p "$pkgdir/usr/share/licenses/recstudio"
  mkdir -p "$pkgdir/usr/bin"

  # Bin.
  install -m755 bin/* "$pkgdir/opt/recstudio/bin"

  # Support.
  cp --no-preserve=ownership -R support/* "$pkgdir/opt/recstudio/support"

  # License.
  install -m644 LICENSE "$pkgdir/usr/share/licenses/recstudio/LICENSE"

  cat > "$pkgdir/usr/bin/recstudio" <<EOF
#!/bin/sh
exec /opt/recstudio/bin/RecStudioLinux
EOF
  chmod +x "$pkgdir/usr/bin/recstudio"
}
