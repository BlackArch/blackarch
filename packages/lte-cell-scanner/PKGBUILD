# This file is part of BlackArch Linux ( https://www.blackarch.org/ ).
# See COPYING for license details.

pkgname=lte-cell-scanner
pkgver=57.5fa3df8
pkgrel=2
groups=('blackarch' 'blackarch-scanner' 'blackarch-mobile' 'blackarch-recon')
pkgdesc='LTE SDR cell scanner optimized to work with very low performance RF front ends (8bit A/D, 20dB noise figure).'
arch=('x86_64' 'aarch64')
url='https://github.com/Evrytania/LTE-Cell-Scanner'
license=('AGPL')
depends=('itpp' 'boost' 'boost-libs' 'fftw' 'rtl-sdr')
makedepends=('git' 'cmake')
source=("$pkgname::git+https://github.com/Evrytania/LTE-Cell-Scanner.git")
sha512sums=('SKIP')

pkgver() {
  cd $pkgname

  echo $(git rev-list --count HEAD).$(git rev-parse --short HEAD)
}

build() {
  cd $pkgname

  mkdir -p build

  cd build

  cmake ..

  make
}

package() {
  cd $pkgname

  install -Dm 755 build/src/CellSearch "$pkgdir/usr/bin/$pkgname"
  install -Dm 644 -t "$pkgdir/usr/share/doc/$pkgname/" README AUTHORS doc/*
  install -Dm 644 COPYING "$pkgdir/usr/share/licenses/$pkgname/COPYING"
}

