# This file is part of BlackArch Linux ( http://blackarch.org ).
# See COPYING for license details.

pkgname='lte-cell-scanner'
pkgver=57.5fa3df8
pkgrel=1
groups=('blackarch' 'blackarch-scanner' 'blackarch-mobile' 'blackarch-recon')
pkgdesc='LTE SDR cell scanner optimized to work with very low performance RF front ends (8bit A/D, 20dB noise figure).'
arch=('x86_64' 'armv6h' 'armv7h' 'aarch64')
url='https://github.com/Evrytania/LTE-Cell-Scanner'
license=('GAGPL')
depends=('itpp' 'boost' 'boost-libs' 'fftw' 'rtl-sdr')
makedepends=('git' 'cmake')
source=('git+https://github.com/Evrytania/LTE-Cell-Scanner.git')
sha1sums=('SKIP')

pkgver() {
  cd "$srcdir/LTE-Cell-Scanner"

  echo $(git rev-list --count HEAD).$(git rev-parse --short HEAD)
}

build() {
  cd "$srcdir/LTE-Cell-Scanner"

  mkdir build

  cd build

  cmake ..

  make
}

package() {
  cd "$srcdir/LTE-Cell-Scanner"

  install -Dm755 build/src/CellSearch "$pkgdir/usr/bin/lte-cell-scanner"
  install -Dm644 -t "$pkgdir/usr/share/doc/lte-cell-scanner/" \
    README AUTHORS doc/*
  install -Dm644 COPYING "$pkgdir/usr/share/licenses/lte-cell-scanner/COPYING"
}
