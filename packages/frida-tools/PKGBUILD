# This file is part of BlackArch Linux ( https://www.blackarch.org/ ).
# See COPYING for license details.

pkgbase=frida-tools
pkgname=('python2-frida-tools' 'python-frida-tools')
pkgver=9.2.5
pkgrel=1
pkgdesc='Frida CLI tools.'
groups=('blackarch' 'blackarch-mobile' 'blackarch-binary' 'blackarch-reversing')
arch=('any')
url='https://pypi.org/project/frida-tools/#files'
license=('custom:unknown')
makedepends=('python2-setuptools' 'python-setuptools')
source=("https://files.pythonhosted.org/packages/d8/c0/4d00f9e85b499bed8857971040e9a5b9e6f1a995fb3569d25984eaf5294c/$pkgbase-$pkgver.tar.gz"
        'setup.py.patch')
sha512sums=('fb343a0329dffaefdb97f21ef1d49f67a9c57117c9a5196a8b784c59ee3e8ed5b3976b029b462c5ec31327a8b66b5cce2bb539c420b7b578618a41c748c18327'
            '659ccd3e3c0f0e431b03d7ecd6948120a413563002ff9a94fdf4d80c9ca50a910bca54bb6bc5dfcf75ca75235756e0fa8181a16bd3b6880d074f73b3f7232fc9')

prepare() {
  patch -p1 < setup.py.patch

  cp -a "$pkgbase-$pkgver"{,-2}
}

build() {
  cd "$pkgbase-$pkgver-2"

  python2 setup.py build

  cd "$srcdir/$pkgbase-$pkgver"

  python setup.py build
}

package_python2-frida-tools() {
  depends=('python2' 'python2-frida' 'python2-colorama' 'python2-prompt_toolkit'
           'python2-pygments' 'libimobiledevice')

  cd "$pkgbase-$pkgver-2"

  python2 setup.py install --root="$pkgdir" --prefix=/usr -O1 --skip-build

  for i in "$pkgdir/usr/bin/"* ; do
    mv $i "${i}2"
  done
}

package_python-frida-tools() {
  depends=('python' 'python-frida' 'python-colorama' 'python-prompt_toolkit'
           'python-pygments' 'libimobiledevice')

  cd "$pkgbase-$pkgver"

  python setup.py install --root="$pkgdir" --prefix=/usr -O1 --skip-build
}

