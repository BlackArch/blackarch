# This file is part of BlackArch Linux ( https://www.blackarch.org/ ).
# See COPYING for license details.

pkgbase=frida-tools
pkgname=('python2-frida-tools' 'python-frida-tools')
pkgver=10.2.1
pkgrel=1
pkgdesc='Frida CLI tools.'
groups=('blackarch' 'blackarch-mobile' 'blackarch-binary' 'blackarch-reversing')
arch=('any')
url='https://pypi.org/project/frida-tools/#files'
license=('custom:unknown')
makedepends=('python2-setuptools' 'python-setuptools')
source=("https://files.pythonhosted.org/packages/e4/88/b7f60b3797a32e7ecb1d94fe105f30e483ea9d1423e851f0eae53a22aee7/$pkgbase-$pkgver.tar.gz"
        'setup.py.patch')
sha512sums=('e5cf29e13714f0c2925995bebc9c52131313e1bdb8ca48d44ac0d29b96265ace9eae5b9dd85db2a08ac2944263da7c9dcb9cca935bf8e0976e18bec6bd2514c1'
            'f197d57e629372323faacc76cf537c21c0c38ad9b474428a718848e7116aac65ec1cc2fe50d5b1a13e59b67b81e97677206e114a0e6d02d1853af06978369857')

prepare() {
  #patch -p1 < setup.py.patch

  cp -a "$pkgbase-$pkgver"{,-2}
}

build() {
  cd "$pkgbase-$pkgver-2"

  python2 setup.py build

  cd "$srcdir/$pkgbase-$pkgver"

  python setup.py build
}

package_python2-frida-tools() {
  depends=('python2' 'python2-frida' 'python2-colorama' 'python2-prompt_toolkit'
           'python2-pygments' 'libimobiledevice')

  cd "$pkgbase-$pkgver-2"

  python2 setup.py install --root="$pkgdir" --prefix=/usr -O1 --skip-build

  for i in "$pkgdir/usr/bin/"* ; do
    mv $i "${i}2"
  done
}

package_python-frida-tools() {
  depends=('python' 'python-frida' 'python-colorama' 'python-prompt_toolkit'
           'python-pygments' 'libimobiledevice')

  cd "$pkgbase-$pkgver"

  python setup.py install --root="$pkgdir" --prefix=/usr -O1 --skip-build
}

