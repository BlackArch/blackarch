# This file is part of BlackArch Linux ( https://www.blackarch.org/ ).
# See COPYING for license details.

pkgbase=frida-tools
pkgname=('python2-frida-tools' 'python-frida-tools')
pkgver=9.2.3
pkgrel=1
pkgdesc='Frida CLI tools.'
groups=('blackarch' 'blackarch-mobile' 'blackarch-binary' 'blackarch-reversing')
arch=('any')
url='https://pypi.org/project/frida-tools/#files'
license=('custom:unknown')
makedepends=('python2-setuptools' 'python-setuptools')
source=("https://files.pythonhosted.org/packages/d6/92/d8056029ab0d5681ba9339e0d0fd3ecabc11e84ff65fccd0b10ae4c9af61/$pkgbase-$pkgver.tar.gz"
        'setup.py.patch')
sha512sums=('36cb9eda486bdf10fc83999790cc006037ee926c7ecb03c8a2fd75c4cd69d22add9e858385cad25725488176f173657586221f86ff53b6485cf90dfc044592b1'
            '023073983ebaf3869f5bd5eedeedeb64dd4e006e7e7e9e5704d8b14f60b797c51bb8c2725480bad09b25389fdc676f1b7c4751265be8415f6418e1fd0ff9207b')

prepare() {
  patch -p1 < setup.py.patch

  cp -a "$pkgbase-$pkgver"{,-2}
}

build() {
  cd "$pkgbase-$pkgver-2"

  python2 setup.py build

  cd "$srcdir/$pkgbase-$pkgver"

  python setup.py build
}

package_python2-frida-tools() {
  depends=('python2' 'python2-frida' 'python2-colorama' 'python2-prompt_toolkit'
           'python2-pygments' 'libimobiledevice')

  cd "$pkgbase-$pkgver-2"

  python2 setup.py install --root="$pkgdir" --prefix=/usr -O1 --skip-build

  for i in "$pkgdir/usr/bin/"* ; do
    mv $i "${i}2"
  done
}

package_python-frida-tools() {
  depends=('python' 'python-frida' 'python-colorama' 'python-prompt_toolkit'
           'python-pygments' 'libimobiledevice')

  cd "$pkgbase-$pkgver"

  python setup.py install --root="$pkgdir" --prefix=/usr -O1 --skip-build
}

