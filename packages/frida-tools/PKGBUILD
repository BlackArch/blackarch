# This file is part of BlackArch Linux ( https://www.blackarch.org/ ).
# See COPYING for license details.

pkgbase=frida-tools
pkgname=('python2-frida-tools' 'python-frida-tools')
pkgver=8.1.0
pkgrel=1
pkgdesc='Frida CLI tools.'
groups=('blackarch' 'blackarch-mobile' 'blackarch-binary' 'blackarch-reversing')
arch=('any')
url='https://pypi.org/project/frida-tools/#files'
license=('custom:unknown')
makedepends=('python2-setuptools' 'python-setuptools')
source=("https://files.pythonhosted.org/packages/93/fc/83b374242e3960457ac9a36bca30b7eb8a9c75d2b661208424cff567dfdf/$pkgbase-$pkgver.tar.gz"
        'setup.py.patch')
sha512sums=('09d02f9895b319af5d62286d9d0a30aabdee09d6734b6d3026cad3991f69744dd5e684d5e37e062af837ee04c7d4564f7cd4a21019b9507550d3a99698a7cf52'
            '64bcd9f8a3b51ea0eb6d80327eb1bdde03075f02ed5272f84ec10523107cf5aae66f405963ec438a881e859f7b5dcb5bb7bb4cf2279c84a0551135663e9a6495')

prepare() {
  #patch -p1 < setup.py.patch

  cp -a "$pkgbase-$pkgver"{,-2}
}

build() {
  cd "$pkgbase-$pkgver-2"

  python2 setup.py build

  cd "$srcdir/$pkgbase-$pkgver"

  python setup.py build
}

package_python2-frida-tools() {
  depends=('python2' 'python2-frida' 'python2-colorama' 'python2-prompt_toolkit'
           'python2-pygments' 'libimobiledevice')

  cd "$pkgbase-$pkgver-2"

  python2 setup.py install --root="$pkgdir" --prefix=/usr -O1 --skip-build

  for i in "$pkgdir/usr/bin/"* ; do
    mv $i "${i}2"
  done
}

package_python-frida-tools() {
  depends=('python' 'python-frida' 'python-colorama' 'python-prompt_toolkit'
           'python-pygments' 'libimobiledevice')

  cd "$pkgbase-$pkgver"

  python setup.py install --root="$pkgdir" --prefix=/usr -O1 --skip-build
}

