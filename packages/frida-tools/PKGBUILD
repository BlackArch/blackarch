# This file is part of BlackArch Linux ( https://www.blackarch.org/ ).
# See COPYING for license details.

pkgbase=frida-tools
pkgname=('python2-frida-tools' 'python-frida-tools')
pkgver=7.1.0
pkgrel=1
pkgdesc='Frida CLI tools.'
groups=('blackarch' 'blackarch-mobile' 'blackarch-binary' 'blackarch-reversing')
arch=('any')
url='https://pypi.org/project/frida-tools/#files'
license=('custom:unknown')
makedepends=('python2-setuptools' 'python-setuptools')
source=("https://files.pythonhosted.org/packages/99/ba/48d5b1c88a82299b8ceb8c35ee5625b6cc7236b619c2d711a78f5808d0fd/$pkgbase-$pkgver.tar.gz"
        'setup.py.patch')
sha512sums=('d9ae48a9553d04f5e6e79df473c03683cbff06527eda3cc9f8098660e8edc7e62505d24100de5ae02c3d8170baa89ee8f64ea0d0f8adff99de34c4b8939903bf'
            '02d612880f1905b88bc31c90697f486a806ccc6d3210eb0c13770f21fab35e065e0bd09f612933e7250957cf9fd2148a88c6632f9e480286e1a4cfab86f297a8')

prepare() {
  patch -p1 < setup.py.patch

  cp -a "$pkgbase-$pkgver"{,-2}
}

build() {
  cd "$pkgbase-$pkgver-2"

  python2 setup.py build

  cd "$srcdir/$pkgbase-$pkgver"

  python setup.py build
}

package_python2-frida-tools() {
  depends=('python2' 'python2-frida' 'python2-colorama' 'python2-prompt_toolkit'
           'python2-pygments' 'libimobiledevice')

  cd "$pkgbase-$pkgver-2"

  python2 setup.py install --root="$pkgdir" --prefix=/usr -O1 --skip-build

  for i in "$pkgdir/usr/bin/"* ; do
    mv $i "${i}2"
  done
}

package_python-frida-tools() {
  depends=('python' 'python-frida' 'python-colorama' 'python-prompt_toolkit'
           'python-pygments' 'libimobiledevice')

  cd "$pkgbase-$pkgver"

  python setup.py install --root="$pkgdir" --prefix=/usr -O1 --skip-build
}

