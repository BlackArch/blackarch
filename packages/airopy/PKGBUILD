# This file is part of BlackArch Linux ( https://www.blackarch.org/ ).
# See COPYING for license details.

pkgname=airopy
pkgver=5.b83f11d
pkgrel=2
pkgdesc='Get (wireless) clients and access points.'
groups=('blackarch' 'blackarch-wireless' 'blackarch-sniffer')
arch=('any')
url='https://github.com/Josue87/Airopy'
license=('custom:unknown')
depends=('python' 'python-requests' 'python-termcolor' 'scapy')
makedepends=('git')
source=("$pkgname::git+https://github.com/Josue87/Airopy.git")
sha512sums=('SKIP')

pkgver() {
  cd $pkgname

  echo $(git rev-list --count HEAD).$(git rev-parse --short HEAD)
}

package() {
  cd $pkgname

  install -dm 755 "$pkgdir/usr/bin"
  install -dm 755 "$pkgdir/usr/share/$pkgname"

  install -Dm 644 -t "$pkgdir/usr/share/doc/$pkgname/" README.md

  rm README.md

  cp -a * "$pkgdir/usr/share/$pkgname/"

  cat > "$pkgdir/usr/bin/$pkgname" << EOF
#!/bin/sh
cd /usr/share/$pkgname
exec python $pkgname.py "\$@"
EOF

  chmod +x "$pkgdir/usr/bin/$pkgname"
}

