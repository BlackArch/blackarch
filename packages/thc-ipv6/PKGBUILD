# This file is part of BlackArch Linux ( http://blackarch.org ).
# See COPYING for license details.

pkgname='thc-ipv6'
pkgver='2.7'
pkgrel=2
groups=('blackarch' 'blackarch-dos' 'blackarch-exploitation'
        'blackarch-networking')
pkgdesc='A complete tool set to attack the inherent protocol weaknesses of IPv6 and ICMP6, and includes an easy to use packet factory library.'
url='http://thc.org/thc-ipv6/'
depends=('libpcap' 'openssl')
arch=('i686' 'x86_64' 'armv6h' 'armv7h')
license=('GPL')
makedepends=('git')
source=("https://www.thc.org/releases/thc-ipv6-${pkgver}.tar.gz")
sha1sums=('f0796e76daebe25f836bc3426f39d5fc2fc588bb')

prepare() {
  cd "$srcdir/thc-ipv6-$pkgver"

  sed -i 's|+=|=|' Makefile
}

build() {
  cd "$srcdir/thc-ipv6-$pkgver"

  make clean all
}

package() {
  cd "$srcdir/thc-ipv6-$pkgver"

  mkdir -p "$pkgdir/usr/bin"

  find . -type f -perm /o+x | xargs install -t "$pkgdir/usr/bin"
}
