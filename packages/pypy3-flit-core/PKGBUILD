# This file is part of BlackArch Linux ( https://www.blackarch.org/ ).
# See COPYING for license details.

pkgname=pypy3-flit-core
_pkgname=flit_core
pkgver=3.12.0
_pyver=3.11
pkgrel=1
pkgdesc='A PEP 517 build backend for packages using Flit.'
arch=('any')
url='https://github.com/pypa/flit'
license=('BSD-3-Clause')
depends=('pypy3')
source=("https://github.com/pypa/${_pkgname::4}/archive/$pkgver.tar.gz")
sha512sums=('ff6df66dfdae6fdf7b277cc3fbb7c9a569103cbe82a3e3ce6f315ec6adec46be8692eba4549a66b3af537d128e6b57ed8f49316b705636466c25674198503643')

build() {
  cd "${_pkgname::4}-$pkgver/$_pkgname"

  pypy3 -m flit_core.wheel
}

package() {
  cd "${_pkgname::4}-$pkgver/$_pkgname"

  pypy3 bootstrap_install.py --installdir \
    "$pkgdir/opt/pypy3/lib/pypy$_pyver"/site-packages dist/$_pkgname-*.whl
}

