# This file is part of BlackArch Linux ( https://www.blackarch.org/ ).
# See COPYING for license details.

pkgname=koadic
pkgver=397.75592bd
pkgrel=1
epoch=1
pkgdesc='A Windows post-exploitation rootkit similar to other penetration testing tools such as Meterpreter and Powershell Empire.'
groups=('blackarch' 'blackarch-exploitation' 'blackarch-automation')
arch=('any')
url='https://github.com/zerosum0x0/koadic'
license=('custom:unknown')
depends=('python2' 'python2-pycryptodome' 'python2-pyasn1' 'python2-tabulate' 'impacket')
makedepends=('git')
source=('git+https://github.com/zerosum0x0/koadic.git')
sha512sums=('SKIP')

pkgver() {
  cd $pkgname

  echo $(git rev-list --count HEAD).$(git rev-parse --short HEAD)
}

package() {
  cd $pkgname

  install -dm 755 "$pkgdir/usr/bin"
  install -dm 755 "$pkgdir/usr/share/$pkgname"

  install -Dm 644 -t "$pkgdir/usr/share/doc/$pkgname/" README.md DEFCON25.pdf
  install -Dm 644 LICENSE "$pkgdir/usr/share/licenses/$pkgname/LICENSE"

  rm README.md LICENSE DEFCON25.pdf

  cp -a * "$pkgdir/usr/share/$pkgname/"

  cat > "$pkgdir/usr/bin/$pkgname" << EOF
#!/bin/sh
cd /usr/share/$pkgname
exec python2 koadic "\$@"
EOF

  chmod +x "$pkgdir/usr/bin/$pkgname"
}

