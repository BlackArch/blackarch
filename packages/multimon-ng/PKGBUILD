# This file is part of BlackArch Linux ( http://blackarch.org ).
# See COPYING for license details.

pkgname='multimon-ng'
pkgver=20180115
pkgrel=1
pkgdesc='An sdr decoder, supports pocsag, ufsk, clipfsk, afsk, hapn, fsk, dtmf, zvei.'
url='http://dekar.wc3edit.net/2012/05/24/multimonng/'
arch=('x86_64' 'armv6h' 'armv7h' 'aarch64')
groups=('blackarch' 'blackarch-radio')
license=("GPL2")
depends=('ncurses' 'libpulse')
makedepends=('git' 'qt4')
optdepends=('sox: wav conversion')
source=('git://github.com/EliasOenal/multimonNG.git')
sha1sums=('SKIP')

pkgver() {
  cd "$srcdir/multimonNG"

  git show -s --format="%ci" HEAD | cut -d ' ' -f 1 | tr -d '-'
}

build() {
  cd "$srcdir/multimonNG"

  qmake-qt4 PREFIX=/usr multimon-ng.pro

  sed -i 's/-lpulse-simple/& -lpulse/' Makefile

  make PREFIX=/usr
}

package() {
  cd "$srcdir/multimonNG"

  install -Dm755 multimon-ng "$pkgdir/usr/bin/multimon-ng"
}
