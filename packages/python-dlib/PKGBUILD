# This file is part of BlackArch Linux ( https://www.blackarch.org/ ).
# See COPYING for license details.

pkgbase=python-dlib
pkgname=('python2-dlib' 'python-dlib')
_pkgname=dlib
pkgver=19.18.0
pkgrel=1
pkgdesc='A toolkit for making real world machine learning and data analysis applications.'
arch=('x86_64' 'aarch64')
url='https://pypi.org/project/dlib/#files'
license=('custom:unknown')
makedepends=('python2-setuptools' 'python-setuptools' 'cmake')
source=("https://files.pythonhosted.org/packages/1e/62/aacb236d21fbd08148b1d517d58a9d80ea31bdcd386d26f21f8b23b1eb28/$_pkgname-$pkgver.tar.gz")
sha512sums=('6561e7a9d9ec5093696410e11a24afc40e7ea1119b710944d9507a91e56512a1cf0295830fbd5ede23acf0f14b58b8302c091eb7335655a4d4b07909414f369d')

prepare() {
  cp -a "$_pkgname-$pkgver"{,-2}
}

build() {
  cd "$_pkgname-$pkgver-2"

  python2 setup.py build

  cd "$srcdir/$_pkgname-$pkgver"

  python setup.py build
}

package_python2-dlib() {
  depends=('python2')

  cd "$_pkgname-$pkgver-2"

  python2 setup.py install --prefix=/usr --root="$pkgdir" --optimize=1 \
    --skip-build
}

package_python-dlib() {
  depends=('python')

  cd "$_pkgname-$pkgver"

  python setup.py install --prefix=/usr --root="$pkgdir" --optimize=1 \
    --skip-build
}

