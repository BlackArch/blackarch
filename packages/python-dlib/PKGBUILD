# This file is part of BlackArch Linux ( https://www.blackarch.org/ ).
# See COPYING for license details.

pkgbase=python-dlib
pkgname=('python2-dlib' 'python-dlib')
_pkgname=dlib
pkgver=19.19.0
pkgrel=1
pkgdesc='A toolkit for making real world machine learning and data analysis applications.'
arch=('x86_64' 'aarch64')
url='https://pypi.org/project/dlib/#files'
license=('custom:unknown')
makedepends=('python2-setuptools' 'python-setuptools' 'cmake')
source=("https://files.pythonhosted.org/packages/63/92/05c3b98636661cb80d190a5a777dd94effcc14c0f6893222e5ca81e74fbc/$_pkgname-$pkgver.tar.gz")
sha512sums=('b7a290b0f7f15ccaf5bcc60c918763cbd84cace8b6d6be5da925307328a7b9e26342346f5c26f1b82b36ddb4b94312155d2fbb7bed80c6cb5982080593521b85')

prepare() {
  cp -a "$_pkgname-$pkgver"{,-2}
}

build() {
  cd "$_pkgname-$pkgver-2"

  python2 setup.py build

  cd "$srcdir/$_pkgname-$pkgver"

  python setup.py build
}

package_python2-dlib() {
  depends=('python2')

  cd "$_pkgname-$pkgver-2"

  python2 setup.py install --prefix=/usr --root="$pkgdir" --optimize=1 \
    --skip-build
}

package_python-dlib() {
  depends=('python')

  cd "$_pkgname-$pkgver"

  python setup.py install --prefix=/usr --root="$pkgdir" --optimize=1 \
    --skip-build
}

