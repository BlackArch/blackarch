# This file is part of BlackArch Linux ( https://www.blackarch.org/ ).
# See COPYING for license details.

pkgbase=python-dlib
pkgname=('python2-dlib' 'python-dlib')
_pkgname=dlib
pkgver=19.17.0
pkgrel=2
pkgdesc='A toolkit for making real world machine learning and data analysis applications.'
arch=('x86_64' 'armv6h' 'armv7h' 'aarch64')
url='https://pypi.org/project/dlib/#files'
license=('custom:unknown')
makedepends=('python2-setuptools' 'python-setuptools' 'cmake')
source=("https://files.pythonhosted.org/packages/05/57/e8a8caa3c89a27f80bc78da39c423e2553f482a3705adc619176a3a24b36/$_pkgname-$pkgver.tar.gz")
sha512sums=('6eb2dbaabccfef5a3b4e75f9be24fc9c05072f83bbb93103a105c65c2a6822fff84d68c2d294d7064cc777e00e8a33d92f7f79d6d7ceedac69800a075ecce153')

prepare() {
  cp -a "$_pkgname-$pkgver" "$_pkgname-$pkgver-2"
}

build() {
  cd "$_pkgname-$pkgver-2"

  python2 setup.py build

  cd "$srcdir/$_pkgname-$pkgver"

  python setup.py build
}

package_python2-dlib() {
  depends=('python2')

  cd "$_pkgname-$pkgver-2"

  python2 setup.py install --prefix=/usr --root="$pkgdir" --optimize=1 \
    --skip-build
}

package_python-dlib() {
  depends=('python')

  cd "$_pkgname-$pkgver"

  python setup.py install --prefix=/usr --root="$pkgdir" --optimize=1 \
    --skip-build
}

