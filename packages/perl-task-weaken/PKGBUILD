# This file is part of BlackArch Linux ( https://www.blackarch.org/ ).
# See COPYING for license details.

pkgname=perl-task-weaken
_cpanname="Task-Weaken"
pkgver=1.06
pkgrel=2
pkgdesc="Ensure that a platform has weaken support"
arch=('any')
url='https://metacpan.org/pod/Task::Weaken'
license=('PerlArtistic' 'GPL')
options=('!emptydirs')
depends=('perl')
makedepends=('perl-module-install')
source=("http://search.cpan.org/CPAN/authors/id/E/ET/ETHER/Task-Weaken-$pkgver.tar.gz")
sha512sums=('2cd723a748c1fb3df12b465ad278070330959c611ed125f54770c2d820202592cc6e46f16ee43671e310abfcf86fe7ffa639db7dd605d69d7d13fca793d59808')

# Function to change to the working directory and set
# environment variables to override undesired options.
prepareEnvironment() {
  cd "$_cpanname-$pkgver"

  export \
    PERL_MM_USE_DEFAULT=1 \
    PERL_AUTOINSTALL=--skipdeps \
    PERL_MM_OPT="INSTALLDIRS=vendor DESTDIR='$pkgdir'" \
    PERL_MB_OPT="--installdirs vendor --destdir '$pkgdir'" \
    MODULEBUILDRC=/dev/null
}

build() {
  prepareEnvironment
  /usr/bin/perl Makefile.PL
  make
}

package() {
  prepareEnvironment
  make install

  find "$pkgdir" -name .packlist -o -name perllocal.pod -delete
}

