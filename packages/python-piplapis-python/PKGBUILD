# This file is part of BlackArch Linux ( https://www.blackarch.org/ ).
# See COPYING for license details.

pkgbase=python-piplapis-python
pkgname=('python2-piplapis-python' 'python-piplapis-python')
pkgver=5.1.5
pkgrel=1
pkgdesc='Client library for use with the Pipl search API.'
arch=('any')
url='https://pypi.org/project/piplapis-python/#files'
license=('Apache')
makedepends=('python2-setuptools' 'python-setuptools')
source=("https://files.pythonhosted.org/packages/5a/88/aec9ad32c803653cbe06d436c8d542ba631414d1a094de603d48dfbd982a/piplapis-python-$pkgver.tar.gz")
sha512sums=('5cd7166818bc57529fa21b540afb071b934f7b66a49edf390f10588e5c38ccdb5c742de4de406038d5378a69a1a30f88d43a1943edfadc9b8963154304cf2388')

prepare() {
  cp -a "piplapis-python-$pkgver" "piplapis-python2-$pkgver"
}

build() {
  cd "piplapis-python2-$pkgver"

  python2 setup.py build

  cd "$srcdir/piplapis-python-$pkgver"

  python setup.py build
}

package_python2-piplapis-python() {
  depends=('python2-six' 'python2-pytz')

  cd "piplapis-python2-$pkgver"

  python2 setup.py install --prefix=/usr --root="$pkgdir" --optimize=1 \
    --skip-build
}

package_python-piplapis-python() {
  depends=('python-six' 'python-pytz')

  cd "piplapis-python-$pkgver"

  python setup.py install --prefix=/usr --root="$pkgdir" --optimize=1 \
    --skip-build
}

