# This file is part of BlackArch Linux ( https://www.blackarch.org/ ).
# See COPYING for license details.

pkgbase=python-piplapis-python
_pkgname=piplapis-python
pkgname=('python2-piplapis-python' 'python-piplapis-python')
pkgver=5.2.1
pkgrel=1
pkgdesc='Client library for use with the Pipl search API.'
arch=('any')
url='https://pypi.org/project/piplapis-python/#files'
license=('Apache')
makedepends=('python2-setuptools' 'python-setuptools')
source=("https://files.pythonhosted.org/packages/36/6c/02c3d83f5436ba14c3cc36d08c97b341a995966271d7eedc4d35b5a443e9/$_pkgname-$pkgver.tar.gz")
sha512sums=('922945e2c16f1652aa63dc787d71401a87f65133ea540a20eefd4f418538523db5bc668c82e457a18d431ff89d59fd7d2955aa497b3282bb1a9dbe6ad7ae96a2')

prepare() {
  cp -a "piplapis-python-$pkgver" "piplapis-python2-$pkgver"
}

build() {
  cd "piplapis-python2-$pkgver"

  python2 setup.py build

  cd "$srcdir/piplapis-python-$pkgver"

  python setup.py build
}

package_python2-piplapis-python() {
  depends=('python2-six' 'python2-pytz')

  cd "piplapis-python2-$pkgver"

  python2 setup.py install --prefix=/usr --root="$pkgdir" --optimize=1 \
    --skip-build
}

package_python-piplapis-python() {
  depends=('python-six' 'python-pytz')

  cd "piplapis-python-$pkgver"

  python setup.py install --prefix=/usr --root="$pkgdir" --optimize=1 \
    --skip-build
}

