# This file is part of BlackArch Linux ( https://www.blackarch.org/ ).
# See COPYING for license details.

pkgname=gvm-libs
pkgver=22.4.0
pkgrel=2
pkgdesc='greenbone-vulnerability-manager libraries.'
arch=('x86_64' 'aarch64')
url='https://github.com/greenbone/gvm-libs'
license=('GPL')
depends=('gnutls' 'libpcap' 'gpgme' 'libssh' 'glib2' 'libldap' 'hiredis' 'sudo'
         'libnet' 'paho-mqtt-c' 'zlib' 'libgcrypt' 'util-linux-libs' 'libxml2')
makedepends=('cmake' 'doxygen' 'graphviz')
source=("https://github.com/greenbone/gvm-libs/archive/v$pkgver.tar.gz")
sha512sums=('607625a955720b37679c76053bbbaeb99e06832488a90e052c0315900f9cadc34616f2db3f8408d116354d80d30c71618e3c1bf51dcc7f5a80ddeb17b55ab850')
replaces=('openvas-libraries')

prepare() {
  cd "$pkgname-$pkgver"

  sed -i '/-Werror/d' CMakeLists.txt
}

build() {
  cd "$pkgname-$pkgver"

  #LDFLAGS+=" -Wl,--no-as-needed"

  cmake \
    -DCMAKE_BUILD_TYPE=None \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DLIBDIR=/usr/lib \
    -DSYSCONFDIR=/etc \
    -DLOCALSTATEDIR=/var \
    -B build \
    -S "$pkgname-$pkgver" \
    .

  make -C build
}

package() {
  cd "$pkgname-$pkgver"

  make DESTDIR="$pkgdir" -C build install

  rm -rf "$pkgdir/run"
}

