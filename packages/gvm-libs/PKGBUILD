# This file is part of BlackArch Linux ( https://www.blackarch.org/ ).
# See COPYING for license details.

pkgname=gvm-libs
pkgver=22.5.0
pkgrel=1
pkgdesc='greenbone-vulnerability-manager libraries.'
arch=('x86_64' 'aarch64')
url='https://github.com/greenbone/gvm-libs'
license=('GPL')
depends=('gnutls' 'libpcap' 'gpgme' 'libssh' 'glib2' 'libldap' 'hiredis' 'sudo'
         'libnet' 'paho-mqtt-c' 'zlib' 'libgcrypt' 'util-linux-libs' 'libxml2')
makedepends=('cmake' 'doxygen' 'graphviz')
source=("https://github.com/greenbone/gvm-libs/archive/v$pkgver.tar.gz")
sha512sums=('3e7b9d8a9f846198be629417c11f90b981839aa33958f956144609bdc5a06bd3ec7451f0202c1c3d8447b8107428ede3d79ddcae90ea7af440932cccb301c1f4')
replaces=('openvas-libraries')

prepare() {
  cd "$pkgname-$pkgver"

  sed -i '/-Werror/d' CMakeLists.txt
}

build() {
  cd "$pkgname-$pkgver"

  #LDFLAGS+=" -Wl,--no-as-needed"

  cmake \
    -DCMAKE_BUILD_TYPE=None \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DLIBDIR=/usr/lib \
    -DSYSCONFDIR=/etc \
    -DLOCALSTATEDIR=/var \
    -B build \
    -S "$pkgname-$pkgver" \
    .

  make -C build
}

package() {
  cd "$pkgname-$pkgver"

  make DESTDIR="$pkgdir" -C build install

  rm -rf "$pkgdir/run"
}

