# This file is part of BlackArch Linux ( http://blackarch.org ).
# See COPYING for license details.

pkgname='libbde'
pkgver=232.7621829
pkgrel=1
pkgdesc='A library to access the BitLocker Drive Encryption (BDE) format.'
arch=('x86_64' 'armv6h' 'armv7h' 'aarch64')
url='https://github.com/libyal/libbde'
license=('LGPL3')
provides=('libbde')
makedepends=('git' 'autoconf' 'automake' 'libtool' 'gettext' 'pkg-config'
             'bison' 'flex')
source=('git+https://github.com/libyal/libbde.git')
sha1sums=('SKIP')

_gitname='libbde'

pkgver() {
  cd "$srcdir/$_gitname"

  echo $(git rev-list --count HEAD).$(git rev-parse --short HEAD)
}

build() {
  cd "$_gitname"

  ./synclibs.sh
  ./autogen.sh
  ./configure \
    --prefix=/usr \
    --sysconfdir=/etc

  make
}

package() {
  cd "$_gitname"

  make DESTDIR="$pkgdir/" install
}
