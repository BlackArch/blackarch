# This file is part of BlackArch Linux ( http://blackarch.org ).
# See COPYING for license details.

pkgname='phpstan'
pkgver=492.70b3d36
pkgrel=1
pkgdesc='PHP Static Analysis Tool - discover bugs in your code without running it.'
groups=('blackarch' 'blackarch-code-audit')
arch=('any')
url='https://github.com/phpstan/phpstan'
license=('MIT')
depends=('php')
makedepends=('git')
source=('git+https://github.com/phpstan/phpstan.git')
sha1sums=('SKIP')

pkgver() {
  cd "$srcdir/phpstan"

  echo $(git rev-list --count HEAD).$(git rev-parse --short HEAD)
}

package() {
  cd "$srcdir/phpstan"

  mkdir -p "$pkgdir/usr/bin"
  mkdir -p "$pkgdir/usr/share/phpstan"

  install -Dm644 -t "$pkgdir/usr/share/doc/phpstan/" *.md
  install -Dm644 LICENSE "$pkgdir/usr/share/licenses/phpstan/LICENSE"

  rm *.md LICENSE

  cp -a * "$pkgdir/usr/share/phpstan/"

  cat > "$pkgdir/usr/bin/phpstan" << EOF
#!/bin/sh
cd /usr/share/phpstan
exec php bin/phpstan "\${@}"
EOF

  chmod a+x "$pkgdir/usr/bin/phpstan"
}
