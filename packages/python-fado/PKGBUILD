# This file is part of BlackArch Linux ( https://www.blackarch.org/ ).
# See COPYING for license details.

pkgbase=python-fado
pkgname=('python2-fado' 'python-fado')
_pkgname=FAdo
pkgver=2.0.4
pkgrel=1
pkgdesc='Formal Languages manipulation module.'
arch=('any')
url='https://pypi.org/project/FAdo/#files'
license=('GPL')
makedepends=('python2-setuptools' 'python-setuptools')
source=("https://files.pythonhosted.org/packages/source/${_pkgname::1}/$_pkgname/FAdo-$pkgver.tar.gz")
sha512sums=('5d9330a717e8dc48a0e8daa44bd1fe30e6282260d235b95be278efc4e1df8e4704d7089eb6c94455486aac4ff593e8430ee9a62c19a4855a11d0db74e398d72b')

prepare() {
  cp -a "$_pkgname-$pkgver"{,-2}
}

build() {
  cd "$_pkgname-$pkgver-2"

  python2 setup.py build

  cd "$srcdir/$_pkgname-$pkgver"

  python setup.py build
}

package_python2-fado() {
  depends=('python2')

  cd "$_pkgname-$pkgver-2"

  python2 setup.py install --prefix=/usr --root="$pkgdir" --optimize=1 \
    --skip-build
}

package_python-fado() {
  depends=('python')

  cd "$_pkgname-$pkgver"

  python setup.py install --prefix=/usr --root="$pkgdir" --optimize=1 \
    --skip-build
}

