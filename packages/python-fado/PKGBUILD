# This file is part of BlackArch Linux ( https://www.blackarch.org/ ).
# See COPYING for license details.

pkgbase=python-fado
pkgname=('python2-fado' 'python-fado')
_pkgname=FAdo
pkgver=1.3.5.1
pkgrel=5
pkgdesc='Formal Languages manipulation module.'
arch=('any')
url='https://pypi.org/project/FAdo/#files'
license=('GPL')
makedepends=('python2-setuptools' 'python-setuptools')
source=("https://files.pythonhosted.org/packages/dd/d6/d57245b69beda89f2d7b33828a6588f6a986eead3ece1b002d759f64e9ed/$_pkgname-$pkgver.tar.gz")
sha512sums=('a193fe82eea1b316eb22bfceeff50038a12abd8054e1e408117d2d023f0c87629d17463e70619db6c6739f151ef83873109bbc43e1409c31578b39ea9149eaf8')

prepare() {
  cp -a "$_pkgname-$pkgver"{,-2}
}

build() {
  cd "$_pkgname-$pkgver-2"

  python2 setup.py build

  cd "$srcdir/$_pkgname-$pkgver"

  python setup.py build
}

package_python2-fado() {
  depends=('python2')

  cd "$_pkgname-$pkgver-2"

  python2 setup.py install --prefix=/usr --root="$pkgdir" --optimize=1 \
    --skip-build
}

package_python-fado() {
  depends=('python')

  cd "$_pkgname-$pkgver"

  python setup.py install --prefix=/usr --root="$pkgdir" --optimize=1 \
    --skip-build
}

