# This file is part of BlackArch Linux ( https://www.blackarch.org/ ).
# See COPYING for license details.

pkgbase=python-fado
pkgname=('python2-fado' 'python-fado')
_pkgname=FAdo
pkgver=2.0.2
pkgrel=1
pkgdesc='Formal Languages manipulation module.'
arch=('any')
url='https://pypi.org/project/FAdo/#files'
license=('GPL')
makedepends=('python2-setuptools' 'python-setuptools')
source=("https://files.pythonhosted.org/packages/5a/3b/9f5fede2ce75bc5d0628a5f17def8f5f2d7bf78f962d1c28f46f7cf29812/FAdo-$pkgver.tar.gz")
sha512sums=('22f95c470206a064c3c276606bd0c42b28000dbd12da6ae001bc52a293e88bf204477a73bcfc7fae521cc8fdb22f5140e8df5941d2876dcc545075b367bdcfed')

prepare() {
  cp -a "$_pkgname-$pkgver"{,-2}
}

build() {
  cd "$_pkgname-$pkgver-2"

  python2 setup.py build

  cd "$srcdir/$_pkgname-$pkgver"

  python setup.py build
}

package_python2-fado() {
  depends=('python2')

  cd "$_pkgname-$pkgver-2"

  python2 setup.py install --prefix=/usr --root="$pkgdir" --optimize=1 \
    --skip-build
}

package_python-fado() {
  depends=('python')

  cd "$_pkgname-$pkgver"

  python setup.py install --prefix=/usr --root="$pkgdir" --optimize=1 \
    --skip-build
}

