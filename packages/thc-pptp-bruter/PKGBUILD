# This file is part of BlackArch Linux ( http://blackarch.org ).
# See COPYING for license details.

pkgname='thc-pptp-bruter'
pkgver='0.1.4'
pkgrel=7
groups=('blackarch' 'blackarch-cracker')
pkgdesc='A brute force program that works against pptp vpn endpoints (tcp port 1723).'
url='http://www.thc.org'
license=('GPL')
makedepends=('gcc' 'git')
depends=('openssl')
arch=('x86_64' 'armv6h' 'armv7h' 'aarch64')
source=('git+https://github.com/BlackArch/thc-pptp-bruter.git')
sha1sums=('SKIP')

build() {
  cd "$srcdir/thc-pptp-bruter"

  if [[ "$CARCH" == "i686" || "$CARCH" == "x86_64" ]];
  then
    ./configure --prefix=/usr
  else
    ./configure --build=arm --prefix=/usr
  fi

  make CFLAGS="$CFLAGS"
}

package() {
  cd "$srcdir/thc-pptp-bruter"

  make DESTDIR="$pkgdir/" install
}
