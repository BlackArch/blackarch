# This file is part of BlackArch Linux ( http://blackarch.org ).
# See COPYING for license details.

pkgname=imagejs
pkgver=54.1b0b3aa
pkgrel=1
groups=('blackarch' 'blackarch-binary' 'blackarch-webapp')
pkgdesc='Small tool to package javascript into a valid image file.'
arch=('x86_64' 'armv6h' 'armv7h' 'aarch64')
url='https://github.com/jklmnn/imagejs'
license=('GPL3')
depends=('python2')
makedepends=('git' 'cmake')
source=('git+https://github.com/jklmnn/imagejs.git')
sha512sums=('SKIP')

pkgver() {
  cd "$srcdir/imagejs"

  echo $(git rev-list --count HEAD).$(git rev-parse --short HEAD)
}

build() {
  cd "$srcdir/imagejs"

  cmake .
  make
}

package() {
  cd "$srcdir/imagejs"

  install -Dm755 imagejs "$pkgdir/usr/bin/imagejs"
  install -Dm644 README.md "$pkgdir/usr/share/doc/imagejs/README.md"
  install -Dm644 LICENSE "$pkgdir/usr/share/licenses/imagejs/LICENSE"
}
