# This file is part of BlackArch Linux ( https://www.blackarch.org/ ).
# See COPYING for license details.

pkgname=bloodhound-python
_pkgname=BloodHound.py
pkgver=v1.0.1.r16.gfd793b9
pkgrel=2
pkgdesc='Bloodhound python data collector'
arch=('any')
groups=('blackarch' 'blackarch-recon' 'blackarch-windows')
url='https://github.com/fox-it/BloodHound.py'
license=('MIT')
depends=('python' 'python-dnspython' 'impacket' 'python-ldap3' 'python-pyasn1'
         'python-future' 'ldapdomaindump')
makedepends=('python-setuptools' 'git')
source=("git+https://github.com/fox-it/$_pkgname.git")
sha512sums=('SKIP')

pkgver() {
  cd $_pkgname

  git describe --long | sed 's/\([^-]*-g\)/r\1/;s/-/./g'
}

build() {
  cd "$_pkgname"

  python setup.py build
}

package() {
  cd "$_pkgname"

  python setup.py install --root="$pkgdir" --prefix=/usr -O1 --skip-build

  install -Dm 644 -t "$pkgdir/usr/share/doc/$pkgname/" README.md
  install -Dm 644 LICENSE "$pkgdir/usr/share/licenses/$pkgname/LICENSE"
}

