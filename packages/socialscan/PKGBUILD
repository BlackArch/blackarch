# This file is part of BlackArch Linux ( https://www.blackarch.org/ ).
# See COPYING for license details.

pkgname=socialscan
pkgver=81.ba05c84
pkgrel=1
pkgdesc='Check email address and username availability on online platforms.'
arch=('any')
groups=('blackarch' 'blackarch-recon')
url='https://github.com/iojw/socialscan'
license=('MIT')
depends=('python' 'python-aiohttp' 'python-colorama' 'python-tqdm')
makedepends=('git' 'python-setuptools')
source=("git+https://github.com/iojw/$pkgname.git")
sha512sums=('SKIP')

pkgver() {
  cd $pkgname

  echo $(git rev-list --count HEAD).$(git rev-parse --short HEAD)
}

build() {
  cd $pkgname

  python setup.py build
}

package() {
  cd $pkgname

  python setup.py install --prefix=/usr --root="$pkgdir" -O1 --skip-build
}

