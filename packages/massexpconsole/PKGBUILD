# This file is part of BlackArch Linux ( http://blackarch.org ).
# See COPYING for license details.

pkgname='massexpconsole'
pkgver=169.d5af571
pkgrel=1
pkgdesc='A collection of tools and exploits with a cli ui for mass exploitation.'
groups=('blackarch' 'blackarch-automation' 'blackarch-exploitation')
arch=('any')
url='https://github.com/jm33-m0/massExpConsole'
license=('GPL3')
depends=('python2' 'python2-requests' 'python2-beautifulsoup4'
         'python2-pyopenssl' 'python2-html5lib' 'python2-docopt')
makedepends=('git')
source=('git+https://github.com/jm33-m0/massExpConsole')
sha1sums=('SKIP')

pkgver() {
  cd "$srcdir/massExpConsole"

  echo $(git rev-list --count HEAD).$(git rev-parse --short HEAD)
}

package() {
  cd "$srcdir/massExpConsole"

  mkdir -p "$pkgdir/usr/bin"
  mkdir -p "$pkgdir/usr/share/massexpconsole"

  install -Dm644 -t "$pkgdir/usr/share/doc/massexpconsole/" README.md
  install -Dm644 LICENSE "$pkgdir/usr/share/massexpconsole/LICENSE"

  rm README.md LICENSE

  cp -a * "$pkgdir/usr/share/massexpconsole/"

  cat > "$pkgdir/usr/bin/massexpconsole" << EOF
#!/bin/sh
cd /usr/share/massexpconsole
exec python2 mec.py "\${@}"
EOF

  chmod a+x "$pkgdir/usr/bin/massexpconsole"
}
