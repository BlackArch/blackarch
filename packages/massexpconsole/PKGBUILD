# This file is part of BlackArch Linux ( https://www.blackarch.org/ ).
# See COPYING for license details.

pkgname=massexpconsole
pkgver=197.48c2cc4
pkgrel=1
pkgdesc='A collection of tools and exploits with a cli ui for mass exploitation.'
groups=('blackarch' 'blackarch-automation' 'blackarch-exploitation')
arch=('any')
url='https://github.com/jm33-m0/massExpConsole'
license=('GPL3')
depends=('python2' 'python2-requests' 'python2-beautifulsoup4'
         'python2-pyopenssl' 'python2-html5lib' 'python2-docopt')
makedepends=('git')
source=('git+https://github.com/jm33-m0/massExpConsole')
sha512sums=('SKIP')

pkgver() {
  cd massExpConsole

  echo $(git rev-list --count HEAD).$(git rev-parse --short HEAD)
}

package() {
  cd massExpConsole

  install -dm 755 "$pkgdir/usr/bin"
  install -dm 755 "$pkgdir/usr/share/$pkgname"

  install -Dm 644 -t "$pkgdir/usr/share/doc/$pkgname/" README.md
  install -Dm 644 LICENSE "$pkgdir/usr/share/$pkgname/LICENSE"

  rm README.md LICENSE

  cp -a * "$pkgdir/usr/share/$pkgname/"

  cat > "$pkgdir/usr/bin/$pkgname" << EOF
#!/bin/sh
cd /usr/share/$pkgname
exec python2 mec.py "\$@"
EOF

  chmod +x "$pkgdir/usr/bin/$pkgname"
}

