# This file is part of BlackArch Linux ( https://www.blackarch.org/ ).
# See COPYING for license details.

pkgname=massexpconsole
pkgver=200.c3a7e68
pkgrel=1
pkgdesc='A collection of tools and exploits with a cli ui for mass exploitation.'
groups=('blackarch' 'blackarch-automation' 'blackarch-exploitation')
arch=('any')
url='https://github.com/jm33-m0/massExpConsole'
license=('GPL3')
depends=('python' 'python-requests' 'python-beautifulsoup4' 'python-psutil'
         'python-pyopenssl' 'python-html5lib' 'python-docopt' 'python-tqdm')
makedepends=('git')
source=('git+https://github.com/jm33-m0/massExpConsole.git')
sha512sums=('SKIP')

pkgver() {
  cd massExpConsole

  echo $(git rev-list --count HEAD).$(git rev-parse --short HEAD)
}

package() {
  cd massExpConsole

  install -dm 755 "$pkgdir/usr/bin"
  install -dm 755 "$pkgdir/usr/share/$pkgname"

  install -Dm 644 -t "$pkgdir/usr/share/doc/$pkgname/" README.md
  install -Dm 644 LICENSE "$pkgdir/usr/share/$pkgname/LICENSE"

  rm README.md LICENSE

  cp -a * "$pkgdir/usr/share/$pkgname/"

  cat > "$pkgdir/usr/bin/$pkgname" << EOF
#!/bin/sh
cd /usr/share/$pkgname
exec python mec.py "\$@"
EOF

  chmod +x "$pkgdir/usr/bin/$pkgname"
}

