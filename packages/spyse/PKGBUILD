# This file is part of BlackArch Linux ( https://www.blackarch.org/ ).
# See COPYING for license details.

pkgname=spyse
pkgver=47.cd11ba9
pkgrel=11
pkgdesc='Python API wrapper and command-line client for the tools hosted on spyse.com.'
groups=('blackarch' 'blackarch-recon')
arch=('any')
url='https://github.com/zeropwn/spyse.py'
license=('MIT')
depends=('python' 'python-requests')
makedepends=('git' 'python-setuptools' 'python-pip' 'python-wheel')
source=("$pkgname::git+https://github.com/zeropwn/spyse.py.git")
sha512sums=('SKIP')

pkgver() {
  cd $pkgname

  ( set -o pipefail
    git describe --long --tags --abbrev=7 2>/dev/null |
      sed 's/\([^-]*-g\)/r\1/;s/-/./g' ||
    printf "%s.%s" "$(git rev-list --count HEAD)" \
      "$(git rev-parse --short=7 HEAD)"
  )
}

build() {
  cd $pkgname

  python setup.py build
}

package() {
  cd $pkgname

  python setup.py install --root="$pkgdir" --prefix=/usr -O1 --skip-build

  install -Dm 644 -t "$pkgdir/usr/share/doc/$pkgname/" README.md
  install -Dm 644 LICENSE "$pkgdir/usr/share/licenses/$pkgname/LICENSE"
}

