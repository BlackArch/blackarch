# This file is part of BlackArch Linux ( http://blackarch.org ).
# See COPYING for license details.

pkgname=libpfring
_name=PF_RING
pkgver=7.2.0
pkgrel=1
epoch=1
pkgdesc='Library to interface with the PF_RING kernel module.'
url='https://github.com/ntop/PF_RING'
arch=('x86_64' 'i686')
license=('LGPL')
depends=('numactl' 'pfring-kmod')
makedepends=('binutils' 'bison' 'flex' 'git' 'linux-headers' 'make')
options=('staticlibs')
source=("https://github.com/ntop/$_name/archive/$pkgver.tar.gz")
sha512sums=('96134e97b46e993ca5e01441c578d8e0da66d26cc6ca02477791fb5f38023d01625583233cd78131b058dbc7d3b74fc4b833659c5618830e2d235a1a078bf1b6')

build() {
  cd "$_name-$pkgver/userland/lib"

  ./configure --disable-bpf --prefix=/usr --libdir=/usr/lib/libpfring \
    --includedir=/usr/include/pfring

  make
}

package() {
  cd "$_name-$pkgver"

  install -dm 755 "$pkgdir/etc/ld.so.conf.d/"
  install -Dm 644 -t "$pkgdir/usr/share/doc/$pkgname" doc/README.md \
    doc/hugepages.rst
  install -Dm 644 "LICENSE" "$pkgdir/usr/share/licenses/$pkgname/LICENSE"

  echo '/usr/lib/libpfring' > "$pkgdir/etc/ld.so.conf.d/$pkgname.conf"

  cd "userland/lib"

  make DESTDIR="$pkgdir" install
}

