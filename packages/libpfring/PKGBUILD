# This file is part of BlackArch Linux ( https://www.blackarch.org/ ).
# See COPYING for license details.

pkgname=libpfring
pkgver=7.4.0.r133.g9ce51109
pkgrel=1
epoch=2
pkgdesc='Library to interface with the PF_RING kernel module.'
url='https://github.com/ntop/PF_RING'
arch=('x86_64' 'i686')
license=('LGPL')
depends=('numactl' 'pfring-dkms')
makedepends=('binutils' 'bison' 'flex' 'git' 'linux-headers' 'make')
options=('staticlibs')
source=('git+https://github.com/ntop/PF_RING.git')
sha512sums=('SKIP')
_dir_name_=PF_RING

pkgver() {
  cd $_dir_name_

  git describe --long --tags | sed 's/\([^-]*-g\)/r\1/;s/-/./g'
}

build() {
  cd "$_dir_name_/userland/lib"

  ./configure --disable-bpf --prefix=/usr --libdir=/usr/lib/libpfring \
    --includedir=/usr/include/pfring

  make
}

package() {
  cd $_dir_name_

  install -dm 755 "$pkgdir/etc/ld.so.conf.d/"
  install -Dm 644 -t "$pkgdir/usr/share/doc/$pkgname" doc/README.md \
    doc/hugepages.rst
  install -Dm 644 "LICENSE" "$pkgdir/usr/share/licenses/$pkgname/LICENSE"

  echo '/usr/lib/libpfring' > "$pkgdir/etc/ld.so.conf.d/$pkgname.conf"

  cd "userland/lib"

  make DESTDIR="$pkgdir" install
}

