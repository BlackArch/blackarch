# This file is part of BlackArch Linux ( http://blackarch.org ).
# See COPYING for license details.

pkgname='perl-http-parser-xs'
pkgver='0.17'
pkgrel=2
pkgdesc='A fast, primitive HTTP request parser.'
arch=('x86_64' 'armv6h' 'armv7h' 'aarch64')
license=('PerlArtistic' 'GPL')
options=('!emptydirs')
depends=('perl')
makedepends=('perl-module-install')
url='http://search.cpan.org/dist/HTTP-Parser-XS'
source=("http://search.cpan.org/CPAN/authors/id/K/KA/KAZUHO/HTTP-Parser-XS-${pkgver}.tar.gz")
sha1sums=('df6e384fb7b15919e2ea804ab4ca67aa5c3b25ac')
_distdir="HTTP-Parser-XS-${pkgver}"

build() {
  cd "$srcdir"

  ( export PERL_MM_USE_DEFAULT=1 PERL5LIB=""                 \
      PERL_AUTOINSTALL=--skipdeps                            \
      PERL_MM_OPT="INSTALLDIRS=vendor DESTDIR='$pkgdir'"     \
      PERL_MB_OPT="--installdirs vendor --destdir '$pkgdir'" \
      MODULEBUILDRC=/dev/null

    cd "$srcdir/$_distdir"
    /usr/bin/perl Makefile.PL
    make
  )
}

package() {
  cd "$srcdir/$_distdir"

  make install
}
