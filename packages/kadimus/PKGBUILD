# This file is part of BlackArch Linux ( https://www.blackarch.org/ ).
# See COPYING for license details.

pkgname=kadimus
pkgver=57.1d86f89
pkgrel=1
groups=('blackarch' 'blackarch-webapp' 'blackarch-exploitation'
        'blackarch-scanner')
pkgdesc='LFI Scan & Exploit Tool.'
arch=('x86_64' 'armv6h' 'armv7h' 'aarch64')
url='https://github.com/P0cL4bs/Kadimus'
license=('MIT')
depends=('curl' 'pcre' 'libssh' 'openssl')
makedepends=('git')
source=('git+https://github.com/P0cL4bs/Kadimus.git')
sha512sums=('SKIP')

pkgver() {
  cd Kadimus

  echo $(git rev-list --count HEAD).$(git rev-parse --short HEAD)
}

prepare() {
  cd Kadimus

  sed -i 's|-lssh|-lssh -lcrypto|' Makefile

  sed -i 's|./resource/|/usr/share/kadimus/resource/|' src/kadimus_xpl.h
}

build() {
  cd Kadimus

  make
}

package() {
  cd Kadimus

  install -Dm 755 kadimus "$pkgdir/usr/bin/$pkgname"
  install -Dm 644 -t "$pkgdir/usr/share/$pkgname/resource" resource/*
  install -Dm 644 README.md "$pkgdir/usr/share/doc/$pkgname/README.md"
  install -Dm 644 license.txt "$pkgdir/usr/share/licenses/$pkgname/LICENSE"
}

