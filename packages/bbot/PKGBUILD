# This file is part of BlackArch Linux ( https://www.blackarch.org/ ).
# See COPYING for license details.

pkgname=bbot
pkgver=5246.95d1bc5
pkgrel=1
pkgdesc='Multipurpose scanner built to automate your Recon, Bug Bounties, and ASM.'
arch=('any')
groups=('blackarch' 'blackarch-recon')
url='https://github.com/blacklanternsecurity/bbot'
license=('GPL-3.0-or-later')
depends=('python')
makedepends=('git' 'python-virtualenv' 'python-pip')
source=("git+https://github.com/blacklanternsecurity/$pkgname.git")
sha512sums=('SKIP')

pkgver() {
  cd $pkgname

  ( set -o pipefail
    git describe --long --tags --abbrev=7 2>/dev/null |
      sed 's/\([^-]*-g\)/r\1/;s/-/./g' ||
    printf "%s.%s" "$(git rev-list --count HEAD)" \
      "$(git rev-parse --short=7 HEAD)"
  )
}

package() {
    cd $pkgname

    install -dm755 "$pkgdir/usr/share/$pkgname"

    python -m venv "$pkgdir/usr/share/$pkgname/venv"
    source "$pkgdir/usr/share/$pkgname/venv/bin/activate"

    pip install --upgrade pip
    pip install .

    deactivate

    install -Dm755 /dev/stdin "$pkgdir/usr/bin/$pkgname" <<EOF
#!/bin/bash
source /usr/share/$pkgname/venv/bin/activate
exec python -m $pkgname.cli "\$@"
EOF
}

