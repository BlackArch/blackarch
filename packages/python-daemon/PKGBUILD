# This file is part of BlackArch Linux ( https://www.blackarch.org/ ).
# See COPYING for license details.
#
# from AUR - adjusted to our style
#
# Old Maintainer: Jaroslav Lichtblau <svetlemodry@archlinux.org>
# Old Contributor: Anatol Pomozov
# Old Contributor: Massimiliano Torromeo <massimiliano.torromeo@gmail.com>

pkgname=python-daemon
pkgver=3.0.1
pkgrel=3
pkgdesc='Library to implement a well-behaved Unix daemon process'
arch=('any')
url='https://pypi.python.org/pypi/python-daemon'
license=('Apache')
depends=('python' 'python-lockfile' 'python-setuptools')
makedepends=('python-build' 'python-docutils' 'python-installer' 'python-wheel')
checkdepends=('python-testscenarios' 'python-testtools')
source=("https://files.pythonhosted.org/packages/source/p/$pkgname/$pkgname-$pkgver.tar.gz"
        '3bbc4696.patch')
sha512sums=('ea35ec3e930e533ef08a492aa36492a958c9258857cdc75ab2ddb6688a7d8990282cfa0f63ee5cb6b7036b9cb71be03631d5d0d58788a08052425a441bce4e9a'
            '5a24e4fa860fe5809bdbcbc939403b8d61569a181e3f0188f68b137c21099d8a6e56631052e54b9a5ee722a26dbd259d0eedeb5f5a8f316d1bb82a41cde7755d')

prepare() {
  cd "$pkgname-$pkgver"

  patch -p1 -i ../3bbc4696.patch # Fix build
}

build() {
  cd "$pkgname-$pkgver"

  python -m build --wheel --no-isolation
}

package() {
  cd "$pkgname-$pkgver"

  python -m installer --destdir="$pkgdir" dist/*.whl

  install -Dm 644 LICENSE.ASF-2 \
    "$pkgdir/usr/share/licenses/$pkgname/LICENSE.ASF-2"
  install -Dm 644 LICENSE.GPL-3 \
  "$pkgdir/usr/share/licenses/$pkgname/LICENSE.GPL-3"
}

