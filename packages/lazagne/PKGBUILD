# This file is part of BlackArch Linux ( http://blackarch.org ).
# See COPYING for license details.

pkgname=lazagne
pkgver=441.1483df3
pkgrel=1
groups=('blackarch' 'blackarch-forensic' 'blackarch-social')
arch=('any')
pkgdesc='An open source application used to retrieve lots of passwords stored on a local computer.'
url="https://github.com/AlessandroZ/LaZagne"
license=('LGPL3')
depends=('python2' 'python2-argparse' 'python2-psutil' 'python2-pycryptodome'
         'python2-pyasn1' 'python2-pydes' 'python2-pbkdf2' 'python2-libkeepass')
makedepends=('git')
source=('git+https://github.com/AlessandroZ/LaZagne')
sha512sums=('SKIP')

pkgver() {
  cd "$srcdir/LaZagne"

  echo $(git rev-list --count HEAD).$(git rev-parse --short HEAD)
}

prepare() {
  cd "$srcdir/LaZagne"

  grep -iRl python | xargs sed -i 's|python$|python2|g'
}

package() {
  cd "$srcdir/LaZagne"

  install -dm 755 "$pkgdir/usr/bin"
  install -dm 755 "$pkgdir/usr/share/lazagne"

  install -Dm644 -t "$pkgdir/usr/share/doc/lazagne" README.md CHANGELOG
  install -Dm644 LICENSE "$pkgdir/usr/share/licenses/lazagne/LICENSE"

  cp -a --no-preserve=ownership Linux/* "$pkgdir/usr/share/lazagne"

cat >> "$pkgdir/usr/bin/lazagne" << EOF
#!/bin/sh
cd /usr/share/lazagne
exec python2 laZagne.py "\$@"
EOF

  chmod a+x "$pkgdir/usr/bin/lazagne"
}
