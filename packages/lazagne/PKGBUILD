# This file is part of BlackArch Linux ( https://www.blackarch.org/ ).
# See COPYING for license details.

pkgname=lazagne
pkgver=466.4ecef7f
pkgrel=1
groups=('blackarch' 'blackarch-forensic' 'blackarch-social')
arch=('any')
pkgdesc='An open source application used to retrieve lots of passwords stored on a local computer.'
url="https://github.com/AlessandroZ/LaZagne"
license=('LGPL3')
depends=('python2' 'python2-argparse' 'python2-psutil' 'python2-pycryptodome'
         'python2-pyasn1' 'python2-pydes' 'python2-pbkdf2' 'python2-libkeepass')
makedepends=('git')
source=('git+https://github.com/AlessandroZ/LaZagne.git')
sha512sums=('SKIP')

pkgver() {
  cd LaZagne

  echo $(git rev-list --count HEAD).$(git rev-parse --short HEAD)
}

prepare() {
  cd LaZagne

  grep -iRl python | xargs sed -i 's|python$|python2|g'
}

package() {
  cd LaZagne

  install -dm 755 "$pkgdir/usr/bin"
  install -dm 755 "$pkgdir/usr/share/$pkgname"

  install -Dm 644 -t "$pkgdir/usr/share/doc/$pkgname/" README.md CHANGELOG
  install -Dm 644 LICENSE "$pkgdir/usr/share/licenses/$pkgname/LICENSE"

  cp -a --no-preserve=ownership Linux/* "$pkgdir/usr/share/$pkgname/"

cat >> "$pkgdir/usr/bin/$pkgname" << EOF
#!/bin/sh
cd /usr/share/$pkgname
exec python2 laZagne.py "\$@"
EOF

  chmod +x "$pkgdir/usr/bin/$pkgname"
}

