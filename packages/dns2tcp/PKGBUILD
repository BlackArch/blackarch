# This file is part of BlackArch Linux ( http://blackarch.org ).
# See COPYING for license details.

pkgname='dns2tcp'
pkgver='0.5.2'
pkgrel=6
groups=('blackarch' 'blackarch-tunnel')
pkgdesc='A tool for relaying TCP connections over DNS.'
arch=('x86_64' 'armv6h' 'armv7h' 'aarch64')
url='http://www.hsc.fr/ressources/outils/dns2tcp/index.html.en'
license=('GPL')
source=("http://www.hsc.fr/ressources/outils/dns2tcp/download/dns2tcp-${pkgver}.tar.gz")
sha1sums=('b1de53800bbfda6d8c6f92e2844906b1b8a54af2')

build() {
  cd "$srcdir/dns2tcp-$pkgver"

  if [[ "$CARCH" == "i686" || "$CARCH" == "x86_64" ]];
  then
    ./configure --prefix=/usr
  else
    ./configure --build=arm --prefix=/usr
  fi

  make
}

package() {
  cd "$srcdir/dns2tcp-$pkgver"

  make DESTDIR="$pkgdir" install
}
