# This file is part of BlackArch Linux ( http://blackarch.org ).
# See COPYING for license details.

pkgname='samplicator'
pkgver=170.37e0e90
pkgrel=1
pkgdesc='Send copies of (UDP) datagrams to multiple receivers, with optional sampling and spoofing.'
groups=('blackarch' 'blackarch-networking' 'blackarch-spoff')
arch=('x86_64' 'armv6h' 'armv7h' 'aarch64')
url='https://github.com/sleinen/samplicator'
license=('GPL2')
depends=()
makedepends=('git')
source=('git+https://github.com/sleinen/samplicator.git')
sha1sums=('SKIP')

pkgver() {
  cd "$srcdir/samplicator"

  echo $(git rev-list --count HEAD).$(git rev-parse --short HEAD)
}

build() {
  cd "$srcdir/samplicator"

  ./autogen.sh

  ./configure --prefix=/usr

  make
}

package() {
  cd "$srcdir/samplicator"

  make DESTDIR="$pkgdir" install

  install -Dm644 -t "$pkgdir/usr/share/doc/samplicator/" README.md NEWS AUTHORS\
    ChangeLog
  install -Dm644 COPYING "$pkgdir/usr/share/licenses/samplicator/COPYING"
}
