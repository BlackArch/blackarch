# This file is part of BlackArch Linux ( http://blackarch.org ).
# See COPYING for license details.

pkgname=darkjumper
pkgver=5.8
pkgrel=2
pkgdesc="This tool will try to find every website that host at the same server at your target"
depends=('perl' 'python2')
url="http://sourceforge.net/projects/darkjumper/"
groups=('blackarch' 'blackarch-webapp')
license=('GPL')
arch=('any')
source=(http://downloads.sourceforge.net/darkjumper/${pkgname}.v$pkgver.tar.gz)
md5sums=('eca340d0bd033dcb829b57017e5556a1')

prepare(){
  cd "$srcdir/darkjumper.v$pkgver"

  sed -i 's|/usr/bin/python|/usr/bin/env python2|' *.py
  sed -i '/file\.write/d' *.py
}

package() {
  cd $srcdir/darkjumper.v$pkgver

  install -dm755 "$pkgdir/usr/share/darkjumper"
  install -dm755 "$pkgdir/usr/bin"

  cp -a --no-preserve=ownership * "$pkgdir/usr/share/darkjumper"

  chmod -x "$pkgdir/usr/share/darkjumper"/*
  chmod +x "$pkgdir/usr/share/darkjumper"/*.{pl,py}
  rm "$pkgdir/usr/share/darkjumper"/*.pyc

  for i in *.pl *.py ; do
    ln -s "/usr/share/darkjumper/$i" "$pkgdir/usr/bin/$i"
  done
}
