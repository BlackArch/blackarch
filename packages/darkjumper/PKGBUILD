# This file is part of BlackArch Linux ( http://blackarch.org ).
# See COPYING for license details.

pkgname='darkjumper'
pkgver='5.8'
pkgrel=3
pkgdesc='This tool will try to find every website that host at the same server at your target.'
depends=('perl' 'python2')
url='http://sourceforge.net/projects/darkjumper/'
groups=('blackarch' 'blackarch-webapp')
license=('GPL')
arch=('any')
source=("http://downloads.sourceforge.net/darkjumper/${pkgname}.v${pkgver}.tar.gz")
sha1sums=('723bc41c01c8e31d0b7fe0e3c6a567170f253194')

prepare(){
  cd "$srcdir/darkjumper.v$pkgver"

  sed -i 's|/usr/bin/python|/usr/bin/env python2|' *.py
  sed -i '/file\.write/d' *.py
}

package() {
  cd "$srcdir/darkjumper.v$pkgver"

  mkdir -p "$pkgdir/usr/share/darkjumper"
  mkdir -p "$pkgdir/usr/bin"

  cp -a --no-preserve=ownership * "$pkgdir/usr/share/darkjumper"

  chmod -x "$pkgdir/usr/share/darkjumper"/*
  chmod +x "$pkgdir/usr/share/darkjumper"/*.{pl,py}
  rm "$pkgdir/usr/share/darkjumper"/*.pyc

  for i in *.pl *.py ; do
    ln -s "/usr/share/darkjumper/$i" "$pkgdir/usr/bin/$i"
  done
}
