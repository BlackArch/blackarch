# This file is part of BlackArch Linux ( http://blackarch.org ).
#
# This PKGBUILD was extracted from AUR.
# Creator: Dave Reisner <d@falconindy.com>
# Changes by: psf

pkgname='cower'
pkgver='17'
pkgrel=2
pkgdesc='A simple AUR agent with a pretentious name.'
arch=('x86_64' 'armv6h' 'armv7h' 'aarch64')
url='http://github.com/falconindy/cower'
license=('MIT')
depends=('curl' 'pacman' 'yajl')
makedepends=('perl')
source=("https://pkgbuild.com/~dreisner/sources/cower/cower-$pkgver.tar.gz"{,.sig})
validpgpkeys=('487EACC08557AD082088DABA1EB2638FF56C0C53')  # Dave Reisner
sha1sums=('ef09a1f35ca9fa55bf4f5d3edc4c8859f61c19da'
          'SKIP')

build() {
  cd "$srcdir/cower-${pkgver}"

  make
  sed '/^$/q' src/cower.c >LICENSE
}

package() {
  cd "$srcdir/cower-${pkgver}"

  make PREFIX=/usr DESTDIR="${pkgdir}" install

  install -Dm644 LICENSE "${pkgdir}/usr/share/licenses/cower/LICENSE"
}
