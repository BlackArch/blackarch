# This file is part of BlackArch Linux ( http://blackarch.org ).
# See COPYING for license details.

pkgname=wpseku
pkgver=30.f8c0b01
pkgrel=1
epoch=2
pkgdesc='Simple Wordpress Security Scanner.'
groups=('blackarch' 'blackarch-webapp' 'blackarch-scanner')
arch=('any')
url='https://github.com/m4ll0k/WPSeku'
license=('custom:unknown')
depends=('python2' 'python2-requests')
makedepends=('git')
source=('git+https://github.com/m4ll0k/WPSeku.git')
sha512sums=('SKIP')

pkgver() {
  cd "$srcdir/WPSeku"

  echo $(git rev-list --count HEAD).$(git rev-parse --short HEAD)
}

package() {
  cd "$srcdir/WPSeku"

  install -dm 755 "$pkgdir/usr/bin"
  install -dm 755 "$pkgdir/usr/share/wpseku"

  install -Dm644 -t "$pkgdir/usr/share/doc/wpseku/" README.md

  rm README.md

  cp -a * "$pkgdir/usr/share/wpseku/"

  cat > "$pkgdir/usr/bin/wpseku" << EOF
#!/bin/sh
cd /usr/share/wpseku
exec python2 wpseku.py "\${@}"
EOF

  chmod a+x "$pkgdir/usr/bin/wpseku"
}
