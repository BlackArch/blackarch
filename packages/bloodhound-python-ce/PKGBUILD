# This file is part of BlackArch Linux ( https://www.blackarch.org/ ).
# See COPYING for license details.

pkgname=bloodhound-ce-python
_pkgname=BloodHound.py
pkgver=v1.0.1.r182.g3def775
pkgrel=1
pkgdesc='Python data collector for Bloodhound community edition (v5)'
arch=('any')
groups=('blackarch' 'blackarch-recon' 'blackarch-windows')
url='https://github.com/dirkjanm/BloodHound.py'
license=('MIT')
depends=('python' 'python-dnspython' 'impacket' 'python-ldap3' 'python-pyasn1'
         'python-pycryptodome')
makedepends=('python-setuptools' 'git')
source=("git+https://github.com/dirkjanm/$_pkgname.git#branch=bloodhound-ce")
sha512sums=('SKIP')

pkgver() {
  cd $_pkgname

  git describe --long | sed 's/\([^-]*-g\)/r\1/;s/-/./g'
}

build() {
  cd "$_pkgname"

  python setup.py build
}

package() {
  cd "$_pkgname"

  python setup.py install --root="$pkgdir" --prefix=/usr -O1 --skip-build

  install -Dm 644 -t "$pkgdir/usr/share/doc/$pkgname/" README.md
  install -Dm 644 LICENSE "$pkgdir/usr/share/licenses/$pkgname/LICENSE"
}

