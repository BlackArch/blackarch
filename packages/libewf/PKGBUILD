# This file is part of BlackArch Linux ( http://blackarch.org ).
# See COPYING for license details.

pkgname=libewf
pkgver=20170131.faabd9e5
pkgrel=2
pkgdesc='A library to access the Expert Witness Compression Format (EWF)'
arch=('any')
url='https://github.com/libyal/libewf'
license=('LGPL3')
provides=('libewf')
groups=('blackarch' 'blackarch-forensic')
makedepends=('git' 'autoconf' 'automake' 'libtool' 'gettext' 'pkg-config'
             'bison' 'flex')
source=('git+https://github.com/libyal/libewf.git')
sha1sums=('SKIP')

_gitname="libewf"

pkgver() {
  cd "$srcdir/$_gitname"

  git log -1 --format='%cd.%h' --date=short | tr -d -
}

build() {
  cd "$_gitname"

  ./synclibs.sh
  sed -i '/ewftools/d' Makefile.am
  sed -i '/ewftools/d' configure.ac
  ./autogen.sh
  ./configure \
    --prefix=/usr \
    --sysconfdir=/etc

  make
}

package() {
  cd "$_gitname"

  make DESTDIR="$pkgdir/" install
}
