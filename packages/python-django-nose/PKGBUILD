# This file is part of BlackArch Linux ( https://www.blackarch.org/ ).
# See COPYING for license details.

pkgbase=python-django-nose
pkgname=('python2-django-nose' 'python-django-nose')
_pkgname=django-nose
pkgver=1.4.6
pkgrel=3
pkgdesc='Makes your Django tests simple and snappy.'
arch=('any')
url='https://pypi.org/project/django-nose/#files'
license=('BSD')
makedepends=('python2-setuptools' 'python-setuptools')
source=("https://files.pythonhosted.org/packages/91/b4/0f84946a3f18c1b1c75c9eac8272f684dc1f3815b24c4941d59d433d8886/$_pkgname-$pkgver.tar.gz")
sha512sums=('1e3e464d0cc57a03b457e8800eb6b0b8c97e48bfbe35d17506284d6101594a3d671a6cad72874eabd43233d97ba017f7e0a5f15c2cfea041420507c867e116eb')

prepare() {
  cp -a "$_pkgname-$pkgver"{,-2}
}

build() {
  cd "$_pkgname-$pkgver-2"

  python2 setup.py build

  cd "$srcdir/$_pkgname-$pkgver"

  python setup.py build
}

package_python2-django-nose() {
  depends=('python2' 'python2-nose')

  cd "$_pkgname-$pkgver-2"

  python2 setup.py install --prefix=/usr --root="$pkgdir" --optimize=1 \
    --skip-build
}

package_python-django-nose() {
  depends=('python' 'python-nose')

  cd "$_pkgname-$pkgver"

  python setup.py install --prefix=/usr --root="$pkgdir" --optimize=1 \
    --skip-build
}

