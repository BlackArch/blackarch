# This file is part of BlackArch Linux ( http://blackarch.org ).
# See COPYING for license details.

pkgname='udsim'
pkgver=23.35c1710
pkgrel=1
pkgdesc='A graphical simulator that can emulate different modules in a vehicle and respond to UDS request.'
groups=('blackarch' 'blackarch-scanner' 'blackarch-fuzzer' 'blackarch-automobile')
arch=('x86_64' 'armv6h' 'armv7h' 'aarch64')
url='https://github.com/zombieCraig/UDSim/'
license=('GPL3')
depends=('sdl2' 'sdl2_image' 'sdl2_ttf')
makedepends=('git')
source=('git+https://github.com/zombieCraig/UDSim.git')
sha1sums=('SKIP')

pkgver() {
  cd "$srcdir/UDSim"

  echo $(git rev-list --count HEAD).$(git rev-parse --short HEAD)
}

build() {
  cd "$srcdir/UDSim/src"

  make
}

package() {
  cd "$srcdir/UDSim"

  mkdir -p "$pkgdir/usr/bin"
  mkdir -p "$pkgdir/usr/share/udsim"

  install -Dm755 src/udsim "$pkgdir/usr/bin/udsim"
  install -Dm644 -t "$pkgdir/usr/share/doc/udsim/" README.md
  install -Dm644 LICENSE "$pkgdir/usr/share/licenses/udsim/LICENSE"

  cp -a data "$pkgdir/usr/share/udsim/data"
}
