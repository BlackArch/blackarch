# This file is part of BlackArch Linux ( http://blackarch.org ).
# See COPYING for license details.

pkgname='ubertooth'
pkgver='2012.10.R1'
pkgrel=4
groups=('blackarch' 'blackarch-bluetooth')
pkgdesc="A 2.4 GHz wireless development board suitable for Bluetooth experimentation. Open source hardware and software. Tools only"
url="http://sourceforge.net/projects/ubertooth/"
arch=('i686' 'x86_64' 'armv6h' 'armv7h')
license=('GPL')
depends=('libusbx' 'libbtbb' 'bluez-libs')
source=("http://downloads.sourceforge.net/project/ubertooth/ubertooth-${pkgver//./-}.tar.xz")
sha1sums=('c865e639eaec12cdd225576f78a37d187c437de2')

build() {
  cd "$srcdir/ubertooth-${pkgver//./-}/host/bluetooth_rxtx"
  make clock_debug=true
}

package() {
  cd "$srcdir/ubertooth-${pkgver//./-}/host/bluetooth_rxtx"

  # Base directories.
  mkdir -p "$pkgdir/usr/include"
  mkdir -p "$pkgdir/usr/bin"
  mkdir -p "$pkgdir/usr/lib"

  make clock_debug=true LDCONFIG= INSTALL_DIR="$pkgdir/usr/bin/" \
       INCLUDE_DIR="$pkgdir/usr/include/" LIB_DIR="$pkgdir/usr/lib/" install
}
