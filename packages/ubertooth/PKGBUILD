# This file is part of BlackArch Linux ( https://www.blackarch.org/ ).
# See COPYING for license details.

pkgname=ubertooth
pkgver=2020.12.R1.r33.ge0fd34d
pkgrel=1
groups=('blackarch' 'blackarch-bluetooth')
pkgdesc='A 2.4 GHz wireless development board suitable for Bluetooth experimentation. Open source hardware and software. Tools only.'
arch=('x86_64' 'aarch64')
url='https://github.com/greatscottgadgets/ubertooth'
license=('GPL-2.0-or-later')
depends=('libbtbb' 'libusb' 'libpcap' 'python' 'python-libusb1' 'python-qtpy')
makedepends=('git' 'cmake' 'python-setuptools')
source=("git+https://github.com/greatscottgadgets/$pkgname.git"
        "specan-version.patch")
sha512sums=('SKIP'
            '22ba35af65fdca3872cfc7bb889f2c98a1333d1c834906236bda3e7b09d989cf99ba3336acbdb20084475287f942167dca92c0e4457fc7cfe8ec48a2d177d5a3')

pkgver() {
  cd $pkgname

  ( set -o pipefail
    git describe --long --tags --abbrev=7 2>/dev/null |
      sed 's/\([^-]*-g\)/r\1/;s/-/./g' ||
    printf "%s.%s" "$(git rev-list --count HEAD)" \
      "$(git rev-parse --short=7 HEAD)"
  )
}

prepare() {
  cd $pkgname

  patch -p1 < ../specan-version.patch
}

build() {
  cd "$pkgname/host/"

  mkdir -p build

  cd build

  cmake \
    -DCMAKE_INSTALL_PREFIX="$pkgdir/usr" \
    -DCMAKE_POLICY_VERSION_MINIMUM=3.5 \
    -DENABLE_PYTHON=FALSE \
    -DUDEV_RULES_PATH="$pkgdir/etc/udev/rules.d" \
    -DINSTALL_UDEV_RULES=TRUE \
    -DUDEV_RULES_GROUP=uucp \
    ..
  make

  cd ../python/ubtbr/

  python setup.py build

  cd ../../python/specan_ui

  python setup.py build
}

package() {
  # Runtime
  cd "$pkgname/host/build"

  make install

  # GUI
  cd ../python/specan_ui

  python setup.py install --root="$pkgdir/" --optimize=1 --skip-build

  cd ../../python/ubtbr/

  python setup.py install --root="$pkgdir"

  # Firmware
  install -dm755 "$pkgdir/usr/share/ubertooth"

  cd "$srcdir/ubertooth"

  cp -r "hardware" "$pkgdir/usr/share/ubertooth/hardware"
  cp -r "firmware" "$pkgdir/usr/share/ubertooth/firmware"
}

