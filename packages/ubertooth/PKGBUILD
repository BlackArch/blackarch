pkgname=ubertooth
pkgver=2012.10.R1
pkgrel=3
groups=('blackarch-wireless' 'blackarch')
pkgdesc="A 2.4 GHz wireless development board suitable for Bluetooth experimentation. Open source hardware and software. Tools only"
url="http://sourceforge.net/projects/ubertooth/"
arch=('x86_64' 'i686')
license=('GPL')
depends=('libusbx' 'libbtbb' 'bluez-libs')
source=("http://downloads.sourceforge.net/project/ubertooth/ubertooth-${pkgver//./-}.tar.xz")
md5sums=('8eabf8e37449296f799d6985c4c23ca2')

build() {
  cd "$srcdir/ubertooth-${pkgver//./-}/host/bluetooth_rxtx"
  make clock_debug=true
}

package() {
  cd "$srcdir/ubertooth-${pkgver//./-}/host/bluetooth_rxtx"

  # Base directories.
  install -dm755 "$pkgdir/usr/include"
  install -dm755 "$pkgdir/usr/bin"
  install -dm755 "$pkgdir/usr/lib"

  make clock_debug=true LDCONFIG= INSTALL_DIR="$pkgdir/usr/bin/" \
       INCLUDE_DIR="$pkgdir/usr/include/" LIB_DIR="$pkgdir/usr/lib/" install
}
