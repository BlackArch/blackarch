# This file is part of BlackArch Linux ( https://www.blackarch.org/ ).
# See COPYING for license details.

pkgname=ubertooth
pkgver=2018.12.R1
pkgrel=2
groups=('blackarch' 'blackarch-bluetooth')
pkgdesc='A 2.4 GHz wireless development board suitable for Bluetooth experimentation. Open source hardware and software. Tools only.'
url='https://github.com/greatscottgadgets/ubertooth/releases'
arch=('x86_64' 'aarch64')
license=('GPL')
depends=('libusb' 'libbtbb' 'bluez-libs' 'libpcap' 'python2' 'python2-numpy'
         'python2-pyside' 'python2-pyqt4' 'python2-pyusb')
makedepends=('cmake')
source=("https://github.com/greatscottgadgets/ubertooth/archive/${pkgver//./-}.tar.gz")
sha512sums=('b7d1ac06c826261c8089a8e3714354b067ed83447ddca1ca7047078c2dbc9a077fd158cd2075611a23a313ab51034f42352dba16002802ffb30690d9bd3df99f')

prepare() {
  cd "$pkgname-${pkgver//./-}/host"

  find "$srcdir/" -name "*.py" -exec sed -i 's|env python|env python2|' {} \;
  find "$srcdir/" -name "*.py" -exec \
    sed -i 's|/usr/bin/python|/usr/bin/python2|' {} \;
}

build() {
  cd "$pkgname-${pkgver//./-}/host"

  mkdir -p build

  cd build

  cmake -DPYTHON_EXECUTABLE:FILEPATH=/usr/bin/python2.7 \
    -DCMAKE_INSTALL_PREFIX:PATH=/usr ..

  make
}

package() {
  cd "$pkgname-${pkgver//./-}/host"

  cd build

  make DESTDIR="$pkgdir" install
}

