# $Id: PKGBUILD 86192 2013-03-12 21:51:33Z jlichtblau $
# Maintainer: Jaroslav Lichtblau <dragonlord@aur.archlinux.org>
# Contributor: Geoffroy Carrier <geoffroy.carrier@koon.fr>

pkgname=sleuthkit
pkgver=4.0.2
pkgrel=1
pkgdesc='File system and media management forensic analysis tools'
arch=('i686' 'x86_64')
url='http://www.sleuthkit.org/sleuthkit'
license=('GPL2' 'CPL' 'custom:"IBM Public Licence"')
depends=('perl')
options=('!libtool')
changelog=$pkgname.changelog
source=(http://downloads.sourceforge.net/$pkgname/$pkgname-$pkgver.tar.gz)
sha256sums=('112f80fbc6a868c18c7a924f756b35c79eed9d0cc8923a4f973646f00e546e62')

build() {
  cd ${pkgname}-${pkgver}

  ./configure --prefix=/usr
  make
}

package() {
  cd ${pkgname}-${pkgver}

  make DESTDIR=${pkgdir} install

  # licenses
  install -d ${pkgdir}/usr/share/licenses/${pkgname}
  install -Dm0644 licenses/* ${pkgdir}/usr/share/licenses/${pkgname}
}
