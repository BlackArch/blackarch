# This file is part of BlackArch Linux ( https://www.blackarch.org/ ).
# See COPYING for license details.

pkgname=perl-rpc-xml
_pkgname=RPC-XML
pkgver=0.80
pkgrel=3
pkgdesc='A set of classes for core data, message and XML handling.'
arch=('any')
license=('PerlArtistic' 'GPL')
depends=('perl' 'perl-xml-libxml' 'perl-libwww')
optdepends=('perl-xml-libxml: XML::LibXML support'
            'perl-datetime-format-iso8601: DateTime::Format::ISO8601 support'
            'perl-net-server: Net::Server support')
url='https://metacpan.org/pod/RPC::XML'
source=("https://cpan.metacpan.org/authors/id/R/RJ/RJRAY/$_pkgname-$pkgver.tar.gz")
sha512sums=('a91586ea903b3e633f85ca8d9048181a7c3aedcff6788c7ae5fb4971e8552c421c86f30254517c25caa35f1991146dcceb34301bb49002e4ec75a2ee9c195992')

prepare() {
  cd "$_pkgname-$pkgver"

  export PERL_MM_USE_DEFAULT=1 PERL5LIB="" PERL_AUTOINSTALL=--skipdeps \
    PERL_MM_OPT="INSTALLDIRS=vendor DESTDIR='$pkgdir'" \
    PERL_MB_OPT="--installdirs vendor --destdir '$pkgdir'" \
    MODULEBUILDRC=/dev/null

  /usr/bin/perl Makefile.PL
}

build() {
  cd "$_pkgname-$pkgver"

  make
}

package() {
  cd "$_pkgname-$pkgver"

  make install

  find "$pkgdir" -name .packlist -o -name perllocal.pod -delete
}

