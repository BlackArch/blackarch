# This file is part of BlackArch Linux ( https://www.blackarch.org/ ).
# See COPYING for license details.

pkgname=python2-flake8
_pkgname=flake8
pkgver=3.8.4
pkgrel=3
pkgdesc='The modular source code checker: pep8, pyflakes and co.'
url='https://pypi.org/project/flake8/#files'
arch=('any')
license=('MIT')
depends=('python2' 'python2-mccabe' 'python2-enum34' 'python2-pyflakes'
         'python2-entrypoints' 'python2-pycodestyle' 'python2-configparser'
         'python2-functools32' 'python2-typing')
makedepends=('python2-setuptools')
provides=('flake8-python2')
replaces=('flake8-python2')
conflicts=('flake8-python2')
source=("https://files.pythonhosted.org/packages/71/6a/b3341ef7e7f3585add027d876a7d9837cdfe3320b6c6b5fd0cddfa9ceeac/$_pkgname-$pkgver.tar.gz"
        'setup.py.patch')
sha512sums=('51ed09beed50175573e6184b404431b3f89aa6594c40c70ed8288982fe12c2a68992e36d69c99013188f03555bf9fef1ba4f111f8e389cef1e1daa69b41ff5b0'
            'b8948c6bf18aeb4da27287f0666eca4939c17a213e0647acdba971c10f73d12ee9e1eec3203291fc6bd503c1cc5ce4c821e736fec3e5e348ba1df3f8903cc98b')

prepare() {
  patch -p1 < setup.py.patch
}

build() {
  cd "$_pkgname-$pkgver"

  python2 setup.py build
}

package() {
  cd "$_pkgname-$pkgver"

  python2 setup.py install --root="$pkgdir" --prefix=/usr -O1 --skip-build

  mv "$pkgdir/usr/bin/$_pkgname" "$pkgdir/usr/bin/${_pkgname}-2"
}

