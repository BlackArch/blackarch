# This file is part of BlackArch Linux ( https://www.blackarch.org/ ).
# See COPYING for license details.

pkgname=python2-flake8
_pkgname=flake8
pkgver=3.7.9
pkgrel=1
pkgdesc='The modular source code checker: pep8, pyflakes and co.'
url='https://pypi.org/project/flake8/#files'
arch=('any')
license=('MIT')
depends=('python2' 'python2-mccabe' 'python2-enum34' 'python2-pyflakes'
         'python2-entrypoints' 'python2-pycodestyle' 'python2-configparser'
         'python2-functools32' 'python2-typing')
makedepends=('python2-setuptools')
provides=('flake8-python2')
replaces=('flake8-python2')
conflicts=('flake8-python2')
source=("https://files.pythonhosted.org/packages/a5/bb/7e707d8001aca96f15f684b02176ecb0575786f041293f090b44ea04f2d0/$_pkgname-$pkgver.tar.gz"
        'setup.py.patch')
sha512sums=('c3c753ad050d8a8f87f7c28c8a1c953ed9bd04f08b09512d9323152b4eebb57d67fbfb0f5317d629174fa12fa234e1a86d6a633dd951fff6a45330b1c4f62850'
            'b8948c6bf18aeb4da27287f0666eca4939c17a213e0647acdba971c10f73d12ee9e1eec3203291fc6bd503c1cc5ce4c821e736fec3e5e348ba1df3f8903cc98b')

prepare() {
  patch -p1 < setup.py.patch
}

build() {
  cd "$_pkgname-$pkgver"

  python2 setup.py build
}

package() {
  cd "$_pkgname-$pkgver"

  python2 setup.py install --root="$pkgdir" --prefix=/usr -O1 --skip-build

  mv "$pkgdir/usr/bin/$_pkgname" "$pkgdir/usr/bin/${_pkgname}-2"
}

