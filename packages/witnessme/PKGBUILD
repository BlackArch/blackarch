# This file is part of BlackArch Linux ( https://www.blackarch.org/ ).
# See COPYING for license details.

pkgname=witnessme
pkgver=32.4a87653
pkgrel=1
pkgdesc='Web Inventory tool, takes screenshots of webpages using Pyppeteer.'
arch=('any')
groups=('blackarch' 'blackarch-webapp' 'blackarch-recon')
url='https://github.com/byt3bl33d3r/WitnessMe'
license=('GPL3')
depends=('python' 'python-aiosqlite' 'python-appdirs' 'python-imgcat'
         'python-prompt_toolkit' 'python-pyee' 'python-pyppeteer'
         'python-yaml' 'python-six' 'python-terminaltables' 'python-tqdm'
         'python-urllib3' 'python-wcwidth' 'python-websockets'
         'python-xmltodict')
makedepends=('git')
source=("$pkgname::git+https://github.com/byt3bl33d3r/WitnessMe.git")
sha512sums=('SKIP')

pkgver() {
  cd $pkgname

  echo $(git rev-list --count HEAD).$(git rev-parse --short HEAD)
}

package() {
  cd $pkgname

  install -dm 755 "$pkgdir/usr/bin"
  install -dm 755 "$pkgdir/usr/share/$pkgname"

  install -Dm 644 -t "$pkgdir/usr/share/doc/$pkgname/" README.md
  install -Dm 644 LICENSE "$pkgdir/usr/share/licenses/$pkgname/LICENSE"

  cp -a --no-preserve=ownership "$pkgname" *.py "$pkgdir/usr/share/$pkgname/"

  cat > "$pkgdir/usr/bin/$pkgname" << EOF
#!/bin/sh
cd /usr/share/$pkgname
exec python $pkgname.py "\$@"
EOF

  chmod +x "$pkgdir/usr/bin/$pkgname"
}

