# This file is part of BlackArch Linux ( https://www.blackarch.org/ ).
# See COPYING for license details.

pkgbase=python-speechrecognition
pkgname=('python2-speechrecognition' 'python-speechrecognition')
_pkgname=speech_recognition
pkgver=3.9.0
pkgrel=2
pkgdesc='Library for performing speech recognition, with support for several engines and APIs, online and offline.'
arch=('any')
url='https://pypi.org/project/speechrecognition/#files'
license=('BSD')
makedepends=('python2-setuptools' 'python-setuptools')
source=("https://github.com/Uberi/speech_recognition/archive/$pkgver.tar.gz")
sha512sums=('de4f6b9d341f013e34ff67a65b459ac4895b8e8ac3682affdcb7f9c8dfd035ae26c821bb39966a0921c4b65e73ffcbbf2f8787f99568405f59ff61540e890729')

prepare() {
  cp -a "$_pkgname-$pkgver"{,-2}
}

build() {
  cd "$_pkgname-$pkgver-2"

  python2 setup.py build

  cd "$srcdir/$_pkgname-$pkgver"

  python setup.py build
}

package_python2-speechrecognition() {
  depends=('python2')

  cd "$_pkgname-$pkgver-2"

  python2 setup.py install --root="$pkgdir" --prefix=/usr -O1 --skip-build
}

package_python-speechrecognition() {
  depends=('python')

  cd "$_pkgname-$pkgver"

  python setup.py install --root="$pkgdir" --prefix=/usr -O1 --skip-build
}

