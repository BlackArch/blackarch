# This file is part of BlackArch Linux ( http://blackarch.org ).
# See COPYING for license details.

pkgname='hackrf'
pkgver='2014.08.1'
pkgrel=1
pkgdesc='Driver for HackRF, allowing general purpose software defined radio (SDR).'
arch=('i686' 'x86_64' 'armv6h' 'armv7h')
url='https://github.com/mossmann/hackrf'
license=('GPL2')
depends=('libusbx')
makedepends=('cmake')
source=("http://downloads.sourceforge.net/hackrf/hackrf-${pkgver}.tar.xz"
        '52-hackrf.rules')
sha1sums=('f6e1372d128f86d374f58f9ca7b8a27aa54d1466'
          'f25ab50038edb1d995b255555f1ce6656160a6d8')

build() {
  cd "$srcdir/hackrf-$pkgver/host"

  mkdir -p build

  cd build

  cmake -DCMAKE_INSTALL_PREFIX=/usr ..

  make
}

package() {
  cd "$srcdir/hackrf-$pkgver/host/build"

  make DESTDIR="$pkgdir" install

  install -Dm644 "$srcdir/52-hackrf.rules" \
    "$pkgdir/usr/lib/udev/rules.d/52-hackrf.rules"

  cd "$srcdir/hackrf-$pkgver/firmware-bin"
  install -Dm644 -t "$pkgdir/usr/share/hackrf/" *.bin
}
