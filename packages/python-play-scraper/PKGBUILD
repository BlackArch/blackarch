# This file is part of BlackArch Linux ( https://www.blackarch.org/ ).
# See COPYING for license details.

pkgbase=python-play-scraper
pkgname=('python2-play-scraper' 'python-play-scraper')
_pkgname=play_scraper
pkgver=0.5.5
pkgrel=2
pkgdesc='Google Play Store application scraper.'
arch=('any')
url='https://pypi.org/project/play-scraper/#files'
license=('MIT')
makedepends=('python2-setuptools' 'python-setuptools')
source=("https://files.pythonhosted.org/packages/50/aa/1f158ed12eb49f00fc7667e703b8f4dafacc685a9895a42a83492ccdf261/$_pkgname-$pkgver.tar.gz")
sha512sums=('563189b6f8192a1daefc206b243ac393ad0c072752c163d8d560af8cacbddf71f9ec7e24ebe7815d9c36f3de0e8435ad58c1c3d17f4272a89b729b1f09727811')

prepare() {
  cp -a "$_pkgname-$pkgver"{,-2}
}

build() {
  cd "$_pkgname-$pkgver-2"

  python2 setup.py build

  cd "$srcdir/$_pkgname-$pkgver"

  python setup.py build
}

package_python2-play-scraper() {
  depends=('python2' 'python2-beautifulsoup4' 'python2-lxml'
           'python2-requests-futures')

  cd "$_pkgname-$pkgver-2"

  python2 setup.py install --prefix=/usr --root="$pkgdir" --optimize=1 \
    --skip-build
}

package_python-play-scraper() {
  depends=('python' 'python-beautifulsoup4' 'python-lxml'
           'python-requests-futures')

  cd "$_pkgname-$pkgver"

  python setup.py install --prefix=/usr --root="$pkgdir" --optimize=1 \
    --skip-build
}

