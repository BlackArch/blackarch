# This file is part of BlackArch Linux ( https://www.blackarch.org/ ).
# See COPYING for license details.

pkgbase=python-xdis
pkgname=('python2-xdis' 'python-xdis')
_pkgname=xdis
pkgver=5.0.5
pkgrel=2
pkgdesc='Python cross-version byte-code disassembler and marshal routines.'
url='https://pypi.org/project/xdis/#files'
arch=('any')
license=('GPL2')
makedepends=('python2-setuptools' 'python-setuptools')
source=("https://files.pythonhosted.org/packages/18/cf/2de3e551d3411eb7ae8a455595d7dcf052428b7fa7be51f87013eb21d153/$_pkgname-$pkgver.tar.gz")
sha512sums=('6fedf77f93138177082b3c5758ebc4b157d862f37fc68605f64afffd40d31bdfe15b3b1fde2d54d7f20e69317000cf66a97207b47791db4d44a73b0dc0b99b6c')

prepare() {
  cp -a "$_pkgname-$pkgver"{,-2}
}

build() {
  cd "$_pkgname-$pkgver-2"

  python2 setup.py build

  cd "$srcdir/$_pkgname-$pkgver"

  python setup.py build
}

package_python2-xdis() {
  depends=('python2')

  cd "$_pkgname-$pkgver-2"

  python2 setup.py install --prefix=/usr --root="$pkgdir" --optimize=1 \
    --skip-build

  mv "$pkgdir/usr/bin/pydisasm" "$pkgdir/usr/bin/pydisasm2"
}

package_python-xdis() {
  depends=('python')

  cd "$_pkgname-$pkgver"

  python setup.py install --prefix=/usr --root="$pkgdir" --optimize=1 \
    --skip-build
}

