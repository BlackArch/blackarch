# This file is part of BlackArch Linux ( https://www.blackarch.org/ ).
# See COPYING for license details.

pkgbase=python-xdis
pkgname=('python2-xdis' 'python-xdis')
_pkgname=xdis
pkgver=4.2.3
pkgrel=1
pkgdesc='Python cross-version byte-code disassembler and marshal routines.'
url='https://pypi.org/project/xdis/#files'
arch=('any')
license=('GPL2')
makedepends=('python2-setuptools' 'python-setuptools')
source=("https://files.pythonhosted.org/packages/5b/fb/74281d10bb2918fc1296eadb63cc5ffa0870d3dd0007fb61d43c3bc96d86/$_pkgname-$pkgver.tar.gz")
sha512sums=('5e9c5e227cfaa39dbbda2e9422e49af577326e9745cfc5a9ed2441a6d5c490754ad4e1c1378f0a008ce76edc1ff8f621697e37585e9e30509a77f82436d941b3')

prepare() {
  cp -a "$_pkgname-$pkgver"{,-2}
}

build() {
  cd "$_pkgname-$pkgver-2"

  python2 setup.py build

  cd "$srcdir/$_pkgname-$pkgver"

  python setup.py build
}

package_python2-xdis() {
  depends=('python2')

  cd "$_pkgname-$pkgver-2"

  python2 setup.py install --prefix=/usr --root="$pkgdir" --optimize=1 \
    --skip-build

  mv "$pkgdir/usr/bin/pydisasm" "$pkgdir/usr/bin/pydisasm2"
}

package_python-xdis() {
  depends=('python')

  cd "$_pkgname-$pkgver"

  python setup.py install --prefix=/usr --root="$pkgdir" --optimize=1 \
    --skip-build
}

