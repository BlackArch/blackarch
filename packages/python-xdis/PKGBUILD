# This file is part of BlackArch Linux ( https://www.blackarch.org/ ).
# See COPYING for license details.

pkgbase=python-xdis
pkgname=('python2-xdis' 'python-xdis')
_pkgname=xdis
pkgver=4.0.2
pkgrel=2
pkgdesc='Python cross-version byte-code disassembler and marshal routines.'
url='https://pypi.org/project/xdis/#files'
arch=('any')
license=('GPL2')
makedepends=('python2-setuptools' 'python-setuptools')
source=("https://files.pythonhosted.org/packages/2e/c3/3b5964ab108d525748862916aa5f7efd7decdc0fd959d1847db94958ccc7/$_pkgname-$pkgver.tar.gz")
sha512sums=('a295d9a7ae75b5d01e493a37f26f700bd4d3d7c3ee5d3e970845b81ee213f6b48b79e68f70bccbe6dd246d82b77f4eef0457602cab2029757123cdf6c76e8ac5')

prepare() {
  cp -a "$_pkgname-$pkgver" "$_pkgname-$pkgver-2"
}

build() {
  cd "$_pkgname-$pkgver-2"

  python2 setup.py build

  cd "$srcdir/$_pkgname-$pkgver"

  python setup.py build
}

package_python2-xdis() {
  depends=('python2')

  cd "$_pkgname-$pkgver-2"

  python2 setup.py install --prefix=/usr --root="$pkgdir" --optimize=1 \
    --skip-build

  mv "$pkgdir/usr/bin/pydisasm.py" "$pkgdir/usr/bin/pydisasm2.py"
}

package_python-xdis() {
  depends=('python')

  cd "$_pkgname-$pkgver"

  python setup.py install --prefix=/usr --root="$pkgdir" --optimize=1 \
    --skip-build
}

