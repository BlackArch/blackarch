# This file is part of BlackArch Linux ( https://www.blackarch.org/ ).
# See COPYING for license details.

pkgbase=python-xdis
pkgname=('python2-xdis' 'python-xdis')
_pkgname=xdis
pkgver=4.6.1
pkgrel=1
pkgdesc='Python cross-version byte-code disassembler and marshal routines.'
url='https://pypi.org/project/xdis/#files'
arch=('any')
license=('GPL2')
makedepends=('python2-setuptools' 'python-setuptools')
source=("https://files.pythonhosted.org/packages/9c/9f/b576bc6c3469e679bf51d52c688b0b0780d9e51ee737a4aee01dad4feea8/$_pkgname-$pkgver.tar.gz")
sha512sums=('fd7695b431ada695f7fd0be0ccec62736b2319e0abedb1483c0e2348399886358e0ed24b75702a7ef23423c4ee5527f4d676d193a6f8b6615ff2a99752791118')

prepare() {
  cp -a "$_pkgname-$pkgver"{,-2}
}

build() {
  cd "$_pkgname-$pkgver-2"

  python2 setup.py build

  cd "$srcdir/$_pkgname-$pkgver"

  python setup.py build
}

package_python2-xdis() {
  depends=('python2')

  cd "$_pkgname-$pkgver-2"

  python2 setup.py install --prefix=/usr --root="$pkgdir" --optimize=1 \
    --skip-build

  mv "$pkgdir/usr/bin/pydisasm" "$pkgdir/usr/bin/pydisasm2"
}

package_python-xdis() {
  depends=('python')

  cd "$_pkgname-$pkgver"

  python setup.py install --prefix=/usr --root="$pkgdir" --optimize=1 \
    --skip-build
}

