# This file is part of BlackArch Linux ( https://www.blackarch.org/ ).
# See COPYING for license details.

pkgbase=python-xdis
pkgname=('python2-xdis' 'python-xdis')
_pkgname=xdis
pkgver=5.0.4
pkgrel=1
pkgdesc='Python cross-version byte-code disassembler and marshal routines.'
url='https://pypi.org/project/xdis/#files'
arch=('any')
license=('GPL2')
makedepends=('python2-setuptools' 'python-setuptools')
source=("https://files.pythonhosted.org/packages/fd/8d/e6b7b312ef83d65b226a17c5c203fcd8298128fd76622412927eadfbf05c/$_pkgname-$pkgver.tar.gz")
sha512sums=('50cad73185ade5c37c4ae71e5eaeb3e7b295e8dd380533abe1f67c589e97691d605d52b338716b95bf22ddd02b4d994265c88fb4e3fa62bf9d421e1b768779a6')

prepare() {
  cp -a "$_pkgname-$pkgver"{,-2}
}

build() {
  cd "$_pkgname-$pkgver-2"

  python2 setup.py build

  cd "$srcdir/$_pkgname-$pkgver"

  python setup.py build
}

package_python2-xdis() {
  depends=('python2')

  cd "$_pkgname-$pkgver-2"

  python2 setup.py install --prefix=/usr --root="$pkgdir" --optimize=1 \
    --skip-build

  mv "$pkgdir/usr/bin/pydisasm" "$pkgdir/usr/bin/pydisasm2"
}

package_python-xdis() {
  depends=('python')

  cd "$_pkgname-$pkgver"

  python setup.py install --prefix=/usr --root="$pkgdir" --optimize=1 \
    --skip-build
}

