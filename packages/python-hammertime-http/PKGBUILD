# This file is part of BlackArch Linux ( https://www.blackarch.org/ ).
# See COPYING for license details.

pkgname=python-hammertime-http
_pkgname=HammerTime-http
pkgver=0.8
pkgrel=6
pkgdesc='An HTTP client library aiming to perform a large number of requests on a server as fast as it can take them, but without distrupting operations significantly.'
url='https://pypi.org/project/HammerTime-http/#files'
arch=('any')
license=('GPL2')
depends=('python' 'python-aiohttp' 'python-easyinject' 'python-aiodns'
         'python-simhash' 'python-forkedsimhash-py')
makedepends=('python-setuptools')
source=("https://files.pythonhosted.org/packages/2e/37/84263fafd325f3c11f3c254b58a8874502cd21172bec0c37ba13feb71f33/HammerTime-http-$pkgver.tar.gz"
        'setup.py.patch'
        'requires.txt.patch')
sha512sums=('91206ee7f6a5af48b1f814e436fb02fffe6c7a3aabc5fab15beb464e3a845b99fb95d5cd273ad6244b29116cd976842849e7245a2502d26a2f94955f1bb40ecd'
            '5d729f59895ec6b63a17a914f50487e99cf6cc3661d0377d595c81df7ca269b27f74f7c2b348816d87d0e0dd9b734dea91959e900c078bde9343560a58fb4bca'
            '5849a59d0cee9b98fa08588fb05eb983917b59eb82594375e78acd5d3f4163275af632df6fac0944b8d56e88c26a19ebf933acfa08f693c08de57cc67f3b75b2')

prepare() {
  cd "$_pkgname-$pkgver"

  patch -p0 < ../setup.py.patch
  patch -p2 < ../requires.txt.patch
}

build() {
  cd "$_pkgname-$pkgver"

  python setup.py build
}

package() {
  cd "$_pkgname-$pkgver"

  python setup.py install --root="$pkgdir" --prefix=/usr -O1 --skip-build
}

