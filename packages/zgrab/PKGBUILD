# This file is part of BlackArch Linux ( https://www.blackarch.org/ ).
# See COPYING for license details.

pkgname=zgrab
pkgver=800.4f43262
pkgrel=2
groups=('blackarch' 'blackarch-recon')
pkgdesc='Grab banners (optionally over TLS).'
url='https://github.com/zmap/zgrab'
license=('Apache2')
arch=('x86_64' 'armv6h' 'armv7h' 'aarch64')
depends=('glibc')
makedepends=('git' 'go-pie')
source=('git+https://github.com/zmap/zgrab.git')
sha512sums=('SKIP')
_url="github.com/zmap/zgrab"

pkgver() {
  cd $pkgname

  echo $(git rev-list --count HEAD).$(git rev-parse --short HEAD)
}

build() {
  cd "$srcdir"

  mkdir -p src/$_url && rm -rf src/$_url && mv zgrab src/$_url && cd src/$_url

  GOPATH="$srcdir" go get -d -t $_url
  GOPATH="$srcdir" go build -v $_url
}

package() {
  cd src/$_url

  install -Dm 755 zgrab "$pkgdir/usr/bin/$pkgname"
  install -Dm 644 -t "$pkgdir/usr/share/doc/$pkgname" README.md
  install -Dm 644 LICENSE "$pkgdir/usr/share/licenses/$pkgname/LICENSE"
}

