# This file is part of BlackArch Linux ( http://blackarch.org ).
# See COPYING for license details.

pkgname=zgrab
pkgver=799.96cfb9f
pkgrel=1
groups=('blackarch' 'blackarch-recon')
pkgdesc='Grab banners (optionally over TLS).'
url='https://github.com/zmap/zgrab'
license=('Apache2')
arch=('x86_64' 'armv6h' 'armv7h' 'aarch64')
depends=('go')
makedepends=('git')
source=('git+https://github.com/zmap/zgrab.git')
sha512sums=('SKIP')

pkgver() {
  cd "$srcdir/zgrab"

  echo $(git rev-list --count HEAD).$(git rev-parse --short HEAD)
}

build() {
  cd "$srcdir"

  local url="github.com/zmap/zgrab"

  install -dm 755 src/$url && rm -rf src/$url && mv zgrab src/$url && cd src/$url

  GOPATH="$srcdir" go get -d -t github.com/zmap/zgrab
  GOPATH="$srcdir" go build -v github.com/zmap/zgrab
}

package() {
  cd "$srcdir"

  cd src/github.com/zmap/zgrab/

  install -Dm755 zgrab "$pkgdir/usr/bin/zgrab"
  install -Dm644 -t "$pkgdir/usr/share/doc/zgrab" README.md
  install -Dm644 LICENSE "$pkgdir/usr/share/licenses/zgrab/LICENSE"
}
