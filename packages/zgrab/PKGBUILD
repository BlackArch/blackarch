# This file is part of BlackArch Linux ( https://www.blackarch.org/ ).
# See COPYING for license details.

pkgname=zgrab
pkgver=803.031475e
pkgrel=2
groups=('blackarch' 'blackarch-recon')
pkgdesc='Grab banners (optionally over TLS).'
url='https://github.com/zmap/zgrab'
license=('Apache')
arch=('x86_64' 'aarch64')
makedepends=('git' 'go')
source=("git+https://github.com/zmap/$pkgname.git")
sha512sums=('SKIP')

pkgver() {
  cd $pkgname

  echo $(git rev-list --count HEAD).$(git rev-parse --short HEAD)
}

build() {
  cd $pkgname

  GOPATH="$srcdir" go mod init go.mod
  GOPATH="$srcdir" go mod edit -replace github.com/zmap/zgrab@latest=github.com/zmap/zgrab@031475eccfdeae85d3639626cfe40de08880c3c4
  GOPATH="$srcdir" go mod tidy
  GOPATH="$srcdir" go mod download
  GOPATH="$srcdir" go build \
    -trimpath \
    -buildmode=pie \
    -mod=readonly \
    -modcacherw \
    -ldflags "-s -w" \
    -o $pkgname .
}

package() {
  cd $pkgname

  install -Dm 755 $pkgname "$pkgdir/usr/bin/$pkgname"
  install -Dm 644 README.md -t "$pkgdir/usr/share/doc/$pkgname"
  install -Dm 644 LICENSE -t "$pkgdir/usr/share/licenses/$pkgname"
}

