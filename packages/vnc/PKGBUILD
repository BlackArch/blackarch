pkgname=vnc
pkgver=4.1.3
pkgrel=3
pkgdesc="The original open-source cross-platform remote control solution (without xvnc)."
arch=(i686 x86_64)
url='http://www.realvnc.com'
license=('GPL2')
depends=('gcc-libs' 'libxtst' 'libsm')
conflicts=('tightvnc')
source=("${pkgname}-${pkgver}.tar.gz::http://www.realvnc.com/download/binary/785"
        'vnc-gcc43.diff')
md5sums=('a119f3c75ad2767c0588260e2abe39be'
         'bfec27c9ec356f8cde53b27cb7cf5378')

prepare() {
  cd $srcdir/$pkgname-${pkgver//./_}-unixsrc
  patch -p0 < $srcdir/vnc-gcc43.diff
}

build() {
  cd $srcdir/$pkgname-${pkgver//./_}-unixsrc/unix
  ./configure --prefix=/usr
  make
}

package() {
  cd $srcdir/$pkgname-${pkgver//./_}-unixsrc/unix
  install -dm755 "$pkgdir"/usr/{bin,share/man/man1}
  ./vncinstall "$pkgdir/usr/bin" "$pkgdir/usr/share/man"
}
