# This file is part of BlackArch Linux ( http://blackarch.org ).
# See COPYING for license details.

pkgname='leo'
pkgver='5.6'
pkgrel=1
pkgdesc="Literate programmer's editor, outliner, and project manager."
arch=('any')
url='http://webpages.charter.net/edreamleo/front.html'
license=('custom')
depends=('python2' 'tk' 'python2-pmw' 'python2-pyqt')
makedepends=('unzip' 'python2-setuptools')
groups=('blackarch' 'blackarch-misc')
source=("https://downloads.sourceforge.net/project/leo/Leo/${pkgver}/Leo-${pkgver}.zip")
sha1sums=('93a052eddf31dfe34aa342ec6a5f111319e895c9')

package() {
  cd "$srcdir/Leo-$pkgver"

  mkdir -p "$pkgdir/usr/bin"
  mkdir -p "$pkgdir/usr/share/leo"

  install -Dm644 -t "$pkgdir/usr/share/doc/leo/" INSTALL.TXT README.MD
  install -Dm644 LICENSE.TXT "$pkgdir/usr/share/licenses/leo/LICENSE.TXT"

  rm INSTALL.TXT README.MD LICENSE.TXT

  cp -a * "$pkgdir/usr/share/leo/"

  cat > "$pkgdir/usr/bin/leo" << EOF
#!/bin/sh
cd /usr/share/leo
exec python2 launchLeo.py "\${@}"
EOF

  chmod a+x "$pkgdir/usr/bin/leo"
}
