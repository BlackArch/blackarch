# This file is part of BlackArch Linux ( http://blackarch.org ).
# See COPYING for license details.

pkgname=leo
pkgver=5.8
pkgrel=2
pkgdesc="Literate programmer's editor, outliner, and project manager."
arch=('any')
url='https://github.com/leo-editor/leo-editor/'
license=('custom')
depends=('python2' 'tk' 'python2-pmw' 'python2-pyqt')
makedepends=('unzip' 'python2-setuptools')
groups=('blackarch' 'blackarch-misc')
source=("https://github.com/leo-editor/leo-editor/archive/$pkgver.tar.gz")
sha512sums=('88fb9480ba62a50bde587129fbc1af7ca1ad9740e4c581b76a7e17eb443b951c5935133bec38b412149ae07c22253da22a7d6169d29435aa23b384a61020ce98')

package() {
  cd "leo-editor-$pkgver"

  install -dm 755 "$pkgdir/usr/bin"
  install -dm 755 "$pkgdir/usr/share/$pkgname"

  install -Dm644 -t "$pkgdir/usr/share/doc/$pkgname/" INSTALL.TXT README.md
  install -Dm644 LICENSE "$pkgdir/usr/share/licenses/$pkgname/LICENSE"

  rm INSTALL.TXT README.md LICENSE

  cp -a * "$pkgdir/usr/share/$pkgname/"

  cat > "$pkgdir/usr/bin/$pkgname" << EOF
#!/bin/sh
cd /usr/share/leo
exec python2 launchLeo.py "\${@}"
EOF

  chmod a+x "$pkgdir/usr/bin/$pkgname"
}
