# This file is part of BlackArch Linux ( https://www.blackarch.org/ ).
# See COPYING for license details.

pkgname=leo
pkgver=19384.0129e6cdd
pkgrel=1
pkgdesc="Literate programmer's editor, outliner, and project manager."
arch=('any')
url='https://github.com/leo-editor/leo-editor/'
license=('custom:unknown')
depends=('python' 'tk' 'python-pmw' 'python-pyqt5' 'python-yaml' 'python-flask')
makedepends=('git' 'unzip' 'python-setuptools')
groups=('blackarch' 'blackarch-misc')
source=('git+https://github.com/leo-editor/leo-editor.git')
sha512sums=('SKIP')

pkgver() {
  cd leo-editor

  echo $(git rev-list --count HEAD).$(git rev-parse --short HEAD)
}

package() {
  cd leo-editor

  install -dm 755 "$pkgdir/usr/bin"
  install -dm 755 "$pkgdir/usr/share/$pkgname"

  install -Dm 644 -t "$pkgdir/usr/share/doc/$pkgname/" INSTALL.TXT README.md
  install -Dm 644 LICENSE "$pkgdir/usr/share/licenses/$pkgname/LICENSE"

  rm INSTALL.TXT README.md LICENSE

  cp -a * "$pkgdir/usr/share/$pkgname/"

  cat > "$pkgdir/usr/bin/$pkgname" << EOF
#!/bin/sh
cd /usr/share/$pkgname
exec python launchLeo.py "\$@"
EOF

  chmod +x "$pkgdir/usr/bin/$pkgname"
}

