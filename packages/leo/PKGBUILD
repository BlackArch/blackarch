# This file is part of BlackArch Linux ( https://www.blackarch.org/ ).
# See COPYING for license details.

pkgname=leo
pkgver=23486.e46e9315f
pkgrel=2
pkgdesc="Literate programmer's editor, outliner, and project manager."
groups=('blackarch' 'blackarch-misc')
arch=('any')
url='https://github.com/leo-editor/leo-editor/'
license=('custom:unknown')
depends=('python' 'tk' 'python-pmw' 'python-pyqt5' 'python-yaml' 'python-flask')
makedepends=('git' 'unzip' 'python-setuptools')
source=("$pkgname::git+https://github.com/leo-editor/leo-editor.git")
sha512sums=('SKIP')

pkgver() {
  cd $pkgname

  echo $(git rev-list --count HEAD).$(git rev-parse --short HEAD)
}

package() {
  cd $pkgname

  install -dm 755 "$pkgdir/usr/bin"
  install -dm 755 "$pkgdir/usr/share/$pkgname"

  install -Dm 644 -t "$pkgdir/usr/share/doc/$pkgname/" README.md
  install -Dm 644 LICENSE "$pkgdir/usr/share/licenses/$pkgname/LICENSE"

  rm README.md LICENSE

  cp -a * "$pkgdir/usr/share/$pkgname/"

  cat > "$pkgdir/usr/bin/$pkgname" << EOF
#!/bin/sh
cd /usr/share/$pkgname
exec python launchLeo.py "\$@"
EOF

  chmod +x "$pkgdir/usr/bin/$pkgname"
}

