# This file is part of BlackArch Linux ( https://www.blackarch.org/ ).
# See COPYING for license details

pkgname=python-django-tables2
_pkgname=django-tables2
pkgver=2.8.0
pkgrel=2
pkgdesc='A module for rendering Django data sets as HTML tables.'
arch=('any')
url='https://pypi.org/project/django-tables2/'
license=('MIT')
options=(!emptydirs)
depends=('python-django')
makedepends=('python-setuptools')
optdepends=('python-tablib: to export table data as CSV, XLS, etc.')
source=("https://files.pythonhosted.org/packages/source/${_pkgname::1}/$_pkgname/$_pkgname-$pkgver.tar.gz")
sha512sums=('7539196a1bf93bdc0cd6220030f6f26927590fe1d52071b47ccc3bbfe1bf13659c87c3592ead6b89e4040d7eaea6911fabded5af486487c76b87e53ea9275eff')

build() {
  cd "$_pkgname-$pkgver"

  python setup.py build
}

package() {
  cd "$_pkgname-$pkgver"

  python setup.py install --root="$pkgdir" --prefix=/usr -O1 --skip-build

  install -Dm 644 LICENSE "$pkgdir/usr/share/licenses/$pkgname/LICENSE"
}

