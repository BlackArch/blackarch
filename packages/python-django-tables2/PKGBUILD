# This file is part of BlackArch Linux ( https://www.blackarch.org/ ).
# See COPYING for license details

pkgbase=python-django-tables2
pkgname=('python-django-tables2' 'python2-django-tables2')
pkgver=2.0.5
pkgrel=1
pkgdesc='A module for rendering Django data sets as HTML tables.'
arch=('any')
url='https://github.com/bradleyayers/django-tables2'
license=('MIT')
options=(!emptydirs)
makedepends=('python-setuptools' 'python2-setuptools')
source=("https://github.com/bradleyayers/django-tables2/archive/v$pkgver.tar.gz")
sha512sums=('95dc66dc4c178b30eb26c5e39227556219c49de6861bd683f944c309a22721f8d506f4deaaa14829a6c2b2cc7d4ddbd74ec29e27972f693c17d7d70503b3ef49')

package_python-django-tables2() {
  depends=('python-django')
  optdepends=('python-tablib: to export table data as CSV, XLS, etc.')

  cd "django-tables2-$pkgver"
  python setup.py install --root="$pkgdir" --optimize=1
  install -Dm 644 LICENSE "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
}

package_python2-django-tables2() {

  depends=('python2-django')
  optdepends=('python2-tablib: to export table data as CSV, XLS, etc.')

  cd "django-tables2-$pkgver"
  python2 setup.py install --root="$pkgdir" --optimize=1
  install -Dm 644 LICENSE "$pkgdir/usr/share/licenses/$pkgname/LICENSE"
}

