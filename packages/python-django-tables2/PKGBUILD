# This file is part of BlackArch Linux ( https://www.blackarch.org/ ).
# See COPYING for license details

pkgbase=python-django-tables2
pkgname=('python-django-tables2' 'python2-django-tables2')
_pkgname=django-tables2
pkgver=2.2.1
pkgrel=1
pkgdesc='A module for rendering Django data sets as HTML tables.'
arch=('any')
url='https://github.com/bradleyayers/django-tables2'
license=('MIT')
options=(!emptydirs)
makedepends=('python-setuptools' 'python2-setuptools')
source=("https://files.pythonhosted.org/packages/7e/37/b667d3164a0020fe3327a282c30b21d4c4bd74cb242ec391b665a546e56f/$_pkgname-$pkgver.tar.gz")
sha512sums=('f5760f53d76af22033543757dd1b89876f9e40abcccc606bac1019fb10338c83884298a3f9feb280cfc03b209dfc551d07182ff0950c3cba9fafedd8371ecbce')

package_python-django-tables2() {
  depends=('python-django')
  optdepends=('python-tablib: to export table data as CSV, XLS, etc.')

  cd "django-tables2-$pkgver"
  python setup.py install --root="$pkgdir" --optimize=1
  install -Dm 644 LICENSE "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
}

package_python2-django-tables2() {

  depends=('python2-django')
  optdepends=('python2-tablib: to export table data as CSV, XLS, etc.')

  cd "django-tables2-$pkgver"
  python2 setup.py install --root="$pkgdir" --optimize=1
  install -Dm 644 LICENSE "$pkgdir/usr/share/licenses/$pkgname/LICENSE"
}

