# This file is part of BlackArch Linux ( https://www.blackarch.org/ ).
# See COPYING for license details.

pkgbase=python-publicsuffixlist
pkgname=('python2-publicsuffixlist' 'python-publicsuffixlist')
_pkgname=publicsuffixlist
pkgver=0.7.8
pkgrel=1
pkgdesc='Python publicsuffixlist implementation.'
arch=('any')
url='https://pypi.python.org/pypi/publicsuffixlist'
license=('MPL')
makedepends=('python2-setuptools' 'python-setuptools')
source=("https://files.pythonhosted.org/packages/96/c7/6c69b73aa81c0e6ea844c061aae5d97ee05437ca3d50511dd3f6dadc26c2/$_pkgname-$pkgver.tar.gz")
sha512sums=('1cf1e855cd1b8320f95995560e8d286f272d1d458f0bde7703245e088736a526f3044aba77d77c54f784bfae852770b9b0b5c19e27aeab6bc3dae24996f3adea')

prepare() {
  cp -a "$_pkgname-$pkgver"{,-2}
}

build() {
  cd "$_pkgname-$pkgver-2"

  python2 setup.py build

  cd "$srcdir/$_pkgname-$pkgver"

  python setup.py build
}

package_python2-publicsuffixlist() {
  depends=('python2' 'python2-requests')

  cd "$_pkgname-$pkgver-2"

  python2 setup.py install --prefix=/usr --root="$pkgdir" -O1 --skip-build

  mv "$pkgdir/usr/bin/$_pkgname-download" "$pkgdir/usr/bin/$_pkgname-download2"
}

package_python-publicsuffixlist() {
  depends=('python' 'python-requests')

  cd "$_pkgname-$pkgver"

  python setup.py install --prefix=/usr --root="$pkgdir" -O1 --skip-build
}

