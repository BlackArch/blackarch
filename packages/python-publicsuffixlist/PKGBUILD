# This file is part of BlackArch Linux ( https://www.blackarch.org/ ).
# See COPYING for license details.

pkgbase=python-publicsuffixlist
pkgname=('python2-publicsuffixlist' 'python-publicsuffixlist')
_pkgname=publicsuffixlist
pkgver=0.7.2
pkgrel=1
pkgdesc='Python publicsuffixlist implementation.'
arch=('any')
url='https://pypi.python.org/pypi/publicsuffixlist'
license=('MPL')
makedepends=('python2-setuptools' 'python-setuptools')
source=("https://files.pythonhosted.org/packages/ea/df/e76bc544f85328fcb33a4c754309581d16c8321fef778abc01d5a05aafd0/$_pkgname-$pkgver.tar.gz")
sha512sums=('2538ee51a46c3ad23b2006baa4c76c6205aa46121cc31bca732477c76727b231754d224df6b3a58a51e676edf8763c84ecc2d7def45e310686c91828dfe10f70')

prepare() {
  cp -a "$_pkgname-$pkgver"{,-2}
}

build() {
  cd "$_pkgname-$pkgver-2"

  python2 setup.py build

  cd "$srcdir/$_pkgname-$pkgver"

  python setup.py build
}

package_python2-publicsuffixlist() {
  depends=('python2' 'python2-requests')

  cd "$_pkgname-$pkgver-2"

  python2 setup.py install --prefix=/usr --root="$pkgdir" -O1 --skip-build

  mv "$pkgdir/usr/bin/$_pkgname-download" "$pkgdir/usr/bin/$_pkgname-download2"
}

package_python-publicsuffixlist() {
  depends=('python' 'python-requests')

  cd "$_pkgname-$pkgver"

  python setup.py install --prefix=/usr --root="$pkgdir" -O1 --skip-build
}

