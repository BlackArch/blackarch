# This file is part of BlackArch Linux ( https://www.blackarch.org/ ).
# See COPYING for license details.

pkgbase=python-publicsuffixlist
pkgname=('python2-publicsuffixlist' 'python-publicsuffixlist')
_pkgname=publicsuffixlist
pkgver=0.7.4
pkgrel=1
pkgdesc='Python publicsuffixlist implementation.'
arch=('any')
url='https://pypi.python.org/pypi/publicsuffixlist'
license=('MPL')
makedepends=('python2-setuptools' 'python-setuptools')
source=("https://files.pythonhosted.org/packages/05/98/bbab26c4229b62ef3923c07b9cf164c7a3c8271f30244cce4b6739db475e/$_pkgname-$pkgver.tar.gz")
sha512sums=('577fc0d8a1c514fc2862589027817edea878e56231f5919c9171cf423dff56ac4442b036992452ae2e43b54f83df8afa81651aff680b9e026f710ca4000dcac0')

prepare() {
  cp -a "$_pkgname-$pkgver"{,-2}
}

build() {
  cd "$_pkgname-$pkgver-2"

  python2 setup.py build

  cd "$srcdir/$_pkgname-$pkgver"

  python setup.py build
}

package_python2-publicsuffixlist() {
  depends=('python2' 'python2-requests')

  cd "$_pkgname-$pkgver-2"

  python2 setup.py install --prefix=/usr --root="$pkgdir" -O1 --skip-build

  mv "$pkgdir/usr/bin/$_pkgname-download" "$pkgdir/usr/bin/$_pkgname-download2"
}

package_python-publicsuffixlist() {
  depends=('python' 'python-requests')

  cd "$_pkgname-$pkgver"

  python setup.py install --prefix=/usr --root="$pkgdir" -O1 --skip-build
}

