# This file is part of BlackArch Linux ( https://www.blackarch.org/ ).
# See COPYING for license details.

pkgbase=python-publicsuffixlist
pkgname=('python2-publicsuffixlist' 'python-publicsuffixlist')
_pkgname=publicsuffixlist
pkgver=0.7.6
pkgrel=2
pkgdesc='Python publicsuffixlist implementation.'
arch=('any')
url='https://pypi.python.org/pypi/publicsuffixlist'
license=('MPL')
makedepends=('python2-setuptools' 'python-setuptools')
source=("https://files.pythonhosted.org/packages/0e/cb/40d33e49b02d26fd6dddde7f97fcbd066587a0be0750ede46f1bb8f8013a/$_pkgname-$pkgver.tar.gz")
sha512sums=('fbed989b88ee0b9fde9a7b2aa8d2132aa86ce77bf62e3742ef24a357736507d5c094e4bf55103fdca682d06a43b822a68dabd137feff0fc9e9064cd19dd63ae3')

prepare() {
  cp -a "$_pkgname-$pkgver"{,-2}
}

build() {
  cd "$_pkgname-$pkgver-2"

  python2 setup.py build

  cd "$srcdir/$_pkgname-$pkgver"

  python setup.py build
}

package_python2-publicsuffixlist() {
  depends=('python2' 'python2-requests')

  cd "$_pkgname-$pkgver-2"

  python2 setup.py install --prefix=/usr --root="$pkgdir" -O1 --skip-build

  mv "$pkgdir/usr/bin/$_pkgname-download" "$pkgdir/usr/bin/$_pkgname-download2"
}

package_python-publicsuffixlist() {
  depends=('python' 'python-requests')

  cd "$_pkgname-$pkgver"

  python setup.py install --prefix=/usr --root="$pkgdir" -O1 --skip-build
}

