# This file is part of BlackArch Linux ( http://blackarch.org ).
# See COPYING for license details.

pkgname='l0l'
pkgver=322.1319ea7
pkgrel=1
groups=('blackarch' 'blackarch-exploitation')
arch=('x86_64' 'armv6h' 'armv7h' 'aarch64')
pkgdesc='The Exploit Development Kit.'
url="https://github.com/roissy/l0l"
license=('MIT')
depends=('python2')
makedepends=('git')
source=('git+https://github.com/roissy/l0l')
sha1sums=('SKIP')

pkgver() {
  cd "$srcdir/l0l"

  echo $(git rev-list --count HEAD).$(git rev-parse --short HEAD)
}

prepare() {
  cd "$srcdir/l0l"

  grep -iRl 'python' . | xargs sed -i 's|python$|python2|g'
}

build() {
  cd "$srcdir/l0l"

  make
}

package() {
  cd "$srcdir/l0l"

  make DESTDIR="$pkgdir" install

  install -Dm644 -t "$pkgdir/usr/share/doc/l0l/" README.md
  install -Dm644 LICENSE "$pkgdir/usr/share/licenses/l0l/LICENSE"
}
