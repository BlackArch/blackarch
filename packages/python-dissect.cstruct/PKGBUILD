# This file is part of BlackArch Linux ( https://www.blackarch.org/ ).
# See COPYING for license details.

pkgname=python-dissect.cstruct
_pkgname=${pkgname#python-}
pkgver=4.1.r1.g09830d2
pkgrel=1
pkgdesc='A Dissect module implementing a parser for C-like structures.'
arch=('any')
url='https://github.com/fox-it/dissect.cstruct'
license=('Apache-2.0')
depends=('python')
makedepends=('git' 'lzo' 'python-pip' 'python-build' 'python-installer' 'python-wheel' 'python-setuptools' 'python-setuptools-scm')
checkdepends=('python-pytest')
options=(!emptydirs)
source=("git+https://github.com/fox-it/$_pkgname")
sha512sums=('SKIP')

pkgver() {
  cd $_pkgname

  ( set -o pipefail
    git describe --long --tags --abbrev=7 2>/dev/null |
      sed 's/\([^-]*-g\)/r\1/;s/-/./g' ||
    printf "%s.%s" "$(git rev-list --count HEAD)" \
      "$(git rev-parse --short=7 HEAD)"
  )
}

prepare() {
    git -C "${srcdir}/${_pkgname}" clean -dfx
}

build() {
    cd $_pkgname

    python -m build --wheel --no-isolation
}

package() {
    cd $_pkgname

    python -m installer --destdir="$pkgdir" dist/*.whl
}

check() {
    cd $_pkgname

    pytest
}

