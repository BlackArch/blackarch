# This file is part of BlackArch Linux ( https://www.blackarch.org/ ).
# See COPYING for license details.

pkgname=forager
pkgver=115.7439b0a
pkgrel=1
groups=('blackarch' 'blackarch-recon')
arch=('any')
pkgdesc='Multithreaded threat Intelligence gathering utilizing.'
url='https://github.com/byt3smith/Forager'
license=('MIT')
depends=('python' 'python-xlrd' 'python-colorama' 'python-ply'
         'python-pdfminer3k')
makedepends=('git')
source=("$pkgname::git+https://github.com/byt3smith/Forager.git")
sha512sums=('SKIP')

pkgver() {
  cd $pkgname

  echo $(git rev-list --count HEAD).$(git rev-parse --short HEAD)
}

package() {
  cd $pkgname

  install -dm 755 "$pkgdir/usr/bin"
  install -dm 755 "$pkgdir/usr/share/$pkgname"

  install -Dm 644 -t "$pkgdir/usr/share/doc/$pkgname/" README.md
  install -Dm 644 LICENSE "$pkgdir/usr/share/licenses/$pkgname/LICENSE"

  rm README.md LICENSE

  cp -a * "$pkgdir/usr/share/$pkgname/"

cat > "$pkgdir/usr/bin/$pkgname" << EOF
#!/bin/sh
cd /usr/share/$pkgname
exec python Forager.py "\$@"
EOF

  chmod +x "$pkgdir/usr/bin/$pkgname"
}

