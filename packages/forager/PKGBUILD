# This file is part of BlackArch Linux ( http://blackarch.org ).
# See COPYING for license details.

pkgname=forager
pkgver=2.0.3
pkgrel=3
groups=('blackarch' 'blackarch-recon')
arch=('any')
pkgdesc='Multithreaded threat Intelligence gathering utilizing.'
url='https://github.com/byt3smith/Forager'
license=('MIT')
depends=('python' 'python-xlrd' 'python-colorama' 'python-ply'
         'python-pdfminer3k')
source=("https://github.com/byt3smith/Forager/archive/v$pkgver.tar.gz")
sha512sums=('89be38719b00e22cd8451da58e2e1117d2bc4e46c0a8761e3484145e725c571f6c139bc2ee2f01751a8fee1847c667bebd89db3a5d34ad0d1660b1a33555f296')

package() {
  cd "Forager-$pkgver"

  install -dm 755 "$pkgdir/usr/bin"
  install -dm 755 "$pkgdir/usr/share/$pkgname"

  install -Dm 644 -t "$pkgdir/usr/share/doc/$pkgname/" README.md
  install -Dm 644 LICENSE "$pkgdir/usr/share/licenses/$pkgname/LICENSE"

  rm README.md LICENSE

  cp -a * "$pkgdir/usr/share/$pkgname/"

cat > "$pkgdir/usr/bin/$pkgname" << EOF
#!/bin/sh
cd /usr/share/$pkgname
exec python Forager.py "\$@"
EOF

  chmod a+x "$pkgdir/usr/bin/$pkgname"
}

