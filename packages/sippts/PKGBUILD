# This file is part of BlackArch Linux ( https://www.blackarch.org/ ).
# See COPYING for license details.

pkgname=sippts
pkgver=127.3da2878
pkgrel=1
pkgdesc='Set of tools to audit SIP based VoIP Systems.'
groups=('blackarch' 'blackarch-voip')
arch=('any')
url='https://github.com/Pepelux/sippts'
license=('custom:unknown')
depends=('perl' 'perl-dbi' 'perl-io-socket' 'perl-netaddr-ip' 'perl-net-pcap'
         'perl-io-socket-timeout' 'perl-string-hexconvert'
         'perl-perlio-via-timeout')
makedepends=('git')
source=("git+https://github.com/Pepelux/$pkgname.git")
sha512sums=('SKIP')

pkgver() {
  cd $pkgname

  echo $(git rev-list --count HEAD).$(git rev-parse --short HEAD)
}

package() {
  cd $pkgname

  install -dm 755 "$pkgdir/usr/bin"
  install -dm 755 "$pkgdir/usr/share/$pkgname"

  install -Dm 644 -t "$pkgdir/usr/share/doc/$pkgname/" readme.md
  install -Dm 644 LICENSE.txt "$pkgdir/usr/share/licenses/$pkgname/LICENSE"

  rm readme.md

  cp -a * "$pkgdir/usr/share/$pkgname/"

  for i in *.pl; do
    if [ "$i" = "sipcrack.pl" ]
    then
      cat > "$pkgdir/usr/bin/"`echo $i | sed 's/.pl//'`"-sippts" << EOF
#!/bin/sh
cd /usr/share/$pkgname
exec perl $i "\$@"
EOF
      chmod +x "$pkgdir/usr/bin/"`echo $i | sed 's/.pl//'`"-sippts"
    else
      cat > "$pkgdir/usr/bin/"`echo $i | sed 's/.pl//'` << EOF
#!/bin/sh
cd /usr/share/$pkgname
exec perl $i "\$@"
EOF
      chmod +x "$pkgdir/usr/bin/"`echo $i | sed 's/.pl//'`
    fi
  done
}

