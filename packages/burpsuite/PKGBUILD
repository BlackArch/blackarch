# This file is part of BlackArch Linux ( http://blackarch.org ).
# See COPYING for license details.

pkgname=burpsuite
pkgver=1.7.36
pkgrel=1
epoch=1
groups=('blackarch' 'blackarch-fuzzer' 'blackarch-proxy' 'blackarch-scanner'
        'blackarch-webapp')
pkgdesc='An integrated platform for attacking web applications (free edition).'
arch=('any')
url='http://portswigger.net/burp/'
depends=('java-environment' 'giflib')
license=('custom')
noextract=('burpsuite.jar')
source=("burpsuite.jar::https://portswigger.net/burp/releases/download?product=community&version=${pkgver}&type=jar"
        #'burpsuite.jar::http://portswigger.net/DownloadUpdate.ashx?Product=Free'
        'LICENSE'
        'burpsuite.desktop'
        'icon64.png')
sha512sums=('9788720feed1227f196adc7b4de78793de9a2e4602bdf066a37d482bc2f5a9d7c04773ca8325f953591db81125b7d117f77f7a8f9c8dc461e795ea44af4e1cab'
            '37abc24b19571bfd2941e3dd7aa8a8a330bd3bbbafb3d1f813035ab0313e189c1f4a43a68f820a32656b86dd29cca94afcaefc6a4bfb4219ef18dc0fc4923b86'
            '87790fac549e165f93f4e1dcaff93484957f20e7530e8b51586a22f4db594f6bfae730e4748e809c9b127571d764b026432cdaf47c282469a4ab42b037245895'
            '928083e0189ce50304c4b32f8f6ef56be79881090bffdaddb5e990a59186ed2596c03293255693d488a47519e6da4e969e74e9bfe22a0f6ca53491a4e0749575')

package() {
  cd ${srcdir}

  install -dm 755 ${pkgdir}/usr/bin
  install -dm 755 ${pkgdir}/usr/share/{applications,pixmaps,burpsuite,licenses/burpsuite}

  install -Dm644 burpsuite.jar ${pkgdir}/usr/share/burpsuite/burpsuite.jar
  install -Dm644 LICENSE ${pkgdir}/usr/share/licenses/burpsuite/LICENSE
  install -Dm644 burpsuite.desktop ${pkgdir}/usr/share/applications/
  install -Dm644 icon64.png ${pkgdir}/usr/share/pixmaps/burpsuite.png

  cat > ${pkgdir}/usr/bin/burpsuite << EOF
#!/bin/sh
exec \$JAVA_HOME/bin/java -jar /usr/share/burpsuite/burpsuite.jar "\${@}"
EOF

  chmod +x ${pkgdir}/usr/bin/burpsuite
}
