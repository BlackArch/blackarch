# Maintainer: lsfxz (https://github.com/lsfxz/quarry)
# This file is part of BlackArch Linux ( http://blackarch.org ).
# See COPYING for license details.

pkgname='ruby-rubydns-1'
_gemname='rubydns'
pkgver='1.0.3'
pkgrel=1
pkgdesc="An easy to use DNS server and resolver for Ruby."
arch=('any')
url='http://www.codeotaku.com/projects/rubydns'
license=("MIT")
depends=('ruby' 'ruby-celluloid-0.16' 'ruby-celluloid-io-0.16' 'ruby-timers-4.0')
options=('!emptydirs')
source=("https://rubygems.org/downloads/$_gemname-$pkgver.gem" )
noextract=("$_gemname-$pkgver.gem")
sha1sums=('9185f807598cc52df3cd12a2a424ba81ee4dd882' )


package() {
  local _gemdir="$(ruby -rubygems -e 'puts Gem.default_dir')"
  gem install --ignore-dependencies --no-document --no-user-install -i "$pkgdir/$_gemdir" -n "$pkgdir"/usr/bin $_gemname-$pkgver.gem 
  rm "$pkgdir/$_gemdir/cache/$_gemname-$pkgver.gem"
  # non-HEAD version should not install any files in /usr/bin
  rm -r "$pkgdir"/usr/bin/

  # Preserve library and other required directories. Remove junk that gem installs.
  local _copydir=`mktemp -d -t $_gemname.XXX`
  chmod 755 $_copydir
  (cd "$pkgdir/$_gemdir/gems/$_gemname-$pkgver"/ && cp -r --parents lib $_copydir)
  rm -rf "$pkgdir/$_gemdir/gems/$_gemname-$pkgver"
  mv $_copydir "$pkgdir/$_gemdir/gems/$_gemname-$pkgver"

}
