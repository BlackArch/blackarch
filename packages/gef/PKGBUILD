# This file is part of BlackArch Linux ( https://www.blackarch.org/ ).
# See COPYING for license details.

pkgname=gef
pkgver=1863.6846e50
pkgrel=1
groups=('blackarch' 'blackarch-debugger' 'blackarch-exploitation')
pkgdesc='Multi-Architecture GDB Enhanced Features for Exploiters & Reverse-Engineers.'
arch=('any')
url='https://github.com/hugsy/gef'
license=('MIT')
depends=('python' 'python-capstone' 'python-lxml' 'ropgadget' 'gdb'
         'python-keystone' 'keystone' 'capstone' 'ropper' 'python-ropper'
         'python-retdec-python' 'unicorn')
makedepends=('git')
source=("git+https://github.com/hugsy/$pkgname.git")
sha512sums=('SKIP')
install="$pkgname.install"

pkgver() {
  cd $pkgname

  echo $(git rev-list --count HEAD).$(git rev-parse --short HEAD)
}

package() {
  cd $pkgname

  install -dm 755 "$pkgdir/usr/share/$pkgname"

  install -Dm 644 -t "$pkgdir/usr/share/doc/$pkgname" README.md docs/*.md
  cp -r docs/commands "$pkgdir/usr/share/doc/$pkgname/"
  install -Dm 644 LICENSE "$pkgdir/usr/share/licenses/$pkgname/LICENSE"

  rm -rf README.md LICENSE docs/

  cp --no-preserve=ownership -a * "$pkgdir/usr/share/$pkgname"
}

