# This file is part of BlackArch Linux ( http://blackarch.org ).
# See COPYING for license details.

pkgname='gef'
pkgver=1236.fa237e2
pkgrel=1
groups=('blackarch' 'blackarch-debugger' 'blackarch-exploitation')
pkgdesc='Multi-Architecture GDB Enhanced Features for Exploiters & Reverse-Engineers.'
arch=('any')
url='https://github.com/hugsy/gef'
license=('MIT')
depends=('python' 'python-capstone' 'python-lxml' 'ropgadget' 'gdb'
         'python-keystone' 'keystone' 'capstone' 'ropper' 'python-ropper'
         'python-retdec-python' 'unicorn')
makedepends=('git')
source=('git+https://github.com/hugsy/gef.git')
install='gef.install'
sha1sums=('SKIP')

pkgver() {
  cd "$srcdir/gef"

  echo $(git rev-list --count HEAD).$(git rev-parse --short HEAD)
}

package() {
  cd "$srcdir/gef"

  mkdir -p "$pkgdir/usr/share/gef"

  install -Dm644 -t "$pkgdir/usr/share/doc/gef" README.md docs/*.md
  cp -r docs/commands "$pkgdir/usr/share/doc/gef/"
  install -Dm644 LICENSE "$pkgdir/usr/share/licenses/gef/LICENSE"

  rm -rf README.md LICENSE docs/

  cp --no-preserve=ownership -a * "$pkgdir/usr/share/gef"
}
