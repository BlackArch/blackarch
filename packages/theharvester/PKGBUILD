# This file is part of BlackArch Linux ( https://www.blackarch.org/ ).
# See COPYING for license details.

pkgname=theharvester
pkgver=703.5f8e32e
pkgrel=1
groups=('blackarch' 'blackarch-recon')
pkgdesc='Python tool for gathering e-mail accounts and subdomain names from different public sources (search engines, pgp key servers).'
url='http://www.edge-security.com/theHarvester.php'
arch=('any')
license=('GPL2')
depends=('python' 'python-requests' 'python-shodan' 'python-texttable'
         'python-beautifulsoup4' 'python-plotly' 'python-pytest' 'python-yaml'
         'python-censys' 'python-pytz' 'python-decorator' 'python-retrying')
makedepends=('git')
source=('git+https://github.com/laramies/theHarvester.git')
sha512sums=('SKIP')

pkgver() {
  cd theHarvester

  echo $(git rev-list --count HEAD).$(git rev-parse --short HEAD)
}

package() {
  cd theHarvester

  install -dm 755 "$pkgdir/usr/bin"
  install -dm 755 "$pkgdir/usr/share/$pkgname"

  install -Dm 644 -t "$pkgdir/usr/share/doc/$pkgname" changelog.txt
  install -Dm 644 -t "$pkgdir/usr/share/licenses/$pkgname" COPYING LICENSES

  cp --no-preserve=ownership -a * "$pkgdir/usr/share/$pkgname/"

  ln -s "/usr/share/$pkgname/theHarvester.py" "$pkgdir/usr/bin/$pkgname"
}

