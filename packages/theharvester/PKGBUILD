# This file is part of BlackArch Linux ( http://blackarch.org ).
# See COPYING for license details.

pkgname='theharvester'
pkgver=59.2555376
pkgrel=1
groups=('blackarch' 'blackarch-recon')
pkgdesc='Python tool for gathering e-mail accounts and subdomain names from different public sources (search engines, pgp key servers).'
url='http://www.edge-security.com/theHarvester.php'
arch=('any')
license=('GPL2')
depends=('python2' 'python2-requests' 'shodan')
makedepends=('git')
source=('git+https://github.com/laramies/theHarvester.git')
sha1sums=('SKIP')

pkgver() {
  cd "$srcdir/theHarvester"

  echo $(git rev-list --count HEAD).$(git rev-parse --short HEAD)
}

prepare() {
  cd "$srcdir/theHarvester"

  sed -i 's|env python$|env python2|' theHarvester.py
}

package() {
  cd "$srcdir/theHarvester"

  mkdir -p "$pkgdir/usr/bin"
  mkdir -p "$pkgdir/usr/share/theharvester"

  install -Dm755 *.py "$pkgdir/usr/share/theharvester"
  install -Dm644 -t "$pkgdir/usr/share/doc/theharvester" changelog.txt
  install -Dm644 -t "$pkgdir/usr/share/licenses/theharvester" COPYING LICENSES

  cp --no-preserve=ownership -a discovery lib *.txt \
    "$pkgdir/usr/share/theharvester"

  ln -s "/usr/share/theharvester/theHarvester.py" "$pkgdir/usr/bin/theharvester"
}
