# This file is part of BlackArch Linux ( https://www.blackarch.org/ ).
# See COPYING for license details.

pkgname=pfring-dkms
pkgver=7.4.0.r463.g22329032
pkgrel=1
pkgdesc='Linux kernel module for packet capture at wirespeed.'
url='https://github.com/ntop/PF_RING'
arch=('x86_64' 'aarch64')
license=('LGPL')
depends=('dkms' 'linux-headers' 'numactl')
makedepends=('git')
source=("$pkgname::git+https://github.com/ntop/PF_RING.git")
sha512sums=('SKIP')

pkgver() {
  cd $pkgname

  git describe --long --tags | sed 's/\([^-]*-g\)/r\1/;s/-/./g'
}

package() {
  cd $pkgname

  install -Dm 644 kernel/dkms.conf.in \
    "$pkgdir/usr/src/pfring-$pkgver/dkms.conf"

  cp -a --no-preserve=ownership kernel/* "$pkgdir/usr/src/pfring-$pkgver/"
}

