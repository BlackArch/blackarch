# This file is part of BlackArch Linux ( http://blackarch.org ).
# See COPYING for license details.

pkgname='crawlic'
pkgver=51.739fe2b
pkgrel=2
groups=('blackarch' 'blackarch-webapp' 'blackarch-recon')
pkgdesc='Web recon tool (find temporary files, parse robots.txt, search folders, google dorks and search domains hosted on same server).'
arch=('any')
url='https://github.com/Ganapati/Crawlic'
license=('Beerware')
depends=('python2' 'python2-requests' 'python2-simplejson')
makedepends=('git')
source=('git+https://github.com/Ganapati/Crawlic.git')
sha1sums=('SKIP')

pkgver() {
  cd "$srcdir/Crawlic"

  echo $(git rev-list --count HEAD).$(git rev-parse --short HEAD)
}

prepare() {
  cd "$srcdir/Crawlic"

  sed -i 's|path = os.path.dirname(__file__)|path = "/usr/share/crawlic"|' \
    crawlic.py
}

package() {
  cd "$srcdir/Crawlic"

  mkdir -p "$pkgdir/usr/bin"
  mkdir -p "$pkgdir/usr/share/crawlic"

  install -Dm644 -t "$pkgdir/usr/share/doc/crawlic/" README.md
  install -Dm644 LICENSE "$pkgdir/usr/share/licenses/crawlic/LICENSE"

  rm README.md LICENSE

  cp -a * "$pkgdir/usr/share/crawlic"

  cat > "$pkgdir/usr/bin/crawlic" << EOF
#!/bin/sh
cd /usr/share/crawlic
exec python2 crawlic.py "\$@"
EOF

  chmod a+x "$pkgdir/usr/bin/crawlic"
}
