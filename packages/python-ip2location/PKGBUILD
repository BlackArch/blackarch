# This file is part of BlackArch Linux ( https://www.blackarch.org/ ).
# See COPYING for license details.

pkgbase=python-ip2location
pkgname=('python2-ip2location' 'python-ip2location')
_pkgname=IP2Location
pkgver=8.6.0
pkgrel=1
pkgdesc='Python API for IP2Location database.'
arch=('any')
url='https://pypi.org/project/IP2Location/#files'
license=('MIT')
makedepends=('python2-setuptools' 'python-setuptools')
source=("https://files.pythonhosted.org/packages/80/a7/18dfbc5f7ba75f420cf8c317c09fa782081001c52027481669bc4475b786/IP2Location-$pkgver.tar.gz")
sha512sums=('273d8c04871dd8fd219b2fa5b338eff7f2b5d69238737719f247a26c18ce1539ec6922231505bffa186309c15fe69587cac099bafd0e706d6c36f0088717dd55')

prepare() {
  cp -a "$_pkgname-$pkgver"{,-2}
}

build() {
  cd "$_pkgname-$pkgver-2"

  python2 setup.py build

  cd "$srcdir/$_pkgname-$pkgver"

  python setup.py build
}

package_python2-ip2location() {
  depends=('python2')

  cd "$_pkgname-$pkgver-2"

  python2 setup.py install --prefix=/usr --root="$pkgdir" -O1 --skip-build
}

package_python-ip2location() {
  depends=('python')

  cd "$_pkgname-$pkgver"

  python setup.py install --prefix=/usr --root="$pkgdir" -O1 --skip-build
}

