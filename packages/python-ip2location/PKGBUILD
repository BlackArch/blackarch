# This file is part of BlackArch Linux ( https://www.blackarch.org/ ).
# See COPYING for license details.

pkgbase=python-ip2location
pkgname=('python2-ip2location' 'python-ip2location')
_pkgname=IP2Location
pkgver=8.10.2
pkgrel=1
pkgdesc='Python API for IP2Location database.'
arch=('any')
url='https://pypi.org/project/IP2Location/#files'
license=('MIT')
makedepends=('python2-setuptools' 'python-setuptools')
source=("https://files.pythonhosted.org/packages/source/${_pkgname::1}/$_pkgname/IP2Location-$pkgver.tar.gz")
sha512sums=('2da677097d191ae11a1d3f3a9044e1f5a0a28fdc5143f3d62f7ec393e0d171895acc7a48a8c96b34ad74656b15d6da42125673e97e5d07e67754153a3373a3e9')

prepare() {
  cp -a "$_pkgname-$pkgver"{,-2}
}

build() {
  cd "$_pkgname-$pkgver-2"

  python2 setup.py build

  cd "$srcdir/$_pkgname-$pkgver"

  python setup.py build
}

package_python2-ip2location() {
  depends=('python2')

  cd "$_pkgname-$pkgver-2"

  python2 setup.py install --prefix=/usr --root="$pkgdir" -O1 --skip-build
}

package_python-ip2location() {
  depends=('python')

  cd "$_pkgname-$pkgver"

  python setup.py install --prefix=/usr --root="$pkgdir" -O1 --skip-build
}

