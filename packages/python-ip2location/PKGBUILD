# This file is part of BlackArch Linux ( https://www.blackarch.org/ ).
# See COPYING for license details.

pkgbase=python-ip2location
pkgname=('python2-ip2location' 'python-ip2location')
_pkgname=IP2Location
pkgver=8.7.2
pkgrel=1
pkgdesc='Python API for IP2Location database.'
arch=('any')
url='https://pypi.org/project/IP2Location/#files'
license=('MIT')
makedepends=('python2-setuptools' 'python-setuptools')
source=("https://files.pythonhosted.org/packages/7a/2b/dfce3b5f1b15731d9c403418b5120a72747f0d5b8608ec647649427f1dcb/IP2Location-$pkgver.tar.gz")
sha512sums=('2ccfdeb30320e175c424db6bbea045c0021ca0581eb8589fb904a0e6f26f8dc86aa975761b8c49da99def59f94b7ab96b7e974176ba74712542e6c8a4126140c')

prepare() {
  cp -a "$_pkgname-$pkgver"{,-2}
}

build() {
  cd "$_pkgname-$pkgver-2"

  python2 setup.py build

  cd "$srcdir/$_pkgname-$pkgver"

  python setup.py build
}

package_python2-ip2location() {
  depends=('python2')

  cd "$_pkgname-$pkgver-2"

  python2 setup.py install --prefix=/usr --root="$pkgdir" -O1 --skip-build
}

package_python-ip2location() {
  depends=('python')

  cd "$_pkgname-$pkgver"

  python setup.py install --prefix=/usr --root="$pkgdir" -O1 --skip-build
}

