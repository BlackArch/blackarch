# This file is part of BlackArch Linux ( https://www.blackarch.org/ ).
# See COPYING for license details.

pkgbase=python-ip2location
pkgname=('python2-ip2location' 'python-ip2location')
_pkgname=IP2Location
pkgver=8.7.1
pkgrel=1
pkgdesc='Python API for IP2Location database.'
arch=('any')
url='https://pypi.org/project/IP2Location/#files'
license=('MIT')
makedepends=('python2-setuptools' 'python-setuptools')
source=("https://files.pythonhosted.org/packages/12/01/e1adeb8ba1c7a0f8778dfe77a6a8ef7f5387fc5d89007fdae562ab2357bf/IP2Location-$pkgver.tar.gz")
sha512sums=('a35320fc3a523caed330cdb7c3f7159dcbf9f3ab1f33d9a6fb21b3c948d796653508de756be0755553069d7df61aec673a6f1b0adecb95051db8571f428fc14a')

prepare() {
  cp -a "$_pkgname-$pkgver"{,-2}
}

build() {
  cd "$_pkgname-$pkgver-2"

  python2 setup.py build

  cd "$srcdir/$_pkgname-$pkgver"

  python setup.py build
}

package_python2-ip2location() {
  depends=('python2')

  cd "$_pkgname-$pkgver-2"

  python2 setup.py install --prefix=/usr --root="$pkgdir" -O1 --skip-build
}

package_python-ip2location() {
  depends=('python')

  cd "$_pkgname-$pkgver"

  python setup.py install --prefix=/usr --root="$pkgdir" -O1 --skip-build
}

