# This file is part of BlackArch Linux ( https://www.blackarch.org/ ).
# See COPYING for license details.

pkgname=brutexss
pkgver=54.ba753df
pkgrel=2
pkgdesc='Cross-Site Scripting Bruteforcer.'
groups=('blackarch' 'blackarch-webapp' 'blackarch-fuzzer')
arch=('any')
url='https://github.com/shawarkhanethicalhacker/BruteXSS'
license=('GPL3')
depends=('python2' 'python2-lxml')
makedepends=('git')
source=("$pkgname::git+https://github.com/shawarkhanethicalhacker/BruteXSS.git")
sha512sums=('SKIP')

pkgver() {
  cd $pkgname

  echo $(git rev-list --count HEAD).$(git rev-parse --short HEAD)
}

package() {
  cd $pkgname

  install -dm 755 "$pkgdir/usr/bin"
  install -dm 755 "$pkgdir/usr/share/$pkgname"

  install -Dm 644 -t "$pkgdir/usr/share/doc/$pkgname/" README.md
  install -Dm 644 License.txt "$pkgdir/usr/share/licenses/$pkgname/LICENSE"

  rm README.md License.txt

  cp -a * "$pkgdir/usr/share/$pkgname/"

  cat > "$pkgdir/usr/bin/$pkgname" << EOF
#!/bin/sh
cd /usr/share/$pkgname
exec python2 $pkgname.py "\$@"
EOF

  chmod +x "$pkgdir/usr/bin/$pkgname"
}

