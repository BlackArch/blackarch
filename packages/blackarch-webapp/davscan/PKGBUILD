# This file is part of BlackArch Linux ( https://www.blackarch.org/ ).
# See COPYING for license details.

pkgname=davscan
pkgver=30.701f967
pkgrel=2
pkgdesc='Fingerprints servers, finds exploits, scans WebDAV.'
groups=('blackarch' 'blackarch-webapp' 'blackarch-scanner'
        'blackarch-fingerprint' 'blackarch-recon')
arch=('any')
url='https://github.com/Graph-X/davscan'
license=('custom:unknown')
depends=('python2' 'python2-requests' 'python2-pysqlite' 'python2-lxml'
         'python2-beautifulsoup4')
makedepends=('git')
source=("git+https://github.com/Graph-X/$pkgname.git")
sha512sums=('SKIP')

pkgver() {
  cd $pkgname

  echo $(git rev-list --count HEAD).$(git rev-parse --short HEAD)
}

package() {
  cd $pkgname

  install -dm 755 "$pkgdir/usr/bin"
  install -dm 755 "$pkgdir/usr/share/$pkgname"

  install -Dm 644 -t "$pkgdir/usr/share/doc/$pkgname/" README.md
  install -Dm 644 LICENSE "$pkgdir/usr/share/licenses/$pkgname/LICENSE"

  rm README.md LICENSE

  cp -a * "$pkgdir/usr/share/$pkgname/"

  cat > "$pkgdir/usr/bin/$pkgname" << EOF
#!/bin/sh
cd /usr/share/$pkgname
exec python2 $pkgname.py "\$@"
EOF

  chmod +x "$pkgdir/usr/bin/$pkgname"
}

