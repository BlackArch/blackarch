# This file is part of BlackArch Linux ( https://www.blackarch.org/ ).
# See COPYING for license details.

pkgname=filebuster
pkgver=73.2d1749f
pkgrel=1
pkgdesc='An extremely fast and flexible web fuzzer.'
groups=('blackarch' 'blackarch-webapp' 'blackarch-fuzzer')
arch=('any')
url='https://github.com/henshin/filebuster'
license=('GPL3')
depends=('perl' 'perl-yaml' 'perl-switch' 'perl-net-dns' 'perl-list-moreutils'
         'perl-io-socket-ssl' 'perl-io-socket-socks-wrapper' 'perl-uri'
         'perl-furl' 'perl-http-parser-xs' 'perl-class-accessor-lite'
         'perl-cache-lru' 'perl-net-dns-lite' 'perl-exporter-tiny'
         'perl-io-async' 'perl-html-parser' )
makedepends=('git')
source=("git+https://github.com/henshin/$pkgname.git")
sha512sums=('SKIP')

pkgver() {
  cd $pkgname

  echo $(git rev-list --count HEAD).$(git rev-parse --short HEAD)
}

package() {
  cd $pkgname

  install -Dm 755 "$pkgname.pl" "$pkgdir/usr/bin/$pkgname"
  install -Dm 644 -t "$pkgdir/usr/share/$pkgname/wordlists" wordlists/*
  install -Dm 644 -t "$pkgdir/usr/share/doc/$pkgname/" README.md
  install -Dm 644 LICENSE "$pkgdir/usr/share/licenses/$pkgname/LICENSE"
}

