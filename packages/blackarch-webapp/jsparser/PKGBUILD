# This file is part of BlackArch Linux ( https://www.blackarch.org/ ).
# See COPYING for license details.

pkgname=jsparser
pkgver=31.ccd3ab6
pkgrel=3
pkgdesc='Parse javascript using Tornado and JSBeautifier to discover interesting enpoints.'
arch=('any')
groups=('blackarch' 'blackarch-webapp' 'blackarch-reversing')
url='https://github.com/nahamsec/JSParser'
license=('custom:unknown')
depends=('python2' 'python2-safeurl' 'python2-tornado' 'python2-jsbeautifier'
         'python2-netaddr' 'python2-pycurl' 'python2-beautifulsoup4')
makedepends=('git')
source=("$pkgname::git+https://github.com/nahamsec/JSParser.git")
sha512sums=('SKIP')

pkgver() {
  cd $pkgname

  echo $(git rev-list --count HEAD).$(git rev-parse --short HEAD)
}

package() {
  cd $pkgname

  install -dm 755 "$pkgdir/usr/bin"
  install -dm 755 "$pkgdir/usr/share/$pkgname"

  install -Dm 644 -t "$pkgdir/usr/share/doc/$pkgname/" README.md

  rm README.md

  cp -a * "$pkgdir/usr/share/$pkgname/"

  cat > "$pkgdir/usr/bin/$pkgname" << EOF
#!/bin/sh
cd /usr/share/$pkgname
exec python2 handler.py "\$@"
EOF

  chmod +x "$pkgdir/usr/bin/$pkgname"
}

