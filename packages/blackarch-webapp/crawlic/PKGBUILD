# This file is part of BlackArch Linux ( https://www.blackarch.org/ ).
# See COPYING for license details.

pkgname=crawlic
pkgver=51.739fe2b
pkgrel=2
groups=('blackarch' 'blackarch-webapp' 'blackarch-recon')
pkgdesc='Web recon tool (find temporary files, parse robots.txt, search folders, google dorks and search domains hosted on same server).'
arch=('any')
url='https://github.com/Ganapati/Crawlic'
license=('custom:unknown')
depends=('python2' 'python2-requests' 'python2-simplejson')
makedepends=('git')
source=("$pkgname::git+https://github.com/Ganapati/Crawlic.git")
sha512sums=('SKIP')

pkgver() {
  cd $pkgname

  echo $(git rev-list --count HEAD).$(git rev-parse --short HEAD)
}

prepare() {
  cd $pkgname

  sed -i 's|path = os.path.dirname(__file__)|path = "/usr/share/crawlic"|' \
    crawlic.py
}

package() {
  cd $pkgname

  install -dm 755 "$pkgdir/usr/bin"
  install -dm 755 "$pkgdir/usr/share/$pkgname"

  install -Dm 644 -t "$pkgdir/usr/share/doc/$pkgname/" README.md
  install -Dm 644 LICENSE "$pkgdir/usr/share/licenses/$pkgname/LICENSE"

  rm README.md LICENSE

  cp -a * "$pkgdir/usr/share/$pkgname/"

  cat > "$pkgdir/usr/bin/$pkgname" << EOF
#!/bin/sh
cd /usr/share/$pkgname
exec python2 $pkgname.py "\$@"
EOF

  chmod +x "$pkgdir/usr/bin/$pkgname"
}

