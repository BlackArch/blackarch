# This file is part of BlackArch Linux ( https://www.blackarch.org/ ).
# See COPYING for license details.

pkgbase=python-antlerinator
pkgname=('python2-antlerinator' 'python-antlerinator')
_pkgname=antlerinator
pkgver=4.9
pkgrel=1
pkgdesc='Python utility package to help keeping components of ANTLR v4 in sync.'
arch=('any')
url='https://pypi.org/project/antlerinator/#files'
license=('BSD')
makedepends=('python2-setuptools' 'python-setuptools')
source=("https://files.pythonhosted.org/packages/e3/72/875cf4e3c4f983d2a2fb1f9f6b37c8eff712611e9796980590403221ce97/$_pkgname-$pkgver.tar.gz")
sha512sums=('79dbaf4a5ce6431e35cbe796694f9b38717d97675811b57edfb80a73833ad2357a1caa6d2afb36885fc275f3f465063a8748838cc33ef13a96b83a1903c217c0')

prepare() {
  cp -a "$_pkgname-$pkgver"{,-2}
}

build() {
  cd "$_pkgname-$pkgver-2"

  python2 setup.py build

  cd "$srcdir/$_pkgname-$pkgver"

  python setup.py build
}

package_python2-antlerinator() {
  depends=('python2' 'python2-typing')

  cd "$_pkgname-$pkgver-2"

  python2 setup.py install --prefix=/usr --root="$pkgdir" --optimize=1 \
    --skip-build

  mv "$pkgdir/usr/bin/$_pkgname-install" "$pkgdir/usr/bin/$_pkgname-install2"
}

package_python-antlerinator() {
  depends=('python' 'python-typing')

  cd "$_pkgname-$pkgver"

  python setup.py install --prefix=/usr --root="$pkgdir" --optimize=1 \
    --skip-build
}

