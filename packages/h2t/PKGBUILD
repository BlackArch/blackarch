# This file is part of BlackArch Linux ( https://www.blackarch.org/ ).
# See COPYING for license details.

pkgname=h2t
pkgver=36.9183a30
pkgrel=2
pkgdesc='Scans a website and suggests security headers to apply.'
groups=('blackarch' 'blackarch-webapp' 'blackarch-scanner'
        'blackarch-defensive')
arch=('any')
url='https://github.com/gildasio/h2t'
license=('MIT')
depends=('python' 'python-requests' 'python-colorama' 'python-idna'
         'python-chardet' 'python-urllib3' 'python-certifi')
makedepends=('git')
source=("git+https://github.com/gildasio/$pkgname.git")
sha512sums=('SKIP')

pkgver() {
  cd $pkgname

  echo $(git rev-list --count HEAD).$(git rev-parse --short HEAD)
}

package() {
  cd $pkgname

  install -dm 755 "$pkgdir/usr/bin"
  install -dm 755 "$pkgdir/usr/share/$pkgname"

  install -Dm 644 -t "$pkgdir/usr/share/doc/$pkgname/" *.md
  install -Dm 644 LICENSE "$pkgdir/usr/share/licenses/$pkgname/LICENSE"

  rm *.md LICENSE

  cp -a * "$pkgdir/usr/share/$pkgname/"

  cat > "$pkgdir/usr/bin/$pkgname" << EOF
#!/bin/sh
cd /usr/share/$pkgname
exec python $pkgname.py "\$@"
EOF

  chmod +x "$pkgdir/usr/bin/$pkgname"
}

