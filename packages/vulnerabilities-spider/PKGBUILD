# This file is part of BlackArch Linux ( http://blackarch.org ).
# See COPYING for license details.

pkgname=vulnerabilities-spider
pkgver=1.426e70f
pkgrel=2
pkgdesc='A tool to scan for web vulnerabilities.'
groups=('blackarch' 'blackarch-webapp' 'blackarch-scanner')
arch=('any')
url='https://github.com/muhammad-bouabid/Vulnerabilities-spider'
license=('custom:unknown')
depends=('python2' 'python2-beautifulsoup4' 'python2-selenium')
makedepends=('git')
source=('git+https://github.com/muhammad-bouabid/Vulnerabilities-spider.git')
sha512sums=('SKIP')

pkgver() {
  cd "$srcdir/Vulnerabilities-spider"

  echo $(git rev-list --count HEAD).$(git rev-parse --short HEAD)
}

package() {
  cd "$srcdir/Vulnerabilities-spider"

  install -dm 755 "$pkgdir/usr/bin"
  install -dm 755 "$pkgdir/usr/share/vulnerabilities-spider"

  install -Dm644 -t "$pkgdir/usr/share/doc/vulnerabilities-spider/" README.txt

  rm -rf README.txt

  cp -a * "$pkgdir/usr/share/vulnerabilities-spider"

  cat > "$pkgdir/usr/bin/vulnerabilities-spider" << EOF
#!/bin/sh
cd /usr/share/vulnerabilities-spider
exec python2 VulnerabilitiesSpider.py "\${@}"
EOF

  chmod a+x "$pkgdir/usr/bin/vulnerabilities-spider"
}
