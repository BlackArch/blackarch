# This file is part of BlackArch Linux ( https://www.blackarch.org/ ).
# See COPYING for license details.

pkgname=python2-boto
_pkgname=boto
pkgver=2.49.0.20190327
_pkgver=2.49.0
pkgrel=2
pkgdesc='Amazon Web Services Library.'
url='https://pypi.org/project/boto/#files'
arch=('any')
license=('MIT')
depends=('python2')
makedepends=('python2-setuptools')
source=("https://files.pythonhosted.org/packages/source/${_pkgname::1}/$_pkgname/$_pkgname-$_pkgver.tar.gz")
sha512sums=('2175cf30cd25bbc05812e83e5ade7668c3e21b1bb09aa1b43f0f0ac7d6967a646394fb52c9be673ebb65618c5b33a52d6f31f6da702f5cd1d6c9a18169476dd4')

build() {
  cd "$_pkgname-$_pkgver"

  python2 setup.py build
}

package() {
  cd "$_pkgname-$_pkgver"

  python2 setup.py install --root="$pkgdir" --prefix=/usr -O1 --skip-build

  for i in "$pkgdir/usr/bin/"*; do
    mv $i "${i}2"
  done
}

