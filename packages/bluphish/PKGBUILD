# This file is part of BlackArch Linux ( http://blackarch.org ).
# See COPYING for license details.

pkgname='bluphish'
pkgver=9.a7200bd
pkgrel=2
pkgdesc='Bluetooth device and service discovery tool that can be used for security assessment and penetration testing.'
groups=('blackarch' 'blackarch-bluetooth')
arch=('any')
url='https://github.com/olivo/BluPhish'
license=('Apache2')
depends=('java-environment')
makedepends=('git')
source=('git+https://github.com/olivo/BluPhish.git')
sha1sums=('SKIP')

pkgver() {
  cd "$srcdir/BluPhish"

  echo $(git rev-list --count HEAD).$(git rev-parse --short HEAD)
}

package() {
  cd "$srcdir/BluPhish"

  mkdir -p "$pkgdir/usr/bin"
  mkdir -p "$pkgdir/usr/share/bluphish"

  install -Dm644 README.md "$pkgdir/usr/share/doc/bluphish/README.md"
  install -Dm644 LICENSE "$pkgdir/usr/share/licenses/bluphish/LICENSE"

  rm README.md LICENSE

  cp -a * "$pkgdir/usr/share/bluphish"

  cat > "$pkgdir/usr/bin/bluphish" << EOF
#!/bin/sh
cd /usr/share/bluphish
exec java -cp .;bluecove-2.1.1-SNAPSHOT.jar BluPhish "\${@}"
EOF

  chmod a+x "$pkgdir/usr/bin/bluphish"
}
