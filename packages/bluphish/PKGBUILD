# This file is part of BlackArch Linux ( https://www.blackarch.org/ ).
# See COPYING for license details.

pkgname=bluphish
pkgver=9.a7200bd
pkgrel=2
pkgdesc='Bluetooth device and service discovery tool that can be used for security assessment and penetration testing.'
groups=('blackarch' 'blackarch-bluetooth')
arch=('any')
url='https://github.com/olivo/BluPhish'
license=('Apache2')
depends=('java-environment')
makedepends=('git')
source=("$pkgname::git+https://github.com/olivo/BluPhish.git")
sha512sums=('SKIP')

pkgver() {
  cd $pkgname

  echo $(git rev-list --count HEAD).$(git rev-parse --short HEAD)
}

package() {
  cd $pkgname

  install -dm 755 "$pkgdir/usr/bin"
  install -dm 755 "$pkgdir/usr/share/$pkgname"

  install -Dm 644 -t "$pkgdir/usr/share/doc/$pkgname/" README.md
  install -Dm 644 LICENSE "$pkgdir/usr/share/licenses/$pkgname/LICENSE"

  rm README.md LICENSE

  cp -a * "$pkgdir/usr/share/$pkgname/"

  cat > "$pkgdir/usr/bin/$pkgname" << EOF
#!/bin/sh
cd /usr/share/$pkgname
exec java -cp .;bluecove-2.1.1-SNAPSHOT.jar BluPhish "\$@"
EOF

  chmod +x "$pkgdir/usr/bin/$pkgname"
}

