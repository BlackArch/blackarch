# This file is part of BlackArch Linux ( https://www.blackarch.org/ ).
# See COPYING for license details.

pkgname=sn1per
pkgver=6.0
pkgrel=1
epoch=1
pkgdesc='Automated Pentest Recon Scanner.'
groups=('blackarch' 'blackarch-automation' 'blackarch-cracker'
        'blackarch-recon' 'blackarch-scanner')
url='https://github.com/1N3/Sn1per'
license=('custom:unknown')
arch=('any')
depends=('amap' 'arachni' 'bash' 'bind-tools' 'brutex' 'cisco-torch' 'curl'
         'cutycapt' 'dnsenum' 'dos2unix' 'enum4linux' 'firefox' 'golismero'
         'goohak' 'hydra' 'metasploit' 'nbtscan' 'nikto' 'nmap' 'php'
         'postgresql' 'python' 'python-colorama' 'python-dnspython'
         'python-paramiko' 'python-requests' 'python-requests-file'
         'python-tldextract' 'python-urllib3' 'ruby' 'ruby-mechanize'
         'ruby-net-http-persistent' 'ruby-rake' 'ruby-ruby-nmap'
         'ruby-text-table' 'smtp-user-enum' 'sqlmap' 'sslscan' 'sslyze'
         'testssl.sh' 'theharvester' 'unzip' 'w3af' 'wapiti' 'whatweb'
         'whois' 'wpscan' 'xprobe2' 'xsstracer')
source=("https://github.com/1N3/Sn1per/archive/v$pkgver.tar.gz")
install='sn1per.install'
sha512sums=('adf3ae69231a88474a6634237d852c20f388fc018c50f441d1555abe2872a756bb8443f526785db90954d9f591dd065ffdbd0b6cb27a94f50d9e43730bbfca1b')

prepare() {
  cd "Sn1per-$pkgver"

  sed -i '/apt-get/d' install.sh
  sed -i '/msfdb/d' install.sh
}

package() {
  cd "Sn1per-$pkgver"

  install -dm 755 "$pkgdir/usr/bin"
  install -dm 755 "$pkgdir/usr/share/sniper"

  install -Dm 644 -t "$pkgdir/usr/share/doc/sniper/" README.md CHANGELOG.md \
    TODO.md

  rm *.md

  cp -a --no-preserve=ownership * "$pkgdir/usr/share/sniper"

  cat > "$pkgdir/usr/bin/sniper" << EOF
#!/bin/sh
cd /usr/share/sniper
exec bash ./sniper "\$@"
EOF

  chmod +x "$pkgdir/usr/bin/sniper"
}

