# This file is part of BlackArch Linux ( https://www.blackarch.org/ ).
# See COPYING for license details.

pkgname=sn1per
pkgver=444.527fb60
pkgrel=1
epoch=1
pkgdesc='Automated Pentest Recon Scanner.'
groups=('blackarch' 'blackarch-automation' 'blackarch-cracker'
        'blackarch-recon' 'blackarch-scanner')
url='https://github.com/1N3/Sn1per'
license=('custom:unknown')
arch=('any')
depends=('amap' 'arachni' 'bash' 'bind-tools' 'brutex' 'cisco-torch' 'curl'
         'cutycapt' 'dnsenum' 'dos2unix' 'enum4linux' 'firefox' 'golismero'
         'goohak' 'hydra' 'metasploit' 'nbtscan' 'nikto' 'nmap' 'php'
         'postgresql' 'python' 'python-colorama' 'python-dnspython'
         'python-paramiko' 'python-requests' 'python-requests-file'
         'python-tldextract' 'python-urllib3' 'ruby' 'ruby-mechanize'
         'ruby-net-http-persistent' 'ruby-rake' 'ruby-ruby-nmap'
         'ruby-text-table' 'smtp-user-enum' 'sqlmap' 'sslscan' 'sslyze'
         'testssl.sh' 'theharvester' 'unzip' 'w3af' 'wapiti' 'whatweb'
         'whois' 'wpscan' 'xprobe2' 'xsstracer')
makedepends=('git')
source=("$pkgname::git+https://github.com/1N3/Sn1per.git")
sha512sums=('SKIP')
install="$pkgname.install"

pkgver() {
  cd $pkgname

  echo $(git rev-list --count HEAD).$(git rev-parse --short HEAD)
}

prepare() {
  cd $pkgname

  sed -i '/apt-get/d' install.sh
  sed -i '/msfdb/d' install.sh
}

package() {
  cd $pkgname

  install -dm 755 "$pkgdir/usr/bin"
  install -dm 755 "$pkgdir/usr/share/$pkgname"

  install -Dm 644 -t "$pkgdir/usr/share/doc/$pkgname/" README.md CHANGELOG.md

  rm *.md

  cp -a --no-preserve=ownership * "$pkgdir/usr/share/$pkgname/"

  cat > "$pkgdir/usr/bin/$pkgname" << EOF
#!/bin/sh
cd /usr/share/$pkgname
exec bash ./$pkgname "\$@"
EOF

  chmod +x "$pkgdir/usr/bin/$pkgname"
}

