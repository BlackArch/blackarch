# This file is part of BlackArch Linux ( http://blackarch.org ).
# See COPYING for license details.

pkgname='sn1per'
pkgver=210.1ea0c2d
pkgrel=1
pkgdesc='Automated Pentest Recon Scanner.'
groups=('blackarch' 'blackarch-recon' 'blackarch-automation' 'blackarch-scanner'
        'blackarch-cracker')
url='https://github.com/1N3/Sn1per'
license=('custom:unknown')
arch=('any')
depends=('whois' 'theharvester' 'dnsenum' 'curl' 'nmap' 'php' 'w3af' 'amap'
         'wapiti' 'hydra' 'firefox' 'wpscan' 'sqlmap' 'arachni' 'golismero'
         'nbtscan' 'enum4linux' 'cisco-torch' 'metasploit' 'nikto' 'bind-tools'
         'smtp-user-enum' 'whatweb' 'python' 'sslscan' 'bash' 'testssl' 'sslyze'
         'python2' 'dos2unix' 'xsstracer' 'ruby' 'cutycapt' 'postgresql'
         'xprobe2' 'unzip' 'brutex' 'goohak')
makedepends=('git')
source=('git+https://github.com/1N3/Sn1per.git')
install='sn1per.install'
sha1sums=('SKIP')

pkgver() {
  cd "$srcdir/Sn1per"

  echo $(git rev-list --count HEAD).$(git rev-parse --short HEAD)
}

prepare() {
  cd "$srcdir/Sn1per"

  sed -i '/apt-get install/d' install.sh
}

package() {
  cd "$srcdir/Sn1per"

  mkdir -p "$pkgdir/usr/bin"
  mkdir -p "$pkgdir/usr/share/sniper"

  install -Dm644 -t "$pkgdir/usr/share/doc/sniper/" README.md CHANGELOG.md \
    TODO.md

  rm *.md

  cp -a * "$pkgdir/usr/share/sniper"

  cat > "$pkgdir/usr/bin/sn1per" << EOF
#!/bin/sh
cd /usr/share/sniper
exec bash ./sniper "\${@}"
EOF

  chmod a+x "$pkgdir/usr/bin/sn1per"
}
