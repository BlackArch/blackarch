# This file is part of BlackArch Linux ( https://www.blackarch.org/ ).
# See COPYING for license details.

pkgbase=python-python-pptx
pkgname=('python2-python-pptx' 'python-python-pptx')
_pkgname=python-pptx
pkgver=0.6.21
pkgrel=1
pkgdesc='Generate and manipulate Open XML PowerPoint (.pptx) files.'
arch=('any')
url='https://pypi.org/project/python-pptx/#files'
license=('MIT')
makedepends=('python2-setuptools' 'python-setuptools')
source=("https://files.pythonhosted.org/packages/eb/c3/bd8f2316a790291ef5aa5225c740fa60e2cf754376e90cb1a44fde056830/$_pkgname-$pkgver.tar.gz")
sha512sums=('8da10dcfe8256ffac89f58d0ba599217b3b6071e83098b70e915da49859f697902d160a43c1cdf687271e5d87bd3b66484bb0fe97bc1598882a7f59642f5762b')

prepare() {
  cp -a "$_pkgname-$pkgver"{,-2}
}

build() {
  cd "$_pkgname-$pkgver-2"

  python2 setup.py build

  cd "$srcdir/$_pkgname-$pkgver"

  python setup.py build
}

package_python2-python-pptx() {
  depends=('python2' 'python2-lxml' 'python2-pillow6' 'python2-xlsxwriter')

  cd "$_pkgname-$pkgver-2"

  python2 setup.py install --prefix=/usr --root="$pkgdir" -O1 --skip-build
}

package_python-python-pptx() {
  depends=('python' 'python-lxml' 'python-pillow' 'python-xlsxwriter')

  cd "$_pkgname-$pkgver"

  python setup.py install --prefix=/usr --root="$pkgdir" -O1 --skip-build
}

