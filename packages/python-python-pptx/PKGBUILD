# This file is part of BlackArch Linux ( https://www.blackarch.org/ ).
# See COPYING for license details.

pkgbase=python-python-pptx
pkgname=('python2-python-pptx' 'python-python-pptx')
_pkgname=python-pptx
pkgver=0.6.18
pkgrel=2
pkgdesc='Generate and manipulate Open XML PowerPoint (.pptx) files.'
arch=('any')
url='https://pypi.org/project/python-pptx/#files'
license=('MIT')
makedepends=('python2-setuptools' 'python-setuptools')
source=("https://files.pythonhosted.org/packages/bf/86/eb979f7b0333ec769041aae36df8b9f1bd8bea5bbad44620663890dce561/$_pkgname-$pkgver.tar.gz")
sha512sums=('0bd5bd8ce9c8b54884db65a0e7f6efe737d79a8d2f20c5d2d13eaca9c4f5432d47dbaa927c09f7afa8248f2c779ddc311814573f673f0c60b58ff12bd9d2d65c')

prepare() {
  cp -a "$_pkgname-$pkgver"{,-2}
}

build() {
  cd "$_pkgname-$pkgver-2"

  python2 setup.py build

  cd "$srcdir/$_pkgname-$pkgver"

  python setup.py build
}

package_python2-python-pptx() {
  depends=('python2' 'python2-lxml' 'python2-pillow' 'python2-xlsxwriter')

  cd "$_pkgname-$pkgver-2"

  python2 setup.py install --prefix=/usr --root="$pkgdir" -O1 --skip-build
}

package_python-python-pptx() {
  depends=('python' 'python-lxml' 'python-pillow' 'python-xlsxwriter')

  cd "$_pkgname-$pkgver"

  python setup.py install --prefix=/usr --root="$pkgdir" -O1 --skip-build
}

