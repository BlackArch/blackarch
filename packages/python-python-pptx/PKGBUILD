# This file is part of BlackArch Linux ( https://www.blackarch.org/ ).
# See COPYING for license details.

pkgbase=python-python-pptx
pkgname=('python2-python-pptx' 'python-python-pptx')
_pkgname=python-pptx
pkgver=0.6.19
pkgrel=1
pkgdesc='Generate and manipulate Open XML PowerPoint (.pptx) files.'
arch=('any')
url='https://pypi.org/project/python-pptx/#files'
license=('MIT')
makedepends=('python2-setuptools' 'python-setuptools')
source=("https://files.pythonhosted.org/packages/53/ed/547be9730350509253bc7d76631a8ffcd1a62dda4d7482fb25d369696e37/$_pkgname-$pkgver.tar.gz")
sha512sums=('8f193d96834984d2a5c972bc4e35f779296a1a85d219252d3eda9c2f852b1716d05ed37802007585401fbf03c33eac934575a40e919022e1505890905309467e')

prepare() {
  cp -a "$_pkgname-$pkgver"{,-2}
}

build() {
  cd "$_pkgname-$pkgver-2"

  python2 setup.py build

  cd "$srcdir/$_pkgname-$pkgver"

  python setup.py build
}

package_python2-python-pptx() {
  depends=('python2' 'python2-lxml' 'python2-pillow6' 'python2-xlsxwriter')

  cd "$_pkgname-$pkgver-2"

  python2 setup.py install --prefix=/usr --root="$pkgdir" -O1 --skip-build
}

package_python-python-pptx() {
  depends=('python' 'python-lxml' 'python-pillow' 'python-xlsxwriter')

  cd "$_pkgname-$pkgver"

  python setup.py install --prefix=/usr --root="$pkgdir" -O1 --skip-build
}

