# TODO: make package function
# This file is part of BlackArch Linux ( https://www.blackarch.org/ ).
# See COPYING for license details.

pkgname=rubygems1.8
pkgver=2.4.8
pkgrel=2
pkgdesc='A package management framework for the Ruby programming language.'
arch=('any')
url='http://docs.rubygems.org/'
license=('GPL')
depends=('ruby1.8')
source=("https://rubygems.org/rubygems/rubygems-$pkgver.tgz")
provides=('rubygems')
sha512sums=('1cc2e9e19d665268f475ce90547377d41a4ea5ce9a9afab4979de0247a0dd82b04529c087c899d9ced5564fa9051e1d2fe23ddb764ba411915bab2ce344ac419')

package() {
  cd "rubygems-$pkgver"

  export RUBYOPT=
  export GEM_HOME=$pkgdir/usr/share/ruby1.8/lib/ruby/gems/1.8

  ruby-1.8 setup.rb --prefix="$pkgdir/usr/share/ruby1.8"

  install -dm 755 "$pkgdir/usr/share/ruby1.8/lib/ruby/site_ruby/1.8"

  cd "$pkgdir/usr/share/ruby1.8/lib"

  mv rubygems rubygems.rb ubygems.rb \
    "$pkgdir/usr/share/ruby1.8/lib/ruby/site_ruby/1.8"

  sed -ri 's|#!.+$|#!/usr/bin/ruby-1.8|' "$pkgdir/usr/share/ruby1.8/bin/gem"

  install -dm 755 $pkgdir/usr/bin/
  ln -s /usr/share/ruby1.8/bin/gem "$pkgdir/usr/bin/gem-1.8"
}

