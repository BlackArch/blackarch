# This file is part of BlackArch Linux ( https://www.blackarch.org/ ).
# See COPYING for license details.

# Contributor: Giovanni Scafora <giovanni@archlinux.org>
# Contributor: Simon Sapin <simon dot sapin at exyr dot org>
# Contributor: Michal Marek <reqamst at gmail dot com>
# Contributor: Rick W. Chena <stuffcorpse@archlinux.us>
# Contributor: Andrea Fagiani <andfagiani _at_ gmail dot com>

pkgname=python-cssutils
_pkgname=cssutils
pkgver=1.0.2
pkgrel=7
pkgdesc='A CSS Cascading Style Sheets library for Python.'
arch=('any')
url='http://cthedot.de/cssutils/'
license=('LGPL3')
depends=('python')
makedepends=('python-setuptools')
checkdepends=('python-pbr')
source=("https://files.pythonhosted.org/packages/source/c/cssutils/$_pkgname-${pkgver}.tar.gz")
sha512sums=('4988893875a03923d3127e334654799e5c2ebcdc0a7f5b3fb5c9ab27d1b331caa5bf197161312a298531e0757843aa3259ceba5fa19947353eea24a5da53ceda')

prepare() {
  cd "$_pkgname-$pkgver"

  # don't pin old versions of testrunner
  sed -i "/tests_require/{s/pbr < 1\.7\.0/pbr/;s/'mock',//}" setup.py
  sed -i 's/import mock/from unittest import mock/' src/cssutils/tests/*.py
}

build() {
  cd "$_pkgname-$pkgver"

  2to3 --no-diffs -nw src

  python setup.py build
}

package() {
  cd "$_pkgname-$pkgver"

  python setup.py install --root="$pkgdir" --prefix=/usr -O1 --skip-build
}

