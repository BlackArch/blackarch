# This file is part of BlackArch Linux ( https://www.blackarch.org/ ).
# See COPYING for license details.

# Contributor: Giovanni Scafora <giovanni@archlinux.org>
# Contributor: Simon Sapin <simon dot sapin at exyr dot org>
# Contributor: Michal Marek <reqamst at gmail dot com>
# Contributor: Rick W. Chena <stuffcorpse@archlinux.us>
# Contributor: Andrea Fagiani <andfagiani _at_ gmail dot com>

pkgname=python-cssutils
_pkgname=cssutils
pkgver=2.4.1
pkgrel=1
pkgdesc='A CSS Cascading Style Sheets library for Python.'
arch=('any')
url='http://cthedot.de/cssutils/'
license=('LGPL3')
depends=('python')
makedepends=('python-setuptools' 'python-wheel' 'python-pip')
source=("https://files.pythonhosted.org/packages/f9/92/6e77b75d921d1317650bf9cfec8428f513c8b4eb7fe90931aee72f694c7d/$_pkgname-$pkgver.tar.gz")
sha512sums=('f1107a761f20204c214a09647dde3d2e1900ccbe71ecca332049441f3c8916714efaece2b702167326fde016bd1817a76a4fffa6cd4fda9f91359d0a055fe7a3')

prepare() {
  cd "$_pkgname-$pkgver"

  # don't pin old versions of testrunner
  sed -i "/tests_require/{s/pbr < 1\.7\.0/pbr/;s/'mock',//}" setup.py
  #sed -i 's/import mock/from unittest import mock/' src/cssutils/tests/*.py
}

build() {
  cd "$_pkgname-$pkgver"

  #2to3 --no-diffs -nw src

  python setup.py build
}

package() {
  cd "$_pkgname-$pkgver"

  python setup.py install --root="$pkgdir" --prefix=/usr -O1 --skip-build
}

