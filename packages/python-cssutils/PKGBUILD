# This file is part of BlackArch Linux ( https://www.blackarch.org/ ).
# See COPYING for license details.

# Contributor: Giovanni Scafora <giovanni@archlinux.org>
# Contributor: Simon Sapin <simon dot sapin at exyr dot org>
# Contributor: Michal Marek <reqamst at gmail dot com>
# Contributor: Rick W. Chena <stuffcorpse@archlinux.us>
# Contributor: Andrea Fagiani <andfagiani _at_ gmail dot com>

pkgname=python-cssutils
_pkgname=cssutils
pkgver=2.2.0
pkgrel=1
pkgdesc='A CSS Cascading Style Sheets library for Python.'
arch=('any')
url='http://cthedot.de/cssutils/'
license=('LGPL3')
depends=('python')
makedepends=('python-setuptools')
checkdepends=('python-pbr')
source=("https://files.pythonhosted.org/packages/ef/45/05d706ac7c52dcf776960960ce60181f23a1c3bbc2527aeaf6650d351fee/$_pkgname-$pkgver.tar.gz")
sha512sums=('cd9c3d069e18516484894df38f8cbc9abb1ffc5b021538c99c06878d34195619d7928d553049309ac30c144210df6ed616bbd0d04e158d9fbfce166f9935d5a2')

prepare() {
  cd "$_pkgname-$pkgver"

  # don't pin old versions of testrunner
  sed -i "/tests_require/{s/pbr < 1\.7\.0/pbr/;s/'mock',//}" setup.py
  sed -i 's/import mock/from unittest import mock/' src/cssutils/tests/*.py
}

build() {
  cd "$_pkgname-$pkgver"

  2to3 --no-diffs -nw src

  python setup.py build
}

package() {
  cd "$_pkgname-$pkgver"

  python setup.py install --root="$pkgdir" --prefix=/usr -O1 --skip-build
}

