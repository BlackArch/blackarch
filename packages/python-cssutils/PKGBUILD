# This file is part of BlackArch Linux ( https://www.blackarch.org/ ).
# See COPYING for license details.

# Contributor: Giovanni Scafora <giovanni@archlinux.org>
# Contributor: Simon Sapin <simon dot sapin at exyr dot org>
# Contributor: Michal Marek <reqamst at gmail dot com>
# Contributor: Rick W. Chena <stuffcorpse@archlinux.us>
# Contributor: Andrea Fagiani <andfagiani _at_ gmail dot com>

pkgname=python-cssutils
_pkgname=cssutils
pkgver=2.4.0
pkgrel=1
pkgdesc='A CSS Cascading Style Sheets library for Python.'
arch=('any')
url='http://cthedot.de/cssutils/'
license=('LGPL3')
depends=('python')
makedepends=('python-setuptools' 'python-wheel' 'python-pip')
source=("https://files.pythonhosted.org/packages/f4/ea/d09b04b5cf6ad84f45ca4c523d3e153dd9be1f12650c825d1c92ca0618a2/$_pkgname-$pkgver.tar.gz")
sha512sums=('53e18f9d06905dbda5382a3ed6ef1808571e8a32204864364b0a344e641c774f0998bf23ad0997b34ddbe9f04d3d2c7d622249cfeb0a9e846f7fb1530ae9b12b')

prepare() {
  cd "$_pkgname-$pkgver"

  # don't pin old versions of testrunner
  sed -i "/tests_require/{s/pbr < 1\.7\.0/pbr/;s/'mock',//}" setup.py
  #sed -i 's/import mock/from unittest import mock/' src/cssutils/tests/*.py
}

build() {
  cd "$_pkgname-$pkgver"

  #2to3 --no-diffs -nw src

  python setup.py build
}

package() {
  cd "$_pkgname-$pkgver"

  python setup.py install --root="$pkgdir" --prefix=/usr -O1 --skip-build
}

