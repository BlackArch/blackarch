# This file is part of BlackArch Linux ( http://blackarch.org ).
# See COPYING for license details.

pkgname=sniff-probe-req
pkgver=146.c3a8621
pkgrel=1
pkgdesc='Wi-Fi Probe Requests Sniffer.'
groups=('blackarch' 'blackarch-wireless' 'blackarch-sniffer')
arch=('any')
url='https://github.com/SkypLabs/sniff-probe-req'
license=('GPL3')
depends=('python' 'python-argparse' 'python-netaddr' 'python-scapy')
makedepends=('git' 'python-setuptools')
source=('git+https://github.com/SkypLabs/sniff-probe-req.git')
sha512sums=('SKIP')

pkgver() {
  cd "$srcdir/sniff-probe-req"

  echo $(git rev-list --count HEAD).$(git rev-parse --short HEAD)
}

build() {
  cd "$srcdir/sniff-probe-req"

  python3 setup.py build
}

package() {
  cd "$srcdir/sniff-probe-req"

  python3 setup.py install --root="$pkgdir" --prefix=/usr

  install -Dm644 -t "$pkgdir/usr/share/doc/sniff-probe-req/" README.rst docs/*.rst
  install -Dm644 LICENSE "$pkgdir/usr/share/licenses/sniff-probe-req/LICENSE"

  rm -f "$pkgdir/usr/lib/python3.7/site-packages/test/__init__.py"
  rm -f "$pkgdir/usr/lib/python3.7/site-packages/test/__pycache__/__init__.cpython-37.pyc"
}
