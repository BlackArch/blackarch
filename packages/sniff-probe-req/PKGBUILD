# This file is part of BlackArch Linux ( https://www.blackarch.org/ ).
# See COPYING for license details.

pkgname=sniff-probe-req
_pkgname=probequest
pkgver=v0.8.0.r29.g0dacf88
_pyver=3.14
pkgrel=1
epoch=1
pkgdesc='Wi-Fi Probe Requests Sniffer.'
groups=('blackarch' 'blackarch-wireless' 'blackarch-sniffer')
arch=('any')
url='https://github.com/SkypLabs/sniff-probe-req'
license=('GPL-3.0-or-later')
depends=('python' 'python-netaddr' 'python-scapy')
makedepends=('git' 'python-pip' 'python-build' 'python-wheel')
source=("git+https://github.com/SkypLabs/$pkgname.git")
sha512sums=('SKIP')

pkgver() {
  cd $pkgname

  ( set -o pipefail
    git describe --long --tags --abbrev=7 2>/dev/null |
      sed 's/\([^-]*-g\)/r\1/;s/-/./g' ||
    printf "%s.%s" "$(git rev-list --count HEAD)" \
      "$(git rev-parse --short=7 HEAD)"
  )
}

build() {
  cd $pkgname

  python -m build --wheel --outdir="$startdir/dist"
}

package() {
  cd $pkgname

  pip install \
    --verbose \
    --disable-pip-version-check \
    --no-warn-script-location \
    --ignore-installed \
    --no-compile \
    --no-deps \
    --root="$pkgdir" \
    --prefix=/usr \
    --no-index \
    --find-links="file://$startdir/dist" \
    $_pkgname

  install -Dm 644 -t "$pkgdir/usr/share/doc/$pkgname/" README.rst docs/*.rst
  install -Dm 644 LICENSE "$pkgdir/usr/share/licenses/$pkgname/LICENSE"

  rm -rf "$pkgdir/usr/lib/python$_pyver/site-packages/tests"
}

