# This file is part of BlackArch Linux ( https://www.blackarch.org/ ).
# See COPYING for license details.

pkgbase=python-pywebview
pkgname=('python2-pywebview' 'python-pywebview')
_pkgname=pywebview
pkgver=3.3.2
pkgrel=1
pkgdesc='A cross-platform lightweight native wrapper around a web view component.'
arch=('any')
url='https://pypi.org/project/pywebview/#files'
license=('BSD')
makedepends=('python2-setuptools' 'python-setuptools')
source=("https://files.pythonhosted.org/packages/3c/70/962845b1449cf026575d1983e3304be6c9885b2f976e6b4b6e333847d95d/$_pkgname-$pkgver.tar.gz")
sha512sums=('7605c21ddb62696631db607499cd75473048e928e067ce16f32f395b02ca51d3e885917c8b4daa3db6bcd192d477965b4dccdbe43fbcb04d9185d8f0c32b2216')

prepare() {
  cp -a "$_pkgname-$pkgver"{,-2}
}

build() {
  cd "$_pkgname-$pkgver-2"

  python2 setup.py build

  cd "$srcdir/$_pkgname-$pkgver"

  python setup.py build
}

package_python2-pywebview() {
  depends=('python2')

  cd "$_pkgname-$pkgver-2"

  python2 setup.py install --prefix=/usr --root="$pkgdir" --optimize=1 \
    --skip-build
}

package_python-pywebview() {
  depends=('python')

  cd "$_pkgname-$pkgver"

  python setup.py install --prefix=/usr --root="$pkgdir" --optimize=1 \
    --skip-build
}

