# This file is part of BlackArch Linux ( https://www.blackarch.org/ ).
# See COPYING for license details.

pkgbase=python-pywebview
pkgname=('python2-pywebview' 'python-pywebview')
_pkgname=pywebview
pkgver=3.3.4
pkgrel=1
pkgdesc='A cross-platform lightweight native wrapper around a web view component.'
arch=('any')
url='https://pypi.org/project/pywebview/#files'
license=('BSD')
makedepends=('python2-setuptools' 'python-setuptools')
source=("https://files.pythonhosted.org/packages/48/31/ba27ef506581a2eb9ce1623f9c0bda8e3e732d2bcb034e79449b3b9196b2/$_pkgname-$pkgver.tar.gz")
sha512sums=('21e6308d50674e9bbc5acbbcccebde58a785e3625bd145208b99f0ef374c3374e39852026c5acd5509a6ce5044f82f72db2bd3ad692b0d7684667525cbef763c')

prepare() {
  cp -a "$_pkgname-$pkgver"{,-2}
}

build() {
  cd "$_pkgname-$pkgver-2"

  python2 setup.py build

  cd "$srcdir/$_pkgname-$pkgver"

  python setup.py build
}

package_python2-pywebview() {
  depends=('python2')

  cd "$_pkgname-$pkgver-2"

  python2 setup.py install --prefix=/usr --root="$pkgdir" --optimize=1 \
    --skip-build
}

package_python-pywebview() {
  depends=('python')

  cd "$_pkgname-$pkgver"

  python setup.py install --prefix=/usr --root="$pkgdir" --optimize=1 \
    --skip-build
}

