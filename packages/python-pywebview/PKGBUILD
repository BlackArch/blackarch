# This file is part of BlackArch Linux ( https://www.blackarch.org/ ).
# See COPYING for license details.

pkgbase=python-pywebview
pkgname=('python2-pywebview' 'python-pywebview')
_pkgname=pywebview
pkgver=3.4
pkgrel=1
pkgdesc='A cross-platform lightweight native wrapper around a web view component.'
arch=('any')
url='https://pypi.org/project/pywebview/#files'
license=('BSD')
makedepends=('python2-setuptools' 'python-setuptools')
source=("https://files.pythonhosted.org/packages/8c/98/f2b4f38e23f74b5b57ba2576ce9a16c47a2627f8e92a407d4d37567032c8/$_pkgname-$pkgver.tar.gz")
sha512sums=('a30c8c726e1351f3a5cc96c2774246a0a6326e8b6a2e9c25fe6e3bb7d866ff0c7cbd16da0428f6891c8316eafc70fd96c13255515b63d907ef20459a6cabd70a')

prepare() {
  cp -a "$_pkgname-$pkgver"{,-2}
}

build() {
  cd "$_pkgname-$pkgver-2"

  python2 setup.py build

  cd "$srcdir/$_pkgname-$pkgver"

  python setup.py build
}

package_python2-pywebview() {
  depends=('python2')

  cd "$_pkgname-$pkgver-2"

  python2 setup.py install --prefix=/usr --root="$pkgdir" --optimize=1 \
    --skip-build
}

package_python-pywebview() {
  depends=('python')

  cd "$_pkgname-$pkgver"

  python setup.py install --prefix=/usr --root="$pkgdir" --optimize=1 \
    --skip-build
}

