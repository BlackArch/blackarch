# This file is part of BlackArch Linux ( https://www.blackarch.org/ ).
# See COPYING for license details.

pkgbase=python-pywebview
pkgname=('python2-pywebview' 'python-pywebview')
_pkgname=pywebview
pkgver=5.1
pkgrel=1
pkgdesc='A cross-platform lightweight native wrapper around a web view component.'
arch=('any')
url='https://pypi.org/project/pywebview/#files'
license=('BSD')
makedepends=('python2-setuptools' 'python-setuptools')
source=("https://files.pythonhosted.org/packages/source/${_pkgname::1}/$_pkgname/$_pkgname-$pkgver.tar.gz")
sha512sums=('395243c34ce147970ac351bade647ad1c57d844fee390764097b7ec7f02562d298e9239eb76d0606394419e1e06d6c28d2cd58f1f10d46c5a4924759dac87aa0')

prepare() {
  cp -a "$_pkgname-$pkgver"{,-2}
}

build() {
  cd "$_pkgname-$pkgver-2"

  python2 setup.py build

  cd "$srcdir/$_pkgname-$pkgver"

  python setup.py build
}

package_python2-pywebview() {
  depends=('python2')

  cd "$_pkgname-$pkgver-2"

  python2 setup.py install --root="$pkgdir" --prefix=/usr -O1 --skip-build
}

package_python-pywebview() {
  depends=('python')

  cd "$_pkgname-$pkgver"

  python setup.py install --root="$pkgdir" --prefix=/usr -O1 --skip-build
}

