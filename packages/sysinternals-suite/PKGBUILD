# This file is part of BlackArch Linux ( http://blackarch.org ).
# See COPYING for license details.

pkgname=sysinternals-suite
pkgver=2.8
pkgrel=2
epoch=1
groups=('blackarch' 'blackarch-windows')
pkgdesc='Sysinternals tools suite.'
url='http://sysinternals.com/'
license=('sysinternals')
arch=('any')
source=('http://download.sysinternals.com/files/SysinternalsSuite.zip')
sha512sums=('60e6880f8d9c0459ca2b743e7af057f9609dca60fe160fa8abca2c5218785a8a4bc5592c1936248a366caf09a8f4b2cde0a12570dcd89cc2fe3a6f012cfae5d5')

package() {
  cd "$srcdir"

  install -dm 755 "$pkgdir/opt/windows/archive/sysinternals-suite"

  find . -type l -exec rm {} \;

  # Transform all files to lowercase
  for f in *
  do
    lc=$(echo $f | tr '[A-Z]' '[a-z]')

    if [ ! -f $f -o -f $lc ]; then
      continue
    fi

    mv -i $f $lc
  done

  install -m755 *.exe *.sys "$pkgdir/opt/windows/archive/sysinternals-suite"
  install -m644 *.chm *.hlp *.txt "$pkgdir/opt/windows/archive/sysinternals-suite"
}
