# This file is part of BlackArch Linux ( https://www.blackarch.org/ ).
# See COPYING for license details.

pkgname=sysinternals-suite
pkgver=6.1
pkgrel=1
epoch=1
groups=('blackarch' 'blackarch-windows')
pkgdesc='Sysinternals tools suite.'
url='http://sysinternals.com/'
license=('custom:unknown')
arch=('any')
source=('http://download.sysinternals.com/files/SysinternalsSuite.zip')
sha512sums=('cffcf909c8bd326412f29381b36c84228be07a52e1e6a9c2e5cff237ac82f3337b5fbcb6261d53803c49da9d27e8e4564bf3b0d0d522520fcd984f7cd58f417a')

package() {
  install -dm 755 "$pkgdir/usr/share/windows/$pkgname"

  find . -type l -exec rm {} \;

  # Transform all files to lowercase
  for f in * ; do
    lc=$(echo $f | tr '[A-Z]' '[a-z]')

    if [ ! -f $f -o -f $lc ]; then
      continue
    fi

    mv -i $f $lc
  done

  install -m 755 *.exe *.sys "$pkgdir/usr/share/windows/$pkgname"
  install -m 644 *.chm *.txt "$pkgdir/usr/share/windows/$pkgname"
}

