# This file is part of BlackArch Linux ( http://blackarch.org ).
# See COPYING for license details.

pkgname='python2-pygraphviz'
pkgver='1.3rc2'
pkgrel=1
pkgdesc='PyGraphviz is a Python interface to the Graphviz graph layout and visualization package.'
arch=('i686' 'x86_64' 'armv6h' 'armv7h')
license=('LGPL')
url="https://networkx.lanl.gov/pygraphviz"
depends=('python2>=2.4' 'graphviz')
conflicts=('pygraphviz' 'python-pygraphviz')
replaces=('pygraphviz' 'python-pygraphviz')
provides=('pygraphviz' 'python-pygraphviz')
source=("http://pypi.python.org/packages/source/p/pygraphviz/pygraphviz-${pkgver}.tar.gz")
sha1sums=('fae5e5a115859b72c4f612c42ff61799ae268786')

build() {
  cd "$srcdir/pygraphviz-$pkgver"

  python2 setup.py build
}

package() {
  cd "$srcdir/pygraphviz-$pkgver"

  python2 setup.py install --prefix=/usr --root="$pkgdir" --optimize=1

  # Patch to use python2
  find "$pkgdir" -type f \( -name '*.py' -or -executable \) -exec \
    sed -i -e 's|#![ ]*/usr/bin/python$|#!/usr/bin/python2|' \
           -e 's|#![ ]*/usr/bin/env python$|#!/usr/bin/env python2|' \
    {} \;
}
