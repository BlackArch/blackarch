# This file is part of BlackArch Linux ( http://blackarch.org ).
# See COPYING for license details.

pkgname=python2-pygraphviz
pkgver=1.4rc1
pkgrel=2
pkgdesc='PyGraphviz is a Python interface to the Graphviz graph layout and visualization package.'
arch=('x86_64' 'armv6h' 'armv7h' 'aarch64')
license=('LGPL')
url="https://networkx.lanl.gov/pygraphviz"
depends=('python2>=2.4' 'graphviz')
conflicts=('pygraphviz' 'python-pygraphviz')
replaces=('pygraphviz' 'python-pygraphviz')
provides=('pygraphviz' 'python-pygraphviz')
makedepends=('python2-setuptools')
source=("https://pypi.python.org/packages/25/b1/e44c51b47054ad88aadbe9edcf344bf9b3c61d2d6d15719180ee4d130bcd/pygraphviz-${pkgver}.tar.gz")
sha512sums=('401d1d159b8aa2bf017b1c9159f8f58ab999de22785df75aced728835c2ac8d9cb17ad6d81e4b0890fc2a7e9cb767da4cf8d11151f82269ba50d1fc4c0ae9400')

build() {
  cd "$srcdir/pygraphviz-$pkgver"

  python2 setup.py build
}

package() {
  cd "$srcdir/pygraphviz-$pkgver"

  python2 setup.py install --prefix=/usr --root="$pkgdir" --optimize=1

  # Patch to use python2
  find "$pkgdir" -type f \( -name '*.py' -or -executable \) -exec \
    sed -i -e 's|#![ ]*/usr/bin/python$|#!/usr/bin/python2|' \
           -e 's|#![ ]*/usr/bin/env python$|#!/usr/bin/env python2|' \
    {} \;
}
