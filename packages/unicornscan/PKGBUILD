# This file is part of BlackArch Linux ( https://www.blackarch.org/ ).
# See COPYING for license details.

pkgname=unicornscan
pkgver=v0.4.10.r2.g0d94296
pkgrel=1
pkgdesc='A new information gathering and correlation engine.'
arch=('x86_64' 'aarch64')
groups=('blackarch' 'blackarch-scanner')
url='https://github.com/robertelee78/unicornscan'
license=('GPL-2.0-or-later')
depends=('libpcap' 'postgresql-libs' 'geoip' 'libdnet' 'libltdl')
makedepends=('git' 'flex' 'bison')
source=("git+https://github.com/D3vil0p3r/$pkgname.git#branch=patch-2")
sha512sums=('SKIP')

pkgver() {
  cd $pkgname

  ( set -o pipefail
    git describe --long --tags --abbrev=7 2>/dev/null |
      sed 's/\([^-]*-g\)/r\1/;s/-/./g' ||
    printf "%s.%s" "$(git rev-list --count HEAD)" \
      "$(git rev-parse --short=7 HEAD)"
  )
}

prepare() {
  cd "$pkgname"

  if [[ "$CARCH" == "i686" || "$CARCH" == "x86_64" ]]; then
    ./configure CFLAGS="$CFLAGS -D_GNU_SOURCE" LDFLAGS="-lGeoIP" --prefix=/usr \
      --sysconfdir=/etc --localstatedir=/var --with-pgsql
  else
    ./configure CFLAGS="$CFLAGS -D_GNU_SOURCE" LDFLAGS="-lGeoIP" --prefix=/usr \
      --sysconfdir=/etc --localstatedir=/var --with-pgsql --build=arm
  fi
}

build() {
  cd "$pkgname"

  make
}

package() {
  cd "$pkgname"

  make DESTDIR="$pkgdir" install
}

