pkgname=unicornscan
pkgver=0.4.7
pkgrel=7
groups=('blackarch' 'blackarch-scanner')
pkgdesc='A new information gathering and correlation engine.'
url='http://www.unicornscan.org/'
depends=('libpcap' 'postgresql-libs' 'geoip' 'libdnet' 'libltdl')
makedepends=('flex' 'bison')
license=('GPL')
arch=('i686' 'x86_64' 'armv6h' 'armv7h')
source=("http://www.unicornscan.org/releases/unicornscan-$pkgver-2.tar.bz2")
md5sums=('4c5f272eb38c333c0094c32317edf758')

build() {
	cd "$srcdir/unicornscan-$pkgver"

	# prevent crashing geoip dependency
	export LDFLAGS="${LDFLAGS/--as-needed,/}"

	./configure CFLAGS="$CFLAGS -D_GNU_SOURCE" --prefix=/usr --sysconfdir=/etc \
		--localstatedir=/var --with-pgsql --with-geoip
	make
}

package() {
	cd "$srcdir/unicornscan-$pkgver"

  make "DESTDIR=$pkgdir" install

	cd "$pkgdir/etc/unicornscan"
	ln -s /usr/share/GeoIP/GeoIP.dat GeoIP.dat
}
