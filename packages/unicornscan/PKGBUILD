# This file is part of BlackArch Linux ( http://blackarch.org ).
# See COPYING for license details.

pkgname='unicornscan'
pkgver='0.4.7'
pkgrel=8
groups=('blackarch' 'blackarch-scanner')
pkgdesc='A new information gathering and correlation engine.'
url='http://www.unicornscan.org/'
depends=('libpcap' 'postgresql-libs' 'geoip' 'libdnet' 'libltdl')
makedepends=('flex' 'bison')
license=('GPL')
arch=('x86_64' 'armv6h' 'armv7h' 'aarch64')
source=("http://downloads.sourceforge.net/project/osace/unicornscan/unicornscan%20-%200.4.7%20source/unicornscan-${pkgver}-2.tar.bz2")
sha1sums=('5361150afa999e68076a453072830dd23dd9bfee')

build() {
  cd "$srcdir/unicornscan-$pkgver"

  # prevent crashing geoip dependency
  export LDFLAGS="${LDFLAGS/--as-needed,/}"

  if [[ "$CARCH" == "i686" || "$CARCH" == "x86_64" ]];
  then
    ./configure CFLAGS="$CFLAGS -D_GNU_SOURCE" --prefix=/usr --sysconfdir=/etc \
      --localstatedir=/var --with-pgsql --with-geoip
  else
    ./configure CFLAGS="$CFLAGS -D_GNU_SOURCE" --prefix=/usr --sysconfdir=/etc \
      --localstatedir=/var --with-pgsql --with-geoip --build=arm
  fi

  make
}

package() {
  cd "$srcdir/unicornscan-$pkgver"

  make "DESTDIR=$pkgdir" install

  cd "$pkgdir/etc/unicornscan"
  ln -s /usr/share/GeoIP/GeoIP.dat GeoIP.dat
}
