# This file is part of BlackArch Linux ( https://www.blackarch.org/ ).
# See COPYING for license details.

pkgname=python-secretsocks
_pkgname=PySecretSOCKS
pkgver=24.da5be0e
pkgrel=4
pkgdesc='A Python socks server for tunneling a connection over another channel.'
arch=('any')
url='https://github.com/BC-SECURITY/PySecretSOCKS'
license=('MIT')
depends=('python' 'python-asyncore')
makedepends=('git' 'python-setuptools')
options=(!emptydirs)
source=("$pkgname::git+https://github.com/BC-SECURITY/$_pkgname")
sha512sums=('SKIP')

pkgver() {
  cd $pkgname

  ( set -o pipefail
    git describe --long --tags --abbrev=7 2>/dev/null |
      sed 's/\([^-]*-g\)/r\1/;s/-/./g' ||
    printf "%s.%s" "$(git rev-list --count HEAD)" \
      "$(git rev-parse --short=7 HEAD)"
  )
}

build() {
  cd $pkgname

  python setup.py build
}

package() {
  cd $pkgname

  python setup.py install --root="$pkgdir" --prefix=/usr -O1 --skip-build
}

