# This file is part of BlackArch Linux ( https://www.blackarch.org/ ).
# See COPYING for license details.

pkgname=python-secretsocks
_pkgname=PySecretSOCKS
pkgver=23.43c0bed
pkgrel=1
pkgdesc='A Python socks server for tunneling a connection over another channel.'
arch=('any')
url='https://github.com/BC-SECURITY/PySecretSOCKS'
license=('MIT')
depends=('python')
makedepends=('git' 'python-setuptools')
options=(!emptydirs)
source=("$pkgname::git+https://github.com/BC-SECURITY/$_pkgname")
sha512sums=('SKIP')

pkgver() {
  cd $pkgname

  echo $(git rev-list --count HEAD).$(git rev-parse --short HEAD)
}

build() {
  cd $pkgname

  python setup.py build
}

package() {
  cd $pkgname

  python setup.py install --root="$pkgdir" --prefix=/usr -O1 --skip-build
}

