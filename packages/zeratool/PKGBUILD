# This file is part of BlackArch Linux ( https://www.blackarch.org/ ).
# See COPYING for license details.

pkgname=zeratool
pkgver=19.28b0467
pkgrel=1
pkgdesc='Automatic Exploit Generation (AEG) and remote flag capture for exploitable CTF problems.'
groups=('blackarch' 'blackarch-exploitation' 'blackarch-automation')
arch=('any')
url='https://github.com/ChrisTheCoolHut/Zeratool'
license=('GPL3')
depends=('python2' 'python2-argparse' 'python2-claripy' 'python2-r2pipe'
         'ipython2' 'python2-ropper' 'angr-py2' 'python2-timeout-decorator'
         'pwntools' 'python2-claripy' 'python2-idalink')
makedepends=('git')
source=("$pkgname::git+https://github.com/ChrisTheCoolHut/Zeratool.git")
sha512sums=('SKIP')

pkgver() {
  cd $pkgname

  echo $(git rev-list --count HEAD).$(git rev-parse --short HEAD)
}

package() {
  cd $pkgname

  install -dm 755 "$pkgdir/usr/bin"
  install -dm 755 "$pkgdir/usr/share/$pkgname"

  install -Dm 644 -t "$pkgdir/usr/share/doc/$pkgname/" README.md
  install -Dm 644 LICENSE "$pkgdir/usr/share/licenses/$pkgname/LICENSE"

  rm README.md LICENSE

  cp -a * "$pkgdir/usr/share/$pkgname/"

  cat > "$pkgdir/usr/bin/$pkgname" << EOF
#!/bin/sh
cd /usr/share/$pkgname
exec python2 $pkgname.py "\$@"
EOF

  chmod +x "$pkgdir/usr/bin/$pkgname"
}

