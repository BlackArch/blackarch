# This file is part of BlackArch Linux ( http://blackarch.org ).
# See COPYING for license details.

pkgname=zeratool
pkgver=14.7097ff2
pkgrel=1
pkgdesc='Automatic Exploit Generation (AEG) and remote flag capture for exploitable CTF problems.'
groups=('blackarch' 'blackarch-exploitation' 'blackarch-automation')
arch=('any')
url='https://github.com/ChrisTheCoolHut/Zeratool'
license=('GPL3')
depends=('python2' 'python2-argparse' 'python2-claripy' 'python2-r2pipe' 'ipython2'
         'python2-ropper' 'angr' 'python2-timeout-decorator' 'pwntools'
         'python2-claripy')
makedepends=('git')
source=('git+https://github.com/ChrisTheCoolHut/Zeratool.git')
sha512sums=('SKIP')

pkgver() {
  cd "$srcdir/Zeratool"

  echo $(git rev-list --count HEAD).$(git rev-parse --short HEAD)
}

package() {
  cd "$srcdir/Zeratool"

  install -dm 755 "$pkgdir/usr/bin"
  install -dm 755 "$pkgdir/usr/share/zeratool"

  install -Dm644 -t "$pkgdir/usr/share/doc/zeratool/" README.md
  install -Dm644 LICENSE "$pkgdir/usr/share/licenses/zeratool/LICENSE"

  rm README.md LICENSE

  cp -a * "$pkgdir/usr/share/zeratool/"

  cat > "$pkgdir/usr/bin/zeratool" << EOF
#!/bin/sh
cd /usr/share/zeratool
exec python2 zeratool.py "\${@}"
EOF

  chmod a+x "$pkgdir/usr/bin/zeratool"
}
