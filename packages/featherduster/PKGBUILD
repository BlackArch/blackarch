# This file is part of BlackArch Linux ( http://blackarch.org ).
# See COPYING for license details.

pkgname='featherduster'
pkgver=169.81ec1c2
pkgrel=1
pkgdesc='An automated, modular cryptanalysis tool.'
groups=('blackarch' 'blackarch-crypto')
arch=('any')
url='https://github.com/nccgroup/featherduster'
license=('custom:unknown')
depends=('python2' 'python2-gmpy' 'python2-crypto' 'python2-ishell'
         'python2-requests' 'python2-singlefile' 'readline' 'ncurses')
makedepends=('git' 'python2-setuptools')
source=('git+https://github.com/nccgroup/featherduster.git')
sha1sums=('SKIP')

pkgver() {
  cd "$srcdir/featherduster"

  echo $(git rev-list --count HEAD).$(git rev-parse --short HEAD)
}

build() {
  cd "$srcdir/featherduster"

  python2 setup.py build
}

package() {
  cd "$srcdir/featherduster"

  mkdir -p "$pkgdir/usr/bin"
  mkdir -p "$pkgdir/usr/share/featherduster"

  python2 setup.py install --root="$pkgdir" --optimize=1 --skip-build

  install -Dm644 -t "$pkgdir/usr/share/doc/featherduster" README.md INSTALL.md \
    CONTRIBUTING.md

  find "$pkgdir" -type f -iname '*.pyc' -exec rm {} + -or -iname '*.pyo' -exec rm {} \;

  rm *.md

  cp -a * "$pkgdir/usr/share/featherduster/"

  cat > "$pkgdir/usr/bin/featherduster" << EOF
#!/bin/sh
cd /usr/share/featherduster
exec python2 featherduster/featherduster.py "\${@}"
EOF

  chmod a+x "$pkgdir/usr/bin/featherduster"
}
