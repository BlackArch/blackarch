# This file is part of BlackArch Linux ( http://blackarch.org ).
# See COPYING for license details.

pkgname='yasat'
pkgver='848'
pkgrel=1
groups=('blackarch' 'blackarch-scanner' 'blackarch-recon'
        'blackarch-fingerprint')
pkgdesc='Yet Another Stupid Audit Tool.'
arch=('any')
url='http://yasat.sourceforge.net/'
license=('GPL3')
depends=('bash')
source=("http://downloads.sourceforge.net/yasat/yasat-${pkgver}.tar.gz"
        'yasat.conf')
sha1sums=('fb2990ce0434da5ff2ba49d872dcb182053c14ce'
          'd222f9ba777d87e1ae1c49266f0e912e9e22d179')

package() {
  cd "$srcdir/yasat"

  mkdir -p "$pkgdir/usr/share/yasat"

  cp -a plugins "$pkgdir/usr/share/yasat/"

  chmod 644 "$pkgdir"/usr/share/yasat/plugins/*
  chmod 755 "$pkgdir"/usr/share/yasat/plugins/*.test

  install -Dm755 yasat "$pkgdir/usr/bin/yasat"
  install -Dm644 "$srcdir"/yasat.conf "$pkgdir/etc/yasat/yasat.conf"
  install -m644 -t "$pkgdir/usr/share/yasat" yasat.css common osdetection
  install -Dm644 man/yasat.8 "$pkgdir/usr/share/man/man8/yasat.8"
  install -Dm644 -t "$pkgdir/usr/share/doc/yasat" BUGS CHANGELOG README
  install -Dm644 -t "$pkgdir/usr/share/licenses/yasat" COPYRIGHT LICENCE

  touch "$pkgdir"/usr/share/yasat/yasat.advices
  chmod 644 "$pkgdir"/usr/share/yasat/yasat.advices
}
