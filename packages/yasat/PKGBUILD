# This file is part of BlackArch Linux ( http://blackarch.org ).
# See COPYING for license details.

pkgname=yasat
pkgver=848
pkgrel=1
groups=('blackarch' 'blackarch-scanner' 'blackarch-recon'
        'blackarch-fingerprint')
pkgdesc='Yet Another Stupid Audit Tool.'
arch=('any')
url='http://yasat.sourceforge.net/'
license=('GPL3')
depends=('bash')
source=("http://downloads.sourceforge.net/yasat/yasat-${pkgver}.tar.gz"
        'yasat.conf')
sha512sums=('0bc0e8e1068bee05242b5c7a7cdc8ca976503f696aef2889870ef872546021d0956489cf8d5b304a36be412cf8e5c8cbf4be5aa1cce5df008c5185188e5a99dc'
            '5e5403dad904128782216431c8c55a59fac02a8bb0b4df92649e1dc89e679a0648cbaaf593cee91fbdb60dbfcd62ecfa020d65894b4ad45d7e541b40cf854d11')

package() {
  cd "$srcdir/yasat"

  install -dm 755 "$pkgdir/usr/share/yasat"

  cp -a plugins "$pkgdir/usr/share/yasat/"

  chmod 644 "$pkgdir"/usr/share/yasat/plugins/*
  chmod 755 "$pkgdir"/usr/share/yasat/plugins/*.test

  install -Dm755 yasat "$pkgdir/usr/bin/yasat"
  install -Dm644 "$srcdir"/yasat.conf "$pkgdir/etc/yasat/yasat.conf"
  install -m644 -t "$pkgdir/usr/share/yasat" yasat.css common osdetection
  install -Dm644 man/yasat.8 "$pkgdir/usr/share/man/man8/yasat.8"
  install -Dm644 -t "$pkgdir/usr/share/doc/yasat" BUGS CHANGELOG README
  install -Dm644 -t "$pkgdir/usr/share/licenses/yasat" COPYRIGHT LICENCE

  touch "$pkgdir"/usr/share/yasat/yasat.advices
  chmod 644 "$pkgdir"/usr/share/yasat/yasat.advices
}
