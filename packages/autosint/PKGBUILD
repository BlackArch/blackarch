# This file is part of BlackArch Linux ( http://blackarch.org ).
# See COPYING for license details.

pkgname=autosint
pkgver=234.e1f4937
pkgrel=1
pkgdesc='Tool to automate common osint tasks.'
groups=('blackarch' 'blackarch-recon')
arch=('any')
url='https://github.com/bharshbarger/AutOSINT'
license=('GPL2')
depends=('python2' 'python2-python-docx' 'python2-google' 'shodan')
makedepends=('git')
source=('git+https://github.com/bharshbarger/AutOSINT.git')
sha512sums=('SKIP')

pkgver() {
  cd AutOSINT

  echo $(git rev-list --count HEAD).$(git rev-parse --short HEAD)
}

package() {
  cd AutOSINT

  install -dm 755 "$pkgdir/usr/bin"
  install -dm 755 "$pkgdir/usr/share/$pkgname"

  install -Dm644 -t "$pkgdir/usr/share/doc/$pkgname/" README.md

  rm *.md

  cp -a * "$pkgdir/usr/share/$pkgname"

  cat > "$pkgdir/usr/bin/$pkgname" << EOF
#!/bin/sh
cd /usr/share/autosint
exec python2 AutOSINT.py "\$@"
EOF

  chmod a+x "$pkgdir/usr/bin/$pkgname"
}

