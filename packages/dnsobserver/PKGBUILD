# This file is part of BlackArch Linux ( https://www.blackarch.org/ ).
# See COPYING for license details.

pkgname=dnsobserver
pkgver=5.f331482
pkgrel=2
pkgdesc='A handy DNS service written in Go to aid in the detection of several types of blind vulnerabilities.'
arch=('x86_64' 'aarch64')
groups=('blackarch' 'blackarch-networking')
url='https://github.com/allyomalley/dnsobserver'
license=('custom:unknown')
depends=('glibc')
makedepends=('git' 'go')
source=("git+https://github.com/allyomalley/$pkgname.git")
sha256sums=('SKIP')

pkgver() {
  cd $pkgname

  echo $(git rev-list --count HEAD).$(git rev-parse --short HEAD)
}

build() {
  cd $pkgname

  GOPATH="$srcdir" go mod init go.mod
  GOPATH="$srcdir" go mod tidy
  GOPATH="$srcdir" go mod download
  GOPATH="$srcdir" go build \
    -trimpath \
    -buildmode=pie \
    -mod=readonly \
    -modcacherw \
    -ldflags "-s -w" \
    -o $pkgname .

}

package() {
  cd $pkgname

  install -Dm 755 $pkgname "$pkgdir/usr/bin/$pkgname"
  install -Dm 644 -t "$pkgdir/usr/share/$pkgname/" config.yml
  install -Dm 644 -t "$pkgdir/usr/share/doc/$pkgname/" README.md
}

