# This file is part of BlackArch Linux ( https://www.blackarch.org/ ).
# See COPYING for license details

pkgbase=python-intervaltree
pkgname=('python2-intervaltree' 'python-intervaltree')
_pkgname=intervaltree
pkgver=3.0.2
pkgrel=2
pkgdesc='Library providing a mutable, self-balancing interval tree for Python.'
arch=('any')
url='https://pypi.org/project/intervaltree/#files'
license=('APACHE')
makedepends=('python2-setuptools' 'python-setuptools')
source=("https://files.pythonhosted.org/packages/e8/f9/76237755b2020cd74549e98667210b2dd54d3fb17c6f4a62631e61d31225/$_pkgname-$pkgver.tar.gz")
sha512sums=('51d6aa302c95c9a573bcf594de390037ec1a6d674061e6a0b4025fa889dfd0187e37bc0331f13d6703340973e4c539aa1349431b14c742de9d2050cbb53d8255')

prepare() {
  cp -a "$_pkgname-$pkgver"{,-2}
}

build() {
  cd "$_pkgname-$pkgver-2"

  python2 setup.py build

  cd "$srcdir/$_pkgname-$pkgver"

  python setup.py build
}

package_python2-intervaltree() {
  depends=('python2' 'python2-sortedcontainers')

  cd "$_pkgname-$pkgver-2"

  python2 setup.py install --root="$pkgdir" --optimize=1 --skip-build
}

package_python-intervaltree() {
  depends=('python' 'python-sortedcontainers')

  cd "$_pkgname-$pkgver"

  python setup.py install --root="$pkgdir" --optimize=1 --skip-build
}

