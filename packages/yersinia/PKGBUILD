# This file is part of BlackArch Linux ( https://www.blackarch.org/ ).
# See COPYING for license details.

pkgname=yersinia
pkgver=115.8c0d7c5
pkgrel=1
groups=('blackarch' 'blackarch-networking' 'blackarch-scanner')
pkgdesc='A network tool designed to take advantage of some weakness in different network protocols.'
arch=('x86_64' 'armv6h' 'armv7h' 'aarch64')
url='http://www.yersinia.net/'
depends=('libpcap' 'libnet' 'gtk2')
license=('GPL')
source=('git+https://github.com/tomac/yersinia.git')
sha512sums=('SKIP')

pkgver() {
  cd $pkgname

  echo $(git rev-list --count HEAD).$(git rev-parse --short HEAD)
}

build() {
  cd "$pkgname"

  if [[ "$CARCH" == "i686" || "$CARCH" == "x86_64" ]];
  then
    ./configure --prefix=/usr --sbindir=/usr/bin \
      --with-pcap-includes=/usr/include/
  else
    ./configure --build=arm --prefix=/usr --sbindir=/usr/bin \
      --with-pcap-includes=/usr/include/
  fi

  make
}

package() {
  cd "$pkgname"

  make DESTDIR="$pkgdir" install

  install -Dm 644 -t "$pkgdir/usr/share/doc/$pkgname" AUTHORS ChangeLog INSTALL \
    NEWS THANKS FAQ
}

