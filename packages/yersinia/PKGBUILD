# This file is part of BlackArch Linux ( http://blackarch.org ).
# See COPYING for license details.

pkgname='yersinia'
pkgver='0.8.2'
pkgrel=1
groups=('blackarch' 'blackarch-networking' 'blackarch-scanner')
pkgdesc='A network tool designed to take advantage of some weakness in different network protocols.'
arch=('x86_64' 'armv6h' 'armv7h' 'aarch64')
url='http://www.yersinia.net/'
depends=('libpcap' 'libnet' 'gtk2')
license=('GPL')
source=("https://github.com/tomac/yersinia/archive/v${pkgver}.tar.gz")
sha1sums=('a392f9e2d81ff5e7228b29475a068dc316a8cce3')

build() {
  cd "$srcdir/yersinia-$pkgver"

  if [[ "$CARCH" == "i686" || "$CARCH" == "x86_64" ]];
  then
    ./configure --prefix=/usr --sbindir=/usr/bin \
      --with-pcap-includes=/usr/include/
  else
    ./configure --build=arm --prefix=/usr --sbindir=/usr/bin \
      --with-pcap-includes=/usr/include/
  fi

  make
}

package() {
  cd "$srcdir/yersinia-$pkgver"

  make DESTDIR="$pkgdir" install

  install -Dm644 -t "$pkgdir/usr/share/doc/yersinia" AUTHORS ChangeLog INSTALL \
    NEWS THANKS FAQ
}
