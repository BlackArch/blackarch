# This file is part of BlackArch Linux ( https://www.blackarch.org/ ).
# See COPYING for license details.

pkgname=netz
pkgver=v0.1.0.r8.g3754e56
pkgrel=1
pkgdesc='Discover internet-wide misconfigurations while drinking coffee.'
arch=('x86_64' 'aarch64')
groups=('blackarch' 'blackarch-scanner')
url='https://github.com/spectralops/netz'
license=('MIT')
depends=('glibc')
makedepends=('git' 'go')
source=("git+https://github.com/spectralops/$pkgname.git")
md5sums=('SKIP')

pkgver() {
  cd $pkgname

  git describe --long | sed 's/\([^-]*-g\)/r\1/;s/-/./g'
}

build() {
  cd $pkgname

  GOPATH="$srcdir" go mod download
  GOPATH="$srcdir" go build \
    -trimpath \
    -buildmode=pie \
    -mod=readonly \
    -modcacherw \
    -ldflags "-s -w" \
    -o $pkgname .

}

package() {
  cd $pkgname

  install -Dm 755 $pkgname "$pkgdir/usr/bin/$pkgname"
  install -Dm 644 -t "$pkgdir/usr/share/$pkgname/" taskdefinition.json
  install -Dm 644 -t "$pkgdir/usr/share/doc/$pkgname/" *.md
}

