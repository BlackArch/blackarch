# This file is part of BlackArch Linux ( https://www.blackarch.org/ ).
# See COPYING for license details.

pkgname=perl-ip-country
pkgver=2.28
pkgrel=6
pkgdesc='Fast lookup of country codes from IP addresses.'
arch=('any')
url='http://search.cpan.org/~NWETTERS/IP-Country'
license=('GPL' 'PerlArtistic')
depends=('perl-geography-countries')
options=('!emptydirs')
source=("http://www.cpan.org/authors/id/N/NW/NWETTERS/IP-Country-$pkgver.tar.gz")
sha512sums=('3596014eaeb7d0be8edd9b98c2f31e83587cc48c738837eaa78e9dc6c7ee6cee371a7bc230f28fa2901b9eece7fe0fa2126135a3d90d1261488d61cedcec25ac')

prepare() {
  cd "IP-Country-$pkgver"

  sed -i '/^auto_install/d' Makefile.PL
}

build() {
  cd "IP-Country-$pkgver"

  PERL_MM_USE_DEFAULT=1 perl Makefile.PL INSTALLDIRS=vendor

  make
}

package() {
  cd "IP-Country-$pkgver"

  make DESTDIR="$pkgdir" install

  find $pkgdir -name '.packlist' -delete
  find $pkgdir -name '*.pod' -delete
}

