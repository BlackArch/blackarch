# This file is part of BlackArch Linux ( https://www.blackarch.org/ ).
# See COPYING for license details.

pkgbase=python-tokenize-rt
pkgname=('python2-tokenize-rt' 'python-tokenize-rt')
_pkgname=tokenize_rt
pkgver=3.2.0
pkgrel=1
pkgdesc='A wrapper around the stdlib `tokenize` which roundtrips.'
arch=('any')
url='https://pypi.org/project/tokenize-rt/#files'
license=('MIT')
makedepends=('python2-setuptools' 'python-setuptools')
source=("https://files.pythonhosted.org/packages/0c/ed/683a54fb3525c768331ec9c40e09e69a0d54e4ca55d1930e2df1d4b23afa/$_pkgname-$pkgver.tar.gz")
sha512sums=('570c371c42d9f9e886eb76a5fbc12eeda2cb2686ea7320dc22cfb899d079332707925f680295bf989f249de367c6e88ea736fdf104c98f2bdffa2794a013aa19')

prepare() {
  cp -a "$_pkgname-$pkgver"{,-2}
}

build() {
  cd "$_pkgname-$pkgver-2"

  python2 setup.py build

  cd "$srcdir/$_pkgname-$pkgver"

  python setup.py build
}

package_python2-tokenize-rt() {
  depends=('python2' 'python2-typing')

  cd "$_pkgname-$pkgver-2"

  python2 setup.py install --root="$pkgdir" --prefix=/usr -O1 --skip-build

  mv "$pkgdir/usr/bin/tokenize-rt" "$pkgdir/usr/bin/tokenize-rt2"
}

package_python-tokenize-rt() {
  depends=('python')

  cd "$_pkgname-$pkgver"

  python setup.py install --root="$pkgdir" --prefix=/usr -O1 --skip-build
}

