# This file is part of BlackArch Linux ( https://www.blackarch.org/ ).
# See COPYING for license details.

pkgbase=python-osmgpsmap
pkgname=('python2-osmgpsmap' 'python-osmgpsmap')
_pkgname=osm-gps-map
pkgver=1.0.2
pkgrel=2
pkgdesc='Python bindings for osm-gps-map.'
arch=('x86_64' 'armv6h' 'armv7h' 'aarch64')
url='http://nzjrs.github.com/osm-gps-map/'
license=('GPL')
makedepends=('python2-setuptools' 'python-setuptools')
conflicts=('python-osmgpsmap')
provides=('python-osmgpsmap')
source=("https://github.com/nzjrs/osm-gps-map/archive/$pkgver.tar.gz")
sha512sums=('58754e036947cef09a2629c31fbdf3d93b36a65c5b7bce9fa5dc93a9206e142ae2dcdd7f8a9184e473c3033faf3156c3a949894c2956ba38823dfcd58682d51e')

prepare() {
  cp -a "$_pkgname-$pkgver" "$_pkgname-$pkgver-2"
}

build() {
  cd "$_pkgname-$pkgver-2"

  python2 setup.py build

  cd "$srcdir/$_pkgname-$pkgver"

  python setup.py build
}

package_python2-osmgpsmap() {
  depends=('python2-gobject2' 'pygtk' 'osm-gps-map')

  cd "$_pkgname-$pkgver-2"

  python2 setup.py install --prefix=/usr --root="$pkgdir" --optimize=1 \
    --skip-build
}

package_python-osmgpsmap() {
  depends=('python-gobject2' 'pygtk' 'osm-gps-map')

  cd "$_pkgname-$pkgver"

  python setup.py install --prefix=/usr --root="$pkgdir" --optimize=1 \
    --skip-build
}

