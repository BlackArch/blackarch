# This file is part of BlackArch Linux ( https://www.blackarch.org/ ).
# See COPYING for license details.

pkgname=fireprox
pkgver=51.a6e1300
pkgrel=1
pkgdesc='AWS API Gateway management tool for creating on the fly HTTP pass-through proxies for unique IP rotation.'
arch=('any')
groups=('blackarch' 'blackarch-webapp')
url='https://github.com/ustayready/fireprox'
license=('GPL3')
depends=('python' 'python-virtualenv')
makedepends=('git' 'python-setuptools' 'python-pip')
source=("git+https://github.com/ustayready/$pkgname.git")
sha512sums=('SKIP')
install="$pkgname.install"

pkgver() {
  cd $pkgname

  ( set -o pipefail
    git describe --long --tags --abbrev=7 2>/dev/null |
      sed 's/\([^-]*-g\)/r\1/;s/-/./g' ||
    printf "%s.%s" "$(git rev-list --count HEAD)" \
      "$(git rev-parse --short=7 HEAD)"
  )
}

package() {
  cd $pkgname

  install -dm 755 "$pkgdir/usr/bin"
  install -Dm 644 requirements.txt "$pkgdir/usr/share/$pkgname/requirements.txt"
  install -Dm 644 -t "$pkgdir/usr/share/doc/$pkgname/" *.md

  rm -rf LICENSE *.md .gitignore

  cp -a * "$pkgdir/usr/share/$pkgname/"

  cat > "$pkgdir/usr/bin/$pkgname" << EOF
#!/bin/sh
source /usr/share/$pkgname/venv/bin/activate
exec python /usr/share/$pkgname/fire.py "\$@"
EOF

  chmod a+x "$pkgdir/usr/bin/$pkgname"
}

