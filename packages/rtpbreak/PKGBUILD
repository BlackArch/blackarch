# This file is part of BlackArch Linux ( https://www.blackarch.org/ ).
# See COPYING for license details.

pkgname=rtpbreak
pkgver=1.3a
pkgrel=2
epoch=1
groups=('blackarch' 'blackarch-networking')
pkgdesc="Detects, reconstructs and analyzes any RTP session"
url='http://xenion.antifork.org/rtpbreak/'
license=('GPL')
depends=('libpcap' 'libnet')
arch=('x86_64' 'aarch64')
source=("http://dl.packetstormsecurity.net/sniffers/$pkgname-$pkgver.tgz")
sha512sums=('abe0329f91a8c76d4fab5589d834e19615457fe1c5cdb410ce2f6fbf871b750cf917a92a979b92aaf5f0a2644fa4f4dc4ca7e53fb46d71c8fa016a7f41efe5ad')

prepare() {
  cd "$pkgname-$pkgver"

  sed -i 'i\#include <linux/udp.h>' src/main.c
}

build() {
  cd "$pkgname-$pkgver"

  make CFLAGS="$CFLAGS"
}

package() {
  cd "$pkgname-$pkgver"

  install -dm 755 "$pkgdir/usr/share/doc/$pkgname/"

  install -Dm 755 "src/$pkgname" "$pkgdir/usr/bin/$pkgname"
  install -m 644 doc/* "$pkgdir/usr/share/doc/$pkgname/"
}

