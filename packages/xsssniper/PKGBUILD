# This file is part of BlackArch Linux ( https://www.blackarch.org/ ).
# See COPYING for license details.

pkgname=xsssniper
pkgver=0.9
pkgrel=3
groups=('blackarch' 'blackarch-webapp' 'blackarch-fuzzer')
pkgdesc='An automatic XSS discovery tool'
arch=('any')
url='https://github.com/gbrindisi/xsssniper'
license=('GPL2')
depends=('python2' 'python2-mechanize' 'python2-hgapi')
source=("https://github.com/gbrindisi/xsssniper/archive/v$pkgver.tar.gz")
sha512sums=('8583c0c55e5a268c86993b926c7d85c3183b6bf3c38592fda92f635961e04a1eb6c6faef65880d3aa7663754802c9a66d887bd22698b69fc6b0a67d9b3e23048')

prepare() {
  cd "$pkgname-$pkgver"

  sed -i 's|env python|env python2|' core/scanner.py
}

package() {
  cd "$pkgname-$pkgver"

  install -dm 755 "$pkgdir/usr/bin"
  install -dm 755 "$pkgdir/usr/share/$pkgname"

  install -Dm 644 -t "$pkgdir/usr/share/doc/$pkgname/" README.md CHANGELOG

  rm README.md CHANGELOG

  cp -a --no-preserve=ownership * "$pkgdir/usr/share/$pkgname/"

  cat > "$pkgdir/usr/bin/$pkgname" << EOF
#!/bin/sh
cd /usr/share/$pkgname
exec python2 $pkgname.py "\$@"
EOF

  chmod +x "$pkgdir/usr/bin/$pkgname"
}

