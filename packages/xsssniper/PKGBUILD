# This file is part of BlackArch Linux ( https://www.blackarch.org/ ).
# See COPYING for license details.

pkgname=xsssniper
pkgver=79.02b59af
pkgrel=2
groups=('blackarch' 'blackarch-webapp' 'blackarch-fuzzer')
pkgdesc='An automatic XSS discovery tool'
arch=('any')
url='https://github.com/gbrindisi/xsssniper'
license=('GPL2')
depends=('python2' 'python2-mechanize' 'python2-hgapi' 'python2-lxml')
makedepends=('git')
source=("git+https://github.com/gbrindisi/$pkgname.git")
sha512sums=('SKIP')

pkgver() {
  cd $pkgname

  echo $(git rev-list --count HEAD).$(git rev-parse --short HEAD)
}

prepare() {
  cd $pkgname

  sed -i 's|env python|env python2|' core/scanner.py
}

package() {
  cd $pkgname

  install -dm 755 "$pkgdir/usr/bin"
  install -dm 755 "$pkgdir/usr/share/$pkgname"

  install -Dm 644 -t "$pkgdir/usr/share/doc/$pkgname/" README.md CHANGELOG

  rm README.md CHANGELOG

  cp -a --no-preserve=ownership * "$pkgdir/usr/share/$pkgname/"

  cat > "$pkgdir/usr/bin/$pkgname" << EOF
#!/bin/sh
cd /usr/share/$pkgname
exec python2 $pkgname.py "\$@"
EOF

  chmod +x "$pkgdir/usr/bin/$pkgname"
}

