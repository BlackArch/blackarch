# This file is part of BlackArch Linux ( https://www.blackarch.org/ ).
# See COPYING for license details.

pkgname=panhunt
pkgver=63.ec87e88
pkgrel=1
groups=('blackarch' 'blackarch-scanner')
pkgdesc='Searches for credit card numbers (PANs) in directories.'
arch=('any')
url='https://github.com/Dionach/PANhunt'
license=('GPL3')
depends=('python2' 'python2-colorama' 'python2-progressbar')
makedepends=('git')
source=("$pkgname::git+https://github.com/Dionach/PANhunt.git")
sha512sums=('SKIP')

pkgver() {
  cd $pkgname

  echo $(git rev-list --count HEAD).$(git rev-parse --short HEAD)
}

package() {
  cd $pkgname

  install -dm 755 "$pkgdir/usr/bin"
  install -dm 755 "$pkgdir/usr/share/$pkgname"

  install -Dm 644 -t "$pkgdir/usr/share/doc/$pkgname/" README.md
  install -Dm 644 LICENSE "$pkgdir/usr/share/licenses/$pkgname/LICENSE"

  rm README.md LICENSE

  cp -a * "$pkgdir/usr/share/$pkgname/"

  cat > "$pkgdir/usr/bin/$pkgname" << EOF
#!/bin/sh
cd /usr/share/$pkgname
exec python2 $pkgname.py "\$@"
EOF

  chmod +x "$pkgdir/usr/bin/$pkgname"
}

