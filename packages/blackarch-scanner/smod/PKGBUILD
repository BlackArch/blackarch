# This file is part of BlackArch Linux ( https://www.blackarch.org/ ).
# See COPYING for license details.

pkgname=smod
pkgver=53.7eb8423
pkgrel=4
groups=('blackarch' 'blackarch-scanner' 'blackarch-fuzzer' 'blackarch-recon'
        'blackarch-dos')
pkgdesc='A modular framework with every kind of diagnostic and offensive feature you could need in order to pentest modbus protocol.'
arch=('any')
url='https://github.com/enddo/smod'
license=('GPL2')
depends=('python2' 'python2-scapy' 'python2-pexpect' 'python2-asn1crypto'
         'python2-archinfo')
makedepends=('git')
source=("git+https://github.com/enddo/$pkgname.git")
sha512sums=('SKIP')

pkgver() {
  cd $pkgname

  echo $(git rev-list --count HEAD).$(git rev-parse --short HEAD)
}

package() {
  cd $pkgname

  install -dm 755 "$pkgdir/usr/bin"
  install -dm 755 "$pkgdir/usr/share/$pkgname"

  install -Dm 644 -t "$pkgdir/usr/share/doc/$pkgname/" README.md
  install -Dm 644 LICENSE.md "$pkgdir/usr/share/licenses/$pkgname/LICENSE"

  rm README.md LICENSE.md

  cp -a * "$pkgdir/usr/share/$pkgname/"

  cat > "$pkgdir/usr/bin/$pkgname" << EOF
#!/bin/sh
cd /usr/share/$pkgname
exec python2 $pkgname.py "\$@"
EOF

  chmod +x "$pkgdir/usr/bin/$pkgname"
}

