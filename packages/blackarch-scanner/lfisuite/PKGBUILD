# This file is part of BlackArch Linux ( https://www.blackarch.org/ ).
# See COPYING for license details.

pkgname=lfisuite
pkgver=85.470e01f
pkgrel=1
pkgdesc='Totally Automatic LFI Exploiter (+ Reverse Shell) and Scanner.'
groups=('blackarch' 'blackarch-scanner' 'blackarch-webapp'
        'blackarch-exploitation')
arch=('any')
url='https://github.com/D35m0nd142/LFISuite'
license=('GPL3')
depends=('python2')
makedepends=('git')
source=("$pkgname::git+https://github.com/D35m0nd142/LFISuite.git")
sha512sums=('SKIP')

pkgver() {
  cd $pkgname

  echo $(git rev-list --count HEAD).$(git rev-parse --short HEAD)
}

package() {
  cd $pkgname

  install -dm 755 "$pkgdir/usr/bin"
  install -dm 755 "$pkgdir/usr/share/$pkgname"

  install -Dm 644 -t "$pkgdir/usr/share/doc/$pkgname/" README.md
  install -Dm 644 COPYING.GPL "$pkgdir/usr/share/licenses/$pkgname/LICENSE"

  rm README.md COPYING.GPL

  cp -a * "$pkgdir/usr/share/$pkgname/"

  cat > "$pkgdir/usr/bin/$pkgname" << EOF
#!/bin/sh
cd /usr/share/$pkgname
exec python2 $pkgname.py "\$@"
EOF

  chmod +x "$pkgdir/usr/bin/$pkgname"
}

