# This file is part of BlackArch Linux ( https://www.blackarch.org/ ).
# See COPYING for license details.

pkgname=cangibrina
pkgver=123.6de0165
pkgrel=1
groups=('blackarch' 'blackarch-scanner' 'blackarch-webapp')
pkgdesc='Dashboard Finder.'
arch=('any')
url='https://github.com/fnk0c/cangibrina'
license=('GPL2')
depends=('python2' 'python2-beautifulsoup4' 'python2-pysocks'
         'python2-mechanize' 'python2-html5lib')
optdepends=('tor' 'nmap')
makedepends=('git')
source=("git+https://github.com/fnk0c/$pkgname.git")
sha512sums=('SKIP')

pkgver() {
  cd $pkgname

  echo $(git rev-list --count HEAD).$(git rev-parse --short HEAD)
}

package() {
  cd $pkgname

  install -dm 755 "$pkgdir/usr/bin"
  install -dm 755 "$pkgdir/usr/share/$pkgname"

  install -Dm 644 -t "$pkgdir/usr/share/doc/$pkgname/" README.md
  install -Dm 644 LICENSE "$pkgdir/usr/share/licenses/$pkgname/LICENSE"

  rm README.md LICENSE

  cp -a * "$pkgdir/usr/share/$pkgname/"

  cat > "$pkgdir/usr/bin/$pkgname" << EOF
#!/bin/sh
cd /usr/share/$pkgname
exec python2 $pkgname.py "\$@"
EOF

  chmod +x "$pkgdir/usr/bin/$pkgname"
}

