# This file is part of BlackArch Linux ( https://www.blackarch.org/ ).
# See COPYING for license details.

pkgname=svn-extractor
pkgver=39.39941be
pkgrel=2
groups=('blackarch' 'blackarch-scanner')
pkgdesc='A simple script to extract all web resources by means of .SVN folder exposed over network.'
arch=('any')
url='https://github.com/anantshri/svn-extractor'
license=('GPL3')
depends=('python2' 'python2-pysqlite' 'python2-requests')
makedepends=('git')
source=("git+https://github.com/anantshri/$pkgname.git")
sha512sums=('SKIP')

pkgver() {
  cd $pkgname

  echo $(git rev-list --count HEAD).$(git rev-parse --short HEAD)
}

prepare(){
  cd $pkgname

  sed -i 's|python$|python2|' svn_extractor.py
}

package() {
  cd $pkgname

  install -Dm 755 svn_extractor.py "$pkgdir/usr/bin/$pkgname"
  install -Dm 644 -t "$pkgdir/usr/share/doc/$pkgname/" README.md
}

