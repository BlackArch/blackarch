# This file is part of BlackArch Linux ( https://www.blackarch.org/ ).
# See COPYING for license details.

pkgname=subover
pkgver=71.3d258e2
pkgrel=2
groups=('blackarch' 'blackarch-scanner' 'blackarch-recon')
pkgdesc='A Powerful Subdomain Takeover Tool.'
arch=('x86_64' 'aarch64')
url='https://github.com/Ice3man543/SubOver'
license=('BSD')
depends=('glibc')
makedepends=('git' 'go-pie')
source=("$pkgname::git+https://github.com/Ice3man543/SubOver.git")
sha512sums=('SKIP')
_url='github.com/Ice3man543/SubOver'

pkgver() {
  cd $pkgname

  echo $(git rev-list --count HEAD).$(git rev-parse --short HEAD)
}

build() {
  mkdir -p "src/$_url" && rm -rf "src/$_url" && mv $pkgname "src/$_url" &&
    cd "src/$_url"

  GOPATH="$srcdir" go get -d -t $_url
  GOPATH="$srcdir" go build -v $_url
}

package() {
  install -dm 755 "$pkgdir/usr/bin"
  install -dm 755 "$pkgdir/usr/share/$pkgname"

  cd "src/$_url"

  install -Dm 644 -t "$pkgdir/usr/share/doc/$pkgname/" README.md
  install -Dm 644 LICENSE "$pkgdir/usr/share/licenses/$pkgname/LICENSE"

  rm README.md LICENSE

  cp -a * "$pkgdir/usr/share/$pkgname/"

  cat > "$pkgdir/usr/bin/$pkgname" << EOF
#!/bin/sh
cd /usr/share/$pkgname
exec ./SubOver "\$@"
EOF

  chmod +x "$pkgdir/usr/bin/$pkgname"
}

