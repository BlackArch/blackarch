# This file is part of BlackArch Linux ( https://www.blackarch.org/ ).
# See COPYING for license details.

pkgname=fi6s
pkgver=169.f0c15b3
pkgrel=1
pkgdesc='IPv6 network scanner designed to be fast.'
groups=('blackarch' 'blackarch-scanner')
arch=('any')
url='https://github.com/sfan5/fi6s'
license=('GAGPL')
depends=('libpcap')
makedepends=('git')
source=("git+https://github.com/sfan5/$pkgname.git")
sha512sums=('SKIP')

pkgver() {
  cd $pkgname

  echo $(git rev-list --count HEAD).$(git rev-parse --short HEAD)
}

build() {
  cd $pkgname

  make BUILD_TYPE=release
}

package() {
  cd $pkgname

  install -Dm 755 $pkgname "$pkgdir/usr/bin/$pkgname"
  install -Dm 644 -t "$pkgdir/usr/share/doc/$pkgname/" README.md
  install -Dm 644 LICENSE "$pkgdir/usr/share/licenses/$pkgname/LICENSE"
}

