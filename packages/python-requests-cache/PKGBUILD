# This file is part of BlackArch Linux ( https://www.blackarch.org/ ).
# See COPYING for license details.

pkgbase=python-requests-cache
pkgname=('python2-requests-cache' 'python-requests-cache')
_pkgname=requests-cache
pkgver=0.6.2
pkgrel=1
pkgdesc='Persistent cache for requests library.'
arch=('any')
url='https://pypi.org/project/requests-cache/#files'
license=('BSD')
makedepends=('python2-setuptools' 'python-setuptools')
source=("https://files.pythonhosted.org/packages/3d/f3/d0755be42181f7b123c0db6a836f08b6eaa4f4ac4d4b04cb08a470eb140f/$_pkgname-$pkgver.tar.gz")
sha512sums=('45d11756f70bd3156f8986b61097b1ee522e742856782301e7362fed09f166769ccd8373415c59ca0cd75e78a929cd3bd19e77a88781626dd249d16811ba8876')

prepare() {
  cp -a "$_pkgname-$pkgver"{,-2}
}

build() {
  cd "$_pkgname-$pkgver-2"

  python2 setup.py build

  cd "$srcdir/$_pkgname-$pkgver"

  python setup.py build
}

package_python2-requests-cache() {
  depends=('python2' 'python2-requests')

  cd "$_pkgname-$pkgver-2"

  python2 setup.py install --prefix=/usr --root="$pkgdir" --optimize=1 \
    --skip-build
}

package_python-requests-cache() {
  depends=('python' 'python-requests')

  cd "$_pkgname-$pkgver"

  python setup.py install --prefix=/usr --root="$pkgdir" --optimize=1 \
    --skip-build
}

