# This file is part of BlackArch Linux ( https://www.blackarch.org/ ).
# See COPYING for license details.

pkgbase=python-requests-cache
pkgname=('python2-requests-cache' 'python-requests-cache')
_pkgname=requests-cache
pkgver=0.9.2
pkgrel=1
pkgdesc='Persistent cache for requests library.'
arch=('any')
url='https://pypi.org/project/requests-cache/#files'
license=('BSD')
makedepends=('python2-setuptools' 'python-setuptools')
source=("https://files.pythonhosted.org/packages/ed/5a/2177c4b5d949b1b4baefe1451e5f6f3220ec127f442ff97f947f6150232f/$_pkgname-$pkgver.tar.gz")
sha512sums=('f8f832e5baf52bb771769d87637959d4b8423f39238691653836096802a4a4835e19bea5fdec9e4d39ad4f8624f75b6254668e1dee4f31fdacfeb273b0f38e7d')

prepare() {
  cp -a "$_pkgname-$pkgver"{,-2}
}

build() {
  cd "$_pkgname-$pkgver-2"

  python2 setup.py build

  cd "$srcdir/$_pkgname-$pkgver"

  python setup.py build
}

package_python2-requests-cache() {
  depends=('python2' 'python2-requests')

  cd "$_pkgname-$pkgver-2"

  python2 setup.py install --prefix=/usr --root="$pkgdir" --optimize=1 \
    --skip-build
}

package_python-requests-cache() {
  depends=('python' 'python-requests')

  cd "$_pkgname-$pkgver"

  python setup.py install --prefix=/usr --root="$pkgdir" --optimize=1 \
    --skip-build
}

