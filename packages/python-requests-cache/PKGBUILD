# This file is part of BlackArch Linux ( https://www.blackarch.org/ ).
# See COPYING for license details.

pkgbase=python-requests-cache
pkgname=('python2-requests-cache' 'python-requests-cache')
_pkgname=requests-cache
pkgver=0.9.8
pkgrel=1
pkgdesc='Persistent cache for requests library.'
arch=('any')
url='https://pypi.org/project/requests-cache/#files'
license=('BSD')
makedepends=('python2-setuptools' 'python-setuptools')
source=("https://files.pythonhosted.org/packages/source/${_pkgname::1}/$_pkgname/$_pkgname-$pkgver.tar.gz")
sha512sums=('9b050e1f35ef921e48206f72ca33bd006bca4ee8c0c7237cf865fa98367596fd383962891d41fd36f382cb0f6285ed19fd8071044a1215bdd34375b6b27493e9')

prepare() {
  cp -a "$_pkgname-$pkgver"{,-2}
}

build() {
  cd "$_pkgname-$pkgver-2"

  python2 setup.py build

  cd "$srcdir/$_pkgname-$pkgver"

  python setup.py build
}

package_python2-requests-cache() {
  depends=('python2' 'python2-requests')

  cd "$_pkgname-$pkgver-2"

  python2 setup.py install --prefix=/usr --root="$pkgdir" --optimize=1 \
    --skip-build
}

package_python-requests-cache() {
  depends=('python' 'python-requests')

  cd "$_pkgname-$pkgver"

  python setup.py install --prefix=/usr --root="$pkgdir" --optimize=1 \
    --skip-build
}

