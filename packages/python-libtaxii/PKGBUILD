# This file is part of BlackArch Linux ( https://www.blackarch.org/ ).
# See COPYING for license details.

pkgbase=python-libtaxii
pkgname=('python2-libtaxii' 'python-libtaxii')
_pkgname=libtaxii
pkgver=1.1.113
pkgrel=1
pkgdesc='TAXII Library.'
url='https://github.com/TAXIIProject/libtaxii'
license=('BSD')
arch=('any')
makedepends=('python2-setuptools' 'python-setuptools')
source=("https://github.com/TAXIIProject/$_pkgname/archive/$pkgver.tar.gz")
sha512sums=('cf727958df5314afbea5daae198f9764480cc8744c13f451f36367d70266d6aa0a3ef3e81b1151803bc289732a3ef1102feddca5a967636efdd98e647bfee59c')

prepare() {
  cp -a "$_pkgname-$pkgver"{,-2}
}

build() {
  cd "$_pkgname-$pkgver-2"

  python2 setup.py build

  cd "$srcdir/$_pkgname-$pkgver"

  python setup.py build
}

package_python2-libtaxii() {
  depends=('python2-lxml' 'python2-requests' 'python2-dateutil' 'python2-six')

  cd "$_pkgname-$pkgver-2"

  python2 setup.py install --prefix=/usr --root="$pkgdir" --optimize=1 \
    --skip-build

  for i in "$pkgdir/usr/bin/"* ; do
    mv "$i" "${i}2"
  done
}

package_python-libtaxii() {
  depends=('python-lxml' 'python-requests' 'python-dateutil' 'python-six')

  cd "$_pkgname-$pkgver"

  python setup.py install --prefix=/usr --root="$pkgdir" --optimize=1 \
    --skip-build
}

