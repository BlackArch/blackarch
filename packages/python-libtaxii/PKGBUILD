# This file is part of BlackArch Linux ( https://www.blackarch.org/ ).
# See COPYING for license details.

pkgbase=python-libtaxii
pkgname=('python2-libtaxii' 'python-libtaxii')
_pkgname=libtaxii
pkgver=1.1.112
pkgrel=2
pkgdesc='TAXII Library.'
url='https://github.com/TAXIIProject/libtaxii'
license=('BSD')
arch=('any')
makedepends=('python2-setuptools' 'python-setuptools')
source=("https://github.com/TAXIIProject/$_pkgname/archive/$pkgver.tar.gz")
sha512sums=('747b0b794fb7df8e9d2e0fb1245f8ab69e8737003af8d826261729a5b2e407fda78b7424b9e3eabd246a0408230cdb4830c8bc26ba6081380c8d3bc6c2a8eab2')

prepare() {
  cp -a "$_pkgname-$pkgver" "$_pkgname-$pkgver-2"
}

build() {
  cd "$_pkgname-$pkgver-2"

  python2 setup.py build

  cd "$srcdir/$_pkgname-$pkgver"

  python setup.py build
}

package_python2-libtaxii() {
  depends=('python2-lxml' 'python2-requests' 'python2-dateutil' 'python2-six')

  cd "$_pkgname-$pkgver-2"

  python2 setup.py install --prefix=/usr --root="$pkgdir" --optimize=1 \
    --skip-build

  for i in "$pkgdir/usr/bin/"* ; do
    mv "$i" "${i}2"
  done
}

package_python-libtaxii() {
  depends=('python-lxml' 'python-requests' 'python-dateutil' 'python-six')

  cd "$_pkgname-$pkgver"

  python setup.py install --prefix=/usr --root="$pkgdir" --optimize=1 \
    --skip-build
}

