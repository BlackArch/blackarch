# This file is part of BlackArch Linux ( https://www.blackarch.org/ ).
# See COPYING for license details.

pkgname=berate_ap
pkgver=v0.4.6.r98.g4ac163f
pkgrel=1
pkgdesc='Script for orchestrating mana rogue WiFi Access Points.'
arch=('x86_64' 'aarch64')
groups=('blackarch' 'blackarch-wireless' 'blackarch-networking'
        'blackarch-spoof')
url='https://github.com/sensepost/berate_ap'
license=('BSD-2-Clause')
depends=('bash' 'util-linux' 'procps-ng' 'hostapd' 'iproute2' 'iw' 'dnsmasq'
         'iptables')
makedepends=('git')
optdepends=('haveged')
source=("git+https://github.com/sensepost/$pkgname.git")
sha512sums=('SKIP')

pkgver() {
  cd $pkgname

  ( set -o pipefail
    git describe --long --tags --abbrev=7 2>/dev/null |
      sed 's/\([^-]*-g\)/r\1/;s/-/./g' ||
    printf "%s.%s" "$(git rev-list --count HEAD)" \
      "$(git rev-parse --short=7 HEAD)"
  )
}

package() {
  cd $pkgname

  make DESTDIR="$pkgdir" install
}

