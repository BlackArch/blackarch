# This file is part of BlackArch Linux ( https://www.blackarch.org/ ).
# See COPYING for license details.

pkgname=wmi-forensics
pkgver=11.0ab08dc
pkgrel=10
pkgdesc='Scripts used to find evidence in WMI repositories.'
groups=('blackarch' 'blackarch-forensic')
arch=('any')
url='https://github.com/davidpany/WMI_Forensics'
license=('custom:unknown')
depends=('python')
makedepends=('git')
source=("$pkgname::git+https://github.com/davidpany/WMI_Forensics.git")
sha512sums=('SKIP')

pkgver() {
  cd $pkgname

  ( set -o pipefail
    git describe --long --tags --abbrev=7 2>/dev/null |
      sed 's/\([^-]*-g\)/r\1/;s/-/./g' ||
    printf "%s.%s" "$(git rev-list --count HEAD)" \
      "$(git rev-parse --short=7 HEAD)"
  )
}

package() {
  cd $pkgname

  install -Dm 755 CCM_RUA_Finder.py "$pkgdir/usr/bin/CCM_RUA_Finder"
  install -Dm 755 PyWMIPersistenceFinder.py \
    "$pkgdir/usr/bin/PyWMIPersistenceFinder"
  install -Dm 644 -t "$pkgdir/usr/share/doc/$pkgname/" README.md
}

