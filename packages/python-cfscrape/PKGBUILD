# This file is part of BlackArch Linux ( https://www.blackarch.org/ ).
# See COPYING for license details.

pkgbase=python-cfscrape
pkgname=('python2-cfscrape' 'python-cfscrape')
_pkgname=cfscrape
pkgver=2.0.8
pkgrel=1
epoch=1
pkgdesc="A small Python module to bypass Cloudflare's anti-bot page, using Requests."
arch=('any')
url='https://pypi.org/project/cfscrape/#files'
license=('MIT')
makedepends=('python2-setuptools' 'python-setuptools')
source=("https://files.pythonhosted.org/packages/f9/c2/271fbe5bbe2ca78daed14478cec7b2c1af8242c8960873d9eb841fbcf6c8/$_pkgname-$pkgver.tar.gz")
sha512sums=('8191fb975bc0cab3d255fe1d413abe41dba443af84f749825028cb84debe091dbc06dd778428a6acd4b1705aad1d3b706fbb6fd03b314c51fdeb879caff9ccb4')

prepare() {
  cp -a "$_pkgname-$pkgver"{,-2}
}

build() {
  cd "$_pkgname-$pkgver-2"

  python2 setup.py build

  cd "$srcdir/$_pkgname-$pkgver"

  python setup.py build
}

package_python2-cfscrape() {
  depends=('python2' 'python2-requests')

  cd "$_pkgname-$pkgver-2"

  python2 setup.py install --prefix=/usr --root="$pkgdir" --optimize=1 \
    --skip-build
}

package_python-cfscrape() {
  depends=('python' 'python-requests')

  cd "$_pkgname-$pkgver"

  python setup.py install --prefix=/usr --root="$pkgdir" --optimize=1 \
    --skip-build
}

