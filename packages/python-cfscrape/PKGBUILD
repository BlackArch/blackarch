# This file is part of BlackArch Linux ( https://www.blackarch.org/ ).
# See COPYING for license details.

pkgbase=python-cfscrape
pkgname=('python2-cfscrape' 'python-cfscrape')
_pkgname=cfscrape
pkgver=2.0.7
pkgrel=2
epoch=1
pkgdesc="A small Python module to bypass Cloudflare's anti-bot page, using Requests."
arch=('any')
url='https://pypi.org/project/cfscrape/#files'
license=('MIT')
makedepends=('python2-setuptools' 'python-setuptools')
source=("https://files.pythonhosted.org/packages/6c/0b/ea6730d075cabe49c1c46d330392c81ab53f09e1a196d79f50c83c000522/$_pkgname-$pkgver.tar.gz")
sha512sums=('3d3689a43c91e467391fb46ff842a04792f2bed24dc6b7561fd4dd80457e8b0a5119c3698bd0ca4fb456b92de557527d2a53d53e2478c734afd750f89a3f212d')

prepare() {
  cp -a "$_pkgname-$pkgver" "$_pkgname-$pkgver-2"
}

build() {
  cd "$_pkgname-$pkgver-2"

  python2 setup.py build

  cd "$srcdir/$_pkgname-$pkgver"

  python setup.py build
}

package_python2-cfscrape() {
  depends=('python2' 'python2-requests')

  cd "$_pkgname-$pkgver-2"

  python2 setup.py install --prefix=/usr --root="$pkgdir" --optimize=1 \
    --skip-build
}

package_python-cfscrape() {
  depends=('python' 'python-requests')

  cd "$_pkgname-$pkgver"

  python setup.py install --prefix=/usr --root="$pkgdir" --optimize=1 \
    --skip-build
}

