# This file is part of BlackArch Linux ( http://blackarch.org ).
# See COPYING for license details.

pkgname='marc4dasm'
pkgver='6.f11860f'
pkgrel=2
groups=('blackarch' 'blackarch-disassembler' 'blackarch-hardware' 'blackarch-reversing')
pkgdesc='This python-based tool is a disassembler for the Atmel MARC4 (a 4 bit Harvard micro).'
arch=('any')
url='https://github.com/ApertureLabsLtd/marc4dasm'
license=('custom:unknown')
depends=('python2')
makedepends=('git')
source=('git+https://github.com/ApertureLabsLtd/marc4dasm.git')
sha1sums=('SKIP')

pkgver() {
  cd "$srcdir/marc4dasm"

  echo $(git rev-list --count HEAD).$(git rev-parse --short HEAD)
}

prepare() {
  cd "$srcdir/marc4dasm"

  sed -i 's|/usr/bin/env python|/usr/bin/env python2|' marc4dasm.py
}

package() {
  cd "$srcdir/marc4dasm"

  install -Dm755 marc4dasm.py "$pkgdir/usr/bin/marc4dasm"
  install -Dm644 README.md "$pkgdir/usr/share/doc/marc4dasm/README.md"
}
