# This file is part of BlackArch Linux ( https://www.blackarch.org/ ).
# See COPYING for license details.

pkgbase=python-zlib_wrapper
pkgname=('python2-zlib_wrapper' 'python-zlib_wrapper')
_pkgname=zlib_wrapper
pkgver=0.1.3
pkgrel=3
pkgdesc='Wrapper around zlib with custom header crc32.'
arch=('any')
url='https://pypi.python.org/pypi/zlib_wrapper/'
license=('GPL3')
makedepends=('python2-setuptools' 'python-setuptools')
source=("https://pypi.python.org/packages/8c/a9/a1bb430ac87e9b06a48e695e500a2de76d962db531c356eb35e42d1f9cd8/$_pkgname-$pkgver.tar.gz")
sha512sums=('be4ec18b3bc373dcca201b1d4349e9ca606845112f7e8725b67ad3c1a44145651343a4037a5fa35862f9b0832348dbf10228d30eccc9256729b1d22e931bf9e4')

prepare() {
  cp -a "$_pkgname-$pkgver" "$_pkgname-$pkgver-2"
}

build() {
  cd "$_pkgname-$pkgver-2"

  python2 setup.py build

  cd "$srcdir/$_pkgname-$pkgver"

  python setup.py build
}

package_python2-zlib_wrapper() {
  depends=('python2')

  cd "$_pkgname-$pkgver-2"

  python2 setup.py install --prefix=/usr --root="$pkgdir" --optimize=1 \
    --skip-build
}

package_python-zlib_wrapper() {
  depends=('python')

  cd "$_pkgname-$pkgver"

  python setup.py install --prefix=/usr --root="$pkgdir" --optimize=1 \
    --skip-build
}

