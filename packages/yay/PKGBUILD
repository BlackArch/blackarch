# This file is part of BlackArch Linux ( https://www.blackarch.org/ ).
# See COPYING for license details.

pkgname=yay
pkgver=12.3.5
pkgrel=1
pkgdesc='Yet another yogurt. Pacman wrapper and AUR helper written in go.'
arch=('x86_64' 'aarch64')
groups=('blackarch' 'blackarch-misc')
url='https://github.com/Jguer/yay'
license=('GPL3')
depends=('pacman' 'git')
makedepends=('go-pie')
optdepends=('sudo')
source=("https://github.com/Jguer/$pkgname/archive/v$pkgver.tar.gz")
sha512sums=('0d6b1586fdb2772304b7d913970ab45eda0c153824043848947e0410ea99cc082266840f24f0a95b2c15abc97252dae9730422da6a1acfe5246d7483976285f4')

build() {
  cd "$pkgname-$pkgver"

  EXTRA_GOFLAGS="-gcflags all=-trimpath=${PWD} -asmflags all=-trimpath=${PWD}" \
    LDFLAGS="-linkmode external -extldflags \"${LDFLAGS}\"" \
    make VERSION="$pkgver" DESTDIR="$pkgdir" build
}

package() {
  cd "$pkgname-$pkgver"

  make VERSION="$pkgver" DESTDIR="$pkgdir" PREFIX=/usr install
}

