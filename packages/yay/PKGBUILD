# This file is part of BlackArch Linux ( https://www.blackarch.org/ ).
# See COPYING for license details.

pkgname=yay
pkgver=10.2.3
pkgrel=1
pkgdesc='Yet another yogurt. Pacman wrapper and AUR helper written in go.'
arch=('x86_64' 'aarch64')
groups=('blackarch' 'blackarch-misc')
url='https://github.com/Jguer/yay'
license=('GPL3')
depends=('pacman' 'git')
makedepends=('go-pie')
optdepends=('sudo')
source=("https://github.com/Jguer/$pkgname/archive/v$pkgver.tar.gz")
sha512sums=('49c99d16469ff5905d5996474695dbfb7b3bf343e1b805c1fae80853449b07a4d17f5e4d40db61153165b338576738fddaa085e100c2765dbb178840b462d6b2')

build() {
  cd "$pkgname-$pkgver"

  EXTRA_GOFLAGS="-gcflags all=-trimpath=${PWD} -asmflags all=-trimpath=${PWD}" \
    LDFLAGS="-linkmode external -extldflags \"${LDFLAGS}\"" \
    make VERSION="$pkgver" DESTDIR="$pkgdir" build
}

package() {
  cd "$pkgname-$pkgver"

  make VERSION="$pkgver" DESTDIR="$pkgdir" PREFIX=/usr install
}

