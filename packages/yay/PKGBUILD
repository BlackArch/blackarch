# This file is part of BlackArch Linux ( https://www.blackarch.org/ ).
# See COPYING for license details.

pkgname=yay
pkgver=12.5.6
pkgrel=1
pkgdesc='Yet another yogurt. Pacman wrapper and AUR helper written in go.'
arch=('x86_64' 'aarch64')
groups=('blackarch' 'blackarch-misc')
url='https://github.com/Jguer/yay'
license=('GPL3')
depends=('pacman' 'git')
makedepends=('go-pie')
optdepends=('sudo')
source=("https://github.com/Jguer/$pkgname/archive/v$pkgver.tar.gz")
sha512sums=('e1ae2803b51d68fc264768d166a3b5dd5164d582a0aae8f9c2b60a47bd004ed61828631828f17c7e44ce68627f12a95f023291c7233c9417ded8c4876fd3e771')

build() {
  cd "$pkgname-$pkgver"

  EXTRA_GOFLAGS="-gcflags all=-trimpath=${PWD} -asmflags all=-trimpath=${PWD}" \
    LDFLAGS="-linkmode external -extldflags \"${LDFLAGS}\"" \
    make VERSION="$pkgver" DESTDIR="$pkgdir" build
}

package() {
  cd "$pkgname-$pkgver"

  make VERSION="$pkgver" DESTDIR="$pkgdir" PREFIX=/usr install
}

