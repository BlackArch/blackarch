# This file is part of BlackArch Linux ( http://blackarch.org ).
# See COPYING for license details.

pkgname=yay
pkgver=8.1139
pkgrel=1
pkgdesc="Yet another yogurt. Pacman wrapper and AUR helper written in go."
arch=('x86_64' 'armv6h' 'armv7h' 'aarch64')
groups=('blackarch' 'blackarch-misc')
url='https://github.com/Jguer/yay'
license=('GPL3')
depends=('pacman' 'sudo' 'git')
makedepends=('go-pie')
source=("https://github.com/Jguer/yay/archive/v$pkgver.tar.gz")
sha512sums=('a74468b22ffedfbbfb6c33e7683a358d3e06ed0501d7390b60c6fea5d501530a24283c66b3bceb9b37e61d86f138c9f92821feab56f72f95e7293da226b0221d')

build() {
  cd "$pkgname-$pkgver"

  make VERSION="$pkgver" DESTDIR="$pkgdir" PREFIX=/usr
}

package() {
  cd "$pkgname-$pkgver"

  make VERSION=$pkgver DESTDIR="$pkgdir" PREFIX=/usr install
}

