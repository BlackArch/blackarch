# This file is part of BlackArch Linux ( https://www.blackarch.org/ ).
# See COPYING for license details.

pkgname=yay
pkgver=10.2.2
pkgrel=2
pkgdesc='Yet another yogurt. Pacman wrapper and AUR helper written in go.'
arch=('x86_64' 'aarch64')
groups=('blackarch' 'blackarch-misc')
url='https://github.com/Jguer/yay'
license=('GPL3')
depends=('pacman' 'git')
makedepends=('go-pie')
optdepends=('sudo')
source=("https://github.com/Jguer/$pkgname/archive/v$pkgver.tar.gz")
sha512sums=('6b504e5b69c7e51ba176108a7a481637fd3a41b3ba89eed782d5317e9fb6bcc7583e7d7f022cc6b1dd0bc9795f960b9abe6f82746e8a96e9d2441dfd40b0954a')

build() {
  cd "$pkgname-$pkgver"

  EXTRA_GOFLAGS="-gcflags all=-trimpath=${PWD} -asmflags all=-trimpath=${PWD}" \
    LDFLAGS="-linkmode external -extldflags \"${LDFLAGS}\"" \
    make VERSION="$pkgver" DESTDIR="$pkgdir" build
}

package() {
  cd "$pkgname-$pkgver"

  make VERSION="$pkgver" DESTDIR="$pkgdir" PREFIX=/usr install
}

