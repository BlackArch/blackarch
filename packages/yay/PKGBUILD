# This file is part of BlackArch Linux ( https://www.blackarch.org/ ).
# See COPYING for license details.

pkgname=yay
pkgver=1224.0f57ed0
pkgrel=1
pkgdesc="Yet another yogurt. Pacman wrapper and AUR helper written in go."
arch=('x86_64' 'armv6h' 'armv7h' 'aarch64')
groups=('blackarch' 'blackarch-misc')
url='https://github.com/Jguer/yay'
license=('GPL3')
depends=('pacman' 'sudo' 'git')
makedepends=('go-pie')
source=('git+https://github.com/Jguer/yay.git')
sha512sums=('SKIP')

pkgver() {
  cd $pkgname

  echo $(git rev-list --count HEAD).$(git rev-parse --short HEAD)
}

build() {
  cd "$pkgname"

  make VERSION="$pkgver" DESTDIR="$pkgdir" PREFIX=/usr
}

package() {
  cd "$pkgname"

  make VERSION="$pkgver" DESTDIR="$pkgdir" PREFIX=/usr install
}

