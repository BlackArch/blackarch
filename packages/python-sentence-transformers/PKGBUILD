# This file is part of BlackArch Linux ( https://www.blackarch.org/ ).
# See COPYING for license details.

pkgname=python-sentence-transformers
_pkgname=sentence-transformers
pkgver=2.7.0
pkgrel=1
pkgdesc='Multilingual text embeddings.'
arch=('any')
url='https://pypi.org/project/sentence-transformers/#files'
license=('MIT')
depends=('python' 'python-transformers' 'python-tokenizers' 'python-tqdm'
         'python-pytorch' 'python-torchvision' 'python-numpy'
         'python-scikit-learn' 'python-scipy' 'python-nltk'
         'python-sentencepiece' 'python-huggingface-hub')
makedepends=('python-build' 'python-pip' 'python-wheel')
source=("https://files.pythonhosted.org/packages/source/${_pkgname::1}/$_pkgname/$_pkgname-$pkgver.tar.gz")
sha512sums=('9c943bcbd7f59340b3dd45347dd0cf1e25e64eea1f604a559b9864519248873eb14170f8b3716b30a35676424e6f2c3b959d6badd427364f5f1994b9152af2af')

build() {
  cd "$_pkgname-$pkgver"

  python -m build --wheel --outdir="$startdir/dist"
}

package() {
  cd "$_pkgname-$pkgver"

  pip install \
    --verbose \
    --disable-pip-version-check \
    --no-warn-script-location \
    --ignore-installed \
    --no-compile \
    --no-deps \
    --root="$pkgdir" \
    --prefix=/usr \
    --no-index \
    --find-links="file://$startdir/dist" \
    $_pkgname
}

