# This file is part of BlackArch Linux ( https://www.blackarch.org/ ).
# See COPYING for license details.

pkgbase=python-kismet-rest
pkgname=('python2-kismet-rest' 'python-kismet-rest')
_pkgname=kismet_rest
pkgver=2019.5.1
pkgrel=1
pkgdesc='Simplified Python API for the Kismet REST interface.'
arch=('any')
url='https://pypi.org/project/kismet-rest/#files'
license=('GPL2')
makedepends=('python2-setuptools' 'python-setuptools')
source=("https://files.pythonhosted.org/packages/86/2b/d7e34a86fc9c45abd48a9afba995cfa34b6766bf9c6dbca2f32395396fd3/$_pkgname-$pkgver.tar.gz")
sha512sums=('80b378540a144799dc7b2039c6b158269c08c1c3e5a67b02d769c0b02c203f63cb49c47f60fb38aa8c3641630f7d813d4342c1df94b6d5d5e14fae4786014c51')

prepare() {
  cp -a "$_pkgname-$pkgver"{,-2}
}

build() {
  cd "$_pkgname-$pkgver-2"

  python2 setup.py build

  cd "$srcdir/$_pkgname-$pkgver"

  python setup.py build
}

package_python2-kismet-rest() {
  depends=('python2' 'python2-requests')

  cd "$_pkgname-$pkgver-2"

  python2 setup.py install --prefix=/usr --root="$pkgdir" -O1 --skip-build
}

package_python-kismet-rest() {
  depends=('python' 'python-requests')

  cd "$_pkgname-$pkgver"

  python setup.py install --prefix=/usr --root="$pkgdir" -O1 --skip-build
}

