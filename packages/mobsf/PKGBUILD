# This file is part of BlackArch Linux ( https://www.blackarch.org/ ).
# See COPYING for license details.

pkgname=mobsf
pkgver=775.8fcebdf
pkgrel=1
groups=('blackarch' 'blackarch-mobile')
pkgdesc='An intelligent, all-in-one open source mobile application (Android/iOS) automated pen-testing framework capable of performing static, dynamic analysis and web API testing.'
arch=('any')
url='https://github.com/MobSF/Mobile-Security-Framework-MobSF'
license=('GPL3')
depends=('python' 'python-django' 'python-pdfkit' 'androguard'
         'python-lxml' 'python-rsa' 'python-biplist' 'python-requests'
        )
makedepends=('git')
source=('git+https://github.com/MobSF/Mobile-Security-Framework-MobSF.git')
sha512sums=('SKIP')

pkgver() {
  cd Mobile-Security-Framework-MobSF

  echo $(git rev-list --count HEAD).$(git rev-parse --short HEAD)
}

prepare() {
  cd Mobile-Security-Framework-MobSF

  python manage.py migrate
}

package() {
  cd Mobile-Security-Framework-MobSF

  install -dm 755 "$pkgdir/usr/bin"
  install -dm 755 "$pkgdir/usr/share/$pkgname"

  install -Dm 644 -t "$pkgdir/usr/share/doc/$pkgname/" README.md
  install -Dm 644 LICENSE "$pkgdir/usr/share/licenses/$pkgname/LICENSE"

  rm README.* LICENSE

  cp -a * "$pkgdir/usr/share/$pkgname/"

  cat > "$pkgdir/usr/bin/mobsf-mobsfy" << EOF
#!/bin/sh
cd /usr/share/$pkgname
exec python mobsfy.py "\$@"
EOF

  chmod +x "$pkgdir/usr/bin/mobsf-mobsfy"

  cat > "$pkgdir/usr/bin/mobsf-manage" << EOF
#!/bin/sh
cd /usr/share/$pkgname
exec python manage.py "\$@"
EOF

  chmod +x "$pkgdir/usr/bin/mobsf-manage"
}
