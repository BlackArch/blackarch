# This file is part of BlackArch Linux ( https://www.blackarch.org/ ).
# See COPYING for license details.

pkgname=mobsf
pkgver=v4.1.3.r7.g3cf21ce
pkgrel=1
epoch=2
pkgdesc='An intelligent, all-in-one open source mobile application (Android/iOS) automated pen-testing framework capable of performing static, dynamic analysis and web API testing.'
arch=('any')
groups=('blackarch' 'blackarch-mobile')
url='https://github.com/MobSF/Mobile-Security-Framework-MobSF'
license=('GPL3')
depends=('python-django' 'python-lxml' 'python-rsa' 'python-biplist'
         'python-requests' 'python-beautifulsoup4' 'python-colorlog'
         'python-macholib' 'python-waitress' 'python-whitenoise' 'gunicorn'
         'python-psutil' 'python-shelljob' 'libxslt' 'python-asn1crypto'
         'python-oscrypto' 'python-distro' 'python-ip2location' 'python-lief'
         'python-http-tools' 'python-libsast' 'python-pdfkit' 'wkhtmltopdf'
         'python-google-play-scraper' 'androguard' 'apkid' 'quark-engine'
         'python-frida' 'python-tldextract' 'python-openstep-parser'
         'python-svgutils' 'python-arpy' 'python-django-ratelimit'
         'python-paramiko' 'python-saml' 'python-ruamel-yaml' 'python-click'
         'python-decorator' 'python' 'java-environment' 'libjpeg-turbo'
         'libffi' 'libxml2' 'python-apksigtool' 'python-tzdata' 'python-bcrypt'
         'python-packaging' 'python-psycopg2')
makedepends=('git' 'python-build' 'python-pip')
source=("$pkgname::git+https://github.com/MobSF/Mobile-Security-Framework-MobSF.git")
sha512sums=('SKIP')

pkgver() {
  cd $pkgname

  ( set -o pipefail
    git describe --long --tags --abbrev=7 2>/dev/null |
      sed 's/\([^-]*-g\)/r\1/;s/-/./g' ||
    printf "%s.%s" "$(git rev-list --count HEAD)" \
      "$(git rev-parse --short=7 HEAD)"
  )
}

build() {
  cd $pkgname

  python -m build --wheel --outdir="$startdir/dist"
}

package() {
  cd $pkgname

  pip install \
    --verbose \
    --disable-pip-version-check \
    --no-warn-script-location \
    --ignore-installed \
    --no-compile \
    --no-deps \
    --root="$pkgdir" \
    --prefix=/usr \
    --no-index \
    --find-links="file://$startdir/dist" \
    $pkgname
}

