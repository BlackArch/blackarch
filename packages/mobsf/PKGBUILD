# This file is part of BlackArch Linux ( http://blackarch.org ).
# See COPYING for license details.

pkgname=mobsf
pkgver=489.712e800
pkgrel=1
groups=('blackarch' 'blackarch-mobile')
pkgdesc='An intelligent, all-in-one open source mobile application (Android/iOS) automated pen-testing framework capable of performing static, dynamic analysis and web API testing.'
arch=('any')
url='https://github.com/ajinabraham/Mobile-Security-Framework-MobSF'
license=('GPL3')
depends=('python2' 'python2-django' 'python2-rsa' 'python2-tornado'
         'python2-pdfkit' 'python2-pyopenssl' 'python2-xhtml2pdf' 'python2-yara'
         'python2-psutil' 'python2-pysqlite' 'python2-argparse' 'python2-lxml'
         'python2-configparser' 'python2-pytz' 'python2-requests'
         'java-environment')
makedepends=('git')
source=('git+https://github.com/ajinabraham/Mobile-Security-Framework-MobSF.git')
sha512sums=('SKIP')

pkgver() {
  cd Mobile-Security-Framework-MobSF

  echo $(git rev-list --count HEAD).$(git rev-parse --short HEAD)
}

prepare() {
  cd Mobile-Security-Framework-MobSF

  python2 manage.py migrate
}

package() {
  cd Mobile-Security-Framework-MobSF

  install -dm 755 "$pkgdir/usr/bin"
  install -dm 755 "$pkgdir/usr/share/$pkgname"

  install -Dm 644 -t "$pkgdir/usr/share/doc/$pkgname/" README.md
  install -Dm 644 LICENSE "$pkgdir/usr/share/licenses/$pkgname/LICENSE"

  rm README.* LICENSE

  cp -a * "$pkgdir/usr/share/$pkgname/"

  cat > "$pkgdir/usr/bin/mobsf-mobsfy" << EOF
#!/bin/sh
cd /usr/share/$pkgname
exec python2 mobsfy.py "\$@"
EOF

  chmod a+x "$pkgdir/usr/bin/mobsf-mobsfy"

  cat > "$pkgdir/usr/bin/mobsf-manage" << EOF
#!/bin/sh
cd /usr/share/$pkgname
exec python2 manage.py "\$@"
EOF

  chmod a+x "$pkgdir/usr/bin/mobsf-manage"
}

