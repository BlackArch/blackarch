# This file is part of BlackArch Linux ( https://www.blackarch.org/ ).
# See COPYING for license details.

pkgbase=python-proglog
pkgname=('python2-proglog' 'python-proglog')
_pkgname=proglog
pkgver=0.1.10
pkgrel=1
pkgdesc='Logs and progress bars manager for Python.'
arch=('any')
url='https://pypi.org/project/proglog/#files'
license=('MIT')
makedepends=('python2-setuptools' 'python-setuptools' 'python2-wheel'
             'python-wheel' 'python2-pip' 'python-pip')
source=("https://files.pythonhosted.org/packages/bd/b9/1cfff8f6a797ea99bf7ffc8c5fd811c135837948fd90f3ea86fd5166ee4f/$_pkgname-$pkgver.tar.gz")
sha512sums=('03251548ed034aac9e001fa4cdf1b0e684eee9360c649f6b1810861f62fe54b1c5b92072f1b943d0708a5f8ee23ab24f915b9d7692991d84f029508f9efbc162')

prepare() {
  cp -a "$_pkgname-$pkgver"{,-2}
}

build() {
  cd "$_pkgname-$pkgver-2"

  python2 setup.py build

  cd "$srcdir/$_pkgname-$pkgver"

  python setup.py build
}

package_python2-proglog() {
  depends=('python2' 'python2-tqdm')

  cd "$_pkgname-$pkgver-2"

  python2 setup.py install --root="$pkgdir" --prefix=/usr -O1 --skip-build
}

package_python-proglog() {
  depends=('python' 'python-tqdm')

  cd "$_pkgname-$pkgver"

  python setup.py install --root="$pkgdir" --prefix=/usr -O1 --skip-build
}

