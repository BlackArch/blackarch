# This file is part of BlackArch Linux ( https://www.blackarch.org/ ).
# See COPYING for license details.

pkgbase=python-proglog
pkgname=('python2-proglog' 'python-proglog')
_pkgname=proglog
pkgver=0.1.9
pkgrel=4
pkgdesc='Logs and progress bars manager for Python.'
arch=('any')
url='https://pypi.org/project/proglog/#files'
license=('MIT')
makedepends=('python2-setuptools' 'python-setuptools')
source=("https://files.pythonhosted.org/packages/fe/ab/4cb19b578e1364c0b2d6efd6521a8b4b4e5c4ae6528041d31a2a951dd991/$_pkgname-$pkgver.tar.gz")
sha512sums=('9491bee1d1e15e047df19091e764dce02f5d7c698706766e5e8dc9494fd628af5a973751170b713641c8df5882f3964fcfdc8c6db66fb1d20e601cdcbf4bc019')

prepare() {
  cp -a "$_pkgname-$pkgver"{,-2}
}

build() {
  cd "$_pkgname-$pkgver-2"

  python2 setup.py build

  cd "$srcdir/$_pkgname-$pkgver"

  python setup.py build
}

package_python2-proglog() {
  depends=('python2' 'python2-tqdm')

  cd "$_pkgname-$pkgver-2"

  python2 setup.py install --prefix=/usr --root="$pkgdir" --optimize=1 \
    --skip-build
}

package_python-proglog() {
  depends=('python' 'python-tqdm')

  cd "$_pkgname-$pkgver"

  python setup.py install --prefix=/usr --root="$pkgdir" --optimize=1 \
    --skip-build
}

