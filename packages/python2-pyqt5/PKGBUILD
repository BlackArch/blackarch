# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Andrea Scarpino <andrea@archlinux.org>
# Contributor: Yichao Yu <yyc1992@gmail.com>
# Contributor: Douglas Soares de Andrade <douglas@archlinux.org>
# Contributor: riai <riai@bigfoot.com> Ben <ben@benmazer.net>

pkgbase=python2-pyqt5
pkgname=('pyqt5-common' 'python2-pyqt5')
pkgver=5.15.2
pkgrel=2
arch=('x86_64' 'aarch64')
url='https://riverbankcomputing.com/software/pyqt/intro'
license=('GPL')
makedepends=('sip5' 'python2-sip-pyqt5' 'sip' 'python2-opengl' 'python2-enum34'
             'python2-dbus' 'qt5-connectivity' 'qt5-multimedia' 'qt5-tools'
             'qt5-serialport' 'qt5-svg' 'qt5-webkit' 'qt5-websockets'
             'qt5-x11extras' 'qt5-networkauth' 'qt5-xmlpatterns'
             'qt5-remoteobjects')
source=("https://www.riverbankcomputing.com/static/Downloads/PyQt5/PyQt5-$pkgver.dev2009281246.tar.gz")
sha512sums=('9d1320dc095ac5c57d18fd075ca1c5b02cc86ef13db5e2d7ead802fe7971f11edb6eed0588f6b471f80b0353d8baeca155363f1db076817bd61e51ae50832564')

build() {
  cd "PyQt5-$pkgver.dev2009281246"

  python2 configure.py --confirm-license --sip='/usr/bin/sip' --no-sip-files \
    --qsci-api

  make
}

package_pyqt5-common(){
  pkgdesc="Common PyQt files shared between python-pyqt5 and python2-pyqt5"
  depends=('qt5-base')

  cd "PyQt5-$pkgver.dev2009281246"
  install -Dm 644 PyQt5.api "$pkgdir/usr/share/qt/qsci/api/python/PyQt5.api"

  install -d "$pkgdir"/usr/share/sip/PyQt5
  cp -a sip/* "$pkgdir"/usr/share/sip/PyQt5
}

package_python2-pyqt5(){
  pkgdesc="A set of Python 2.x bindings for the Qt5 toolkit"
  depends=('python2-sip-pyqt5' 'python2-enum34')
  optdepends=('python2-opengl: enable OpenGL 3D graphics in PyQt applications'
              'python-dbus: for python-dbus mainloop support'
              'qt5-multimedia: QtMultimedia, QtMultimediaWidgets'
              'qt5-tools: QtHelp, QtDesigner'
              'qt5-svg: QtSvg'
              'qt5-webkit: QtWebKit, QtWebKitWidgets'
              'qt5-xmlpatterns: QtXmlPatterns'
              'qt5-declarative: QtQml, qmlplugin'
              'qt5-serialport: QtSerialPort'
              'qt5-websockets: QtWebSockets'
              'qt5-connectivity: QtNfc, QtBluetooth'
              'qt5-x11extras: QtX11Extras'
              'qt5-networkauth: QtNetworkAuth'
              'qt5-remoteobjects: QtRemoteObjects')

  cd "PyQt5-$pkgver.dev2009281246"
  # INSTALL_ROOT is needed for the QtDesigner module, the other Makefiles use DESTDIR
  make DESTDIR="$pkgdir" INSTALL_ROOT="$pkgdir" install -j1

  # Remove unused py3 version of uic modules:
  rm -r "$pkgdir"/usr/lib/python*/site-packages/PyQt5/uic/port_v3

  # compile Python bytecode
  python2 -m compileall -d / "$pkgdir"/usr/lib
  python2 -O -m compileall -d / "$pkgdir"/usr/lib

  # Fix conflicts with python-pyqt5
  mv "$pkgdir"/usr/bin/{,python2-}pyuic5
  mv "$pkgdir"/usr/bin/{,python2-}pylupdate5
  mv "$pkgdir"/usr/bin/{,python2-}pyrcc5

  rm "$pkgdir"/usr/lib/qt/plugins/designer/libpyqt5.so
  rm "$pkgdir"/usr/lib/qt/plugins/PyQt5/libpyqt5qmlplugin.so
  rm "$pkgdir"/usr/share/qt/qsci/api/python/PyQt5.api
}

