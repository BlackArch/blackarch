# This file is part of BlackArch Linux ( http://blackarch.org ).
# See COPYING for license details.

pkgname=amoco
pkgver=v2.4.1.r224.g684df77
pkgrel=1
epoch=1
pkgdesc='Yet another tool for analysing binaries.'
groups=('blackarch' 'blackarch-binary' 'blackarch-reversing')
arch=('any')
url='https://github.com/bdcht/amoco'
license=('GPL2')
depends=('python2' 'python2-pyqt5' 'python2-grandalf' 'pygoocanvas'
         'python2-crysp')
makedepends=('git' 'python2-setuptools')
source=('git+https://github.com/bdcht/amoco.git')
sha512sums=('SKIP')

pkgver() {
  cd "$pkgname"

  git describe --long | sed 's/\([^-]*-g\)/r\1/;s/-/./g'
}

build() {
  cd "$srcdir/amoco"

  python2 setup.py build
}

package() {
  cd "$srcdir/amoco"

  install -dm 755 "$pkgdir/usr/bin"

  python2 setup.py install --root="$pkgdir" --optimize=1

  install -Dm 644 -t "$pkgdir/usr/share/doc/$pkgname/" README.rst doc/*
  install -Dm 644 LICENSE "$pkgdir/usr/share/licenses/$pkgname/LICENSE"
}

