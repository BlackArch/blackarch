# This file is part of BlackArch Linux ( http://blackarch.org ).
# See COPYING for license details.

pkgname='amoco'
pkgver=310.5acf6c9
pkgrel=1
pkgdesc='Yet another tool for analysing binaries.'
groups=('blackarch' 'blackarch-binary' 'blackarch-reversing')
arch=('any')
url='https://github.com/bdcht/amoco'
license=('GPL2')
depends=('python2' 'python2-pyqt5' 'python2-grandalf' 'pygoocanvas'
         'python2-crysp')
makedepends=('git' 'python2-setuptools')
source=('git+https://github.com/bdcht/amoco.git')
sha1sums=('SKIP')

pkgver() {
  cd "$srcdir/amoco"

  echo $(git rev-list --count HEAD).$(git rev-parse --short HEAD)
}

build() {
  cd "$srcdir/amoco"

  python2 setup.py build
}

package() {
  cd "$srcdir/amoco"

  mkdir -p "$pkgdir/usr/bin"

  python2 setup.py install --root="$pkgdir" --optimize=1

  install -Dm644 -t "$pkgdir/usr/share/doc/amoco/" README.rst doc/*
  install -Dm644 LICENSE "$pkgdir/usr/share/licenses/amoco/LICENSE"
}
