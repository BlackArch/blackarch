# This file is part of BlackArch Linux ( https://www.blackarch.org/ ).
# See COPYING for license details.

pkgname=python-minikerberos
_pkgname=minikerberos
pkgver=0.4.4
_pyver=3.12
pkgrel=1
epoch=1
pkgdesc='Kerberos manipulation library in pure Python.'
arch=('any')
url='https://pypi.org/project/minikerberos/#files'
license=('MIT')
depends=('python' 'python-asn1crypto' 'python-oscrypto' 'python-asysocks'
         'python-unicrypto')
makedepends=('python-setuptools')
source=("https://files.pythonhosted.org/packages/source/${_pkgname::1}/$_pkgname/$_pkgname-$pkgver.tar.gz")
sha512sums=('96016196f899b3daf3aefa5a9a0c63435825e49b9fde7ff0028da3aa22ba9444e7ddd4947c6d52d6400983dc8f5b3ca04f47dcad38ce53c8e5b4d5eb2efb3a58')

build() {
  cd "$_pkgname-$pkgver"

  python setup.py build
}

package() {
  cd "$_pkgname-$pkgver"

  python setup.py install --root="$pkgdir" --prefix=/usr -O1 --skip-build

  # workaround until https://github.com/skelsec/minikerberos/pull/30 is merged
  # and the python source package is updated
  rm -rf "$pkgdir/usr/lib/python$_pyver/site-packages/tests"
}

