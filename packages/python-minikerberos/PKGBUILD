# This file is part of BlackArch Linux ( https://www.blackarch.org/ ).
# See COPYING for license details.

pkgname=python-minikerberos
_pkgname=minikerberos
pkgver=0.4.0
_pyver=3.10
pkgrel=2
epoch=1
pkgdesc='Kerberos manipulation library in pure Python.'
arch=('any')
url='https://pypi.org/project/minikerberos/#files'
license=('MIT')
depends=('python' 'python-asn1crypto' 'python-oscrypto' 'python-asysocks'
         'python-unicrypto')
makedepends=('python-setuptools')
source=("https://files.pythonhosted.org/packages/source/${_pkgname::1}/$_pkgname/$_pkgname-$pkgver.tar.gz")
sha512sums=('73adc69648f8c7b417ea74770e96ce7381989150f6c969013b7cef513f5465254d218ad3cb7e764173e4ba5c7f398aea196668e55e0d5e38f67b349997cb74b6')

build() {
  cd "$_pkgname-$pkgver"

  python setup.py build
}

package() {
  cd "$_pkgname-$pkgver"

  python setup.py install --root="$pkgdir" --prefix=/usr -O1 --skip-build

  # workaround until https://github.com/skelsec/minikerberos/pull/30 is merged
  # and the python source package is updated
  rm -rf "$pkgdir/usr/lib/python$_pyver/site-packages/tests"
}

