# This file is part of BlackArch Linux ( http://blackarch.org ).
# See COPYING for license details.

pkgname='packetq'
pkgver=206.8fba027
pkgrel=1
pkgdesc='A tool that provides a basic SQL-frontend to PCAP-files.'
groups=('blackarch' 'blackarch-networking' 'blackarch-misc')
arch=('x86_64' 'armv6h' 'armv7h' 'aarch64')
url='https://github.com/DNS-OARC/PacketQ'
license=('GPL3')
depends=('zlib')
makedepends=('git')
source=('git+https://github.com/DNS-OARC/PacketQ.git')
sha1sums=('SKIP')

pkgver() {
  cd "$srcdir/PacketQ"

  echo $(git rev-list --count HEAD).$(git rev-parse --short HEAD)
}

build() {
  cd "$srcdir/PacketQ"

  ./autogen.sh

  ./configure --prefix=/usr

  make
}

package() {
  cd "$srcdir/PacketQ"

  mkdir -p "$pkgdir/usr/share/packetq"

  make DESTDIR="$pkgdir" install

  install -Dm644 -t "$pkgdir/usr/share/doc/packetq/" *.md CHANGES
  install -Dm644 COPYING "$pkgdir/usr/share/licenses/packetq/COPYING"

  rm *.md COPYING CHANGES

  cp -a * "$pkgdir/usr/share/packetq/"
}
