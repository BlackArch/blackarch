# This file is part of BlackArch Linux ( http://blackarch.org ).
# See COPYING for license details.

pkgname=drozer
pkgver=2.4.4
pkgrel=1
groups=('blackarch' 'blackarch-mobile' 'blackarch-scanner' 'blackarch-fuzzer')
pkgdesc='A security testing framework for Android - Precompiled binary from official repository.'
arch=('any')
url='https://github.com/mwrlabs/drozer'
license=('BSD')
depends=('jre7-openjdk' 'python2' 'python2-protobuf')
options=(!strip)
sha512sums=('39c4926477470914f646eebb22014527f70e194e8dcc2de3157ead1cf413df4c590f219ae8421476893deea05a0dc812cbba4245470bc109be301b9aabfaf418')
source=("https://github.com/mwrlabs/drozer/releases/download/${pkgver}/drozer_${pkgver}.deb")

prepare() {
  cd "$srcdir"

  bsdtar xfJ data.tar.xz
}

package() {
  cd "$srcdir"

  install -dm 755 "$pkgdir/usr/bin"
  install -dm 755 "$pkgdir/usr/share/drozer"

  cp -a usr/lib/python2.7/dist-packages/* "$pkgdir/usr/share/drozer"

  install -Dm644 usr/bin/drozer "$pkgdir/usr/share/drozer/_drozer.py"

  install -Dm644 usr/bin/drozer-complete \
    "$pkgdir/usr/share/drozer/drozer-complete"

  install -Dm644 usr/bin/drozer-repository \
    "$pkgdir/usr/share/drozer/drozer-repository"

  install -Dm644 -t "$pkgdir/usr/share/doc/drozer/" \
    usr/share/doc/drozer/README.md

  install -Dm644 usr/share/doc/drozer/LICENSE \
    "$pkgdir/usr/share/licenses/drozer/LICENSE"

  install -Dm644 etc/bash_completion.d/drozer \
    "$pkgdir/etc/bash_completion.d/drozer"

  cat > "$pkgdir/usr/bin/drozer" << EOF
#!/bin/sh
cd /usr/share/drozer
exec python2 _drozer.py "\${@}"
EOF

  chmod a+x "$pkgdir/usr/bin/drozer"

  cat > "$pkgdir/usr/bin/drozer-complete" << EOF
#!/bin/sh
cd /usr/share/drozer
exec python2 drozer-complete.py "\${@}"
EOF

  chmod a+x "$pkgdir/usr/bin/drozer-complete"

cat > "$pkgdir/usr/bin/drozer-repository" << EOF
#!/bin/sh
cd /usr/share/drozer
exec python2 drozer-repository.py "\${@}"
EOF

  chmod a+x "$pkgdir/usr/bin/drozer-repository"
}
