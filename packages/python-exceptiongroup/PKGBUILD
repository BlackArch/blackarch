# This file is part of BlackArch Linux ( https://www.blackarch.org/ ).
# See COPYING for license details.

pkgname=python-exceptiongroup
_pyname=${pkgname#python-}
pkgver=1.3.0
pkgrel=1
pkgdesc='Backport of PEP 654 (exception groups).'
arch=('any')
url='https://github.com/agronholm/exceptiongroup'
license=('MIT')
depends=('python')
makedepends=('python-build' 'python-installer' 'python-wheel' 'python-flit-scm')
source=("https://github.com/agronholm/exceptiongroup/archive/refs/tags/$pkgver.tar.gz")
sha512sums=('da4915c607d987015c64a19804a81d37347f322b49f13bc269798808f84db732536b4d319a39818107600e9666a6406fc9f922b705e4cfcf21e1ebd4ac0ddb3a')

build() {
	cd "$_pyname-$pkgver"

	export SETUPTOOLS_SCM_PRETEND_VERSION=$pkgver

  python -m build -wn
}

package() {
	cd "$_pyname-$pkgver"

  export SETUPTOOLS_SCM_PRETEND_VERSION=$pkgver

  python -m installer -d "$pkgdir" dist/*.whl

  install -Dm 644 -t "$pkgdir/usr/share/licenses/$pkgname/" LICENSE
}

