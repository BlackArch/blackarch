# This file is part of BlackArch Linux ( http://blackarch.org ).
# See COPYING for license details.

pkgname='pyersinia'
pkgver=49.73f4056
pkgrel=2
groups=('blackarch' 'blackarch-networking' 'blackarch-fuzzer'
        'blackarch-dos' 'blackarch-voip' 'blackarch-scanner'
        'blackarch-exploitation')
pkgdesc='Network attack tool like yersinia but written in Python.'
arch=('any')
url='https://github.com/nottinghamprisateam/pyersinia'
license=('MIT')
depends=('python2' 'scapy' 'python2-sphinx' 'python2-termcolor')
makedepends=('git')
source=('git+https://github.com/nottinghamprisateam/pyersinia.git')
sha1sums=('SKIP')

pkgver() {
  cd "$srcdir/pyersinia"

  echo $(git rev-list --count HEAD).$(git rev-parse --short HEAD)
}

build() {
  cd "$srcdir/pyersinia"

  python2 setup.py build
}

package() {
  cd "$srcdir/pyersinia"

  install -Dm644 -t "$pkgdir/usr/share/doc/pyersinia/" README.md CHANGELOG
  install -Dm644 LICENSE "$pkgdir/usr/share/licenses/pyersinia/LICENSE"

  python2 setup.py install --root="$pkgdir" --optimize=1
}
