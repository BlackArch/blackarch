# This file is part of BlackArch Linux ( https://www.blackarch.org/ ).
# See COPYING for license details.

pkgname=flamerobin
pkgver=2687.7d85a473
pkgrel=1
pkgdesc='A tool to handle Firebird database management.'
groups=('blackarch' 'blackarch-database')
arch=('x86_64' 'aarch64')
url='http://www.flamerobin.org/'
license=('custom:unknown')
depends=('wxgtk2' 'boost' 'boost-libs' 'libfbclient')
makedepends=('git')
source=("$pkgname::git://git.code.sf.net/p/flamerobin/flamerobin-git.git")
sha512sums=('SKIP')

pkgver() {
  cd $pkgname

  echo $(git rev-list --count HEAD).$(git rev-parse --short HEAD)
}

build() {
  cd $pkgname

  CXXFLAGS=-fpermissive ./configure --prefix=/usr \
    --with-wx-config=/usr/bin/wx-config

  make
}

package() {
  cd $pkgname

  make DESTDIR="$pkgdir" install
}

