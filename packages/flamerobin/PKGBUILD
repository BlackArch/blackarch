# This file is part of BlackArch Linux ( https://www.blackarch.org/ ).
# See COPYING for license details.

pkgname=flamerobin
pkgver=2370.c75f8618
pkgrel=2
pkgdesc='A tool to handle Firebird database management.'
groups=('blackarch' 'blackarch-database')
arch=('x86_64' 'armv6h' 'armv7h' 'aarch64')
url='http://www.flamerobin.org/'
license=('expat')
depends=('wxgtk' 'boost' 'libfbclient')
makedepends=('git')
source=('git://git.code.sf.net/p/flamerobin/flamerobin-git.git')
sha512sums=('SKIP')

pkgver() {
  cd flamerobin-git

  echo $(git rev-list --count HEAD).$(git rev-parse --short HEAD)
}

build() {
  cd flamerobin-git

  CXXFLAGS=-fpermissive ./configure --prefix=/usr \
    --with-wx-config=/usr/bin/wx-config

  make
}

package() {
  cd flamerobin-git

  make DESTDIR="$pkgdir" install
}

