# This file is part of BlackArch Linux ( https://www.blackarch.org/ ).
# See COPYING for license details.

pkgname=domato
pkgver=92.8320455
pkgrel=1
pkgdesc='DOM fuzzer.'
groups=('blackarch' 'blackarch-fuzzer')
arch=('any')
url='https://github.com/googleprojectzero/domato'
license=('Apache')
depends=('python')
makedepends=('git')
source=("git+https://github.com/googleprojectzero/$pkgname.git")
sha512sums=('SKIP')

pkgver() {
  cd $pkgname

  echo $(git rev-list --count HEAD).$(git rev-parse --short HEAD)
}

package() {
  cd $pkgname

  install -dm 755 "$pkgdir/usr/bin"
  install -dm 755 "$pkgdir/usr/share/$pkgname"

  install -Dm 644 -t "$pkgdir/usr/share/doc/$pkgname/" *.md
  install -Dm 644 LICENSE "$pkgdir/usr/share/licenses/$pkgname/LICENSE"

  rm *.md LICENSE

  cp -a * "$pkgdir/usr/share/$pkgname/"

  cat > "$pkgdir/usr/bin/$pkgname-generator" << EOF
#!/bin/sh
cd /usr/share/$pkgname
exec python generator.py "\$@"
EOF

  chmod +x "$pkgdir/usr/bin/$pkgname-generator"
}

