# This file is part of BlackArch Linux ( https://www.blackarch.org/ ).
# See COPYING for license details.

pkgname=web2ldap
pkgver=1.5.96
pkgrel=2
pkgdesc='Full-featured LDAP client running as web application.'
groups=('blackarch' 'blackarch-misc')
arch=('any')
url='https://web2ldap.de/'
license=('Apache')
depends=('python' 'python-dnspython' 'python-paramiko' 'python-asn1crypto'
         'python-xlwt' 'python-ldap0')
makedepends=('python-setuptools')
source=("https://www.web2ldap.de/download/$pkgname-$pkgver.tar.gz")
sha512sums=('108a27b4c61a10e078ba593865e189e0b0d9750f328a97fa692a27cf10bcf3d115209a486b199e7e7ead491be04e2ca9d0379194bb3cb4d8a7929c150169764a')

build() {
  cd "$pkgname-$pkgver"

  python setup.py build
}

package() {
  cd "$pkgname-$pkgver"

  python setup.py install --root="$pkgdir" --prefix=/usr -O1 --skip-build

  install -Dm 644 -t "$pkgdir/usr/share/doc/$pkgname/" README.md
  cp -a "$pkgdir/usr/etc" "$pkgdir/etc"

  chmod 644 \
    "$pkgdir/usr/lib/python3.9/site-packages/web2ldap-$pkgver-py3.9.egg-info/"*
}

