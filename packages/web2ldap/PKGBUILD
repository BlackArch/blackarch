# This file is part of BlackArch Linux ( https://www.blackarch.org/ ).
# See COPYING for license details.

pkgname=web2ldap
pkgver=1.6.21
_pyver=3.10
pkgrel=1
pkgdesc='Full-featured LDAP client running as web application.'
groups=('blackarch' 'blackarch-misc')
arch=('any')
url='https://web2ldap.de/'
license=('Apache')
depends=('python' 'python-dnspython' 'python-paramiko' 'python-asn1crypto'
         'python-xlwt' 'python-ldap0')
makedepends=('python-setuptools' 'python-pip' 'python-wheel')
source=("https://www.web2ldap.de/download/$pkgname-$pkgver.tar.gz")
sha512sums=('97cb1dd549b1a96de796a23756430bae67b509c334a1f9d84d829a4327d7e8340dde1d092a6d818892677754548b7fd2bc4f4cb36ff4fb284badae2897b1dac8')

build() {
  cd "$pkgname-$pkgver"

  python setup.py build
}

package() {
  cd "$pkgname-$pkgver"

  python setup.py install --root="$pkgdir" --prefix=/usr -O1 --skip-build

  install -Dm 644 -t "$pkgdir/usr/share/doc/$pkgname/" README.md
  cp -a "$pkgdir/usr/etc" "$pkgdir/etc"

  chmod 644 \
    "$pkgdir/usr/lib/python$_pyver/site-packages/web2ldap-$pkgver-py$_pyver.egg-info/"*
}

