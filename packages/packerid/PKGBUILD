# This file is part of BlackArch Linux ( http://blackarch.org ).
# See COPYING for license details.

pkgname='packerid'
pkgver='1.4'
pkgrel=7
groups=('blackarch' 'blackarch-binary' 'blackarch-packer' 'blackarch-reversing')
pkgdesc='Script which uses a PEiD database to identify which packer (if any) is being used by a binary.'
arch=('any')
license=('custom:unknown')
url='http://handlers.sans.org/jclausing/'
depends=('python2' 'python2-pefile')
source=('http://handlers.sans.org/jclausing/packerid.py'
        'http://handlers.sans.org/jclausing/userdb.txt')
sha1sums=('922607efa7e8534ca5038f3ffbffae136ab87fda'
          '767f0f1730c0680ef89172034812fd12fd8784e9')

prepare(){
  cd "$srcdir"

  sed -i 's|/usr/bin/python$|/usr/bin/python2|' packerid.py
  sed -i 's|/usr/local/etc|/usr/share/packerid|' packerid.py
}

package() {
  cd "$srcdir"

  install -Dm755 packerid.py "$pkgdir/usr/bin/packerid"
  install -Dm644 userdb.txt "$pkgdir/usr/share/packerid/userdb.txt"
}
