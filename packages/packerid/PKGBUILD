# This file is part of BlackArch Linux ( https://www.blackarch.org/ ).
# See COPYING for license details.

pkgname=packerid
pkgver=16.bc54e6d
pkgrel=2
pkgdesc='Script which uses a PEiD database to identify which packer (if any) is being used by a binary.'
arch=('any')
groups=('blackarch' 'blackarch-binary' 'blackarch-packer' 'blackarch-reversing')
url='https://github.com/sooshie/packerid'
license=('custom:unknown')
depends=('python' 'python-pefile' 'capstone')
makedepends=('git')
source=("git+https://github.com/sooshie/$pkgname.git")
sha512sums=('SKIP')

pkgver() {
  cd $pkgname

  ( set -o pipefail
    git describe --long --tags --abbrev=7 2>/dev/null |
      sed 's/\([^-]*-g\)/r\1/;s/-/./g' ||
    printf "%s.%s" "$(git rev-list --count HEAD)" \
      "$(git rev-parse --short=7 HEAD)"
  )
}

prepare(){
  cd $pkgname

  sed -i -e 's|/usr/local/bin|/usr/bin|' \
    -e 's|/usr/local/etc|/usr/share/packerid|' "$pkgname.py"
}

package() {
  cd $pkgname

  install -dm 755 "$pkgdir/usr/bin"
  install -dm 755 "$pkgdir/usr/share/$pkgname"

  install -Dm 644 -t "$pkgdir/usr/share/doc/$pkgname/" *.md

  cp -a * "$pkgdir/usr/share/$pkgname/"

  cat > "$pkgdir/usr/bin/$pkgname" << EOF
#!/bin/sh
exec python /usr/share/$pkgname/packerid.py "\$@"
EOF

  chmod +x "$pkgdir/usr/bin/$pkgname"
}

