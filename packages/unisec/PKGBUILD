# This file is part of BlackArch Linux ( https://www.blackarch.org/ ).
# See COPYING for license details.

pkgname=unisec
_gemname=$pkgname
pkgver=0.0.2
pkgrel=1
pkgdesc='Unicode Security Toolkit.'
arch=('any')
groups=('blackarch' 'blackarch-misc')
url='https://acceis.github.io/unisec'
#url='https://github.com/Acceis/unisec'
license=('MIT')
depends=('ruby' 'ctf-party' 'ruby-dry-cli' 'ruby-paint' 'ruby-twitter_cldr'
         'ruby-unicode-confusable')
source=("https://rubygems.org/downloads/$_gemname-$pkgver.gem")
noextract=("$_gemname-$pkgver.gem")
sha512sums=('5ba018e43c4531c98a39aeb6d547deed22aad9d4fed8f84ffbb163da4bf46726d1c16f0c0935f8126877f519efe60c13e963073199fe88434971b12c187aa2dd')

package() {
  _gemhome="$(gem env home)"

  gem install --ignore-dependencies --no-user-install --no-document \
    -i "$pkgdir/$_gemhome" -n "$pkgdir/usr/bin" "$_gemname-$pkgver.gem"

  rm "$pkgdir/$_gemhome/cache/$_gemname-$pkgver.gem"

  install -Dm 644 "$pkgdir/$_gemhome/gems/$_gemname-$pkgver/LICENSE" \
    "$pkgdir/usr/share/licenses/$pkgname/LICENSE"
}

