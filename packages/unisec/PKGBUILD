# This file is part of BlackArch Linux ( https://www.blackarch.org/ ).
# See COPYING for license details.

pkgname=unisec
_gemname=$pkgname
pkgver=0.0.3
pkgrel=1
pkgdesc='Unicode Security Toolkit.'
arch=('any')
groups=('blackarch' 'blackarch-misc')
url='https://acceis.github.io/unisec'
#url='https://github.com/Acceis/unisec'
license=('MIT')
depends=('ruby' 'ctf-party' 'ruby-dry-cli' 'ruby-paint' 'ruby-twitter_cldr'
         'ruby-unicode-confusable')
source=("https://rubygems.org/downloads/$_gemname-$pkgver.gem")
noextract=("$_gemname-$pkgver.gem")
sha512sums=('c1a7e6e720aae9402c41483c2cbc7412c4efbbfbf01727d26026f7e38c12df2454d2a440544eb7d6f2cf55c868ad79de2fb856e646ade363ba2cadbc6a3aaf3a')

package() {
  _gemhome="$(gem env home)"

  gem install --ignore-dependencies --no-user-install --no-document \
    -i "$pkgdir/$_gemhome" -n "$pkgdir/usr/bin" "$_gemname-$pkgver.gem"

  rm "$pkgdir/$_gemhome/cache/$_gemname-$pkgver.gem"

  install -Dm 644 "$pkgdir/$_gemhome/gems/$_gemname-$pkgver/LICENSE" \
    "$pkgdir/usr/share/licenses/$pkgname/LICENSE"
}

