# This file is part of BlackArch Linux ( https://www.blackarch.org/ ).
# See COPYING for license details.

pkgname=prometheus-firewall
pkgver=176.a316d66
pkgrel=10
groups=('blackarch' 'blackarch-networking')
pkgdesc='A Firewall analyzer written in ruby'
arch=('any')
url='https://github.com/averagesecurityguy/prometheus'
license=('GPL2')
depends=('ruby')
makedepends=('git')
source=("$pkgname::git+https://github.com/averagesecurityguy/prometheus.git")
sha512sums=('SKIP')

pkgver() {
  cd $pkgname

  echo $(git rev-list --count HEAD).$(git rev-parse --short HEAD)
}

package() {
  cd $pkgname

  install -dm 755 "$pkgdir/usr/bin"
  install -dm 755 "$pkgdir/usr/share/$pkgname"

  install -Dm 644 -t "$pkgdir/usr/share/doc/$pkgname/" README.md
  install -Dm 644 LICENSE "$pkgdir/usr/share/licenses/$pkgname/LICENSE"

  rm README.md LICENSE

  cp -a * "$pkgdir/usr/share/$pkgname/"

  cat > "$pkgdir/usr/bin/$pkgname" << EOF
#!/bin/sh
cd /usr/share/$pkgname
exec ruby prometheus.rb "\$@"
EOF

  chmod +x "$pkgdir/usr/bin/$pkgname"
}

