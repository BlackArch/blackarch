# This file is part of BlackArch Linux ( http://blackarch.org ).
# See COPYING for license details.

pkgname=binnavi
pkgver=6.1.0
pkgrel=3
pkgdesc='A binary analysis IDE that allows to inspect, navigate, edit and annotate control flow graphs and call graphs of disassembled code.'
groups=('blackarch' 'blackarch-disassembler' 'blackarch-reversing'
        'blackarch-binary')
arch=('any')
url='https://github.com/google/binnavi'
license=('Apache')
depends=('java-environment' 'postgresql')
makedepends=('maven' 'ant')
source=("https://github.com/google/binnavi/releases/download/v${pkgver}/binnavi-all.jar")
noextract=('binnavi-all.jar')
sha512sums=('2b14f2dc67839a208d335281ebf1b610b5b99706985871c9816cb2a69a3eb1801891600758f159adcb6b4eed3e38188f1a3d98fe1209eeab57d4c201197560b2')

package() {
  install -dm 755 "$pkgdir/usr/bin"
  install -dm 755 "$pkgdir/usr/share/binnavi"

  install -Dm644 binnavi-all.jar \
    "$pkgdir/usr/share/binnavi/binnavi-all.jar"

  cat > "$pkgdir/usr/bin/binnavi" << EOF
#!/bin/sh
exec java -jar /usr/share/binnavi/binnavi-all.jar "\$@"
EOF

  chmod a+x "$pkgdir/usr/bin/binnavi"
}

