# This file is part of BlackArch Linux ( https://www.blackarch.org/ ).
# See COPYING for license details.

pkgbase=python-beautifultable
pkgname=('python2-beautifultable' 'python-beautifultable')
_pkgname=beautifultable
pkgver=0.8.0
pkgrel=2
pkgdesc='Print ASCII tables for terminals.'
arch=('any')
url='https://pypi.org/project/beautifultable/#files'
license=('MIT')
makedepends=('python2-setuptools' 'python-setuptools')
source=("https://files.pythonhosted.org/packages/94/3b/d69cd69840eeff02beb4f35009ca0da56e5dee0e265075b3154b41f23d14/$_pkgname-$pkgver.tar.gz")
sha512sums=('ae52c4b2e353093deee9f1f9ca1cb9e09a6ef81ed7568e869a2bb0f01bf2b17c46231ad2c00f13425b5301a07ac98c8e510e38ce6bbc496663735c0783b4748f')

prepare() {
  cp -a "$_pkgname-$pkgver"{,-2}
}

build() {
  cd "$_pkgname-$pkgver-2"

  python2 setup.py build

  cd "$srcdir/$_pkgname-$pkgver"

  python setup.py build
}

package_python2-beautifultable() {
  depends=('python2' 'python2-enum34')

  cd "$_pkgname-$pkgver-2"

  python2 setup.py install --prefix=/usr --root="$pkgdir" -O1 --skip-build
}

package_python-beautifultable() {
  depends=('python')

  cd "$_pkgname-$pkgver"

  python setup.py install --prefix=/usr --root="$pkgdir" -O1 --skip-build
}

