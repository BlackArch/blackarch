# This file is part of BlackArch Linux ( https://www.blackarch.org/ ).
# See COPYING for license details.

pkgbase=python-beautifultable
pkgname=('python2-beautifultable' 'python-beautifultable')
_pkgname=beautifultable
pkgver=0.7.0
pkgrel=1
pkgdesc='Print ASCII tables for terminals.'
arch=('any')
url='https://pypi.org/project/beautifultable/#files'
license=('MIT')
makedepends=('python2-setuptools' 'python-setuptools')
source=("https://files.pythonhosted.org/packages/fa/60/c7274427e65f2beb3da102bf189effff6a34d5e958fa2c48544d6b384094/$_pkgname-$pkgver.tar.gz")
sha512sums=('5115e0c3d26fcf4a4b4616c54b7991e27125ea005847c606573763d3ea548a0b54904e13ca8fb78cb7474400186d1013056b79d635eb41735c2c86072c381ff5')

prepare() {
  cp -a "$_pkgname-$pkgver"{,-2}
}

build() {
  cd "$_pkgname-$pkgver-2"

  python2 setup.py build

  cd "$srcdir/$_pkgname-$pkgver"

  python setup.py build
}

package_python2-beautifultable() {
  depends=('python2' 'python2-enum34')

  cd "$_pkgname-$pkgver-2"

  python2 setup.py install --prefix=/usr --root="$pkgdir" -O1 --skip-build
}

package_python-beautifultable() {
  depends=('python')

  cd "$_pkgname-$pkgver"

  python setup.py install --prefix=/usr --root="$pkgdir" -O1 --skip-build
}

