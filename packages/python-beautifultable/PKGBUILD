# This file is part of BlackArch Linux ( https://www.blackarch.org/ ).
# See COPYING for license details.

pkgbase=python-beautifultable
pkgname=('python2-beautifultable' 'python-beautifultable')
_pkgname=beautifultable
pkgver=1.0.0
pkgrel=2
pkgdesc='Print ASCII tables for terminals.'
arch=('any')
url='https://pypi.org/project/beautifultable/#files'
license=('MIT')
makedepends=('python2-setuptools' 'python-setuptools')
source=("https://files.pythonhosted.org/packages/78/d0/2e98fb2c43afb3178b171cd2fd1633e765cbcb8fe4504e3308d0b1a0466b/$_pkgname-$pkgver.tar.gz")
sha512sums=('9b5e4a4329e17a1e66888796ec605d24c55b53d95ed403788a5765c1fa87cfed87f6aa219749978b03107009f4069c97660c8ce5390e368dfe62bcc5753e8a69')

prepare() {
  cp -a "$_pkgname-$pkgver"{,-2}
}

build() {
  cd "$_pkgname-$pkgver-2"

  python2 setup.py build

  cd "$srcdir/$_pkgname-$pkgver"

  python setup.py build
}

package_python2-beautifultable() {
  depends=('python2' 'python2-enum34')

  cd "$_pkgname-$pkgver-2"

  python2 setup.py install --prefix=/usr --root="$pkgdir" -O1 --skip-build
}

package_python-beautifultable() {
  depends=('python')

  cd "$_pkgname-$pkgver"

  python setup.py install --prefix=/usr --root="$pkgdir" -O1 --skip-build
}

