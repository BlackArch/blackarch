# This file is part of BlackArch Linux ( https://www.blackarch.org/ ).
# See COPYING for license details.

pkgbase=python-beautifultable
pkgname=('python2-beautifultable' 'python-beautifultable')
_pkgname=beautifultable
pkgver=1.0.1
pkgrel=1
pkgdesc='Print ASCII tables for terminals.'
arch=('any')
url='https://pypi.org/project/beautifultable/#files'
license=('MIT')
makedepends=('python2-setuptools' 'python-setuptools')
source=("https://files.pythonhosted.org/packages/a9/ab/4a36beedf6637423e7201fc6dc42458bfb37162783b4d5e4919fe3b3748f/$_pkgname-$pkgver.tar.gz")
sha512sums=('f8f575476a6a30e0307942d5ff5d202ecd9a803bca153dd790fd4dc19bda3dac9ef7c3f91d2764fe7f806582e475efdf58fcbaefd5d23ffa45aae5e6d6cac401')

prepare() {
  cp -a "$_pkgname-$pkgver"{,-2}
}

build() {
  cd "$_pkgname-$pkgver-2"

  python2 setup.py build

  cd "$srcdir/$_pkgname-$pkgver"

  python setup.py build
}

package_python2-beautifultable() {
  depends=('python2' 'python2-enum34')

  cd "$_pkgname-$pkgver-2"

  python2 setup.py install --prefix=/usr --root="$pkgdir" -O1 --skip-build
}

package_python-beautifultable() {
  depends=('python')

  cd "$_pkgname-$pkgver"

  python setup.py install --prefix=/usr --root="$pkgdir" -O1 --skip-build
}

