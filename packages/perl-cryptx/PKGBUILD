# This file is part of BlackArch Linux ( http://blackarch.org ).
# See COPYING for license details.

pkgname=perl-cryptx
pkgver=0.060
pkgrel=2
pkgdesc='Cryptographically secure random number generator.'
arch=('x86_64' 'armv6h' 'armv7h' 'aarch64')
license=('PerlArtistic' 'GPL')
options=('!emptydirs')
depends=('perl')
url='http://search.cpan.org/~mik/CryptX-0.051/lib/Crypt/PRNG.pm'
source=("http://search.cpan.org/CPAN/authors/id/M/MI/MIK/CryptX-${pkgver}.tar.gz")
sha512sums=('25be4447261780085c01089bb06e581298627f2a8b86cf003d6ae16b751a43a5d2c72fbd4719ffb1f40742fc343c71584aa7708e100a737b272394a035bd6825')

# Function to change to the working directory and set
# environment variables to override undesired options.
_enviro() {
  cd "$srcdir/CryptX-$pkgver"

  export \
    PERL_MM_USE_DEFAULT=1 \
    PERL_AUTOINSTALL=--skipdeps \
    PERL_MM_OPT="INSTALLDIRS=vendor DESTDIR='$pkgdir'" \
    PERL_MB_OPT="--installdirs vendor --destdir '$pkgdir'" \
    MODULEBUILDRC=/dev/null
}

build() {
  cd "$srcdir"

  _enviro

  /usr/bin/perl Makefile.PL

  make
}

package() {
  cd "$srcdir"

  _enviro

  make install

  # Remove "perllocal.pod" and ".packlist".
  find "$pkgdir" -name .packlist -o -name perllocal.pod -delete
}
