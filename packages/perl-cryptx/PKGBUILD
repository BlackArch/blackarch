# This file is part of BlackArch Linux ( http://blackarch.org ).
# See COPYING for license details.

pkgname='perl-cryptx'
pkgver='0.054'
pkgrel=1
pkgdesc='Cryptographically secure random number generator.'
arch=('x86_64' 'armv6h' 'armv7h' 'aarch64')
license=('PerlArtistic' 'GPL')
options=('!emptydirs')
depends=('perl')
url='http://search.cpan.org/~mik/CryptX-0.051/lib/Crypt/PRNG.pm'
source=("http://search.cpan.org/CPAN/authors/id/M/MI/MIK/CryptX-${pkgver}.tar.gz")
sha1sums=('8aa43a3344493a0625f34634c8ee1a00cc4875f5')

# Function to change to the working directory and set
# environment variables to override undesired options.
_enviro() {
  cd "$srcdir/CryptX-$pkgver"

  export \
    PERL_MM_USE_DEFAULT=1 \
    PERL_AUTOINSTALL=--skipdeps \
    PERL_MM_OPT="INSTALLDIRS=vendor DESTDIR='$pkgdir'" \
    PERL_MB_OPT="--installdirs vendor --destdir '$pkgdir'" \
    MODULEBUILDRC=/dev/null
}

build() {
  cd "$srcdir"

  _enviro

  /usr/bin/perl Makefile.PL

  make
}

package() {
  cd "$srcdir"

  _enviro

  make install

  # Remove "perllocal.pod" and ".packlist".
  find "$pkgdir" -name .packlist -o -name perllocal.pod -delete
}
