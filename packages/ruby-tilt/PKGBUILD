# Maintainer: lsfxz (https://github.com/lsfxz/quarry)
# Many thanks to anatol for quarry and gem2arch!
# This file is part of BlackArch Linux ( http://blackarch.org ).
# See COPYING for license details.
pkgname='ruby-tilt'
_gemname='tilt'
pkgver='2.0.5'
pkgrel=3
pkgdesc="Generic interface to multiple Ruby template engines"
arch=('any')
url='http://github.com/rtomayko/tilt/'
license=("MIT")
depends=('ruby')
options=('!emptydirs')
source=("https://rubygems.org/downloads/$_gemname-$pkgver.gem" )
noextract=("$_gemname-$pkgver.gem")
sha1sums=('97efbf20a6a5ca8edfe950b17788644535d6447a' )


package() {
  local _gemdir="$(ruby -rubygems -e 'puts Gem.default_dir')"
  gem install --ignore-dependencies --no-document --no-user-install -i "$pkgdir/$_gemdir" -n "$pkgdir"/usr/bin $_gemname-$pkgver.gem 
  rm "$pkgdir/$_gemdir/cache/$_gemname-$pkgver.gem"
  install -D -m644 "$pkgdir/$_gemdir/gems/$_gemname-$pkgver/COPYING" "$pkgdir/usr/share/licenses/$pkgname/COPYING"

  rm -rf "$pkgdir/$_gemdir/gems/$_gemname-$pkgver"/{CHANGELOG.md,COPYING,Gemfile,HACKING,README.md,Rakefile,docs,man,test,tilt.gemspec}
}
