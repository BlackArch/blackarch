# This file is part of BlackArch Linux ( http://blackarch.org ).
# See COPYING for license details.

pkgname='zmap'
pkgver='1.2.1'
pkgrel=1
groups=('blackarch' 'blackarch-scanner')
pkgdesc='Fast network scanner designed for Internet-wide network surveys.'
url='https://zmap.io/'
license=('Apache')
arch=('i686' 'x86_64' 'armv6h' 'armv7h')
depends=('gmp' 'libpcap')
makedepends=('byacc-noconflict' 'cmake' 'gengetopt')
backup=('etc/zmap/blacklist.conf' 'etc/zmap/zmap.conf')
source=("https://github.com/zmap/zmap/archive/v${pkgver}.tar.gz")
sha1sums=('1ce5529d8685a7b7fbca1f3b04670b1614838fa7')

build() {
  cd "$srcdir/zmap-$pkgver"

  cmake -DENABLE_HARDENING=ON

  make
}

package() {
  cd "$srcdir/zmap-$pkgver/src"

  mkdir -p "$pkgdir/etc/zmap"

  install -Dm755 zmap "$pkgdir/usr/bin/zmap"
  install -Dm644 zmap.1 "$pkgdir/usr/share/man/man1/zmap.1"
  install -m644 ../conf/* "$pkgdir/etc/zmap"

  cd ..
  for f in AUTHORS CHANGELOG INSTALL README; do
    install -Dm644 $f "$pkgdir/usr/share/doc/zmap/$f";
  done

  cp -a examples "$pkgdir/usr/share/doc/zmap"
}
