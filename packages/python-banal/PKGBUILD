# This file is part of BlackArch Linux ( https://www.blackarch.org/ ).
# See COPYING for license details.

pkgbase=python-banal
pkgname=('python2-banal' 'python-banal')
_pkgname=banal
pkgver=0.4.2
pkgrel=2
pkgdesc='Commons of banal micro-functions for Python.'
arch=('any')
url='https://pypi.org/project/banal/#files'
license=('MIT')
makedepends=('python2-setuptools' 'python-setuptools')
source=("https://files.pythonhosted.org/packages/ad/40/bea784ea35134ab8f11844c4c0f8f89b311031676d4894ee9926e2f64fa7/$_pkgname-$pkgver.tar.gz")
sha512sums=('54af077b1136802153ae24557d128fe187b563cadc4b7855d7f8e486f7db36816bb1eefe587c52629c20862e894b94eeb7fa2a7f95f07e1cd68f41be2d00541a')

prepare() {
  cp -a "$_pkgname-$pkgver"{,-2}
}

build() {
  cd "$_pkgname-$pkgver-2"

  python2 setup.py build

  cd "$srcdir/$_pkgname-$pkgver"

  python setup.py build
}

package_python2-banal() {
  depends=('python2' 'python2-six')

  cd "$_pkgname-$pkgver-2"

  python2 setup.py install --prefix=/usr --root="$pkgdir" --optimize=1 \
    --skip-build
}

package_python-banal() {
  depends=('python' 'python-six')

  cd "$_pkgname-$pkgver"

  python setup.py install --prefix=/usr --root="$pkgdir" --optimize=1 \
    --skip-build
}

