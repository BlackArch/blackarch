# This file is part of BlackArch Linux ( https://www.blackarch.org/ ).
# See COPYING for license details.

pkgbase=python-banal
pkgname=('python2-banal' 'python-banal')
_pkgname=banal
pkgver=1.0.1
pkgrel=1
pkgdesc='Commons of banal micro-functions for Python.'
arch=('any')
url='https://pypi.org/project/banal/#files'
license=('MIT')
makedepends=('python2-setuptools' 'python-setuptools')
source=("https://files.pythonhosted.org/packages/f2/9a/2494c3fcdf194d2bf737912c27944e448e6e009f71a1c0acb8b8acf9dbcd/$_pkgname-$pkgver.tar.gz")
sha512sums=('3279944f5bc63897e989d36b3cc4097b7f6d03ef2ce958140d21e907a0143a80d2d24ae723b6c62476a24d99f9677d4d5a406a908c6d14698bd07740502040da')

prepare() {
  cp -a "$_pkgname-$pkgver"{,-2}
}

build() {
  cd "$_pkgname-$pkgver-2"

  python2 setup.py build

  cd "$srcdir/$_pkgname-$pkgver"

  python setup.py build
}

package_python2-banal() {
  depends=('python2' 'python2-six')

  cd "$_pkgname-$pkgver-2"

  python2 setup.py install --prefix=/usr --root="$pkgdir" --optimize=1 \
    --skip-build
}

package_python-banal() {
  depends=('python' 'python-six')

  cd "$_pkgname-$pkgver"

  python setup.py install --prefix=/usr --root="$pkgdir" --optimize=1 \
    --skip-build
}

