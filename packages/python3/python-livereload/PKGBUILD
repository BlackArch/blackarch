# This file is part of BlackArch Linux ( https://www.blackarch.org/ ).
# See COPYING for license details.

pkgbase='python-livereload'
pkgname=('python2-livereload' 'python-livereload')
_pkgname='livereload'
pkgver=2.6.1
pkgrel=1
pkgdesc='An awesome tool for web developers.'
arch=('any')
url='https://github.com/lepture/python-livereload'
license=('BSD')
depends=('python' 'python2')
makedepends=('python-setuptools' 'python2-setuptools')
source=("https://files.pythonhosted.org/packages/27/26/85ba3851d2e4905be7d2d41082adca833182bb1d7de9dfc7f623383d36e1/$_pkgname-$pkgver.tar.gz")
sha512sums=('77449618e687102123921015ca3c7a15bccefc5a193470c87eb358989fd3188ebc7f67c6cff02aa1215f6f22d89252a11386d564d554d2e280394f38a27774b6')

prepare() {
  cp -a "$_pkgname-$pkgver"{,-2}
}

build() {
  cd "$_pkgname-$pkgver-2"

  python2 setup.py build

  cd "$srcdir/$_pkgname-$pkgver"

  python setup.py build
}

package_python2-livereload() {
  depends=('python2' 'python2-six' 'python2-tornado')

  cd "$_pkgname-$pkgver-2"

  python2 setup.py install --root="$pkgdir" --prefix=/usr -O1 --skip-build

  mv "$pkgdir/usr/bin/$_pkgname" "$pkgdir/usr/bin/${_pkgname}2"
}

package_python-livereload() {
  depends=('python' 'python-six' 'python-tornado')

  cd "$_pkgname-$pkgver"

  python setup.py install --root="$pkgdir" --prefix=/usr -O1 --skip-build
}

