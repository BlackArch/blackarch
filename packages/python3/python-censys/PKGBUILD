# This file is part of BlackArch Linux ( https://www.blackarch.org/ ).
# See COPYING for license details.

pkgbase=python-censys
pkgname=('python2-censys' 'python-censys')
_pkgname=censys
pkgver=1.0.0
pkgrel=1
pkgdesc='Python library for interacting with Censys Search Engine (censys.io).'
arch=('any')
url='https://pypi.org/project/censys/#files'
license=('Apache')
makedepends=('python2-setuptools' 'python-setuptools')
source=("https://files.pythonhosted.org/packages/54/29/a816154ed9795234a9b70fe783b52a1594eb62c2066e51f1170f9c3b3b00/$_pkgname-$pkgver.tar.gz")
sha512sums=('8a24fa679643d410c8110fffcd9b317810ee5440f0e78a56bc1af2c9349ec124d32e91fb7608d902c487aeaef2d9dc5e60e40785cea37b37f7ae025e931c387a')

prepare() {
  cp -a "$_pkgname-$pkgver"{,-2}
}

build() {
  cd "$_pkgname-$pkgver-2"

  python2 setup.py build

  cd "$srcdir/$_pkgname-$pkgver"

  python setup.py build
}

package_python2-censys() {
  depends=('python2' 'python2-requests' 'python2-netaddr')

  cd "$_pkgname-$pkgver-2"

  python2 setup.py install --prefix=/usr --root="$pkgdir" --optimize=1 \
    --skip-build

  mv "$pkgdir/usr/bin/$_pkgname" "$pkgdir/usr/bin/${_pkgname}2"
}

package_python-censys() {
  depends=('python' 'python-requests' 'python-netaddr')

  cd "$_pkgname-$pkgver"

  python setup.py install --prefix=/usr --root="$pkgdir" --optimize=1 \
    --skip-build
}

