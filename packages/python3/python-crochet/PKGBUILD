# This file is part of BlackArch Linux ( https://www.blackarch.org/ ).
# See COPYING for license details.

pkgbase=python-crochet
pkgname=('python2-crochet' 'python-crochet')
_pkgname=crochet
pkgver=1.12.0
pkgrel=1
pkgdesc='Use Twisted anywhere! - An MIT-licensed library that makes it easier to use Twisted from regular blocking code.'
url='https://pypi.org/project/crochet/'
license=('MIT')
arch=('any')
makedepends=('python2-setuptools' 'python-setuptools')
source=("https://files.pythonhosted.org/packages/fc/c1/d77a1e49fc3cd7a8da3137d865ff17fb57ecb9c475f87b007b6c02639bb2/$_pkgname-$pkgver.tar.gz")
sha512sums=('bdd7e68bb4f478c29d993047ed484dfbbfd2a5c6cd33eb8f31c2b7cefdb69321efb09c741338beb1cca10480371ded821682d1116b9e520c2676d4ba388c5b7b')

prepare() {
  cp -a "$_pkgname-$pkgver"{,-2}
}

build() {
  cd "$_pkgname-$pkgver-2"

  python2 setup.py build

  cd "$srcdir/$_pkgname-$pkgver"

  python setup.py build
}

package_python2-crochet() {
  depends=('python2' 'python2-twisted' 'python2-wrapt')

  cd "$_pkgname-$pkgver-2"

  python2 setup.py install --prefix=/usr --root="$pkgdir" --optimize=1 \
    --skip-build
}

package_python-crochet() {
  depends=('python' 'python-twisted' 'python-wrapt')

  cd "$_pkgname-$pkgver"

  python setup.py install --prefix=/usr --root="$pkgdir" --optimize=1 \
    --skip-build
}

