# This file is part of BlackArch Linux ( https://blackarch.org )
# See COPYING for license details.

pkgbase=python-flask-httpauth
pkgname=('python2-flask-httpauth' 'python-flask-httpauth')
_pkgname=Flask-HTTPAuth
pkgver=4.1.0
pkgrel=1
pkgdesc='Basic and Digest HTTP authentication for Flask routes.'
arch=('any')
url='https://pypi.org/project/Flask-httpauth/#files'
makedepends=('python2-setuptools' 'python-setuptools')
license=('MIT')
source=("https://files.pythonhosted.org/packages/79/a0/ff7dc053f03c49f1614a79171d15f58cc7d824c85b1ba11d0df6ef3aa92b/Flask-HTTPAuth-$pkgver.tar.gz")
sha512sums=('5d4a88d04363a1dee5969d437fd05464c577f6ed8c04a254226d29d50150de2b8284e0e6a2e441ddff431e611510838546f5bf7f0f999e8817b2ef54928b6f08')

prepare() {
  cp -a "$_pkgname-$pkgver"{,-2}
}

build() {
  cd "$_pkgname-$pkgver-2"

  python2 setup.py build

  cd "$srcdir/$_pkgname-$pkgver"

  python setup.py build
}

package_python2-flask-httpauth() {
  depends=('python2' 'python2-flask')

  cd "$_pkgname-$pkgver-2"

  python2 setup.py install --prefix=/usr --root="$pkgdir" --optimize=1 \
    --skip-build
}

package_python-flask-httpauth() {
  depends=('python' 'python-flask')

  cd "$_pkgname-$pkgver"

  python setup.py install --prefix=/usr --root="$pkgdir" --optimize=1 \
    --skip-build
}

