# This file is part of BlackArch Linux ( https://www.blackarch.org/ ).
# See COPYING for license details.

pkgbase=python-scp
pkgname=('python2-scp' 'python-scp')
_pkgname=scp
pkgver=0.13.2
pkgrel=3
pkgdesc='SCP module for paramiko.'
arch=('any')
url='https://pypi.org/project/scp/#files'
license=('GPL')
makedepends=('python2-setuptools' 'python-setuptools')
source=("https://files.pythonhosted.org/packages/05/e0/ac4169e773e12a08d941ca3c006cb8c91bee9d6d80328a15af850b5e7480/$_pkgname-$pkgver.tar.gz")
sha512sums=('76c9f80efd95dc3f41013c133c34e0ba1ec988c6ca5ba8339cc7b393c2c47c866a373d85281cd4987f9d0c33512693c20788bb201244477050c3c61179cb47c8')

prepare() {
  cp -a "$_pkgname-$pkgver"{,-2}
}

build() {
  cd "$_pkgname-$pkgver-2"

  python2 setup.py build

  cd "$srcdir/$_pkgname-$pkgver"

  python setup.py build
}

package_python2-scp() {
  depends=('python2')

  cd "$_pkgname-$pkgver-2"

  python2 setup.py install --prefix=/usr --root="$pkgdir" --optimize=1 \
    --skip-build
}

package_python-scp() {
  depends=('python')

  cd "$_pkgname-$pkgver"

  python setup.py install --prefix=/usr --root="$pkgdir" --optimize=1 \
    --skip-build
}

