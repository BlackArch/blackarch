# This file is part of BlackArch Linux ( https://www.blackarch.org/ ).
# See COPYING for license details.

pkgbase=python-liblnk-python
pkgname=('python2-liblnk-python' 'python-liblnk-python')
_pkgname=liblnk
pkgver=20191221
pkgrel=3
pkgdesc='Library and tools to access the Windows Shortcut File (LNK) format.'
url='https://github.com/libyal/liblnk'
arch=('x86_64' 'aarch64')
license=('LGPL3')
makedepends=('python2-setuptools' 'python-setuptools')
source=("https://files.pythonhosted.org/packages/d8/2e/ebaac6f1e41e3c9df6dadf2ab8dc2cdf6ef031e1be7f0a92948b30df1f5f/$_pkgname-python-$pkgver.tar.gz")
sha512sums=('6f1400016fbb3c43ad575af9fe03be6d7900c161e4d20bcc654f73ea4f05976d080610163bbec0f084005bab72a725a4ab5ac1251e66d63e30dc952fc6c3dd95')

prepare() {
  cp -a "$_pkgname-$pkgver"{,-2}
}

build() {
  cd "$_pkgname-$pkgver-2"

  python2 setup.py build

  cd "$srcdir/liblnk-$pkgver"

  python setup.py build
}

package_python2-liblnk-python() {
  depends=('python2')

  cd "$_pkgname-$pkgver-2"

  python2 setup.py install --root="$pkgdir" --prefix=/usr -O1 --skip-build
}

package_python-liblnk-python() {
  depends=('python')

  cd "$_pkgname-$pkgver"

  python setup.py install --root="$pkgdir" --prefix=/usr -O1 --skip-build
}

