# This file is part of BlackArch Linux ( https://blackarch.org )
# See COPYING for license details.

pkgbase=python-flask-mongoengine
pkgname=('python2-flask-mongoengine' 'python-flask-mongoengine')
_pkgname=flask-mongoengine
pkgver=0.9.5
pkgrel=3
pkgdesc='A Flask extension that provides integration with MongoEngine and WTF model forms.'
arch=('any')
url='https://pypi.org/project/flask-mongoengine/#files'
makedepends=('python2-setuptools' 'python-setuptools')
license=('BSD')
source=("https://files.pythonhosted.org/packages/20/53/1bb8ad34ad5c2047a11651290325e55086bc18fce7cfdbbe6f5522bd0ae5/$_pkgname-$pkgver.tar.gz")
sha512sums=('d747a330a2653f1fcc367f50e14afd6ab6ae77b49c23e7b038b975050c1c7de6bcfd01fecf962d61e06a6d4dee7b2cf5db3181e6d01cc0266d0c19cf6f627ea7')

prepare() {
  cp -a "$_pkgname-$pkgver"{,-2}
}

build() {
  cd "$_pkgname-$pkgver-2"

  python2 setup.py build

  cd "$srcdir/$_pkgname-$pkgver"

  python setup.py build
}

package_python2-flask-mongoengine() {
  depends=('python2' 'python2-flask' 'python2-flask-wtf' 'python2-mongoengine'
           'python2-six' 'python2-blinker')

  cd "$_pkgname-$pkgver-2"

  python2 setup.py install --prefix=/usr --root="$pkgdir" --optimize=1 \
    --skip-build
}

package_python-flask-mongoengine() {
  depends=('python' 'python-flask' 'python-flask-wtf' 'python-mongoengine'
           'python-six' 'python-blinker')

  cd "$_pkgname-$pkgver"

  python setup.py install --prefix=/usr --root="$pkgdir" --optimize=1 \
    --skip-build
}

