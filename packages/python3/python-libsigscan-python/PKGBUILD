# This file is part of BlackArch Linux ( https://www.blackarch.org/ ).
# See COPYING for license details.

pkgbase=python-libsigscan-python
pkgname=('python2-libsigscan-python' 'python-libsigscan-python')
_pkgname=libsigscan
__pkgname=libsigscan-python
pkgver=20191221
pkgrel=1
pkgdesc='Python bindings module for libsigscan.'
arch=('x86_64' 'aarch64')
url='https://pypi.python.org/pypi/libsigscan-python/'
license=('LGPL3')
makedepends=('python2-setuptools' 'python-setuptools')
source=("https://files.pythonhosted.org/packages/bc/8e/674f60fc7d68797df2d650053e55f3a9638cc4c0eb9ef9e9701afc4fae28/$__pkgname-$pkgver.tar.gz")
sha512sums=('d5d8525f9eb4a7674c57ea7040ebad8402b894ebf5a9e0e75986b98c8c154e4a90dd8a38dbe3a67a4da659a2bdf84895394519ac68084f65554c17a0bd7462cc')

prepare() {
  cp -a "$_pkgname-$pkgver"{,-2}
}

build() {
  cd "$_pkgname-$pkgver-2"

  python2 setup.py build

  cd "$srcdir/$_pkgname-$pkgver"

  python setup.py build
}

package_python2-libsigscan-python() {
  depends=('python2')

  cd "$_pkgname-$pkgver-2"

  python2 setup.py install --root="$pkgdir" --prefix=/usr -O1 --skip-build
}

package_python-libsigscan-python() {
  depends=('python')

  cd "$_pkgname-$pkgver"

  python setup.py install --root="$pkgdir" --prefix=/usr -O1 --skip-build
}

