# This file is part of BlackArch Linux ( https://www.blackarch.org/ ).
# See COPYING for license details.

pkgbase=python-libtaxii
pkgname=('python2-libtaxii' 'python-libtaxii')
_pkgname=libtaxii
pkgver=1.1.116
pkgrel=1
pkgdesc='TAXII Library.'
url='https://github.com/TAXIIProject/libtaxii'
license=('BSD')
arch=('any')
makedepends=('python2-setuptools' 'python-setuptools')
source=("https://files.pythonhosted.org/packages/06/72/0a7a8833fa28440f0157077ee891b197018e0dad00365f7aaf700be1ff9c/$_pkgname-$pkgver.tar.gz")
sha512sums=('d9f791c9594dbcc72dbe55679d6acc16c5acc2ba21759f76e98434039cca90a9adf93c351d931c32ed67e94ba64513985ff2f44a20f507a56415a37ff11e1c2e')

prepare() {
  cp -a "$_pkgname-$pkgver"{,-2}
}

build() {
  cd "$_pkgname-$pkgver-2"

  python2 setup.py build

  cd "$srcdir/$_pkgname-$pkgver"

  python setup.py build
}

package_python2-libtaxii() {
  depends=('python2-lxml' 'python2-requests' 'python2-dateutil' 'python2-six')

  cd "$_pkgname-$pkgver-2"

  python2 setup.py install --prefix=/usr --root="$pkgdir" -O1 --skip-build

  for i in "$pkgdir/usr/bin/"* ; do
    mv "$i" "${i}2"
  done
}

package_python-libtaxii() {
  depends=('python-lxml' 'python-requests' 'python-dateutil' 'python-six')

  cd "$_pkgname-$pkgver"

  python setup.py install --prefix=/usr --root="$pkgdir" -O1 --skip-build
}

