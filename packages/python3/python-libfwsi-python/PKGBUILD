# This file is part of BlackArch Linux ( https://www.blackarch.org/ ).
# See COPYING for license details.

pkgbase=python-libfwsi-python
pkgname=('python2-libfwsi-python' 'python-libfwsi-python')
_pkgname=libfwsi
pkgver=20191221
pkgrel=1
pkgdesc='Library to access the Windows Shell Item format.'
url='https://github.com/libyal/libfwsi'
arch=('x86_64' 'aarch64')
license=('LGPLv3')
makedepends=('python2-setuptools' 'python-setuptools')
source=("https://files.pythonhosted.org/packages/8f/4e/e8e6103e2a82cffcf12c86659a823d5ec4555d4956c1092eb8689d2fa969/$_pkgname-python-$pkgver.tar.gz")
sha512sums=('f349fae221ac7fc7cd4d97d19206e1ed5ad6da685c04689e38b0e95f1d9c699e35dcdd3efa66d66b5c3024057efbb8cf2a987e1f6a8085c527c91ff38a95322c')

prepare() {
  cp -a "$_pkgname-$pkgver"{,-2}
}

build() {
  cd "$_pkgname-$pkgver-2"

  python2 setup.py build

  cd "$srcdir/$_pkgname-$pkgver"

  python setup.py build
}

package_python2-libfwsi-python() {
  depends=('python2')

  cd "$_pkgname-$pkgver-2"

  python2 setup.py install --root="$pkgdir" --prefix=/usr -O1 --skip-build
}

package_python-libfwsi-python() {
  depends=('python')

  cd "$_pkgname-$pkgver"

  python setup.py install --root="$pkgdir" --prefix=/usr -O1 --skip-build
}

