# This file is part of BlackArch Linux ( https://www.blackarch.org/ ).
# See COPYING for license details.

pkgbase=python-bitstring
pkgname=('python2-bitstring' 'python-bitstring')
_pkgname=bitstring
pkgver=3.1.7
pkgrel=1
pkgdesc='Simple construction, analysis and modification of binary data.'
arch=('any')
url='https://pypi.org/project/bitstring/#files'
license=('MIT')
makedepends=('python2-setuptools' 'python-setuptools' 'unzip')
options=(!emptydirs)
source=("https://files.pythonhosted.org/packages/c3/fc/ffac2c199d2efe1ec5111f55efeb78f5f2972456df6939fea849f103f9f5/$_pkgname-$pkgver.tar.gz")
sha512sums=('3c05222b6a6a885664adebb1ea455ef4e199c91698b89bc136a7d3f5f7ceb38868c9883509ba4067c163e1521f85c36abbff9763a590cbfa99705fd08d2a6a6f')

prepare() {
  cp -a "$_pkgname-$pkgver"{,-2}
}

build() {
  cd "$_pkgname-$pkgver-2"

  python2 setup.py build

  cd "$srcdir/$_pkgname-$pkgver"

  python setup.py build
}

package_python2-bitstring() {
  depends=('python2')

  cd "$_pkgname-$pkgver-2"

  python2 setup.py install --prefix=/usr --root="$pkgdir" --optimize=1 \
    --skip-build
}

package_python-bitstring() {
  depends=('python')

  cd "$_pkgname-$pkgver"

  python setup.py install --prefix=/usr --root="$pkgdir" --optimize=1 \
    --skip-build
}

