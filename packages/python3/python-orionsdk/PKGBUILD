# This file is part of BlackArch Linux ( https://www.blackarch.org/ ).
# See COPYING for license details.

pkgbase=python-orionsdk
pkgname=('python2-orionsdk' 'python-orionsdk')
_pkgname=orionsdk
pkgver=0.3.0
pkgrel=2
pkgdesc='Nanosecond resolution temporal types.'
arch=('any')
url='https://pypi.org/project/orionsdk/#files'
license=('APACHE')
makedepends=('python2-setuptools' 'python-setuptools')
source=("https://files.pythonhosted.org/packages/b2/9a/ac58ff6034f7f937f7ffb52773cbf213cbd184986c476898b53dc763c70f/$_pkgname-$pkgver.tar.gz")
sha512sums=('186ee4957d29298fd5dacf81c65730d8b4cd8d617e67006acb720344c1c48847a7d56e6f82b683d22929c7b5f68bc21bce9d9d164bcd50dcd2dc3fe6eba6413c')

prepare() {
  cp -a "$_pkgname-$pkgver"{,-2}
}

build() {
  cd "$_pkgname-$pkgver-2"

  python2 setup.py build

  cd "$srcdir/$_pkgname-$pkgver"

  python setup.py build
}

package_python2-orionsdk() {
  depends=('python2' 'python2-requests' 'python2-six')

  cd "$_pkgname-$pkgver-2"

  python2 setup.py install --prefix=/usr --root="$pkgdir" --optimize=1 \
    --skip-build
}

package_python-orionsdk() {
  depends=('python' 'python-requests' 'python-six')

  cd "$_pkgname-$pkgver"

  python setup.py install --prefix=/usr --root="$pkgdir" --optimize=1 \
    --skip-build
}

