# This file is part of BlackArch Linux ( https://www.blackarch.org/ ).
# See COPYING for license details.

pkgname=procdump
pkgver=12.1d03045
pkgrel=1
pkgdesc='Generate coredumps based off performance triggers.'
arch=('x86_64' 'armv6h' 'armv7h' 'aarch64')
groups=('blackarch' 'blackarch-binary' 'blackarch-misc')
url='https://github.com/Microsoft/ProcDump-for-Linux'
license=('MIT')
depends=('gdb')
makedepends=('git')
source=('git+https://github.com/Microsoft/ProcDump-for-Linux.git')
sha512sums=('SKIP')

pkgver() {
  cd ProcDump-for-Linux

  echo $(git rev-list --count HEAD).$(git rev-parse --short HEAD)
}

build() {
  cd ProcDump-for-Linux

  make -j1
}

package() {
  cd ProcDump-for-Linux

  install -Dm 755 bin/procdump "$pkgdir/usr/bin/$pkgname"
  install -Dm 644 -t "$pkgdir/usr/share/doc/$pkgname" *.md
  install -Dm 644 procdump.1 "$pkgdir/usr/share/man/man1/procdump.1"
  install -Dm 644 LICENSE "$pkgdir/usr/share/licenses/$pkgname/LICENSE"
}

