# This file is part of BlackArch Linux ( http://blackarch.org ).
# See COPYING for license details.
pkgname='ruby-thor'
_gemname='thor'
pkgver='0.19.4'
pkgrel=2
pkgdesc="Thor is a toolkit for building powerful command-line interfaces."
arch=('any')
url='http://whatisthor.com/'
license=("MIT")
depends=('ruby')
options=('!emptydirs')
source=("https://rubygems.org/downloads/$_gemname-$pkgver.gem" )
noextract=("$_gemname-$pkgver.gem")
sha1sums=('be200759879238e3d250c77bda59ca130d983ab2' )


package() {
  local _gemdir="$(ruby -rubygems -e 'puts Gem.default_dir')"
  gem install --ignore-dependencies --no-document --no-user-install -i "$pkgdir/$_gemdir" -n "$pkgdir"/usr/bin $_gemname-$pkgver.gem 
  rm "$pkgdir/$_gemdir/cache/$_gemname-$pkgver.gem"
  install -D -m644 "$pkgdir/$_gemdir/gems/$_gemname-$pkgver/LICENSE.md" "$pkgdir/usr/share/licenses/$pkgname/LICENSE.md"

  # Preserve library and other required directories. Remove junk that gem installs.
  local _copydir=`mktemp -d -t $_gemname.XXX`
  chmod 755 $_copydir
  (cd "$pkgdir/$_gemdir/gems/$_gemname-$pkgver"/ && cp -r --parents lib bin $_copydir)
  rm -rf "$pkgdir/$_gemdir/gems/$_gemname-$pkgver"
  mv $_copydir "$pkgdir/$_gemdir/gems/$_gemname-$pkgver"

}
