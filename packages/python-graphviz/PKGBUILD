# This file is part of BlackArch Linux ( https://www.blackarch.org/ ).
# See COPYING for license details.

pkgbase=python-graphviz
pkgname=('python2-graphviz' 'python-graphviz')
_pkgname=graphviz
pkgver=0.15
pkgrel=2
pkgdesc='Simple Python interface for Graphviz.'
arch=('any')
url='https://pypi.org/project/graphviz/#files'
license=('MIT')
makedepends=('python2-setuptools' 'python-setuptools')
source=("https://files.pythonhosted.org/packages/33/c4/82459071796f59ef218d3c22d43d35aa0fbcf74f9fcce8829672febd7f5e/$_pkgname-$pkgver.zip")
sha512sums=('c2a77f0d52595a70b4f8ccd906907453b59622560f0ff84e267e874f5a1dc9a4b721192d236b0519ee7ec85bae9b225b9919ae51bcf599c93bd5816934afd6cc')

prepare() {
  cp -a "$_pkgname-$pkgver"{,-2}
}

build() {
  cd "$_pkgname-$pkgver-2"

  python2 setup.py build

  cd "$srcdir/$_pkgname-$pkgver"

  python setup.py build
}

package_python2-graphviz() {
  depends=('python2')

  cd "$_pkgname-$pkgver-2"

  python2 setup.py install --prefix=/usr --root="$pkgdir" -O1 --skip-build
}

package_python-graphviz() {
  depends=('python')

  cd "$_pkgname-$pkgver"

  python setup.py install --prefix=/usr --root="$pkgdir" -O1 --skip-build
}

