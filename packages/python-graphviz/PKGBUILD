# This file is part of BlackArch Linux ( https://www.blackarch.org/ ).
# See COPYING for license details.

pkgbase=python-graphviz
pkgname=('python2-graphviz' 'python-graphviz')
_pkgname=graphviz
pkgver=0.16
pkgrel=1
pkgdesc='Simple Python interface for Graphviz.'
arch=('any')
url='https://pypi.org/project/graphviz/#files'
license=('MIT')
makedepends=('python2-setuptools' 'python-setuptools')
source=("https://files.pythonhosted.org/packages/cc/a3/8f49063fee2037892f66f1a4d55da8ba25235e76dc27887f7edf95272154/$_pkgname-$pkgver.zip")
sha512sums=('7896901decf712657d4cb16fad58eab8492e1476e60a32b1a7fa37ede2e9c47ed31c00b7dedcaafdee291e2cfe6b2e2affa352282665782829fd3fb2f30bdf92')

prepare() {
  cp -a "$_pkgname-$pkgver"{,-2}
}

build() {
  cd "$_pkgname-$pkgver-2"

  python2 setup.py build

  cd "$srcdir/$_pkgname-$pkgver"

  python setup.py build
}

package_python2-graphviz() {
  depends=('python2')

  cd "$_pkgname-$pkgver-2"

  python2 setup.py install --prefix=/usr --root="$pkgdir" -O1 --skip-build
}

package_python-graphviz() {
  depends=('python')

  cd "$_pkgname-$pkgver"

  python setup.py install --prefix=/usr --root="$pkgdir" -O1 --skip-build
}

