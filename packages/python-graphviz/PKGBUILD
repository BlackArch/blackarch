# This file is part of BlackArch Linux ( https://www.blackarch.org/ ).
# See COPYING for license details.

pkgbase=python-graphviz
pkgname=('python2-graphviz' 'python-graphviz')
_pkgname=graphviz
pkgver=0.14.2
pkgrel=1
pkgdesc='Simple Python interface for Graphviz.'
arch=('any')
url='https://pypi.org/project/graphviz/#files'
license=('MIT')
makedepends=('python2-setuptools' 'python-setuptools')
source=("https://files.pythonhosted.org/packages/d9/2f/0a34b00d05e3244ce65041cabf7b637ed2e727c3a053c86b1cebd11f190d/$_pkgname-$pkgver.zip")
sha512sums=('a8bcf395ebed7c43f5355bc167aae157c6eddb127860a6fb58eba0d882235e04a33a5bcf589245f484e3af9dce5eff8a5893ced73203901c92afd92c4f5b05be')

prepare() {
  cp -a "$_pkgname-$pkgver"{,-2}
}

build() {
  cd "$_pkgname-$pkgver-2"

  python2 setup.py build

  cd "$srcdir/$_pkgname-$pkgver"

  python setup.py build
}

package_python2-graphviz() {
  depends=('python2')

  cd "$_pkgname-$pkgver-2"

  python2 setup.py install --prefix=/usr --root="$pkgdir" -O1 --skip-build
}

package_python-graphviz() {
  depends=('python')

  cd "$_pkgname-$pkgver"

  python setup.py install --prefix=/usr --root="$pkgdir" -O1 --skip-build
}

