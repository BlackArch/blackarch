# This file is part of BlackArch Linux ( https://www.blackarch.org/ ).
# See COPYING for license details.

pkgbase=python-graphviz
pkgname=('python2-graphviz' 'python-graphviz')
_pkgname=graphviz
pkgver=0.14
pkgrel=1
pkgdesc='Simple Python interface for Graphviz.'
arch=('any')
url='https://pypi.org/project/graphviz/#files'
license=('MIT')
makedepends=('python2-setuptools' 'python-setuptools')
source=("https://files.pythonhosted.org/packages/3e/58/11b713c7aceffb5a993a04141c1a9667d57b5d254a1e617107620ff29988/$_pkgname-$pkgver.zip")
sha512sums=('4f1b50e1756e1c0c5c9c80d2e5c4a900bce7b6c825fe541b19d4fc19f3cab3e2921bc0f4398b3c0ed2f1326b4c4b65f178b6bf0aadc6086dccb780cf969fbf32')

prepare() {
  cp -a "$_pkgname-$pkgver"{,-2}
}

build() {
  cd "$_pkgname-$pkgver-2"

  python2 setup.py build

  cd "$srcdir/$_pkgname-$pkgver"

  python setup.py build
}

package_python2-graphviz() {
  depends=('python2')

  cd "$_pkgname-$pkgver-2"

  python2 setup.py install --prefix=/usr --root="$pkgdir" -O1 --skip-build
}

package_python-graphviz() {
  depends=('python')

  cd "$_pkgname-$pkgver"

  python setup.py install --prefix=/usr --root="$pkgdir" -O1 --skip-build
}

