# This file is part of BlackArch Linux ( http://blackarch.org ).
# See COPYING for license details.

pkgname=xsstracer
pkgver=5.f2ed21a
pkgrel=3
groups=('blackarch' 'blackarch-scanner')
pkgdesc='Python script that checks remote web servers for Clickjacking, Cross-Frame Scripting, Cross-Site Tracing and Host Header Injection.'
arch=('any')
url='https://github.com/1N3/XSSTracer'
license=('custom:unknown')
makedepends=('git')
depends=('python2')
source=('git+https://github.com/1N3/XSSTracer.git')
sha512sums=('SKIP')

pkgver() {
  cd XSSTracer

  echo $(git rev-list --count HEAD).$(git rev-parse --short HEAD)
}

package() {
  cd XSSTracer

  install -dm 755 "$pkgdir/usr/bin"
  install -dm 755 "$pkgdir/usr/share/$pkgname"

  install -Dm 644 README.md "$pkgdir/usr/share/doc/$pkgname/README.md"

  rm README.md

  cp -a * "$pkgdir/usr/share/$pkgname/"

  cat > "$pkgdir/usr/bin/$pkgname" << EOF
#!/bin/sh
cd /usr/share/$pkgname
exec python2 $pkgname.py "\$@"
EOF

  chmod a+x "$pkgdir/usr/bin/$pkgname"
}

