# This file is part of BlackArch Linux ( http://blackarch.org ).
# See COPYING for license details.

pkgname='netattack'
pkgver=24.230b856
pkgrel=1
epoch=2
pkgdesc='Python script to scan and attack wireless networks.'
groups=('blackarch' 'blackarch-wireless')
arch=('any')
url='https://github.com/chrizator/netattack2'
license=('MIT')
depends=('python2' 'python2-argparse' 'scapy' 'iw' 'wireless_tools')
makedepends=('git')
source=('git+https://github.com/chrizator/netattack2.git')
sha1sums=('SKIP')

pkgver() {
  cd "$srcdir/netattack2"

  echo $(git rev-list --count HEAD).$(git rev-parse --short HEAD)
}

prepare() {
  cd "$srcdir/netattack2"

  sed -i 's|bin/env python|bin/env python2|' netattack2.py
}

package() {
  cd "$srcdir/netattack2"

  install -Dm755 netattack2.py "$pkgdir/usr/bin/netattack"
  install -Dm644 -t "$pkgdir/usr/share/doc/netattack/" README.md
  install -Dm644 LICENSE "$pkgdir/usr/share/licenses/netattack/LICENSE"
}
