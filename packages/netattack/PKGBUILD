# This file is part of BlackArch Linux ( http://blackarch.org ).
# See COPYING for license details.

pkgname='netattack'
pkgver=9.c332c7d
pkgrel=1
pkgdesc='Python script to scan and attack wireless networks.'
groups=('blackarch' 'blackarch-wireless')
arch=('any')
url='https://github.com/chrizator/netattack'
license=('custom:unknown')
depends=('python2' 'python2-argparse' 'scapy' 'iw' 'wireless_tools')
makedepends=('git')
source=('git+https://github.com/chrizator/netattack.git')
sha1sums=('SKIP')

pkgver() {
  cd "$srcdir/netattack"

  echo $(git rev-list --count HEAD).$(git rev-parse --short HEAD)
}

prepare() {
  cd "$srcdir/netattack"

  sed -i 's|bin/env python|bin/env python2|' netattack.py
}

package() {
  cd "$srcdir/netattack"

  install -Dm755 netattack.py "$pkgdir/usr/bin/netattack"
  install -Dm644 -t "$pkgdir/usr/share/doc/netattack/" README.md
}
