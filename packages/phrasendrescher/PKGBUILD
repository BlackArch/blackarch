# This file is part of BlackArch Linux ( http://blackarch.org ).
# See COPYING for license details.

pkgname='phrasendrescher'
pkgver='1.2.2'
pkgrel=7
groups=('blackarch' 'blackarch-cracker')
pkgdesc='A modular and multi processing pass phrase cracking tool.'
arch=('x86_64' 'armv6h' 'armv7h' 'aarch64')
url='http://www.leidecker.info/projects/phrasendrescher/'
license=('BSD')
depends=('openssl' 'libssh2' 'gpgme')
options=('!libtool')
source=("http://www.leidecker.info/projects/phrasendrescher/phrasendrescher-${pkgver}.tar.gz"
        'phrasendrescher.patch')
sha1sums=('040430a3a103b4391310e9c1efa3f92ffd6d3aa8'
          '9d83e517d95fea9c876b8a1f3b39ed5aa2cb108d')

prepare() {
  cd "$srcdir/phrasendrescher-$pkgver"

  patch -p0 -i ../phrasendrescher.patch
}

build() {
  cd "$srcdir/phrasendrescher-$pkgver"

  if [[ "$CARCH" == "i686" || "$CARCH" == "x86_64" ]];
  then
    ./configure --prefix=/usr --with-plugins
  else
    ./configure --build=arm --prefix=/usr --with-plugins
  fi

  make
}

package() {
  cd "$srcdir/phrasendrescher-$pkgver"

  make DESTDIR=$pkgdir install

  # Fix conflict with community/pd.
  mv "$pkgdir/usr/bin/pd" "$pkgdir/usr/bin/phrasendrescher"
  mv "$pkgdir/usr/share/man/man1/pd.1" "$pkgdir/usr/share/man/man1/phrasendrescher.1"
}
