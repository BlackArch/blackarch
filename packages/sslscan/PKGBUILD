pkgname=sslscan
pkgver=1.8.2
pkgrel=3
groups=('blackarch' 'blackarch-scanner')
pkgdesc="A fast tools to scan SSL services, such as HTTPS."
arch=('i686' 'x86_64')
url='http://sourceforge.net/projects/sslscan/'
license=('GPL')
depends=('openssl')
source=("http://sourceforge.net/projects/sslscan/files/sslscan/sslscan-$pkgver.tgz")
md5sums=('1a890d031996ab252efb535511bd58db')

build() {
  cd "$srcdir/sslscan-$pkgver"
  sed -i 's/-lssl/-lssl -lcrypto/g' Makefile
  make
}

package() {
  cd "$srcdir/sslscan-$pkgver"

  # Make base directories.
  install -dm755 $pkgdir/usr/bin
  install -dm755 $pkgdir/usr/share/man/man1

  make BINPATH="$pkgdir/usr/bin/" MANPATH="$pkgdir/usr/share/man/" install
}
