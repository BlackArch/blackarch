# This file is part of BlackArch Linux ( https://www.blackarch.org/ ).
# See COPYING for license details.

pkgname=reconnoitre
pkgver=398.758247a
pkgrel=2
pkgdesc='A security tool for multithreaded information gathering and service enumeration.'
groups=('blackarch' 'blackarch-recon')
arch=('any')
url='https://github.com/codingo/Reconnoitre'
license=('GPL3')
depends=('python' 'python-argparse' 'python-requests')
makedepends=('git')
source=("$pkgname::git+https://github.com/codingo/Reconnoitre.git")
sha512sums=('SKIP')

pkgver() {
  cd $pkgname

  echo $(git rev-list --count HEAD).$(git rev-parse --short HEAD)
}

prepare() {
  cd $pkgname

  find . -type f -print0 | xargs -0 sed -i 's/from file_helper/from lib.file_helper/g'
}

package() {
  cd $pkgname

  install -dm 755 "$pkgdir/usr/bin"
  install -dm 755 "$pkgdir/usr/share/$pkgname"

  install -Dm 644 -t "$pkgdir/usr/share/doc/$pkgname/" README.md
  install -Dm 644 LICENSE "$pkgdir/usr/share/licenses/$pkgname/LICENSE"

  rm README.md LICENSE

  cp -a * "$pkgdir/usr/share/$pkgname/"

  cat > "$pkgdir/usr/bin/$pkgname" << EOF
#!/bin/sh
cd /usr/share/$pkgname/Reconnoitre
exec python $pkgname.py "\$@"
EOF

  chmod +x "$pkgdir/usr/bin/$pkgname"
}

