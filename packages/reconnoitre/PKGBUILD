# This file is part of BlackArch Linux ( http://blackarch.org ).
# See COPYING for license details.

pkgname='reconnoitre'
pkgver=289.249b48c
pkgrel=1
pkgdesc='A security tool for multithreaded information gathering and service enumeration.'
groups=('blackarch' 'blackarch-recon')
arch=('any')
url='https://github.com/codingo/Reconnoitre'
license=('GPL3')
depends=('python' 'python-argparse')
makedepends=('git')
source=('git+https://github.com/codingo/Reconnoitre.git')
sha1sums=('SKIP')

pkgver() {
  cd "$srcdir/Reconnoitre"

  echo $(git rev-list --count HEAD).$(git rev-parse --short HEAD)
}

package() {
  cd "$srcdir/Reconnoitre"

  mkdir -p "$pkgdir/usr/bin"
  mkdir -p "$pkgdir/usr/share/reconnoitre"

  install -Dm644 -t "$pkgdir/usr/share/doc/reconnoitre/" README.md
  install -Dm644 LICENSE "$pkgdir/usr/share/licenses/reconnoitre/LICENSE"

  rm README.md LICENSE

  cp -a * "$pkgdir/usr/share/reconnoitre/"

  cat > "$pkgdir/usr/bin/reconnoitre" << EOF
#!/bin/sh
cd /usr/share/reconnoitre/reconnoitre
exec python reconnoitre.py "\${@}"
EOF

  chmod a+x "$pkgdir/usr/bin/reconnoitre"
}
