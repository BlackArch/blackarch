# This file is part of BlackArch Linux ( https://www.blackarch.org/ ).
# See COPYING for license details.

pkgname=pret
pkgver=81.4f3820a
pkgrel=1
pkgdesc='Printer Exploitation Toolkit - The tool that made dumpster diving obsolete.'
groups=('blackarch' 'blackarch-exploitation' 'blackarch-fuzzer'
        'blackarch-recon' 'blackarch-scanner')
arch=('any')
url='https://github.com/RUB-NDS/PRET'
license=('GPL2')
depends=('python2' 'python2-colorama' 'python2-pysnmp')
makedepends=('git')
source=("$pkgname::git+https://github.com/RUB-NDS/PRET.git"
        'helper.py.patch')
sha512sums=('SKIP'
            'a550236d8a5d9301515472c9faa94a6981cd9b7caf866f702bd680e252f45b8050ddd82fa9bc36991e7670a302f26d1eb94ffa0689f4205a635ee338c74c8a4d')

pkgver() {
  cd $pkgname

  echo $(git rev-list --count HEAD).$(git rev-parse --short HEAD)
}

prepare() {
  patch -p1 < helper.py.patch
}

package() {
  cd $pkgname

  install -dm 755 "$pkgdir/usr/bin"
  install -dm 755 "$pkgdir/usr/share/$pkgname"

  install -Dm 644 -t "$pkgdir/usr/share/doc/$pkgname/" README.md
  install -Dm 644 LICENSE.md "$pkgdir/usr/share/licenses/$pkgname/LICENSE.md"

  rm *.md

  cp -a * "$pkgdir/usr/share/$pkgname/"

  cat > "$pkgdir/usr/bin/$pkgname" << EOF
#!/bin/sh
cd /usr/share/$pkgname
exec python2 $pkgname.py "\$@"
EOF

  chmod +x "$pkgdir/usr/bin/$pkgname"
}

