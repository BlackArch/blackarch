# This file is part of BlackArch Linux ( https://www.blackarch.org/ ).
# See COPYING for license details.

pkgname=ldap-brute
pkgver=21.acc06e3
pkgrel=2
groups=('blackarch' 'blackarch-cracker')
pkgdesc='A semi fast tool to bruteforce values of LDAP injections over HTTP.'
url='https://github.com/droope/ldap-brute'
arch=('any')
license=('GPL3')
depends=('python2' 'python2-httmock' 'python2-mock' 'python2-requests')
makedepends=('git')
source=("git+https://github.com/droope/$pkgname.git")
sha512sums=('SKIP')

pkgver() {
  cd $pkgname

  echo $(git rev-list --count HEAD).$(git rev-parse --short HEAD)
}

package() {
  cd $pkgname

  install -dm 755 "$pkgdir/usr/bin"
  install -dm 755 "$pkgdir/usr/share/$pkgname"


  install -Dm 644 -t "$pkgdir/usr/share/doc/$pkgname/" README.md
  install -Dm 644 LICENSE "$pkgdir/usr/share/licenses/$pkgname/LICENSE"

  rm README.md LICENSE

  cp -a * "$pkgdir/usr/share/$pkgname/"

  cat > "$pkgdir/usr/bin/$pkgname" << EOF
#!/bin/sh
cd /usr/share/$pkgname
exec python2 ldap_brute.py "\$@"
EOF

  chmod +x "$pkgdir/usr/bin/$pkgname"
}

