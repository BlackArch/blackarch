# This file is part of BlackArch Linux ( https://www.blackarch.org/ ).
# See COPYING for license details.

pkgname=doozer
pkgver=9.5cfc8f8
pkgrel=3
groups=('blackarch' 'blackarch-cracker')
pkgdesc='A Password cracking utility.'
arch=('any')
url='https://github.com/CoalfireLabs/crackHOR'
license=('GPL2')
depends=('python2' 'python2-requests' 'python2-django' 'ophcrack' 'hashcat')
makedepends=('git')
source=("$pkgname::git+https://github.com/CoalfireLabs/Doozer.git")
sha512sums=('SKIP')

pkgver() {
  cd $pkgname

  echo $(git rev-list --count HEAD).$(git rev-parse --short HEAD)
}

prepare() {
  cd $pkgname

  find . -type f -name '*.py' -exec sed -i 's|python$|python2|' {} +
}

package() {
  cd $pkgname

  install -dm 755 "$pkgdir/usr/bin"
  install -dm 755 "$pkgdir/usr/share/$pkgname"

  install -Dm 644 -t "$pkgdir/usr/share/doc/$pkgname/" README.md

  rm README.md

  cp -a * "$pkgdir/usr/share/$pkgname/"

  cat > "$pkgdir/usr/bin/$pkgname" << EOF
#!/bin/sh
cd /usr/share/$pkgname/$pkgname
exec python2 $pkgname.py "\$@"
EOF

  chmod +x "$pkgdir/usr/bin/$pkgname"

  cat > "$pkgdir/usr/bin/doozestop" << EOF
#!/bin/sh
cd /usr/share/$pkgname/$pkgname
exec python2 doozestop.py "\$@"
EOF

  chmod +x "$pkgdir/usr/bin/doozestop"

  cat > "$pkgdir/usr/bin/doozer-monitor" << EOF
#!/bin/sh
cd /usr/share/$pkgname/$pkgname
exec python2 monitor.py "\$@"
EOF

  chmod +x "$pkgdir/usr/bin/doozer-monitor"
}

