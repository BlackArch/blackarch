# This file is part of BlackArch Linux ( https://www.blackarch.org/ ).
# See COPYING for license details.

pkgname=pipal
pkgver=1.1
pkgrel=7
groups=('blackarch' 'blackarch-cracker')
pkgdesc='A password analyser.'
url='http://www.digininja.org/projects/pipal.php'
arch=('any')
license=('GPL3')
depends=('ruby1.9')
source=("http://www.digininja.org/files/${pkgname}_${pkgver}.tar.bz2"
        'require.patch')
sha512sums=('683b83863fae0650c2e1071418d09152419dfbe25909053a3677574fcf4e1251c3bb0862d762fc35b44035e8b541302e871e443538588a185341e09d94169237'
            '6c88204ccfc4eb4229d6742281b07cd3214dc0ddfc774bf931b703dfd0f83b0969a83d0a18c15b26970878fd7d9176b95b102308efc72fd7c6b316bb743725e3')

prepare() {
  cd $pkgname

  patch < "$srcdir/require.patch"
}

package() {
  cd $pkgname

  install -dm 755 "$pkgdir/usr/bin"
  install -dm 755 "$pkgdir/usr/share/$pkgname"

  cp -a * "$pkgdir/usr/share/$pkgname/"

  cat > "$pkgdir/usr/bin/$pkgname" << EOF
#!/bin/sh
exec ruby-1.9 /usr/share/$pkgname/$pkgname.rb "\$@"
EOF

  chmod +x "$pkgdir/usr/bin/$pkgname"
}

