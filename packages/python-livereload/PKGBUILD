# This file is part of BlackArch Linux ( https://www.blackarch.org/ ).
# See COPYING for license details.

_pkgname='livereload'
pkgname=("python-$_pkgname" "python2-$_pkgname")
pkgver=2.6.0
pkgrel=1
pkgdesc='An awesome tool for web developers.'
arch=('any')
url='https://github.com/lepture/python-livereload'
license=('BSD')
depends=('python' 'python2')
makedepends=('python-setuptools' 'python2-setuptools')
source=("https://github.com/lepture/python-$_pkgname/archive/v$pkgver.tar.gz")
sha512sums=('351fa56c64a3bea7c43e1a60c3633bcb80a6093aaadaa73e8f0bc6a27f939c52f272d60922e5d1d956834b7cff16b8c7ee702cbc85473e554629a38efaef9993')

build() {
  cd python-$_pkgname-$pkgver
  python setup.py build
  python2 setup.py build
}

package_python-livereload() {
  depends=('python' 'python-six' 'python-tornado')
  cd python-$_pkgname-$pkgver
  python setup.py install --skip-build --root="$pkgdir" --optimize=1
}

package_python2-livereload() {
  depends=('python2' 'python2-six' 'python2-tornado')
  cd python-$_pkgname-$pkgver
  python2 setup.py install --skip-build --root="$pkgdir" --optimize=1
  mv "$pkgdir/usr/bin/$_pkgname" "$pkgdir/usr/bin/${_pkgname}2"
}

