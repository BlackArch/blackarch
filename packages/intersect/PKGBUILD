# This file is part of BlackArch Linux ( http://blackarch.org ).
# See COPYING for license details.

pkgname='intersect'
pkgver='2.5'
pkgrel=1
pkgdesc="Post-exploitation framework"
arch=('x86_64' 'armv7h' 'aarch64')
url="https://github.com/ohdae/Intersect-2.5"
license=('custom')
groups=('blackarch' 'blackarch-automation')
depends=('python')
makedepends=('git')
source=("git+https://www.github.com/ohdae/Intersect-$pkgver"
        "Intersect.patch")
sha1sums=('SKIP'
          'fade759d5976a5f3303f8ba5941d25d05c18be82')

prepare() {
  cd "$srcdir/Intersect-$pkgver"

  patch -p1 -i "$srcdir/Intersect.patch"
}

package() {
  cd "$srcdir/Intersect-$pkgver"

  install -Dm755 Create.py "$pkgdir/usr/bin/intersect-create"

  mkdir -p "$pkgdir/usr/share/Intersect"

  for i in Docs/ Junk/ Logs/ Scripts/ src/ Storage/ Tools/; do
        cp -R $i "$pkgdir/usr/share/Intersect/";
  done
}
