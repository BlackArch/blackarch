# This file is part of BlackArch Linux ( http://blackarch.org ).
# See COPYING for license details.

pkgname='svn-extractor'
pkgver=39.39941be
pkgrel=1
groups=('blackarch' 'blackarch-scanner')
pkgdesc='A simple script to extract all web resources by means of .SVN folder exposed over network.'
arch=('any')
url='https://github.com/anantshri/svn-extractor'
license=('GPL3')
depends=('python2' 'python2-pysqlite')
makedepends=('git')
source=('git+https://github.com/anantshri/svn-extractor.git')
sha1sums=('SKIP')

pkgver() {
  cd "$srcdir/svn-extractor"

  echo $(git rev-list --count HEAD).$(git rev-parse --short HEAD)
}

prepare(){
  cd "$srcdir/svn-extractor"

  sed -i 's|python$|python2|' svn_extractor.py
}

package() {
  cd "$srcdir/svn-extractor"

  install -Dm755 "svn_extractor.py" "$pkgdir/usr/bin/svn-extractor"
  install -Dm644 "README.md" "$pkgdir/usr/share/doc/svn-extractor/README.md"
}
