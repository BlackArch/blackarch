# This file is part of BlackArch Linux ( https://www.blackarch.org/ ).
# See COPYING for license details.

pkgname=svn-extractor
pkgver=42.c27831c
pkgrel=2
groups=('blackarch' 'blackarch-recon' 'blackarch-scanner')
pkgdesc='A simple script to extract all web resources by means of .SVN folder exposed over network.'
arch=('any')
url='https://github.com/anantshri/svn-extractor'
license=('GPL3')
depends=('python' 'python-pysqlite3' 'python-requests')
makedepends=('git')
source=("git+https://github.com/anantshri/$pkgname.git")
sha512sums=('SKIP')

pkgver() {
  cd $pkgname

  echo $(git rev-list --count HEAD).$(git rev-parse --short HEAD)
}

package() {
  cd $pkgname

  install -Dm 755 svn_extractor.py "$pkgdir/usr/bin/$pkgname"
  install -Dm 644 -t "$pkgdir/usr/share/doc/$pkgname/" README.md
}

