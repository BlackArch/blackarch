# This file is part of BlackArch Linux ( http://blackarch.org ).
# See COPYING for license details.

pkgname='xpire-crossdomain-scanner'
pkgver=1.0cb8d3b
pkgrel=3
groups=('blackarch' 'blackarch-scanner')
pkgdesc='Scans crossdomain.xml policies for expired domain names.'
arch=('any')
url='https://github.com/mandatoryprogrammer/xpire-crossdomain-scanner'
license=('GPL2')
depends=('python2' 'python2-tldextract' 'python2-defusedxml' 'python2-netaddr'
         'python2-requests' 'python2-python-whois')
makedepends=('git')
source=('git+https://github.com/mandatoryprogrammer/xpire-crossdomain-scanner.git')
sha1sums=('SKIP')

pkgver() {
  cd "$srcdir/xpire-crossdomain-scanner"

  echo $(git rev-list --count HEAD).$(git rev-parse --short HEAD)
}

package() {
  cd "$srcdir/xpire-crossdomain-scanner"

  mkdir -p "$pkgdir/usr/bin"
  mkdir -p "$pkgdir/usr/share/xpire-crossdomain-scanner"

  install -Dm644 -t "$pkgdir/usr/share/doc/xpire-crossdomain-scanner/" \
    README.md requirements.txt
  install -Dm644 LICENSE \
    "$pkgdir/usr/share/licenses/xpire-crossdomain-scanner/LICENSE"

  rm README.md LICENSE requirements.txt

  cp -a * "$pkgdir/usr/share/xpire-crossdomain-scanner"

  cat > "$pkgdir/usr/bin/xpire-crossdomain-scanner" << EOF
#!/bin/sh
cd /usr/share/xpire-crossdomain-scanner
exec python2 crossdomain-scan.py "\$@"
EOF

  chmod a+x "$pkgdir/usr/bin/xpire-crossdomain-scanner"
}
