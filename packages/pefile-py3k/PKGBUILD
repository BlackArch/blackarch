# This file is part of BlackArch Linux ( https://www.blackarch.org/ ).
# See COPYING for license details.

pkgname=pefile-py3k
pkgver=10.4fba161
pkgrel=3
pkgdesc='Fork of pefile for python 3.'
arch=('any')
url='https://github.com/simonzack/pefile-py3k'
license=('custom:unknown')
makedepends=('git')
depends=('python3')
source=('git+https://github.com/simonzack/pefile-py3k.git')
sha512sums=('SKIP')

pkgver() {
  cd $pkgname

  echo $(git rev-list --count HEAD).$(git rev-parse --short HEAD)
}

build() {
  cd $pkgname

  python setup.py build
}

package() {
  cd $pkgname

  python setup.py install --root="$pkgdir" --prefix=/usr -O1 --skip-build

  install -Dm 644 -t "$pkgdir/usr/share/doc/$pkgname/" README
  install -Dm 644 COPYING "$pkgdir/usr/share/licenses/$pkgname/COPYING"
}

