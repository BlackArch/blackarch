# This file is part of BlackArch Linux ( https://www.blackarch.org/ ).
# See COPYING for license details.

pkgname=shellerator
pkgver=28.2a3f9fd
pkgrel=1
pkgdesc='Simple command-line tool aimed to help pentesters quickly generate one-liner reverse/bind shells in multiple languages.'
arch=('any')
groups=('blackarch' 'blackarch-automation')
url='https://github.com/ShutdownRepo/shellerator'
license=('GPL3')
depends=('python' 'python-virtualenv' 'python-psutil' 'python-colorama'
         'python-argparse' 'python-console-menu' 'python-simple-term-menu')
makedepends=('git' 'python-pip')
source=("git+https://github.com/ShutdownRepo/$pkgname.git")
sha512sums=('SKIP')

pkgver() {
  cd $pkgname

  echo $(git rev-list --count HEAD).$(git rev-parse --short HEAD)
}

package() {
  cd $pkgname

  install -dm 755 "$pkgdir/usr/bin"
  install -dm 755 "$pkgdir/usr/share/$pkgname"

  install -Dm 644 -t "$pkgdir/usr/share/doc/$pkgname/" README.md
  install -Dm 644 LICENSE "$pkgdir/usr/share/licenses/$pkgname/LICENSE"

  rm -rf assets/ *.md LICENSE *.txt
  cp -a --no-preserve=ownership * "$pkgdir/usr/share/$pkgname/"


  cat > "$pkgdir/usr/bin/$pkgname" << EOF
#!/bin/sh
exec python /usr/share/$pkgname/$pkgname.py "\$@"
EOF

  chmod a+x "$pkgdir/usr/bin/$pkgname"
}

