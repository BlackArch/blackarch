# This file is part of BlackArch Linux ( https://www.blackarch.org/ ).
# See COPYING for license details.

pkgname=shellerator
pkgver=69.4047fd2
pkgrel=1
pkgdesc='Simple command-line tool aimed to help pentesters quickly generate one-liner reverse/bind shells in multiple languages.'
arch=('any')
groups=('blackarch' 'blackarch-automation')
url='https://github.com/ShutdownRepo/shellerator'
license=('GPL-3.0-or-later')
depends=('python' 'python-psutil' 'python-console-menu' 'python-colorama'
         'python-simple-term-menu')
makedepends=('git' 'python-setuptools')
source=("git+https://github.com/ShutdownRepo/$pkgname.git")
sha512sums=('SKIP')

pkgver() {
  cd $pkgname

  ( set -o pipefail
    git describe --long --tags --abbrev=7 2>/dev/null |
      sed 's/\([^-]*-g\)/r\1/;s/-/./g' ||
    printf "%s.%s" "$(git rev-list --count HEAD)" \
      "$(git rev-parse --short=7 HEAD)"
  )
}

build() {
  cd $pkgname

  python setup.py build
}

package() {
  cd $pkgname

  python setup.py install --root="$pkgdir" --prefix=/usr -O1 --skip-build
}

