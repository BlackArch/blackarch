# This file is part of BlackArch Linux ( https://www.blackarch.org/ ).
# See COPYING for license details.

pkgname=deblaze
pkgver=3.a24555d
pkgrel=1
epoch=1
pkgdesc='Performs method enumeration and interrogation against flash remoting end points.'
arch=('any')
groups=('blackarch' 'blackarch-scanner')
url='https://github.com/SpiderLabs/deblaze'
license=('GPL-3.0-or-later')
depends=('python2' 'python2-pillow')
makedepends=('git')
source=("git+https://github.com/SpiderLabs/$pkgname.git")
sha512sums=('SKIP')

pkgver() {
  cd $pkgname

  ( set -o pipefail
    git describe --long --tags --abbrev=7 2>/dev/null |
      sed 's/\([^-]*-g\)/r\1/;s/-/./g' ||
    printf "%s.%s" "$(git rev-list --count HEAD)" \
      "$(git rev-parse --short=7 HEAD)"
  )
}

prepare() {
  cd $pkgname

  sed 's|import Image|from PIL import Image|g' -i $pkgname.py
}

package() {
  cd $pkgname

  install -dm 755 "$pkgdir/usr/bin"
  install -dm 755 "$pkgdir/usr/share/$pkgname"

  cp -a --no-preserve=ownership * "$pkgdir/usr/share/$pkgname/"

  cat > "$pkgdir/usr/bin/$pkgname" << EOF
#!/bin/sh
cd /usr/share/$pkgname
exec python2 $pkgname.py
EOF

  chmod +x "$pkgdir/usr/bin/$pkgname"
}

