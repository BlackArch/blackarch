# This file is part of BlackArch Linux ( https://www.blackarch.org/ ).
# See COPYING for license details.

pkgname=xsstrike
pkgver=373.20dfc7f
pkgrel=1
pkgdesc='An advanced XSS detection and exploitation suite.'
groups=('blackarch' 'blackarch-webapp' 'blackarch-scanner')
arch=('any')
url='https://github.com/UltimateHackers/XSStrike'
license=('MIT')
depends=('python' 'python-prettytable' 'python-requests' 'python-fuzzywuzzy'
         'python-levenshtein' 'python-selenium')
makedepends=('git')
source=('git+https://github.com/UltimateHackers/XSStrike.git')
sha512sums=('SKIP')

pkgver() {
  cd XSStrike

  echo $(git rev-list --count HEAD).$(git rev-parse --short HEAD)
}

package() {
  cd XSStrike

  install -dm 755 "$pkgdir/usr/bin"
  install -dm 755 "$pkgdir/usr/share/$pkgname"

  install -Dm644 -t "$pkgdir/usr/share/doc/$pkgname/" *.md

  rm *.md

  cp -a * "$pkgdir/usr/share/$pkgname"

  cat > "$pkgdir/usr/bin/$pkgname" << EOF
#!/bin/sh
cd /usr/share/$pkgname
exec python xsstrike.py "\$@"
EOF

  chmod +x "$pkgdir/usr/bin/$pkgname"
}

