# This file is part of BlackArch Linux ( http://blackarch.org ).
# See COPYING for license details.

pkgname=xsstrike
pkgver=165.d71a565
pkgrel=1
pkgdesc='An advanced XSS detection and exploitation suite.'
groups=('blackarch' 'blackarch-webapp' 'blackarch-scanner')
arch=('any')
url='https://github.com/UltimateHackers/XSStrike'
license=('MIT')
depends=('python' 'python-prettytable' 'python-requests' 'python-fuzzywuzzy'
         'python-levenshtein')
makedepends=('git')
source=('git+https://github.com/UltimateHackers/XSStrike.git')
sha512sums=('SKIP')

pkgver() {
  cd "$srcdir/XSStrike"

  echo $(git rev-list --count HEAD).$(git rev-parse --short HEAD)
}

package() {
  cd "$srcdir/XSStrike"

  install -dm 755 "$pkgdir/usr/bin"
  install -dm 755 "$pkgdir/usr/share/xsstrike"

  install -Dm644 -t "$pkgdir/usr/share/doc/xsstrike/" *.md
  install -Dm644 LICENSE "$pkgdir/usr/share/licenses/xsstrike/LICENSE"

  rm *.md LICENSE

  cp -a * "$pkgdir/usr/share/xsstrike"

  cat > "$pkgdir/usr/bin/xsstrike" << EOF
#!/bin/sh
cd /usr/share/xsstrike
exec python3 xsstrike "\${@}"
EOF

  chmod a+x "$pkgdir/usr/bin/xsstrike"
}
