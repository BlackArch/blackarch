# This file is part of BlackArch Linux ( https://www.blackarch.org/ ).
# See COPYING for license details.

pkgname=stenographer
pkgver=486.355604b
pkgrel=2
groups=('blackarch' 'blackarch-sniffer' 'blackarch-networking'
        'blackarch-forensic')
pkgdesc='A packet capture solution which aims to quickly spool all packets to disk, then provide simple, fast access to subsets of those packets.'
arch=('x86_64' 'aarch64')
url='https://github.com/google/stenographer'
license=('APACHE')
makedepends=('git' 'go')
source=("git+https://github.com/google/$pkgname.git")
sha512sums=('SKIP')

pkgver() {
  cd $pkgname

  echo $(git rev-list --count HEAD).$(git rev-parse --short HEAD)
}

build() {
  cd $pkgname

  GOPATH="$srcdir" go mod init go.mod
  GOPATH="$srcdir" go mod tidy
  GOPATH="$srcdir" go mod download
  GOPATH="$srcdir" go build \
    -trimpath \
    -buildmode=pie \
    -mod=readonly \
    -modcacherw \
    -ldflags "-s -w" \
    -o $pkgname .
}

package() {
  cd $pkgname

  install -Dm 755 $pkgname "$pkgdir/usr/bin/$pkgname"
  install -Dm 644 README.md -t "$pkgdir/usr/share/doc/$pkgname"
  install -Dm 644 LICENSE -t "$pkgdir/usr/share/licenses/$pkgname"
}

