# This file is part of BlackArch Linux ( https://www.blackarch.org/ ).
# See COPYING for license details.

pkgbase=python-pypermacc
pkgname=('python2-pypermacc' 'python-pypermacc')
_pkgname=pypermacc
pkgver=4.d896512
pkgrel=3
pkgdesc='Python3 wrapper for the perma.cc API.'
arch=('any')
url='https://pypi.org/project/pypermacc/#files'
license=('MIT')
makedepends=('git' 'python2-setuptools' 'python-setuptools')
source=("git+https://github.com/Te-k/$_pkgname.git")
sha512sums=('SKIP')

pkgver() {
  cd $_pkgname

  echo $(git rev-list --count HEAD).$(git rev-parse --short HEAD)
}

prepare() {
  cp -a "$_pkgname"{,-2}
}

build() {
  cd "$_pkgname-2"

  python2 setup.py build

  cd "$srcdir/$_pkgname"

  python setup.py build
}

package_python2-pypermacc() {
  depends=('python2' 'python2-requests')

  cd "$_pkgname-2"

  python2 setup.py install --prefix=/usr --root="$pkgdir" --optimize=1 \
    --skip-build
}

package_python-pypermacc() {
  depends=('python' 'python-requests')

  cd $_pkgname

  python setup.py install --prefix=/usr --root="$pkgdir" --optimize=1 \
    --skip-build
}

