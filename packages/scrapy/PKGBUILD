# This file is part of BlackArch Linux ( http://blackarch.org ).
# See COPYING for license details.

pkgname='scrapy'
pkgver=4738.3a4d57b
pkgrel=2
pkgdesc='A fast high-level scraping and web crawling framework.'
arch=('any')
groups=('blackarch' 'blackarch-scanner' 'blackarch-webapp')
license=('BSD')
url='http://www.scrapy.org/'
depends=('twisted' 'libxml2' 'python2-w3lib' 'python2-lxml' 'python2-six'
         'python2-queuelib' 'python2-setuptools' 'python2-cssselect'
         'python2-pyopenssl' 'ipython2' 'python2-pybloom'
         'python2-service-identity')
makedepends=('git' 'python2-setuptools')
provides=('scrapy')
conflicts=('scrapy-hg' 'scrapy-git')
options=(!emptydirs)
source=('git+https://github.com/scrapy/scrapy.git')
sha1sums=('SKIP')

pkgver() {
  cd "$srcdir/scrapy"

  echo $(git rev-list --count HEAD).$(git rev-parse --short HEAD)
}

package() {
  cd "$srcdir/scrapy"

  python2 setup.py install --root="$pkgdir" --optimize=1

  install -Dm644 README.rst "$pkgdir/usr/share/doc/scrapy/README.rst"
  install -Dm644 LICENSE "$pkgdir/usr/share/licenses/scrapy/LICENSE"
  install -Dm644 docs/intro/install.rst \
    "$pkgdir/usr/share/doc/scrapy/INSTALL.rst"
}
