# This file is part of BlackArch Linux ( https://www.blackarch.org/ ).
# See COPYING for license details.

pkgbase=python-graphillion
pkgname=('python2-graphillion' 'python-graphillion')
_pkgname=Graphillion
pkgver=1.4
pkgrel=2
pkgdesc='Fast, lightweight library for a huge number of graphs.'
arch=('x86_64' 'aarch64')
url='https://pypi.org/project/Graphillion/'
license=('MIT')
makedepends=('python2-setuptools' 'python-setuptools')
source=("https://files.pythonhosted.org/packages/8c/6a/80976fce3e952ee42658895fbc4bb755f1d772159c1d871c467efb6447f4/Graphillion-$pkgver.tar.gz")
sha512sums=('dea5f701a300a16c23208e2cdf0c48ed5f42e04db8f5f0bc48f2bed5d19a9cc2ccfc0eb00940791946d4ec8589f16e56c00a18fb6d5ac1115e1432ab9c8e1b3d')

prepare() {
  cp -a "$_pkgname-$pkgver"{,-2}
}

build() {
  cd "$_pkgname-$pkgver-2"

  python2 setup.py build

  cd "$srcdir/$_pkgname-$pkgver"

  python setup.py build
}

package_python2-graphillion() {
  depends=('python2' 'python2-future')

  cd "$_pkgname-$pkgver-2"

  python2 setup.py install --prefix=/usr --root="$pkgdir" --optimize=1 \
    --skip-build
}

package_python-graphillion() {
  depends=('python' 'python-future')

  cd "$_pkgname-$pkgver"

  python setup.py install --prefix=/usr --root="$pkgdir" --optimize=1 \
    --skip-build
}

