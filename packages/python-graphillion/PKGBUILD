# This file is part of BlackArch Linux ( https://www.blackarch.org/ ).
# See COPYING for license details.

pkgbase=python-graphillion
pkgname=('python2-graphillion' 'python-graphillion')
_pkgname=graphillion
pkgver=2.1
pkgrel=1
pkgdesc='Fast, lightweight library for a huge number of graphs.'
arch=('x86_64' 'aarch64')
url='https://pypi.org/project/Graphillion/'
license=('MIT')
makedepends=('python2-setuptools' 'python-setuptools')
source=("https://files.pythonhosted.org/packages/source/${_pkgname::1}/$_pkgname/$_pkgname-$pkgver.tar.gz"
        'setup.py.patch')
sha512sums=('7dc74c35cac9a8d2f3f412d8aafa9c53fa4a70f70ea54c367149f9501a5fd0aea7246efb3317bcd38de261219ad433e78692a73be02c153c3d8868a2c7401b63'
            '303c3b32b1220c1fe01efe9a0811cb7bf61d59a81abd00a6a8d142d5ca3bd375edfd19f0153010fdfe0cc59ef023cca9c8b17c4761f670b63577881b404000fe')

prepare() {
  cp -a "$_pkgname-$pkgver"{,-2}

  cd "$_pkgname-$pkgver-2"
  patch -p0 < ../setup.py.patch
}

build() {
  cd "$_pkgname-$pkgver-2"

  python2 setup.py build

  cd "$srcdir/$_pkgname-$pkgver"

  python setup.py build
}

package_python2-graphillion() {
  depends=('python2' 'python2-future')

  cd "$_pkgname-$pkgver-2"

  python2 setup.py install --root="$pkgdir" --prefix=/usr -O1 --skip-build
}

package_python-graphillion() {
  depends=('python' 'python-future')

  cd "$_pkgname-$pkgver"

  python setup.py install --root="$pkgdir" --prefix=/usr -O1 --skip-build
}

