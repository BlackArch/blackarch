# This file is part of BlackArch Linux ( https://www.blackarch.org/ ).
# See COPYING for license details.

pkgbase=python-graphillion
pkgname=('python2-graphillion' 'python-graphillion')
_pkgname=Graphillion
pkgver=1.5
pkgrel=2
pkgdesc='Fast, lightweight library for a huge number of graphs.'
arch=('x86_64' 'aarch64')
url='https://pypi.org/project/Graphillion/'
license=('MIT')
makedepends=('python2-setuptools' 'python-setuptools')
source=("https://files.pythonhosted.org/packages/source/${_pkgname::1}/$_pkgname/Graphillion-$pkgver.tar.gz")
sha512sums=('79e5e0a0abc1fa0e9ff96a10f0822bf569c0a6fc5431727d63d8f0bfcd4c4e66d4a2432421fd6d75a9699eeff0f78a1157bfddfb8a7b7312f8693593c61beca8')

prepare() {
  cp -a "$_pkgname-$pkgver"{,-2}
}

build() {
  cd "$_pkgname-$pkgver-2"

  python2 setup.py build

  cd "$srcdir/$_pkgname-$pkgver"

  python setup.py build
}

package_python2-graphillion() {
  depends=('python2' 'python2-future')

  cd "$_pkgname-$pkgver-2"

  python2 setup.py install --root="$pkgdir" --prefix=/usr -O1 --skip-build
}

package_python-graphillion() {
  depends=('python' 'python-future')

  cd "$_pkgname-$pkgver"

  python setup.py install --root="$pkgdir" --prefix=/usr -O1 --skip-build
}

