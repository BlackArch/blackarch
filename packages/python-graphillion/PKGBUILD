# This file is part of BlackArch Linux ( https://www.blackarch.org/ ).
# See COPYING for license details.

pkgbase=python-graphillion
pkgname=('python2-graphillion' 'python-graphillion')
_pkgname=Graphillion
pkgver=1.3
pkgrel=2
pkgdesc='Fast, lightweight library for a huge number of graphs.'
arch=('x86_64' 'aarch64')
url='https://pypi.org/project/Graphillion/'
license=('MIT')
makedepends=('python2-setuptools' 'python-setuptools')
source=("https://files.pythonhosted.org/packages/3d/5b/5104de075566e8e5c3588a017f988dcb00ef53558bb603c5acc7630558a5/$_pkgname-$pkgver.tar.gz")
sha512sums=('fa25a254b0c4389f7cd8a7c748eb548c67ee59440dd6d430f4d6910e1b68e5d011c88170e6c233b28d7ed136577fb34aef4f40ddeaf618b3a5c80d51021c2292')

prepare() {
  cp -a "$_pkgname-$pkgver"{,-2}
}

build() {
  cd "$_pkgname-$pkgver-2"

  python2 setup.py build

  cd "$srcdir/$_pkgname-$pkgver"

  python setup.py build
}

package_python2-graphillion() {
  depends=('python2' 'python2-future')

  cd "$_pkgname-$pkgver-2"

  python2 setup.py install --prefix=/usr --root="$pkgdir" --optimize=1 \
    --skip-build
}

package_python-graphillion() {
  depends=('python' 'python-future')

  cd "$_pkgname-$pkgver"

  python setup.py install --prefix=/usr --root="$pkgdir" --optimize=1 \
    --skip-build
}

