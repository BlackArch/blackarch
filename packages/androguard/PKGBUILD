 # This file is part of BlackArch Linux ( https://www.blackarch.org/ ).
# See COPYING for license details.

pkgname=androguard
pkgver=3.2.1
pkgrel=1
epoch=2
groups=('blackarch' 'blackarch-binary' 'blackarch-disassembler'
        'blackarch-malware')
pkgdesc='Reverse engineering, Malware and goodware analysis of Android applications and more.'
arch=('any')
url='https://github.com/androguard/androguard'
license=('Apache2')
depends=('python' 'python-lxml' 'sparsehash' 'muparser' 'snappy' 'bzip2'
         'zlib' 'python-pydot' 'python-magic' 'python-networkx')
makedepends=('git' 'python-setuptools')
optdepends=(
  'ipython: interactive analysis support'
  'psyco-svn: acceleration support'
  'python-pygments'
)
source=("https://github.com/$pkgname/$pkgname/archive/v.$pkgver.tar.gz")
sha512sums=('76c68266a2f7c509ef6a8eea8e39e491d13bdf9e972d8df0e62c1d0efaf41effce3c0c10baadc43eebbeb5375b55c44f9e43a4ec5aeab7bbdce4bb91ca24f20f')

build() {
  cd $pkgname-v.$pkgver

  python setup.py build
}

package() {
  cd $pkgname-v.$pkgver

  install -Dm 644 README.md "$pkgdir/usr/share/doc/$pkgname/README.md"
  install -Dm 644 LICENCE-2.0 "$pkgdir/usr/share/licenses/$pkgname/LICENSE"

  python setup.py install --root="$pkgdir" --optimize=1 --skip-build

  for bin in $pkgdir/usr/bin/*
  do
    if [[ "$bin" != *"androguard"* ]]
    then
      mv $bin `echo $bin | sed 's/.py//'`
    fi
  done
}

