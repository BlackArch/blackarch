# This file is part of BlackArch Linux ( https://www.blackarch.org/ ).
# See COPYING for license details.

pkgname=androguard
pkgver=3.3.1
pkgrel=1
epoch=2
groups=('blackarch' 'blackarch-binary' 'blackarch-disassembler'
        'blackarch-malware')
pkgdesc='Reverse engineering, Malware and goodware analysis of Android applications and more.'
arch=('any')
url='https://github.com/androguard/androguard'
license=('Apache2')
depends=('python' 'ipython' 'python-future' 'python-networkx' 'python-pygments'
         'python-lxml' 'python-colorama' 'python-matplotlib' 'python-asn1crypto'
         'python-click')
makedepends=('python-setuptools')
optdepends=('python-pyperclip: GUI'
            'python-pyqt5: GUI'
            'python-magic: magic'
            'python-filemagic: magic alternative'
            'python-sphinx: docs'
            'python-sphinx_rtd_theme: docs'
            'python-pydot: graphing')
source=("https://github.com/$pkgname/$pkgname/archive/v$pkgver.tar.gz")
sha512sums=('6e352190b6957433713b070ebe02281644a8dbeae6bd93f6a18ff6d57b63bc21659b8d495ac7d0a5e21a8c72393f99790e2dd56c1257362d858184c578072902')

build() {
  cd "$pkgname-$pkgver"

  python setup.py build
}

package() {
  cd "$pkgname-$pkgver"

  install -dm 755 "$pkgdir/usr/bin"

  install -Dm 644 README.md "$pkgdir/usr/share/doc/$pkgname/README.md"
  install -Dm 644 LICENCE-2.0 "$pkgdir/usr/share/licenses/$pkgname/LICENSE"

  python setup.py install --root="$pkgdir" --optimize=1 --skip-build

  for bin in $pkgdir/usr/bin/*
  do
    if [ "$(echo $bin|awk -F/ '{print $NF}')" != "androguard" ]
    then
      mv $bin `echo $bin | sed 's/.py//'`
    fi
  done
}

