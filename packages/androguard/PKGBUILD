 # This file is part of BlackArch Linux ( https://www.blackarch.org/ ).
# See COPYING for license details.

pkgname=androguard
pkgver=1622.d93ed5d6
pkgrel=1
epoch=1
groups=('blackarch' 'blackarch-binary' 'blackarch-disassembler'
        'blackarch-malware')
pkgdesc='Reverse engineering, Malware and goodware analysis of Android applications and more.'
arch=('any')
url='https://github.com/androguard/androguard'
license=('Apache2')
depends=('python2' 'python2-lxml' 'sparsehash' 'muparser' 'snappy' 'bzip2'
         'zlib' 'python2-pydot' 'python2-magic' 'python2-networkx')
makedepends=('git' 'python2-setuptools')
optdepends=(
  'ipython2: interactive analysis support'
  'psyco2-svn: acceleration support'
  'python2-pygments'
)
source=('git+https://github.com/androguard/androguard.git')
sha512sums=('SKIP')

pkgver() {
  cd $pkgname

  echo $(git rev-list --count HEAD).$(git rev-parse --short HEAD)
}

build() {
  cd $pkgname

  python2 setup.py build
}

package() {
  cd $pkgname

  install -Dm 644 -t "$pkgdir/usr/share/doc/$pkgname/" README.md
  install -Dm 644 LICENCE-2.0 "$pkgdir/usr/share/licenses/$pkgname/LICENSE"

  python2 setup.py install --root="$pkgdir" --optimize=1

  for bin in $pkgdir/usr/bin/*
  do
    mv $bin `echo $bin | sed 's/.py//'`
  done
}

