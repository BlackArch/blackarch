# This file is part of BlackArch Linux ( https://www.blackarch.org/ ).
# See COPYING for license details.

pkgbase=python-mispy
pkgname=('python2-mispy' 'python-mispy')
_pkgname=mispy
pkgver=0.3
pkgrel=1
pkgdesc='A pythonic MISP module.'
arch=('any')
url='https://pypi.org/project/mispy/#files'
license=('APACHE')
makedepends=('python2-setuptools' 'python-setuptools')
source=("https://files.pythonhosted.org/packages/17/30/0a87936beb981c5ffb5ed2027a653bc08f194b829458657f0f1c335bb982/$_pkgname-$pkgver.tar.gz")
sha512sums=('208c9c24b8ac873619e53961c238b1ea5efa44331852a88061b41eca1bbebf32e83a5ad6ae346994ebf5cac83b011f17cd0e28e4e174b310a5a178a877473fcc')

prepare() {
  cp -a "$_pkgname-$pkgver"{,-2}
}

build() {
  cd "$_pkgname-$pkgver-2"

  python2 setup.py build

  cd "$srcdir/$_pkgname-$pkgver"

  python setup.py build
}

package_python2-mispy() {
  depends=('python2' 'python2-lxml' 'python2-requests')

  cd "$_pkgname-$pkgver-2"

  python2 setup.py install --prefix=/usr --root="$pkgdir" --optimize=1 \
    --skip-build
}

package_python-mispy() {
  depends=('python' 'python-lxml' 'python-requests')

  cd "$_pkgname-$pkgver"

  python setup.py install --prefix=/usr --root="$pkgdir" --optimize=1 \
    --skip-build
}

