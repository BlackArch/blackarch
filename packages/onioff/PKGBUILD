# This file is part of BlackArch Linux ( http://blackarch.org ).
# See COPYING for license details.

pkgname=onioff
pkgver=80.91bdffc
pkgrel=2
groups=('blackarch' 'blackarch-recon' 'blackarch-recon')
arch=('any')
pkgdesc='An onion url inspector for inspecting deep web links.'
url='https://github.com/k4m4/onioff'
license=('MIT')
depends=('python' 'python-requests' 'python-beautifulsoup4'
         'python-termcolor')
makedepends=('git')
source=('git+https://github.com/k4m4/onioff.git')
sha512sums=('SKIP')

pkgver() {
  cd "$srcdir/onioff"

  echo $(git rev-list --count HEAD).$(git rev-parse --short HEAD)
}

package() {
  cd "$srcdir/onioff"

  install -dm755 "$pkgdir/usr/bin"
  install -dm755 "$pkgdir/usr/share/onioff"

  install -Dm644 -t "$pkgdir/usr/share/doc/onioff/" readme.md \
    code-of-conduct.md
  install -Dm644 license "$pkgdir/usr/share/licenses/onioff/license"

  rm *.md license

  cp -a --no-preserve=ownership * "$pkgdir/usr/share/onioff"

cat > "$pkgdir/usr/bin/onioff" << EOF
#!/bin/sh
cd /usr/share/onioff
exec python3 onioff.py "\${@}"
EOF

  chmod a+x "$pkgdir/usr/bin/onioff"
}
