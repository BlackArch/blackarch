# This file is part of BlackArch Linux ( http://blackarch.org ).
# See COPYING for license details.

pkgname='inguma'
pkgver='0.1.1'
pkgrel=8
pkgdesc='A free penetration testing and vulnerability discovery toolkit entirely written in python. Framework includes modules to discover hosts, gather information about, fuzz targets, brute force usernames and passwords, exploits, and a disassembler.'
arch=('any')
groups=('blackarch' 'blackarch-cracker' 'blackarch-disassembler'
        'blackarch-exploitation' 'blackarch-fuzzer' 'blackarch-scanner')
url='http://inguma.sourceforge.net'
license=('GPL')
depends=('python2' 'python2-paramiko' 'python2-pyqt')
optdepends=('python2-cx_oracle: Access to Oracle databases'
            'python2-pysnmp: SNMP support'
            'pyqt3: GUI')
source=("http://downloads.sourceforge.net/inguma/inguma-${pkgver}.tar.gz")
sha1sums=('d9e59c5e11e5cf4aa7f4cd5b188023cf6fe9bca6')

package() {
  cd "$srcdir/inguma"

  mkdir -p "$pkgdir/usr/bin"
  mkdir -p "$pkgdir/usr/share/inguma"

  install -Dm644 -t "$pkgdir/usr/share/doc/inguma" AUTHORS BUGS ChangeLog TODO \
    THANKS REQUIRES README MODULES.txt TUTORIAL.txt
  install -Dm644 LICENSE "$pkgdir/usr/share/licenses/inguma/LICENSE"

  rm AUTHORS BUGS ChangeLog TODO THANKS REQUIRES README MODULES.txt \
    TUTORIAL.txt LICENSE COPYING.txt

  cp --no-preserve=ownership -a * "$pkgdir/usr/share/inguma"

  cat > "$pkgdir/usr/bin/inguma" << EOF
#!/bin/sh
cd /usr/share/inguma
exec python2 inguma.py
EOF

  chmod +x "$pkgdir/usr/bin/inguma"
}
