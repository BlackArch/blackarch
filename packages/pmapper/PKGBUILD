# This file is part of BlackArch Linux ( https://www.blackarch.org/ ).
# See COPYING for license details.

pkgname=pmapper
pkgver=15.d38a5de
pkgrel=1
pkgdesc='A tool for quickly evaluating IAM permissions in AWS.'
groups=('blackarch' 'blackarch-recon')
arch=('any')
url='https://github.com/nccgroup/PMapper'
license=('GAGPL')
depends=('python' 'python-argparse' 'python-botocore' 'python-pydot'
         'python-tqdm')
makedepends=('git')
source=('git+https://github.com/nccgroup/PMapper.git')
sha512sums=('SKIP')

pkgver() {
  cd PMapper

  echo $(git rev-list --count HEAD).$(git rev-parse --short HEAD)
}

package() {
  cd PMapper

  install -dm 755 "$pkgdir/usr/bin"
  install -dm 755 "$pkgdir/usr/share/$pkgname"

  install -Dm 644 -t "$pkgdir/usr/share/doc/$pkgname/" Readme.md
  install -Dm 644 LICENSE "$pkgdir/usr/share/licenses/$pkgname/LICENSE"

  rm Readme.md LICENSE

  cp -a * "$pkgdir/usr/share/$pkgname/"

  cat > "$pkgdir/usr/bin/$pkgname" << EOF
#!/bin/sh
cd /usr/share/$pkgname
exec python3 pmapper.py "\$@"
EOF

  chmod +x "$pkgdir/usr/bin/$pkgname"
}

