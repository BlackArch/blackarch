# This file is part of BlackArch Linux ( http://blackarch.org ).
# See COPYING for license details.

pkgname=pmapper
pkgver=15.d38a5de
pkgrel=1
pkgdesc='A tool for quickly evaluating IAM permissions in AWS.'
groups=('blackarch' 'blackarch-recon')
arch=('any')
url='https://github.com/nccgroup/PMapper'
license=('GAGPL')
depends=('python' 'python-argparse' 'python-botocore' 'python-pydot'
         'python-tqdm')
makedepends=('git')
source=('git+https://github.com/nccgroup/PMapper.git')
sha512sums=('SKIP')

pkgver() {
  cd "$srcdir/PMapper"

  echo $(git rev-list --count HEAD).$(git rev-parse --short HEAD)
}

package() {
  cd "$srcdir/PMapper"

  install -dm 755 "$pkgdir/usr/bin"
  install -dm 755 "$pkgdir/usr/share/pmapper"

  install -Dm644 -t "$pkgdir/usr/share/doc/pmapper/" Readme.md
  install -Dm644 LICENSE "$pkgdir/usr/share/licenses/pmapper/LICENSE"

  rm Readme.md LICENSE

  cp -a * "$pkgdir/usr/share/pmapper/"

  cat > "$pkgdir/usr/bin/pmapper" << EOF
#!/bin/sh
cd /usr/share/pmapper
exec python3 pmapper.py "\${@}"
EOF

  chmod a+x "$pkgdir/usr/bin/pmapper"
}
