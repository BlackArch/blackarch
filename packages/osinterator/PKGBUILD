# This file is part of BlackArch Linux ( http://blackarch.org ).
# See COPYING for license details.

pkgname='osinterator'
pkgver=3.8447f58
pkgrel=2
pkgdesc='Open Source Toolkit for Open Source Intelligence Gathering.'
groups=('blackarch' 'blackarch-recon')
arch=('any')
url='https://github.com/guitarmanj/OSINTerator'
license=('MIT')
depends=('python2' 'python2-xgoogle')
makedepends=('git')
source=('git+https://github.com/guitarmanj/OSINTerator.git')
sha1sums=('SKIP')

pkgver() {
  cd "$srcdir/OSINTerator"

  echo $(git rev-list --count HEAD).$(git rev-parse --short HEAD)
}

prepare() {
  cd "$srcdir/OSINTerator"

  sed -i 's|bin/python|bin/python2|' dorkedIn/dorkedIn.py
}

package() {
  cd "$srcdir/OSINTerator"

  install -Dm755 dorkedIn/dorkedIn.py "$pkgdir/usr/bin/osinterator"
  install -Dm644 dorkedIn/README.md "$pkgdir/usr/share/doc/osinterator/README.md"
  install -Dm644 LICENSE "$pkgdir/usr/share/licenses/osinterator/LICENSE"
}
