# This file is part of BlackArch Linux ( https://www.blackarch.org/ ).
# See COPYING for license details.

pkgname=bkcrack
pkgver=v1.3.0.r0.g6bcb65d
pkgrel=1
pkgdesc='Crack legacy zip encryption with Biham and Kocher known plaintext attack.'
arch=('aarch64' 'x86_64')
groups=('blackarch' 'blackarch-crackers')
url='https://github.com/kimci86/bkcrack'
license=('ZLIB')
depends=('gcc-libs')
makedepends=('git' 'cmake')
source=("git+https://github.com/kimci86/$pkgname.git")
sha512sums=('SKIP')

pkgver() {
  cd "$pkgname"

  git describe --long --tags | sed 's/\([^-]*-g\)/r\1/;s/-/./g'
}

build() {
  cd "$pkgname"

  cmake .

  make
}

package() {
  cd "$pkgname"

  install -dm 755 "$pkgdir/usr/share/$pkgname/"

  install -Dm 755 "src/bkcrack" "$pkgdir/usr/bin/bkcrack"

  install -Dm 644 license.txt "$pkgdir/usr/share/licenses/$pkgname/LICENSE"
  install -Dm 644 -t "$pkgdir/usr/share/doc/$pkgname/" *.md

  rm license.txt *.md

  cp -a * "$pkgdir/usr/share/$pkgname"
}

