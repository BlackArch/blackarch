# This file is part of BlackArch Linux ( https://www.blackarch.org/ ).
# See COPYING for license details.

pkgbase=python-splinter
pkgname=('python2-splinter' 'python-splinter')
_pkgname=splinter
pkgver=0.18.1
pkgrel=1
pkgdesc='Browser abstraction for web acceptance testing.'
arch=('any')
url='https://pypi.org/project/splinter/#files'
license=('custom:unknown')
makedepends=('python2-setuptools' 'python-setuptools')
source=("https://files.pythonhosted.org/packages/bc/dd/0ee4d53a3e652cebb045ef5507dd319d929170895667f16fe0c992b49ae2/$_pkgname-$pkgver.tar.gz")
sha512sums=('61e2569e55c635649e4ed2dbf20711b0847a7f92d25ef14105ef0409c487d8941eb5d440e6d35798385eba44a32b7da4b5539c9322b5a37816ffa60b0bbb4bb6')

prepare() {
  cp -a "$_pkgname-$pkgver"{,-2}
}

build() {
  cd "$_pkgname-$pkgver-2"

  python2 setup.py build

  cd "$srcdir/$_pkgname-$pkgver"

  python setup.py build
}

package_python2-splinter() {
  depends=('python2' 'python2-selenium')

  cd "$_pkgname-$pkgver-2"

  python2 setup.py install --prefix=/usr --root="$pkgdir" --optimize=1 \
    --skip-build
}

package_python-splinter() {
  depends=('python' 'python-selenium')

  cd "$_pkgname-$pkgver"

  python setup.py install --prefix=/usr --root="$pkgdir" --optimize=1 \
    --skip-build
}

