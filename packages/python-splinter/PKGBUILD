# This file is part of BlackArch Linux ( https://www.blackarch.org/ ).
# See COPYING for license details.

pkgbase=python-splinter
pkgname=('python2-splinter' 'python-splinter')
_pkgname=splinter
pkgver=0.10.0
pkgrel=3
pkgdesc='Browser abstraction for web acceptance testing.'
arch=('any')
url='https://pypi.org/project/splinter/#files'
license=('custom:unknown')
makedepends=('python2-setuptools' 'python-setuptools')
source=("https://files.pythonhosted.org/packages/6e/86/fe3b6771846165ce8dc88996aa8e0846d0e6839e7c5a74f4e34ba30e1019/$_pkgname-$pkgver.tar.gz")
sha512sums=('2fa8015a4d395c7b2293b03738c28169ed8b4275069640c53ead5d50b637b7e15ed8bf98a35003b29e9d810444948906b08d0ffa94a20eaab8ad316c2c6605af')

prepare() {
  cp -a "$_pkgname-$pkgver" "$_pkgname-$pkgver-2"
}

build() {
  cd "$_pkgname-$pkgver-2"

  python2 setup.py build

  cd "$srcdir/$_pkgname-$pkgver"

  python setup.py build
}

package_python2-splinter() {
  depends=('python2' 'python2-selenium')

  cd "$_pkgname-$pkgver-2"

  python2 setup.py install --prefix=/usr --root="$pkgdir" --optimize=1 \
    --skip-build
}

package_python-splinter() {
  depends=('python' 'python-selenium')

  cd "$_pkgname-$pkgver"

  python setup.py install --prefix=/usr --root="$pkgdir" --optimize=1 \
    --skip-build
}

