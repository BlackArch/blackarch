# This file is part of BlackArch Linux ( https://www.blackarch.org/ ).
# See COPYING for license details.

pkgbase=python-splinter
pkgname=('python2-splinter' 'python-splinter')
_pkgname=splinter
pkgver=0.13.0
pkgrel=1
pkgdesc='Browser abstraction for web acceptance testing.'
arch=('any')
url='https://pypi.org/project/splinter/#files'
license=('custom:unknown')
makedepends=('python2-setuptools' 'python-setuptools')
source=("https://files.pythonhosted.org/packages/d1/46/bb01079ef246d61c2432420c6cd63ecb11e0e909a5da42abcb407e0fb4e2/$_pkgname-$pkgver.tar.gz")
sha512sums=('a5a2da3ca01acf37b03d4b12f6565f50e50f75234ce884c13d9e9a4eb991849c668808bb45487174526a950b6577a01f1f72221fdcc22ff90d9ad71c4711a291')

prepare() {
  cp -a "$_pkgname-$pkgver"{,-2}
}

build() {
  cd "$_pkgname-$pkgver-2"

  python2 setup.py build

  cd "$srcdir/$_pkgname-$pkgver"

  python setup.py build
}

package_python2-splinter() {
  depends=('python2' 'python2-selenium')

  cd "$_pkgname-$pkgver-2"

  python2 setup.py install --prefix=/usr --root="$pkgdir" --optimize=1 \
    --skip-build
}

package_python-splinter() {
  depends=('python' 'python-selenium')

  cd "$_pkgname-$pkgver"

  python setup.py install --prefix=/usr --root="$pkgdir" --optimize=1 \
    --skip-build
}

