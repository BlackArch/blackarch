# This file is part of BlackArch Linux ( https://www.blackarch.org/ ).
# See COPYING for license details.

pkgbase=python-splinter
pkgname=('python2-splinter' 'python-splinter')
_pkgname=splinter
pkgver=0.17.0
pkgrel=1
pkgdesc='Browser abstraction for web acceptance testing.'
arch=('any')
url='https://pypi.org/project/splinter/#files'
license=('custom:unknown')
makedepends=('python2-setuptools' 'python-setuptools')
source=("https://files.pythonhosted.org/packages/53/ba/12be8f4b433e49fb9f5dfed62ae13f3871dff265625fb291f32cae3a364e/$_pkgname-$pkgver.tar.gz")
sha512sums=('9a12bb9ca7593ed95d95427ff092541f711ff8e70674c02e4a81c51146b4fbe9e56e70d92f61e4c63dbb3c55b87427c61d77dedac449ce2912e705be39863ae5')

prepare() {
  cp -a "$_pkgname-$pkgver"{,-2}
}

build() {
  cd "$_pkgname-$pkgver-2"

  python2 setup.py build

  cd "$srcdir/$_pkgname-$pkgver"

  python setup.py build
}

package_python2-splinter() {
  depends=('python2' 'python2-selenium')

  cd "$_pkgname-$pkgver-2"

  python2 setup.py install --prefix=/usr --root="$pkgdir" --optimize=1 \
    --skip-build
}

package_python-splinter() {
  depends=('python' 'python-selenium')

  cd "$_pkgname-$pkgver"

  python setup.py install --prefix=/usr --root="$pkgdir" --optimize=1 \
    --skip-build
}

