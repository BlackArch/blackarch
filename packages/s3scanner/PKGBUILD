# This file is part of BlackArch Linux ( https://www.blackarch.org/ ).
# See COPYING for license details.

pkgname=s3scanner
pkgver=405.6f7a679
pkgrel=1
pkgdesc='Scan for open S3 buckets and dump.'
groups=('blackarch' 'blackarch-scanner')
arch=('any')
url='https://github.com/sa7mon/S3Scanner'
license=('MIT')
depends=('python' 'aws-cli' 'python-argparse' 'python-pytest-xdist'
         'python-coloredlogs' 'python-boto3' 'python-requests' 'python-sh')
makedepends=('git')
source=("$pkgname::git+https://github.com/sa7mon/S3Scanner.git")
sha512sums=('SKIP')

pkgver() {
  cd $pkgname

  echo $(git rev-list --count HEAD).$(git rev-parse --short HEAD)
}

package() {
  cd $pkgname

  install -dm 755 "$pkgdir/usr/bin"
  install -dm 755 "$pkgdir/usr/share/$pkgname"

  install -Dm 644 -t "$pkgdir/usr/share/doc/$pkgname/" README.md

  rm README.md

  cp -a * "$pkgdir/usr/share/$pkgname/"

  cat > "$pkgdir/usr/bin/$pkgname" << EOF
#!/bin/sh
cd /usr/share/$pkgname
exec python $pkgname.py "\$@"
EOF

  chmod +x "$pkgdir/usr/bin/$pkgname"
}

