# This file is part of BlackArch Linux ( https://www.blackarch.org/ ).
# See COPYING for license details.

pkgname=s3Scanner
_pkgname=s3scanner
pkgver=451.7e470a9
pkgrel=1
pkgdesc='A tool to find open S3 buckets in AWS or other cloud providers.'
arch=('x86_64')
groups=('blackarch' 'blackarch-scanner')
url='https://github.com/sa7mon/S3Scanner'
license=('MIT')
depends=()
makedepends=('git' 'go')
source=("git+https://github.com/sa7mon/$pkgname.git")
sha512sums=('SKIP')

pkgver() {
  cd $pkgname

  echo $(git rev-list --count HEAD).$(git rev-parse --short HEAD)
}

build() {
  cd $pkgname

  GOPATH="$srcdir" go mod download
  GOPATH="$srcdir" go build \
    -trimpath \
    -buildmode=pie \
    -mod=readonly \
    -modcacherw \
    -ldflags "-s -w" \
    -o $_pkgname .
}

package() {
  cd $pkgname

  install -Dm 755 $_pkgname "$pkgdir/usr/bin/$_pkgname"
  install -Dm 644 -t "$pkgdir/usr/share/doc/$_pkgname/" README.md
  install -Dm 644 LICENSE "$pkgdir/usr/share/licenses/$_pkgname/LICENSE"
}

