# This file is part of BlackArch Linux ( https://www.blackarch.org/ ).
# See COPYING for license details.

pkgname=s3scanner
_pkgname=s3Scanner
pkgver=457.9ee77f9
pkgrel=1
pkgdesc='A tool to find open S3 buckets in AWS or other cloud providers.'
arch=('x86_64' 'aarch64')
groups=('blackarch' 'blackarch-scanner')
url='https://github.com/sa7mon/S3Scanner'
license=('MIT')
depends=('glibc')
makedepends=('git' 'go')
source=("git+https://github.com/sa7mon/$_pkgname.git")
sha512sums=('SKIP')

pkgver() {
  cd $_pkgname

  echo $(git rev-list --count HEAD).$(git rev-parse --short HEAD)
}

build() {
  cd $_pkgname

  GOPATH="$srcdir" go mod download
  GOPATH="$srcdir" go build \
    -trimpath \
    -buildmode=pie \
    -mod=readonly \
    -modcacherw \
    -ldflags "-s -w" \
    -o $pkgname .
}

package() {
  cd $_pkgname

  install -Dm 755 $pkgname "$pkgdir/usr/bin/$pkgname"
  install -Dm 644 -t "$pkgdir/usr/share/doc/$pkgname/" README.md
  install -Dm 644 LICENSE "$pkgdir/usr/share/licenses/$pkgname/LICENSE"
}

