# This file is part of BlackArch Linux ( https://www.blackarch.org/ ).
# See COPYING for license details.

pkgname=bokken
pkgver=1.8
pkgrel=5
epoch=1
pkgdesc='GUI for radare2 and pyew.'
arch=('any')
groups=('blackarch' 'blackarch-misc' 'blackarch-reversing'
        'blackarch-disassembler')
url='http://inguma.eu/projects/bokken/'
license=('GPL')
depends=('python2' 'graphviz' 'r2-bindings' 'pyew' 'pygtk' 'gtksourceview2'
         'python2-pillow' 'gtksourceview3' 'radare2' 'python2-cairo' 'pygtk'
         'python2-xdot' 'pygtksourceview2')
makedepends=('mercurial')
options=('!strip')
source=("http://www.mirrorservice.org/sites/ftp.netbsd.org/pub/pkgsrc/distfiles/bokken-$pkgver.tar.gz")
#source=("https://inguma.eu/attachments/download/212/bokken-$pkgver.tar.gz")
sha512sums=('4fc34c054b018bdf2012024827d909420f4384d3de32196b319299794a9e7ccf0d1ce22987bad5526af077fe2486b0b3d89ce62ef762c3c84dee58cb32fa09d0')

prepare() {
  cd "$pkgname-$pkgver"

  sed -e 's/env python$/env python2/g' -i ui/xdot.py lib/web/__init__.py \
    lib/web/utils.py
  sed -e 's|/usr/bin/python|/usr/bin/env python2|g' -i bokken.py
}

package() {
  cd "$pkgname-$pkgver"

  install -dm 755 "$pkgdir/usr/bin"
  install -dm 755 "$pkgdir/usr/share/$pkgname"

  cp -a --no-preserve=ownership * "$pkgdir/usr/share/$pkgname/"

  cat > "$pkgdir/usr/bin/$pkgname" << EOF
#!/bin/sh
cd /usr/share/$pkgname/
exec ./$pkgname "\$@"
EOF

  chmod +x "$pkgdir/usr/bin/$pkgname"
}

