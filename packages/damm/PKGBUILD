# This file is part of BlackArch Linux ( https://www.blackarch.org/ ).
# See COPYING for license details.

pkgname=damm
pkgver=32.60e7ec7
pkgrel=1
groups=('blackarch' 'blackarch-malware' 'blackarch-binary' 'blackarch-reversing')
pkgdesc='Differential Analysis of Malware in Memory.'
arch=('any')
url='https://github.com/504ensicsLabs/DAMM'
license=('GPL2')
depends=('python2')
makedepends=('git')
source=('git+https://github.com/504ensicsLabs/DAMM.git')
sha512sums=('SKIP')

pkgver() {
  cd DAMM

  echo $(git rev-list --count HEAD).$(git rev-parse --short HEAD)
}

package() {
  cd DAMM

  install -dm 755 "$pkgdir/usr/bin"
  install -dm 755 "$pkgdir/usr/share/$pkgname"

  install -Dm 644 README.md "$pkgdir/usr/share/doc/$pkgname/README.md"
  install -Dm 644 LICENSE "$pkgdir/usr/share/licenses/$pkgname/LICENSE"

  rm README.md LICENSE

  cp -a * "$pkgdir/usr/share/$pkgname/"

  cat > "$pkgdir/usr/bin/$pkgname" << EOF
#!/bin/sh
cd /usr/share/$pkgname
exec python2 damm.py "\$@"
EOF

  chmod a+x "$pkgdir/usr/bin/$pkgname"
}

