# This file is part of BlackArch Linux ( https://www.blackarch.org/ ).
# See COPYING for license details.

pkgbase=python-cx_oracle
pkgname=('python2-cx_oracle' 'python-cx_oracle')
_pkgname=cx_Oracle
pkgver=8.0.1
pkgrel=2
pkgdesc='Python interface to Oracle.'
arch=('x86_64' 'aarch64')
url='https://pypi.org/project/cx_Oracle/#files'
license=('BSD')
makedepends=('python2-setuptools' 'python-setuptools')
source=("https://files.pythonhosted.org/packages/37/4c/e364244a2f76bc5af3987d874f56803bcc4c7e62f03d0de937d0ee457055/cx_Oracle-$pkgver.tar.gz")
sha512sums=('57c907f6ee812f6a58af0437acba927c2d9b3c9c06592d02c22fe4766d063807595a0ececa941ccb24d35f74fe1a53d28a7ba6c5acbe35ea997ad413a8fcd2e2')

prepare() {
  cp -a "$_pkgname-$pkgver"{,-2}
}

build() {
  cd "$_pkgname-$pkgver-2"

  python2 setup.py build

  cd "$srcdir/$_pkgname-$pkgver"

  python setup.py build
}

package_python2-cx_oracle() {
  depends=('python2')

  install -dm 755 "$pkgdir/usr/share/"{doc,"licenses/${pkgname[0]}"}

  cd "$_pkgname-$pkgver-2"

  python2 setup.py install --prefix=/usr --root="$pkgdir" --optimize=1 \
    --skip-build

  mv "$pkgdir/usr/$_pkgname-doc" "$pkgdir/usr/share/doc/${pkgname[0]}/"
  mv "$pkgdir/usr/share/doc/${pkgname[0]}/LICENSE.txt" \
    "$pkgdir/usr/share/licenses/${pkgname[0]}/LICENSE"
}

package_python-cx_oracle() {
  depends=('python')

  install -dm 755 "$pkgdir/usr/share/"{doc,"licenses/${pkgname[1]}"}

  cd "$_pkgname-$pkgver"

  python setup.py install --prefix=/usr --root="$pkgdir" --optimize=1 \
    --skip-build

  mv "$pkgdir/usr/$_pkgname-doc" "$pkgdir/usr/share/doc/${pkgname[1]}/"
  mv "$pkgdir/usr/share/doc/${pkgname[1]}/LICENSE.txt" \
    "$pkgdir/usr/share/licenses/${pkgname[1]}/LICENSE"
}

