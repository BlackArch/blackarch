# This file is part of BlackArch Linux ( https://www.blackarch.org/ ).
# See COPYING for license details.

pkgbase=python-cx_oracle
pkgname=('python2-cx_oracle' 'python-cx_oracle')
_pkgname=cx_Oracle
pkgver=7.2.3
pkgrel=1
pkgdesc='Python interface to Oracle.'
arch=('x86_64' 'aarch64')
url='https://pypi.org/project/cx_Oracle/#files'
license=('BSD')
makedepends=('python2-setuptools' 'python-setuptools')
source=("https://files.pythonhosted.org/packages/d0/00/2d4b1e27da6dda2408dcfe4e192b26c2e190be573c6aadb0c97739f15774/cx_Oracle-$pkgver.tar.gz")
sha512sums=('9293fcbec920ff164fb863d2ad5e9085a2fca3773730887ab20bd4e93b11aae078a1e14e13196e357ca8985debecd67ce0ad0532fd71296c9d159d0bd00b887b')

prepare() {
  cp -a "$_pkgname-$pkgver"{,-2}
}

build() {
  cd "$_pkgname-$pkgver-2"

  python2 setup.py build

  cd "$srcdir/$_pkgname-$pkgver"

  python setup.py build
}

package_python2-cx_oracle() {
  depends=('python2')

  install -dm 755 "$pkgdir/usr/share/"{doc,"licenses/${pkgname[0]}"}

  cd "$_pkgname-$pkgver-2"

  python2 setup.py install --prefix=/usr --root="$pkgdir" --optimize=1 \
    --skip-build

  mv "$pkgdir/usr/$_pkgname-doc" "$pkgdir/usr/share/doc/${pkgname[0]}/"
  mv "$pkgdir/usr/share/doc/${pkgname[0]}/LICENSE.txt" \
    "$pkgdir/usr/share/licenses/${pkgname[0]}/LICENSE"
}

package_python-cx_oracle() {
  depends=('python')

  install -dm 755 "$pkgdir/usr/share/"{doc,"licenses/${pkgname[1]}"}

  cd "$_pkgname-$pkgver"

  python setup.py install --prefix=/usr --root="$pkgdir" --optimize=1 \
    --skip-build

  mv "$pkgdir/usr/$_pkgname-doc" "$pkgdir/usr/share/doc/${pkgname[1]}/"
  mv "$pkgdir/usr/share/doc/${pkgname[1]}/LICENSE.txt" \
    "$pkgdir/usr/share/licenses/${pkgname[1]}/LICENSE"
}

