# This file is part of BlackArch Linux ( https://www.blackarch.org/ ).
# See COPYING for license details.

pkgbase=python-cx_oracle
pkgname=('python2-cx_oracle' 'python-cx_oracle')
_pkgname=cx_Oracle
pkgver=7.1.3
pkgrel=2
pkgdesc='Python interface to Oracle.'
arch=('x86_64' 'armv6h' 'armv7h' 'aarch64')
url='https://pypi.org/project/cx_Oracle/#files'
license=('BSD')
makedepends=('python2-setuptools' 'python-setuptools')
source=("https://files.pythonhosted.org/packages/74/cc/502ac9d82f609f1612f3137afb53e18c07430132f28fa2bedd96f7634898/$_pkgname-$pkgver.tar.gz")
sha512sums=('640b74e73eb215129251309d75787ec68dfea0d66adbb174d982e50c429dcfc6ba2ea67abed4c9a1fea4dc520052fb1bbbe9e393ee16fdcca4eae2feaf2c0ce9')

prepare() {
  cp -a "$_pkgname-$pkgver"{,-2}
}

build() {
  cd "$_pkgname-$pkgver-2"

  python2 setup.py build

  cd "$srcdir/$_pkgname-$pkgver"

  python setup.py build
}

package_python2-cx_oracle() {
  depends=('python2')

  install -dm 755 "$pkgdir/usr/share/"{doc,"licenses/${pkgname[0]}"}

  cd "$_pkgname-$pkgver-2"

  python2 setup.py install --prefix=/usr --root="$pkgdir" --optimize=1 \
    --skip-build

  mv "$pkgdir/usr/$_pkgname-doc" "$pkgdir/usr/share/doc/${pkgname[0]}/"
  mv "$pkgdir/usr/share/doc/${pkgname[0]}/LICENSE.txt" \
    "$pkgdir/usr/share/licenses/${pkgname[0]}/LICENSE"
}

package_python-cx_oracle() {
  depends=('python')

  install -dm 755 "$pkgdir/usr/share/"{doc,"licenses/${pkgname[1]}"}

  cd "$_pkgname-$pkgver"

  python setup.py install --prefix=/usr --root="$pkgdir" --optimize=1 \
    --skip-build

  mv "$pkgdir/usr/$_pkgname-doc" "$pkgdir/usr/share/doc/${pkgname[1]}/"
  mv "$pkgdir/usr/share/doc/${pkgname[1]}/LICENSE.txt" \
    "$pkgdir/usr/share/licenses/${pkgname[1]}/LICENSE"
}

