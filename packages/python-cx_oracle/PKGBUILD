# This file is part of BlackArch Linux ( https://www.blackarch.org/ ).
# See COPYING for license details.

pkgbase=python-cx_oracle
pkgname=('python2-cx_oracle' 'python-cx_oracle')
_pkgname=cx_Oracle
pkgver=7.2.1
pkgrel=1
pkgdesc='Python interface to Oracle.'
arch=('x86_64' 'armv6h' 'armv7h' 'aarch64')
url='https://pypi.org/project/cx_Oracle/#files'
license=('BSD')
makedepends=('python2-setuptools' 'python-setuptools')
source=("https://files.pythonhosted.org/packages/ec/b3/bb3cc4f8d0cabf2da7aabc7b1667d160907c5c5557fd379e9d7bd0a20e97/cx_Oracle-$pkgver.tar.gz")
sha512sums=('fcf13575599d1dedf6ae807d0e117dbbe5d1e476ca3e6223f075e9546e38986a886c83c99e03ead737044fdc7d95f6432826696fda238be6777d8a4041fb8f6c')

prepare() {
  cp -a "$_pkgname-$pkgver"{,-2}
}

build() {
  cd "$_pkgname-$pkgver-2"

  python2 setup.py build

  cd "$srcdir/$_pkgname-$pkgver"

  python setup.py build
}

package_python2-cx_oracle() {
  depends=('python2')

  install -dm 755 "$pkgdir/usr/share/"{doc,"licenses/${pkgname[0]}"}

  cd "$_pkgname-$pkgver-2"

  python2 setup.py install --prefix=/usr --root="$pkgdir" --optimize=1 \
    --skip-build

  mv "$pkgdir/usr/$_pkgname-doc" "$pkgdir/usr/share/doc/${pkgname[0]}/"
  mv "$pkgdir/usr/share/doc/${pkgname[0]}/LICENSE.txt" \
    "$pkgdir/usr/share/licenses/${pkgname[0]}/LICENSE"
}

package_python-cx_oracle() {
  depends=('python')

  install -dm 755 "$pkgdir/usr/share/"{doc,"licenses/${pkgname[1]}"}

  cd "$_pkgname-$pkgver"

  python setup.py install --prefix=/usr --root="$pkgdir" --optimize=1 \
    --skip-build

  mv "$pkgdir/usr/$_pkgname-doc" "$pkgdir/usr/share/doc/${pkgname[1]}/"
  mv "$pkgdir/usr/share/doc/${pkgname[1]}/LICENSE.txt" \
    "$pkgdir/usr/share/licenses/${pkgname[1]}/LICENSE"
}

