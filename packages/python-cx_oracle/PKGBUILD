# This file is part of BlackArch Linux ( https://www.blackarch.org/ ).
# See COPYING for license details.

pkgbase=python-cx_oracle
pkgname=('python2-cx_oracle' 'python-cx_oracle')
_pkgname=cx_Oracle
pkgver=7.3.0
pkgrel=3
pkgdesc='Python interface to Oracle.'
arch=('x86_64' 'aarch64')
url='https://pypi.org/project/cx_Oracle/#files'
license=('BSD')
makedepends=('python2-setuptools' 'python-setuptools' 'python2-pip' 'python-pip'
             'python2-wheel' 'python-wheel')
source=("https://files.pythonhosted.org/packages/source/${_pkgname::1}/$_pkgname/$_pkgname-$pkgver.tar.gz")
sha512sums=('158041e6de3ea17f356d846ca651609aef746ddcf5fe0852bc4ac31b5316414e3e9c734fdb4b236324bb5294bba43c6ad274f145085f8a80428e2682b2fd414f')

prepare() {
  cp -a "$_pkgname-$pkgver"{,-2}
}

build() {
  cd "$_pkgname-$pkgver-2"

  python2 setup.py build

  cd "$srcdir/$_pkgname-$pkgver"

  python setup.py build
}

package_python2-cx_oracle() {
  depends=('python2')

  install -dm 755 "$pkgdir/usr/share/"{doc,"licenses/${pkgname[0]}"}

  cd "$_pkgname-$pkgver-2"

  python2 setup.py install --root="$pkgdir" --prefix=/usr -O1 --skip-build

  mv "$pkgdir/usr/$_pkgname-doc" "$pkgdir/usr/share/doc/${pkgname[0]}/"
  mv "$pkgdir/usr/share/doc/${pkgname[0]}/LICENSE.txt" \
    "$pkgdir/usr/share/licenses/${pkgname[0]}/LICENSE"
}

package_python-cx_oracle() {
  depends=('python')

  install -dm 755 "$pkgdir/usr/share/"{doc,"licenses/${pkgname[1]}"}

  cd "$_pkgname-$pkgver"

  python setup.py install --root="$pkgdir" --prefix=/usr -O1 --skip-build

  mv "$pkgdir/usr/$_pkgname-doc" "$pkgdir/usr/share/doc/${pkgname[1]}/"
  mv "$pkgdir/usr/share/doc/${pkgname[1]}/LICENSE.txt" \
    "$pkgdir/usr/share/licenses/${pkgname[1]}/LICENSE"
}

