# This file is part of BlackArch Linux ( http://blackarch.org ).
# See COPYING for license details.

pkgname='mosca'
pkgver=109.e9bc968
pkgrel=1
groups=('blackarch' 'blackarch-code-audit')
pkgdesc='Static analysis tool to find bugs like a grep unix command.'
arch=('x86_64' 'armv6h' 'armv7h' 'aarch64')
url='https://github.com/CoolerVoid/Mosca'
license=('GPL3')
depends=('pcre')
makedepends=('git')
source=('git+https://github.com/CoolerVoid/Mosca.git')
sha1sums=('SKIP')

pkgver() {
  cd "$srcdir/Mosca"

  echo $(git rev-list --count HEAD).$(git rev-parse --short HEAD)
}

build() {
  cd "$srcdir/Mosca"

  make
}

package() {
  cd "$srcdir/Mosca"

  install -Dm755 mosca "$pkgdir/usr/bin/mosca"
  install -Dm644 -t "$pkgdir/usr/share/doc/mosca/" README.md TODO
  install -Dm644 LICENSE.txt "$pkgdir/usr/share/licenses/mosca/LICENSE.txt"
}
