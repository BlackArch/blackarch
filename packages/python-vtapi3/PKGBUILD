# This file is part of BlackArch Linux ( https://www.blackarch.org/ ).
# See COPYING for license details.

pkgname=python-vtapi3
_pkgname=virustotalapi3
pkgver=265.55b43d5
pkgrel=3
pkgdesc='The module that implements the VirusTotal API functions (3 version).'
arch=('any')
url='https://github.com/drobotun/virustotalapi3'
license=('MIT')
depends=('python' 'python-requests')
makedepends=('python-setuptools' 'git')
options=(!emptydirs)
source=("git+https://github.com/drobotun/$_pkgname.git")
sha512sums=('SKIP')

pkgver() {
  cd $_pkgname

  echo $(git rev-list --count HEAD).$(git rev-parse --short HEAD)
}

build() {
  cd "$_pkgname"

  python setup.py build
}

package() {
  cd "$_pkgname"

  python setup.py install --root="$pkgdir" --prefix=/usr -O1 --skip-build
}

