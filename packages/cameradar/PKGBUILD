# This file is part of BlackArch Linux ( https://www.blackarch.org/ ).
# See COPYING for license details.

pkgname=cameradar
pkgver=3.0.0
pkgrel=1
pkgdesc='Hacks its way into RTSP videosurveillance cameras.'
arch=('any')
groups=('blackarch' 'blackarch-scanner')
url='https://github.com/Ullaakut/cameradar'
license=('MIT')
makedepends=('go-pie' 'dep' 'git')
source=("https://github.com/Ullaakut/cameradar/archive/v$pkgver.tar.gz")
sha512sums=('0645d3f33ac9a049c1134b297cee9714c35c81215a727d1d7114e144f40b65df96114e1d043f574b8d043cfa3bef68c091e75bd8ffe13106daed71fb54c4322a')

prepare() {
  mkdir -p "gopath/src/github.com/Ullaakut"
  ln -rTsf "$pkgname-$pkgver" "gopath/src/github.com/Ullaakut/$pkgname"

  export GOPATH="$srcdir/gopath"
  cd "$GOPATH/src/github.com/Ullaakut/$pkgname"

  dep ensure
}

build() {
  export GOPATH="$srcdir/gopath"
  cd "$GOPATH/src/github.com/Ullaakut/$pkgname/$pkgname"

  go install
}

package() {
  export GOPATH="$srcdir/gopath"
  cd "$GOPATH/bin"

  install -Dm 755 $pkgname "$pkgdir/usr/bin/$pkgname"
  install -Dm 755 "$GOPATH/src/github.com/Ullaakut/$pkgname/LICENSE" \
    "$pkgdir/usr/share/licenses/$pkgname/LICENSE"
  install -Dm 755 "$GOPATH/src/github.com/Ullaakut/$pkgname/README.md" \
    "$pkgdir/usr/share/doc/$pkgname/README.md"
}

