# This file is part of BlackArch Linux ( https://www.blackarch.org/ ).
# See COPYING for license details.

pkgname=cameradar
pkgver=3.0.0
pkgrel=2
pkgdesc='Hacks its way into RTSP videosurveillance cameras.'
arch=('x86_64')
groups=('blackarch' 'blackarch-scanner')
url='https://github.com/Ullaakut/cameradar'
license=('MIT')
makedepends=('go-pie' 'dep' 'git')
source=("https://github.com/Ullaakut/cameradar/archive/v$pkgver.tar.gz")
sha512sums=('0645d3f33ac9a049c1134b297cee9714c35c81215a727d1d7114e144f40b65df96114e1d043f574b8d043cfa3bef68c091e75bd8ffe13106daed71fb54c4322a')
_src='src/github.com/Ullaakut'
_gopath="gopath/$_src"

prepare() {
  mkdir -p $_gopath
  ln -rTsf "$pkgname-$pkgver" "$_gopath/$pkgname"

  export GOPATH="$srcdir/gopath"
  cd "$GOPATH/$_src/$pkgname"

  dep ensure
}

build() {
  export GOPATH="$srcdir/gopath"
  cd "$GOPATH/$_src/$pkgname/$pkgname"

  go install
}

package() {
  export GOPATH="$srcdir/gopath"
  cd "$GOPATH/bin"

  install -dm 755 "$pkgdir/usr/bin"
  install -dm 755 "$pkgdir/usr/share/$pkgname/$_src/$pkgname"

  install -Dm 755 $pkgname "$pkgdir/usr/share/$pkgname/$pkgname"
  install -Dm 755 "$GOPATH/$_src/$pkgname/LICENSE" \
    "$pkgdir/usr/share/licenses/$pkgname/LICENSE"
  install -Dm 644 -t "$pkgdir/usr/share/doc/$pkgname/" \
    "$GOPATH/$_src/$pkgname/"*.md \

  cd "$srcdir/$_gopath/$pkgname"
  cp -a dictionaries examples images "$pkgdir/usr/share/$pkgname/"

  cat > "$pkgdir/usr/bin/$pkgname" << EOF
#!/bin/sh
export GOPATH="/usr/share/$pkgname/"
exec /usr/share/$pkgname/$pkgname "\$@"
EOF

  chmod a+x "$pkgdir/usr/bin/$pkgname"
}

