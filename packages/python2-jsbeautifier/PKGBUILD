# This file is part of BlackArch Linux ( https://www.blackarch.org/ ).
# See COPYING for license details.

pkgname=python2-jsbeautifier
_pkgname=js-beautify
pkgver=1.11.0
pkgrel=1
pkgdesc='JavaScript unobfuscator and beautifier.'
groups=('blackarch' 'blackarch-webapp' 'blackarch-reversing')
arch=('any')
license=('MIT')
url='https://github.com/beautify-web/js-beautify'
depends=('python2' 'python2-six' 'python2-editorconfig')
makedepends=('python2-setuptools')
source=("$pkgname-$pkgver.tar.gz::https://github.com/beautify-web/$_pkgname/archive/v$pkgver.tar.gz")
sha512sums=('1bcf0da38a9f465f4afebd981800b19e2650e0d7cb48dbf6a6d801da449e5e25b57244a6b57654d6760d9287496f8a1d23f27ab3e52e66b27a69ce6750989a73')

build() {
  cd "$_pkgname-$pkgver/python"

  python2 setup.py build
}

package() {
  cd "$_pkgname-$pkgver/python"

  python2 setup.py install --root="$pkgdir" --optimize=1 --skip-build

  install -Dm 644 ../LICENSE "$pkgdir/usr/share/licenses/$pkgname/LICENSE"

  mv "$pkgdir/usr/bin/$_pkgname" "$pkgdir/usr/bin/${_pkgname}2"
}

