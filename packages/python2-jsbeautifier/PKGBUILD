# This file is part of BlackArch Linux ( https://www.blackarch.org/ ).
# See COPYING for license details.

pkgname=python2-jsbeautifier
_pkgname=js-beautify
pkgver=1.10.2
pkgrel=1
pkgdesc='JavaScript unobfuscator and beautifier.'
groups=('blackarch' 'blackarch-webapp' 'blackarch-reversing')
arch=('any')
license=('MIT')
url='https://github.com/beautify-web/js-beautify'
depends=('python2' 'python2-six' 'python2-editorconfig')
makedepends=('python2-setuptools')
source=("$pkgname-$pkgver.tar.gz::https://github.com/beautify-web/$_pkgname/archive/v$pkgver.tar.gz")
sha512sums=('6a65bcf0a0c2277c40d3eed6be47152aa4b27cd7613100b411522a18381fe15c2cf92ca64d5dcb1b65903e2ee47ac6be9a2b9c1b8d5d4caa7250e7af8454a49c')

build() {
  cd "$_pkgname-$pkgver/python"

  python2 setup.py build
}

package() {
  cd "$_pkgname-$pkgver/python"

  python2 setup.py install --root="$pkgdir" --optimize=1 --skip-build

  install -Dm 644 ../LICENSE "$pkgdir/usr/share/licenses/$pkgname/LICENSE"

  mv "$pkgdir/usr/bin/$_pkgname" "$pkgdir/usr/bin/${_pkgname}2"
}

