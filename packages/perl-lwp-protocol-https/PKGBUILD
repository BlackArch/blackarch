pkgname=perl-lwp-protocol-https
pkgver=6.04
pkgrel=1
pkgdesc="Provide https support for LWP::UserAgent"
arch=('any')
url="https://metacpan.org/release/LWP-Protocol-https"
license=('PerlArtistic' 'GPL')
depends=('perl' 'perl-io-socket-ssl' 'perl-mozilla-ca' 'perl-net-http' 'perl-libwww')
options=('!emptydirs')
source=("http://search.cpan.org/CPAN/authors/id/G/GA/GAAS/LWP-Protocol-https-${pkgver}.tar.gz")
md5sums=('1b422a7d3b5fed1eb4d748fdc9fd79a4')

build() {
  cd LWP-Protocol-https-${pkgver}
  perl Makefile.PL INSTALLDIRS=vendor
  make
}

check() {
  cd LWP-Protocol-https-${pkgver}
  make test
}

package() {
  cd LWP-Protocol-https-${pkgver}
  make DESTDIR="$pkgdir" install
}
