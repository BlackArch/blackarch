# This file is part of BlackArch Linux ( http://blackarch.org ).
# See COPYING for license details.

pkgname='xssscan'
pkgver=8256.1b72952
pkgrel=1
pkgdesc='Command line tool for detection of XSS attacks in URLs. Based on ModSecurity rules from OWASP CRS.'
groups=('blackarch' 'blackarch-webapp' 'blackarch-scanner' 'blackarch-fuzzer')
arch=('i686' 'x86_64' 'armv6h' 'armv7h')
url='https://github.com/gwroblew/detectXSSlib'
license=('custom')
depends=('gcc-libs')
makedepends=('git')
source=('git+https://github.com/gwroblew/detectXSSlib.git')
sha1sums=('SKIP')

pkgver() {
  cd "$srcdir"

  echo $(git rev-list --count HEAD).$(git rev-parse --short HEAD)
}

build() {
  cd "$srcdir/detectXSSlib/tools/xssscan"

  g++ -Wno-multichar -I../../detectxsslib -o xssscan xssscan.cpp \
    ../../detectxsslib/detectxsslib.c
}

package() {
  cd "$srcdir/detectXSSlib/tools/xssscan"

  install -Dm755 xssscan "$pkgdir/usr/bin/xssscan"
  install -Dm644 "$srcdir/detectXSSlib/license.txt" \
    "$pkgdir/usr/share/licenses/xssscan/license.txt"
}
