# This file is part of BlackArch Linux ( https://www.blackarch.org/ ).
# See COPYING for license details.

pkgbase=python-colored
pkgname=('python2-colored' 'python-colored')
_pkgname=colored
pkgver=1.4.3
pkgrel=3
pkgdesc='Simple library for color and formatting to terminal.'
arch=('any')
url='https://pypi.org/project/colored/#files'
makedepends=('python2-setuptools' 'python-setuptools')
license=('MIT')
options=(!emptydirs)
source=("https://files.pythonhosted.org/packages/source/${_pkgname::1}/$_pkgname/$_pkgname-$pkgver.tar.gz")
sha512sums=('a32c9c382d75ed460e32804e8b8c12d8fb01561029d0b3c207922969eb6f9221b6b2d34c72bbc1171df27f9e5033f79814f551d7e5f741eca690ef361181db5f')

prepare() {
  cp -a "$_pkgname-$pkgver"{,-2}
}

build() {
  cd "$_pkgname-$pkgver-2"

  python2 setup.py build

  cd "$srcdir/$_pkgname-$pkgver"

  python setup.py build
}

package_python2-colored() {
  depends=('python2')

  cd "$_pkgname-$pkgver-2"

  python2 setup.py install --root="$pkgdir" --prefix=/usr -O1 --skip-build
}

package_python-colored() {
  depends=('python')

  cd "$_pkgname-$pkgver"

  python setup.py install --root="$pkgdir" --prefix=/usr -O1 --skip-build
}

