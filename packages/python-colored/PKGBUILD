# This file is part of BlackArch Linux ( https://www.blackarch.org/ ).
# See COPYING for license details.

pkgbase=python-colored
pkgname=('python2-colored' 'python-colored')
_pkgname=colored
pkgver=1.4.1
pkgrel=1
pkgdesc='Simple library for color and formatting to terminal.'
arch=('any')
url='https://pypi.org/project/colored/#files'
makedepends=('python2-setuptools' 'python-setuptools')
license=('MIT')
options=(!emptydirs)
source=("https://files.pythonhosted.org/packages/49/28/757731f5a6023649064d976913d588da060678c50a20e9dc4abed5aedeb4/$_pkgname-$pkgver.tar.gz")
sha512sums=('c34eaf7f99706b062b2860cfb3ce8c9b9ea2c6a94720bc8ea48fb8b0c54c994f7fc37750cb3cf584fedf0ba5674a928890fc5b98df85980d510e6d91c1b8ea3c')

prepare() {
  cp -a "$_pkgname-$pkgver"{,-2}
}

build() {
  cd "$_pkgname-$pkgver-2"

  python2 setup.py build

  cd "$srcdir/$_pkgname-$pkgver"

  python setup.py build
}

package_python2-colored() {
  depends=('python2')

  cd "$_pkgname-$pkgver-2"

  python2 setup.py install --prefix=/usr --root="$pkgdir" --optimize=1 \
    --skip-build
}

package_python-colored() {
  depends=('python')

  cd "$_pkgname-$pkgver"

  python setup.py install --prefix=/usr --root="$pkgdir" --optimize=1 \
    --skip-build
}

