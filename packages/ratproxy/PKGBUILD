pkgname=ratproxy
pkgver=1.58
pkgrel=1
pkgdesc="A passive web application security assessment tool - patched to also work with Metasploit's WMAP"
arch=('any')
url='http://code.google.com/p/ratproxy/'
license=('Apache')
depends=('openssl' 'sqlite')
source=("http://ratproxy.googlecode.com/files/ratproxy-${pkgver}.tar.gz"
	 "ratproxy_wmap.diff")
md5sums=('e4d5e64a0bd8f8f92032cbcee77fb3cf'
	 '0f78716d2559401373e234ea26d4079f')


prepare() {
  cd "$srcdir/ratproxy"

  sed "s|'ratproxy-back.png'|'file:///usr/share/ratproxy/ratproxy-back.png'|" -i ratproxy-report.sh
  sed "s|messages.list|/usr/share/ratproxy/messages.list|" -i ratproxy-report.sh
  sed "s|\"flare\"|\"/usr/bin/ratproxy-flare\"|g" -i ratproxy.c
  sed "s|\"./flare\"|\"/usr/bin/ratproxy-flare\"|" -i ratproxy.c
  sed "s|\"keyfile.pem\"|\"/usr/share/ratproxy/keyfile.pem\"|" -i ssl.c

  # Patching Ratproxy to work with Metasploit and WMAP
  patch -Np1 -i "$srcdir/ratproxy_wmap.diff"
}

build() {
  cd "$srcdir/ratproxy"
  make 
}

package() {
  cd "$srcdir/ratproxy"

  # Base directories
  install -d -Dm755 "$pkgdir/usr/bin"
  install -d -Dm755 "$pkgdir/usr/share/ratproxy"
  install -d -Dm755 "$pkgdir/usr/share/doc/ratproxy"

  # Bin.
  install -m755 ratproxy "$pkgdir/usr/bin"
  install -m755 ratproxy-report.sh "$pkgdir/usr/bin/ratproxy-report"
  install -m755 flare-dist/flare "$pkgdir/usr/bin/ratproxy-flare"

  # Share.
  install -m644 messages.list keyfile.pem ratproxy-back.png "$pkgdir/usr/share/ratproxy"

  # Doc.
  install -m644 doc/README "$pkgdir/usr/share/doc/ratproxy/README"
}
