# This file is part of BlackArch Linux ( https://www.blackarch.org/ ).
# See COPYING for license details.

pkgbase=python-config
pkgname=('python2-config' 'python-config')
_pkgname=config
pkgver=0.4.2
pkgrel=2
pkgdesc='A hierarchical, easy-to-use, powerful configuration module for Python.'
arch=('any')
license=('MIT')
url='https://pypi.python.org/pypi/config/'
makedepends=('python2-setuptools' 'python-setuptools')
source=("https://files.pythonhosted.org/packages/59/6c/4ab0d80b22dca3baab49670b75ae2183b59649e9f27c11018075e509048e/$_pkgname-$pkgver.tar.gz")
sha512sums=('a113268dcc759fa3bc569e40fdf3ad955fe8befe9343bc5dc1f3ba32d14ec3cfdbb3fc66aeb7853736e1683298bad8427c52611e1fa5ccb36865ec4a543d3c3f')

prepare() {
  cp -a "$_pkgname-$pkgver"{,-2}
}

build() {
  cd "$_pkgname-$pkgver-2"

  python2 setup.py build

  cd "$srcdir/$_pkgname-$pkgver"

  python setup.py build
}

package_python2-config() {
  depends=('python2')

  cd "$_pkgname-$pkgver-2"

  python2 setup.py install --prefix=/usr --root="$pkgdir" --optimize=1 \
    --skip-build
}

package_python-config() {
  depends=('python')

  cd "$_pkgname-$pkgver"

  python setup.py install --prefix=/usr --root="$pkgdir" --optimize=1 \
    --skip-build
}

