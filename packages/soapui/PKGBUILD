# This file is part of BlackArch Linux ( https://www.blackarch.org/ ).
# See COPYING for license details.

pkgname=soapui
_pkgname=SoapUI
pkgver=5.7.0
pkgrel=1
pkgdesc='The Swiss-Army Knife for SOAP Testing.'
groups=('blackarch' 'blackarch-proxy' 'blackarch-fuzzer')
arch=('x86_64')
url='https://www.soapui.org/downloads/soapui/source-forge.html'
license=('custom:unknown')
depends=('java-environment')
makedepends=('git')
source=("https://s3.amazonaws.com/downloads.eviware/soapuios/$pkgver/$_pkgname-$pkgver-linux-bin.tar.gz")
sha512sums=('19438f440023e16b047177350a10427d6ad9bd7b0cbba850355ad56d3e5ee7855dbebc06bd0a7705076db688000ce6e1845c9b27f374fccc11c6c9f9fea5424b')

package() {
  cd "$_pkgname-$pkgver"

  install -dm 755 "$pkgdir/usr/bin"
  install -dm 755 "$pkgdir/usr/share/$pkgname"

  install -Dm 644 -t "$pkgdir/usr/share/doc/$pkgname/" README.md RELEASENOTES.txt
  install -Dm 644 LICENSE.txt "$pkgdir/usr/share/licenses/$pkgname/LICENSE"

  rm README.md RELEASENOTES.txt LICENSE.txt

  cp -a * "$pkgdir/usr/share/$pkgname"

  cat > "$pkgdir/usr/bin/$pkgname" << EOF
#!/bin/sh
exec sh /usr/share/$pkgname/bin/$pkgname.sh "\$@"
EOF

  chmod +x "$pkgdir/usr/bin/$pkgname"
}

