# This file is part of BlackArch Linux ( https://www.blackarch.org/ ).
# See COPYING for license details.

pkgname=soapui
_pkgname=SoapUI
pkgver=5.7.2
pkgrel=1
pkgdesc='The Swiss-Army Knife for SOAP Testing.'
groups=('blackarch' 'blackarch-proxy' 'blackarch-fuzzer')
arch=('x86_64')
url='https://www.soapui.org/downloads/soapui/source-forge.html'
license=('custom:unknown')
depends=('java-environment')
makedepends=('git')
source=("https://dl.eviware.com/soapuios/5.7.2/$_pkgname-$pkgver-linux-bin.tar.gz")
sha512sums=('0e8e05c66777bd03d9b49d5ff3725aa0418d3eb004f70d109aea08dafe759e39094dbcfffbc838b2e26aa9ed59672d5edb71a95505862890dc81054aa7ae4353')

package() {
  cd "$_pkgname-$pkgver"

  install -dm 755 "$pkgdir/usr/bin"
  install -dm 755 "$pkgdir/usr/share/$pkgname"

  install -Dm 644 -t "$pkgdir/usr/share/doc/$pkgname/" README.md RELEASENOTES.txt
  install -Dm 644 LICENSE.txt "$pkgdir/usr/share/licenses/$pkgname/LICENSE"

  rm README.md RELEASENOTES.txt LICENSE.txt

  cp -a * "$pkgdir/usr/share/$pkgname"

  cat > "$pkgdir/usr/bin/$pkgname" << EOF
#!/bin/sh
exec sh /usr/share/$pkgname/bin/$pkgname.sh "\$@"
EOF

  chmod +x "$pkgdir/usr/bin/$pkgname"
}

