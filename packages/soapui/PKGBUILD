# This file is part of BlackArch Linux ( https://www.blackarch.org/ ).
# See COPYING for license details.

pkgname=soapui
pkgver=5.4.0
pkgrel=1
pkgdesc='The Swiss-Army Knife for SOAP Testing.'
groups=('blackarch' 'blackarch-proxy' 'blackarch-fuzzer')
arch=('x86_64')
url='https://www.soapui.org/downloads/soapui/source-forge.html'
license=('EUPL')
depends=('java-environment')
makedepends=('git')
source=("https://s3.amazonaws.com/downloads.eviware/soapuios/$pkgver/SoapUI-$pkgver-linux-bin.tar.gz")
#source=("https://s3.amazonaws.com/downloads.eviware/soapuios/$pkgver/SoapUI-x64-$pkgver.sh")
#source=("http://cdn01.downloads.smartbear.com/soapui/$pkgver/SoapUI-$pkgver-linux-bin.tar.gz")
sha512sums=('7b1fa050e5686d7b787e286f6fe29a54526244948cfac5ca8d547b1918b471c4e8eb124f86f1742c775928f4456d0e07bee4a0d95668c74e82a79dbddc3fa934')

package() {
  cd "SoapUI-$pkgver"

  install -dm 755 "$pkgdir/usr/bin"
  install -dm 755 "$pkgdir/usr/share/soapui"

  install -Dm 644 -t "$pkgdir/usr/share/doc/$pkgname/" README.md RELEASENOTES.txt
  install -Dm 644 -t "$pkgdir/usr/share/licenses/$pkgname/" *License*.txt

  rm README.md RELEASENOTES.txt *License*.txt

  cp -a * "$pkgdir/usr/share/$pkgname"

  cat > "$pkgdir/usr/bin/$pkgname" << EOF
#!/bin/sh
exec sh /usr/share/$pkgname/bin/soapui.sh "\$@"
EOF

  chmod +x "$pkgdir/usr/bin/$pkgname"
}

