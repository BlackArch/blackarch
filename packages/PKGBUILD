# This file is part of BlackArch Linux ( https://www.blackarch.org/ ).
# See COPYING for license details.

pkgbase=python-puremagic
pkgname=('python-puremagic' 'python2-puremagic')
_pkgname=puremagic
pkgver=1.10
pkgrel=1
pkgdesc='pure python module that will identify a file based off itâ€™s magic numbers.'
arch=('any')
url='https://pypi.org/project/puremagic/'
license=('MIT')
makedepends=('python-setuptools' 'python2-setuptools')
source=("https://files.pythonhosted.org/packages/source/p/$_pkgname/$_pkgname-$pkgver.tar.gz")
sha512sums=('fcbd9513c290e7a4ae65795fbd9b787f54ad6b1a1e27d16dec999c90614b534c46d121216869490838a91abd3fe4d758561474000ad37f379305ab19bf75d58b')

prepare() {
  cp -a "$_pkgname-$pkgver"{,-2}
}

build() {
  cd "$_pkgname-$pkgver-2"

  python2 setup.py build

  cd "$srcdir/$_pkgname-$pkgver"

  python setup.py build
}

package_python2-puremagic() {
  depends=('python2' 'python2-argparse')

  cd "$_pkgname-$pkgver-2"

  python2 setup.py install --root="$pkgdir" --prefix=/usr -O1 --skip-build

  install -Dm 644 LICENSE "$pkgdir/usr/share/licenses/$pkgname/LICENSE"
}

package_python-puremagic() {
  depends=('python' 'python-argparse')

  cd "$_pkgname-$pkgver"

  python setup.py install --root="$pkgdir" --prefix=/usr -O1 --skip-build

  install -Dm 644 LICENSE "$pkgdir/usr/share/licenses/$pkgname/LICENSE"
}

