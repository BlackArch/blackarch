# This file is part of BlackArch Linux ( https://www.blackarch.org/ ).
# See COPYING for license details.

pkgname=blackarch-installer
pkgver=1.2.1
pkgrel=1
pkgdesc='The official BlackArch Linux installer.'
arch=('any')
url='https://github.com/BlackArch/blackarch-installer'
license=('GPL3')
depends=('bash')
source=("https://github.com/BlackArch/$pkgname/archive/v$pkgver.tar.gz")
sha512sums=('9b0e4cc97f09abc364505f3a88fb8a642e5e38529714c3f9c7ace5117c6c0be3853eff5195759b03febe317e5e6db7268d6209542ec22850a324b708d8c307fa')

package() {
  cd "$pkgname-$pkgver"

  install -dm 755 "$pkgdir/usr/bin/"
  install -dm 755 "$pkgdir/usr/share/$pkgname"

  install -Dm 644 -t "$pkgdir/usr/share/doc/$pkgname/" docs/* \
    README.md

  rm -rf docs README.md

  cp -a * "$pkgdir/usr/share/$pkgname/"

  chmod +x "$pkgdir/usr/share/$pkgname/blackarch-install"

  ln -s "/usr/share/$pkgname/blackarch-install" \
    "$pkgdir/usr/bin/blackarch-install"
}

