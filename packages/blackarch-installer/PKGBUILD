# This file is part of BlackArch Linux ( https://www.blackarch.org/ ).
# See COPYING for license details.

pkgname=blackarch-installer
pkgver=1.2.12
pkgrel=1
pkgdesc='The official BlackArch Linux installer.'
arch=('any')
url='https://github.com/BlackArch/blackarch-installer'
license=('GPL3')
depends=('bash')
source=("https://github.com/BlackArch/$pkgname/archive/v$pkgver.tar.gz")
sha512sums=('fe0117c30def78bad3e8708d07e2f99fccc27baa811705b170c6afb79f4404dcbf2d47864dc4772e05937c9606deaac56db5112c9d22f22d345460efcdb34a87')

package() {
  cd "$pkgname-$pkgver"

  install -dm 755 "$pkgdir/usr/bin/"
  install -dm 755 "$pkgdir/usr/share/$pkgname"

  install -Dm 644 -t "$pkgdir/usr/share/doc/$pkgname/" docs/* \
    README.md

  rm -rf docs README.md

  cp -a * "$pkgdir/usr/share/$pkgname/"

  chmod +x "$pkgdir/usr/share/$pkgname/blackarch-install"

  ln -s "/usr/share/$pkgname/blackarch-install" \
    "$pkgdir/usr/bin/blackarch-install"
}

