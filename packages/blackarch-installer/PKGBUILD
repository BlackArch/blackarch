# This file is part of BlackArch Linux ( http://blackarch.org ).
# See COPYING for license details.

pkgname=blackarch-installer
pkgver=0.9.2
pkgrel=1
pkgdesc='The official BlackArch Linux installer.'
arch=('any')
url='https://github.com/BlackArch/blackarch-installer'
license=('GPL3')
depends=('bash')
source=("https://github.com/BlackArch/blackarch-installer/archive/v${pkgver}.tar.gz")
sha512sums=('008c9fc45280f9267a503468e9e6941ac9d94be78ec5603d17b27b73bfab6578718441f5e2cb289611166d5ce633154d496a581fe3ab23bb734e6be3519704b9')

package() {
  cd "$pkgname-$pkgver"

  install -dm 755 "$pkgdir/usr/bin/"
  install -dm 755 "$pkgdir/usr/share/$pkgname"

  install -Dm 644 -t "$pkgdir/usr/share/doc/$pkgname/" docs/* \
    README.md

  rm -rf docs README.md

  cp -a * "$pkgdir/usr/share/$pkgname"

  chmod a+x "$pkgdir/usr/share/$pkgname/blackarch-install"

  ln -s "/usr/share/$pkgname/blackarch-install" \
    "$pkgdir/usr/bin/blackarch-install"
}

