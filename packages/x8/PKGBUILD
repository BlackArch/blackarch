# This file is part of BlackArch Linux ( https://www.blackarch.org/ ).
# See COPYING for license details.

pkgname=x8
pkgver=v4.1.0.r0.gf72ff05
pkgrel=1
epoch=1
pkgdesc='Hidden parameters discovery suite.'
arch=('x86_64' 'aarch64')
groups=('blackarch' 'blackarch-webapp' 'blackarch-scanner')
url='https://github.com/Sh1Yo/x8'
license=('custom:unknown')
depends=('openssl')
makedepends=('git' 'cargo')
source=("git+https://github.com/Sh1Yo/$pkgname.git")
sha512sums=('SKIP')

pkgver() {
  cd $pkgname

  git describe --long --tags | sed 's/\([^-]*-g\)/r\1/;s/-/./g'
}

build() {
  cd $pkgname

  cargo build --release
}

package() {
  cd $pkgname

  install -Dm 755 "target/release/$pkgname" "$pkgdir/usr/bin/$pkgname"
  install -Dm 644 README.md -t "$pkgdir/usr/share/doc/$pkgname"
}

