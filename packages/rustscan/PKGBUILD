# This file is part of BlackArch Linux ( https://www.blackarch.org/ ).
# See COPYING for license details.

pkgname=rustscan
pkgver=640.90bbed2
pkgrel=1
pkgdesc='Faster Nmap Scanning with Rust.'
arch=('x86_64' 'aarch64')
groups=('blackarch' 'blackarch-scanner')
url='https://github.com/brandonskerritt/RustScan'
license=('GPL3')
makedepends=('git' 'cargo')
source=("$pkgname::git+https://github.com/brandonskerritt/RustScan.git")
options=(!strip)
sha512sums=('SKIP')

pkgver() {
  cd $pkgname

  echo $(git rev-list --count HEAD).$(git rev-parse --short HEAD)
}

build() {
  cd $pkgname

  cargo build --release
}

package() {
  cd $pkgname

  install -dm 755 "$pkgdir/usr/share/$pkgname"

  install -Dm 755 "target/release/$pkgname" "$pkgdir/usr/bin/$pkgname"
  install -Dm 644 -t "$pkgdir/usr/share/doc/$pkgname/" *.md
  install -Dm 644 LICENSE "$pkgdir/usr/share/licenses/$pkgname/LICENSE"
}

