# This file is part of BlackArch Linux ( http://blackarch.org ).
# See COPYING for license details.

pkgname='joomscan'
pkgver='2012.03.10'
pkgrel=10
groups=('blackarch' 'blackarch-webapp')
pkgdesc="Detects file inclusion, sql injection, command execution vulnerabilities of a target Joomla! web site."
arch=('any')
depends=('perl-switch' 'perl-www-mechanize')
license=('LGPL3')
url='http://joomscan.sourceforge.net/'
source=("http://downloads.sourceforge.net/project/joomscan/joomscan/${pkgver//./-}/joomscan-latest.zip")
sha1sums=('ae85cbdfd4b12c42aa2030460eb3ab582c8d3bcd')

package() {
  cd "$srcdir"

  # Base directories.
  mkdir -p "$pkgdir/usr/bin"
  mkdir -p "$pkgdir/usr/share/joomscan"
  mkdir -p "$pkgdir/usr/share/doc/joomscan"

  install -m755 joomscan.pl "$pkgdir/usr/share/joomscan"
  install -m755 joomscandb.txt "$pkgdir/usr/share/joomscan"
  install -m755 joomscandb-info.txt "$pkgdir/usr/share/joomscan"

  cp --no-preserve=ownership -R doc/* "$pkgdir/usr/share/doc/joomscan"
  cp --no-preserve=ownership -R report "$pkgdir/usr/share/joomscan"

  # This is necessary to stop a warning from appearing.
  ln -s /usr/share/doc/joomscan "$pkgdir/usr/share/joomscan/doc"

  cat > "$pkgdir/usr/bin/joomscan" <<EOF
#!/bin/sh
cd /usr/share/joomscan
exec ./joomscan.pl "\$@"
EOF
  chmod +x "$pkgdir/usr/bin/joomscan"
}
