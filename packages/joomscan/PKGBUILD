# This file is part of BlackArch Linux ( https://www.blackarch.org/ ).
# See COPYING for license details.

pkgname=joomscan
pkgver=63.b2dad6a
pkgrel=1
epoch=1
groups=('blackarch' 'blackarch-webapp')
pkgdesc='Detects file inclusion, sql injection, command execution vulnerabilities of a target Joomla! web site.'
arch=('any')
depends=('perl-switch' 'perl-www-mechanize' 'perl-term-ansicolor')
makedepends=('git')
license=('GPL3')
url='http://joomscan.sourceforge.net/'
source=('git+https://github.com/rezasp/joomscan.git')
sha512sums=('SKIP')

pkgver() {
  cd $pkgname

  echo $(git rev-list --count HEAD).$(git rev-parse --short HEAD)
}

package() {
  cd $pkgname

  install -dm 755 "$pkgdir/usr/bin"

  install -Dm 755 joomscan.pl "$pkgdir/usr/share/$pkgname/joomscan.pl"
  install -Dm 644 -t "$pkgdir/usr/share/doc/$pkgname" README.md CHANGELOG.md
  install -Dm 644 LICENSE.md "$pkgdir/usr/share/licenses/$pkgname/LICENSE.md"

  rm *.md

  cp -a * "$pkgdir/usr/share/$pkgname/"

  # This is necessary to stop a warning from appearing.
  ln -s /usr/share/doc/joomscan "$pkgdir/usr/share/$pkgname/doc"

  cat > "$pkgdir/usr/bin/$pkgname" << EOF
#!/bin/sh
cd /usr/share/$pkgname
exec ./joomscan.pl "\$@"
EOF

  chmod 755 "$pkgdir/usr/bin/$pkgname"
}

