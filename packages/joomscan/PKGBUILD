# This file is part of BlackArch Linux ( http://blackarch.org ).
# See COPYING for license details.

pkgname=joomscan
pkgver=44.e3ccf9f
pkgrel=1
epoch=1
groups=('blackarch' 'blackarch-webapp')
pkgdesc='Detects file inclusion, sql injection, command execution vulnerabilities of a target Joomla! web site.'
arch=('any')
depends=('perl-switch' 'perl-www-mechanize' 'perl-term-ansicolor')
makedepends=('git')
license=('GPL3')
url='http://joomscan.sourceforge.net/'
source=('git+https://github.com/rezasp/joomscan')
sha512sums=('SKIP')

pkgver() {
  cd joomscan

  echo $(git rev-list --count HEAD).$(git rev-parse --short HEAD)
}

package() {
  cd joomscan

  install -dm 755 "$pkgdir/usr/bin"

  install -Dm755 joomscan.pl "$pkgdir/usr/share/$pkgname/joomscan.pl"
  install -Dm644 -t "$pkgdir/usr/share/doc/$pkgname" README.md CHANGELOG.md
  install -Dm644 LICENSE.md "$pkgdir/usr/share/licenses/$pkgname/LICENSE.md"

  rm *.md

  cp -a * "$pkgdir/usr/share/$pkgname"

  # This is necessary to stop a warning from appearing.
  ln -s /usr/share/doc/joomscan "$pkgdir/usr/share/$pkgname/doc"

  cat > "$pkgdir/usr/bin/joomscan" <<EOF
#!/bin/sh
cd /usr/share/joomscan
exec ./joomscan.pl "\${@}"
EOF

  chmod 755 "$pkgdir/usr/bin/joomscan"
}

