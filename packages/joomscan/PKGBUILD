pkgname=joomscan
pkgver=2012.03.10
pkgrel=6
groups=('blackarchlinux' 'blackarchlinux-webapp')
pkgdesc="Detects file inclusion, sql injection, command execution vulnerabilities of a target Joomla! web site."
arch=('i686' 'x86_64' 'armv6h' 'armv7h')
depends=('perl-switch' 'perl-www-mechanize')
license=('LGPL3')
url='http://joomscan.sourceforge.net/'
source=("http://downloads.sourceforge.net/project/joomscan/joomscan/${pkgver//./-}/joomscan-latest.zip")
md5sums=('82a8610dbee65bc587c05870910b545d')

package() {
  cd "$srcdir"

  # Base directories.
  install -dm755 "$pkgdir/usr/bin"
  install -dm755 "$pkgdir/usr/share/joomscan"
  install -dm755 "$pkgdir/usr/share/doc/joomscan"

  install -m755 joomscan.pl "$pkgdir/usr/share/joomscan"
  install -m755 joomscandb.txt "$pkgdir/usr/share/joomscan"
  install -m755 joomscandb-info.txt "$pkgdir/usr/share/joomscan"

  cp --no-preserve=ownership -R doc/* "$pkgdir/usr/share/doc/joomscan"
  cp --no-preserve=ownership -R report "$pkgdir/usr/share/joomscan"

  # This is necessary to stop a warning from appearing.
  ln -s /usr/share/doc/joomscan "$pkgdir/usr/share/joomscan/doc"

  cat > "$pkgdir/usr/bin/joomscan" <<EOF
#!/bin/sh
cd /usr/share/joomscan && ./joomscan.pl "\$@"
EOF
  chmod +x "$pkgdir/usr/bin/joomscan"
}
