# This file is part of BlackArch Linux ( https://www.blackarch.org/ ).
# See COPYING for license details.

pkgname=osrframework
pkgver=789.83437f4
pkgrel=2
pkgdesc='A project focused on providing API and tools to perform more accurate online researches.'
groups=('blackarch' 'blackarch-recon' 'blackarch-social')
arch=('any')
url='https://github.com/i3visio/osrframework'
license=('AGPL3')
depends=('python2' 'python2-requests' 'python2-tweepy' 'python2-networkx'
         'python2-python-emailahoy' 'python2-beautifulsoup3' 'python2-pyexcel'
         'python2-decorator' 'python2-validate-email' 'python2-pydns'
         'python2-tabulate' 'python2-oauthlib' 'python2-flask' 'python2-yaml'
         'python2-mechanize' 'python2-python-whois' 'python2-colorama'
         'python2-configparser' 'python2-cfscrape' 'python2-pyexcel-ods'
         'python2-pyexcel-xls' 'python2-pyexcel-xlsx' 'python2-pyexcel-io'
         'python2-pyexcel-text' 'python2-future')
makedepends=('git' 'python2-setuptools' 'python2-pip')
source=("git+https://github.com/i3visio/$pkgname.git"
        'setup.py.patch')
sha512sums=('SKIP'
            '39c28411da297e5d977b617684eddc8b3e2d7ede83521b4fe470574d2597f578c0c166fe85b158e0e41fafcb5a3f4f2c1f201e28533e0c99b882cf6d7ac4b623')

pkgver() {
  cd $pkgname

  echo $(git rev-list --count HEAD).$(git rev-parse --short HEAD)
}

prepare() {
  cd $pkgname

  patch -i "$srcdir/setup.py.patch" < setup.py
}

package() {
  cd $pkgname

  #PIP_CONFIG_FILE=/dev/null pip2 install --isolated --root="$pkgdir" \
  #  --ignore-installed --no-deps ./

  python2 setup.py install --root="$pkgdir" --prefix=/usr --optimize=1

  install -Dm 644 -t "$pkgdir/usr/share/doc/$pkgname/" *.md doc/*
  install -Dm 644 COPYING "$pkgdir/usr/share/licenses/$pkgname/COPYING"

  rm -rf *.md doc COPYING

  cd "$pkgdir/usr/bin"

  for i in * ; do
    mv "$i" `echo "$pkgdir/usr/bin/osr-$i" | sed 's/.py//'`
  done

  find "$pkgdir" -type f \( -iname '*.pyc' -or -iname '*.pyo' \) -exec rm {} \;
}

