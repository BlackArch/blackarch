# This file is part of BlackArch Linux ( http://blackarch.org ).
# See COPYING for license details.

pkgname='osrframework'
pkgver=752.427d0d4
pkgrel=2
pkgdesc='A project focused on providing API and tools to perform more accurate online researches.'
groups=('blackarch' 'blackarch-recon' 'blackarch-social')
arch=('any')
url='https://github.com/i3visio/osrframework'
license=('GPL3')
depends=('python' 'python-requests' 'python-tweepy' 'python-networkx'
         'python-python-emailahoy' 'python-beautifulsoup4' 'python-pyexcel'
         'python-decorator' 'python-validate_email' 'python-pydns'
         'python-tabulate' 'python-oauthlib' 'python-flask' 'python-pyyaml'
         'python-python-whois' 'python-colorama')
makedepends=('git' 'python-setuptools')
source=('git+https://github.com/i3visio/osrframework.git')
sha1sums=('SKIP')

pkgver() {
  cd "$srcdir/osrframework"

  echo $(git rev-list --count HEAD).$(git rev-parse --short HEAD)
}

build() {
  cd "$srcdir/osrframework"

  python setup.py build
}

package() {
  cd "$srcdir/osrframework"

  python setup.py install --root="$pkgdir" --prefix=/usr --optimize=1

  install -Dm644 -t "$pkgdir/usr/share/doc/osrframework/" README.md doc/*
  install -Dm644 COPYING "$pkgdir/usr/share/licenses/osrframework/COPYING"

  cd $pkgdir/usr/bin

  for i in *
  do
    mv $i `echo $pkgdir/usr/bin/osr-$i | sed 's/.py//'`
  done
}
