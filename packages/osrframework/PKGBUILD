# This file is part of BlackArch Linux ( http://blackarch.org ).
# See COPYING for license details.

pkgname='osrframework'
pkgver=774.44e0495
pkgrel=1
pkgdesc='A project focused on providing API and tools to perform more accurate online researches.'
groups=('blackarch' 'blackarch-recon' 'blackarch-social')
arch=('any')
url='https://github.com/i3visio/osrframework'
license=('GPL3')
depends=('python2' 'python2-requests' 'python2-tweepy' 'python2-networkx'
         'python2-python-emailahoy' 'python2-beautifulsoup4' 'python2-pyexcel'
         'python2-decorator' 'python2-validate_email' 'python2-pydns'
         'python2-tabulate' 'python2-oauthlib' 'python2-flask'
         'python2-python-whois' 'python2-colorama' 'python2-configparser')
makedepends=('git' 'python2-setuptools' 'python2-pip')
source=('git+https://github.com/i3visio/osrframework.git')
sha1sums=('SKIP')

pkgver() {
  cd "$srcdir/osrframework"

  echo $(git rev-list --count HEAD).$(git rev-parse --short HEAD)
}

package() {
  cd "$srcdir/osrframework"

  #PIP_CONFIG_FILE=/dev/null pip2 install --isolated --root="$pkgdir" \
  #  --ignore-installed --no-deps ./

  python2 setup.py install --root="$pkgdir" --prefix=/usr --optimize=1

  install -Dm644 -t "$pkgdir/usr/share/doc/osrframework/" README.md doc/*
  install -Dm644 COPYING "$pkgdir/usr/share/licenses/osrframework/COPYING"

  rm -rf README.md doc COPYING

  cd "$pkgdir/usr/bin"

  for i in *
  do
    mv $i `echo $pkgdir/usr/bin/osr-$i | sed 's/.py//'`
  done
}
