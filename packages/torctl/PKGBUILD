# This file is part of BlackArch Linux ( https://www.blackarch.org/ ).
# See COPYING for license details.

pkgname=torctl
pkgver=13.dd9587e
pkgrel=1
epoch=1
groups=('blackarch' 'blackarch-automation')
pkgdesc='Script to redirect all traffic through tor network including dns queries for anonymizing entire system.'
arch=('any')
url='https://github.com/BlackArch/torctl'
license=('GPL3')
depends=('tor' 'systemd' 'wget' 'iptables' 'secure-delete')
source=('git+https://github.com/BlackArch/torctl.git')
sha512sums=('SKIP')

pkgver() {
  cd $pkgname

  echo $(git rev-list --count HEAD).$(git rev-parse --short HEAD)
}

package() {
  cd "$pkgname"

  install -Dm 755 "$pkgname" "$pkgdir/usr/bin/$pkgname"
  install -Dm 644 -t "$pkgdir/usr/share/doc/$pkgname/" README.md
  install -Dm 644 LICENSE "$pkgdir/usr/share/licenses/$pkgname/LICENSE"
  install -Dm 644 service/$pkgname-autostart.service \
    "$pkgdir/etc/systemd/system/$pkgname-autostart.service"
  install -Dm 644 service/$pkgname-memwipe.service \
    "$pkgdir/etc/systemd/system/$pkgname-memwipe.service"
  install -Dm 644 bash-completion/$pkgname \
    "$pkgdir/usr/share/bash-completion/completions/$pkgname"
}

