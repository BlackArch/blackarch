# This file is part of BlackArch Linux ( https://www.blackarch.org/ ).
# See COPYING for license details.

pkgname=torctl
pkgver=0.5.1
pkgrel=1
epoch=1
groups=('blackarch' 'blackarch-automation')
pkgdesc='Script to redirect all traffic through tor network including dns queries for anonymizing entire system.'
arch=('any')
url='https://github.com/BlackArch/torctl'
license=('GPL3')
depends=('tor' 'systemd' 'wget' 'iptables' 'secure-delete')
source=("https://github.com/BlackArch/torctl/archive/v$pkgver.tar.gz")
sha512sums=('25f2abfdb8c3ab4fbb3528d7e2a7fc4a4d812ab7795b1c5452eabb1fda1020fc7625faff624a83af678b3760b6215a60c7850805d5adcd59de1e0c4cafe2de2c')

package() {
  cd "$pkgname-$pkgver"

  install -Dm 755 "$pkgname" "$pkgdir/usr/bin/$pkgname"
  install -Dm 644 -t "$pkgdir/usr/share/doc/$pkgname/" README.md
  install -Dm 644 LICENSE "$pkgdir/usr/share/licenses/$pkgname/LICENSE"
  install -Dm 644 service/$pkgname-autostart.service \
    "$pkgdir/etc/systemd/system/$pkgname-autostart.service"
  install -Dm 644 service/$pkgname-memwipe.service \
    "$pkgdir/etc/systemd/system/$pkgname-memwipe.service"
  install -Dm 644 bash-completion/$pkgname \
    "$pkgdir/usr/share/bash-completion/completions/$pkgname"
}

