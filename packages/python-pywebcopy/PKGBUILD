# This file is part of BlackArch Linux ( https://www.blackarch.org/ ).
# See COPYING for license details.

pkgname=python-pywebcopy
_pkgname=pywebcopy
pkgver=7.0.2
_pyver=3.10
pkgrel=2
pkgdesc='Python package for cloning complete webpages and websites to local storage.'
arch=('any')
url='https://pypi.org/project/pywebcopy/#files'
license=('Apache')
depends=('python' 'python-requests' 'python-lxml' 'python-pyquery'
         'python-parse' 'python-w3lib' 'python-beautifulsoup4' 'python-fire')
makedepends=('python-setuptools' 'python-pip' 'python-wheel')
source=("https://files.pythonhosted.org/packages/source/${_pkgname::1}/$_pkgname/$_pkgname-$pkgver.tar.gz")
sha512sums=('97d63cee35ed77ca91d9c8f19d098125971eb2dd8bee448decc579f6bb4700929bc18e1681881b9feee0fde31a75ee13a8902c14644ed76ab3423e9d7dc984e8')

prepare() {
  cd "$_pkgname-$pkgver"

  touch requirements.txt
}

build() {
  cd "$_pkgname-$pkgver"

  python setup.py build
}

package() {
  cd "$_pkgname-$pkgver"

  python setup.py install --root="$pkgdir" --prefix=/usr -O1 --skip-build

  rm -rf "$pkgdir/usr/lib/python$_pyver/site-packages/tests/"
}

