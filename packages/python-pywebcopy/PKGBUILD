# This file is part of BlackArch Linux ( https://www.blackarch.org/ ).
# See COPYING for license details.

pkgname=python-pywebcopy
_pkgname=pywebcopy
pkgver=7.1
_pyver=3.14
pkgrel=2
pkgdesc='Python package for cloning complete webpages and websites to local storage.'
arch=('any')
url='https://pypi.org/project/pywebcopy/#files'
license=('Apache')
depends=('python' 'python-cachecontrol' 'python-lxml-html-clean'
         'python-requests' 'python-six')
makedepends=('python-setuptools')
source=("https://files.pythonhosted.org/packages/source/${_pkgname::1}/$_pkgname/$_pkgname-$pkgver.tar.gz")
sha512sums=('c65c9d725f640e7f0586604149d4b7ae2fd7e359442e2ace448b0bf03daba3cecf089f9e802ba13b60062389415930cc7f831b9885fe9ee9352209a2b06fa6d4')

prepare() {
  cd "$_pkgname-$pkgver"

  touch requirements.txt
}

build() {
  cd "$_pkgname-$pkgver"

  python setup.py build
}

package() {
  cd "$_pkgname-$pkgver"

  python setup.py install --root="$pkgdir" --prefix=/usr -O1 --skip-build

  rm -rf "$pkgdir/usr/lib/python$_pyver/site-packages/tests/"
}

