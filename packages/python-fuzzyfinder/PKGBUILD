# This file is part of BlackArch Linux ( https://www.blackarch.org/ ).
# See COPYING for license details.

pkgbase=python-fuzzyfinder
pkgname=('python2-fuzzyfinder' 'python-fuzzyfinder')
_pkgname=fuzzyfinder
pkgver=2.2.0
pkgrel=1
pkgdesc='Fuzzy Finder implemented in Python.'
arch=('any')
url='https://pypi.org/project/fuzzyfinder/#files'
license=('BSD')
makedepends=('python2-setuptools' 'python-setuptools')
source=("https://files.pythonhosted.org/packages/source/${_pkgname::1}/$_pkgname/$_pkgname-$pkgver.tar.gz")
sha512sums=('484cdac19d98d79c56f52405b9cf3ece74f3c608a46dd526ed44813167c44023e60bbe7de46bee9bba2e0c5ff7653457cf23a838c34993c8d6958dbcb4238174')

prepare() {
  cp -a "$_pkgname-$pkgver"{,-2}
}

build() {
  cd "$_pkgname-$pkgver-2"

  python2 setup.py build

  cd "$srcdir/$_pkgname-$pkgver"

  python setup.py build
}

package_python2-fuzzyfinder() {
  depends=('python2')

  cd "$_pkgname-$pkgver-2"

  python2 setup.py install --prefix=/usr --root="$pkgdir" --optimize=1 \
    --skip-build
}

package_python-fuzzyfinder() {
  depends=('python')

  cd "$_pkgname-$pkgver"

  python setup.py install --prefix=/usr --root="$pkgdir" --optimize=1 \
    --skip-build
}

