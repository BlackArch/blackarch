# This file is part of BlackArch Linux ( https://www.blackarch.org/ ).
# See COPYING for license details.

pkgbase=python-fuzzyfinder
pkgname=('python2-fuzzyfinder' 'python-fuzzyfinder')
_pkgname=fuzzyfinder
pkgver=2.1.0
pkgrel=4
pkgdesc='Fuzzy Finder implemented in Python.'
arch=('any')
url='https://pypi.org/project/fuzzyfinder/#files'
license=('BSD')
makedepends=('python2-setuptools' 'python-setuptools')
source=("https://files.pythonhosted.org/packages/44/ef/b526179eef7d8567b75e1df52c5163b4d1259de51478516ea139584695d2/$_pkgname-$pkgver.tar.gz")
sha512sums=('9bade55a060613b0c9620376c74ebdcecb8331bdbbe45df2f4b566fc351bcf1f73b71f2d9282065c87eaca4d896382f80622f668a29c10a0c9b2c4ccdc2be97f')

prepare() {
  cp -a "$_pkgname-$pkgver"{,-2}
}

build() {
  cd "$_pkgname-$pkgver-2"

  python2 setup.py build

  cd "$srcdir/$_pkgname-$pkgver"

  python setup.py build
}

package_python2-fuzzyfinder() {
  depends=('python2')

  cd "$_pkgname-$pkgver-2"

  python2 setup.py install --prefix=/usr --root="$pkgdir" --optimize=1 \
    --skip-build
}

package_python-fuzzyfinder() {
  depends=('python')

  cd "$_pkgname-$pkgver"

  python setup.py install --prefix=/usr --root="$pkgdir" --optimize=1 \
    --skip-build
}

