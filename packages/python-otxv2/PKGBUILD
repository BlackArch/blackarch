# This file is part of BlackArch Linux ( https://www.blackarch.org/ ).
# See COPYING for license details.

pkgbase=python-otxv2
pkgname=('python2-otxv2' 'python-otxv2')
_pkgname=OTXv2
pkgver=1.5.6
pkgrel=1
epoch=1
pkgdesc='AlienVault OTX API.'
arch=('any')
url='https://pypi.python.org/pypi/OTXv2/'
license=('APACHE')
makedepends=('python2-setuptools' 'python-setuptools')
source=("https://files.pythonhosted.org/packages/44/c1/40db50a6f4d8b1a2994271782b53734090625dec32d1ad7d4c360962d26a/OTXv2-$pkgver.tar.gz")
sha512sums=('ecf746e0f1bbe8ec734baae72aebfbff14a3ec2c6aa94581e2dffed73526c455efdc87df3fe111a9e4b1d0b70ba572f2f596e00450fb9d7eeb53e8879f85f870')

prepare() {
  cp -a "$_pkgname-$pkgver"{,-2}
}

build() {
  cd "$_pkgname-$pkgver-2"

  python2 setup.py build

  cd "$srcdir/$_pkgname-$pkgver"

  python setup.py build
}

package_python2-otxv2() {
  depends=('python2' 'python2-requests' 'python2-dateutil' 'python2-pytz')

  install -dm 755 "$pkgdir/usr/share/${pkgname[0]}"

  cd "$_pkgname-$pkgver-2"

  python2 setup.py install --prefix=/usr --root="$pkgdir" -O1 --skip-build
}

package_python-otxv2() {
  depends=('python' 'python-requests' 'python-dateutil' 'python-pytz')

  install -dm 755 "$pkgdir/usr/share/${pkgname[1]}"

  cd "$_pkgname-$pkgver"

  python setup.py install --prefix=/usr --root="$pkgdir" -O1 --skip-build
}

