# This file is part of BlackArch Linux ( https://www.blackarch.org/ ).
# See COPYING for license details.

pkgbase=python-otxv2
pkgname=('python2-otxv2' 'python-otxv2')
_pkgname=OTXv2
pkgver=1.5.5
pkgrel=1
epoch=1
pkgdesc='AlienVault OTX API.'
arch=('any')
url='https://pypi.python.org/pypi/OTXv2/'
license=('APACHE')
makedepends=('python2-setuptools' 'python-setuptools')
source=("https://files.pythonhosted.org/packages/74/75/4127f801c55e2a5de3088073881f1d477fb5c819461b158b4e1c987f9f94/OTXv2-$pkgver.tar.gz")
sha512sums=('d611b06e0086704d63b1fedd9c4191db83cd8b3b6640aee120a1e7b592a17a5e172b528707defd71a55ba29b41791c5faaeae602665f8a804169c1b78fab819f')

prepare() {
  cp -a "$_pkgname-$pkgver"{,-2}
}

build() {
  cd "$_pkgname-$pkgver-2"

  python2 setup.py build

  cd "$srcdir/$_pkgname-$pkgver"

  python setup.py build
}

package_python2-otxv2() {
  depends=('python2' 'python2-requests' 'python2-dateutil' 'python2-pytz')

  install -dm 755 "$pkgdir/usr/share/${pkgname[0]}"

  cd "$_pkgname-$pkgver-2"

  python2 setup.py install --prefix=/usr --root="$pkgdir" -O1 --skip-build
}

package_python-otxv2() {
  depends=('python' 'python-requests' 'python-dateutil' 'python-pytz')

  install -dm 755 "$pkgdir/usr/share/${pkgname[1]}"

  cd "$_pkgname-$pkgver"

  python setup.py install --prefix=/usr --root="$pkgdir" -O1 --skip-build
}

