# This file is part of BlackArch Linux ( https://www.blackarch.org/ ).
# See COPYING for license details.

pkgbase=python-otxv2
pkgname=('python2-otxv2' 'python-otxv2')
_pkgname=OTXv2
pkgver=1.5.8
pkgrel=1
epoch=1
pkgdesc='AlienVault OTX API.'
arch=('any')
url='https://pypi.python.org/pypi/OTXv2/'
license=('APACHE')
makedepends=('python2-setuptools' 'python-setuptools')
source=("https://files.pythonhosted.org/packages/3d/09/fea1bef9992e41fa35e6dcd402317d2ea0da6c57bd46e5d96981c357d90f/OTXv2-$pkgver.tar.gz")
sha512sums=('0d3ab77fbc2460e0a0fa89a65dec9482de4a65047096391b64afc372776963867e5c1b8a9978b4e80e34819a436f08ab4b83c73ce68769246c4fe3d1e8ae9eab')

prepare() {
  cp -a "$_pkgname-$pkgver"{,-2}
}

build() {
  cd "$_pkgname-$pkgver-2"

  python2 setup.py build

  cd "$srcdir/$_pkgname-$pkgver"

  python setup.py build
}

package_python2-otxv2() {
  depends=('python2' 'python2-requests' 'python2-dateutil' 'python2-pytz')

  install -dm 755 "$pkgdir/usr/share/${pkgname[0]}"

  cd "$_pkgname-$pkgver-2"

  python2 setup.py install --prefix=/usr --root="$pkgdir" -O1 --skip-build
}

package_python-otxv2() {
  depends=('python' 'python-requests' 'python-dateutil' 'python-pytz')

  install -dm 755 "$pkgdir/usr/share/${pkgname[1]}"

  cd "$_pkgname-$pkgver"

  python setup.py install --prefix=/usr --root="$pkgdir" -O1 --skip-build
}

