# Maintainer: Pedro 'psf' Freitas <team@blackarch.org>
# Extracted from Arch Linux Repositories
# Arch Contributor: Levente Polyak <anthraxx[at]archlinux[dot]org>
# Contributor: Sabart Otto - Seberm <seberm[at]seberm[dot]com>
# Contributor: Tobias Veit - nIcE <m.on.key.tobi[at]gmail[dot]com>

pkgname=metasploit-git
_pkgname=metasploit
pkgver=6.0.21.r18.gfc701733f9
pkgrel=1
pkgdesc='Advanced open-source platform for developing, testing, and using exploit code (git)'
#url='https://www.metasploit.com/'
url='https://github.com/rapid7/metasploit-framework'
#arch=('x86_64')
arch=('any')
license=('BSD')
conflicts=('metasploit')
provides=('metasploit')
depends=('ruby' 'libpcap' 'postgresql-libs' 'ruby-bundler' 'sqlite' 'libxslt' 'libxml2' 'inetutils' 'msfdb')
makedepends=('git')
options=('!strip' '!emptydirs')
source=("$_pkgname::git+https://github.com/rapid7/$_pkgname-framework.git")
#sha512sums=('7f52253a1dac3fae94e06b889de62f4413cb0fade422facf72f2b3bfef36d77139793cbcd74096d94ec0924cb8884fd3be7048294e92929d0f8505bba2c26f45')
sha512sums=('SKIP')
#b2sums=('d49dcb46ec810a64d2a30b64af7e041bfb1b9f11106b8c0642531f072426d5110f1cb474a1c53d952e3832462c3e449c33c1b48372e3e3682df8460659759bdb')
b2sums=('SKIP')

pkgver() {
  cd $_pkgname

  git describe --long --tags | sed 's/\([^-]*-g\)/r\1/;s/-/./g'
}

prepare() {
  cd $_pkgname

  bundle config build.nokogiri --use-system-libraries
  sed 's|git ls-files|find -type f|' -i metasploit-framework.gemspec
}

build() {
  cd $_pkgname

  CFLAGS+=" -I/usr/include/libxml2"
  bundle install -j"$(nproc)" --no-cache --deployment
  find vendor/bundle/ruby -exec chmod o+r '{}' \;
  find vendor/bundle/ruby \( -name gem_make.out -or -name mkmf.log \) -delete
}

package() {
  cd $_pkgname

  install -dm 755 "$pkgdir/opt/$_pkgname" "$pkgdir/usr/bin"
  cp -r . "$pkgdir/opt/$_pkgname"

  for f in "$pkgdir"/opt/$_pkgname/msf*; do
    local _msffile="$pkgdir/usr/bin/`basename "$f"`"
    echo -e "#!/bin/sh\nBUNDLE_GEMFILE=/opt/$_pkgname/Gemfile bundle exec ruby /opt/$_pkgname/`basename "$f"` \"\$@\"" > "$_msffile"
    chmod 755 "${_msffile}"
  done

  (cd "$pkgdir/opt/$_pkgname"
    for f in tools/*/*.rb; do
      install -Dm 755 "$f" ".$f"
      echo -e "#!/bin/sh\nBUNDLE_GEMFILE=/opt/$_pkgname/Gemfile bundle exec ruby /opt/$_pkgname/."$f" \"\$@\"" > "$f"
      chmod 755 "$f"
    done
  )

  install -Dm 644 external/zsh/_* -t "$pkgdir/usr/share/zsh/site-functions"
  install -Dm 644 LICENSE COPYING -t "$pkgdir/usr/share/licenses/$_pkgname"
  install -d "$pkgdir/usr/share/doc"
  mv "$pkgdir/opt/$_pkgname/documentation" "$pkgdir/usr/share/doc/$_pkgname"
  rm "$pkgdir/usr/bin/msfupdate"
  rm "$pkgdir/usr/bin/msfdb"
  rm -rf "$pkgdir/opt/$_pkgname/.git"
  rm -rf "$pkgdir/opt/$_pkgname/vendor/bundle/ruby/*/cache"
}

