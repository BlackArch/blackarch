# This file is part of BlackArch Linux ( http://blackarch.org ).
# See COPYING for license details.

pkgname='firewalk'
pkgver='5.0'
pkgrel=4
groups=('blackarch' 'blackarch-fuzzer' 'blackarch-scanner')
pkgdesc="An active reconnaissance network security tool"
arch=('x86_64' 'armv6h' 'armv7h' 'aarch64')
license=('BSD')
depends=('libdnet' 'libnet' 'libpcap')
source=('http://packetfactory.openwall.net/firewalk/dist/firewalk.tar.gz'
        'firewalk-gcc3.4.patch')
url='http://packetfactory.openwall.net/projects/firewalk/'
sha1sums=('d6603ab0ccb84facf65220331fe94d6ec742932c'
          'a4529727a763604c2cb06623ddcf37e990a8cb3b')

prepare() {
  cd "$srcdir/Firewalk"

  patch -Np1 < "$srcdir/firewalk-gcc3.4.patch"
}

build() {
  cd "$srcdir/Firewalk"

  if [[ $CARCH == arm* ]]; then
    ./configure --prefix=/usr --build=arm
  else
    ./configure --prefix=/usr
  fi
  make
}

package() {
  cd "$srcdir/Firewalk"

  install -Dm755 firewalk "$pkgdir/usr/bin/firewalk"
}
