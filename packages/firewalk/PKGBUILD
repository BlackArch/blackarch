pkgname=firewalk
pkgver=5.0
pkgrel=2
pkgdesc="An active reconnaissance network security tool"
arch=('i686' 'x86_64')
license=('BSD')
depends=('libdnet' 'libnet' 'libpcap')
source=('http://packetfactory.openwall.net/firewalk/dist/firewalk.tar.gz'
				'firewalk-gcc3.4.patch')
url='http://packetfactory.openwall.net/projects/firewalk/'
groups=('blackarch' 'blackarch-analysis')
md5sums=('02412f13218ea052427bbf038a88a026'
				'd94be823c96efbaeee8f75941cfdd3b7')

prepare() {
  cd "$srcdir/Firewalk"

  patch -Np1 < "$srcdir/firewalk-gcc3.4.patch"
}

build() {
  cd "$srcdir/Firewalk"

  ./configure --prefix=/usr
  make
}

package() {
  cd "$srcdir/Firewalk"

  install -Dm755 firewalk "$pkgdir/usr/bin/firewalk"
}
