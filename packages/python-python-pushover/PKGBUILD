# This file is part of BlackArch Linux ( https://www.blackarch.org/ ).
# See COPYING for license details.

pkgbase=python-python-pushover
pkgname=('python2-python-pushover' 'python-python-pushover')
_pkgname=python-pushover
pkgver=0.3
pkgrel=1
pkgdesc='Comprehensive bindings and command line utility for the Pushover notification service.'
arch=('any')
url='https://pypi.org/project/python-pushover/#files'
license=('GPL3')
makedepends=('python2-setuptools' 'python-setuptools')
source=("https://files.pythonhosted.org/packages/8f/c0/cc563191ab786dea6bdb787184e2546755b01ce3216b66d8301f0341d7d9/$_pkgname-$pkgver.tar.gz")
sha512sums=('67be74489c39960ae49a55b6644716e35891c9de2853d79d59a3a4e802b948514bce273876467ed075ea31f63fdb343bb432ec68a25985f2e399bc7699c00833')

prepare() {
  cp -a "$_pkgname-$pkgver"{,-2}
}

build() {
  cd "$_pkgname-$pkgver-2"

  python2 setup.py build

  cd "$srcdir/$_pkgname-$pkgver"

  python setup.py build
}

package_python2-python-pushover() {
  depends=('python2' 'python2-requests')

  cd "$_pkgname-$pkgver-2"

  python2 setup.py install --prefix=/usr --root="$pkgdir" --optimize=1 \
    --skip-build

  mv "$pkgdir/usr/bin/pushover" "$pkgdir/usr/bin/pushover2"
}

package_python-python-pushover() {
  depends=('python' 'python-requests')

  cd "$_pkgname-$pkgver"

  python setup.py install --prefix=/usr --root="$pkgdir" --optimize=1 \
    --skip-build
}

