# This file is part of BlackArch Linux ( http://blackarch.org ).
# See COPYING for license details.

pkgname='coffee-script'
pkgver='1.9.0'
pkgrel=1
pkgdesc='A little language that compiles into JavaScript.'
arch=('any')
url='http://coffeescript.org'
license=('MIT')
depends=('nodejs' 'underscorejs' 'nodejs-uglify-js')
optdepends=('nodejs-profile: to load coffee-script in node')
source=("https://github.com/jashkenas/coffee-script/archive/${pkgver}.tar.gz"
        'coffee-script.1')
sha1sums=('372b10088187797d6a94bfe53617972ef51b2e16'
          'af37e5f02be13198214c9b8edfa07e6459c13a67')

prepare() {
  cd "$srcdir/coffeescript-$pkgver"

  npm install
}

build() {
  cd "$srcdir/coffeescript-$pkgver"

  bin/cake build
  bin/cake build:parser
  bin/cake build:browser
}

package() {
  cd "$srcdir/coffeescript-$pkgver"

  mkdir -p "$pkgdir/usr/lib/node_modules"

  HOME="$PWD" bin/cake --prefix "$pkgdir/usr" install

  mv "$pkgdir/usr/lib/coffee-script" "$pkgdir/usr/lib/node_modules/"

  ln -sf /usr/lib/node_modules/coffee-script/bin/{cake,coffee} "$pkgdir/usr/bin/"

  install -Dm644 "$srcdir/coffee-script.1" "$pkgdir/usr/share/man/man1/coffee.1"
  install -Dm644 LICENSE "$pkgdir/usr/share/licenses/coffee-script/LICENSE"
  install -Dm644 README "$pkgdir/usr/share/doc/coffee-script/README"

  npm install
}
