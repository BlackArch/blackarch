# This file is part of BlackArch Linux ( http://blackarch.org ).
# See COPYING for license details.

pkgname='coffee-script'
pkgver='1.10.0'
pkgrel=1
pkgdesc='A little language that compiles into JavaScript.'
arch=('any')
url='http://coffeescript.org'
license=('MIT')
depends=('nodejs' 'underscorejs' 'nodejs-uglify-js')
makedepends=('npm')
optdepends=('nodejs-profile: to load coffee-script in node')
source=("https://github.com/jashkenas/coffee-script/archive/${pkgver}.tar.gz"
        'coffee-script.1')
sha1sums=('1f6bcb1e0cd5f831db6455b10d6ff152056d8d5c'
          'af37e5f02be13198214c9b8edfa07e6459c13a67')

prepare() {
  cd "$srcdir/coffeescript-$pkgver"

  npm install
}

build() {
  cd "$srcdir/coffeescript-$pkgver"

  MINIFY=false bin/cake build
  MINIFY=false bin/cake build:parser
  MINIFY=false bin/cake build:browser
}

package() {
  cd "$srcdir/coffeescript-$pkgver"

  mkdir -p "$pkgdir/usr/bin"
  mkdir -p "$pkgdir/usr/lib/node_modules"

  install -Dm644 "$srcdir/coffee-script.1" "$pkgdir/usr/share/man/man1/coffee.1"
  install -Dm644 -t "$pkgdir/usr/share/doc/coffee-script" README.md \
    CONTRIBUTING.md
  cp -a {documentation,examples} "$pkgdir/usr/share/doc/coffee-script/"

  install -Dm644 LICENSE "$pkgdir/usr/share/licenses/coffee-script/LICENSE"

  install -Dm644 package.json \
    "$pkgdir/usr/lib/node_modules/coffee-script/package.json"

  cp -a {bin,extras,lib,node_modules,src,test} \
    "$pkgdir/usr/lib/node_modules/coffee-script/"

  install -m644 *.js "$pkgdir/usr/lib/node_modules/coffee-script/"

  ln -s /usr/lib/node_modules/coffee-script/bin/{cake,coffee} "$pkgdir/usr/bin/"
}
