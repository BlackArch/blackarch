# This file is part of BlackArch Linux ( https://www.blackarch.org/ ).
# See COPYING for license details.

pkgname=webanalyze
pkgver=91.4b7ed27
pkgrel=2
pkgdesc='Port of Wappalyzer (uncovers technologies used on websites) in go to automate scanning.'
arch=('x86_64' 'aarch64')
groups=('blackarch' 'blackarch-webapp' 'blackarch-recon' 'blackarch-scanner'
        'blackarch-fingerprint')
url='https://github.com/rverton/webanalyze'
license=('GPL3')
makedepends=('git' 'go')
source=("git+https://github.com/rverton/$pkgname.git")
sha512sums=('SKIP')

pkgver() {
  cd $pkgname

  echo $(git rev-list --count HEAD).$(git rev-parse --short HEAD)
}

build() {
  cd $pkgname

  GOPATH="$srcdir" go mod download
  GOPATH="$srcdir" go build \
    -trimpath \
    -buildmode=pie \
    -mod=readonly \
    -modcacherw \
    -ldflags "-s -w" \
    -o $pkgname ./cmd/$pkgname
}

package() {
  cd $pkgname

  install -Dm 755 $pkgname "$pkgdir/usr/bin/$pkgname"
  install -Dm 644 technologies.json -t "$pkgdir/usr/share/$pkgname"
  install -Dm 644 -t "$pkgdir/usr/share/doc/$pkgname/" README.md
  install -Dm 644 LICENSE -t "$pkgdir/usr/share/licenses/$pkgname"
}

