# This file is part of BlackArch Linux ( https://www.blackarch.org/ ).
# See COPYING for license details.

pkgbase=python-shelljob
pkgname=('python2-shelljob' 'python-shelljob')
_pkgname=shelljob
pkgver=0.5.8
pkgrel=1
pkgdesc='Run multiple subprocesses asynchronous/in parallel with streamed output/non-blocking reading. Also various tools to replace shell scripts.'
arch=('any')
url='https://pypi.org/project/shelljob/#files'
license=('GPL3')
makedepends=('python2-setuptools' 'python-setuptools')
source=("https://files.pythonhosted.org/packages/e9/3e/0ac0490df850c4096fa048313a21edf730fe337da8e1cf860eca19e0fb61/$_pkgname-$pkgver.tar.gz")
sha512sums=('3bf16be6bca44354cfd5d47cba261d9298ce4ae95ab1780dd624e699710134e34b8059998e23d7be47894f988c518478fe81b05cc9c80b988442aa529163fe4b')

prepare() {
  cp -a "$_pkgname-$pkgver"{,-2}
}

build() {
  cd "$_pkgname-$pkgver-2"

  python2 setup.py build

  cd "$srcdir/$_pkgname-$pkgver"

  python setup.py build
}

package_python2-shelljob() {
  depends=('python2')

  cd "$_pkgname-$pkgver-2"

  python2 setup.py install --prefix=/usr --root="$pkgdir" -O1 --skip-build
}

package_python-shelljob() {
  depends=('python')

  cd "$_pkgname-$pkgver"

  python setup.py install --prefix=/usr --root="$pkgdir" -O1 --skip-build
}

