# This file is part of BlackArch Linux ( https://www.blackarch.org/ ).
# See COPYING for license details.

pkgbase=python-shelljob
pkgname=('python2-shelljob' 'python-shelljob')
_pkgname=shelljob
pkgver=0.5.6
pkgrel=2
pkgdesc='Run multiple subprocesses asynchronous/in parallel with streamed output/non-blocking reading. Also various tools to replace shell scripts.'
arch=('any')
url='https://pypi.org/project/shelljob/#files'
license=('GPL3')
makedepends=('python2-setuptools' 'python-setuptools')
source=("https://files.pythonhosted.org/packages/7f/c4/3f1393d9960dd11b85e96a640a7587ce5bf65d75773fec82a744ba52add7/$_pkgname-$pkgver.tar.gz")
sha512sums=('cdfa521389dc46206d05fb7c3c2c58bf02fc53205926f0f2f2c0b0e95e40e4a245b6919791d2f6bf5d18a8cc564b92f0e3083f5956ee83424f9401d004609b21')

prepare() {
  cp -a "$_pkgname-$pkgver"{,-2}
}

build() {
  cd "$_pkgname-$pkgver-2"

  python2 setup.py build

  cd "$srcdir/$_pkgname-$pkgver"

  python setup.py build
}

package_python2-shelljob() {
  depends=('python2')

  cd "$_pkgname-$pkgver-2"

  python2 setup.py install --prefix=/usr --root="$pkgdir" -O1 --skip-build
}

package_python-shelljob() {
  depends=('python')

  cd "$_pkgname-$pkgver"

  python setup.py install --prefix=/usr --root="$pkgdir" -O1 --skip-build
}

