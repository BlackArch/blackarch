# This file is part of BlackArch Linux ( https://www.blackarch.org/ ).
# See COPYING for license details.

pkgbase=python-isc_dhcp_leases
pkgname=('python2-isc_dhcp_leases' 'python-isc_dhcp_leases')
_pkgname=isc_dhcp_leases
pkgver=0.9.1
pkgrel=5
pkgdesc='Small python module for reading /var/lib/dhcp/dhcpd.leases from isc-dhcp-server.'
arch=('any')
license=('MIT')
url='https://pypi.org/project/isc_dhcp_leases/'
makedepends=('python2-setuptools' 'python-setuptools')
source=("https://files.pythonhosted.org/packages/0f/2e/548f4e2cd14616edddf38b8356b115203d73f7a6919278fb3d64491f2f4e/$_pkgname-$pkgver.tar.gz")
sha512sums=('271b449ae9c90e399596dea986042dd85eac3634005ad602dadcf0489b4213ee3e6845d9ddf69373ce1577da907a74f4d2568394e9dae0dad60ff24c4e9be6f3')

prepare() {
  cp -a "$_pkgname-$pkgver"{,-2}
}

build() {
  cd "$_pkgname-$pkgver-2"

  python2 setup.py build

  cd "$srcdir/$_pkgname-$pkgver"

  python setup.py build
}

package_python2-isc_dhcp_leases() {
  depends=('python2' 'python2-six')

  cd "$_pkgname-$pkgver-2"

  python2 setup.py install --root="$pkgdir" --optimize=1 --skip-build
}

package_python-isc_dhcp_leases() {
  depends=('python' 'python-six')

  cd "$_pkgname-$pkgver"

  python setup.py install --root="$pkgdir" --optimize=1 --skip-build
}

