# This file is part of BlackArch Linux ( https://www.blackarch.org/ ).
# See COPYING for license details.

pkgbase=python-sphinxcontrib-httpdomain
pkgname=('python2-sphinxcontrib-httpdomain' 'python-sphinxcontrib-httpdomain')
_pkgname=sphinxcontrib-httpdomain
pkgver=1.7.0
pkgrel=3
pkgdesc='Sphinx domain for documenting HTTP APIs.'
arch=('any')
url='https://pypi.org/project/sphinxcontrib-httpdomain/#files'
license=('BSD')
makedepends=('python2-setuptools' 'python-setuptools')
source=("https://files.pythonhosted.org/packages/b4/8d/8dbb8b6745d7a59084cf1b28837b32c9717c1b4a97333d5b25e25fa9813b/$_pkgname-$pkgver.tar.gz")
sha512sums=('132803f60af263a4948108145ac27635da50254eb79dad2c461c4f364b04eb6b28d4917449fae31b792fc8773ce948f1fd20b06b22e167bfb4584dad756c8f17')

prepare() {
  cp -a "$_pkgname-$pkgver"{,-2}
}

build() {
  cd "$_pkgname-$pkgver-2"

  python2 setup.py build

  cd "$srcdir/$_pkgname-$pkgver"

  python setup.py build
}

package_python2-sphinxcontrib-httpdomain() {
  depends=('python2' 'python2-sphinx' 'python2-six')

  cd "$_pkgname-$pkgver-2"

  python2 setup.py install --prefix=/usr --root="$pkgdir" --optimize=1 \
    --skip-build
}

package_python-sphinxcontrib-httpdomain() {
  depends=('python' 'python-sphinx' 'python-six')

  cd "$_pkgname-$pkgver"

  python setup.py install --prefix=/usr --root="$pkgdir" --optimize=1 \
    --skip-build
}

