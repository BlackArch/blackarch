# This file is part of BlackArch Linux ( https://www.blackarch.org/ ).
# See COPYING for license details.

pkgbase=python-sphinxcontrib-httpdomain
pkgname=('python2-sphinxcontrib-httpdomain' 'python-sphinxcontrib-httpdomain')
_pkgname=sphinxcontrib-httpdomain
pkgver=1.8.0
pkgrel=2
pkgdesc='Sphinx domain for documenting HTTP APIs.'
arch=('any')
url='https://pypi.org/project/sphinxcontrib-httpdomain/#files'
license=('BSD')
makedepends=('python2-setuptools' 'python-setuptools')
source=("https://files.pythonhosted.org/packages/6f/00/6560676deac1d2be1009d5e14b2b16fcc9f5e9c3ac00ed50c3b297a60d3a/$_pkgname-$pkgver.tar.gz")
sha512sums=('108fab5b7f104a16fc33fb9c442d864866231387ca5a9ec6519f0c5542a93670da5aa8f0bf92cafa5da540b88c2523ea118d842daeb10604bf8457c93c4c668d')

prepare() {
  cp -a "$_pkgname-$pkgver"{,-2}
}

build() {
  cd "$_pkgname-$pkgver-2"

  python2 setup.py build

  cd "$srcdir/$_pkgname-$pkgver"

  python setup.py build
}

package_python2-sphinxcontrib-httpdomain() {
  depends=('python2' 'python2-sphinx' 'python2-six')

  cd "$_pkgname-$pkgver-2"

  python2 setup.py install --prefix=/usr --root="$pkgdir" --optimize=1 \
    --skip-build
}

package_python-sphinxcontrib-httpdomain() {
  depends=('python' 'python-sphinx' 'python-six')

  cd "$_pkgname-$pkgver"

  python setup.py install --prefix=/usr --root="$pkgdir" --optimize=1 \
    --skip-build
}

