# This file is part of BlackArch Linux ( http://blackarch.org ).
# See COPYING for license details.

pkgname='mongoaudit'
pkgver=216.28d1e03
pkgrel=1
pkgdesc='A powerful MongoDB auditing and pentesting tool .'
groups=('blackarch' 'blackarch-scanner' 'blackarch-fuzzer')
arch=('any')
url='https://github.com/stampery/mongoaudit'
license=('MIT')
depends=('python2' 'python2-pymongo' 'python2-urwid')
makedepends=('git' 'python2-setuptools')
source=('git+https://github.com/stampery/mongoaudit.git')
sha1sums=('SKIP')

pkgver() {
  cd "$srcdir/mongoaudit"

  echo $(git rev-list --count HEAD).$(git rev-parse --short HEAD)
}

build() {
  cd "$srcdir/mongoaudit"

  python2 setup.py build
}

package() {
  cd "$srcdir/mongoaudit"

  python2 setup.py install --root="$pkgdir" --prefix=/usr

  install -Dm644 -t "$pkgdir/usr/share/doc/mongoaudit/" README.*
  install -Dm644 LICENSE "$pkgdir/usr/share/licenses/mongoaudit/LICENSE"
}
