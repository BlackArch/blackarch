# This file is part of BlackArch Linux ( https://www.blackarch.org/ ).
# See COPYING for license details.

pkgname=certipy
_pkgname=Certipy
pkgver=4.3.0.r0.gdcb873e
pkgrel=3
pkgdesc='Active Directory Certificate Services enumeration and abuse.'
arch=('any')
groups=('blackarch' 'blackarch-windows' 'blackarch-exploitation')
url='https://github.com/ly4k/Certipy'
license=('MIT')
depends=('python' 'python-asn1crypto' 'python-cryptography' 'impacket'
         'python-ldap3' 'python-pyasn1' 'python-dnspython' 'python-dsinternals'
         'python-pyopenssl' 'python-requests' 'python-requests-ntlm'
         'python-future' 'ldapdomaindump')
makedepends=('git' 'python-setuptools')
source=("git+https://github.com/ly4k/$_pkgname.git")
sha512sums=('SKIP')

pkgver() {
  cd $_pkgname

  git describe --long --tags | sed 's/\([^-]*-g\)/r\1/;s/-/./g'
}

build() {
  cd $_pkgname

  python setup.py build
}

package() {
  cd $_pkgname

  python setup.py install --root="$pkgdir" --prefix=/usr -O1 --skip-build
}

