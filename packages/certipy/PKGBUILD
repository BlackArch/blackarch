# This file is part of BlackArch Linux ( https://www.blackarch.org/ ).
# See COPYING for license details.

pkgname=certipy
_pkgname=Certipy
pkgver=4.8.2.r0.g2780d53
pkgrel=3
pkgdesc='Active Directory Certificate Services enumeration and abuse.'
arch=('any')
groups=('blackarch' 'blackarch-windows' 'blackarch-exploitation')
url='https://github.com/ly4k/Certipy'
license=('MIT')
depends=('python' 'python-asn1crypto' 'python-cryptography' 'impacket'
         'python-ldap3' 'python-pyasn1' 'python-dnspython' 'python-dsinternals'
         'python-pyopenssl' 'python-requests' 'python-requests-ntlm'
          'python-pycryptodome' 'python-unicrypto'
          # missing from impacket
          # future missing upstream https://github.com/fortra/impacket/pull/1724
          # others are missing from AL
          'python-charset-normalizer' 'python-future' 'python-chardet'
          'ldapdomaindump'
        )
        # unused? 'python-pyspnego')
makedepends=('git' 'python-setuptools')
source=("git+https://github.com/ly4k/$_pkgname.git")
sha512sums=('SKIP')

pkgver() {
  cd $_pkgname

  git describe --long --tags | sed 's/\([^-]*-g\)/r\1/;s/-/./g'
}

prepare() {
  cd $_pkgname

  # https://github.com/ly4k/Certipy/pull/204
  sed -i 's/pyasn1==0.4.8/pyasn1>=0.4.8,<0.6.0/' setup.py
}

build() {
  cd $_pkgname

  python setup.py build
}

package() {
  cd $_pkgname

  python setup.py install --root="$pkgdir" --prefix=/usr -O1 --skip-build
}

