# This file is part of BlackArch Linux ( https://www.blackarch.org/ ).
# See COPYING for license details.

pkgname=python-click-repl
_pkgname=click-repl
pkgver=0.3.0
pkgrel=5
pkgdesc='REPL plugin for Click.'
arch=('any')
url='https://pypi.org/project/click-repl/#files'
license=('MIT')
depends=('python' 'python-click' 'python-prompt_toolkit')
makedepends=('python-build' 'python-pip' 'python-wheel')
source=("https://files.pythonhosted.org/packages/source/${_pkgname::1}/$_pkgname/$_pkgname-$pkgver.tar.gz")
sha512sums=('ce6763c8b194c7e8c2100dc140c3af9696e3528e5d74befa98129cde2cff98df7e3c26de65ae42dff8c001b85264e4a7094f1de9421129cd986415e4f3a5a1fa')

build() {
  cd "$_pkgname-$pkgver"

  python -m build --wheel --outdir="$startdir/dist"
}

package() {
  cd "$_pkgname-$pkgver"

  pip install \
    --verbose \
    --disable-pip-version-check \
    --no-warn-script-location \
    --ignore-installed \
    --no-compile \
    --no-deps \
    --root="$pkgdir" \
    --prefix=/usr \
    --no-index \
    --find-links="file://$startdir/dist" \
    $_pkgname
}

