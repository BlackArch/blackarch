pkgname=btscanner
pkgver=2.1
groups=('blackarch' 'blackarch-wireless')
pkgrel=5
pkgdesc="Bluetooth device scanner."
url='http://www.pentest.co.uk'
depends=('bluez-libs' 'bluez-utils' 'libxml2' 'ncurses')
arch=('i686' 'x86_64')
license=('GPL')
changelog="btscanner.changelog"
source=(http://www.pentest.co.uk/src/btscanner-$pkgver.tar.bz2
        btscanner.patch)
md5sums=('587ec5847647d432eac1704b260af020'
         '481f2f5b49ba83b8ab8155983c911db9')

prepare() {
  cd "$srcdir/btscanner-$pkgver"

  patch -p1 < ../btscanner.patch
}

build() {
  cd "$srcdir/btscanner-$pkgver"

  ./configure --prefix=/usr --sysconf=/etc/btscanner --datadir=/usr/share/btscanner
  make
}

package() {
  cd "$srcdir/btscanner-$pkgver"

  make DESTDIR="${pkgdir}" install

  install -Dm644 README "${pkgdir}/usr/share/doc/btscanner/README"
  install -Dm644 USAGE  "${pkgdir}/usr/share/doc/btscanner/USAGE"
}
