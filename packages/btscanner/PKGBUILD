# This file is part of BlackArch Linux ( http://blackarch.org ).
# See COPYING for license details.

pkgname='btscanner'
pkgver='2.1'
pkgrel=7
groups=('blackarch' 'blackarch-bluetooth' 'blackarch-scanner')
pkgdesc="Bluetooth device scanner."
url='http://www.pentest.co.uk'
depends=('bluez-libs' 'libxml2' 'ncurses')
arch=('x86_64' 'armv6h' 'armv7h' 'aarch64')
license=('GPL')
changelog="btscanner.changelog"
source=("http://www.pentest.co.uk/src/btscanner-${pkgver}.tar.bz2"
        'btscanner.patch')
sha1sums=('9396bc977ea9b33ebf4f699628f0cdd66688529d'
          '4f0e99ffd2d5fee5875c08a7fe8b81964f91de76')

prepare() {
  cd "$srcdir/btscanner-$pkgver"

  patch -p1 < ../btscanner.patch
}

build() {
  cd "$srcdir/btscanner-$pkgver"

  ./configure --prefix=/usr --sysconf=/etc/btscanner \
    --datadir=/usr/share/btscanner

  make
}

package() {
  cd "$srcdir/btscanner-$pkgver"

  make DESTDIR="$pkgdir" install

  install -Dm644 README "${pkgdir}/usr/share/doc/btscanner/README"
  install -Dm644 USAGE  "${pkgdir}/usr/share/doc/btscanner/USAGE"
}
