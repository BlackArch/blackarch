# This file is part of BlackArch Linux ( https://www.blackarch.org/ ).
# See COPYING for license details.

pkgbase=python-enaml
pkgname=('python2-enaml' 'python-enaml')
_pkgname=enaml
pkgver=0.10.4
pkgrel=1
pkgdesc='Declarative DSL for building rich user interfaces in Python.'
arch=('x86_64' 'aarch64')
url='https://pypi.org/project/enaml/#files'
license=('BSD')
makedepends=('python2-setuptools' 'python-setuptools')
source=("https://files.pythonhosted.org/packages/af/e4/4bce906f456c9d3ca5fc59fb2fb02b031dd47e26c6294bda1dbf13deea85/$_pkgname-$pkgver.tar.gz")
sha512sums=('bf2d97a2f50c7110b01193f09fe60b5f8fb0a40e77dc9a49e9652660350662087cee1198633af1f0db08f3c5128b3bed02f27b769c82a7f518590f973ae53190')

prepare() {
  cp -a "$_pkgname-$pkgver"{,-2}
}

build() {
  cd "$_pkgname-$pkgver-2"

  python2 setup.py build

  cd "$srcdir/$_pkgname-$pkgver"

  python setup.py build
}

package_python2-enaml() {
  depends=('python2' 'python2-ply' 'python2-kiwisolver' 'python2-atom'
           'python2-qtpy')

  cd "$_pkgname-$pkgver"

  python setup.py install --prefix=/usr --root="$pkgdir" --optimize=1 \
    --skip-build

  mv "$pkgdir/usr/bin/enaml-compileall" "$pkgdir/usr/bin/enaml-compileall2"
  mv "$pkgdir/usr/bin/enaml-run" "$pkgdir/usr/bin/enaml-run2"
}

package_python-enaml() {
  depends=('python' 'python-ply' 'python-kiwisolver' 'python-atom'
           'python-qtpy')

  cd "$_pkgname-$pkgver"

  python setup.py install --prefix=/usr --root="$pkgdir" --optimize=1 \
    --skip-build
}

