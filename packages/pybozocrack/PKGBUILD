# This file is part of BlackArch Linux ( http://blackarch.org ).
# See COPYING for license details.

pkgname='pybozocrack'
pkgver=75.9900883
pkgrel=1
pkgdesc='A silly & effective MD5 cracker in Python.'
groups=('blackarch' 'blackarch-cracker' 'blackarch-crypto')
arch=('any')
url='https://github.com/ikkebr/PyBozoCrack'
license=('custom:unknown')
depends=('python2' 'python2-wheel')
makedepends=('git' 'python2-setuptools')
source=('git+https://github.com/ikkebr/PyBozoCrack')
sha1sums=('SKIP')

pkgver() {
  cd "$srcdir/PyBozoCrack"

  echo $(git rev-list --count HEAD).$(git rev-parse --short HEAD)
}

build() {
  cd "$srcdir/PyBozoCrack"

  python2 setup.py build
}

package() {
  cd "$srcdir/PyBozoCrack"

  python2 setup.py install --root="$pkgdir" --prefix=/usr --optimize=1

  install -Dm644 -t "$pkgdir/usr/share/doc/pybozocrack/" *.rst
  install -Dm644 LICENSE "$pkgdir/usr/share/licenses/pybozocrack/LICENSE"
}
