# This file is part of BlackArch Linux ( https://www.blackarch.org/ ).
# See COPYING for license details.

pkgname=python-mdutils
_pkgname=${pkgname#python-}
pkgver=1.8.1
pkgrel=2
pkgdesc='Set of basic tools that can help to create a markdown file.'
arch=('any')
url='https://github.com/didix21/mdutils'
license=('MIT')
depends=('python')
makedepends=('python-build' 'python-pip')
options=(!emptydirs)
source=("https://github.com/didix21/$_pkgname/archive/refs/tags/v$pkgver.tar.gz")
sha512sums=('94c81f5ccc571fe84f4211079199f9d6e63dd3bfb078856f858bf6497af1a20e144df1d94fd5dc7d42a87dcb1ed183d18cd948baef28fa5be754ef0c453190a7')

build() {
  cd "$_pkgname-$pkgver"

  python -m build --wheel --outdir="$startdir/dist"
}

package() {
  cd "$_pkgname-$pkgver"

  pip install \
    --verbose \
    --disable-pip-version-check \
    --no-warn-script-location \
    --ignore-installed \
    --no-compile \
    --no-deps \
    --root="$pkgdir" \
    --prefix=/usr \
    --no-index \
    --find-links="file://$startdir/dist" \
    $_pkgname
}

