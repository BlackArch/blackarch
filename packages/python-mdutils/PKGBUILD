# This file is part of BlackArch Linux ( https://www.blackarch.org/ ).
# See COPYING for license details.

pkgname=python-mdutils
_pkgname=${pkgname#python-}
pkgver=v1.6.0.r4.geaa500d
pkgrel=1
pkgdesc='Set of basic tools that can help to create a markdown file.'
arch=('any')
url='https://github.com/didix21/mdutils'
license=('MIT')
depends=('python')
makedepends=('python-setuptools')
source=("git+https://github.com/didix21/$_pkgname.git")
sha512sums=('SKIP')

pkgver() {
  cd $_pkgname

  ( set -o pipefail
    git describe --long --tags --abbrev=7 2>/dev/null |
      sed 's/\([^-]*-g\)/r\1/;s/-/./g' ||
    printf "%s.%s" "$(git rev-list --count HEAD)" \
      "$(git rev-parse --short=7 HEAD)"
  )
}

build() {
  cd $_pkgname

  python setup.py build
}

package() {
  cd $_pkgname

  python setup.py install --root="$pkgdir" --prefix=/usr -O1 --skip-build
}

