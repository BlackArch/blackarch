# This file is part of BlackArch Linux ( http://blackarch.org ).
# See COPYING for license details.

pkgname='tilt'
pkgver=90.2bc2ef2
pkgrel=3
groups=('blackarch' 'blackarch-recon')
pkgdesc='An easy and simple tool implemented in Python for ip reconnaissance, with reverse ip lookup.'
arch=('any')
url='https://github.com/AeonDave/tilt'
license=('MIT')
depends=('python2')
makedepends=('git')
source=('git+https://github.com/AeonDave/tilt.git')
sha1sums=('SKIP')

pkgver() {
  cd "$srcdir/tilt"

  echo $(git rev-list --count HEAD).$(git rev-parse --short HEAD)
}

prepare() {
  cd "$srcdir/tilt"

  sed -i 's|/usr/bin/python|/usr/bin/python2|g' tilt.py
}

package() {
  cd "$srcdir/tilt"

  mkdir -p "$pkgdir/usr/bin"
  mkdir -p "$pkgdir/usr/share/tilt"

  install -Dm644 README.md "$pkgdir/usr/share/doc/tilt/README.md"
  install -Dm644 LICENSE "$pkgdir/usr/share/licenses/tilt/LICENSE"

  rm README.md LICENSE

  cp -a * "$pkgdir/usr/share/tilt"

  cat > "$pkgdir/usr/bin/tilt.py" << EOF
#!/bin/sh
cd /usr/share/tilt
exec python2 tilt.py
EOF

  chmod +x "$pkgdir/usr/bin/tilt.py"
}
