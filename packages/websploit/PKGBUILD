# This file is part of BlackArch Linux ( https://www.blackarch.org/ ).
# See COPYING for license details.

pkgname=websploit
pkgver=4.0.4
pkgrel=1
groups=('blackarch' 'blackarch-exploitation' 'blackarch-fuzzer' 'blackarch-scanner' 'blackarch-social')
pkgdesc='An Open Source Project For, Social Engineering Works, Scan, Crawler & Analysis Web, Automatic Exploiter, Support Network Attacks'
arch=('any')
url='https://github.com/websploit/websploit'
license=('GPL3')
source=("https://github.com/websploit/websploit/archive/$pkgver.tar.gz")
depends=('python' 'scapy')
sha512sums=('5e5b229932a07be306d58bfb2a6547b464b7582e475b245796cf96412f3da77a7218e1048830c85609113bb753151e72fce8202e06dbf9715bd7729153628641')

package() {
  cd $pkgname-$pkgver

  install -dm 755 "$pkgdir/usr/bin"
  install -dm 755 "$pkgdir/usr/share/websploit"

  cd "$srcdir/$pkgname-$pkgver/$pkgname"

  install -Dm 644 -t "$pkgdir/usr/share/doc/$pkgname/" ../README.md
  install -Dm 644 ../LICENSE.txt "$pkgdir/usr/share/licenses/$pkgname/LICENSE"

  cp -a * "$pkgdir/usr/share/websploit"

  # Fix incompetence.
  find "$pkgdir" -name '*~' -delete

  cat > "$pkgdir/usr/bin/$pkgname" << EOF
#!/bin/sh
cd /usr/share/$pkgname
exec python $pkgname "\$@"
EOF

  chmod +x "$pkgdir/usr/bin/$pkgname"
}

