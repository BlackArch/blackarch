# This file is part of BlackArch Linux ( https://www.blackarch.org/ ).
# See COPYING for license details.

pkgname=python-api-dnsdumpster
_pkgname=api-dnsdumpster
pkgver=v0.10.r0.g0f8ba2b
pkgrel=1
epoch=1
groups=('blackarch' 'blackarch-recon' 'blackarch-scanner')
pkgdesc='Unofficial Python API for http://dnsdumpster.com/.'
arch=('any')
url='https://github.com/PaulSec/API-dnsdumpster.com'
license=('custom:unknown')
depends=('python' 'python-beautifulsoup4' 'python-requests')
makedepends=('git' 'python-setuptools')
source=("$_pkgname::git+https://github.com/PaulSec/API-dnsdumpster.com.git")
sha512sums=('SKIP')

pkgver() {
  cd $_pkgname

  ( set -o pipefail
    git describe --long --tags --abbrev=7 2>/dev/null |
      sed 's/\([^-]*-g\)/r\1/;s/-/./g' ||
    printf "%s.%s" "$(git rev-list --count HEAD)" \
      "$(git rev-parse --short=7 HEAD)"
  )
}

build() {
  cd $_pkgname

  python setup.py build
}

package() {
  cd $_pkgname

  install -dm 755 "$pkgdir/usr/bin"
  install -dm 755 "$pkgdir/usr/share/$pkgname"

  python setup.py install --root="$pkgdir" --prefix=/usr -O1 --skip-build

  install -Dm 644 -t "$pkgdir/usr/share/$pkgname/" README.md

  rm README.md

  cp -a dnsdumpster/* "$pkgdir/usr/share/$pkgname/"

  cat > "$pkgdir/usr/bin/$_pkgname" << EOF
#!/bin/sh
exec python /usr/share/$pkgname/API_example.py "\$@"
EOF

  chmod +x "$pkgdir/usr/bin/$_pkgname"

  find "$pkgdir" -type f -iname '*.pyc' -exec rm {} \;
}

