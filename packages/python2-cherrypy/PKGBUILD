# This file is part of BlackArch Linux ( https://www.blackarch.org/ ).
# See COPYING for license details.

pkgname=python2-cherrypy
_pkgname=CherryPy
pkgver=17.4.2
pkgrel=2
pkgdesc='Object-Oriented HTTP framework.'
arch=('any')
url='https://pypi.org/project/CherryPy/17.4.2/#files'
license=('BSD')
depends=('python2' 'python2-six' 'python2-cheroot' 'python2-more-itertools'
         'python2-portend' 'python2-jaraco' 'python2-contextlib2'
         'python2-zc.lockfile')
makedepends=('python2-setuptools' 'python2-setuptools-scm' 'python2-wheel')
source=("https://files.pythonhosted.org/packages/92/f6/c152461b5076fedcb1b9de8f7ff2e533e3aa9ce5ddf9f8f99671bf38dd14/$_pkgname-$pkgver.tar.gz")
sha512sums=('1f06d8228b45161773095dd05e163fa55ad0060ac6f740051294aa2ecc53036c367fe5a5ef327a8fd56b5a4afd1f8550ef00a3f71fcc13c6124d772e77459ff4')

build() {
  export SETUPTOOLS_SCM_PRETEND_VERSION="$pkgver"

  cd "$_pkgname-$pkgver"

  python2 ./setup.py build
}

package() {
  cd "$_pkgname-$pkgver"

  python2 setup.py install --root="$pkgdir" --prefix=/usr -O1 --skip-build

  mv "$pkgdir/usr/bin/cherryd" "$pkgdir/usr/bin/cherryd2"
}

