# This file is part of BlackArch Linux ( https://www.blackarch.org/ ).
# See COPYING for license details.

pkgname=openvas-scanner
pkgver=22.5.0
pkgrel=1
pkgdesc='The OpenVAS scanning Daemon.'
groups=('blackarch' 'blackarch-scanner' 'blackarch-recon' 'blackarch-fuzzer'
        'blackarch-exploitation')
arch=('x86_64' 'aarch64')
url='https://github.com/greenbone/openvas-scanner/'
license=('GPL')
depends=('gvm-libs' 'redis' 'rsync' 'json-glib' 'gvmd')
conflicts=('openvas')
makedepends=('cmake' 'doxygen')
install="$pkgname.install"
source=("https://github.com/greenbone/$pkgname/archive/v$pkgver.tar.gz"
        "greenbone-nvt-sync.service"
        "greenbone-nvt-sync.timer"
        "$pkgname.install"
      )
sha512sums=('9af44effc313d8c5898a05803ceef2559e39029f4e7573c18758fbfade4c6439d9957361481878e0cd1cd8237076b619ca5d233f747b2829f0cfbe08faf94ac5'
            '1ef913c24721533b0f779047532de63d195e8498885b90972a7bbbfb734444c0f7cbdcc52c437b2ce773881d45eadfcec03fcd00886e02962cb06d318d0c7c96'
            '2df63297cd9bd1cfb7b774152b1791ff946a1a410e563e54212652d847063b3c22b5362cd287b1bd73684be1fbd66464a2f8bb79b92cd086ba58e9a801d675ec'
            'a7a743ab2899187cf4dd71c605ed3acb722f19d5e3077858f3a6b3199b384de3d59c05c2c5d6cf0ce2df7ab1de99f058de6ff8c931b6fd33a8fbccf8d3afd82c')

prepare() {
  cd "$pkgname-$pkgver"

  sed -i '/-Werror/d' CMakeLists.txt
}

build() {
  cd "$pkgname-$pkgver"

  cmake \
    -DCMAKE_BUILD_TYPE=Release \
    -DSBINDIR=/usr/bin \
    -DLIBDIR=/usr/lib \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DSYSCONFDIR=/etc \
    -DLOCALSTATEDIR=/var \
    -DOPENVAS_FEED_LOCK_PATH=/run/gvm/feed-update.lock \
    -DOPENVAS_NVT_DIR=/var/lib/openvas \
    --debug-output \
    .

  make
}

package() {
  cd "$pkgname-$pkgver"

  make DESTDIR="$pkgdir" install

  cp "$srcdir/$pkgname-$pkgver/config/redis-openvas.conf" "$pkgdir/etc/openvas"

  install -m 755 -d "$pkgdir/usr/lib/systemd/system"

  install -m 644 "$srcdir/greenbone-nvt-sync.service" \
    "$pkgdir/usr/lib/systemd/system"
  install -m 644 "$srcdir/greenbone-nvt-sync.timer" \
    "$pkgdir/usr/lib/systemd/system"
}

