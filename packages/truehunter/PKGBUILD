# This file is part of BlackArch Linux ( http://blackarch.org ).
# See COPYING for license details.

pkgname=truehunter
pkgver=11.c757b02
pkgrel=1
pkgdesc='Detect TrueCrypt containers using a fast and memory efficient approach.'
groups=('blackarch' 'blackarch-forensic')
arch=('any')
url='https://github.com/adoreste/truehunter'
license=('custom:unknown')
depends=('python2' 'python2-argparse' 'python2-pysqlite')
makedepends=('git')
source=('git+https://github.com/adoreste/truehunter.git')
sha512sums=('SKIP')

pkgver() {
  cd "$srcdir/truehunter"

  echo $(git rev-list --count HEAD).$(git rev-parse --short HEAD)
}

prepare() {
  cd "$srcdir/truehunter"

  sed -i 's|bin/python|bin/python2|' truehunter.py
}

package() {
  cd "$srcdir/truehunter"

  install -Dm755 truehunter.py "$pkgdir/usr/bin/truehunter"
  install -Dm644 -t "$pkgdir/usr/share/doc/truehunter/" README.md
}
