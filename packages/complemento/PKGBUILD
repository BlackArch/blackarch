# This file is part of BlackArch Linux ( http://blackarch.org ).
# See COPYING for license details.

pkgname=complemento
pkgver=0.7.6
pkgrel=3
pkgdesc="A collection of tools for pentester: LetDown is a powerful tcp flooder ReverseRaider is a domain scanner that use wordlist scanning or reverse resolution scanning Httsquash is an http server scanner, banner grabber and data retriever"
url="http://complemento.sourceforge.net"
license=('GPL')
groups=('blackarch' 'blackarch-fingerprint' 'blackarch-scanner')
arch=('i686' 'x86_64' 'armv6h' 'armv7h')
depends=('c-ares' 'libnet' 'python2' 'libpcap')
source=(http://downloads.sourceforge.net/complemento/complemento-$pkgver.tar.gz)
md5sums=('dbc1c22769109f27b04556314cf58968')

prepare() {
  cd "$srcdir/complemento/letdown/"
  sed -i 's/python2.5/python2/g' Makefile

  cd "$srcdir/complemento/httsquash/"
  sed -i 's/python2.5/python2/g' Makefile

  cd "$srcdir/complemento/reverseraider/"
  sed -i 's/python2.5/python2/g' Makefile
}

build() {
  cd "$srcdir/complemento"

  make clean
  make
}

package() {
  # Base directories
  install -dm755 "$pkgdir/usr/share/complemento/reverseraider/wordlists"
  install -dm755 "$pkgdir/usr/share/complemento/letdown/payloads"
  install -dm755 "$pkgdir/usr/share/complemento/httsquash"
  install -dm755 "$pkgdir/usr/share/doc/complemento"
  install -dm755 "$pkgdir/usr/bin"

  cd "$srcdir/complemento"

  # Doc
  install -m644 readme "$pkgdir/usr/share/doc/complemento"

  # --- LETDOWN ---

  cd "$srcdir/complemento/letdown"

  # Bin
  install -m755 letdown "$pkgdir/usr/share/complemento/letdown"

  cat > "$pkgdir/usr/bin/letdown" <<EOF
#!/bin/sh
echo >&2
echo >&2 'Payloads are at /usr/share/complemento/letdown/payloads'
echo >&2 'Enjoy.'
echo >&2

/usr/share/complemento/letdown/letdown
EOF
  chmod +x "$pkgdir/usr/bin/letdown"

  # Doc
  install -m644 readme "$pkgdir/usr/share/doc/complemento/readme-letdown"

  # Payloads
  install -m644 payloads/* "$pkgdir/usr/share/complemento/letdown/payloads"

  # --- HTTSQUASH ---

  cd "$srcdir/complemento/httsquash"

  # Bin
  install -m755 httsquash "$pkgdir/usr/share/complemento/httsquash"
  ln -s /usr/share/complemento/httsquash/httsquash "$pkgdir/usr/bin/httsquash"

  # Doc
  install -m644 readme "$pkgdir/usr/share/doc/complemento/readme-httsquash"

  # --- REVERSERAIDER ---

  cd "$srcdir/complemento/reverseraider"

  # Bin
  install -m755 reverseraider "$pkgdir/usr/share/complemento/reverseraider"

  cat > "$pkgdir/usr/bin/reverseraider" <<EOF
#!/bin/sh
echo >&2
echo >&2 'Wordlists are at /usr/share/complemento/reverseraider/wordlists'
echo >&2 'Enjoy.'
echo >&2

/usr/share/complemento/reverseraider/reverseraider
EOF
  chmod +x "$pkgdir/usr/bin/reverseraider"


  # Wordlists
  install -m644 wordlists/* "$pkgdir/usr/share/complemento/reverseraider/wordlists"

}
