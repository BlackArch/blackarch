# This file is part of BlackArch Linux ( https://www.blackarch.org/ ).
# See COPYING for license details.

pkgbase=python-taxii2-client
pkgname=('python2-taxii2-client' 'python-taxii2-client')
_pkgname=cti-taxii-client
pkgver=2.0.0
pkgrel=1
pkgdesc='OASIS TC Open Repository: TAXII 2 Client Library Written in Python.'
arch=('any')
url='https://github.com/oasis-open/cti-taxii-client'
license=('BSD')
makedepends=('git' 'python2-setuptools' 'python-setuptools')
options=(!emptydirs)
source=("https://files.pythonhosted.org/packages/6d/65/6509611d88e032fb01a65f90016808c8e32aa5700d0b7a6d9dc3a2e10e0a/$_pkgname-$pkgver.tar.gz")
sha512sums=('3e9be8dc1e85c8bab007912f12d574bcc1d2091ab8d023fff4aa8cd1787259d8b4724f8026d38c591fbf24a5705328480066461344ececcd60437785a0da568f')

prepare() {
  cp -a "$_pkgname-$pkgver"{,-2}
}

build() {
  cd "$_pkgname-$pkgver-2"

  python2 setup.py build

  cd "$srcdir/$_pkgname-$pkgver"

  python setup.py build
}

package_python2-taxii2-client() {
  depends=('python2' 'python2-requests' 'python2-six' 'python2-pytz')

  cd "$_pkgname-$pkgver-2"

  python2 setup.py install --root="$pkgdir" --prefix=/usr -O1 --skip-build
}

package_python-taxii2-client() {
  depends=('python' 'python-requests' 'python-six' 'python-pytz')

  cd "$_pkgname-$pkgver"

  python setup.py install --root="$pkgdir" --prefix=/usr -O1 --skip-build
}

