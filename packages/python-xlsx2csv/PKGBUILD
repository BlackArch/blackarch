# This file is part of BlackArch Linux ( https://www.blackarch.org/ ).
# See COPYING for license details.

pkgbase=python-xlsx2csv
pkgname=('python2-xlsx2csv' 'python-xlsx2csv')
_pkgname=xlsx2csv
pkgver=0.8.1
pkgrel=1
pkgdesc='XLSX to CV converter.'
arch=('any')
url='https://pypi.org/project/xlsx2csv/#files'
license=('GPL2')
makedepends=('python2-setuptools' 'python-setuptools')
source=("https://files.pythonhosted.org/packages/source/${_pkgname::1}/$_pkgname/$_pkgname-$pkgver.tar.gz")
sha512sums=('0d091a3912a3c0a10837acb2bf1a4c42478a891b0c9cfaceb74a8e511425c63360f24cb08db138ec51a7c189f0b76f3901789edb45f894ab8926dd4c458c7ff5')

prepare() {
  cp -a "$_pkgname-$pkgver"{,-2}
}

build() {
  cd "$_pkgname-$pkgver-2"

  python2 setup.py build

  cd "$srcdir/$_pkgname-$pkgver"

  python setup.py build
}

package_python2-xlsx2csv() {
  depends=('python2')

  cd "$_pkgname-$pkgver-2"

  python2 setup.py install --prefix=/usr --root="$pkgdir" --optimize=1 \
    --skip-build

  mv "$pkgdir/usr/bin/$_pkgname" "$pkgdir/usr/bin/${_pkgname}2"
}

package_python-xlsx2csv() {
  depends=('python')

  cd "$_pkgname-$pkgver"

  python setup.py install --prefix=/usr --root="$pkgdir" --optimize=1 \
    --skip-build
}

