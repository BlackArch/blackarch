# This file is part of BlackArch Linux ( https://www.blackarch.org/ ).
# See COPYING for license details.

pkgbase=python-xlsx2csv
pkgname=('python2-xlsx2csv' 'python-xlsx2csv')
_pkgname=xlsx2csv
pkgver=0.7.7
pkgrel=2
pkgdesc='XLSX to CV converter.'
arch=('any')
url='https://pypi.org/project/xlsx2csv/#files'
license=('GPL2')
makedepends=('python2-setuptools' 'python-setuptools')
source=("https://files.pythonhosted.org/packages/ac/8c/96123b503a4d986b56278ad87aa70e1c6528d775d15380007bf1abfcf7b8/$_pkgname-$pkgver.tar.gz")
sha512sums=('0b89d37226afc1b233888e0ea7a003b1944209f45af55e1de0518ca2128985621dc715ab645c44429217bb79edf627ecd28ae1a0b985c6bacbce8fe0de41bfbf')

prepare() {
  cp -a "$_pkgname-$pkgver"{,-2}
}

build() {
  cd "$_pkgname-$pkgver-2"

  python2 setup.py build

  cd "$srcdir/$_pkgname-$pkgver"

  python setup.py build
}

package_python2-xlsx2csv() {
  depends=('python2')

  cd "$_pkgname-$pkgver-2"

  python2 setup.py install --prefix=/usr --root="$pkgdir" --optimize=1 \
    --skip-build

  mv "$pkgdir/usr/bin/$_pkgname" "$pkgdir/usr/bin/${_pkgname}2"
}

package_python-xlsx2csv() {
  depends=('python')

  cd "$_pkgname-$pkgver"

  python setup.py install --prefix=/usr --root="$pkgdir" --optimize=1 \
    --skip-build
}

