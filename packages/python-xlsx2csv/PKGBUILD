# This file is part of BlackArch Linux ( https://www.blackarch.org/ ).
# See COPYING for license details.

pkgbase=python-xlsx2csv
pkgname=('python2-xlsx2csv' 'python-xlsx2csv')
_pkgname=xlsx2csv
pkgver=0.8.6
pkgrel=1
pkgdesc='XLSX to CV converter.'
arch=('any')
url='https://pypi.org/project/xlsx2csv/#files'
license=('GPL2')
makedepends=('python2-setuptools' 'python-setuptools')
source=("https://files.pythonhosted.org/packages/source/${_pkgname::1}/$_pkgname/$_pkgname-$pkgver.tar.gz")
sha512sums=('d8d8aad85deaf75b3b18760c9a2a837142d26870866f2ae3d63ec3181447e838501ae1d017ca6e1e05a32b37401fc4dd0e938a51f75f75a7527dfa7c92a59d4f')

prepare() {
  cp -a "$_pkgname-$pkgver"{,-2}
}

build() {
  cd "$_pkgname-$pkgver-2"

  python2 setup.py build

  cd "$srcdir/$_pkgname-$pkgver"

  python setup.py build
}

package_python2-xlsx2csv() {
  depends=('python2')

  cd "$_pkgname-$pkgver-2"

  python2 setup.py install --root="$pkgdir" --prefix=/usr -O1 --skip-build

  mv "$pkgdir/usr/bin/$_pkgname" "$pkgdir/usr/bin/${_pkgname}2"
}

package_python-xlsx2csv() {
  depends=('python')

  cd "$_pkgname-$pkgver"

  python setup.py install --root="$pkgdir" --prefix=/usr -O1 --skip-build
}

