# This file is part of BlackArch Linux ( http://blackarch.org ).
# See COPYING for license details.

pkgname='shellen'
pkgver=55.2bea7c5
pkgrel=2
pkgdesc='Interactive shellcoding environment to easily craft shellcodes.'
groups=('blackarch' 'blackarch-exploitation')
arch=('any')
url='https://github.com/merrychap/shellen'
license=('MIT')
depends=('python' 'python-requests' 'python-termcolor' 'python-colorama'
         'python-pygments' 'python-terminaltables' 'python-capstone'
         'python-keystone' 'python-prompt_toolkit' 'python-keystone-engine')
makedepends=('git' 'python-setuptools')
source=('git+https://github.com/merrychap/shellen.git')
sha1sums=('SKIP')

pkgver() {
  cd "$srcdir/shellen"

  echo $(git rev-list --count HEAD).$(git rev-parse --short HEAD)
}

build() {
  cd "$srcdir/shellen"

  python setup.py build
}

package() {
  cd "$srcdir/shellen"

  python setup.py install --root="$pkgdir" --prefix=/usr

  install -Dm644 -t "$pkgdir/usr/share/doc/shellen/" README.*
  install -Dm644 -t "$pkgdir/usr/share/licenses/shellen/" LICENSE.*
}
