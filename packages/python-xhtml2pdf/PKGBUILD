# This file is part of BlackArch Linux ( https://www.blackarch.org/ ).
# See COPYING for license details.

pkgbase=python-xhtml2pdf
pkgname=('python2-xhtml2pdf' 'python-xhtml2pdf')
_pkgname=xhtml2pdf
pkgver=0.2.6
pkgrel=1
pkgdesc='PDF generator using HTML and CSS.'
arch=('any')
url='https://pypi.org/project/xhtml2pdf/#files'
license=('APACHE')
makedepends=('python2-setuptools' 'python-setuptools')
source=("https://files.pythonhosted.org/packages/92/67/e50c6fdd2af9c2fbb70b792f88e65dec5c0ccc4d0a3df4faf2db9f28971b/$_pkgname-$pkgver.tar.gz")
sha512sums=('3117ef74d0a9758efb857f47ae73299b7adc348057093d7b4e4c9b61c9c71d9a0d8a66fb3fd757f49d2135c9aa416b0a1524743adaaf2569c195329cd9e79568')

prepare() {
  cp -a "$_pkgname-$pkgver"{,-2}
}

build() {
  cd "$_pkgname-$pkgver-2"

  python2 setup.py build

  cd "$srcdir/$_pkgname-$pkgver"

  python setup.py build
}

package_python2-xhtml2pdf() {
  depends=('python2' 'python2-html5lib' 'python2-pillow6' 'python2-six'
           'python2-pypdf2' 'python2-reportlab')

  cd "$_pkgname-$pkgver-2"

  python2 setup.py install --root="$pkgdir" --prefix=/usr -O1 --skip-build

  for i in "$pkgdir/usr/bin/"* ; do
    mv $i "${i}2"
  done
}

package_python-xhtml2pdf() {
  depends=('python' 'python-html5lib' 'python-pillow' 'python-six'
           'python-pypdf2' 'python-reportlab')

  cd "$_pkgname-$pkgver"

  python setup.py install --root="$pkgdir" --prefix=/usr -O1 --skip-build
}

