# This file is part of BlackArch Linux ( https://www.blackarch.org/ ).
# See COPYING for license details.

pkgname=dwarf
pkgver=1070.d418234
pkgrel=1
pkgdesc='Full featured multi arch/os debugger built on top of PyQt5 and frida.'
groups=('blackarch' 'blackarch-binary' 'blackarch-debugger'
        'blackarch-disassembler' 'blackarch-exploitation' 'blackarch-mobile'
        'blackarch-reversing')
arch=('any')
url='https://github.com/iGio90/Dwarf'
license=('GPL3')
depends=('python-capstone' 'python-event-bus' 'python-frida' 'python-hexdump'
         'python-pyperclip' 'python-pyqt5' 'python-qdarkstyle' 'python-requests'
         'python-unicorn' )
optdepends=('python-keystone')
makedepends=('git')
source=("$pkgname::git+https://github.com/iGio90/Dwarf.git")
sha512sums=('SKIP')

pkgver() {
  cd $pkgname

  echo $(git rev-list --count HEAD).$(git rev-parse --short HEAD)
}

package() {
  cd $pkgname

  install -dm 755 "$pkgdir/usr/bin"
  install -dm 755 "$pkgdir/usr/share/$pkgname"

  cp -a * "$pkgdir/usr/share/$pkgname/"

  cat > "$pkgdir/usr/bin/$pkgname" << EOF
#!/bin/sh
cd /usr/share/$pkgname
exec python $pkgname.py "\$@"
EOF

  chmod +x "$pkgdir/usr/bin/$pkgname"
}

