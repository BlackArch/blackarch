# This file is part of BlackArch Linux ( http://blackarch.org ).
# See COPYING for license details.
pkgname='ruby-sinatra'
_gemname='sinatra'
pkgver='2.0.0'
pkgrel=1
pkgdesc="Classy web-development dressed in a DSL"
arch=('any')
url='http://www.sinatrarb.com/'
license=("MIT")
depends=('ruby' 'ruby-mustermann' 'ruby-rack' 'ruby-rack-protection' 'ruby-tilt')
options=('!emptydirs')
source=("https://rubygems.org/downloads/$_gemname-$pkgver.gem" )
noextract=("$_gemname-$pkgver.gem")
sha1sums=('bdc7077dfe08c27d50e4e33d763dc8b9921144cb' )


package() {
  local _gemdir="$(ruby -rubygems -e 'puts Gem.default_dir')"
  gem install --ignore-dependencies --no-document --no-user-install -i "$pkgdir/$_gemdir" -n "$pkgdir"/usr/bin $_gemname-$pkgver.gem 
  rm "$pkgdir/$_gemdir/cache/$_gemname-$pkgver.gem"
  install -D -m644 "$pkgdir/$_gemdir/gems/$_gemname-$pkgver/LICENSE" "$pkgdir/usr/share/licenses/$pkgname/LICENSE"

  # Preserve library and other required directories. Remove junk that gem installs.
  local _copydir=`mktemp -d -t $_gemname.XXX`
  chmod 755 $_copydir
  (cd "$pkgdir/$_gemdir/gems/$_gemname-$pkgver"/ && cp -r --parents lib $_copydir)
  rm -rf "$pkgdir/$_gemdir/gems/$_gemname-$pkgver"
  mv $_copydir "$pkgdir/$_gemdir/gems/$_gemname-$pkgver"

}
