# This file is part of BlackArch Linux ( http://blackarch.org ).
# See COPYING for license details.

pkgname='middler'
pkgver='1.0'
pkgrel=5
pkgdesc='A Man in the Middle tool to demonstrate protocol middling attacks.'
arch=('any')
url='http://code.google.com/p/middler/'
license=('GPL')
depends=('libpcap' 'python2' 'scapy' 'libdnet')
makedepends=('python2')
groups=('blackarch' 'blackarch-networking')
source=("http://middler.googlecode.com/files/middler-${pkgver}.tgz")
sha1sums=('8940d56a9fa229b7986717b9a63ae63b1d0e21d8')

prepare() {
  cd "$srcdir/TheMiddler"

  sed -i 's:middlerlib:libmiddler:' setup.py
  sed -i 's:middlerlib.plugins:libmiddler.plugins:' setup.py
  sed -i "s:'middler.plugins.support'::" setup.py
}

package() {
  cd "$srcdir/TheMiddler"

  mkdir -p "$pkgdir/usr/bin"
  mkdir -p "$pkgdir/usr/share/middler"

  cp -r * "$pkgdir/usr/share/middler"

  cat > "$pkgdir/usr/bin/middler" << EOF
#!/bin/sh
cd /usr/share/middler/
exec python2 middler.py \$@
EOF

  chmod a+x "$pkgdir/usr/bin/middler"
}
