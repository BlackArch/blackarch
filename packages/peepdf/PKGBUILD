# This file is part of BlackArch Linux ( http://blackarch.org ).
# See COPYING for license details.

pkgname=peepdf
pkgver=0.3
pkgrel=3
groups=('blackarch' 'blackarch-forensic' 'blackarch-malware')
pkgdesc='A Python tool to explore PDF files in order to find out if the file can be harmful or not.'
arch=('any')
url='http://eternal-todo.com/tools/peepdf-pdf-analysis-tool'
license=('GPL3')
depends=('python2' 'python2-jsbeautifier' 'python2-pylibemu' 'python2-pyv8')
source=("http://eternal-todo.com/files/pdf/peepdf/peepdf_${pkgver}.tar.gz")
sha512sums=('71039e7c06e7d0a85dd31a96cd7f6c37edb38246fc7201b57c8db66168b6be90fcd81a405f70417a50ab431b678d11efecf083a18455a51aea9af4f9f7e7b1f0')

prepare() {
  cd "$srcdir"

  find -name '*.py' -exec sed -i 's/env python$/env python2/' '{}' \;
}

package() {
  cd "$srcdir/peepdf_0.3"

  install -dm 755 "$pkgdir/usr/bin"
  install -dm 755 "$pkgdir/usr/share/peepdf"

  cp --no-preserve=ownership -R * "$pkgdir/usr/share/peepdf"

  cat > "$pkgdir/usr/bin/peepdf" << EOF
#!/bin/sh
exec python2 /usr/share/peepdf/peepdf.py "\${@}"
EOF

  chmod +x "$pkgdir/usr/bin/peepdf"
}
