# This file is part of BlackArch Linux ( http://blackarch.org ).
# See COPYING for license details.

pkgname='peepdf'
pkgver='0.3'
pkgrel=1
groups=('blackarch' 'blackarch-forensic' 'blackarch-malware')
pkgdesc="A Python tool to explore PDF files in order to find out if the file can be harmful or not"
arch=('any')
url='http://eternal-todo.com/tools/peepdf-pdf-analysis-tool'
license=('GPL3')
depends=('python2')
source=("http://eternal-todo.com/files/pdf/peepdf/peepdf_$pkgver.tar.gz")
sha1sums=('f2ac65fde10a44d2b561a6962674c8923d856db0')

prepare() {
  cd "$srcdir"

  find -name '*.py' -exec sed -i 's/env python$/env python2/' '{}' \;
}

package() {
  cd "$srcdir/peepdf_0.3"

  # Base directories.
  install -dm755 "$pkgdir/usr/share/peepdf"
  install -dm755 "$pkgdir/usr/bin"

  # Entire program.
  cp --no-preserve=ownership -R * "$pkgdir/usr/share/peepdf"

  # Joint script.
  cat > "$pkgdir/usr/bin/peepdf" <<EOF
#!/bin/sh
exec python2 /usr/share/peepdf/peepdf.py "\$@"
EOF
  chmod +x "$pkgdir/usr/bin/peepdf"
}
