pkgname=peepdf
pkgver=0.2
pkgrel=3
pkgdesc="A Python tool to explore PDF files in order to find out if the file can be harmful or not"
arch=('any')
groups=('blackarch-forensics' 'blackarch')
url='http://code.google.com/p/peepdf/'
license=('GPL3')
makedepends=('subversion')
depends=('python2')
source=('svn+http://peepdf.googlecode.com/svn/trunk/')
md5sums=('SKIP')

prepare() {
  cd "$srcdir/trunk"

  find -name '*.py' -exec sed -i 's/env python$/env python2/' '{}' \;
}

package() {
  cd "$srcdir/trunk"

  # Base directories.
  install -dm755 "$pkgdir/usr/share/peepdf"
  install -dm755 "$pkgdir/usr/bin"

  # Entire program.
  cp --no-preserve=ownership -R * "$pkgdir/usr/share/peepdf"

  # Joint script.
  cat > "$pkgdir/usr/bin/peepdf" <<EOF
#!/bin/sh
cd /usr/share/peepdf
python2 peepdf.py "\$@"
EOF
  chmod +x "$pkgdir/usr/bin/peepdf"
}
