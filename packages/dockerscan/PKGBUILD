# This file is part of BlackArch Linux ( http://blackarch.org ).
# See COPYING for license details.

pkgname=dockerscan
pkgver=51.c93e277
pkgrel=3
pkgdesc='Docker security analysis & hacking tools.'
groups=('blackarch' 'blackarch-scanner')
arch=('any')
url='https://github.com/cr0hn/dockerscan'
license=('BSD')
depends=('python' 'python-requests' 'python-pytest' 'python-click'
         'python-python-dxf' 'python-colorlog' 'python-booby-ng')
makedepends=('git' 'python-setuptools')
source=('git+https://github.com/cr0hn/dockerscan.git')
sha512sums=('SKIP')

pkgver() {
  cd "$srcdir/dockerscan"

  echo $(git rev-list --count HEAD).$(git rev-parse --short HEAD)
}

prepare() {
  cd "$srcdir/dockerscan"

  sed -i 's/==.*//g' requirements.txt
}

build() {
  cd "$srcdir/dockerscan"

  python setup.py build
}

package() {
  cd "$srcdir/dockerscan"

  python setup.py install --root="$pkgdir" --prefix=/usr

  install -Dm644 -t "$pkgdir/usr/share/doc/dockerscan/" README.rst CHANGELOG
  install -Dm644 LICENSE "$pkgdir/usr/share/licenses/dockerscan/LICENSE"
}
