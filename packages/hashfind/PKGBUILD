# This file is part of BlackArch Linux ( https://www.blackarch.org/ ).
# See COPYING for license details.

pkgname=hashfind
pkgver=8.e9a9a14
pkgrel=2
groups=('blackarch' 'blackarch-crypto')
pkgdesc='A tool to search files for matching password hash types and other interesting data.'
arch=('any')
url='https://github.com/rurapenthe/hashfind'
license=('GPL2')
depends=('python2')
makedepends=('git')
source=("git+https://github.com/rurapenthe/$pkgname.git")
sha512sums=('SKIP')

pkgver() {
  cd $pkgname

  echo $(git rev-list --count HEAD).$(git rev-parse --short HEAD)
}

prepare() {
  cd $pkgname

  sed -i 's|/usr/bin/env python|/usr/bin/env python2|' "$pkgname.py"
}

package() {
  cd $pkgname

  install -Dm 755 "$pkgname.py" "$pkgdir/usr/bin/$pkgname"
  install -Dm 644 -t "$pkgdir/usr/share/doc/$pkgname/" doc/* README.md
  install -Dm 644 LICENSE "$pkgdir/usr/share/licenses/$pkgname/LICENSE"
}

