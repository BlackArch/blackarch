# This file is part of BlackArch Linux ( https://www.blackarch.org/ ).
# See COPYING for license details.

pkgbase=python-mss
pkgname=('python2-mss' 'python-mss')
_pkgname=mss
pkgver=4.0.3
pkgrel=2
pkgdesc='An ultra fast cross-platform multiple screenshots module in pure python using ctypes.'
url='https://pypi.org/project/mss/#files'
arch=('any')
license=('MIT')
makedepends=('python2-setuptools' 'python-setuptools')
source=("https://files.pythonhosted.org/packages/5d/25/18d48655f5a7375930d0cb592a36099d2dfab06d648232fa53a7b563e0ef/$_pkgname-$pkgver.tar.gz")
sha512sums=('5b8f0692e8a984a22c0b3bf2f120c780d403238c5300130f341669f772ccf6c06ce8d1ced2ccaaa8c9279e5d35b132ee4ec9ce098f4f0ee177b71d4c1b492f9b')

prepare() {
  cp -a "$_pkgname-$pkgver" "$_pkgname-$pkgver-2"
}

build() {
  cd "$_pkgname-$pkgver-2"

  python2 setup.py build

  cd "$srcdir/$_pkgname-$pkgver"

  python setup.py build
}

package_python2-mss() {
  depends=('python2' 'python2-argparse')

  cd "$_pkgname-$pkgver-2"

  python2 setup.py install --prefix=/usr --root="$pkgdir" --optimize=1 \
    --skip-build

  mv "$pkgdir/usr/bin/$_pkgname" "$pkgdir/usr/bin/${_pkgname}2"
}

package_python-mss() {
  depends=('python' 'python-argparse')

  cd "$_pkgname-$pkgver"

  python setup.py install --prefix=/usr --root="$pkgdir" --optimize=1 \
    --skip-build
}

