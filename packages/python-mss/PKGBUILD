# This file is part of BlackArch Linux ( https://www.blackarch.org/ ).
# See COPYING for license details.

pkgbase=python-mss
pkgname=('python2-mss' 'python-mss')
_pkgname=mss
pkgver=9.0.2
pkgrel=1
pkgdesc='An ultra fast cross-platform multiple screenshots module in pure python using ctypes.'
url='https://pypi.org/project/mss/#files'
arch=('any')
license=('MIT')
makedepends=('python2-setuptools' 'python-setuptools')
source=("https://files.pythonhosted.org/packages/source/${_pkgname::1}/$_pkgname/$_pkgname-$pkgver.tar.gz")
sha512sums=('b4a9a6dd97810eb56d91c9ba65ac5e628c554e2cd038e830d87e583aa35cecf19cce337e61da814f10f8278017dca758c92c18220167f3660e322a7fba05488c')

prepare() {
  cp -a "$_pkgname-$pkgver"{,-2}
}

build() {
  cd "$_pkgname-$pkgver-2"

  python2 setup.py build

  cd "$srcdir/$_pkgname-$pkgver"

  python setup.py build
}

package_python2-mss() {
  depends=('python2' 'python2-argparse')

  cd "$_pkgname-$pkgver-2"

  python2 setup.py install --prefix=/usr --root="$pkgdir" --optimize=1 \
    --skip-build

  mv "$pkgdir/usr/bin/$_pkgname" "$pkgdir/usr/bin/${_pkgname}2"
}

package_python-mss() {
  depends=('python' 'python-argparse')

  cd "$_pkgname-$pkgver"

  python setup.py install --prefix=/usr --root="$pkgdir" --optimize=1 \
    --skip-build
}

