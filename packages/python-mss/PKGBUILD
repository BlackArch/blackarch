# This file is part of BlackArch Linux ( https://www.blackarch.org/ ).
# See COPYING for license details.

pkgbase=python-mss
pkgname=('python2-mss' 'python-mss')
_pkgname=mss
pkgver=5.0.0
pkgrel=1
pkgdesc='An ultra fast cross-platform multiple screenshots module in pure python using ctypes.'
url='https://pypi.org/project/mss/#files'
arch=('any')
license=('MIT')
makedepends=('python2-setuptools' 'python-setuptools')
source=("https://files.pythonhosted.org/packages/d9/bc/d5f30d0cc8cb1b3b4b090957dcdac8c380d557f3f2890361aeed8ac42bd3/$_pkgname-$pkgver.tar.gz")
sha512sums=('27464b31a5477dfe3926bf80b7cd83433690f249e49d2e2b2e596a3af2f2cc8bb6b2540f040d4f84bd969cb2acd60e425eafa1ca70fdb7dc1b530b0aa3c6fd0b')

prepare() {
  cp -a "$_pkgname-$pkgver"{,-2}
}

build() {
  cd "$_pkgname-$pkgver-2"

  python2 setup.py build

  cd "$srcdir/$_pkgname-$pkgver"

  python setup.py build
}

package_python2-mss() {
  depends=('python2' 'python2-argparse')

  cd "$_pkgname-$pkgver-2"

  python2 setup.py install --prefix=/usr --root="$pkgdir" --optimize=1 \
    --skip-build

  mv "$pkgdir/usr/bin/$_pkgname" "$pkgdir/usr/bin/${_pkgname}2"
}

package_python-mss() {
  depends=('python' 'python-argparse')

  cd "$_pkgname-$pkgver"

  python setup.py install --prefix=/usr --root="$pkgdir" --optimize=1 \
    --skip-build
}

