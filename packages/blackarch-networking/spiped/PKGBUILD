# This file is part of BlackArch Linux ( https://www.blackarch.org/ ).
# See COPYING for license details.

pkgname=spiped
pkgver=1.6.1
pkgrel=1
groups=('blackarch' 'blackarch-networking' 'blackarch-crypto')
pkgdesc='A utility for creating symmetrically encrypted and authenticated pipes between socket addresses.'
url='https://www.tarsnap.com/spiped.html'
arch=('x86_64' 'aarch64')
license=('custom:unknown')
makedepends=('gcc')
depends=('openssl')
source=("https://www.tarsnap.com/$pkgname/$pkgname-$pkgver.tgz")
sha512sums=('ec69cb49a19e4e5705526ec74ee683ac3923412bc5ca3a45fc8268699367421cd37354ccc03c18c2d48f06cdb8a4c4bce3ef3b60934997c6770547e02e6f66ae')

build() {
  cd "$pkgname-$pkgver"

  make
}

package() {
  cd "$pkgname-$pkgver"

  install -dm 755 "$pkgdir/usr/bin"
  install -dm 755 "$pkgdir/usr/share/man/man1"

  install -Dm 644 COPYRIGHT "$pkgdir/usr/share/licenses/$pkgname/COPYRIGHT"
  install -Dm 644 -t "$pkgdir/usr/share/doc/$pkgname/" BUILDING CHANGELOG \
    README.md STYLE

  make BINDIR="$pkgdir/usr/bin" MAN1DIR="$pkgdir/usr/share/man/man1/" install
}

