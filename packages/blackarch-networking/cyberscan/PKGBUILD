# This file is part of BlackArch Linux ( https://www.blackarch.org/ ).
# See COPYING for license details.

pkgname=cyberscan
pkgver=75.ca85794
pkgrel=1
pkgdesc='A Network Pentesting Tool'
groups=('blackarch' 'blackarch-networking' 'blackarch-scanner')
arch=('any')
url='https://github.com/medbenali/CyberScan'
license=('GPL3')
depends=('python2' 'python2-pygeoip' 'python2-argparse' 'python2-asn1crypto'
         'python2-scapy')
makedepends=('git')
source=("$pkgname::git+https://github.com/medbenali/CyberScan.git")
sha512sums=('SKIP')

pkgver() {
  cd $pkgname

  echo $(git rev-list --count HEAD).$(git rev-parse --short HEAD)
}

package() {
  cd $pkgname

  install -dm 755 "$pkgdir/usr/bin"
  install -dm 755 "$pkgdir/usr/share/$pkgname"

  install -Dm 644 -t "$pkgdir/usr/share/doc/$pkgname/" README.md
  install -Dm 644 LICENSE "$pkgdir/usr/share/licenses/$pkgname/LICENSE"

  rm README.md LICENSE

  cp -a * "$pkgdir/usr/share/$pkgname/"

  cat > "$pkgdir/usr/bin/$pkgname" << EOF
#!/bin/sh
cd /usr/share/$pkgname
exec python2 CyberScan.py "\$@"
EOF

  chmod +x "$pkgdir/usr/bin/$pkgname"
}

