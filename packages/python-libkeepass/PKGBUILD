# This file is part of BlackArch Linux ( https://www.blackarch.org/ ).
# See COPYING for license details.

pkgbase=python2-libkeepass
pkgname=('python2-libkeepass' 'python-libkeepass')
_pkgname=libkeepass
pkgver=0.3.1
pkgrel=1
pkgdesc='A library to access KeePass 1.x/KeePassX (v3) and KeePass 2.x (v4) files.'
arch=('any')
url='https://pypi.org/project/libkeepass/'
license=('GPL')
makedepends=('python2-setuptools' 'python-setuptools')
source=("https://files.pythonhosted.org/packages/30/dc/4380fbee97052b202dd20576c1ce788b6d66fb8313ae6a59e1f62d3c4282/$_pkgname-$pkgver.tar.gz")
sha512sums=('697bb3f27e78e35293d08d14b4d30f44efd1a5101d3d1aace0c6d3d2042d1ae39c62df1cd2f978e6f6c36079d35098ce21e73d22e70c70eabb0148b1ba8d7a97')

prepare() {
  cp -a "$_pkgname-$pkgver"{,-2}
}

build() {
  cd "$_pkgname-$pkgver-2"

  python2 setup.py build

  cd "$srcdir/$_pkgname-$pkgver"

  python setup.py build
}

package_python2-libkeepass() {
  depends=('python2' 'python2-lxml' 'python2-pycryptodome' 'python2-colorama')

  cd "$_pkgname-$pkgver-2"

  python2 setup.py install --prefix=/usr --root="$pkgdir" --optimize=1 \
    --skip-build
}

package_python-libkeepass() {
  depends=('python' 'python-lxml' 'python-pycryptodome' 'python-colorama')

  cd "$_pkgname-$pkgver"

  python setup.py install --prefix=/usr --root="$pkgdir" --optimize=1 \
    --skip-build
}

