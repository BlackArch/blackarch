# This file is part of BlackArch Linux ( http://blackarch.org ).
# See COPYING for license details.

pkgname='smbspider'
pkgver=10.7db9323
pkgrel=3
groups=('blackarch' 'blackarch-scanner')
pkgdesc='A lightweight python utility for searching SMB/CIFS/Samba file shares.'
arch=('any')
url='https://github.com/T-S-A/smbspider'
license=('GPL2')
depends=('python2' 'python2-netaddr' 'python2-pysmbc' 'python2-pysmb')
makedepends=('git')
source=('git+https://github.com/T-S-A/smbspider.git')
sha1sums=('SKIP')

pkgver() {
  cd "$srcdir/smbspider"

  echo $(git rev-list --count HEAD).$(git rev-parse --short HEAD)
}

prepare() {
  cd "$srcdir/smbspider"

  sed -i 's|/usr/bin/python|/usr/bin/python2|' smbspider.py
}

package() {
  cd "$srcdir/smbspider"

  install -Dm755 smbspider.py "$pkgdir/usr/bin/smbspider"
  install -Dm644 README.md "$pkgdir/usr/share/doc/smbspider/README.md"
  install -Dm644 LICENSE "$pkgdir/usr/share/licenses/smbspider/LICENSE"
}
