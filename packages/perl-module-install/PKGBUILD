# This file is part of BlackArch Linux ( http://blackarch.org ).
# See COPYING for license details.

pkgname='perl-module-install'
_perlmod=Module-Install
_author='E/ET/ETHER'
pkgver=1.19
pkgrel=2
pkgdesc="Module::Install - Standalone, extensible Perl module installer"
arch=('any')
url='http://search.cpan.org/dist/Module-Install'
license=('PerlArtistic' 'GPL')
options=('!emptydirs')
depends=('perl-file-remove' 'perl-json' 'perl-module-scandeps' 'perl-par-dist'
         'perl-cpan-meta' 'perl-yaml-tiny')
makedepends=('perl-yaml-tiny')
source=("http://search.cpan.org/CPAN/authors/id/${_author}/${_perlmod}-${pkgver}.tar.gz")
sha512sums=('68a255402c98955cfcb5a8a99555fe511b89d5fccf96ee1c498cab347c8945f3abe53485ea936f7419420d9c7beb52c861516f4cfd299812cebf80eab50fa5ba')
unset PERL5LIB PERL_MM_OPT PERL_MB_OPT PERL_LOCAL_LIB_ROOT
export PERL_MM_USE_DEFAULT=1 PERL_AUTOINSTALL=--skipdeps MODULEBUILDRC=/dev/null

build() {
  cd "$srcdir"/$_perlmod-$pkgver

  /usr/bin/perl Makefile.PL

  make
}

package() {
  cd "$srcdir"/$_perlmod-$pkgver

  make install INSTALLDIRS=perl DESTDIR="$pkgdir"
}
