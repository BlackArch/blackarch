# This file is part of BlackArch Linux ( https://www.blackarch.org/ ).
# See COPYING for license details.

pkgname=bashscan
pkgver=94.80c066c
pkgrel=1
pkgdesc='A port scanner built to utilize /dev/tcp for network and service discovery.'
groups=('blackarch' 'blackarch-scanner')
arch=('any')
url='https://github.com/astryzia/BashScan'
license=('MIT')
depends=('bash' 'curl' 'bc' 'bind' 'net-tools')
makedepends=('git')
source=("$pkgname::git+https://github.com/astryzia/BashScan.git")
sha512sums=('SKIP')

pkgver() {
  cd $pkgname

  echo $(git rev-list --count HEAD).$(git rev-parse --short HEAD)
}

package() {
  cd $pkgname

  install -dm 755 "$pkgdir/usr/bin"
  install -dm 755 "$pkgdir/usr/share/$pkgname"

  install -Dm 644 -t "$pkgdir/usr/share/doc/$pkgname/" README.md
  install -Dm 644 LICENSE "$pkgdir/usr/share/licenses/$pkgname/LICENSE"

  rm README.md LICENSE

  cp -a * "$pkgdir/usr/share/$pkgname/"

  cat > "$pkgdir/usr/bin/$pkgname" << EOF
#!/bin/sh
cd /usr/share/$pkgname
exec bash run.sh "\$@"
EOF

  chmod +x "$pkgdir/usr/bin/$pkgname"
}

