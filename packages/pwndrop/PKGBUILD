# This file is part of BlackArch Linux ( https://www.blackarch.org/ ).
# See COPYING for license details.

pkgname=pwndrop
pkgver=18.385ba70
pkgrel=2
pkgdesc='Self-deployable file hosting service for red teamers, allowing to easily upload and share payloads over HTTP and WebDAV.'
arch=('x86_64' 'aarch64')
groups=('blackarch' 'blackarch-webapp' 'blackarch-exploitation'
        'blackarch-automation')
url='https://github.com/kgretzky/pwndrop'
license=('GPL3')
makedepends=('git' 'go')
source=("git+https://github.com/kgretzky/$pkgname.git")
sha512sums=('SKIP')
install="$pkgname.install"

pkgver() {
  cd $pkgname

  echo $(git rev-list --count HEAD).$(git rev-parse --short HEAD)
}

build() {
  cd $pkgname

  GOPATH="$srcdir" go mod download
  GOPATH="$srcdir" go build \
    -trimpath \
    -buildmode=pie \
    -mod=readonly \
    -modcacherw \
    -ldflags "-s -w" \
    -o $pkgname .
}

package() {
  cd $pkgname

  install -dm 755 "$pkgdir/usr/share/$pkgname"

  install -Dm 755 $pkgname "$pkgdir/usr/bin/$pkgname"
  install -Dm 644 -t "$pkgdir/usr/share/doc/$pkgname/" README.md media/*

  cp -r --no-preserve=ownership www "$pkgdir/usr/share/$pkgname/admin"
}

