# This file is part of BlackArch Linux ( https://www.blackarch.org/ ).
# See COPYING for license details.

pkgbase=python-proxylist
pkgname=('python2-proxylist' 'python-proxylist')
_pkgname=proxylist
pkgver=0.2.1
pkgrel=2
pkgdesc='Managing list of proxies.'
arch=('any')
url='https://pypi.org/project/proxylist/#files'
license=('MIT')
makedepends=('python2-setuptools' 'python-setuptools')
source=("https://files.pythonhosted.org/packages/source/${_pkgname::1}/$_pkgname/$_pkgname-$pkgver.tar.gz")
sha512sums=('9c935e2dcc606f6f1c96a2a52befb1997409442841414950742646274c145fdad065978bcd674c0909e969298f1119aaf1248b77f6ec4a50558d656a65ee863d')

prepare() {
  cp -a "$_pkgname-$pkgver"{,-2}
}

build() {
  cd "$_pkgname-$pkgver-2"

  python2 setup.py build

  cd "$srcdir/$_pkgname-$pkgver"

  python setup.py build
}

package_python2-proxylist() {
  depends=('python2' 'python2-six')

  cd "$_pkgname-$pkgver-2"

  python2 setup.py install --prefix=/usr --root="$pkgdir" --optimize=1 \
    --skip-build
}

package_python-proxylist() {
  depends=('python' 'python-six')

  cd "$_pkgname-$pkgver"

  python setup.py install --prefix=/usr --root="$pkgdir" --optimize=1 \
    --skip-build
}

