# This file is part of BlackArch Linux ( https://www.blackarch.org/ ).
# See COPYING for license details.

pkgbase=python-proxylist
pkgname=('python2-proxylist' 'python-proxylist')
_pkgname=proxylist
pkgver=0.1.4
pkgrel=3
pkgdesc='Managing list of proxies.'
arch=('any')
url='https://pypi.org/project/proxylist/#files'
license=('MIT')
makedepends=('python2-setuptools' 'python-setuptools')
source=("https://files.pythonhosted.org/packages/93/b7/139c28b61e30fa12aedc687e6d0eb8584709588c07017b6d82c109fc8411/$_pkgname-$pkgver.tar.gz")
sha512sums=('768952e458ed3c2f060fa373f1873b11a728d6f87cc60e1fe6d63754cb8105f3ea6c0c69342b4781e404feb1727018a958770c01f87148324a1c307b8787fd14')

prepare() {
  cp -a "$_pkgname-$pkgver"{,-2}
}

build() {
  cd "$_pkgname-$pkgver-2"

  python2 setup.py build

  cd "$srcdir/$_pkgname-$pkgver"

  python setup.py build
}

package_python2-proxylist() {
  depends=('python2' 'python2-six')

  cd "$_pkgname-$pkgver-2"

  python2 setup.py install --prefix=/usr --root="$pkgdir" --optimize=1 \
    --skip-build
}

package_python-proxylist() {
  depends=('python' 'python-six')

  cd "$_pkgname-$pkgver"

  python setup.py install --prefix=/usr --root="$pkgdir" --optimize=1 \
    --skip-build
}

