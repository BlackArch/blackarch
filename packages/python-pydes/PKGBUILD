# This file is part of BlackArch Linux ( https://www.blackarch.org/ ).
# See COPYING for license details.

pkgbase=python-pydes
pkgname=('python2-pydes' 'python-pydes')
_pkgname=pyDes
pkgver=2.0.1
pkgrel=6
pkgdesc='Pure python implementation of DES and TRIPLE DES encryption algorithm.'
arch=('any')
url='http://twhiteman.netfirms.com/des.html'
license=('custom:Public Domain')
makedepends=('python2-setuptools' 'python-setuptools')
source=("http://twhiteman.netfirms.com/pyDES/$_pkgname-$pkgver.zip"
        'LICENSE')
sha512sums=('d9107d532801ff2eb096eeb606c1b81fd68a76fccffdf095a6a2a79bd19815b4c366549989b6f4508c99a2f9afb063eceb7b0702e504ebb4d73dc8175057cb28'
            '5c9755f31433e5ee540049124fc7b6bdbb575eb682966247658424d0cc842cda92c9e8bc500e96462177ebeee71db993bbf9da75e3899eee1f00e48346d6c342')

prepare() {
  cp -a "$_pkgname-$pkgver"{,-2}

  find "$_pkgname-$pkgver-2" -type f -name '*.py' |
  xargs sed -i 's|#!/usr/bin/python|#!/usr/bin/python2|'
}

build() {
  cd "$_pkgname-$pkgver-2"

  python2 setup.py build

  cd "$srcdir/$_pkgname-$pkgver"

  python setup.py build
}

package_python2-pydes() {
  cd "$_pkgname-$pkgver-2"

  python2 setup.py install --prefix=/usr --root="$pkgdir" --optimize=1 \
    --skip-build

  install -Dm 644 ../LICENSE \
    "$pkgdir/usr/share/licenses/${pkgname[0]}/LICENSE"
}

package_python-pydes() {
  cd "$_pkgname-$pkgver"

  python setup.py install --prefix=/usr --root="$pkgdir" --optimize=1 \
    --skip-build

  install -Dm 644 ../LICENSE \
    "$pkgdir/usr/share/licenses/${pkgname[1]}/LICENSE"
}

