# This file is part of BlackArch Linux ( https://www.blackarch.org/ ).
# See COPYING for license details.

pkgname=chapcrack
pkgver=17.ae2827f
pkgrel=4
groups=('blackarch' 'blackarch-cracker')
pkgdesc='A tool for parsing and decrypting MS-CHAPv2 network handshakes.'
arch=('any')
url='https://github.com/moxie0/chapcrack'
license=('GPL3')
depends=('python2' 'python2-passlib' 'python2-dpkt')
makedepends=('git' 'python2-setuptools')
source=("git+https://github.com/moxie0/$pkgname.git")
sha512sums=('SKIP')

pkgver() {
  cd $pkgname

  echo $(git rev-list --count HEAD).$(git rev-parse --short HEAD)
}

build() {
  cd $pkgname

  python2 setup.py build
}

package() {
  cd $pkgname

  python2 setup.py install --root="$pkgdir" --prefix=/usr -O1 --skip-build

  install -Dm 644 -t "$pkgdir/usr/share/doc/$pkgname/" README.md
}

