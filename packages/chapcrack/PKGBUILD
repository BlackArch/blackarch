# This file is part of BlackArch Linux ( http://blackarch.org ).
# See COPYING for license details.

pkgname='chapcrack'
pkgver=17.ae2827f
pkgrel=4
groups=('blackarch' 'blackarch-cracker')
pkgdesc='A tool for parsing and decrypting MS-CHAPv2 network handshakes.'
arch=('any')
url='https://github.com/moxie0/chapcrack'
license=('GPL3')
depends=('python2' 'python2-passlib' 'python2-dpkt')
makedepends=('git' 'python2-setuptools')
source=('git+https://github.com/moxie0/chapcrack.git')
sha1sums=('SKIP')

pkgver() {
  cd "$srcdir/chapcrack"

  echo $(git rev-list --count HEAD).$(git rev-parse --short HEAD)
}

build() {
  cd "$srcdir/chapcrack"

  python2 setup.py build
}

package() {
  cd "$srcdir/chapcrack"

  python2 setup.py install --root="$pkgdir" --optimize=1

  install -Dm644 README.md "$pkgdir/usr/share/doc/chapcrack/README.md"
}
