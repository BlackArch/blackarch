# This file is part of BlackArch Linux ( https://www.blackarch.org/ ).
# See COPYING for license details.

pkgname=gloom
pkgver=93.cd6e927
pkgrel=1
epoch=1
pkgdesc='Linux Penetration Testing Framework.'
groups=('blackarch' 'blackarch-scanner' 'blackarch-exploitation'
        'blackarch-recon' 'blackarch-fuzzer' 'blackarch-social')
arch=('any')
url='https://github.com/StreetSec/Gloom-Framework'
license=('GPL3')
depends=('python2' 'python2-mechanize' 'python2-pythonwhois' 'python2-requests'
         'python2-termcolor' 'python2-dateutil' 'python2-beautifulsoup4'
         'python2-geoip' 'python2-scapy' 'wifijammer' 'nmap' 'curl')
makedepends=('git')
source=("$pkgname::git+https://github.com/StreetSec/Gloom-Framework.git")
sha512sums=('SKIP')

pkgver() {
  cd $pkgname

  echo $(git rev-list --count HEAD).$(git rev-parse --short HEAD)
}

package() {
  cd $pkgname

  install -dm 755 "$pkgdir/usr/bin"
  install -dm 755 "$pkgdir/usr/share/$pkgname"

  install -Dm 644 -t "$pkgdir/usr/share/doc/$pkgname/" README.md
  install -Dm 644 LICENSE "$pkgdir/usr/share/licenses/$pkgname/LICENSE"

  rm README.md LICENSE

  cp -a * "$pkgdir/usr/share/$pkgname/"

  cat > "$pkgdir/usr/bin/$pkgname" << EOF
#!/bin/sh
cd /usr/share/$pkgname
exec python2 $pkgname.py "\$@"
EOF

  chmod +x "$pkgdir/usr/bin/$pkgname"
}

