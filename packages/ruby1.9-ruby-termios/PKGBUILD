# This file is part of BlackArch Linux ( http://blackarch.org ).
# See COPYING for license details.

_gemname='ruby-termios'
pkgname="ruby1.9-$_gemname"
pkgver='1.0.2'
pkgrel=1
pkgdesc='A simple wrapper of termios(3).'
arch=('x86_64' 'armv6h' 'armv7h' 'aarch64')
url='http://arika.org/ruby/termios'
license=('custom')
depends=('ruby1.9')
options=(!emptydirs)
source=("https://rubygems.org/downloads/${_gemname}-${pkgver}.gem"
        'https://www.ruby-lang.org/en/about/license.txt')
noextract=("${_gemname}-${pkgver}.gem")
sha1sums=('8925ab4fd8d7c96a4ce398f0817f82ec2b0d9ff1'
          'df734091d6dbc9d20382b4084e68396a483582bd')

package() {
  cd "$srcdir"

  local _gemdir="$(ruby-1.9 -e'puts Gem.default_dir')"

  gem-1.9 install --ignore-dependencies --no-user-install \
    -i "$pkgdir/$_gemdir" -n "$pkgdir/usr/bin" "${_gemname}-${pkgver}.gem"

  rm "$pkgdir/$_gemdir/cache/${_gemname}-${pkgver}.gem"

  install -D -m644 "$srcdir/license.txt" \
    "$pkgdir/usr/share/licenses/ruby1.9-ruby-termios/license.txt"
}
