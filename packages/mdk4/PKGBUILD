# This file is part of BlackArch Linux ( http://blackarch.org ).
# See COPYING for license details.

pkgname=mdk4
pkgver=29.002f64a
pkgrel=1
groups=('blackarch' 'blackarch-fuzzer' 'blackarch-wireless')
pkgdesc="WLAN penetration tool version 4"
arch=('any')
license=('GPL3')
url='https://github.com/aircrack-ng/mdk4'
depends=('glibc')
makedepends=('git' 'make')
source=("git+https://github.com/aircrack-ng/$pkgname.git"
        "Makefile.patch")
sha512sums=('SKIP'
            'ed63cf6ae4d20ddd4adddc9d9ba2170bde08d17cbace4072bbd732b7fb8461bfa1388e04e689eb290cabd50b72f22c75db49eebedacff6c64174d71bf077dc8c')

prepare() {
  cd $pkgname

  patch < "$srcdir/Makefile.patch"
  sed -i "s|-g -O3|$CFLAGS|g" Makefile

  sed 's|sbin|bin|g' -i "src/Makefile"
}

build() {
  cd "$pkgname"

  make
}

package() {
  cd "$pkgname"

  make DESTDIR="$pkgdir" install
}

