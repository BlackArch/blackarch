# This file is part of BlackArch Linux ( http://blackarch.org ).
# See COPYING for license details.

pkgname=slackpirate
pkgver=0.20.r6.g99e470f
pkgrel=1
epoch=1
pkgdesc='Slack Enumeration and Extraction Tool - extract sensitive information from a Slack Workspace.'
arch=('any')
groups=('blackarch' 'blackarch-social' 'blackarch-recon')
url='https://github.com/emtunc/SlackPirate'
license=('GPL-3.0-or-later')
depends=('python-requests' 'python-colorama' 'python-termcolor')
makedepends=('git')
source=("$pkgname::git+https://github.com/emtunc/SlackPirate.git")
sha512sums=('SKIP')

pkgver() {
  cd $pkgname

  ( set -o pipefail
    git describe --long --tags --abbrev=7 2>/dev/null |
      sed 's/\([^-]*-g\)/r\1/;s/-/./g' ||
    printf "%s.%s" "$(git rev-list --count HEAD)" \
      "$(git rev-parse --short=7 HEAD)"
  )
}

package() {
  cd $pkgname

  install -dm 755 "$pkgdir/usr/bin"
  install -dm 755 "$pkgdir/usr/share/$pkgname"

  install -Dm 644 -t "$pkgdir/usr/share/doc/$pkgname/" README.md
  install -Dm 644 LICENSE "$pkgdir/usr/share/licenses/$pkgname/LICENSE"

  cp -a --no-preserve=ownership * "$pkgdir/usr/share/$pkgname/"

  cat > "$pkgdir/usr/bin/$pkgname" << EOF
#!/bin/sh
cd /usr/share/$pkgname
exec python SlackPirate.py "\$@"
EOF

  chmod +x "$pkgdir/usr/bin/$pkgname"
}

