# This file is part of BlackArch Linux ( http://blackarch.org ).
# See COPYING for license details.

pkgname=recuperabit
pkgver=38.3a7c133
pkgrel=1
pkgdesc='A tool for forensic file system reconstruction.'
groups=('blackarch' 'blackarch-forensic')
arch=('any')
url='https://github.com/Lazza/RecuperaBit'
license=('GPL3')
depends=('python2')
makedepends=('git')
source=('git+https://github.com/Lazza/RecuperaBit.git')
sha512sums=('SKIP')

pkgver() {
  cd "$srcdir/RecuperaBit"

  echo $(git rev-list --count HEAD).$(git rev-parse --short HEAD)
}

package() {
  cd "$srcdir/RecuperaBit"

  install -dm 755 "$pkgdir/usr/bin"
  install -dm 755 "$pkgdir/usr/share/recuperabit"

  install -Dm644 -t "$pkgdir/usr/share/doc/recuperabit/" README.md
  install -Dm644 LICENSE "$pkgdir/usr/share/licenses/recuperabit/LICENSE"

  rm README.md LICENSE

  cp -a * "$pkgdir/usr/share/recuperabit"

  cat > "$pkgdir/usr/bin/recuperabit" << EOF
#!/bin/sh
cd /usr/share/recuperabit
exec python2 main.py "\${@}"
EOF

  chmod a+x "$pkgdir/usr/bin/recuperabit"
}
