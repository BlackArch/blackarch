# This file is part of BlackArch Linux ( https://www.blackarch.org/ ).
# See COPYING for license details.
#
# taken from AUR, adapted to our style.
#
# Old Contributor: Ivan Shapovalov <intelfx@intelfx.name>
# Old Contributor: Alexander F. RÃ¸dseth <xyproto@archlinux.org>
# Old Contributor: fanningert <thomas@fanninger.at>

pkgname=glide
pkgver=0.13.3
pkgrel=1
pkgdesc='Vendor Package Management for Golang.'
arch=('x86_64' 'aarch64')
url='https://github.com/Masterminds/glide'
license=('MIT')
makedepends=('git' 'go-pie')
options=('!strip' '!emptydirs')
_commit=8ed5b9292379d86c39592a7e6a58eb9c903877cf  # tags/v0.13.3^0
source=("git+$url#tag=v$pkgver")
sha512sums=('SKIP')

prepare() {
  export GOPATH="$srcdir/go"
  _gosrc="$GOPATH/src/${url#https://}"

  rm -rf "$GOPATH"
  mkdir -p "${_gosrc%/*}"
  mv -T glide "$_gosrc"
}

build() {
  export GOPATH="$srcdir/go"
  cd "$GOPATH/src/${url#https://}"

  go build -o $pkgname -ldflags "-X main.version=$pkgver" "$pkgname.go"
}

package() {
  export GOPATH="$srcdir/go"
  cd "$GOPATH/src/${url#https://}"

  install -Dm 755 $pkgname "$pkgdir/usr/bin/$pkgname"
  install -Dm 644 -t "$pkgdir/usr/share/doc/$pkgname/" *.md
  install -Dm 644 LICENSE "$pkgdir/usr/share/licenses/$pkgname/LICENSE"
}

