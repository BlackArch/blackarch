# This file is part of BlackArch Linux ( https://www.blackarch.org/ ).
# See COPYING for license details.

pkgname=vulnx
pkgver=312.91fb370
pkgrel=2
pkgdesc='Cms and vulnerabilites detector & An intelligent bot auto shell injector.'
groups=('blackarch' 'blackarch-webapp' 'blackarch-scanner'
        'blackarch-fingerprint' 'blackarch-recon')
arch=('any')
url='https://github.com/anouarbensaad/vulnx'
license=('MIT')
depends=('python' 'python-argparse' 'python-requests' 'python-beautifulsoup4')
makedepends=('git')
source=("git+https://github.com/anouarbensaad/$pkgname.git")
sha512sums=('SKIP')

pkgver() {
  cd $pkgname

  echo $(git rev-list --count HEAD).$(git rev-parse --short HEAD)
}

package() {
  cd $pkgname

  install -dm 755 "$pkgdir/usr/bin"
  install -dm 755 "$pkgdir/usr/share/$pkgname"

  install -Dm 644 -t "$pkgdir/usr/share/doc/$pkgname/" *.md
  install -Dm 644 LICENSE "$pkgdir/usr/share/licenses/$pkgname/LICENSE"

  rm *.md LICENSE

  cp -a * "$pkgdir/usr/share/$pkgname/"

  cat > "$pkgdir/usr/bin/$pkgname" << EOF
#!/bin/sh
cd /usr/share/$pkgname
exec python $pkgname.py "\$@"
EOF

  chmod +x "$pkgdir/usr/bin/$pkgname"
}

