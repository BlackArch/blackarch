# This file is part of BlackArch Linux ( http://blackarch.org ).
# See COPYING for license details.

pkgname='perl-cpan-meta'
pkgver='2.150001'
pkgrel=1
pkgdesc='The distribution metadata for a CPAN dist.'
arch=('any')
license=('PerlArtistic' 'GPL')
options=('!emptydirs')
depends=('perl' 'perl-cpan-meta-requirements' 'perl-parse-cpan-meta>=1.4403')
url='http://search.cpan.org/dist/CPAN-Meta'
source=("http://search.cpan.org/CPAN/authors/id/D/DA/DAGOLDEN/CPAN-Meta-${pkgver}.tar.gz")
sha1sums=('5ae263253a4e535472861fbf74bd48579cd6ccd2')

prepare_environment() {
  cd "$srcdir/CPAN-Meta-$pkgver"

  export PERL_MM_USE_DEFAULT=1 PERL5LIB=""                 \
    PERL_AUTOINSTALL=--skipdeps                            \
    PERL_MM_OPT="INSTALLDIRS=vendor DESTDIR='$pkgdir'"     \
    PERL_MB_OPT="--installdirs vendor --destdir '$pkgdir'" \
    MODULEBUILDRC=/dev/null
}

build() {
  cd "$srcdir"

  prepare_environment

  /usr/bin/perl Makefile.PL

  make
}

package() {
  cd "$srcdir"

  prepare_environment

  make install

  find "$pkgdir" -name .packlist -o -name perllocal.pod -delete
}
