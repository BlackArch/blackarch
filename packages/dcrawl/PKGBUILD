# This file is part of BlackArch Linux ( https://www.blackarch.org/ ).
# See COPYING for license details.

pkgname=dcrawl
pkgver=7.3273c35
pkgrel=3
pkgdesc='Simple, but smart, multi-threaded web crawler for randomly gathering huge lists of unique domain names.'
groups=('blackarch' 'blackarch-scanner' 'blackarch-webapp')
arch=('x86_64' 'aarch64')
url='https://github.com/kgretzky/dcrawl'
license=('MIT')
depends=('glibc')
makedepends=('git' 'go')
source=("git+https://github.com/kgretzky/$pkgname.git")
sha512sums=('SKIP')

pkgver() {
  cd $pkgname

  echo $(git rev-list --count HEAD).$(git rev-parse --short HEAD)
}

build() {
  cd $pkgname

  GOPATH="$srcdir" go mod init go.mod
  GOPATH="$srcdir" go mod tidy
  GOPATH="$srcdir" go mod download
  GOPATH="$srcdir" go build \
    -trimpath \
    -buildmode=pie \
    -mod=readonly \
    -modcacherw \
    -ldflags "-s -w" \
    -o $pkgname $pkgname.go
}

package() {
  cd $pkgname

  install -Dm 755 $pkgname "$pkgdir/usr/bin/$pkgname"
  install -Dm 644 -t "$pkgdir/usr/share/doc/$pkgname/" README.md
  install -Dm 644 LICENSE "$pkgdir/usr/share/licenses/$pkgname/LICENSE"
}

