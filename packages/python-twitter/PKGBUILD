# This file is part of BlackArch Linux ( https://www.blackarch.org/ ).
# See COPYING for license details.

pkgbase=python-twitter
pkgname=('python2-twitter' 'python-twitter')
_pkgname=twitter
pkgver=1.18.0
pkgrel=3
pkgdesc='Python Twitter API.'
arch=('any')
url='https://pypi.org/project/twitter/#files'
license=('MIT')
makedepends=('python2-setuptools' 'python-setuptools')
source=("https://files.pythonhosted.org/packages/8a/9d/cea0ec784ba05d56fbd8a56a674ca12d9b012487528ce91e0064b19224f7/$_pkgname-$pkgver.tar.gz")
sha512sums=('dda752b194efe88df88a2c9e2279cd3045a36c466f05493727a73aaaa74ca99873c1dc1742491f2f4092eff5afb9ad7680a779f16041d9c3dae7c6eea013ea48')

prepare() {
  cp -a "$_pkgname-$pkgver"{,-2}
}

build() {
  cd "$_pkgname-$pkgver-2"

  python2 setup.py build

  cd "$srcdir/$_pkgname-$pkgver"

  python setup.py build
}

package_python2-twitter() {
  depends=('python2' 'python2-argparse')

  cd "$_pkgname-$pkgver-2"

  python2 setup.py install --prefix=/usr --root="$pkgdir" --optimize=1 \
    --skip-build

  for i in "$pkgdir/usr/bin/"* ; do
    mv $i "${i}2"
  done
}

package_python-twitter() {
  depends=('python' 'python-argparse')

  cd "$_pkgname-$pkgver"

  python setup.py install --prefix=/usr --root="$pkgdir" --optimize=1 \
	--skip-build
}

