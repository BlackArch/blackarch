# This file is part of BlackArch Linux ( https://www.blackarch.org/ ).
# See COPYING for license details.

pkgbase=python-twitter
pkgname=('python2-twitter' 'python-twitter')
_pkgname=twitter
pkgver=1.19.3
pkgrel=2
pkgdesc='Python Twitter API.'
arch=('any')
url='https://pypi.org/project/twitter/#files'
license=('MIT')
makedepends=('python2-setuptools' 'python-setuptools')
source=("https://files.pythonhosted.org/packages/39/ae/1df3708172d04371b1a3efd9d1d790ec4c5eef6da548a9ef85d86f216ab1/$_pkgname-$pkgver.tar.gz")
sha512sums=('fe3cdb9f67c0060a89c7ec9ccaa767e6e0648ff25a1b7ff790d6bab0916dda5edc152dbdbdef40411699bd3c900e75f2c99308a99f3c69b10e90135466d64009')

prepare() {
  cp -a "$_pkgname-$pkgver"{,-2}
}

build() {
  cd "$_pkgname-$pkgver-2"

  python2 setup.py build

  cd "$srcdir/$_pkgname-$pkgver"

  python setup.py build
}

package_python2-twitter() {
  depends=('python2' 'python2-argparse')

  cd "$_pkgname-$pkgver-2"

  python2 setup.py install --prefix=/usr --root="$pkgdir" --optimize=1 \
    --skip-build

  for i in "$pkgdir/usr/bin/"* ; do
    mv $i "${i}2"
  done
}

package_python-twitter() {
  depends=('python' 'python-argparse')

  cd "$_pkgname-$pkgver"

  python setup.py install --prefix=/usr --root="$pkgdir" --optimize=1 \
	--skip-build
}

