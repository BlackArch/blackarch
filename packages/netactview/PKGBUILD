# This file is part of BlackArch Linux ( http://blackarch.org ).
# See COPYING for license details.

pkgname='netactview'
pkgver='0.6.4'
pkgrel=1
pkgdesc='A graphical network connections viewer for Linux similar in functionality with Netstat.'
groups=('blackarch' 'blackarch-misc' 'blackarch-networking')
arch=('x86_64' 'armv6h' 'armv7h' 'aarch64')
url='http://netactview.sourceforge.net/index.html'
license=('GPL')
depends=('libgnome' 'libglade' 'gksu' 'perl-xml-parser')
makedepends=('intltool')
source=("http://downloads.sourceforge.net/netactview/netactview-${pkgver}.tar.bz2")
sha1sums=('82e04a9263dfe55b98e6cd87b411927ef1f6fb5c')

build() {
  cd "$srcdir/netactview-$pkgver"

  if [[ "$CARCH" == "i686" || "$CARCH" == "x86_64" ]];
  then
    ./configure --prefix=/usr --mandir=/usr/share/man
  else
    ./configure --build=arm --prefix=/usr --mandir=/usr/share/man
  fi

  make
}

package(){
  cd "$srcdir/netactview-$pkgver"

  make DESTDIR="$pkgdir" install
}
