# This file is part of BlackArch Linux ( https://www.blackarch.org/ ).
# See COPYING for license details.

pkgbase=python-colorlog
pkgname=('python2-colorlog' 'python-colorlog')
_pkgname=colorlog
pkgver=4.0.2
pkgrel=2
epoch=1
pkgdesc='Log formatting with colors.'
arch=('any')
url='https://pypi.org/project/colorlog/#files'
license=('BSD')
makedepends=('python2-setuptools' 'python-setuptools')
source=("https://files.pythonhosted.org/packages/fc/30/6ba1282b773e9f44d9cfaafa38b6cc180441307c5fe0edd8db13a8903e3f/$_pkgname-$pkgver.tar.gz")
sha512sums=('dc2b641bc4376204ca66ce0cc548c6cc8ee243ebfba84e2be84b07bf6021a92a85ce175128c069f35836abf601240fc59fbc4047af746518f8485891f5a2584c')

prepare() {
  cp -a "$_pkgname-$pkgver" "$_pkgname-$pkgver-2"
}

build() {
  cd "$_pkgname-$pkgver-2"

  python2 setup.py build

  cd "$srcdir/$_pkgname-$pkgver"

  python setup.py build
}

package_python2-colorlog() {
  depends=('python2')

  cd "$_pkgname-$pkgver-2"

  python2 setup.py install --prefix=/usr --root="$pkgdir" --optimize=1 \
    --skip-build
}

package_python-colorlog() {
  depends=('python')

  cd "$_pkgname-$pkgver"

  python setup.py install --prefix=/usr --root="$pkgdir" --optimize=1 \
    --skip-build
}

