# This file is part of BlackArch Linux ( https://www.blackarch.org/ ).
# See COPYING for license details.

pkgbase=python-colorlog
pkgname=('python2-colorlog' 'python-colorlog')
_pkgname=colorlog
pkgver=4.1.0
pkgrel=1
epoch=1
pkgdesc='Log formatting with colors.'
arch=('any')
url='https://pypi.org/project/colorlog/#files'
license=('BSD')
makedepends=('python2-setuptools' 'python-setuptools')
source=("https://files.pythonhosted.org/packages/a5/51/c6e1f2c7e6d7524b580d5a8d7691fd4530f894ae8a23ba66a065291ceba2/$_pkgname-$pkgver.tar.gz")
sha512sums=('af9a84355e6122a59e9176b0d30bb444bdebf3a54d25e71ae61698f83ef24f42594220ee1fae9c3cfeca1fc442871b4f1a62dcedb5bff5ce9f528e3433f80500')

prepare() {
  cp -a "$_pkgname-$pkgver"{,-2}
}

build() {
  cd "$_pkgname-$pkgver-2"

  python2 setup.py build

  cd "$srcdir/$_pkgname-$pkgver"

  python setup.py build
}

package_python2-colorlog() {
  depends=('python2')

  cd "$_pkgname-$pkgver-2"

  python2 setup.py install --prefix=/usr --root="$pkgdir" --optimize=1 \
    --skip-build
}

package_python-colorlog() {
  depends=('python')

  cd "$_pkgname-$pkgver"

  python setup.py install --prefix=/usr --root="$pkgdir" --optimize=1 \
    --skip-build
}

