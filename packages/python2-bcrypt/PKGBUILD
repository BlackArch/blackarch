# This file is part of BlackArch Linux ( https://www.blackarch.org/ ).
# See COPYING for license details.

pkgname=python2-bcrypt
_pkgname=bcrypt
pkgver=3.1.7
pkgrel=5
pkgdesc='Modern password hashing for your software and your servers.'
url='https://pypi.org/project/bcrypt/#files'
arch=('x86_64' 'aarch64')
license=('Apache')
depends=('python2' 'python2-six' 'python2-cffi')
makedepends=('python2-setuptools')
source=("https://files.pythonhosted.org/packages/fa/aa/025a3ab62469b5167bc397837c9ffc486c42a97ef12ceaa6699d8f5a5416/$_pkgname-$pkgver.tar.gz")
sha512sums=('bb9a431c8de97d5615d1a7fb67f868d08d839313fd5f3e17d8db40db1870f8b624594352c24c163c6a03485217de873cd92578040350678888728885c7b749de')

build() {
  cd "$_pkgname-$pkgver"

  python2 setup.py build
}

package() {
  cd "$_pkgname-$pkgver"

  python2 setup.py install --root="$pkgdir" --prefix=/usr -O1 --skip-build
}

