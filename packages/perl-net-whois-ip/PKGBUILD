pkgname=perl-net-whois-ip
pkgver=1.10
pkgrel=2
pkgdesc="Perl extension for looking up the whois information for ip addresses"
arch=('any')
url='http://search.cpan.org/dist/Net-Whois-IP'
license=('GPL' 'PerlArtistic')
depends=('perl')
options=('!emptydirs')
source=(http://search.cpan.org/CPAN/authors/id/B/BS/BSCHMITZ/Net-Whois-IP-$pkgver.tar.gz)
md5sums=('67edb62a49801464e0b4c6049cfbdfc4')

build() {
  cd  ${srcdir}/Net-Whois-IP-${pkgver}
  export PERL_MM_USE_DEFAULT=1 PERL_AUTOINSTALL=--skipdeps \
    PERL_MM_OPT="INSTALLDIRS=vendor DESTDIR='$pkgdir'" \
    PERL_MB_OPT="--installdirs vendor --destdir '$pkgdir'" \
    MODULEBUILDRC=/dev/null

  perl Makefile.PL
  make
}

check() {
  cd  ${srcdir}/Net-Whois-IP-${pkgver}
  make test
}

package() {
  cd  ${srcdir}/Net-Whois-IP-${pkgver}
  make install

  find ${pkgdir} -name '.packlist' -delete
  find ${pkgdir} -name '*.pod' -delete
}
