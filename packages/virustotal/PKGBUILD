# This file is part of BlackArch Linux ( http://blackarch.org ).
# See COPYING for license details.

pkgname='virustotal'
pkgver=4.9aea023
pkgrel=3
groups=('blackarch' 'blackarch-malware')
pkgdesc='Command-line utility to automatically lookup on VirusTotal all files recursively contained in a directory.'
arch=('any')
url='https://github.com/botherder/virustotal'
license=('custom:unknown')
depends=('python2' 'python2-simplejson')
makedepends=('git')
source=('git+https://github.com/botherder/virustotal.git')
sha1sums=('SKIP')

pkgver() {
  cd "$srcdir/virustotal"

  echo $(git rev-list --count HEAD).$(git rev-parse --short HEAD)
}

prepare() {
  cd "$srcdir/virustotal"

  sed -i 's|/usr/bin/env python|/usr/bin/env python2|' vt.py
}

package() {
  cd "$srcdir/virustotal"

  install -Dm755 vt.py "$pkgdir/usr/bin/virustotal"
  install -Dm644 README.md "$pkgdir/usr/share/doc/virustotal/README.md"
}
