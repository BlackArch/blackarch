# This file is part of BlackArch Linux ( https://www.blackarch.org/ ).
# See COPYING for license details.

pkgname=gpp-decrypt
pkgver=v1.1.r2.g801b9d5
pkgrel=2
epoch=1
pkgdesc='Parse the Group Policy Preferences XML file which extracts the username and decrypts the cpassword attribute.'
arch=('any')
groups=('blackarch' 'blackarch-crypto' 'blackarch-cracker')
url='https://github.com/t0thkr1s/gpp-decrypt'
license=('GPL-3.0-or-later')
depends=('python' 'python-colorama' 'python-crypto')
makedepends=('git')
source=("git+https://github.com/t0thkr1s/$pkgname.git")
sha512sums=('SKIP')

pkgver() {
  cd $pkgname

  ( set -o pipefail
    git describe --long --tags --abbrev=7 2>/dev/null |
      sed 's/\([^-]*-g\)/r\1/;s/-/./g' ||
    printf "%s.%s" "$(git rev-list --count HEAD)" \
      "$(git rev-parse --short=7 HEAD)"
  )
}

prepare() {
  cd $pkgname

  sed -i "s/banner = '''/banner = r'''/g" gpp-decrypt.py
}

package() {
  cd $pkgname

  install -dm 755 "$pkgdir/usr/bin"
  install -dm 755 "$pkgdir/usr/share/$pkgname"
  install -Dm 644 -t "$pkgdir/usr/share/doc/$pkgname/" *.md

  rm -rf LICENSE *.md .gitignore Dockerfile

  cp -a * "$pkgdir/usr/share/$pkgname/"

  cat > "$pkgdir/usr/bin/$pkgname" << EOF
#!/bin/sh
exec python /usr/share/$pkgname/gpp-decrypt.py "\$@"
EOF

  chmod +x "$pkgdir/usr/bin/$pkgname"
}

