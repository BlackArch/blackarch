# This file is part of BlackArch Linux ( https://www.blackarch.org/ ).
# See COPYING for license details.

pkgbase=python-dfwinreg
pkgname=('python2-dfwinreg' 'python-dfwinreg')
pkgver=20190517
pkgrel=1
pkgdesc='Digital Forensics Windows Registry.'
arch=('any')
url='https://pypi.org/project/dfwinreg/#files'
license=('Apache2')
makedepends=('python2-setuptools' 'python-setuptools')
source=("https://files.pythonhosted.org/packages/c2/59/7c387ea0cd6ab8ee0ba2a714689c894acc280ace4cfb6928f4a53df4b966/dfwinreg-20190517.tar.gz")
sha512sums=('6af43a816d2c5743be63cfefd88f508c83ffce8f0b295edfce709f3aecabbe9fde3f36b1c56fac53bf902dd93f15025cefc9f142d491342bd75fbf71860fb162')

package_python2-dfwinreg() {
  depends=('python2')

  cd "dfwinreg-$pkgver"

  python2 setup.py install --prefix=/usr --root="$pkgdir" --optimize=1
  mv $pkgdir/usr/share/doc/dfwinreg{,2}
}

package_python-dfwinreg() {
  depends=('python')

  cd "dfwinreg-$pkgver"

  python setup.py install --prefix=/usr --root="$pkgdir" --optimize=1
}

