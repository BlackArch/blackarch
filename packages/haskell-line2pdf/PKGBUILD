# This file is part of BlackArch Linux ( http://blackarch.org ).
# See COPYING for license details.

pkgname='haskell-line2pdf'
pkgver='0.0.7'
pkgrel=3
pkgdesc='Simple command-line utility to convert text into PDF.'
arch=('x86_64' 'armv6h' 'armv7h' 'aarch64')
url='http://hackage.haskell.org/package/line2pdf'
license=('GPL')
makedepends=('ghc')
depends=('haskell-regex-compat' 'haskell-mtl')
install='haskell-line2pdf.install'
source=("http://hackage.haskell.org/packages/archive/line2pdf/$pkgver/line2pdf-${pkgver}.tar.gz")
sha1sums=('b09ecfda64345dddb48c3fe02ffc102948cd1bb0')
options=('staticlibs')

build() {
  cd "$srcdir/line2pdf-$pkgver"

  runhaskell Setup configure -O -p --enable-split-objs --enable-shared \
    --prefix=/usr --docdir="/usr/share/doc/haskell-line2pdf" \
    --libsubdir=\$compiler/site-local/\$pkgid
  runhaskell Setup build
  runhaskell Setup haddock
  runhaskell Setup register   --gen-script
  runhaskell Setup unregister --gen-script
  sed -i -r -e "s|ghc-pkg.*unregister[^ ]* |&'--force' |" unregister.sh
}

package() {
  cd "$srcdir/line2pdf-$pkgver"

  install -Dm744 register.sh "$pkgdir/usr/share/haskell/haskell-line2pdf/register.sh"
  install -m744 unregister.sh "$pkgdir/usr/share/haskell/haskell-line2pdf/unregister.sh"
  mkdir -p "$pkgdir/usr/share/doc/ghc/html/libraries"
  ln -s "/usr/share/doc/haskell-line2pdf/html" "$pkgdir/usr/share/doc/ghc/html/libraries/line2pdf"
  runhaskell Setup copy --destdir="$pkgdir"
}
