# This file is part of BlackArch Linux ( https://www.blackarch.org/ ).
# See COPYING for license details.

pkgname=slurp-s3
pkgver=6.6f64f3e
pkgrel=1
groups=('blackarch' 'blackarch-scanner')
pkgdesc='S3 bucket enumerator'
arch=('x86_64' 'armv6h' 'armv7h' 'aarch64')
url='https://github.com/bbb31/slurp'
license=('AGPLv3')
depends=('go')
makedepends=('go' 'git')
source=('git+https://github.com/bbb31/slurp.git')
sha512sums=('SKIP')
_url='github.com/bbb31/slurp'

pkgver() {
  cd slurp

  echo $(git rev-list --count HEAD).$(git rev-parse --short HEAD)
}

build() {
  install -dm 755 src/$_url && rm -rf src/$_url && mv slurp src/$_url && \
    cd src/$_url

  GOPATH="$srcdir" go get -d -t $_url
}

package() {
  GOPATH="$srcdir" go build -v $_url

  install -dm 755 "$pkgdir/usr/bin"
  install -dm 755 "$pkgdir/usr/share/$pkgname"

  install -Dm 755 "$srcdir/slurp" "$pkgdir/usr/share/$pkgname/$pkgname"
  install -Dm 644 src/$_url/readme.md \
    "$pkgdir/usr/share/doc/$pkgname/README.md"

  cat > "$pkgdir/usr/bin/$pkgname" << EOF
#!/bin/sh
cd /usr/share/$pkgname
exec ./slurp "\$@"
EOF

  chmod 755 "$pkgdir/usr/bin/$pkgname"
}

