# This file is part of BlackArch Linux ( https://www.blackarch.org/ ).
# See COPYING for license details.

pkgname=pwdlyser
pkgver=11.b84c635
pkgrel=1
pkgdesc='Python-based CLI Password Analyser (Reporting Tool).'
groups=('blackarch' 'blackarch-crypto' 'blackarch-misc')
arch=('any')
url='https://github.com/ins1gn1a/pwdlyser'
license=('MIT')
depends=('python' 'python-argparse')
makedepends=('git')
source=("git+https://github.com/ins1gn1a/$pkgname.git")
sha512sums=('SKIP')

pkgver() {
  cd $pkgname

  echo $(git rev-list --count HEAD).$(git rev-parse --short HEAD)
}

package() {
  cd $pkgname

  install -dm 755 "$pkgdir/usr/share/$pkgname"

  install -Dm 755 "$pkgname.py" "$pkgdir/usr/bin/$pkgname"
  install -Dm 644 -t "$pkgdir/etc/$pkgname" *.conf
  install -Dm 644 -t "$pkgdir/usr/share/doc/$pkgname/" README.md
  install -Dm 644 LICENCE "$pkgdir/usr/share/licenses/$pkgname/LICENSE"

  rm README.md LICENCE *.conf pwdlyser.py

  cp -a * "$pkgdir/usr/share/$pkgname/"
}

