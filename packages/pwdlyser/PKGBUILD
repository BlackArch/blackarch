# This file is part of BlackArch Linux ( http://blackarch.org ).
# See COPYING for license details.

pkgname=pwdlyser
pkgver=136.483b9bc
pkgrel=2
pkgdesc='Python-based CLI Password Analyser (Reporting Tool).'
groups=('blackarch' 'blackarch-crypto' 'blackarch-misc')
arch=('any')
url='https://github.com/ins1gn1a/pwdlyser'
license=('MIT')
depends=('python' 'python-argparse')
makedepends=('git')
source=('git+https://github.com/ins1gn1a/pwdlyser.git')
sha512sums=('SKIP')

pkgver() {
  cd "$srcdir/pwdlyser"

  echo $(git rev-list --count HEAD).$(git rev-parse --short HEAD)
}

package() {
  cd "$srcdir/pwdlyser"

  install -dm 755 "$pkgdir/usr/share/pwdlyser"

  install -Dm755 pwdlyser.py "$pkgdir/usr/bin/pwdlyser"
  install -Dm644 -t "$pkgdir/etc/pwdlyser" *.conf
  install -Dm644 -t "$pkgdir/usr/share/doc/pwdlyser/" README.md
  install -Dm644 LICENCE "$pkgdir/usr/share/licenses/pwdlyser/LICENSE"

  rm README.md LICENCE *.conf pwdlyser.py

  cp -a * "$pkgdir/usr/share/pwdlyser/"
}
