# This file is part of BlackArch Linux ( https://www.blackarch.org/ ).
# See COPYING for license details.

pkgbase=python-easyprocess
pkgname=('python2-easyprocess' 'python-easyprocess')
_pkgname=EasyProcess
pkgver=0.2.9
pkgrel=1
pkgdesc='Easy to use python subprocess interface.'
arch=('any')
url='https://pypi.python.org/pypi/easyprocess/'
license=('BSD')
makedepends=('python2-setuptools' 'python-setuptools')
source=("https://files.pythonhosted.org/packages/30/2d/b8afe262637636f27ec45af5792585d756b8f55380e657d5681d265d41c6/EasyProcess-$pkgver.tar.gz")
sha512sums=('2ac7b80b22e53cb34d1612a386ff3f659c39cb6cf265f07420c41e67255d0907d38199a78ccb8f3840c9380978a021dd67440d188484c7a419607fbcd70c882d')

prepare() {
  cp -a "$_pkgname-$pkgver"{,-2}
}

build() {
  cd "$_pkgname-$pkgver-2"

  python2 setup.py build

  cd "$srcdir/$_pkgname-$pkgver"

  python setup.py build
}

package_python2-easyprocess() {
  depends=('python2')

  cd "$_pkgname-$pkgver-2"

  python2 setup.py install --prefix=/usr --root="$pkgdir" --optimize=1 \
    --skip-build
}

package_python-easyprocess() {
  depends=('python')

  cd "$_pkgname-$pkgver"

  python setup.py install --prefix=/usr --root="$pkgdir" --optimize=1 \
    --skip-build
}

