# This file is part of BlackArch Linux ( https://www.blackarch.org/ ).
# See COPYING for license details.

pkgbase=python-easyprocess
pkgname=('python2-easyprocess' 'python-easyprocess')
_pkgname=EasyProcess
pkgver=0.2.8
pkgrel=1
pkgdesc='Easy to use python subprocess interface.'
arch=('any')
url='https://pypi.python.org/pypi/easyprocess/'
license=('BSD')
makedepends=('python2-setuptools' 'python-setuptools')
source=("https://files.pythonhosted.org/packages/9e/07/d8895f0908e9424c1a782617e4c1b45d935936e58798140e3e649d05165a/EasyProcess-$pkgver.tar.gz")
sha512sums=('b3072b4aa6c90bccfe630c3940bc4a4dd4388fe06a8660f51145c863193fbf88bf8ad7e7a542c781a3bb584de3ba52bc898fec266d998890c11e9440456e71d5')

prepare() {
  cp -a "$_pkgname-$pkgver"{,-2}
}

build() {
  cd "$_pkgname-$pkgver-2"

  python2 setup.py build

  cd "$srcdir/$_pkgname-$pkgver"

  python setup.py build
}

package_python2-easyprocess() {
  depends=('python2')

  cd "$_pkgname-$pkgver-2"

  python2 setup.py install --prefix=/usr --root="$pkgdir" --optimize=1 \
    --skip-build
}

package_python-easyprocess() {
  depends=('python')

  cd "$_pkgname-$pkgver"

  python setup.py install --prefix=/usr --root="$pkgdir" --optimize=1 \
    --skip-build
}

