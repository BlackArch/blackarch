# This file is part of BlackArch Linux ( https://www.blackarch.org/ ).
# See COPYING for license details.

pkgbase=python-easyprocess
pkgname=('python2-easyprocess' 'python-easyprocess')
_pkgname=EasyProcess
pkgver=0.2.10
pkgrel=1
pkgdesc='Easy to use python subprocess interface.'
arch=('any')
url='https://pypi.python.org/pypi/easyprocess/'
license=('BSD')
makedepends=('python2-setuptools' 'python-setuptools')
source=("https://files.pythonhosted.org/packages/0b/1b/87836fe6bfc157fac6d0356c5d7752dabb5097980e40c0123464acd9324b/EasyProcess-$pkgver.tar.gz")
sha512sums=('c7e6f88372728d05d7e59f19bd6072b7444e5c5b2be11710e7928287ec7b98a1c68b6b2c805ebe99648f6cca94c29eec7e4d237a0644181ab88a0daf46d73769')

prepare() {
  cp -a "$_pkgname-$pkgver"{,-2}
}

build() {
  cd "$_pkgname-$pkgver-2"

  python2 setup.py build

  cd "$srcdir/$_pkgname-$pkgver"

  python setup.py build
}

package_python2-easyprocess() {
  depends=('python2')

  cd "$_pkgname-$pkgver-2"

  python2 setup.py install --prefix=/usr --root="$pkgdir" --optimize=1 \
    --skip-build
}

package_python-easyprocess() {
  depends=('python')

  cd "$_pkgname-$pkgver"

  python setup.py install --prefix=/usr --root="$pkgdir" --optimize=1 \
    --skip-build
}

