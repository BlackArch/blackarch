# This file is part of BlackArch Linux ( https://www.blackarch.org/ ).
# See COPYING for license details.

pkgbase=python-easyprocess
pkgname=('python2-easyprocess' 'python-easyprocess')
_pkgname=EasyProcess
pkgver=0.2.7
pkgrel=2
pkgdesc='Easy to use python subprocess interface.'
arch=('any')
url='https://pypi.python.org/pypi/easyprocess/'
license=('BSD')
makedepends=('python2-setuptools' 'python-setuptools')
source=("https://files.pythonhosted.org/packages/7e/34/6d985708b1503f101ceaf132c6a789474e01318f711e99c8c40183ef92c4/$_pkgname-$pkgver.tar.gz")
sha512sums=('6ad8e7c32b8edb1237f4d73c18f039f847ac177532914c5b2d4c4802aa8111aab6e1088fd62a4d819810d70b2bc6b6b178e82d872a740e8c70bd5b39d1c398af')

prepare() {
  cp -a "$_pkgname-$pkgver" "$_pkgname-$pkgver-2"
}

build() {
  cd "$_pkgname-$pkgver-2"

  python2 setup.py build

  cd "$srcdir/$_pkgname-$pkgver"

  python setup.py build
}

package_python2-easyprocess() {
  depends=('python2')

  cd "$_pkgname-$pkgver-2"

  python2 setup.py install --prefix=/usr --root="$pkgdir" --optimize=1 \
    --skip-build
}

package_python-easyprocess() {
  depends=('python')

  cd "$_pkgname-$pkgver"

  python setup.py install --prefix=/usr --root="$pkgdir" --optimize=1 \
    --skip-build
}

