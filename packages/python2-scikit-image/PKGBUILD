# This file is part of BlackArch Linux ( https://www.blackarch.org/ ).
# See COPYING for license details.

pkgname=python2-scikit-image
pkgver=0.14.1
pkgrel=1
pkgdesc='Image processing routines for SciPy.'
arch=('x86_64' 'armv6h' 'armv7h' 'aarch64')
url='https://pypi.org/project/scikit-image/#files'
license=('BSD')
depends=('python2-scipy' 'python2-matplotlib' 'python2-networkx'
         'python2-pillow' 'python2-pywavelets' 'python2-six'
         'python2-cloudpickle')
makedepends=('cython2')
optdepends=('python2-pyqt4: for imshow(x, fancy=True) and skivi'
            'freeimage: for reading various types of image file formats')
options=('!emptydirs')
source=("https://files.pythonhosted.org/packages/a4/5d/05d40ad281cb16bc9dbd91982a918162b374c43e84eb2c8f7070e4be6729/scikit-image-$pkgver.tar.gz")
sha512sums=('30c2b7c985568f9b03b82be4839c16882adfdec664f69293aaccf1deee12305e88d42ff9a6f193a46e4ecd1337b032d319a1b7ea1a0fdbb01f96451cf7ed73cf')

prepare() {
  cd "scikit-image-$pkgver"

  sed -e "s|#![ ]*/usr/bin/python$|#!/usr/bin/python2|" \
    -e "s|#![ ]*/usr/bin/env python$|#!/usr/bin/env python2|" \
    -e "s|#![ ]*/bin/env python$|#!/usr/bin/env python2|" \
    -i $(find . -name '*.py')
}

build() {
  cd "scikit-image-$pkgver"

  python2 setup.py build
}

package() {
  cd "scikit-image-$pkgver"

  python2 setup.py install --prefix=/usr --root="$pkgdir" --optimize=1 \
    --skip-build

  mv "$pkgdir/usr/bin/skivi" "$pkgdir/usr/bin/skivi2"
}


