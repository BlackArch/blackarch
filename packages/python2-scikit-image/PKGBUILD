# This file is part of BlackArch Linux ( https://www.blackarch.org/ ).
# See COPYING for license details.

pkgname=python2-scikit-image
pkgver=0.15.0
pkgrel=1
pkgdesc='Image processing routines for SciPy.'
arch=('x86_64' 'aarch64')
url='https://pypi.org/project/scikit-image/#files'
license=('BSD')
depends=('python2-scipy' 'python2-matplotlib' 'python2-networkx'
         'python2-pillow' 'python2-pywavelets' 'python2-six'
         'python2-cloudpickle')
makedepends=('cython2')
optdepends=('python2-pyqt4: for imshow(x, fancy=True) and skivi'
            'freeimage: for reading various types of image file formats')
options=('!emptydirs')
source=("https://files.pythonhosted.org/packages/e0/46/ca035f5d7d3414124a3a5ef22cd2e75c0c5149042a668375f1d44eb69f8f/scikit-image-0.15.0.tar.gz")
sha512sums=('9d0be54ddd564c0f28eae979b6f348011f7b38c8811f70ad460330905e706fe145dd6a30b502d15126450148fdbb3f54a271227d46e2d078030054ad00f35bc3')

prepare() {
  cd "scikit-image-$pkgver"

  sed -e "s|#![ ]*/usr/bin/python$|#!/usr/bin/python2|" \
    -e "s|#![ ]*/usr/bin/env python$|#!/usr/bin/env python2|" \
    -e "s|#![ ]*/bin/env python$|#!/usr/bin/env python2|" \
    -i $(find . -name '*.py')
}

build() {
  cd "scikit-image-$pkgver"

  python2 setup.py build
}

package() {
  cd "scikit-image-$pkgver"

  python2 setup.py install --prefix=/usr --root="$pkgdir" --optimize=1 \
    --skip-build

  mv "$pkgdir/usr/bin/skivi" "$pkgdir/usr/bin/skivi2"
}


