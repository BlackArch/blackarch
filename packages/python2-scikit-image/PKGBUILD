# This file is part of BlackArch Linux ( https://www.blackarch.org/ ).
# See COPYING for license details.

pkgname=python2-scikit-image
_pkgname=scikit-image
pkgver=0.14.5
pkgrel=1
pkgdesc='Image processing routines for SciPy.'
arch=('x86_64' 'aarch64')
url='https://pypi.org/project/scikit-image/#files'
license=('BSD')
depends=('python2-scipy' 'python2-matplotlib' 'python2-networkx'
         'python2-pillow' 'python2-pywavelets' 'python2-six'
         'python2-cloudpickle')
makedepends=('cython2')
optdepends=('python2-pyqt4: for imshow(x, fancy=True) and skivi'
            'freeimage: for reading various types of image file formats')
options=('!emptydirs')
source=("https://files.pythonhosted.org/packages/37/1e/26a39d9a88458eaefbab790596a19364604816e3fa5bee9618239356df50/$_pkgname-$pkgver.tar.gz")
sha512sums=('f69214f4fb6f465aef36f941f7e63c981509a9d8086af1600b28345ec51deacf3d7e039264799690fed72a7af6825ffc5b2757dc64cfad798e22abd988c44af4')

prepare() {
  cd "$_pkgname-$pkgver"

  sed -e "s|#![ ]*/usr/bin/python$|#!/usr/bin/python2|" \
    -e "s|#![ ]*/usr/bin/env python$|#!/usr/bin/env python2|" \
    -e "s|#![ ]*/bin/env python$|#!/usr/bin/env python2|" \
    -i $(find . -name '*.py')
}

build() {
  cd "$_pkgname-$pkgver"

  python2 setup.py build
}

package() {
  cd "$_pkgname-$pkgver"

  python2 setup.py install --prefix=/usr --root="$pkgdir" -O1 --skip-build

  mv "$pkgdir/usr/bin/skivi" "$pkgdir/usr/bin/skivi2"
}

