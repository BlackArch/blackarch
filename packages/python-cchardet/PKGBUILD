# This file is part of BlackArch Linux ( https://www.blackarch.org/ ).
# See COPYING for license details.

pkgname=python-cchardet
_pkgname=cchardet
pkgver=2.1.7
pkgrel=9
pkgdesc='High speed universal character encoding detector.'
arch=('x86_64' 'aarch64')
url='https://pypi.org/project/cchardet/#files'
license=('GPL-1.0-or-later' 'LGPL-2.0-or-later' 'MPL-1.0')
depends=('python' 'python-requests' 'python-chardet' 'python-wikipedia')
makedepends=('python-build' 'python-installer' 'python-setuptools'
             'python-wheel' 'cython')
options=(!emptydirs)
source=("https://files.pythonhosted.org/packages/source/${_pkgname::1}/$_pkgname/$_pkgname-$pkgver.tar.gz")
sha512sums=('43e663e30ec079b2a954862de5e8136a2e40f69e300d65eb4ce9d7ffa5d8c496dc7c0937b3306b4096cfad12a1d0617628f8f0115534ab6faf9eb39d2b3935a2')

build() {
  cd "$_pkgname-$pkgver"

  python -m build --wheel --no-isolation
}

package() {
  cd "$_pkgname-$pkgver"

  python -m installer --destdir="$pkgdir" dist/*.whl
}

