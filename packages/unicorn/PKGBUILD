# This file is part of BlackArch Linux ( http://blackarch.org ).
# See COPYING for license details.

pkgname=('unicorn' 'python-unicorn' 'python2-unicorn')
pkgver='0.9'
pkgrel=5
epoch=1
pkgdesc='Lightweight, multi-platform, multi-architecture CPU emulator framework based on QEMU.'
url='http://www.unicorn-engine.org'
arch=('i686' 'x86_64' 'armv6h' 'armv7h')
license=('GPL2')
makedepends=('pkg-config' 'glib2' 'python' 'python2')
checkdepends=('cmocka')
options=('staticlibs' '!emptydirs')
source=("https://github.com/unicorn-engine/unicorn/archive/${pkgver}.tar.gz")
sha1sums=('1504c21f28f19ff1f71bcd74043c2284decb1d9f')

prepare() {
  cd "$srcdir/unicorn-$pkgver"

  sed 's|libdir=$(LIBDIR)|libdir=$(PREFIX)/$(LIBDIRARCH)|g' -i Makefile
  sed 's|includedir=$(INCDIR)|includedir=$(PREFIX)/include|g' -i Makefile
  sed 's|ifeq ($(UNICORN_DEBUG),yes)|ifneq ($(UNICORN_DEBUG),yes)|g' -i Makefile
  sed 's|-O3|-O2|g' -i Makefile qemu/configure
  sed 's|-g ||g' -i qemu/configure
  sed 's|UNICORN_DEBUG ?= yes|UNICORN_DEBUG ?= no|g' -i config.mk
  sed 's|unicorn|.unicorn|g' -i bindings/python/unicorn/__init__.py
  sed 's|unicorn_const|.unicorn_const|g' -i bindings/python/unicorn/unicorn.py

  cp -ra bindings/python{,2}

  sed -r 's|(python)$|\12|' -i bindings/python2/*.py
}

build() {
  cd "$srcdir/unicorn-$pkgver"

  make UNICORN_QEMU_FLAGS="--python=/usr/bin/python2"

  cd "$srcdir/unicorn-$pkgver/bindings/python/"
  python setup.py build

  cd "$srcdir/unicorn-$pkgver/bindings/python2"
  python2 setup.py build
}

package_unicorn() {
  cd "$srcdir/unicorn-$pkgver"

  depends=('glib2')

  make DESTDIR="${pkgdir}" install

  install -Dm644 samples/*.c -t "${pkgdir}/usr/share/doc/unicorn/samples"
}

package_python-unicorn() {
  cd "$srcdir/unicorn-${pkgver}/bindings/python"

  depends=('python' 'unicorn')

  python setup.py install --root="$pkgdir" -O1 --skip-build

  install -Dm 644 sample* shellcode.py -t \
    "${pkgdir}/usr/share/doc/unicorn/samples"
}

package_python2-unicorn() {
  cd "$srcdir/unicorn-${pkgver}/bindings/python2"

  depends=('python2' 'unicorn')

  python2 setup.py install --root="$pkgdir" -O1 --skip-build

  install -Dm 644 sample* shellcode.py -t \
    "${pkgdir}/usr/share/doc/${pkgname}/samples"
}
