# This file is part of BlackArch Linux ( http://blackarch.org ).
# See COPYING for license details.

pkgname='needle'
pkgver=573.3776adf
pkgrel=1
pkgdesc='The iOS Security Testing Framework.'
groups=('blackarch' 'blackarch-mobile')
arch=('any')
url='https://github.com/mwrlabs/needle'
license=('custom:unknown')
depends=('python2' 'python2-pysqlite' 'python2-paramiko' 'python2-pyopenssl'
         'python2-frida' 'sshtunnel' 'mitmproxy')
makedepends=('git')
source=('git+https://github.com/mwrlabs/needle.git')
sha1sums=('SKIP')

pkgver() {
  cd "$srcdir/needle"

  echo $(git rev-list --count HEAD).$(git rev-parse --short HEAD)
}


package() {
  cd "$srcdir/needle"

  mkdir -p "$pkgdir/usr/bin"
  mkdir -p "$pkgdir/usr/share/needle"

  install -Dm644 -t "$pkgdir/usr/share/doc/needle/" README.md CHANGELOG.md
  install -Dm644 LICENSE.md "$pkgdir/usr/share/licenses/needle/LICENSE.md"

  rm *.md

  cp -a needle/* "$pkgdir/usr/share/needle"

  cat > "$pkgdir/usr/bin/needle" << EOF
#!/bin/sh
cd /usr/share/needle/
exec python2 needle.py "\${@}"
EOF

  chmod a+x "$pkgdir/usr/bin/needle"
}
