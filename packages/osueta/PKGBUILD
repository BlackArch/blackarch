# This file is part of BlackArch Linux ( http://blackarch.org ).
# See COPYING for license details.

pkgname=osueta
pkgver=73.03c873a
pkgrel=1
groups=('blackarch' 'blackarch-exploitation')
pkgdesc='A simple Python script to exploit the OpenSSH User Enumeration Timing Attack.'
arch=('any')
url='https://github.com/c0r3dump3d/osueta'
license=('GPL2')
depends=('python2' 'python2-ipy' 'python2-python-nmap' 'python2-paramiko')
makedepends=('git')
source=('git+https://github.com/c0r3dump3d/osueta.git')
sha512sums=('SKIP')

pkgver() {
  cd "$srcdir/osueta"

  echo $(git rev-list --count HEAD).$(git rev-parse --short HEAD)
}

package() {
  cd "$srcdir/osueta"

  install -dm 755 "$pkgdir/usr/bin"
  install -dm 755 "$pkgdir/usr/share/$pkgname"

  install -Dm 644 README.md "$pkgdir/usr/share/doc/$pkgname/README.md"
  install -Dm 644 README "$pkgdir/usr/share/doc/$pkgname/README"
  install -Dm 644 LICENSE "$pkgdir/usr/share/licenses/$pkgname/LICENSE"

  cp -a *.py "$pkgdir/usr/share/$pkgname/"

  cat > "$pkgdir/usr/bin/$pkgname" << EOF
#!/bin/sh
cd /usr/share/$pkgname
exec python2 osueta.py "\$@"
EOF

  chmod +x "$pkgdir/usr/bin/$pkgname"
}

