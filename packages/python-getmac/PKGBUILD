# This file is part of BlackArch Linux ( https://www.blackarch.org/ ).
# See COPYING for license details.

pkgbase=python-getmac
pkgname=('python2-getmac' 'python-getmac')
_pkgname=getmac
pkgver=0.9.2
pkgrel=1
pkgdesc='Get MAC addresses of remote hosts and local interfaces.'
arch=('any')
url='https://pypi.org/project/getmac/#files'
license=('MIT')
makedepends=('python2-setuptools' 'python-setuptools')
source=("https://files.pythonhosted.org/packages/source/${_pkgname::1}/$_pkgname/$_pkgname-$pkgver.tar.gz")
sha512sums=('36b9e16ce1907f3990b842a4f3e543c120d2c8d9499c8fb16cab7ce4fcd6bf928f1c8ca85f74db8fe9b4d05bfbf6d26b7d81612b4b51ccb42b950cf3cccae6d4')

prepare() {
  cp -a "$_pkgname-$pkgver"{,-2}
}

build() {
  cd "$_pkgname-$pkgver-2"

  python2 setup.py build

  cd "$srcdir/$_pkgname-$pkgver"

  python setup.py build
}

package_python2-getmac() {
  depends=('python2')

  cd "$_pkgname-$pkgver-2"

  python2 setup.py install --prefix=/usr --root="$pkgdir" -O1 --skip-build
}

package_python-getmac() {
  depends=('python')

  cd "$_pkgname-$pkgver"

  python setup.py install --prefix=/usr --root="$pkgdir" -O1 --skip-build
}

