# This file is part of BlackArch Linux ( https://www.blackarch.org/ ).
# See COPYING for license details.

pkgbase=python-getmac
pkgname=('python2-getmac' 'python-getmac')
_pkgname=getmac
pkgver=0.8.2
pkgrel=2
pkgdesc='Get MAC addresses of remote hosts and local interfaces.'
arch=('any')
url='https://pypi.org/project/getmac/#files'
license=('MIT')
makedepends=('python2-setuptools' 'python-setuptools')
source=("https://files.pythonhosted.org/packages/60/a2/d64aed61f8f86827eab2e77e49a6158630690daa8130f7452a4b676acefc/$_pkgname-$pkgver.tar.gz")
sha512sums=('92ef648e99cf5d23dd91fe2303d5e2524a5efccbeeeaa2715e56f483e4abac1471cfe4af0c66beac77aecc7bec1ba6c24bc7afd3f2c6d48325d5a2203350f768')

prepare() {
  cp -a "$_pkgname-$pkgver"{,-2}
}

build() {
  cd "$_pkgname-$pkgver-2"

  python2 setup.py build

  cd "$srcdir/$_pkgname-$pkgver"

  python setup.py build
}

package_python2-getmac() {
  depends=('python2')

  cd "$_pkgname-$pkgver-2"

  python2 setup.py install --prefix=/usr --root="$pkgdir" -O1 --skip-build
}

package_python-getmac() {
  depends=('python')

  cd "$_pkgname-$pkgver"

  python setup.py install --prefix=/usr --root="$pkgdir" -O1 --skip-build
}

