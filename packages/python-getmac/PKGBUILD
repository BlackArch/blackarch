# This file is part of BlackArch Linux ( https://www.blackarch.org/ ).
# See COPYING for license details.

pkgbase=python-getmac
pkgname=('python2-getmac' 'python-getmac')
_pkgname=getmac
pkgver=0.8.1
pkgrel=1
pkgdesc='Get MAC addresses of remote hosts and local interfaces.'
arch=('any')
url='https://pypi.org/project/getmac/#files'
license=('MIT')
makedepends=('python2-setuptools' 'python-setuptools')
source=("https://files.pythonhosted.org/packages/6a/18/87b919ec7c2f1d90d80ba0c32b1c5a55aefebbbda2f97b6ceda3666ec718/$_pkgname-$pkgver.tar.gz")
sha512sums=('f319f1b56cc8d623c1ec304ff628583f44e7cc24b11db139d1bc97a535b8b1389d2570e9091bea62329894ceb507e960a1983c8ae521f9550257e7b8fdeb1d78')

prepare() {
  cp -a "$_pkgname-$pkgver"{,-2}
}

build() {
  cd "$_pkgname-$pkgver-2"

  python2 setup.py build

  cd "$srcdir/$_pkgname-$pkgver"

  python setup.py build
}

package_python2-getmac() {
  depends=('python2')

  cd "$_pkgname-$pkgver-2"

  python2 setup.py install --prefix=/usr --root="$pkgdir" -O1 --skip-build
}

package_python-getmac() {
  depends=('python')

  cd "$_pkgname-$pkgver"

  python setup.py install --prefix=/usr --root="$pkgdir" -O1 --skip-build
}

