# This file is part of BlackArch Linux ( https://www.blackarch.org/ ).
# See COPYING for license details.

pkgbase=python-getmac
pkgname=('python2-getmac' 'python-getmac')
_pkgname=getmac
pkgver=0.8.3
pkgrel=1
pkgdesc='Get MAC addresses of remote hosts and local interfaces.'
arch=('any')
url='https://pypi.org/project/getmac/#files'
license=('MIT')
makedepends=('python2-setuptools' 'python-setuptools')
source=("https://files.pythonhosted.org/packages/source/${_pkgname::1}/$_pkgname/$_pkgname-$pkgver.tar.gz")
sha512sums=('c30ad48092f51aff6dfd73ed914ada26098d2a7a4b44270183f0d3e9bece486feb54b09f8ca7085e0a965ef197c17facd2111748882656b3e621d28ac8c0b860')

prepare() {
  cp -a "$_pkgname-$pkgver"{,-2}
}

build() {
  cd "$_pkgname-$pkgver-2"

  python2 setup.py build

  cd "$srcdir/$_pkgname-$pkgver"

  python setup.py build
}

package_python2-getmac() {
  depends=('python2')

  cd "$_pkgname-$pkgver-2"

  python2 setup.py install --prefix=/usr --root="$pkgdir" -O1 --skip-build
}

package_python-getmac() {
  depends=('python')

  cd "$_pkgname-$pkgver"

  python setup.py install --prefix=/usr --root="$pkgdir" -O1 --skip-build
}

