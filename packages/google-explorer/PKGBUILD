# This file is part of BlackArch Linux ( https://www.blackarch.org/ ).
# See COPYING for license details.

pkgname=google-explorer
pkgver=140.0b21b57
pkgrel=2
pkgdesc='Google mass exploit robot - Make a google search, and parse the results for a especific exploit you define.'
groups=('blackarch' 'blackarch-automation' 'blackarch-exploitation')
arch=('any')
url='https://github.com/anarcoder/google_explorer'
license=('custom:unknown')
depends=('python' 'python-selenium' 'python-docopt' 'python-colorama'
         'python-requests' 'python-lxml' 'python-beautifulsoup4' 'chromium')
makedepends=('git')
source=("$pkgname::git+https://github.com/anarcoder/google_explorer.git")
sha512sums=('SKIP')

pkgver() {
  cd $pkgname

  echo $(git rev-list --count HEAD).$(git rev-parse --short HEAD)
}

package() {
  cd $pkgname

  install -dm 755 "$pkgdir/usr/bin"
  install -dm 755 "$pkgdir/usr/share/$pkgname"

  install -Dm 644 -t "$pkgdir/usr/share/doc/$pkgname/" README.md

  rm README.md

  cp -a * "$pkgdir/usr/share/$pkgname/"

  cat > "$pkgdir/usr/bin/$pkgname" << EOF
#!/bin/sh
cd /usr/share/$pkgname
exec python google_explorer.py "\$@"
EOF

  chmod +x "$pkgdir/usr/bin/$pkgname"
}

