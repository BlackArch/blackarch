# This file is part of BlackArch Linux ( https://www.blackarch.org/ ).
# See COPYING for license details.

pkgbase=python-singlefile
pkgname=('python2-singlefile' 'python-singlefile')
_pkgname=singlefile
pkgver=0.0.1a0
pkgrel=4
pkgdesc='Import from web. Share your snippets and import them from web.'
arch=('any')
url='https://pypi.org/project/singlefile/#files'
license=('MIT')
makedepends=('python2-setuptools' 'python-setuptools')
source=("https://files.pythonhosted.org/packages/3b/05/202081c2231f10e9f981b850b016f6086678c447029d6741fa47c0993fcb/$_pkgname-$pkgver.tar.gz")
sha512sums=('2a2b06aae9fa375d36d7f9e6c301c48ed69b65089803961b25ad435bf537bfc98b1384a26b709d801c71c4c677e9d2fa2fd181320bf5fc4314cc29ab57b229cd')

prepare() {
  cp -a "$_pkgname-$pkgver"{,-2}
}

build() {
  cd "$_pkgname-$pkgver-2"

  python2 setup.py build

  cd "$srcdir/$_pkgname-$pkgver"

  python setup.py build
}

package_python2-singlefile() {
  depends=('python2' 'python2-requests')

  cd "$_pkgname-$pkgver-2"

  python2 setup.py install --prefix=/usr --root="$pkgdir" --optimize=1 \
    --skip-build
}

package_python-singlefile() {
  depends=('python' 'python-requests')

  cd "$_pkgname-$pkgver"

  python setup.py install --prefix=/usr --root="$pkgdir" --optimize=1 \
    --skip-build
}

