pkgname=cowpatty
pkgver=4.6
pkgrel=6
pkgdesc="Wireless WPA/WPA2 PSK handshake cracking utility"
groups=('blackarch' 'blackarch-wireless')
arch=('i686' 'x86_64')
url='http://www.wirelessdefence.org/Contents/Files/'
license=('GPL')
depends=('openssl' 'libpcap')
options=(docs !makeflags)
source=("http://www.wirelessdefence.org/Contents/Files/cowpatty-$pkgver.tgz")
md5sums=('b90fd36ad987c99e7cc1d2a05a565cbd')

build() {
  cd "$srcdir/cowpatty-$pkgver"
  # _FORTIFY_SOURCE causes issues.
  CPPFLAGS= make
}

package() {
  cd "$srcdir/cowpatty-$pkgver"

  make DESTDIR="$pkgdir/" "BINDIR=/usr/bin/" install

  install -Dm644 dict "$pkgdir/usr/share/cowpatty/dict"
  install -Dm644 COPYING "$pkgdir/usr/share/licenses/cowpatty/COPYING"
}
