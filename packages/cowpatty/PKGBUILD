# This file is part of BlackArch Linux ( http://blackarch.org ).
# See COPYING for license details.

pkgname='cowpatty'
pkgver='4.6'
pkgrel=7
groups=('blackarch' 'blackarch-cracker' 'blackarch-wireless')
pkgdesc='Wireless WPA/WPA2 PSK handshake cracking utility.'
arch=('x86_64' 'armv6h' 'armv7h' 'aarch64')
url='http://www.wirelessdefence.org/Contents/Files/'
license=('GPL')
depends=('openssl' 'libpcap')
options=(docs !makeflags)
source=("http://www.wirelessdefence.org/Contents/Files/cowpatty-${pkgver}.tgz")
sha1sums=('2dc09d725e4131a68a33c8717d3a7317e5616df2')

build() {
  cd "$srcdir/cowpatty-$pkgver"

  # _FORTIFY_SOURCE causes issues.
  CPPFLAGS= make
}

package() {
  cd "$srcdir/cowpatty-$pkgver"

  make DESTDIR="$pkgdir/" "BINDIR=/usr/bin/" install

  install -Dm644 dict "$pkgdir/usr/share/cowpatty/dict"
  install -Dm644 COPYING "$pkgdir/usr/share/licenses/cowpatty/COPYING"
}
