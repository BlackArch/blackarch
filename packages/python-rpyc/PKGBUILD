# This file is part of BlackArch Linux ( https://www.blackarch.org/ ).
# See COPYING for license details.

pkgbase=python-rpyc
pkgname=('python2-rpyc' 'python-rpyc')
_pkgname=rpyc
pkgver=4.1.5
pkgrel=2
pkgdesc='Remote Python Call (RPyC), a transparent and symmetric RPC library.'
arch=('any')
url='https://pypi.org/project/rpyc/#files'
license=('MIT')
makedepends=('python2-setuptools' 'python-setuptools')
source=("https://github.com/tomerfiliba/$_pkgname/archive/$pkgver.tar.gz")
sha512sums=('27f67c7e7bf6f1e94815b3124f300da5f1317e7b454280953d85eca8c7146afc33937ac66a1bb8ac2344b73532ae232b190a647ef90aa723b8b5173b96877454')

prepare() {
  cp -a "$_pkgname-$pkgver"{,-2}
}

build() {
  cd "$_pkgname-$pkgver-2"

  python2 setup.py build

  cd "$srcdir/$_pkgname-$pkgver"

  python setup.py build
}

package_python2-rpyc() {
  depends=('python2' 'python2-plumbum')

  cd "$_pkgname-$pkgver-2"

  python2 setup.py install --root="$pkgdir" --prefix=/usr -O1 --skip-build

  for i in "$pkgdir/usr/bin/"* ; do
    mv $i "${i}2"
  done
}

package_python-rpyc() {
  depends=('python' 'python-plumbum')

  cd "$_pkgname-$pkgver"

  python setup.py install --root="$pkgdir" --prefix=/usr -O1 --skip-build
}

