# This file is part of BlackArch Linux ( https://www.blackarch.org/ ).
# See COPYING for license details.

pkgbase=python-rpyc
pkgname=('python2-rpyc' 'python-rpyc')
_pkgname=rpyc
pkgver=4.1.2
pkgrel=1
pkgdesc='Remote Python Call (RPyC), a transparent and symmetric RPC library.'
arch=('any')
url='https://pypi.org/project/rpyc/#files'
license=('MIT')
makedepends=('python2-setuptools' 'python-setuptools')
source=("https://files.pythonhosted.org/packages/0e/36/3d8c7ba73535aa0df1319b55af882d0d8b87d5297c016d4991970d4e7999/$_pkgname-$pkgver.tar.gz")
sha512sums=('cd5e23204187a77b6dc08703e36ce3f1d7b194e850f402e214e0e6c90cbda4dd8afa42e4d090aa44446be6ed274de6e374684b7ea281345f114109a0c449cfa1')

prepare() {
  cp -a "$_pkgname-$pkgver"{,-2}
}

build() {
  cd "$_pkgname-$pkgver-2"

  python2 setup.py build

  cd "$srcdir/$_pkgname-$pkgver"

  python setup.py build
}

package_python2-rpyc() {
  depends=('python2' 'python2-plumbum')

  cd "$_pkgname-$pkgver-2"

  python2 setup.py install --prefix=/usr --root="$pkgdir" --optimize=1 \
    --skip-build

  for i in "$pkgdir/usr/bin/"* ; do
    mv $i "${i}2"
  done
}

package_python-rpyc() {
  depends=('python' 'python-plumbum')

  cd "$_pkgname-$pkgver"

  python setup.py install --prefix=/usr --root="$pkgdir" --optimize=1 \
    --skip-build
}

