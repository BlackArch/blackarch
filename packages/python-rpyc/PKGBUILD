# This file is part of BlackArch Linux ( https://www.blackarch.org/ ).
# See COPYING for license details.

pkgbase=python-rpyc
pkgname=('python2-rpyc' 'python-rpyc')
_pkgname=rpyc
pkgver=4.1.1
pkgrel=1
pkgdesc='Remote Python Call (RPyC), a transparent and symmetric RPC library.'
arch=('any')
url='https://pypi.org/project/rpyc/#files'
license=('MIT')
makedepends=('python2-setuptools' 'python-setuptools')
source=("https://files.pythonhosted.org/packages/bd/30/24cd10eaa6d39e8eff367f6c9a40f9d94d66fecdec2c0f351b7a0184c4eb/$_pkgname-$pkgver.tar.gz")
sha512sums=('5e3bd5986a1c1167836198e7d2e0a8019ae9c1f52f79cf6f94a8d6fcd08ba02802bf0d3fe17eb963c517dd5e68e9cde20e5de93a2c61baacdc9819558c7f74e1')

prepare() {
  cp -a "$_pkgname-$pkgver"{,-2}
}

build() {
  cd "$_pkgname-$pkgver-2"

  python2 setup.py build

  cd "$srcdir/$_pkgname-$pkgver"

  python setup.py build
}

package_python2-rpyc() {
  depends=('python2' 'python2-plumbum')

  cd "$_pkgname-$pkgver-2"

  python2 setup.py install --prefix=/usr --root="$pkgdir" --optimize=1 \
    --skip-build

  for i in "$pkgdir/usr/bin/"* ; do
    mv $i "${i}2"
  done
}

package_python-rpyc() {
  depends=('python' 'python-plumbum')

  cd "$_pkgname-$pkgver"

  python setup.py install --prefix=/usr --root="$pkgdir" --optimize=1 \
    --skip-build
}

