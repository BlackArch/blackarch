# This file is part of BlackArch Linux ( https://www.blackarch.org/ ).
# See COPYING for license details.

pkgbase=python-rpyc
pkgname=('python2-rpyc' 'python-rpyc')
_pkgname=rpyc
pkgver=4.1.2
pkgrel=2
pkgdesc='Remote Python Call (RPyC), a transparent and symmetric RPC library.'
arch=('any')
url='https://pypi.org/project/rpyc/#files'
license=('MIT')
makedepends=('python2-setuptools' 'python-setuptools')
source=("https://github.com/tomerfiliba/$_pkgname/archive/$pkgver.tar.gz")
sha512sums=('10ab73a296d1c4a212f58bbd38ab8af4475360a06cf7bb972a4553f623bfa14677bdeb2d616d11f018e4232174c0fa7562a40a1a64d21e56682f700bb1317ec0')

prepare() {
  cp -a "$_pkgname-$pkgver"{,-2}
}

build() {
  cd "$_pkgname-$pkgver-2"

  python2 setup.py build

  cd "$srcdir/$_pkgname-$pkgver"

  python setup.py build
}

package_python2-rpyc() {
  depends=('python2' 'python2-plumbum')

  cd "$_pkgname-$pkgver-2"

  python2 setup.py install --root="$pkgdir" --prefix=/usr -O1 --skip-build

  for i in "$pkgdir/usr/bin/"* ; do
    mv $i "${i}2"
  done
}

package_python-rpyc() {
  depends=('python' 'python-plumbum')

  cd "$_pkgname-$pkgver"

  python setup.py install --root="$pkgdir" --prefix=/usr -O1 --skip-build
}

