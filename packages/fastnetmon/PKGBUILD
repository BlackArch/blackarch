# This file is part of BlackArch Linux ( https://www.blackarch.org/ ).
# See COPYING for license details.

pkgname=fastnetmon
pkgver=v1.1.4.r61.gc2441d7
pkgrel=1
pkgdesc='High performance DoS/DDoS load analyzer built on top of multiple packet capture engines.'
url='https://github.com/pavel-odintsov/fastnetmon'
arch=('x86_64')
groups=('blackarch' 'blackarch-defensive' 'blackarch-sniffer')
license=('GPL')
depends=('binutils' 'boost' 'hiredis' 'json-c' 'libpfring' 'log4cpp' 'luajit'
         'mongo-c-driver' 'ndpi' 'numactl' 'pfring-dkms' 'pfring-kmod')
makedepends=('binutils' 'boost' 'cmake' 'git' 'hiredis' 'json-c' 'libpfring'
             'log4cpp' 'luajit' 'make' 'mongo-c-driver' 'ndpi' 'numactl')
source=("git+https://github.com/pavel-odintsov/$pkgname.git"
        'CMakeLists.txt.patch')
sha512sums=('SKIP'
            '18c3e6793d43114a70afcaf95dc8e74880d88660e8bd73086b8458e737bec75f01ef8e478d9972c3232004cdd2332d5d8d98e304bd67d870151b69cd79f3951e')

pkgver() {
  cd $pkgname

  git describe --long --tags | sed 's/\([^-]*-g\)/r\1/;s/-/./g'
}

prepare() {
  cd "$pkgname/src"

  patch < ../../CMakeLists.txt.patch
}

build() {
  cd "$pkgname/src"

  mkdir -p build
  cd build

  cmake ..
  make
}

package() {
  cd $pkgname

  rm -rf docs/images

  install -dm 755 "$pkgdir/usr/lib"

  install -Dm 644 -t "$pkgdir/usr/share/doc/$pkgname" docs/* README.md
  install -Dm 644 LICENSE "$pkgdir/usr/share/licenses/$pkgname/LICENSE"

  cd src/build

  make DESTDIR="$pkgdir" install

  # fix for conflicting files

  cp -r --no-preserve=owner "$pkgdir/lib/systemd" "$pkgdir/usr/lib"
  cp -r --no-preserve=owner "$pkgdir/usr/sbin/$pkgname" "$pkgdir/usr/bin"

  rm -rf "$pkgdir/lib"
  rm -rf "$pkgdir/usr/sbin"
}

