# This file is part of BlackArch Linux ( https://www.blackarch.org/ ).
# See COPYING for license details.

pkgname=fastnetmon
pkgver=v1.1.4.r8.gc890ffe
pkgrel=1
pkgdesc='High performance DoS/DDoS load analyzer built on top of multiple packet capture engines.'
url='https://github.com/pavel-odintsov/fastnetmon'
arch=('x86_64')
groups=('blackarch' 'blackarch-defensive' 'blackarch-sniffer')
license=('GPL')
depends=('binutils' 'boost' 'hiredis' 'json-c' 'libpfring' 'log4cpp' 'luajit'
         'mongo-c-driver' 'ndpi' 'numactl' 'pfring-dkms')
makedepends=('binutils' 'boost' 'cmake' 'git' 'hiredis' 'json-c' 'libpfring'
             'log4cpp' 'luajit' 'make' 'mongo-c-driver' 'ndpi' 'numactl')
source=('git+https://github.com/pavel-odintsov/fastnetmon.git'
        'CMakeLists.txt.patch')
sha512sums=('SKIP'
            '2f23ef3a3febce24e80a08d3d255a63284f955aeb68fe835c37a867bb10f3c1ab06e0d8241c1f4557bedc42dc2c4f4dc049be430bcdade44b3de303fa3015b70')

pkgver() {
  cd $pkgname

  git describe --long --tags | sed 's/\([^-]*-g\)/r\1/;s/-/./g'
}

prepare() {
  cd "$pkgname/src"

  patch < ../../CMakeLists.txt.patch
}

build() {
  cd "$pkgname/src"

  mkdir -p build
  cd build

  cmake ..
  make
}

package() {
  cd $pkgname

  rm -rf docs/images

  install -dm 755 "$pkgdir/usr/lib"

  install -Dm 644 -t "$pkgdir/usr/share/doc/$pkgname" docs/* README.md
  install -Dm 644 LICENSE "$pkgdir/usr/share/licenses/$pkgname/LICENSE"

  cd "src/build"

  make DESTDIR="$pkgdir" install

  # fix for conflicting files

  cp -r --no-preserve=owner "$pkgdir/lib/systemd" "$pkgdir/usr/lib"
  cp -r --no-preserve=owner "$pkgdir/usr/sbin/$pkgname" "$pkgdir/usr/bin"

  rm -rf "$pkgdir/lib"
  rm -rf "$pkgdir/usr/sbin"
}

