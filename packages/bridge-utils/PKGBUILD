# This file is part of BlackArch Linux ( https://www.blackarch.org/ ).
# See COPYING for license details.

pkgname=bridge-utils
pkgver=1.7.1
pkgrel=4
pkgdesc='Utilities for configuring the Linux ethernet bridge.'
arch=('x86_64' 'aarch64')
url='https://wiki.linuxfoundation.org/networking/bridge'
license=('GPL-2.0-or-later')
depends=('glibc')
source=("https://mirrors.edge.kernel.org/pub/linux/utils/net/$pkgname/$pkgname-$pkgver.tar.xz")
sha512sums=('6a3824853bdaa1733c552973e6a5082280facdddc52c3feeb2c8c9375ba9469a5930b559092c9de1a73b464f388f019e147b45ac1e2d10ce8968057779fcd12c')

prepare() {
  cd "$pkgname-$pkgver"

  aclocal
  autoconf
}

build() {
  cd "$pkgname-$pkgver"

  ./configure \
    --prefix=/usr \
    --sbindir=/usr/bin \
    --sysconfdir=/etc \
    --with-linux-headers=/usr/include

  make
}

package() {
  cd "$pkgname-$pkgver"

  make DESTDIR="$pkgdir" install
}

