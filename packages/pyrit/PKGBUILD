# This file is part of BlackArch Linux ( https://www.blackarch.org/ ).
# See COPYING for license details.
#
# Old Maintainer: Levente Polyak <anthraxx[at]archlinux[dot]org>
# Old Contributor: Fabian Melters <melters [at] gmail [dot] com>

pkgname=pyrit
_pkgname=Pyrit
pkgver=0.5.0
pkgrel=6
groups=('blackarch' 'blackarch-cracker' 'blackarch-wireless')
pkgdesc='The famous WPA precomputed cracker.'
url='https://github.com/JPaulMora/Pyrit'
arch=('x86_64' 'aarch64')
license=('GPL3')
depends=('openssl' 'python2' 'zlib' 'libpcap')
optdepends=('scapy: packet capture analyzation support')
checkdepends=('scapy')
source=("$pkgname-$pkgver.tar.gz"::https://github.com/JPaulMora/Pyrit/archive/v${pkgver}.tar.gz
        'scapy-2.3.2.patch'
        'fix-broken-join.patch')
sha512sums=('912742cd7d9cd9231b9b3aa155a5170c0ba36c3865280423e100c1d12df34e093ee56305d8738d4f406abbfef9b1d540872872b8ef23d699466298d41289ad25'
            '99d4ae2159be2d829337f3481cbb6580da1c6a2d2b6b28c7ed17348daa983859b7cd6ba3ee8575cbcba16efa27bf230bac075599b1fa0b531178dbdc45f853b2'
            '4a8d868a4adc94d5c6dd3c467572edd49efe0e0f4b0ea61ccfe830499e7cdedb0da81b27166caac23332fe90505fe0bbcfb67e61fde632cd58ce290e46426d67')

prepare() {
  cd "$_pkgname-$pkgver"

  patch -p1 < "$srcdir/scapy-2.3.2.patch"
  patch -p1 -i ../fix-broken-join.patch # Fix tests (Debian)
}

build() {
  cd "$_pkgname-$pkgver"

  python2 setup.py build
}

package() {
  cd "$_pkgname-$pkgver"

  python2 setup.py install --root="$pkgdir" --prefix=/usr -O1 --skip-build

  install -Dm 644 README.md CHANGELOG -t "$pkgdir/usr/share/doc/$pkgname"
}

