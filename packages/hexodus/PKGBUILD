# This file is part of BlackArch Linux ( https://www.blackarch.org/ ).
# See COPYING for license details.

pkgname=hexodus
pkgver=72.69a96f6
pkgrel=2
_pyver=3.14
pkgdesc='Python framework project designed to enumerate and help in Active Directory attacks through Windows protocols like SMB, LDAP, WinRM and other.'
arch=('any')
groups=('blackarch' 'blackarch-windows' 'blackarch-exploitation'
        'blackarch-recon')
url='https://github.com/0xdsm/hexodus'
license=('MIT')
depends=('python' 'python-flask' 'python-ldap3' 'python-rich' 'impacket')
makedepends=('git' 'python-build' 'python-installer' 'python-setuptools'
             'python-wheel')
source=("git+https://github.com/0xdsm/$pkgname.git")
sha512sums=('SKIP')

pkgver() {
  cd $pkgname

  ( set -o pipefail
    git describe --long --tags --abbrev=7 2>/dev/null |
      sed 's/\([^-]*-g\)/r\1/;s/-/./g' ||
    printf "%s.%s" "$(git rev-list --count HEAD)" \
      "$(git rev-parse --short=7 HEAD)"
  )
}

build() {
  cd $pkgname

  python -m build --wheel --no-isolation
}

package() {
  cd $pkgname

  install -dm 755 "$pkgdir/usr/lib/python$_pyver/site-packages/$pkgname"

  python -m installer --destdir="$pkgdir" dist/*.whl

  rm -rf "$pkgdir/usr/lib/python$_pyver/site-packages/__pycache__/"
  #mv "$pkgdir/usr/lib/python$_pyver/site-packages/"{core.py,handlers,__init__.py,modules,parsers,protocols,webapp.py} \
  #  "$pkgdir/usr/lib/python$_pyver/site-packages/$pkgname/"
}

