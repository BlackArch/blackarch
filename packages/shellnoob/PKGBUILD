pkgname=shellnoob
pkgver=2.0.0.0.1
pkgrel=3
pkgdesc="A toolkit that eases the writing and debugging of shellcode"
url="https://github.com/reyammer/shellnoob"
arch=('any')
license=('MIT')
depends=('python2' 'binutils' 'gcc')
source=(https://github.com/reyammer/${pkgname}/archive/v${pkgver}.tar.gz)
sha256sums=('2849afc4d650157678d532005ab105c99cb4b14da286e5bad32ea00bf5310ea7')

prepare() {
  cd ${pkgname}-${pkgver}
  sed -e 's/env python$/env python2/g' -i shellnoob.py
  sed -e "s|README|/usr/share/doc/${pkgname}/README.md|g" -i shellnoob.py
}

package() {
  cd ${pkgname}-${pkgver}

  install -Dm 755 shellnoob.py "${pkgdir}/usr/lib/python2.7/site-packages/shellnoob.py"
  install -d "${pkgdir}/usr/bin"
  ln -s "/usr/lib/python2.7/site-packages/shellnoob.py" "${pkgdir}/usr/bin/snoob"

  install -Dm 644 COPYRIGHT "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
  install -Dm 644 README.md "${pkgdir}/usr/share/doc/${pkgname}/README.md"
  install -d "${pkgdir}/usr/share/doc/${pkgname}/samples/x86-linux"
  install -Dm 644 samples/x86-linux/* "${pkgdir}/usr/share/doc/${pkgname}/samples/x86-linux"
}

