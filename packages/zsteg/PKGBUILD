# This file is part of BlackArch Linux ( https://www.blackarch.org/ ).
# See COPYING for license details.

pkgname=zsteg
_gemname=zsteg
pkgver=v0.2.11.r0.g93cad73
pkgrel=1
pkgdesc='Detect stegano-hidden data in PNG and BMP.'
arch=('any')
groups=('blackarch' 'blackarch-stego' 'blackarch-anti-forensic')
url='https://github.com/zed-0xff/zsteg'
license=('MIT')
depends=('ruby' 'ruby-zpng' 'ruby-iostruct')
options=(!emptydirs)
source=("git+https://github.com/zed-0xff/$_gemname.git")
sha512sums=('SKIP')

pkgver() {
  cd $_gemname

  git describe --long --tags | sed 's/\([^-]*-g\)/r\1/;s/-/./g'
}

build() {
  cd $_gemname

  gem build $pkgname.gemspec
}

package() {
  cd $_gemname

  _gemdir="$(ruby -e'puts Gem.default_dir')"
  _release=$(gem build $pkgname.gemspec | grep Version | cut -d' ' -f4)

  gem install --ignore-dependencies --no-user-install --no-document \
    -i "$pkgdir/$_gemdir" -n "$pkgdir/usr/bin" "$_gemname-$_release.gem"

  rm "$pkgdir/$_gemdir/cache/$_gemname-$_release.gem"

  find "$pkgdir/$_gemdir/extensions/" -name *.so -delete
}
