# This file is part of BlackArch Linux ( http://blackarch.org ).
# See COPYING for license details.

pkgname='vfeed'
pkgver=36.a0fdf06
pkgrel=6
groups=('blackarch' 'blackarch-misc')
pkgdesc='Open Source Cross Linked and Aggregated Local Vulnerability Database main repository.'
url='http://www.toolswatch.org/vfeed'
arch=('any')
license=('BSD')
depends=('python2')
makedepends=('git')
source=('git+https://github.com/toolswatch/vFeed.git')
sha1sums=('SKIP')

pkgver() {
  cd "$srcdir/vFeed"

  echo $(git rev-list --count HEAD).$(git rev-parse --short HEAD)
}

package() {
  cd "$srcdir/vFeed"

  mkdir -p "$pkgdir/usr/bin/"
  mkdir -p "$pkgdir/usr/share/vfeed"
  mkdir -p "$pkgdir/usr/share/doc/vfeed"

  install -Dm644 LICENSE "$pkgdir/usr/share/licenses/vfeed/LICENSE"

  cp -a CHANGELOG.rst README.rst docs/* "$pkgdir/usr/share/doc/vfeed/"

  rm -rf LICENSE CHANGELOG.rst README.rst docs/

  cp -a --no-preserve=ownership * "$pkgdir/usr/share/vfeed"

  cat > "$pkgdir/usr/bin/vfeed" << EOF
#!/bin/sh
cd /usr/share/vfeed
exec python2 vfeedcli.py "\$@"
EOF

  chmod a+x "$pkgdir/usr/bin/vfeed"
}
