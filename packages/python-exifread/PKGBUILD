# This file is part of BlackArch Linux ( https://www.blackarch.org/ ).
# See COPYING for license details.

pkgbase=python-exifread
pkgname=('python2-exifread' 'python-exifread')
_pkgname=exif-py
pkgver=2.1.2
pkgrel=5
pkgdesc='Python library to extract EXIF data from tiff and jpeg files.'
arch=('any')
url='https://github.com/ianare/exif-py/'
license=('BSD')
depends=('python' )
makedepends=('python2-setuptools' 'python-setuptools')
source=("https://github.com/ianare/$_pkgname/archive/$pkgver.tar.gz")
sha512sums=('4636738fa304b4def3cce2adc03494b78318857a1172b2bfbf372e8b88fb2838b1dbb8aab1aefac02540b14d0a571d4ab524166191f539b61c239704bb50c063')

prepare() {
  cp -a "$_pkgname-$pkgver"{,-2}
}

build() {
  cd "$_pkgname-$pkgver-2"

  python2 setup.py build

  cd "$srcdir/$_pkgname-$pkgver"

  python setup.py build
}

package_python2-exifread() {
  depends=('python2')

  cd "$_pkgname-$pkgver-2"

  python2 setup.py install --root="$pkgdir" --optimize=1 --skip-build

  mv "$pkgdir/usr/bin/EXIF.py" "$pkgdir/usr/bin/EXIF.py2"
}

package_python-exifread() {
  depends=('python')

  cd "$_pkgname-$pkgver"

  python setup.py install --root="$pkgdir" --optimize=1 --skip-build
}

