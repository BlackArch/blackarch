# This file is part of BlackArch Linux ( https://www.blackarch.org/ ).
# See COPYING for license details.

pkgname=corscanner
pkgver=51.c8274f3
pkgrel=1
pkgdesc='Fast CORS misconfiguration vulnerabilities scanner.'
arch=('any')
groups=('blackarch' 'blackarch-webapp' 'blackarch-scanner')
url='https://github.com/chenjj/CORScanner'
license=('MIT')
depends=('python2' 'python2-requests' 'python2-requests-file' 'python2-gevent'
         'python2-tldextract' 'python2-argparse' 'python2-colorama')
makedepends=('git')
source=("$pkgname::git+https://github.com/chenjj/CORScanner.git")
sha512sums=('SKIP')

pkgver() {
  cd $pkgname

  echo $(git rev-list --count HEAD).$(git rev-parse --short HEAD)
}

package() {
  cd $pkgname

  install -dm 755 "$pkgdir/usr/bin"
  install -dm 755 "$pkgdir/usr/share/$pkgname"

  install -Dm 644 -t "$pkgdir/usr/share/doc/$pkgname/" README.md
  install -Dm 644 -t "$pkgdir/usr/share/licenses/$pkgname/" LICENSE

  rm -r README.md LICENSE requirements.txt images/

  cp -a --no-preserve=ownership * "$pkgdir/usr/share/$pkgname/"

  cat > "$pkgdir/usr/bin/$pkgname" << EOF
#!/bin/sh
cd /usr/share/$pkgname
exec python2 cors_scan.py "\$@"
EOF

  chmod +x "$pkgdir/usr/bin/$pkgname"
}

