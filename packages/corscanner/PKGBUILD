# This file is part of BlackArch Linux ( https://www.blackarch.org/ ).
# See COPYING for license details.

pkgname=corscanner
pkgver=1.0.1.r2.g593043f
pkgrel=1
epoch=1
pkgdesc='Fast CORS misconfiguration vulnerabilities scanner.'
arch=('any')
groups=('blackarch' 'blackarch-webapp' 'blackarch-scanner')
url='https://github.com/chenjj/CORScanner'
license=('MIT')
depends=('python' 'python-requests' 'python-requests-file' 'python-gevent'
         'python-tldextract' 'python-colorama' 'python-future')
makedepends=('git')
source=("$pkgname::git+https://github.com/chenjj/CORScanner.git")
sha512sums=('SKIP')

pkgver() {
  cd $pkgname

  ( set -o pipefail
    git describe --long --tags --abbrev=7 2>/dev/null |
      sed 's/\([^-]*-g\)/r\1/;s/-/./g' ||
    printf "%s.%s" "$(git rev-list --count HEAD)" \
      "$(git rev-parse --short=7 HEAD)"
  )
}

package() {
  cd $pkgname

  install -dm 755 "$pkgdir/usr/bin"
  install -dm 755 "$pkgdir/usr/share/$pkgname"

  install -Dm 644 -t "$pkgdir/usr/share/doc/$pkgname/" README.md
  install -Dm 644 -t "$pkgdir/usr/share/licenses/$pkgname/" LICENSE

  rm -r README.md LICENSE requirements.txt images/

  cp -a --no-preserve=ownership * "$pkgdir/usr/share/$pkgname/"

  cat > "$pkgdir/usr/bin/$pkgname" << EOF
#!/bin/sh
cd /usr/share/$pkgname
exec python cors_scan.py "\$@"
EOF

  chmod +x "$pkgdir/usr/bin/$pkgname"
}

