# This file is part of BlackArch Linux ( https://www.blackarch.org/ ).
# See COPYING for license details.

pkgname=corscanner
pkgver=71.ba44ea1
pkgrel=2
pkgdesc='Fast CORS misconfiguration vulnerabilities scanner.'
arch=('any')
groups=('blackarch' 'blackarch-webapp' 'blackarch-scanner')
url='https://github.com/chenjj/CORScanner'
license=('MIT')
depends=('python' 'python-requests' 'python-requests-file' 'python-gevent'
         'python-tldextract' 'python-argparse' 'python-colorama'
         'python-future')
makedepends=('git')
source=("$pkgname::git+https://github.com/chenjj/CORScanner.git")
sha512sums=('SKIP')

pkgver() {
  cd $pkgname

  echo $(git rev-list --count HEAD).$(git rev-parse --short HEAD)
}

package() {
  cd $pkgname

  install -dm 755 "$pkgdir/usr/bin"
  install -dm 755 "$pkgdir/usr/share/$pkgname"

  install -Dm 644 -t "$pkgdir/usr/share/doc/$pkgname/" README.md
  install -Dm 644 -t "$pkgdir/usr/share/licenses/$pkgname/" LICENSE

  rm -r README.md LICENSE requirements.txt images/

  cp -a --no-preserve=ownership * "$pkgdir/usr/share/$pkgname/"

  cat > "$pkgdir/usr/bin/$pkgname" << EOF
#!/bin/sh
cd /usr/share/$pkgname
exec python cors_scan.py "\$@"
EOF

  chmod +x "$pkgdir/usr/bin/$pkgname"
}

