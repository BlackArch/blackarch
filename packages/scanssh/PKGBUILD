# This file is part of BlackArch Linux ( https://www.blackarch.org/ ).
# See COPYING for license details.

pkgname=scanssh
pkgver=2.1
pkgrel=11
groups=('blackarch' 'blackarch-scanner')
pkgdesc='Fast SSH server and open proxy scanner.'
arch=('x86_64' 'aarch64')
depends=('glibc' 'libdnet' 'libevent' 'openssl' 'libpcap' 'libmd' 'openssl-1.1')
makedepends=('openssl-1.0' 'openssl-1.1')
url='http://www.monkey.org/~provos/scanssh/'
license=('GPL-1.0-or-later')
source=("http://www.monkey.org/~provos/$pkgname-$pkgver.tar.gz"
        'atomicio.c.patch'
        'md5.c.patch')
sha512sums=('1ddff6f8e51735bab28032c8d7cbb641ff0cd19a404a27ab489589ef1e30e3721dd486854eb4fef15368cc53f10c9c2650ffaf7fa7f767dc2202fcc5540ae59c'
            '348b675f3a566d5cea434ac43ce43c1ebc3f33674afe50c1965b9a84b6a023ef922ba16a18bc077d7b774924a19838b87e197730172f358ca81a52129f9d4b92'
            '2e0aca14f448a9e869463dca8141595b2b3c734539d38c5f21e87df19d2f5171a348da222ca15d749dd3829818ff3baa96559b305f44e4bc86f57f24a8761a9d')

prepare() {
  cd "$pkgname-$pkgver"

  patch -Np0 < "$srcdir/atomicio.c.patch"
  patch -Np0 < "$srcdir/md5.c.patch"
}

build() {
  cd "$pkgname-$pkgver"

  if [[ "$CARCH" == "i686" || "$CARCH" == "x86_64" ]];
  then
    ./configure --prefix=/usr
  else
    ./configure --build=arm --prefix=/usr
  fi

  make
}

package() {
  cd "$pkgname-$pkgver"

  install -dm 755 "$pkgdir/usr/share"

  make DESTDIR="$pkgdir" install

}

