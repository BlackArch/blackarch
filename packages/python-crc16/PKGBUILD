# This file is part of BlackArch Linux ( https://www.blackarch.org/ ).
# See COPYING for license details.

pkgbase=python-crc16
pkgname=('python2-crc16' 'python-crc16')
_pkgname=crc16
pkgver=0.1.1
pkgrel=3
pkgdesc='Library for calculating CRC16.'
arch=('x86_64' 'armv6h' 'armv7h' 'aarch64')
url='http://code.google.com/p/pycrc16'
makedepends=('python2-setuptools' 'python-setuptools')
license=('LGPL')
source=("https://pypi.python.org/packages/source/c/$_pkgname/$_pkgname-$pkgver.zip")
sha512sums=('54778e3e041cfbb7801a2eb1c64deb7e52798c6981a0f64983b5be3e3f166b97bde3e6e39f2fbfe4eb0876b8a439c429f01d4ea2181dabb41da64affd884a942')

prepare() {
  cp -a "$_pkgname-$pkgver" "$_pkgname-$pkgver-2"
}

build() {
  cd "$_pkgname-$pkgver-2"

  python2 setup.py build

  cd "$srcdir/$_pkgname-$pkgver"

  python setup.py build
}

package_python2-crc16() {
  depends=('python2')

  cd "$_pkgname-$pkgver-2"

  python2 setup.py install --prefix=/usr --root="$pkgdir" --optimize=1 \
    --skip-build
}

package_python-crc16() {
  depends=('python')

  cd "$_pkgname-$pkgver"

  python setup.py install --prefix=/usr --root="$pkgdir" --optimize=1 \
    --skip-build
}

