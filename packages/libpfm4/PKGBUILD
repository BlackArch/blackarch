# This file is part of BlackArch Linux ( http://blackarch.org ).
# See COPYING for license details.

pkgname=libpfm4
pkgver=4.9.0
conflicts=('libpfm3')
pkgrel=1
pkgdesc='Library to encode performance events for use by perf tool.'
arch=('x86_64' 'armv6h' 'armv7h' 'aarch64')
url="http://perfmon2.sourceforge.net/"
license=('MIT')
depends=('glibc')
source=("http://downloads.sourceforge.net/project/perfmon2/libpfm4/libpfm-${pkgver}.tar.gz")
sha512sums=('9e62556a5e8029b94383bd54176c5171fec7f919b6826d5d348837540922874c922eb0e01d7835c388ae28809c798f4b3da8ba751777767ce4a0e47ddab2b645')

build() {
  cd "$srcdir/libpfm-$pkgver"

  sed -i 's|PREFIX=/usr/local|PREFIX=/usr|g' config.mk
  sed -i 's|LDCONFIG=ldconfig|LDCONFIG=true|g' config.mk

  make
}

package() {
  cd "$srcdir/libpfm-$pkgver"

  make DESTDIR="$pkgdir" install
  make DESTDIR="$pkgdir" install_examples

  install -Dm644 COPYING "$pkgdir/usr/share/licenses/libpfm4/LICENSE"
}
