# This file is part of BlackArch Linux ( http://blackarch.org ).
# See COPYING for license details.

pkgname='libpfm4'
pkgver='4.8.0'
conflicts=('libpfm3')
pkgrel=1
pkgdesc='Library to encode performance events for use by perf tool.'
arch=('x86_64' 'armv6h' 'armv7h' 'aarch64')
url="http://perfmon2.sourceforge.net/"
license=('MIT')
depends=('glibc')
source=("http://downloads.sourceforge.net/project/perfmon2/libpfm4/libpfm-${pkgver}.tar.gz")
sha1sums=('dc707f48121d727507ba4f4d548ae60e6b15794e')

build() {
  cd "$srcdir/libpfm-$pkgver"

  sed -i 's|PREFIX=/usr/local|PREFIX=/usr|g' config.mk
  sed -i 's|LDCONFIG=ldconfig|LDCONFIG=true|g' config.mk

  make
}

package() {
  cd "$srcdir/libpfm-$pkgver"

  make DESTDIR="$pkgdir" install
  make DESTDIR="$pkgdir" install_examples

  install -Dm644 COPYING "$pkgdir/usr/share/licenses/libpfm4/LICENSE"
}
