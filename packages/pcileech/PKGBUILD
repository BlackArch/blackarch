# This file is part of BlackArch Linux ( https://www.blackarch.org/ ).
# See COPYING for license details.

pkgname=pcileech
pkgver=4.5
pkgrel=1
pkgdesc='Tool, which uses PCIe hardware devices to read and write from the target system memory.'
arch=('x86_64')
groups=('blackarch' 'blackarch-hardware' 'blackarch-reversing')
url='https://github.com/ufrisk/pcileech/releases'
license=('GPL3')
depends=('libusb' 'leechcore' 'linux-headers')
makedepends=('gcc')
source=("https://github.com/ufrisk/$pkgname/archive/v$pkgver.tar.gz"
        'makefile.patch')
sha512sums=('a06fccadba1fdc8afa33240b77e228940ee7ab2bf64b5e75790cac728cb393d100fe25ac1a8165345f819af92ced00b4203eb4d6fe20f51eee4b9ee0d6198e0d'
            '7835ada3bff46b93159cc563363a89af8692ab4c785ba8027b8e81915617872fbcd28531617517cc6a0c749b0df953847bc5ff37b05dfaa816d0ce6819927f70')

prepare() {
  cd "$pkgname-$pkgver/$pkgname"

  patch -p2 < "$srcdir/makefile.patch" Makefile
}

build() {
  cd "$pkgname-$pkgver/$pkgname"

  make

# Commented source lines are because https://github.com/ufrisk/pcileech/issues/111
#  cd ../pcileech_kmd/linux
#  make
}

package() {
  cd "$pkgname-$pkgver"

  install -dm 755 "$pkgdir/usr/lib/modules/extramodules-ARCH"

  install -Dm 755 "files/$pkgname" "$pkgdir/usr/bin/$pkgname"
#  install -m 644 pcileech_kmd/linux/pcileech_kmd.ko \
#    "$pkgdir/usr/lib/modules/extramodules-ARCH/pcileech_kmd.ko"
  install -Dm 644 -t "$pkgdir/usr/share/doc/$pkgname/" readme.md
  install -Dm 644 LICENSE "$pkgdir/usr/share/licenses/$pkgname/LICENSE"
}

