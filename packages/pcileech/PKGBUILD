# This file is part of BlackArch Linux ( https://www.blackarch.org/ ).
# See COPYING for license details.

pkgname=pcileech
pkgver=4.15
_pkgver=20220804
pkgrel=1
pkgdesc='Tool, which uses PCIe hardware devices to read and write from the target system memory.'
arch=('x86_64')
groups=('blackarch' 'blackarch-hardware' 'blackarch-reversing')
url='https://github.com/ufrisk/pcileech/releases'
license=('GPL3')
depends=('libusb' 'leechcore' 'linux-headers')
options=('!strip')
source=("https://github.com/ufrisk/pcileech/releases/download/v$pkgver/PCILeech_files_and_binaries_v$pkgver.0-linux_x64-$_pkgver.tar.gz")
sha512sums=('a19fa313e645081249cd97c93ab2728b591403a78d18f9e0c7b45b746df466a0f9a2a6e32950c929e4c53ac9ac4c11168613fca1b6e0991b6cc5ab4cce5d2c86')

package() {
  install -dm 755 "$pkgdir/usr/bin"
  install -dm 755 "$pkgdir/usr/share/$pkgname"

  cp -a --no-preserve=ownership * "$pkgdir/usr/share/$pkgname/"

  chmod 755 "$pkgdir/usr/share/$pkgname/$pkgname"

  cat > "$pkgdir/usr/bin/$pkgname" << EOF
#!/bin/sh
cd /usr/share/$pkgname
exec ./$pkgname "\$@"
EOF

  chmod +x "$pkgdir/usr/bin/$pkgname"
}

