# This file is part of BlackArch Linux ( https://www.blackarch.org/ ).
# See COPYING for license details.

pkgname=pcileech
pkgver=4.1
pkgrel=1
pkgdesc='Tool, which uses PCIe hardware devices to read and write from the target system memory.'
arch=('x86_64')
groups=('blackarch' 'blackarch-hardware' 'blackarch-reversing')
url='https://github.com/ufrisk/pcileech/releases'
license=('GPL3')
depends=('libusb' 'leechcore' 'linux-headers')
makedepends=('gcc' 'make')
source=("https://github.com/ufrisk/$pkgname/archive/v$pkgver.tar.gz"
        'makefile.patch')
sha512sums=('834cfa7fcf826ef949882ef29d9ba85ab13d4675955b5b6efcda5dcc997ec073542f40d0c8c69c3effa171e83022a58f64d9290bc0bf75e6a475cc160b08c940'
            '0850dbe4a07db5aaf2c2de7c6f1b4a3b4a8a6adc70d909ab0d26cdba45029bae34d66a190f4c2f8df035ccc97665cd6d83bc3750eab3b7374e15b93ef2397132')

prepare() {
  patch -p1 < makefile.patch
}

build() {
  cd "$pkgname-$pkgver/$pkgname"

  make

  cd ../pcileech_kmd/linux

  make
}

package() {
  cd "$pkgname-$pkgver"

  install -Dm 755 "files/$pkgname" "$pkgdir/usr/bin/$pkgname"
  install -Dm 644 pcileech_kmd/linux/pcileech_kmd.ko \
    "$pkgdir/lib/modules/extramodules-ARCH/pcileech_kmd.ko"
  install -Dm 644 -t "$pkgdir/usr/share/doc/$pkgname/" readme.md
  install -Dm 644 LICENSE "$pkgdir/usr/share/licenses/$pkgname/LICENSE"
}

