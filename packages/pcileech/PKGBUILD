# This file is part of BlackArch Linux ( https://www.blackarch.org/ ).
# See COPYING for license details.

pkgname=pcileech
pkgver=4.3
pkgrel=1
pkgdesc='Tool, which uses PCIe hardware devices to read and write from the target system memory.'
arch=('x86_64')
groups=('blackarch' 'blackarch-hardware' 'blackarch-reversing')
url='https://github.com/ufrisk/pcileech/releases'
license=('GPL3')
depends=('libusb' 'leechcore' 'linux-headers')
makedepends=('gcc' 'make')
source=("https://github.com/ufrisk/$pkgname/archive/v$pkgver.tar.gz"
        'makefile.patch')
sha512sums=('318dd768ba4aa4c813a24b1a36f5810f80002c101e9a1f41de7573ab60ac87d2d5e2e4a28778226954a060f2e5246b3022e611a1a91acd50c6a39ffafa9be103'
            'e4cbad7a7c90d856c8fa12a0da77fd47d861beab3cad27cfc761d93f0463f1f3e1f4a465ee31c6b365447ebdccce5f5568dec07bab7246d98f3f03b25b978817')

prepare() {
  patch -p1 < makefile.patch
}

build() {
  cd "$pkgname-$pkgver/$pkgname"

  make

  cd ../pcileech_kmd/linux

  make
}

package() {
  cd "$pkgname-$pkgver"

  install -dm 755 "$pkgdir/usr/lib/modules/extramodules-ARCH"

  install -Dm 755 "files/$pkgname" "$pkgdir/usr/bin/$pkgname"
  install -m 644 pcileech_kmd/linux/pcileech_kmd.ko \
    "$pkgdir/usr/lib/modules/extramodules-ARCH/pcileech_kmd.ko"
  install -Dm 644 -t "$pkgdir/usr/share/doc/$pkgname/" readme.md
  install -Dm 644 LICENSE "$pkgdir/usr/share/licenses/$pkgname/LICENSE"
}

