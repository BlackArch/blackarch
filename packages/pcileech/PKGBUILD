# This file is part of BlackArch Linux ( https://www.blackarch.org/ ).
# See COPYING for license details.

pkgname=pcileech
pkgver=4.19.5
_pkgver=4.19
__pkgver=20260224
pkgrel=1
pkgdesc='Tool, which uses PCIe hardware devices to read and write from the target system memory.'
arch=('x86_64' 'aarch64')
groups=('blackarch' 'blackarch-hardware' 'blackarch-reversing')
url='https://github.com/ufrisk/pcileech/releases'
license=('AGPL-3.0-only')
depends=('libusb' 'leechcore')
optdepends=('linux-headers' 'linux-lts-headers' 'linux-hardened-headers'
            'linux-rt-headers' 'linux-rt-lts-headers' 'linux-zen-headers')
options=('!strip')
source_x86_64=("https://github.com/ufrisk/pcileech/releases/download/v$_pkgver/PCILeech_files_and_binaries_v$pkgver-linux_x64-$__pkgver.tar.gz")
source_aarch64=("https://github.com/ufrisk/pcileech/releases/download/v$_pkgver/PCILeech_files_and_binaries_v$pkgver-linux_aarch64-$__pkgver.tar.gz")
sha512sums_x86_64=('ee59fb0396149a724586d88e462d193e73dd3708b2989bcdc55484b67d40a97a2ab0969cd52cbd22ec3a233f818b4316d509536883e523413c9268c67e827425')
sha512sums_aarch64=('d533271044746c182bcdeab519a9188f717f137de93c3b54bff5cf950bacb7dd10ce8ec32c1e5d3be07d4064b23325c6ee5226eed3fb65c352bee929a211208d')

package() {
  install -dm 755 "$pkgdir/usr/bin"
  install -dm 755 "$pkgdir/usr/share/$pkgname"

  cp -a --no-preserve=ownership * "$pkgdir/usr/share/$pkgname/"

  chmod 755 "$pkgdir/usr/share/$pkgname/$pkgname"

  cat > "$pkgdir/usr/bin/$pkgname" << EOF
#!/bin/sh
cd /usr/share/$pkgname
exec ./$pkgname "\$@"
EOF

  chmod +x "$pkgdir/usr/bin/$pkgname"

  rm -f \
    "$pkgdir/usr/share/$pkgname/PCILeech_files_and_binaries_v$pkgver-linux_x64-$__pkgver.tar.gz" \
    "$pkgdir/usr/share/$pkgname/PCILeech_files_and_binaries_v$pkgver-linux_aarch64-$__pkgver.tar.gz"
}

