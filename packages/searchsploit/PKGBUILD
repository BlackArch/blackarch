# This file is part of BlackArch Linux ( https://www.blackarch.org/ ).
# See COPYING for license details.

pkgname=searchsploit
_gitname=exploitdb
pkgver=2018.01.19
_pkgver=$(echo $pkgver | sed 's/\./\-/g')
pkgrel=1
groups=('blackarch' 'blackarch-exploitation')
pkgdesc='The official Exploit Database search tool.'
url='https://github.com/offensive-security/exploitdb'
arch=('any')
license=('GPL2')
depends=('git' 'libxml2')
options=('!strip')
source=("https://github.com/offensive-security/$_gitname/archive/$_pkgver.tar.gz")
sha512sums=('176677d164452f6ff16522d6a8119109a608d9f5a27cf6dfb7d162bac0b132a093ee2127bc7ce305171735575d3380f3ab519c8862921def2dd884ec6b7833a5')
install='searchsploit.install'

package() {
  install -dm 755 "$pkgdir/opt" "$pkgdir/usr/bin"

  cp -a --no-preserve=ownership $_gitname-$_pkgver "$pkgdir/opt/$pkgname"

  sed -i 's|path_array+=(.*)|path_array+=("/opt/searchsploit")|g' \
    "$pkgdir/opt/searchsploit/.searchsploit_rc"

  ln -s /opt/searchsploit/searchsploit "$pkgdir/usr/bin/$pkgname"

  chmod +x "$pkgdir/opt/$pkgname/$pkgname"
}

