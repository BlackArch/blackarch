# This file is part of BlackArch Linux ( https://www.blackarch.org/ ).
# See COPYING for license details.

pkgname=searchsploit
_gitname=exploitdb
pkgver=1812.fd2946662
_pkgver=$(echo $pkgver | sed 's/\./\-/g')
pkgrel=1
epoch=1
groups=('blackarch' 'blackarch-exploitation')
pkgdesc='The official Exploit Database search tool.'
url='https://github.com/offensive-security/exploitdb'
arch=('any')
license=('GPL2')
depends=('git' 'libxml2')
makedepends=('git')
options=('!strip')
source=("git+https://github.com/offensive-security/$_gitname.git")
sha512sums=('SKIP')
install='searchsploit.install'

pkgver() {
  cd $_gitname

  echo $(git rev-list --count HEAD).$(git rev-parse --short HEAD)
}

package() {
  install -dm 755 "$pkgdir/opt" "$pkgdir/usr/bin"

  cp -a --no-preserve=ownership $_gitname "$pkgdir/opt/$pkgname/"

  sed -i 's|path_array+=(.*)|path_array+=("/opt/searchsploit")|g' \
    "$pkgdir/opt/searchsploit/.searchsploit_rc"

  ln -s /opt/searchsploit/searchsploit "$pkgdir/usr/bin/$pkgname"

  chmod +x "$pkgdir/opt/$pkgname/$pkgname"
}

