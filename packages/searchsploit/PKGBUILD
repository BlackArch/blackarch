# This file is part of BlackArch Linux ( http://blackarch.org ).
# See COPYING for license details.

pkgname=searchsploit
_gitname='exploit-database'
pkgver=2018.09.08
pkgrel=1
groups=('blackarch' 'blackarch-exploitation')
pkgdesc='The official Exploit Database search tool.'
url='https://github.com/offensive-security/exploit-database'
arch=('any')
license=('GPL-2.0')
depends=('git' 'libxml2')
source=("https://github.com/offensive-security/${_gitname}/archive/${pkgver//./-}.tar.gz")
sha512sums=('08d0b2564b3561463f36de925aadbd28294a0862195b74c1639f3cf4f3e5ec414265ea43779a0e1466704b45e3620a2938b5a7ed34f2cb60433cce2b0b3279e5')
install='searchsploit.install'
options=('!strip')

package() {
  cd "${_gitname}-${pkgver//./-}"

  mkdir -p ${pkgdir}/opt ${pkgdir}/usr/bin

  cp -fr ${srcdir}/${_gitname}-${pkgver//./-} ${pkgdir}/opt/searchsploit

  sed -i 's|path_array+=(.*)|path_array+=("/opt/exploit-database")|g' \
    ${pkgdir}/opt/searchsploit/.searchsploit_rc

  ln -s /opt/searchsploit/searchsploit ${pkgdir}/usr/bin/searchsploit

  chmod a+x ${pkgdir}/opt/searchsploit/searchsploit
}
