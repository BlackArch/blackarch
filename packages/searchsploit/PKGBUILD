# This file is part of BlackArch Linux ( https://www.blackarch.org/ ).
# See COPYING for license details.

pkgname=searchsploit
_gitname=exploitdb
pkgver=2018.11.08
_pkgver=$(echo $pkgver | sed 's/\./\-/g')
pkgrel=1
groups=('blackarch' 'blackarch-exploitation')
pkgdesc='The official Exploit Database search tool.'
url='https://github.com/offensive-security/exploitdb'
arch=('any')
license=('GPL2')
depends=('git' 'libxml2')
options=('!strip')
source=("https://github.com/offensive-security/$_gitname/archive/$_pkgver.tar.gz")
sha512sums=('5e354b4181ee49d221be35f2e0634de1d9c66017a34244d9e2a22fecc84a5a6a06cb6964edd536300cb50894e442a5615b62b12e7e1d4bdc7306193d51e27fe7')
install='searchsploit.install'

package() {
  install -dm 755 "$pkgdir/opt" "$pkgdir/usr/bin"

  cp -a --no-preserve=ownership $_gitname-$_pkgver "$pkgdir/opt/$pkgname"

  sed -i 's|path_array+=(.*)|path_array+=("/opt/searchsploit")|g' \
    "$pkgdir/opt/searchsploit/.searchsploit_rc"

  ln -s /opt/searchsploit/searchsploit "$pkgdir/usr/bin/$pkgname"

  chmod a+x "$pkgdir/opt/$pkgname/$pkgname"
}

