# This file is part of BlackArch Linux ( http://blackarch.org ).
# See COPYING for license details.

pkgname='searchsploit'
_gitname='exploit-database'
pkgver='2018.05.15'
pkgrel=1
groups=('blackarch' 'blackarch-automation' 'blackarch-exploitation')
pkgdesc='The official Exploit Database search tool.'
url='https://github.com/offensive-security/exploit-database'
arch=('any')
license=('GPL-2.0')
depends=('git' 'libxml2')
source=("https://github.com/offensive-security/${_gitname}/archive/${pkgver//./-}.tar.gz")
sha1sums=('69c44e2407449c4d3a96eeda95120dcd83ecb5a6')
install='searchsploit.install'
options=('!strip')

package() {
  cd ${srcdir}/${_gitname}-${pkgver//./-}
  mkdir -p ${pkgdir}/opt ${pkgdir}/usr/bin

  cp -fr ${srcdir}/${_gitname}-${pkgver//./-} ${pkgdir}/opt/searchsploit

  sed -i 's|path_array+=(.*)|path_array+=("/opt/exploit-database")|g' \
    ${pkgdir}/opt/searchsploit/.searchsploit_rc

  ln -s /opt/searchsploit/searchsploit ${pkgdir}/usr/bin/searchsploit

  chmod a+x ${pkgdir}/opt/searchsploit/searchsploit
}
