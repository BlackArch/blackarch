# This file is part of BlackArch Linux ( https://www.blackarch.org/ ).
# See COPYING for license details.

pkgname=searchsploit
_gitname=exploitdb
pkgver=2018.01.19
_pkgver=$(echo $pkgver | sed 's/\./\-/g')
pkgrel=1
groups=('blackarch' 'blackarch-exploitation')
pkgdesc='The official Exploit Database search tool.'
url='https://github.com/offensive-security/exploitdb'
arch=('any')
license=('GPL2')
depends=('git' 'libxml2')
options=('!strip')
source=("https://github.com/offensive-security/$_gitname/archive/$_pkgver.tar.gz")
sha512sums=('85a1b756dd9fbbf97aeeb7b4af94ea4e14035b76f812b58a6d09849ef66c4b495be92e0c5c9efa40eba6a66976a8dcece1ac8d180312bdaab32b5cf0f63063ea')
install='searchsploit.install'

package() {
  install -dm 755 "$pkgdir/opt" "$pkgdir/usr/bin"

  cp -a --no-preserve=ownership $_gitname-$_pkgver "$pkgdir/opt/$pkgname"

  sed -i 's|path_array+=(.*)|path_array+=("/opt/searchsploit")|g' \
    "$pkgdir/opt/searchsploit/.searchsploit_rc"

  ln -s /opt/searchsploit/searchsploit "$pkgdir/usr/bin/$pkgname"

  chmod +x "$pkgdir/opt/$pkgname/$pkgname"
}

