# This file is part of BlackArch Linux ( http://blackarch.org ).
# See COPYING for license details.

pkgname='autonessus'
pkgver=24.7933022
pkgrel=1
pkgdesc='This script communicates with the Nessus API in an attempt to help with automating scans.'
groups=('blackarch' 'blackarch-automation')
arch=('any')
url='https://github.com/redteamsecurity/AutoNessus'
license=('GPL3')
depends=('python2')
makedepends=('git')
source=('git+https://github.com/redteamsecurity/AutoNessus.git')
sha1sums=('SKIP')

pkgver() {
  cd "$srcdir/AutoNessus"

  echo $(git rev-list --count HEAD).$(git rev-parse --short HEAD)
}

prepare() {
  cd "$srcdir/AutoNessus"

  sed -i 's|bin/python|bin/python2|' autoNessus.py
}

package() {
  cd "$srcdir/AutoNessus"

  install -Dm755 autoNessus.py "$pkgdir/usr/bin/autonessus"
  install -Dm644 -t "$pkgdir/usr/share/doc/autonessus/" README.md
  install -Dm644 LICENSE "$pkgdir/usr/share/licenses/autonessus/LICENSE"
}
