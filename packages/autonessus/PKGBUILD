# This file is part of BlackArch Linux ( https://www.blackarch.org/ ).
# See COPYING for license details.

pkgname=autonessus
pkgver=24.7933022
pkgrel=1
pkgdesc='This script communicates with the Nessus API in an attempt to help with automating scans.'
groups=('blackarch' 'blackarch-automation')
arch=('any')
url='https://github.com/redteamsecurity/AutoNessus'
license=('GPL3')
depends=('python2')
makedepends=('git')
source=('git+https://github.com/redteamsecurity/AutoNessus.git')
sha512sums=('SKIP')

pkgver() {
  cd AutoNessus

  echo $(git rev-list --count HEAD).$(git rev-parse --short HEAD)
}

prepare() {
  cd AutoNessus

  sed -i 's|bin/python|bin/python2|' autoNessus.py
}

package() {
  cd AutoNessus

  install -Dm 755 autoNessus.py "$pkgdir/usr/bin/$pkgname"
  install -Dm 644 -t "$pkgdir/usr/share/doc/$pkgname/" README.md
  install -Dm 644 LICENSE "$pkgdir/usr/share/licenses/$pkgname/LICENSE"
}

