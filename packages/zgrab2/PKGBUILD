# This file is part of BlackArch Linux ( https://www.blackarch.org/ ).
# See COPYING for license details.

pkgname=zgrab2
pkgver=v1.0.0.RC2.r19.gd011be6
pkgrel=1
epoch=1
pkgdesc='Fast Application Layer Scanner.'
arch=('x86_64' 'aarch64')
groups=('blackarch' 'blackarch-fingerprint' 'blackarch-scanner'
        'blackarch-recon')
url='https://github.com/zmap/zgrab2'
license=('APACHE' 'ISC' 'custom:unknown')
makedepends=('git' 'go')
source=("git+https://github.com/zmap/$pkgname.git")
sha512sums=('SKIP')

pkgver() {
  cd $pkgname

  ( set -o pipefail
    git describe --long --tags --abbrev=7 2>/dev/null |
      sed 's/\([^-]*-g\)/r\1/;s/-/./g' ||
    printf "%s.%s" "$(git rev-list --count HEAD)" \
      "$(git rev-parse --short=7 HEAD)"
  )
}

build() {
  cd $pkgname

  GOPATH="$srcdir" go mod download
  GOPATH="$srcdir" go build \
    -trimpath \
    -buildmode=pie \
    -mod=readonly \
    -modcacherw \
    -ldflags "-s -w" \
    -o $pkgname ./cmd/$pkgname
}

package() {
  cd $pkgname

  install -Dm 755 $pkgname "$pkgdir/usr/bin/$pkgname"
  install -Dm 644 README.md -t "$pkgdir/usr/share/doc/$pkgname"
  install -Dm 644 LICENSE -t "$pkgdir/usr/share/licenses/$pkgname"
}

