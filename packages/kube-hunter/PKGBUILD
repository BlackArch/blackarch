# This file is part of BlackArch Linux ( https://www.blackarch.org/ ).
# See COPYING for license details.

pkgname=kube-hunter
pkgver=402.09e9a0c
pkgrel=1
pkgdesc='Hunt for security weaknesses in Kubernetes clusters.'
arch=('any')
groups=('blackarch' 'blackarch-scanner')
url='https://github.com/aquasecurity/kube-hunter'
license=('Apache')
depends=('python2' 'python2-netaddr' 'python2-netifaces' 'python2-enum34'
         'python2-scapy' 'python2-requests' 'python2-prettytable'
         'python2-urllib3' 'python2-ruamel-yaml')
makedepends=('git')
source=("git+https://github.com/aquasecurity/$pkgname.git")
sha512sums=('SKIP')

pkgver() {
  cd $pkgname

  echo $(git rev-list --count HEAD).$(git rev-parse --short HEAD)
}

package() {
  cd $pkgname

  install -dm 755 "$pkgdir/usr/bin/"
  install -dm 755 "$pkgdir/usr/share/$pkgname"

  install -Dm 644 LICENSE "$pkgdir/usr/share/licenses/$pkgname/LICENSE"
  install -Dm 644 README.md "$pkgdir/usr/share/doc/$pkgname/README"

  cp -a --no-preserve=ownership * "$pkgdir/usr/share/$pkgname/"

  cat > "$pkgdir/usr/bin/$pkgname" << EOF
#!/bin/sh
cd /usr/share/$pkgname
exec python2 $pkgname.py "\$@"
EOF

  chmod +x "$pkgdir/usr/bin/$pkgname"
}

