# This file is part of BlackArch Linux ( https://www.blackarch.org/ ).
# See COPYING for license details.

pkgname=kube-hunter
pkgver=626.78e1672
pkgrel=1
pkgdesc='Hunt for security weaknesses in Kubernetes clusters.'
arch=('any')
groups=('blackarch' 'blackarch-scanner')
url='https://github.com/aquasecurity/kube-hunter'
license=('Apache')
depends=('python' 'python-netaddr' 'python-netifaces' 'python-scapy'
         'python-requests' 'python-prettytable' 'python-urllib3'
         'python-ruamel-yaml' 'python-future' 'python-packaging')
makedepends=('git')
source=("git+https://github.com/aquasecurity/$pkgname.git"
        'setup.cfg.patch')
sha512sums=('SKIP'
            'f99733000c3920ad3b0d39bf64e4b3a7a09a03eec54fa82f875e97f65b1182d728566abb32de88aa5af13d962f1923cde10baba4bc901c0a9d444125dbcfbfbf')

pkgver() {
  cd $pkgname

  echo $(git rev-list --count HEAD).$(git rev-parse --short HEAD)
}

prepare() {
  patch -p1 < setup.cfg.patch
}

build() {
  cd $pkgname

  python setup.py build
}

package() {
  cd $pkgname

  python setup.py install --root="$pkgdir" --prefix=/usr -O1 --skip-build

  install -Dm 644 -t "$pkgdir/usr/share/doc/$pkgname/" README.md
  install -Dm 644 LICENSE "$pkgdir/usr/share/licenses/$pkgname/LICENSE"

  rm -rf "$pkgdir/usr/lib/python3.8/site-packages/tests"
}

