# This file is part of BlackArch Linux ( http://blackarch.org ).
# See COPYING for license details.

pkgname='sulley'
pkgver=1.0.e72d343
pkgrel=1
epoch=3
groups=('blackarch' 'blackarch-fuzzer')
pkgdesc='A pure-python fully automated and unattended fuzzing framework.'
arch=('any')
url='https://github.com/OpenRCE/sulley/'
license=('GPL2')
makedepends=('git')
depends=('python2' 'libpcap' 'python2-pcapy' 'impacket')
source=('git+http://github.com/OpenRCE/sulley.git')
sha1sums=('SKIP')

pkgver() {
  cd "$srcdir/sulley"

  # Grabbing major-version from install script
  printf "%s.%s" \
    $(grep -i 'define PRODUCT_VERSION' installer/Nullsoft\ Installer\ Script.nsi | cut -d'"' -f2) \
    "$(git rev-parse --short HEAD)"
}

prepare() {
  cd "$srcdir/sulley"
  grep -iRl '/usr/bin/python' "$srcdir/sulley" | xargs sed -i 's|#!.*/usr/bin/python$|#!/usr/bin/python2|'
}

package() {
  cd "$srcdir/sulley"

  mkdir -p "$pkgdir/usr/share/sulley"

  cp -a --no-preserve=ownership * "$pkgdir/usr/share/sulley/"
}
