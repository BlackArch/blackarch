pkgname=foremost
pkgver=1.5.7
pkgrel=4
groups=('blackarch' 'blackarch-forensics')
pkgdesc="A console program to recover files based on their headers, footers, and internal data structures"
arch=('i686' 'x86_64')
url='http://foremost.sourceforge.net/'
license=('custom')
makedepends=('patch')
backup=(etc/foremost.conf)
source=(http://foremost.sourceforge.net/pkg/foremost-$pkgver.tar.gz
        http://kambing.ui.edu/gentoo-portage/app-forensics/foremost/files/foremost-1.4-config-location.patch
        foremost-makefile.diff)
md5sums=('860119c49665c2a3fb2b0b1d3dbad02a'
         '2666ad901eab09061c265b706494e140'
         '0e6b7f653b6b7811158ba7732dead04e')

prepare() {
  cd ${srcdir}/foremost-$pkgver
  patch -Np1 -i ${srcdir}/foremost-1.4-config-location.patch
  patch -Np0 -i ${srcdir}/foremost-makefile.diff
}

build() {
  cd ${srcdir}/foremost-$pkgver
  make
}

package () {
  cd ${srcdir}/foremost-$pkgver

  # Make base directories.
  install -dm755 ${pkgdir}/{etc,{usr/bin,usr/share/man/man8}}

  make DESTDIR=${pkgdir} install

  # Install license.
  ./foremost -V > COPYRIGHT
  install -D -m644 COPYRIGHT ${pkgdir}/usr/share/licenses/foremost/COPYRIGHT
}
