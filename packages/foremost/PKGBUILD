# This file is part of BlackArch Linux ( http://blackarch.org ).
# See COPYING for license details.

pkgname=foremost
pkgver=1.5.7
pkgrel=5
groups=('blackarch' 'blackarch-forensic')
pkgdesc="A console program to recover files based on their headers, footers, and internal data structures"
arch=('x86_64' 'armv6h' 'armv7h' 'aarch64')
url='http://foremost.sourceforge.net/'
license=('custom')
makedepends=('patch')
backup=('etc/foremost.conf')
source=("http://foremost.sourceforge.net/pkg/foremost-${pkgver}.tar.gz"
        'http://kambing.ui.edu/gentoo-portage/app-forensics/foremost/files/foremost-1.4-config-location.patch'
        'foremost-makefile.diff')
sha512sums=('8827c29d52496783be26374f3943eb26a154d842f34e50fb489f87b3a5045bf85f1e44d7d8d8b12b2355ba3fe4b06a0db979cc22c0f431593c5976001eb931ab'
            '7b6dcc18cba4676ef4d54b87c9c31747754ecbebd9424340b2dd059495c9c6cec11513307fff802a336a99bc07c4bcd2ffcfad1c77ccdc49277ef7e40e30029e'
            '94860b3d355ff23af25ac42cef6ff6e12d66cb27d1cf86df93d6fd546654a741c7dd9f24922fddd16ade0f28cacf912d8ebbacf819c9954a675b30eae644b405')

prepare() {
  cd "$srcdir/foremost-$pkgver"

  patch -Np1 -i $srcdir/foremost-1.4-config-location.patch
  patch -Np0 -i $srcdir/foremost-makefile.diff
}

build() {
  cd "$srcdir/foremost-$pkgver"

  make
}

package () {
  cd "$srcdir/foremost-$pkgver"

  install -dm 755 $pkgdir/{etc,{usr/bin,usr/share/man/man8}}

  make DESTDIR="$pkgdir" install

  ./foremost -V > COPYRIGHT
  install -Dm644 COPYRIGHT "$pkgdir/usr/share/licenses/foremost/COPYRIGHT"
}
