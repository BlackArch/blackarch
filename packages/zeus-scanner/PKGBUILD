# This file is part of BlackArch Linux ( https://www.blackarch.org/ ).
# See COPYING for license details.

pkgname=zeus-scanner
pkgver=414.21b8756
pkgrel=3
pkgdesc='Advanced dork searching utility.'
groups=('blackarch' 'blackarch-recon')
arch=('any')
url='https://github.com/Ekultek/Zeus-Scanner'
license=('GPL3')
depends=('python' 'python-requests' 'python-python-nmap' 'python-selenium'
         'python-whichcraft' 'python-pyvirtualdisplay' 'python-beautifulsoup4'
         'python-lxml' 'python-psutil' 'nmap')
makedepends=('git')
source=("$pkgname::git+https://github.com/Ekultek/Zeus-Scanner.git")
sha512sums=('SKIP')

pkgver() {
  cd $pkgname

  echo $(git rev-list --count HEAD).$(git rev-parse --short HEAD)
}

package() {
  cd $pkgname

  install -dm 755 "$pkgdir/usr/bin"
  install -dm 755 "$pkgdir/usr/share/$pkgname"

  install -Dm 644 -t "$pkgdir/usr/share/doc/$pkgname/" README.md

  rm *.md

  cp -a * "$pkgdir/usr/share/$pkgname/"

  cat > "$pkgdir/usr/bin/$pkgname" << EOF
#!/bin/sh
cd /usr/share/$pkgname
exec python zeus.py "\$@"
EOF

  chmod +x "$pkgdir/usr/bin/$pkgname"
}

