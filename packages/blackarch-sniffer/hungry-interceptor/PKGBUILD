# This file is part of BlackArch Linux ( https://www.blackarch.org/ ).
# See COPYING for license details.

pkgname=hungry-interceptor
pkgver=391.1aea7f3
pkgrel=3
groups=('blackarch' 'blackarch-sniffer')
pkgdesc='Intercepts data, does something with it, stores it.'
arch=('any')
url='https://github.com/nbuechler/hungry-interceptor'
license=('GPL3')
depends=('python2' 'python2-flask-wtf' 'python2-flask-cors' 'python2-pymongo'
         'python2-flask-pymongo' 'python2-py2neo')
makedepends=('git')
source=("git+https://github.com/nbuechler/$pkgname.git")
sha512sums=('SKIP')

pkgver() {
  cd $pkgname

  echo $(git rev-list --count HEAD).$(git rev-parse --short HEAD)
}

package() {
  cd $pkgname

  install -dm 755 "$pkgdir/usr/bin"
  install -dm 755 "$pkgdir/usr/share/$pkgname"

  install -Dm 644 -t "$pkgdir/usr/share/doc/$pkgname/" README.md
  install -Dm 644 LICENSE "$pkgdir/usr/share/licenses/$pkgname/LICENSE"

  rm README.md LICENSE

  cp -a * "$pkgdir/usr/share/$pkgname/"

  cat > "$pkgdir/usr/bin/$pkgname" << EOF
#!/bin/sh
cd /usr/share/$pkgname/app
exec python2 runserver.py "\$@"
EOF

  chmod +x "$pkgdir/usr/bin/$pkgname"
}

