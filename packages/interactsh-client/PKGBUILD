# This file is part of BlackArch Linux ( https://www.blackarch.org/ ).
# See COPYING for license details.

pkgname=interactsh-client
_pkgname=interactsh
pkgver=v1.2.4.r2.g6e4c85b
pkgrel=1
pkgdesc='Open-Source Solution for Out of band Data Extraction.'
arch=('x86_64' 'aarch64')
groups=('blackarch' 'blackarch-webapp')
url='https://github.com/projectdiscovery/interactsh'
license=('MIT')
depends=('glibc')
makedepends=('git' 'go')
source=("$pkgname::git+https://github.com/projectdiscovery/$_pkgname.git")
sha512sums=('SKIP')

pkgver() {
  cd $pkgname

  git describe --long --tags | sed 's/\([^-]*-g\)/r\1/;s/-/./g'
}

build() {
  cd $pkgname

  GOPATH="$srcdir" go mod download
  GOPATH="$srcdir" go build \
    -trimpath \
    -buildmode=pie \
    -mod=readonly \
    -modcacherw \
    -ldflags "-s -w" \
    -v \
    -o $pkgname cmd/$pkgname/main.go

}

package() {
  cd $pkgname

  install -Dm 755 $pkgname "$pkgdir/usr/bin/$pkgname"
  install -Dm 644 -t "$pkgdir/usr/share/doc/$pkgname/" README.md

  install -Dm 644 LICENSE.md "$pkgdir/usr/share/licenses/$pkgname/LICENSE"
}

