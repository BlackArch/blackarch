# This file is part of BlackArch Linux ( http://blackarch.org ).
# See COPYING for license details.

pkgname=dnschef
pkgver=0.3
pkgrel=2
groups=('blackarch' 'blackarch-proxy')
pkgdesc='A highly configurable DNS proxy for pentesters.'
url='http://thesprawl.org/projects/dnschef/'
arch=('any')
depends=('python2')
license=('GPL3')
source=("https://thesprawl.org/media/projects/dnschef-${pkgver}.zip")
DLAGENTS=("https::/usr/bin/curl -k -o %o %u")
sha512sums=('472429f1ff562cdc4eca814574508eaf197e9fc9fe8ad03fe7807f0b4c9bd325b8281575e91dac10a78f876b4ceeb4acec42a19ab41e285c2228eb9dc5cf4645')

prepare() {
  cd "$srcdir/dnschef-$pkgver"

  sed -i 's/env python/env python2/' *.py
}

package() {
  cd "$srcdir/dnschef-$pkgver"

  install -dm 755 "$pkgdir/usr/bin"
  install -dm 755 "$pkgdir/usr/share/dnschef"

  cp -a * "$pkgdir/usr/share/dnschef"

  cat > "$pkgdir/usr/bin/dnschef" << EOF
#!/bin/sh
cd /usr/share/dnschef
exec python2 dnschef.py "\$@"
EOF

  chmod +x "$pkgdir/usr/bin/dnschef"
}
