pkgname=jast
pkgver=r14.bb6c8bd
pkgrel=1
pkgdesc="Just Another Screenshot Tool"
arch=('any')
url="https://github.com/mikehacksthings/jast"
license=('custom::unknown')
groups=('blackarch'  'blackarch-webapp'  'blackarch-recon'  'blackarch-misc')
depends=('python' 'python-selenium' 'python-requests' 'python-docopt'
         'python-urllib3')
makedepends=('git')
provides=('jast')
source=("${pkgname}::git+https://github.com/mikehacksthings/jast.git")
sha512sums=('SKIP')

pkgver() {
  cd "${pkgname}"
  echo $(git rev-list --count HEAD).$(git rev-parse --short HEAD)
}

package() {
  cd "${pkgname}"

  # Install directories
  install -dm 755 "${pkgdir}/usr/bin"
  install -dm 755 "${pkgdir}/usr/share/${pkgname}"
  
  # Install the stuff in proper directories
  install -Dm 755 -t "${pkgdir}/usr/share/${pkgname}" *.py
  install -Dm 644 -t "${pkgdir}/usr/share/doc/${pkgname}" README.md
  
  cat > "${pkgdir}/usr/bin/${pkgname}" << EOF
#!/bin/sh
cd /usr/share/${pkgname}
exec python3 jast.py "\${@}"
EOF
  chmod +x "${pkgdir}/usr/bin/${pkgname}"
}
