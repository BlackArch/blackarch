# This file is part of BlackArch Linux ( http://blackarch.org ).
# See COPYING for license details.

pkgname=bfuzz
_pkgname=BFuzz
pkgver=1.4
pkgrel=1
pkgdesc='Input based fuzzer tool for browsers.'
arch=('any')
groups=('blackarch' 'blackarch-fuzzer')
url='https://github.com/RootUp/BFuzz'
license=('custom:unknown')
depends=('python2')
source=("https://github.com/RootUp/$_pkgname/archive/Beta$pkgver.tar.gz")
sha512sums=('79b717b1db4fd933273e3c55fda14787f73d732939db1f1785ac801da5cbece13cae5048da9e6affd58aeaf7122efb10340bc177e6e56959a45eb95670ac43ac')

package() {
  cd "$_pkgname-Beta$pkgver"

  install -dm 755 "$pkgdir/usr/share/$pkgname"
  install -dm 755 "$pkgdir/usr/bin"

  install -Dm 644 README.md "$pkgdir/usr/share/doc/$pkgname/README"

  cp -a --no-preserve=ownership * "$pkgdir/usr/share/$pkgname/"

  cat > "$pkgdir/usr/bin/$pkgname" << EOF
#!/bin/sh
cd /usr/share/$pkgname
exec python2 $_pkgname.py "\$@"
EOF

  cat > "$pkgdir/usr/bin/$pkgname-gen" << EOF
#!/bin/sh
if [ \$UID -eq 0 ]; then
  cd /usr/share/$pkgname
  exec sh generate.sh "\${@}"
else
  echo "You need be root."
fi
EOF

  chmod +x "$pkgdir/usr/bin/$pkgname" "$pkgdir/usr/bin/$pkgname-gen"
}

