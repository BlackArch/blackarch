# This file is part of BlackArch Linux ( https://www.blackarch.org/ ).
# See COPYING for license details.
#
# NOTE: this package was already offered by us (as many others). community took
# it and now dropped it ... so we gonna keep it for a while.
#
# Old Maintainer: NicoHood <archlinux {cat} nicohood {dog} de>
# PGP ID: 97312D5EB9D7AE7D0BD4307351DAE9B7C1AE9161
# Old Contributor: Xiaoxiao Pu <i@xiaoxiao.im>
pkgname=create_ap
pkgver=265.462c09f
pkgrel=1
pkgdesc='A shell script to create a NATed/Bridged Software Access Point.'
groups=('blackarch' 'blackarch-wireless')
arch=('any')
url='https://github.com/oblique/create_ap'
license=('BSD')
depends=('bash' 'hostapd' 'iproute2' 'iw' 'dnsmasq' 'iptables' 'util-linux'
         'procps-ng' 'which')
makedepends=('git')
optdepends=('haveged: boost low entropy')
backup=('etc/create_ap.conf')
source=("git+https://github.com/oblique/$pkgname.git")
sha512sums=('SKIP')

pkgver() {
  cd $pkgname

  echo $(git rev-list --count HEAD).$(git rev-parse --short HEAD)
}

package() {
  make -C "$pkgname" DESTDIR="$pkgdir" install

  install -Dm 644 "$pkgname/LICENSE" \
    "$pkgdir/usr/share/licenses/$pkgname/LICENSE"
}

