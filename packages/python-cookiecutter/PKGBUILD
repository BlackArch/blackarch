# This file is part of BlackArch Linux ( https://www.blackarch.org/ ).
# See COPYING for license details.

pkgbase=python-cookiecutter
pkgname=('python2-cookiecutter' 'python-cookiecutter')
_pkgname=cookiecutter
pkgver=1.7.2
pkgrel=2
pkgdesc='A command-line utility that creates projects from cookiecutters (project templates).'
arch=('any')
url='https://github.com/audreyr/cookiecutter'
license=('BSD')
makedepends=('python2-setuptools' 'python-setuptools')
source=("https://files.pythonhosted.org/packages/f2/6d/eb4edcf99c3dd17b6d931c5b0b19e093a7147400840f3d8985901c27029f/$_pkgname-$pkgver.tar.gz")
sha512sums=('8fb6db75703a2c6276d275817b81c0e5d5f15226236e8c5442fbda6198aaec19c3fd68c030f79f6bca55258c0c248660b32d1344f7d6b06ecf5563b43474d877')

prepare() {
  cp -a "$_pkgname-$pkgver"{,-2}

  cd "$_pkgname-$pkgver-2"

  grep -iRl 'python' * | xargs \
    sed -i 's|#!.*/usr/bin/python|#!/usr/bin/python2|;s|#!.*/usr/bin/env python$|#!/usr/bin/env python2|'
}

package_python2-cookiecutter() {
  depends=('python2-future' 'python2-jinja' 'python2-yaml' 'python2-binaryornot'
           'python2-click' 'python2-poyo' 'python2-requests'
           'python2-jinja-time' 'python2-whichcraft')

  cd "$_pkgname-$pkgver-2"

  python2 setup.py install --prefix=/usr --root="$pkgdir" --optimize=1 \
    --skip-build

  mv "$pkgdir/usr/bin/$_pkgname" "$pkgdir/usr/bin/${_pkgname}2"
}

package_python-cookiecutter() {
  depends=('python-future' 'python-jinja' 'python-yaml' 'python-binaryornot'
           'python-click' 'python-poyo' 'python-requests' 'python-jinja-time'
           'python-whichcraft')

  cd "$_pkgname-$pkgver"

  python setup.py install --prefix=/usr --root="$pkgdir" --optimize=1 \
    --skip-build
}

