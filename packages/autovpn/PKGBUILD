# This file is part of BlackArch Linux ( https://www.blackarch.org/ ).
# See COPYING for license details.

pkgname=autovpn
pkgver=17.4e19a80
pkgrel=1
pkgdesc='Easily connect to a VPN in a country of your choice.'
groups=('blackarch' 'blackarch-automation' 'blackarch-misc')
arch=('x86_64' 'armv6h' 'armv7h' 'aarch64')
url='https://github.com/adtac/autovpn'
license=('GPL3')
depends=('go' 'openvpn')
makedepends=('git')
source=('git+https://github.com/adtac/autovpn.git')
sha512sums=('SKIP')

pkgver() {
  cd $pkgname

  echo $(git rev-list --count HEAD).$(git rev-parse --short HEAD)
}

build() {
  cd $pkgname

  go build autovpn.go
}

package() {
  cd $pkgname

  install -Dm 755 autovpn "$pkgdir/usr/bin/$pkgname"
  install -Dm 644 -t "$pkgdir/usr/share/doc/$pkgname/" README.md
  install -Dm 644 LICENSE "$pkgdir/usr/share/licenses/$pkgname/LICENSE"
}

