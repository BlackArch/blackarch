# This file is part of BlackArch Linux ( http://blackarch.org ).
# See COPYING for license details.

pkgname=('metasploit' 'metasploit-msfupdate')
pkgver=37201.b868f7c
pkgrel=1
epoch=4
groups=('blackarch' 'blackarch-exploitation' 'blackarch-fuzzer'
        'blackarch-scanner' 'blackarch-recon' 'blackarch-networking')
arch=('any')
makedepends=('git')
url='http://www.metasploit.com'
license=('BSD')
options=('!strip')
source=('git+https://github.com/rapid7/metasploit-framework')
sha1sums=('SKIP')
install='metasploit.install'

pkgver() {
  cd "metasploit-framework"

  echo $(git rev-list --count HEAD).$(git rev-parse --short HEAD)
}

# The metasploit package contains everything except the git files/dir.
package_metasploit() {

  pkgdesc='An open source platform that supports vulnerability research, exploit development and the creation of custom security tools representing the largest collection of quality-assured exploits.'
  depends=('ruby' 'ruby-bundler' 'java-environment' 'postgresql' 'libpcap'
           'make' 'tigervnc' 'libxslt')
  optdepends=('dradis: dradis database system'
              'metasploit-msfupdate: In order to use the msfupdate tool.')

  cd "$srcdir/metasploit-framework"

  # Base directories.
  mkdir -p "$pkgdir/usr/bin"
  mkdir -p "$pkgdir/etc"
  mkdir -p "$pkgdir/usr/share/metasploit"
  mkdir -p "$pkgdir/usr/share/doc/metasploit"

  # Share.
  cp -a documentation/* "$pkgdir/usr/share/doc/metasploit"
  cp -a README.md "$pkgdir/usr/share/doc/metasploit"

  # Source + Bin + dot files
  cp -a * "$pkgdir/usr/share/metasploit"
  cp -a .r* .m* .s* .t* .y* "$pkgdir/usr/share/metasploit"
  rm -rf "$pkgdir/usr/share/metasploit"/{README.md,documentation}

  # Conform to the FHS (config/ needed for msfupdate).
  cp -r "$pkgdir/usr/share/metasploit/config" "$pkgdir/etc/metasploit"
  ln -s /etc/metasploit "$pkgdir/usr/share/metasploit/config"

  # Bin.
  for b in      \
    msfbinscan  \
    msfconsole  \
    msfd        \
    msfelfscan  \
    msfmachscan \
    msfpescan   \
    msfrop      \
    msfrpc      \
    msfrpcd     \
    msfupdate   \
    msfvenom
  do
  cat > "$pkgdir/usr/bin/$b" << EOF
#!/bin/sh
exec ruby /usr/share/metasploit/$b "\$@"
EOF
done

  chmod +x "$pkgdir"/usr/bin/*

  # not created automatically for some reason
  install -Dm666 /dev/null "$pkgdir/usr/share/metasploit/log/production.log"
}

# This package contains only the git files/dir.
# It has no dependencies being an optional one of metasploit.
package_metasploit-msfupdate() {
  cd "$srcdir"

  pkgdesc='Data necessary for the msfupdate tool of the metasploit framework.'
  install=
  # Assumption: People using msfupdate are able to read an optdepend statement.
  # Proof by:   Removing this package from groups and wait for bug reports.
  groups=()

  cd "$srcdir/metasploit-framework"

  mkdir -p "$pkgdir/usr/share/metasploit"

  cp -a .g* "$pkgdir/usr/share/metasploit"
}
