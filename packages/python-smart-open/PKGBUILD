# This file is part of BlackArch Linux ( https://www.blackarch.org/ ).
# See COPYING for license details.

pkgbase=python-smart-open
pkgname=('python2-smart-open' 'python-smart-open')
_pkgname=smart_open
pkgver=2.2.1
pkgrel=1
pkgdesc='Utils for streaming large files (S3, HDFS, gzip, bz2...).'
arch=('any')
url='https://pypi.org/project/smart-open/#files'
license=('MIT')
makedepends=('python2-setuptools' 'python-setuptools')
options=(!emptydirs)
source=("https://files.pythonhosted.org/packages/53/9c/2ee26604648f92251f26a00a79fd164079163693f53792a3ba99f6152349/smart_open-$pkgver.tar.gz")
sha512sums=('923b73f9996e01483cca3b8f85bfd44f5f40bc303fc8b7620ac15bc827196035f04552e5612bb8a5abc2c6e2b1209b9701c203d5f17a012837650d8e747dfa3d')

prepare() {
  cp -a "$_pkgname-$pkgver"{,-2}
}

build() {
  cd "$_pkgname-$pkgver-2"

  python2 setup.py build

  cd "$srcdir/$_pkgname-$pkgver"

  python setup.py build
}

package_python2-smart-open() {
  depends=('python2' 'python2-boto' 'python2-requests' 'python2-boto3')

  cd "$_pkgname-$pkgver-2"

  python2 setup.py install --root="$pkgdir" --prefix=/usr -O1 --skip-build
}

package_python-smart-open() {
  depends=('python' 'python-boto' 'python-requests' 'python-boto3')

  cd "$_pkgname-$pkgver"

  python setup.py install --root="$pkgdir" --prefix=/usr -O1 --skip-build
}

