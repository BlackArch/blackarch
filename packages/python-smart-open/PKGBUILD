# This file is part of BlackArch Linux ( https://www.blackarch.org/ ).
# See COPYING for license details.

pkgbase=python-smart-open
pkgname=('python2-smart-open' 'python-smart-open')
_pkgname=smart_open
pkgver=5.1.0
pkgrel=1
pkgdesc='Utils for streaming large files (S3, HDFS, gzip, bz2...).'
arch=('any')
url='https://pypi.org/project/smart-open/#files'
license=('MIT')
makedepends=('python2-setuptools' 'python-setuptools')
options=(!emptydirs)
source=("https://files.pythonhosted.org/packages/46/a4/04cf6652e7d372096ef08d2b07f95883cd6db136ead49d36d7734f5ffb42/smart_open-$pkgver.tar.gz")
sha512sums=('4072e8fee212f47561dd4a4a4d0b2830c179e1eb242da17a885c69706dbb65fb1a346d57d862946b76dbc92f928d756e4d935b96155e2c1aec077bac2aa17062')

prepare() {
  cp -a "$_pkgname-$pkgver"{,-2}
}

build() {
  cd "$_pkgname-$pkgver-2"

  python2 setup.py build

  cd "$srcdir/$_pkgname-$pkgver"

  python setup.py build
}

package_python2-smart-open() {
  depends=('python2' 'python2-boto' 'python2-requests' 'python2-boto3')

  cd "$_pkgname-$pkgver-2"

  python2 setup.py install --root="$pkgdir" --prefix=/usr -O1 --skip-build
}

package_python-smart-open() {
  depends=('python' 'python-boto' 'python-requests' 'python-boto3')

  cd "$_pkgname-$pkgver"

  python setup.py install --root="$pkgdir" --prefix=/usr -O1 --skip-build
}

