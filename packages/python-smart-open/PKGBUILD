# This file is part of BlackArch Linux ( https://www.blackarch.org/ ).
# See COPYING for license details.

pkgbase=python-smart-open
pkgname=('python2-smart-open' 'python-smart-open')
_pkgname=smart_open
pkgver=7.5.0
pkgrel=1
pkgdesc='Utils for streaming large files (S3, HDFS, gzip, bz2...).'
arch=('any')
url='https://pypi.org/project/smart-open/#files'
license=('MIT')
makedepends=('python2-setuptools' 'python-setuptools')
options=(!emptydirs)
source=("https://files.pythonhosted.org/packages/source/${_pkgname::1}/$_pkgname/smart_open-$pkgver.tar.gz")
sha512sums=('f59f18863f6060fbdf133d07b76ce7a6edeb7433376fd34b5ad6882113c24e74af47be074fef628980f8a72f0547f1da74a9451abed77ce36a37a404b5010ffa')

prepare() {
  cp -a "$_pkgname-$pkgver"{,-2}
}

build() {
  cd "$_pkgname-$pkgver-2"

  python2 setup.py build

  cd "$srcdir/$_pkgname-$pkgver"

  python setup.py build
}

package_python2-smart-open() {
  depends=('python2' 'python2-boto' 'python2-requests' 'python2-boto3')

  cd "$_pkgname-$pkgver-2"

  python2 setup.py install --root="$pkgdir" --prefix=/usr -O1 --skip-build
}

package_python-smart-open() {
  depends=('python' 'python-boto' 'python-requests' 'python-boto3')

  cd "$_pkgname-$pkgver"

  python setup.py install --root="$pkgdir" --prefix=/usr -O1 --skip-build
}

