# This file is part of BlackArch Linux ( http://blackarch.org ).
# See COPYING for license details.

pkgname='qark'
pkgver=136.1dd2fea
pkgrel=1
groups=('blackarch' 'blackarch-mobile' 'blackarch-fuzzer' 'blackarch-scanner'
        'blackarch-exploitation')
pkgdesc='Tool to look for several security related Android application vulnerabilities.'
arch=('any')
url='https://github.com/linkedin/qark'
license=('custom:unknown')
depends=('python2' 'java-environment' 'python2-pubsub' 'python2-lxml'
         'python2-pywebview' 'python2-tblib' 'python2-simplejson'
         'python2-html5lib' 'python2-coloredlogs' 'python2-beautifulsoup4')
makedepends=('git')
source=('git+https://github.com/linkedin/qark.git')
sha1sums=('SKIP')

pkgver() {
  cd "$srcdir/qark"

  echo $(git rev-list --count HEAD).$(git rev-parse --short HEAD)
}

package() {
  cd "$srcdir/qark"

  mkdir -p "$pkgdir/usr/bin"
  mkdir -p "$pkgdir/usr/share/qark"

  install -Dm644 README.md "$pkgdir/usr/share/doc/qark/README.md"
  install -Dm644 LICENSE "$pkgdir/usr/share/licenses/qark/LICENSE"

  cp -a * "$pkgdir/usr/share/qark"

  cat > "$pkgdir/usr/bin/qark" << EOF
#!/bin/sh
cd /usr/share/qark/qark
exec python2 qarkMain.py "\$@"
EOF

  chmod a+x "$pkgdir/usr/bin/qark"
}
