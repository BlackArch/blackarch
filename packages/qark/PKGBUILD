# This file is part of BlackArch Linux ( http://blackarch.org ).
# See COPYING for license details.

pkgname=qark
pkgver=284.bf5f83f
pkgrel=1
groups=('blackarch' 'blackarch-mobile' 'blackarch-fuzzer' 'blackarch-scanner'
        'blackarch-exploitation')
pkgdesc='Tool to look for several security related Android application vulnerabilities.'
arch=('any')
url='https://github.com/linkedin/qark'
license=('custom:unknown')
depends=('python2' 'java-environment' 'python2-pubsub' 'python2-lxml'
         'python2-pywebview' 'python2-tblib' 'python2-simplejson'
         'python2-html5lib' 'python2-coloredlogs' 'python2-beautifulsoup4')
makedepends=('git')
source=('git+https://github.com/linkedin/qark.git')
sha512sums=('SKIP')

pkgver() {
  cd $pkgname

  echo $(git rev-list --count HEAD).$(git rev-parse --short HEAD)
}

package() {
  cd $pkgname

  install -dm 755 "$pkgdir/usr/bin"
  install -dm 755 "$pkgdir/usr/share/$pkgname"

  install -Dm 644 README.md "$pkgdir/usr/share/doc/$pkgname/README.md"
  install -Dm 644 LICENSE "$pkgdir/usr/share/licenses/$pkgname/LICENSE"

  cp -a * "$pkgdir/usr/share/$pkgname"

  cat > "$pkgdir/usr/bin/$pkgname" << EOF
#!/bin/sh
cd /usr/share/$pkgname/$pkgname
exec python2 qarkMain.py "\$@"
EOF

  chmod a+x "$pkgdir/usr/bin/$pkgname"
}

