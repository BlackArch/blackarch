# This file is part of BlackArch Linux ( https://www.blackarch.org/ ).
# See COPYING for license details.

pkgname=qark
pkgver=301.ba1b265
pkgrel=1
groups=('blackarch' 'blackarch-mobile' 'blackarch-fuzzer' 'blackarch-scanner'
        'blackarch-exploitation')
pkgdesc='Tool to look for several security related Android application vulnerabilities.'
arch=('any')
url='https://github.com/linkedin/qark'
license=('custom:unknown')
depends=('python2' 'python2-asn1crypto' 'python2-certifi'
         'python2-cffi' 'python2-chardet' 'python2-click'
         'python2-cryptography' 'python2-enum34' 'python2-idna'
         'python2-ipaddress' 'python2-javalang' 'python2-jinja'
         'python2-markupsafe' 'python2-pluginbase' 'python2-pycparser'
         'python2-pyopenssl' 'python2-requests' 'python2-six'
         'python2-urllib3')
makedepends=('git' 'python2')
source=("git+https://github.com/linkedin/$pkgname.git")
sha512sums=('SKIP')

pkgver() {
  cd $pkgname

  echo $(git rev-list --count HEAD).$(git rev-parse --short HEAD)
}

build() {
  cd $pkgname

  python2 setup.py build
}

package() {
  cd $pkgname

  python2 setup.py install --prefix=/usr --root="$pkgdir" --optimize=1 \
    --skip-build
}

