# This file is part of BlackArch Linux ( https://www.blackarch.org/ ).
# See COPYING for license details.

pkgname=recomposer
pkgver=2.90f85ed
pkgrel=1
pkgdesc="Randomly changes Win32/64 PE Files for 'safer' uploading to malware and sandbox sites."
groups=('blackarch' 'blackarch-automation' 'blackarch-binary')
arch=('any')
url='https://github.com/secretsquirrel/recomposer'
license=('custom:unknown')
depends=('python2')
makedepends=('git')
source=("$pkgname::git+https://github.com/secretsquirrel/$pkgname.git")
sha512sums=('SKIP')

pkgver() {
  cd $pkgname

  echo $(git rev-list --count HEAD).$(git rev-parse --short HEAD)
}

prepare() {
  cd $pkgname

  sed 's|#!/usr/bin/env python|#!/usr/bin/env python2|g' -i "$pkgname.py"
}

package() {
  cd $pkgname

  install -Dm 755 "$pkgname.py" "$pkgdir/usr/bin/$pkgname"
  install -Dm 644 -t "$pkgdir/usr/share/doc/$pkgname/" README.md
}

