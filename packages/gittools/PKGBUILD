# This file is part of BlackArch Linux ( http://blackarch.org ).
# See COPYING for license details.

pkgname='gittools'
pkgver=28.91f4137
pkgrel=1
pkgdesc="A repository with 3 tools for pwn'ing websites with .git repositories available'."
groups=('blackarch' 'blackarch-webapp' 'blackarch-scanner')
arch=('any')
url='https://github.com/internetwache/GitTools'
license=('custom:unknown')
depends=('python2' 'curl' 'git')
makedepends=('git')
source=('git+https://github.com/internetwache/GitTools.git')
sha1sums=('SKIP')

pkgver() {
  cd "$srcdir/GitTools"

  echo $(git rev-list --count HEAD).$(git rev-parse --short HEAD)
}

package() {
  cd "$srcdir/GitTools"

  mkdir -p "$pkgdir/usr/share/doc/gittools/"{dumper,extractor,finder}

  install -Dm755 Dumper/gitdumper.sh "$pkgdir/usr/bin/gittools-gitdumper"
  install -Dm755 Extractor/extractor.sh "$pkgdir/usr/bin/gittools-extractor"
  install -Dm755 Finder/gitfinder.py "$pkgdir/usr/bin/gittools-gitfinder"

  install -Dm644 README.md "$pkgdir/usr/share/doc/gittools/README.md"
  install -Dm644 Dumper/README.md \
    "$pkgdir/usr/share/doc/gittools/dumper/README.md"
  install -Dm644 Extractor/README.md \
    "$pkgdir/usr/share/doc/gittools/extractor/README.md"
  install -Dm644 Finder/README.md \
    "$pkgdir/usr/share/doc/gittools/finder/README.md"
}
