# This file is part of BlackArch Linux ( https://www.blackarch.org/ ).
# See COPYING for license details.

pkgname=python-pyexcel-xlsx
_pkgname=pyexcel-xlsx
pkgver=0.6.0
pkgrel=3
pkgdesc='A wrapper library to read, manipulate and write data in xlsx and xlsmformat.'
arch=('any')
url='https://pypi.org/project/pyexcel-xlsx/#files'
license=('BSD')
depends=('python' 'python-pyexcel-io' 'python-openpyxl')
makedepends=('python-setuptools')
source=("https://files.pythonhosted.org/packages/source/${_pkgname::1}/$_pkgname/$_pkgname-$pkgver.tar.gz")
sha512sums=('e241766d52d35c5d68fc05e391302fc2bb752f722d92c50e69e2efc39f4373403d975d6aae72703873548efab629f4bee74f40c9f457c2d00e2289247130fc8e')

prepare(){
  cd "$_pkgname-$pkgver"

  sed -i 's/openpyxl>=2.5.0,<2.6.0/openpyxl/g' setup.py
  sed -i 's/pyexcel-io>=0.5.3/pyexcel-io/g' setup.py
}

build() {
  cd "$_pkgname-$pkgver"

  python setup.py build
}

package() {
  cd "$_pkgname-$pkgver"

  python setup.py install --root="$pkgdir" --prefix=/usr -O1 --skip-build
}

