# This file is part of BlackArch Linux ( http://blackarch.org ).
# See COPYING for license details.

pkgname='zizzania'
pkgver=115.a063e6c
pkgrel=1
groups=('blackarch' 'blackarch-wireless' 'blackarch-dos' 'blackarch-sniffer')
pkgdesc='Automated DeAuth attack.'
arch=('i686' 'x86_64' 'armv6h' 'armv7h')
url='https://github.com/cyrus-and/zizzania'
license=('custom:unknown')
depends=('libpcap' 'uthash')
makedepends=('git' 'scons')
source=('git+https://github.com/cyrus-and/zizzania.git')
sha1sums=('SKIP')

pkgver() {
  cd "$srcdir/zizzania"

  echo $(git rev-list --count HEAD).$(git rev-parse --short HEAD)
}

prepare() {
  cd "$srcdir/zizzania"

  sed -i 's|/usr/local|/usr|g' Makefile
}

build() {
  cd "$srcdir/zizzania"

  make
}

package() {
  cd "$srcdir/zizzania"

  install -Dm755 src/zizzania "$pkgdir/usr/bin/zizzania"
  install -Dm644 README.md "$pkgdir/usr/share/doc/zizzania/README.md"
  install -Dm644 LICENSE "$pkgdir/usr/share/licenses/zizzania/LICENSE"
}
