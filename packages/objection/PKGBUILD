# This file is part of BlackArch Linux ( https://www.blackarch.org/ ).
# See COPYING for license details.

pkgname=objection
pkgver=1.9.0
pkgrel=1
pkgdesc='Instrumented Mobile Pentest Framework.'
groups=('blackarch' 'blackarch-mobile' 'blackarch-reversing' 'blackarch-binary')
arch=('any')
url='https://pypi.org/project/objection/#files'
license=('custom:unknown')
depends=('python-frida' 'python-frida-tools' 'python-prompt_toolkit'
         'python-click' 'python-tabulate' 'python-requests' 'python-flask'
         'python-delegator.py' 'python-pexpect' 'python-litecli')
makedepends=('python-setuptools')
source=("https://files.pythonhosted.org/packages/8e/12/f933fae6018063728608d71f257b3d424d2be643207b560ca47bd4518249/$pkgname-$pkgver.tar.gz"
        'requirements.txt.patch')
sha512sums=('17b4dc9a3258fbd7f2e515aca1ee41bc902c9611aa5fb163bd34a3b379a9aa5f963adf59b4987d96e4ffec9e3aa69840e1e37d880a9441856ee03ae0e9956aff'
            'c97e262ea95db72540c9e4929806a53419c653109fe9350bbef2323239a17fac7f703bec2e9789b9d7e70a09ed9dde8a11b8e868e619af761db5d3f9ca52912e')

prepare() {
  patch -p1 < requirements.txt.patch
}

build() {
  cd "$pkgname-$pkgver"

  python setup.py build
}

package() {
  cd "$pkgname-$pkgver"

  python setup.py install --root="$pkgdir" --prefix=/usr -O1 --skip-build

  rm -rf "$pkgdir/usr/lib/python3.8/site-packages/tests"
}

