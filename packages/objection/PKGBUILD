# This file is part of BlackArch Linux ( https://www.blackarch.org/ ).
# See COPYING for license details.

pkgname=objection
pkgver=1.8.4
pkgrel=1
pkgdesc='Instrumented Mobile Pentest Framework.'
groups=('blackarch' 'blackarch-mobile' 'blackarch-reversing' 'blackarch-binary')
arch=('any')
url='https://pypi.org/project/objection/#files'
license=('custom:unknown')
depends=('python-frida' 'python-frida-tools' 'python-prompt_toolkit'
         'python-click' 'python-tabulate' 'python-requests' 'python-flask'
         'python-delegator.py' 'python-pexpect' 'python-litecli')
makedepends=('python-setuptools')
source=("https://files.pythonhosted.org/packages/33/40/d36e7f877bc8112a54c72363ee54668d633936061465a24f10c455f4e8a4/$pkgname-$pkgver.tar.gz"
        'requirements.txt.patch')
sha512sums=('0e225f6dd6ba2efaff68f210bf245563f2d6bcbf372efce22c3fcfb150c0150e66e40b1941b15efef3df490f85363000fbe48997978313c34757e6fa18b1e9e3'
            'da1021d802e36589e6cefa98b82859e64a85c0d06299459216e380cf26dfebd914a297b9fcf77c51613746b3a7bac46b4e54235c0b80a372f7319d2a1a7b4dec')

prepare() {
  patch -p1 < requirements.txt.patch
}

build() {
  cd "$pkgname-$pkgver"

  python setup.py build
}

package() {
  cd "$pkgname-$pkgver"

  python setup.py install --root="$pkgdir" --prefix=/usr -O1 --skip-build

  rm -rf "$pkgdir/usr/lib/python3.8/site-packages/tests"
}

