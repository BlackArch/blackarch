# This file is part of BlackArch Linux ( https://www.blackarch.org/ ).
# See COPYING for license details.

pkgname=objection
pkgver=1.8.3
pkgrel=1
pkgdesc='Instrumented Mobile Pentest Framework.'
groups=('blackarch' 'blackarch-mobile' 'blackarch-reversing' 'blackarch-binary')
arch=('any')
url='https://pypi.org/project/objection/#files'
license=('custom:unknown')
depends=('python-frida' 'python-frida-tools' 'python-prompt_toolkit'
         'python-click' 'python-tabulate' 'python-requests' 'python-flask'
         'python-delegator.py' 'python-pexpect' 'python-litecli')
makedepends=('python-setuptools')
source=("https://files.pythonhosted.org/packages/b3/12/aba1cee91a1183f51eafefec5ae71040159dbb0fbf04d2603dbb1f362e6a/$pkgname-$pkgver.tar.gz"
        'requirements.txt.patch')
sha512sums=('f4c5407e7b580173dc9ccffc991575b8b67af3ee2180bba11399da2c9c05a6124da7c356890f3667ca7d8dea66d5431647f1c9b90c0f992a8c3f27ef87f827f5'
            'fc4bdd657e0e989999ef649403f976afc098633e2d88821f21beeca4a0e9a85481d3531126a40f585284172f954fc5b697c0be330a00d6e20c3ee863ee38e35c')

prepare() {
  patch -p1 < requirements.txt.patch
}

build() {
  cd "$pkgname-$pkgver"

  python setup.py build
}

package() {
  cd "$pkgname-$pkgver"

  python setup.py install --root="$pkgdir" --prefix=/usr -O1 --skip-build

  rm -rf "$pkgdir/usr/lib/python3.8/site-packages/tests"
}

