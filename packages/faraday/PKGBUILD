# This file is part of BlackArch Linux ( https://www.blackarch.org/ ).
# See COPYING for license details.

pkgname=faraday
pkgver=8717.464bb0c9
pkgrel=1
pkgdesc='A new concept (IPE) Integrated Penetration-Test Environment a multiuser Penetration test IDE. Designed for distribution, indexation and analyze of the generated data during the process of a security audit.'
groups=('blackarch' 'blackarch-scanner' 'blackarch-exploitation'
        'blackarch-fuzzer' 'blackarch-fingerprint' 'blackarch-automation'
        'blackarch-misc')
arch=('x86_64' 'aarch64')
url='http://www.faradaysec.com/'
depends=('couchdb' 'python2' 'gtk3' 'gobject-introspection' 'python2-argparse'
         'gobject-introspection-runtime' 'python2-gobject' 'vte3' 'zsh' 'curl'
         'python2-couchdbkit' 'python2-mockito' 'python2-whoosh' 'python2-ipy'
         'python2-restkit' 'python2-requests' 'python2-tornado' 'python2-flask'
         'python2-colorama' 'java-environment' 'python2-pip' 'python2-dateutil'
         'pyqt3' 'python2-pysvn' 'libpqxx' 'python2-psycopg2' 'ruby'
         'python2-lxml' 'python2-pysqlite' 'python2-sphinx' 'python2-twisted'
         'python2-beautifulsoup4' 'python2-sqlalchemy' 'python2-werkzeug'
         'python2-w3af-api-client' 'python2-urllib3' 'python2-responses'
         'python2-simplejson' 'python2-pytz' 'python2-pytest' 'python2-msgpack'
         'python2-itsdangerous' 'python2-deprecation' 'python2-click'
         'python2-python-slugify' 'python2-hypothesis' 'python2-autobahn'
         'python2-pgcli' 'python2-alembic' 'python2-websocket-client'
         'python2-tqdm' 'python2-flask-sqlalchemy' 'python2-marshmallow'
         'python2-pillow' 'python2-service-identity' 'python2-webargs'
         'python2-selenium' 'python2-shodan')
makedepends=('git')
options=('!strip')
license=('GPL')
source=("git+https://github.com/infobyte/$pkgname.git")
sha512sums=('SKIP')

pkgver() {
  cd $pkgname

  echo $(git rev-list --count HEAD).$(git rev-parse --short HEAD)
}

package() {
  cd $pkgname

  _tools="faraday faraday-server"

  install -dm 755 "$pkgdir/usr/bin"
  install -dm 755 "$pkgdir/usr/share/$pkgname"

  install -Dm 644 -t "$pkgdir/usr/share/doc/$pkgname/" AUTHORS *.md
  install -Dm 644 -t "$pkgdir/usr/share/licenses/$pkgname/" doc/*

  cp -r --no-preserve=ownership * "$pkgdir/usr/share/$pkgname/"

  for t in $_tools ; do
    cat > "$pkgdir/usr/bin/$t" << EOF
#!/bin/sh
cd /usr/share/$pkgname/
exec python2 $t.py "\$@"
EOF
    chmod +x "$pkgdir/usr/bin/$pkgname"
  done
}

