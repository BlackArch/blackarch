# This file is part of BlackArch Linux ( https://www.blackarch.org/ ).
# See COPYING for license details.

pkgname=sh00t
pkgver=170.f604cea
pkgrel=1
pkgdesc='A Testing Environment for Manual Security Testers.'
groups=('blackarch' 'blackarch-misc')
arch=('any')
url='https://github.com/pavanw3b/sh00t'
license=('MIT')
depends=('python' 'python-django-rest-framework' 'python-pytz'
         'python-django-tables2')
makedepends=('git')
source=("git+https://github.com/pavanw3b/$pkgname.git"
        'sh00t.install')
sha512sums=('SKIP'
            '24bbdbdcf98ef1c1ddfce83c5cceb3da6bdbe1c3d2ff97c095bd32ac2ff8c948bf95b055776450ef8ffa5d6350543c9e1646a91b03578c339600eaf713ab2207')
install="$pkgname.install"

pkgver() {
  cd $pkgname

  echo $(git rev-list --count HEAD).$(git rev-parse --short HEAD)
}

package() {
  cd $pkgname

  install -dm 755 "$pkgdir/usr/bin"
  install -dm 755 "$pkgdir/usr/share/$pkgname"

  install -Dm 644 README.md "$pkgdir/usr/share/doc/$pkgname/README"
  install -Dm 644 LICENSE "$pkgdir/usr/share/licenses/$pkgname/LICENSE"

  cp -a --no-preserve=ownership * "$pkgdir/usr/share/$pkgname/"

  cat > "$pkgdir/usr/bin/$pkgname" << EOF
#!/bin/sh
cd /usr/share/$pkgname
exec python manage.py "\$@"
EOF

  chmod +x "$pkgdir/usr/bin/$pkgname"
}

