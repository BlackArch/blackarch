# This file is part of BlackArch Linux ( https://www.blackarch.org/ ).
# See COPYING for license details.

pkgname=androwarn
pkgver=135.626c02d
pkgrel=11
pkgdesc='Yet another static code analyzer for malicious Android applications.'
groups=('blackarch' 'blackarch-mobile' 'blackarch-code-audit')
arch=('any')
url='https://github.com/maaaaz/androwarn'
license=('GPL-3.0-or-later')
depends=('python' 'python-lxml' 'python-future' 'python-jinja'
         'python-play-scraper' 'androguard')
makedepends=('git')
source=("git+https://github.com/maaaaz/$pkgname.git")
sha512sums=('SKIP')

pkgver() {
  cd $pkgname

  ( set -o pipefail
    git describe --long --tags --abbrev=7 2>/dev/null |
      sed 's/\([^-]*-g\)/r\1/;s/-/./g' ||
    printf "%s.%s" "$(git rev-list --count HEAD)" \
      "$(git rev-parse --short=7 HEAD)"
  )
}

package() {
  cd $pkgname

  install -dm 755 "$pkgdir/usr/bin"
  install -dm 755 "$pkgdir/usr/share/$pkgname"

  install -Dm 644 README.md "$pkgdir/usr/share/doc/$pkgname/README.md"
  install -Dm 644 -t "$pkgdir/usr/share/licenses/$pkgname/" COPYING \
    COPYING.LESSER

  rm README.md COPYING COPYING.LESSER

  cp -a * "$pkgdir/usr/share/$pkgname/"

  cat > "$pkgdir/usr/bin/$pkgname" << EOF
#!/bin/sh
cd /usr/share/$pkgname
exec python $pkgname.py "\$@"
EOF

  chmod +x "$pkgdir/usr/bin/$pkgname"
}

