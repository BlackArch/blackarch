# This file is part of BlackArch Linux ( https://www.blackarch.org/ ).
# See COPYING for license details.

pkgname=netcommander
pkgver=1.3
pkgrel=6
groups=('blackarch' 'blackarch-spoof' 'blackarch-networking')
pkgdesc='An easy-to-use arp spoofing tool.'
arch=('any')
url='https://github.com/evilsocket/netcommander'
license=('GPL3')
depends=('python2' 'python2-scapy')
makedepends=('git')
source=("$pkgname::git+https://github.com/evilsocket/NetCommander.git")
sha512sums=('SKIP')

prepare() {
  cd $pkgname

  sed -i 's|mac-prefixes|/usr/share/netcommander/mac-prefixes|' netcmd.py
  sed -i 's/python$/python2/' netcmd.py
}

package() {
  cd $pkgname

  install -Dm 755 netcmd.py "$pkgdir/usr/bin/$pkgname"
  install -Dm 644 mac-prefixes "$pkgdir/usr/share/$pkgname/mac-prefixes"
}

