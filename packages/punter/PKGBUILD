# This file is part of BlackArch Linux ( http://blackarch.org ).
# See COPYING for license details.

pkgname=punter
pkgver=44.fff76ac
pkgrel=2
pkgdesc='Hunt domain names using DNSDumpster, WHOIS, Reverse WHOIS, Shodan, Crimeflare.'
groups=('blackarch' 'blackarch-recon')
arch=('any')
url='https://github.com/nethunteros/punter'
license=('custom:unknown')
depends=('python2' 'python2-argparse' 'python2-requests' 'python2-dnspython'
         'python2-beautifulsoup4' 'python2-python-whois' 'python2-ipwhois'
         'python2-configparser' 'api-dnsdumpster' 'shodan' 'python2-ipaddr'
         'python2-future' 'python2-lxml')
makedepends=('git')
source=('git+https://github.com/nethunteros/punter.git')
sha512sums=('SKIP')

pkgver() {
  cd punter

  echo $(git rev-list --count HEAD).$(git rev-parse --short HEAD)
}

package() {
  cd punter

  install -dm 755 "$pkgdir/usr/bin"
  install -dm 755 "$pkgdir/usr/share/punter"

  install -Dm644 -t "$pkgdir/usr/share/doc/punter/" README.MD
  install -Dm644 LICENSE "$pkgdir/usr/share/licenses/punter/LICENSE"

  rm README.MD LICENSE

  cp -a * "$pkgdir/usr/share/punter/"

  cat > "$pkgdir/usr/bin/punter" << EOF
#!/bin/sh
cd /usr/share/punter
exec python2 main.py "\${@}"
EOF

  chmod a+x "$pkgdir/usr/bin/punter"
}

