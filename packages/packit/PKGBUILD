# This file is part of BlackArch Linux ( https://www.blackarch.org/ ).
# See COPYING for license details.

pkgname=packit
pkgver=2.0
pkgrel=1
pkgdesc='A network auditing tool with the ability to customize, inject, monitor, and manipulate IP traffic.'
arch=('x86_64' 'aarch64')
groups=('blackarch' 'blackarch-networking')
url='http://packetfactory.openwall.net/projects/packit/'
license=('GPL-2.0-or-later')
depends=('libpcap' 'libnet')
source=("https://github.com/Obsidian-Covenant/$pkgname/archive/refs/tags/$pkgver.tar.gz")
sha512sums=('80c8371462f9e80cc046894f5e5193b7f5234fa132b9844c6c1481cebbeedb5fae701673293d4b653dd35c8a99fecf379e2ed4cda029ad6cc8c3e634598fb01a')

prepare() {
  cd "$pkgname-$pkgver"

  autoreconf -i

  if [[ "$CARCH" == "i686" || "$CARCH" == "x86_64" ]];
  then
    ./configure --prefix=/usr --mandir=/usr/share/man
  else
    ./configure --build=arm --prefix=/usr --mandir=/usr/share/man
  fi
}

build() {
  cd "$pkgname-$pkgver"

  make
}

package() {
  cd "$pkgname-$pkgver"

  make DESTDIR="$pkgdir" install
}

