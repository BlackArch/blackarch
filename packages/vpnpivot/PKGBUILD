# This file is part of BlackArch Linux ( http://blackarch.org ).
# See COPYING for license details.

pkgname='vpnpivot'
pkgver=22.37bbde0
pkgrel=1
pkgdesc='Explore the network using this tool.'
groups=('blackarch' 'blackarch-recon' 'blackarch-networking')
arch=('x86_64' 'armv6h' 'armv7h' 'aarch64')
url='https://github.com/0x36/VPNPivot'
license=('custom:unknown')
depends=('linux-headers' 'openssl')
makedepends=('git')
source=('git+https://github.com/0x36/VPNPivot.git')
sha1sums=('SKIP')

pkgver() {
  cd "$srcdir/VPNPivot"

  echo $(git rev-list --count HEAD).$(git rev-parse --short HEAD)
}

build() {
  cd "$srcdir/VPNPivot"

  ./autogen.sh

  ./configure --prefix=/usr --sbindir=/usr/bin

  make -j1
}

package() {
  cd "$srcdir/VPNPivot"

  make DESTDIR="$pkgdir" install

  install -Dm644 -t "$pkgdir/usr/share/doc/vpnpivot/" README.md
}
