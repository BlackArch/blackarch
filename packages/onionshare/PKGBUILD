# This file is part of BlackArch Linux ( http://blackarch.org ).
# See COPYING for license details.

pkgname=onionshare
pkgver=1875.9812e75
pkgrel=1
groups=('blackarch' 'blackarch-misc' 'blackarch-defensive')
pkgdesc='Securely and anonymously share a file of any size.'
url='https://github.com/micahflee/onionshare/'
arch=('any')
license=('GPL3')
depends=('python' 'tor' 'python-stem' 'python-pyqt5' 'python-flask'
         'python-nose' 'python-simplejson' 'python-requests' 'python-pytest'
         'python-werkzeug')
makedepends=('git' 'python-setuptools')
source=('git+https://github.com/micahflee/onionshare.git')
sha512sums=('SKIP')

pkgver() {
  cd "$srcdir/onionshare"

  echo $(git rev-list --count HEAD).$(git rev-parse --short HEAD)
}

package() {
  cd "$srcdir/onionshare"

  install -Dm644 README.md "$pkgdir/usr/share/doc/onionshare/README.md"
  install -Dm644 LICENSE "$pkgdir/usr/share/licenses/onionshare/LICENSE"

  python setup.py install --root="$pkgdir" --optimize=1
}
