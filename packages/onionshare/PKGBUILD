# This file is part of BlackArch Linux ( https://www.blackarch.org/ ).
# See COPYING for license details.

pkgname=onionshare
pkgver=2560.9a327f2
pkgrel=2
groups=('blackarch' 'blackarch-misc' 'blackarch-defensive')
pkgdesc='Securely and anonymously share a file of any size.'
url='https://github.com/micahflee/onionshare/'
arch=('any')
license=('GPL3')
depends=('python' 'tor' 'python-stem' 'python-pyqt5' 'python-flask'
         'python-nose' 'python-simplejson' 'python-requests' 'python-pytest'
         'python-werkzeug' 'python-flask-httpauth' 'python-pycryptodome')
makedepends=('git' 'python-setuptools')
source=("git+https://github.com/micahflee/$pkgname.git")
sha512sums=('SKIP')

pkgver() {
  cd $pkgname

  echo $(git rev-list --count HEAD).$(git rev-parse --short HEAD)
}

build() {
  cd $pkgname

  python setup.py build
}

package() {
  cd $pkgname

  install -Dm 644 -t "$pkgdir/usr/share/doc/$pkgname/" README.md
  install -Dm 644 LICENSE "$pkgdir/usr/share/licenses/$pkgname/LICENSE"

  python setup.py install --root="$pkgdir" -O1 --skip-build
}

