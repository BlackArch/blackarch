# This file is part of BlackArch Linux ( http://blackarch.org ).
# See COPYING for license details.

pkgname='onionshare'
pkgver=1214.a068bf4
pkgrel=1
groups=('blackarch' 'blackarch-misc' 'blackarch-defensive')
pkgdesc='Securely and anonymously share a file of any size.'
url='https://github.com/micahflee/onionshare/'
arch=('any')
license=('GPL3')
depends=('python2' 'tor' 'python2-stem' 'python2-pyqt4' 'python2-flask'
         'python2-nose' 'python2-simplejson')
makedepends=('git' 'python2-setuptools')
source=('git+https://github.com/micahflee/onionshare.git')
sha1sums=('SKIP')

pkgver() {
  cd "$srcdir/onionshare"

  echo $(git rev-list --count HEAD).$(git rev-parse --short HEAD)
}

package() {
  cd "$srcdir/onionshare"

  install -Dm644 README.md "$pkgdir/usr/share/doc/onionshare/README.md"
  install -Dm644 LICENSE "$pkgdir/usr/share/licenses/onionshare/LICENSE"

  python2 setup.py install --root="$pkgdir" --optimize=1
}
