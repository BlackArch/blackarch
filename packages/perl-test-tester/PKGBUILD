# This file is part of BlackArch Linux ( https://www.blackarch.org/ ).
# See COPYING for license details.

pkgname=perl-test-tester
pkgver=0.109
pkgrel=5
pkgdesc='Ease testing test modules built with Test::Builder.'
arch=('any')
url='https://search.cpan.org/dist/Test-Tester'
license=('GPL' 'PerlArtistic')
depends=('perl')
options=('!emptydirs')
source=("https://www.cpan.org/authors/id/F/FD/FDALY/Test-Tester-$pkgver.tar.gz")
sha512sums=('f79942a85c787b1ea29da8f5c5611a0ce1fa8fabe372a4de04f0e5b98af137f43cd3966a558dc8fc5d0b049eb162f9b583d73ca39674bab7523290756b25cfe7')

build() {
  cd  "Test-Tester-$pkgver"

  PERL_MM_USE_DEFAULT=1 perl Makefile.PL INSTALLDIRS=vendor

  make
}

package() {
  cd  "Test-Tester-$pkgver"

  make install DESTDIR="$pkgdir"

  find "$pkgdir" -name '.packlist' -delete
  find "$pkgdir" -name '*.pod' -delete
}

