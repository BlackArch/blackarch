# This file is part of BlackArch Linux ( https://www.blackarch.org/ ).
# See COPYING for license details.

pkgname=pdfgrab
pkgver=15.1327508
pkgrel=3
pkgdesc='Tool for searching pdfs withthin google and extracting pdf metadata.'
groups=('blackarch' 'blackarch-recon')
arch=('any')
url='https://github.com/c0decave/pdfgrab'
license=('GPL2')
depends=('python' 'python-requests' 'python-pypdf2' 'python-google' 'ipython'
         'python-json2html' 'python-pycryptodome')
makedepends=('git')
source=("git+https://github.com/c0decave/$pkgname.git")
sha512sums=('SKIP')

pkgver() {
  cd $pkgname

  echo $(git rev-list --count HEAD).$(git rev-parse --short HEAD)
}

package() {
  cd $pkgname

  install -dm 755 "$pkgdir/usr/bin"
  install -dm 755 "$pkgdir/usr/share/$pkgname"

  install -Dm 644 -t "$pkgdir/usr/share/doc/$pkgname/" Readme.md
  install -Dm 644 LICENSE "$pkgdir/usr/share/licenses/$pkgname/LICENSE"

  rm *.md LICENSE

  cp -a * "$pkgdir/usr/share/$pkgname/"

  cat > "$pkgdir/usr/bin/$pkgname" << EOF
#!/bin/sh
cd /usr/share/$pkgname
exec python $pkgname.py "\$@"
EOF

  chmod +x "$pkgdir/usr/bin/$pkgname"
}

