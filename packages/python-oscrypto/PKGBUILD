# This file is part of BlackArch Linux ( https://www.blackarch.org/ ).
# See COPYING for license details.

pkgbase=python-oscrypto
pkgname=('python2-oscrypto' 'python-oscrypto')
_pkgname=oscrypto
pkgver=1.2.0
pkgrel=1
pkgdesc='TLS (SSL) sockets, key generation, encryption, decryption, signing, verification and KDFs using the OS crypto libraries.'
arch=('any')
url='https://pypi.org/project/oscrypto/#files'
license=('MIT')
makedepends=('python2-setuptools' 'python-setuptools')
source=("https://files.pythonhosted.org/packages/1e/da/11eb7141582415eca72d41f7d6075405f515303b21ba548fe492a26e300a/$_pkgname-$pkgver.tar.gz")
sha512sums=('a8c2c4fa53f1288610b5bfe3a9304d67dd9ca43ee94021c3780bcd68d8ac74b21575778281469ad19230ebf0042b7fc7c919da81b365bab3bfbe04e71a3ad54d')

prepare() {
  cp -a "$_pkgname-$pkgver"{,-2}
}

build() {
  cd "$_pkgname-$pkgver-2"

  python2 setup.py build

  cd "$srcdir/$_pkgname-$pkgver"

  python setup.py build
}

package_python2-oscrypto() {
  depends=('python2' 'python2-asn1crypto')

  cd "$_pkgname-$pkgver-2"

  python2 setup.py install --prefix=/usr --root="$pkgdir" -O1 --skip-build
}

package_python-oscrypto() {
  depends=('python' 'python-asn1crypto')

  cd "$_pkgname-$pkgver"

  python setup.py install --prefix=/usr --root="$pkgdir" -O1 --skip-build
}

