# This file is part of BlackArch Linux ( https://www.blackarch.org/ ).
# See COPYING for license details.

pkgname=climber
pkgver=30.5530a78
pkgrel=3
groups=('blackarch' 'blackarch-scanner')
pkgdesc='Check UNIX/Linux systems for privilege escalation.'
url='https://github.com/raffaele-forte/climber'
arch=('any')
license=('GPL3')
depends=('python2' 'python2-pycryptodome' 'python2-mako' 'python2-paramiko'
         'python2-exscript')
makedepends=('git')
source=("git+https://github.com/raffaele-forte/$pkgname.git")
sha512sums=('SKIP')

pkgver() {
  cd $pkgname

  echo $(git rev-list --count HEAD).$(git rev-parse --short HEAD)
}

package() {
  cd $pkgname

  install -dm 755 "$pkgdir/usr/bin"
  install -dm 755 "$pkgdir/usr/share/$pkgname"

  install -Dm 644 -t "$pkgdir/usr/share/doc/$pkgname/" README.md
  install -Dm 644 LICENSE "$pkgdir/usr/share/licenses/$pkgname/LICENSE"

  rm README.md LICENSE

  cp -a * "$pkgdir/usr/share/$pkgname/"

  cat > "$pkgdir/usr/bin/$pkgname" << EOF
#!/bin/sh
cd /usr/share/$pkgname
exec python2 $pkgname.py "\$@"
EOF

  chmod +x "$pkgdir/usr/bin/$pkgname"
}

