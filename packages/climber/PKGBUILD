# This file is part of BlackArch Linux ( http://blackarch.org ).
# See COPYING for license details.

pkgname='climber'
pkgver=30.5530a78
pkgrel=2
groups=('blackarch' 'blackarch-scanner')
pkgdesc='Check UNIX/Linux systems for privilege escalation.'
url='https://github.com/raffaele-forte/climber'
arch=('any')
license=('GPL3')
depends=('python2' 'python2-crypto' 'python2-mako' 'python2-paramiko'
        'python2-exscript')
makedepends=('git')
source=('git+https://github.com/raffaele-forte/climber.git')
sha512sums=('SKIP')

pkgver() {
  cd "$srcdir/climber"

  echo $(git rev-list --count HEAD).$(git rev-parse --short HEAD)
}

package() {
  cd "$srcdir/climber"

  mkdir -p "$pkgdir/usr/bin"
  mkdir -p "$pkgdir/usr/share/climber"

  install -Dm644 README.md "$pkgdir/usr/share/doc/climber/README.md"
  install -Dm644 LICENSE "$pkgdir/usr/share/licenses/climber/LICENSE"

  rm README.md LICENSE

  cp -a * "$pkgdir/usr/share/climber"

  cat > "$pkgdir/usr/bin/climber" << EOF
#!/bin/sh
cd /usr/share/climber
exec python2 climber.py "\$@"
EOF

  chmod +x "$pkgdir/usr/bin/climber"
}
