# This file is part of BlackArch Linux ( http://blackarch.org ).
# See COPYING for license details.

pkgname='fuddly'
pkgver=465.ed17a0b
pkgrel=1
groups=('blackarch' 'blackarch-fuzzer')
pkgdesc='Fuzzing and Data Manipulation Framework (for GNU/Linux).'
arch=('any')
url='https://github.com/k0retux/fuddly'
license=('GPL3')
depends=('python2' 'python2-pycups' 'python2-rpyc' 'python2-pysqlite'
         'python2-crcmod' 'python2-xtermcolor' 'sqlite')
makedepends=('git' 'python2-sphinx')
source=('git+https://github.com/k0retux/fuddly.git')
install=fuddly.install
sha1sums=('SKIP')

pkgver() {
  cd "$srcdir/fuddly"

  echo $(git rev-list --count HEAD).$(git rev-parse --short HEAD)
}

prepare() {
  cd "$srcdir/fuddly"

  cd docs

  sed -i 's|sphinx-build|sphinx-build2|' Makefile
}

build() {
  cd "$srcdir/fuddly"

  cd docs

  make html
}

package() {
  cd "$srcdir/fuddly"

  mkdir -p "$pkgdir/usr/bin"
  mkdir -p "$pkgdir/usr/share/fuddly"

  install -Dm644 -t "$pkgdir/usr/share/doc/fuddly/" README.rst TODO
  cp -a docs/build/html/ "$pkgdir/usr/share/doc/fuddly/"
  install -Dm644 COPYING "$pkgdir/usr/share/licenses/fuddly/COPYING"

  rm -rf README.rst TODO COPYING docs

  cp -a * "$pkgdir/usr/share/fuddly"

  cat > "$pkgdir/usr/bin/fuddly" << EOF
#!/bin/sh
cd /usr/share/fuddly
exec python2 client.py "\$@"
EOF

  chmod a+x "$pkgdir/usr/bin/fuddly"
}
