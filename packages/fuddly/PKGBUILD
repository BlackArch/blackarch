# This file is part of BlackArch Linux ( https://www.blackarch.org/ ).
# See COPYING for license details.

pkgname=fuddly
pkgver=0.30.dev.r253.g70348ca
pkgrel=1
epoch=1
groups=('blackarch' 'blackarch-fuzzer')
pkgdesc='Fuzzing and Data Manipulation Framework (for GNU/Linux).'
arch=('any')
url='https://github.com/k0retux/fuddly'
license=('GPL3')
depends=('python' 'python-argcomplete' 'python-crcmod' 'python-pycups'
         'python-rpyc' 'python-graphviz' 'python-matplotlib' 'python-paramiko'
         'python-pyserial' 'python-z3-solver' 'python-pyxdg'
         'python-xtermcolor')
makedepends=('git' 'python-build' 'python-pip')
source=("git+https://github.com/k0retux/$pkgname.git")
sha512sums=('SKIP')

pkgver() {
  cd $pkgname

  ( set -o pipefail
    git describe --long --tags --abbrev=7 2>/dev/null |
      sed 's/\([^-]*-g\)/r\1/;s/-/./g' ||
    printf "%s.%s" "$(git rev-list --count HEAD)" \
      "$(git rev-parse --short=7 HEAD)"
  )
}

build() {
  cd $pkgname

  python -m build --wheel --outdir="$startdir/dist"
}

package() {
  cd $pkgname

  pip install \
    --verbose \
    --disable-pip-version-check \
    --no-warn-script-location \
    --ignore-installed \
    --no-compile \
    --no-deps \
    --root="$pkgdir" \
    --prefix=/usr \
    --no-index \
    --find-links="file://$startdir/dist" \
    $pkgname
}

