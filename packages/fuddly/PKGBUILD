# This file is part of BlackArch Linux ( https://www.blackarch.org/ ).
# See COPYING for license details.

pkgname=fuddly
pkgver=539.90de14e
pkgrel=1
groups=('blackarch' 'blackarch-fuzzer')
pkgdesc='Fuzzing and Data Manipulation Framework (for GNU/Linux).'
arch=('any')
url='https://github.com/k0retux/fuddly'
license=('GPL3')
depends=('python2' 'python2-pycups' 'python2-rpyc' 'python2-pysqlite'
         'python2-crcmod' 'python2-xtermcolor' 'sqlite')
makedepends=('git' 'python2-sphinx')
source=('git+https://github.com/k0retux/fuddly.git')
install=fuddly.install
sha512sums=('SKIP')

pkgver() {
  cd $pkgname

  echo $(git rev-list --count HEAD).$(git rev-parse --short HEAD)
}

prepare() {
  cd $pkgname

  cd docs

  sed -i 's|sphinx-build|sphinx-build2|' Makefile
}

build() {
  cd $pkgname

  cd docs

  make html
}

package() {
  cd $pkgname

  install -dm 755 "$pkgdir/usr/bin"
  install -dm 755 "$pkgdir/usr/share/$pkgname"

  install -Dm644 -t "$pkgdir/usr/share/doc/$pkgname/" README.rst TODO
  cp -a docs/build/html/ "$pkgdir/usr/share/doc/$pkgname/"
  install -Dm644 COPYING "$pkgdir/usr/share/licenses/$pkgname/COPYING"

  rm -rf README.rst TODO COPYING docs

  cp -a * "$pkgdir/usr/share/$pkgname/"

  cat > "$pkgdir/usr/bin/$pkgname" << EOF
#!/bin/sh
cd /usr/share/$pkgname
exec python2 client.py "\$@"
EOF

  chmod +x "$pkgdir/usr/bin/$pkgname"
}

