# This file is part of BlackArch Linux ( https://www.blackarch.org/ ).
# See COPYING for license details.

pkgname=xcat
pkgver=1.0.2
pkgrel=1
pkgdesc='A command line tool to automate the exploitation of blind XPath injection vulnerabilities.'
url='https://github.com/orf/xcat'
groups=('blackarch' 'blackarch-exploitation' 'blackarch-automation')
depends=('python' 'python-ipgetter' 'python-colorama' 'python-xmltodict'
         'python-click' 'python-logbook' 'python-aiohttp' 'python-tqdm'
         'python-xpath-expressions' 'python-prompt_toolkit' 'python-cchardet')
makedepends=('python-setuptools')
license=('MIT')
arch=('any')
source=("https://files.pythonhosted.org/packages/9a/b7/6e2a3b63e73152b392c6645eb748a2cf79e17fedf4ae4db6ff53b6dc9441/xcat-$pkgver.tar.gz")
install='xcat.install'
sha512sums=('02c7f16a979da57a7c7e3fa9ac66eb03a8efbc9dce928413e3094fddde2ff707ee44c0f365d64320373cf7054ccc5c1e25178f5549b99276b5590aa021aaac1b')

prepare() {
  cd "$pkgname-$pkgver"

  sed -i 's|xpath-expressions~=1.0|xpath-expressions|' setup.py
  sed -i 's|aiohttp~=3.0|aiohttp|' setup.py
}

build() {
  cd "$pkgname-$pkgver"

  python setup.py build
}

package() {
  cd "$pkgname-$pkgver"

  python setup.py install --root="$pkgdir" --optimize=1 --skip-build
}

