# This file is part of BlackArch Linux ( https://www.blackarch.org/ ).
# See COPYING for license details.

pkgname=xcat
pkgver=1.0.1
pkgrel=1
pkgdesc='A command line tool to automate the exploitation of blind XPath injection vulnerabilities.'
url='https://github.com/orf/xcat'
groups=('blackarch' 'blackarch-exploitation' 'blackarch-automation')
depends=('python' 'python-ipgetter' 'python-colorama' 'python-xmltodict'
         'python-click' 'python-logbook' 'python-aiohttp' 'python-tqdm'
         'python-xpath-expressions' 'python-prompt_toolkit' 'python-cchardet')
makedepends=('python-setuptools')
license=('MIT')
arch=('any')
source=("https://files.pythonhosted.org/packages/4e/5d/c5e505657b7f32083b683a24983422d6889876e7343ef1ecbac58f5b456c/xcat-$pkgver.tar.gz")
install='xcat.install'
sha512sums=('0a38cb36a5c46bc73135465b101b75a91ad13ed5e8350f6c637c7678a30cb6c8160b3a7d7da89ea28f6eea382fffa544f1c3af0333bb694fb9e921ac22e0f9ec')

prepare() {
  cd "$pkgname-$pkgver"

  sed -i 's|xpath-expressions~=0.2|xpath-expressions|' setup.py
  sed -i 's|aiohttp~=2.1|aiohttp|' setup.py
}

build() {
  cd "$pkgname-$pkgver"

  python setup.py build
}

package() {
  cd "$pkgname-$pkgver"

  python setup.py install --root="$pkgdir" --optimize=1 --skip-build
}

