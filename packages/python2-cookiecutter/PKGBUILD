# This file is part of BlackArch Linux ( http://blackarch.org ).
# See COPYING for license details.

pkgname='python2-cookiecutter'
pkgver='1.4.0'
pkgrel=1
pkgdesc='A command-line utility that creates projects from cookiecutters (project templates).'
arch=('any')
url='https://github.com/audreyr/cookiecutter'
license=('BSD')
depends=('python2-jinja' 'python2-yaml' 'python2-binaryornot')
makedepends=('python2-setuptools')
source=("https://pypi.python.org/packages/source/c/cookiecutter/cookiecutter-${pkgver}.tar.gz")
sha1sums=('0d14c0c637a20fdcc74ed5a8aa5b5e6664e5d208')

prepare() {
  cd "$srcdir/cookiecutter-$pkgver"

  grep -iRl 'python' * | xargs \
    sed -i 's|#!.*/usr/bin/python|#!/usr/bin/python2|;s|#!.*/usr/bin/env python$|#!/usr/bin/env python2|'
}

package() {
  cd "$srcdir/cookiecutter-$pkgver"

  python2 setup.py install --root="$pkgdir/" --optimize=1

  install -Dm644 LICENSE "$pkgdir/usr/share/licenses/cookiecutter/LICENSE"
}
