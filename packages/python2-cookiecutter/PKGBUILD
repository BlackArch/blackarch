# This file is part of BlackArch Linux ( http://blackarch.org ).
# See COPYING for license details.

pkgname='python2-cookiecutter'
pkgver='1.5.1'
pkgrel=1
pkgdesc='A command-line utility that creates projects from cookiecutters (project templates).'
arch=('any')
url='https://github.com/audreyr/cookiecutter'
license=('BSD')
depends=('python2-jinja' 'python2-yaml' 'python2-binaryornot')
makedepends=('python2-setuptools')
source=("https://pypi.python.org/packages/97/ed/8f0b6f36c119e5083fb789ffa7c1169d98b15d5b3123b105207e46fb9026/cookiecutter-${pkgver}.tar.gz")
sha1sums=('1af262c326864642108b057b020d998cba6cbecc')

prepare() {
  cd "$srcdir/cookiecutter-$pkgver"

  grep -iRl 'python' * | xargs \
    sed -i 's|#!.*/usr/bin/python|#!/usr/bin/python2|;s|#!.*/usr/bin/env python$|#!/usr/bin/env python2|'
}

package() {
  cd "$srcdir/cookiecutter-$pkgver"

  python2 setup.py install --root="$pkgdir/" --optimize=1

  install -Dm644 LICENSE "$pkgdir/usr/share/licenses/cookiecutter/LICENSE"
}
