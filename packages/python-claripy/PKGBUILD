# This file is part of BlackArch Linux ( https://www.blackarch.org/ ).
# See COPYING for license details.

pkgname=python-claripy
_pkgname=${pkgname#python-}
pkgver=9.2.187
pkgrel=1
pkgdesc='An abstraction layer for constraint solvers.'
arch=('any')
url='https://pypi.org/project/claripy/#files'
license=('custom:unknown')
depends=('python' 'z3' 'python-future' 'python-cachetools' 'python-decorator'
         'python-pysmt')
makedepends=('python-build' 'python-pip')
source=("https://files.pythonhosted.org/packages/source/${_pkgname::1}/$_pkgname/$_pkgname-$pkgver.tar.gz")
sha512sums=('084f4ba7b07b6751f0bbeecadbb75aed6a16c28c00d6e4178d1be389a7f4dc0cc76c0983a5961a56d65bc33139a8743a50295d2994a4d72b0557d9a36792cd5c')

build() {
  cd "$_pkgname-$pkgver"

  python -m build --wheel --outdir="$startdir/dist"
}

package() {
  cd "$_pkgname-$pkgver"

  pip install \
    --verbose \
    --disable-pip-version-check \
    --no-warn-script-location \
    --ignore-installed \
    --no-compile \
    --no-deps \
    --root="$pkgdir" \
    --prefix=/usr \
    --no-index \
    --find-links="file://$startdir/dist" \
    $_pkgname
}

