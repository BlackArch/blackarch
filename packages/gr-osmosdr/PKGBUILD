# Maintainer: Dominik Heidler <dheidler@gmail.com>
pkgname=gr-osmosdr
pkgver=0.1.1
pkgrel=5
_git_commit='0d10f5e9bc950d6d2b3c39ae574e2d325a0fbeb6'
pkgdesc="GNU Radio source block for Funcube Dongle, RTL-SDR, USRP and OsmoSDR devices"
arch=('i686' 'x86_64')
url="http://sdr.osmocom.org/trac/"
license=('GPL')
depends=('gnuradio' 'swig' 'rtl-sdr' 'libuhd' 'gr-fcdproplus' 'hackrf' 'bladerf')
makedepends=('git' 'cmake' 'boost' 'python2-cheetah')
conflicts=('gr-osmosdr-git')
source=("git://git.osmocom.org/gr-osmosdr#commit=$_git_commit")
md5sums=('SKIP')

build() {
  cd "$srcdir/gr-osmosdr"

  mkdir build
  cd build

  cmake \
    -Wno-dev \
    -DCMAKE_BUILD_TYPE=Release \
    -DPYTHON_EXECUTABLE=$(which python2) \
    -DPYTHON_INCLUDE_DIR=$(echo /usr/include/python2*) \
    -DPYTHON_LIBRARY=$(echo /usr/lib/libpython2.*.so) \
    -DCMAKE_INSTALL_PREFIX=/usr ../
  make
}

package() {
  cd "$srcdir/gr-osmosdr/build/"
  make "DESTDIR=$pkgdir" install
}
