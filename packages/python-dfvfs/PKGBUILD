# This file is part of BlackArch Linux ( https://www.blackarch.org/ ).
# See COPYING for license details.

pkgbase=python-dfvfs
pkgname=('python2-dfvfs' 'python-dfvfs')
_pkgname=dfvfs
pkgver=20200211
pkgrel=1
pkgdesc='Python module that provides read-only access to file-system objects from various storage media types and file formats.'
arch=('any')
url='https://pypi.org/project/dfvfs/'
license=('Apache')
makedepends=('python2-setuptools' 'python-setuptools')
source=("https://files.pythonhosted.org/packages/5c/95/771c37bb916ae0b1a4c3e8eedd5a48f16233274f3edc4a9e704fe24d20e1/$_pkgname-$pkgver.tar.gz")
sha512sums=('7ca6228fe0c7d3c3e87699f1173aa32aad3bac7aa6bfc861e505f9199a2c9774118db3f6ef06a106bf7d268b68a6ef5445dafbeecb45f35de69451b4af36114f')

package_python2-dfvfs() {
  depends=('python2')

  cd "$_pkgname-$pkgver"

  python2 setup.py install --root="$pkgdir" --prefix=/usr -O1

  mv $pkgdir/usr/share/doc/dfvfs{,2}
}

package_python-dfvfs() {
  depends=('python')

  cd "$_pkgname-$pkgver"

  python setup.py install --root="$pkgdir" --prefix=/usr -O1
}

