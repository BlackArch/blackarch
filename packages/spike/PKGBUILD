# This file is part of BlackArch Linux ( http://blackarch.org ).
# See COPYING for license details.

pkgname=spike
pkgver=2.9
pkgrel=6
groups=('blackarch' 'blackarch-fuzzer')
pkgdesc="IMMUNITYsec's fuzzer creation kit in C"
arch=('x86_64' 'armv6h' 'armv7h' 'aarch64')
url='http://www.immunitysec.com/resources-freesoftware.shtml'
license=('GPL2')
depends=('gcc' 'automake' 'openssl')
optdepends=('python2: needed to run SPIKE_Console.py python GUI'
            'wxpython: needed to run SPIKE_Console.py python GUI')
source=("http://www.immunitysec.com/downloads/SPIKE${pkgver}.tgz")
sha512sums=('733d788dfb0558bb1fa8e083e7dca095f188cbc65d7bb94349dd487a84a627937b2dea5fb32d16272deea8d464085b8c6efc374ffd6806710106358d873439e2')

prepare() {
  cd $srcdir/SPIKE/SPIKE/src
  sed -i 's|/usr/bin/python|/usr/bin/python2|g' SPIKE_Console.py
}

build() {
  cd $srcdir/SPIKE/SPIKE/src
  aclocal
  automake --add-missing
  autoconf
  ./configure --prefix=/usr
  make
}

package() {
  cd $srcdir/SPIKE/SPIKE/src

  # Create directory structure.
  install -dm 755 "$pkgdir/usr/bin"
  install -dm 755 "$pkgdir/usr/lib"
  install -dm 755 "$pkgdir/usr/include/spike"
  install -dm 755 "$pkgdir/usr/share/spike"
  install -dm 755 "$pkgdir/usr/share/doc/spike"

  # Install binaries.
  # The package doesn't provide a decent method of installation.
  for bin in citrix closed_source_web_server_fuzz dceoversmb dltest do_post \
    generic_chunked generic_listen_tcp generic_send_tcp generic_send_udp \
    generic_web_server_fuzz generic_web_server_fuzz2 gopherd halflife \
    line_send_tcp msrpcfuzz msrpcfuzz_udp ntlm2 ntlm_brute pmspike post_fuzz \
    post_spike quake quakeserver sendmsrpc ss_spike statd_spike sunrpcfuzz \
    webfuzz x11_spike
  do
    install -m755 "$bin" "$pkgdir/usr/bin/spike-${bin/_/-}"
  done

  # Install libraries.
  install -m755 *.so "$pkgdir/usr/lib/"

  cd $srcdir/SPIKE/SPIKE

  # Install includes.
  install -m644 include/* "$pkgdir/usr/include/spike"

  # Install documentation.
  install -m644 *.txt documentation/* \
          $pkgdir/usr/share/doc/spike

  # Install data.
  cp --no-preserve=ownership -R dcedump data encrypted "$pkgdir/usr/share/spike"
}
