# This file is part of BlackArch Linux ( https://www.blackarch.org/ ).
# See COPYING for license details.

pkgbase=python-bson
pkgname=('python2-bson' 'python-bson')
_pkgname=bson
pkgver=0.5.9
pkgrel=1
pkgdesc='BSON codec for Python.'
arch=('any')
url='https://pypi.org/project/bson/#files'
license=('BSD')
makedepends=('python2-setuptools' 'python2-pip' 'python-setuptools'
             'python-pip')
source=("https://files.pythonhosted.org/packages/68/dd/fe19add4f6f6d6e8cac5f00f62ff33892bbf93bf23669501c75a63346d70/$_pkgname-$pkgver.tar.gz")
sha512sums=('7536db67ab5d0f9fe502cd4951dbdf667253f83eeaabeaa634c4168d57545499d833c57daa5b1012dfc5c541faf94454a379b690bada0a9e5f978f388b257021')

prepare() {
  cp -a "$_pkgname-$pkgver"{,-2}
}

build() {
  cd "$_pkgname-$pkgver-2"

  python2 setup.py build

  cd "$srcdir/$_pkgname-$pkgver"

  python setup.py build
}

package_python2-bson() {
  depends=('python2' 'python2-six' 'python2-dateutil')

  cd "$_pkgname-$pkgver-2"

  python2 setup.py install --root="$pkgdir" --prefix=/usr -O1 --skip-build

  rm -f "$pkgdir/usr/lib/python2.7/site-packages/bson/__init__."{py,pyc,pyo}
}

package_python-bson() {
  depends=('python' 'python-six' 'python-dateutil')

  cd "$_pkgname-$pkgver"

  python setup.py install --root="$pkgdir" --prefix=/usr -O1 --skip-build

  rm -f "$pkgdir/usr/lib/python3.8/site-packages/bson/__init__."{py,pyc,pyo}
}

