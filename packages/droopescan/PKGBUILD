# This file is part of BlackArch Linux ( https://www.blackarch.org/ ).
# See COPYING for license details.

pkgname=droopescan
pkgver=1.41.2
pkgrel=2
groups=('blackarch' 'blackarch-webapp' 'blackarch-scanner')
pkgdesc='A plugin-based scanner that aids security researchers in identifying issues with several CMSs, mainly Drupal & Silverstripe.'
arch=('any')
url='https://github.com/droope/droopescan'
license=('GPL3')
depends=('python' 'python-cement' 'python-requests' 'python-pystache'
         'python-mock' 'python-lxml' 'python-nose' 'python-beautifulsoup4'
         'python-simplejson')
makedepends=('git')
source=("https://github.com/droope/droopescan/archive/$pkgver.tar.gz")
sha512sums=('2863dc97ee76e51283d00293d5aeaff1e42f65d45bd6ef973759a093eea72c42f2683fcb43f28b77d95e451289efd7ba8d8de61a77172ff8efe5e0636e202c0a')

build() {
  cd "$pkgname-$pkgver"

  python setup.py build
}

package() {
  cd "$pkgname-$pkgver"

  python setup.py install --prefix=/usr --root="$pkgdir" --optimize=1 \
    --skip-build

  install -Dm 644 -t "$pkgdir/usr/share/doc/$pkgname/" README.md CHANGELOG
  install -Dm 644 LICENSE "$pkgdir/usr/share/licenses/$pkgname/LICENSE"
}

