# This file is part of BlackArch Linux ( http://blackarch.org ).
# See COPYING for license details.

pkgname='droopescan'
pkgver='1.39.0'
pkgrel=1
groups=('blackarch' 'blackarch-scanner' 'blackarch-webapp')
pkgdesc='A plugin-based scanner that aids security researchers in identifying issues with several CMSs, mainly Drupal & Silverstripe.'
arch=('any')
url='https://github.com/droope/droopescan'
license=('GPL3')
depends=('python2' 'python2-cement' 'python2-requests' 'python2-pystache'
         'python2-futures' 'python2-mock' 'python2-lxml' 'python2-nose'
         'python2-beautifulsoup4' 'python2-simplejson')
makedepends=('git')
source=("https://github.com/droope/droopescan/archive/${pkgver}.tar.gz")
sha1sums=('f3278dd5ed34db84ea55df722af0fcb74d57b82e')

build() {
  cd "$srcdir/droopescan-$pkgver"

  python2 setup.py build
}

package() {
  cd "$srcdir/droopescan-$pkgver"

  python2 setup.py install --prefix=/usr --root="$pkgdir" --optimize=1

  install -Dm644 -t "$pkgdir/usr/share/doc/droopescan/" README.md CHANGELOG \
    requirements.txt requirements_test.txt
  install -Dm644 LICENSE "$pkgdir/usr/share/licenses/droopescan/LICENSE"
}
