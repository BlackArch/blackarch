# This file is part of BlackArch Linux ( https://www.blackarch.org/ ).
# See COPYING for license details.

pkgname=droopescan
pkgver=1.42.0
pkgrel=1
groups=('blackarch' 'blackarch-webapp' 'blackarch-scanner')
pkgdesc='A plugin-based scanner that aids security researchers in identifying issues with several CMSs, mainly Drupal & Silverstripe.'
arch=('any')
url='https://github.com/droope/droopescan'
license=('GPL3')
depends=('python' 'python-cement' 'python-requests' 'python-pystache'
         'python-mock' 'python-lxml' 'python-nose' 'python-beautifulsoup4'
         'python-simplejson' 'python-twisted' 'python-dateutil')
makedepends=('git')
source=("https://github.com/droope/$pkgname/archive/$pkgver.tar.gz")
sha512sums=('ace441cb676d3e56d19bf4315502aea654c70c1cd4c2d93e51375fe3ed389144808c1a06b01302d5e1610c6a80e9448fe2c8aea1eabce6ea40a456992aaff5d3')

build() {
  cd "$pkgname-$pkgver"

  python setup.py build
}

package() {
  cd "$pkgname-$pkgver"

  python setup.py install --root="$pkgdir" --prefix=/usr -O1 --skip-build

  install -Dm 644 -t "$pkgdir/usr/share/doc/$pkgname/" README.md CHANGELOG
  install -Dm 644 LICENSE "$pkgdir/usr/share/licenses/$pkgname/LICENSE"
}

