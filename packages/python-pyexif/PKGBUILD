# This file is part of BlackArch Linux ( https://www.blackarch.org/ ).
# See COPYING for license details.

pkgbase=python-pyexif
pkgname=('python2-pyexif' 'python-pyexif')
_pkgname=pyexif
pkgver=0.9.0
pkgrel=2
pkgdesc='Python module to read/write EXIF image data'
arch=('any')
url='https://pypi.org/project/pyexif/#files'
license=('Python')
makedepends=('python2-setuptools' 'python-setuptools')
source=("https://files.pythonhosted.org/packages/31/bc/230deeb4f7b16a860d0c8e6b0c011ca3116b0050740b0b1b7c95775b4b3d/$_pkgname-$pkgver.tar.gz")
sha512sums=('e2862fa1fee3dc32266aef241837d389e6c6b6801d484e6639bc434429b95da61316add3adfb6cba92a831c4b10b89c91b706d4c863bcb7b9857498dee6548ca')

prepare() {
  cp -a "$_pkgname-$pkgver"{,-2}
}

build() {
  cd "$_pkgname-$pkgver-2"

  python2 setup.py build

  cd "$srcdir/$_pkgname-$pkgver"

  python setup.py build
}

package_python2-pyexif() {
  depends=('python2')

  cd "$_pkgname-$pkgver-2"

  python2 setup.py install --prefix=/usr --root="$pkgdir" --optimize=1 \
    --skip-build
}

package_python-pyexif() {
  depends=('python')

  cd "$_pkgname-$pkgver"

  python setup.py install --prefix=/usr --root="$pkgdir" --optimize=1 \
    --skip-build
}

