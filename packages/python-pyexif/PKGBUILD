# This file is part of BlackArch Linux ( https://www.blackarch.org/ ).
# See COPYING for license details.

pkgbase=python-pyexif
pkgname=('python2-pyexif' 'python-pyexif')
_pkgname=pyexif
pkgver=0.5.0
pkgrel=3
pkgdesc='Python module to read/write EXIF image data'
arch=('any')
url='https://pypi.org/project/pyexif/#files'
license=('Python')
makedepends=('python2-setuptools' 'python-setuptools')
source=("https://files.pythonhosted.org/packages/4f/cf/00fc5a405f6831a3006ad37fcfde01f53994e926d7e8905f8216638105a7/$_pkgname-$pkgver.tar.gz")
sha512sums=('89aa16795fe151bc02edff32f73a76e3894e86695048949791e583d78a4cd50127c283129addf34a94b6e70e9a7104b5ac9f8fc0d98e41e5fc0367892ad7c3e0')

prepare() {
  cp -a "$_pkgname-$pkgver"{,-2}
}

build() {
  cd "$_pkgname-$pkgver-2"

  python2 setup.py build

  cd "$srcdir/$_pkgname-$pkgver"

  python setup.py build
}

package_python2-pyexif() {
  depends=('python2')

  cd "$_pkgname-$pkgver-2"

  python2 setup.py install --prefix=/usr --root="$pkgdir" --optimize=1 \
    --skip-build
}

package_python-pyexif() {
  depends=('python')

  cd "$_pkgname-$pkgver"

  python setup.py install --prefix=/usr --root="$pkgdir" --optimize=1 \
    --skip-build
}

