# This file is part of BlackArch Linux ( https://www.blackarch.org/ ).
# See COPYING for license details.
# Original Maintainer (AUR): Christian Rebischke <chris.rebischke[at]archlinux[dot]org>
_pyname=oletools
pkgname=python2-oletools
pkgver=0.54.1
pkgrel=1
epoch=1
pkgdesc="Python tools to analyze security characteristics of MS Office and OLE files"
depends=('python2')
arch=('any')
url="https://bitbucket.org/decalage/oletools"
license=('BSD')
source=("https://files.pythonhosted.org/packages/41/7b/2630a92a0ad7e72ac4eeb095e3154eb1b4c1673c23401a2acd669556c7dd/oletools-0.54.1.zip")
sha512sums=('6a21ed37a245c927ea01bbc7363ee3eca28ca5f2b4825910e83f1afaf36d3335dffa06232f555e3d687a16ff5ae0fcf0812905677e216a8c28858abe04c26836')

package() {
  cd "${srcdir}/${_pyname}-${pkgver}"
  python2 setup.py install -O1 --root="${pkgdir}"
  install -Dm 644 ${_pyname}/LICENSE.txt "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
  install -Dm 644 README.md "${pkgdir}/usr/share/doc/${pkgname}/README"
  cp ${_pyname}/doc/* "${pkgdir}/usr/share/doc/${pkgname}"

  # fixing shebang line
  cd "${pkgdir}/usr/lib/python2.7/site-packages/oletools/"
  for file in *.py; do
    if [ "${file}" != "__init__.py" ]; then
      sed -i '1s/python/python2/' "${file}"
    fi
  done
}

# vim:set et sw=2 ts=2 tw=79:
