# This file is part of BlackArch Linux ( http://blackarch.org ).
# See COPYING for license details.
# Original Maintainer (AUR): Christian Rebischke <chris.rebischke[at]archlinux[dot]org>
_pyname=oletools
pkgname=python2-oletools
pkgver=0.53.1
pkgrel=2
pkgdesc="Python tools to analyze security characteristics of MS Office and OLE files"
depends=('python2')
arch=('any')
url="https://bitbucket.org/decalage/oletools"
license=('BSD')
source=("https://files.pythonhosted.org/packages/79/f5/9b1a89145ac9bce77c235fee549fc7af617d778bb29af4c8dd1561813a10/oletools-$pkgver.zip")
sha512sums=('86357ade13b29dc53aabc24a4bf4f8ea12fd9de88fc5021c47706aaacdd8572fdf7ecdb9054acc7ad061ee3704361d852d7f16911203b63383decddd170c10c0')

package() {
  cd "${srcdir}/${_pyname}-${pkgver}"
  python2 setup.py install -O1 --root="${pkgdir}"
  install -Dm 644 ${_pyname}/LICENSE.txt "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
  install -Dm 644 README.md "${pkgdir}/usr/share/doc/${pkgname}/README"
  cp ${_pyname}/doc/* "${pkgdir}/usr/share/doc/${pkgname}"

  # fixing shebang line
  cd "${pkgdir}/usr/lib/python2.7/site-packages/oletools/"
  for file in *.py; do
    if [ "${file}" != "__init__.py" ]; then
      sed -i '1s/python/python2/' "${file}"
    fi
  done
}

# vim:set et sw=2 ts=2 tw=79:
