# This file is part of BlackArch Linux ( https://www.blackarch.org/ ).
# See COPYING for license details.

pkgbase=python-pathlib2
pkgname=('python2-pathlib2' 'python-pathlib2')
_pkgname=pathlib2
pkgver=2.3.6
pkgrel=2
pkgdesc='Object-oriented filesystem paths.'
url='https://pypi.org/project/pathlib2/#files'
arch=('any')
license=('MIT')
makedepends=('python2-setuptools' 'python-setuptools')
source=("https://files.pythonhosted.org/packages/df/16/e9d6bcf1aed52a55bc1696324ab22586716053b3e97b85266e0f3ad36bae/$_pkgname-$pkgver.tar.gz")
sha512sums=('ed41792338d5808cdd068f6c04021b2918e5376fc6297f163712dec6b00a1f96f7905eeea0a601c333d253e0f1663a0472d842b0d920d680992045bbc36db970')

prepare() {
  cp -a "$_pkgname-$pkgver"{,-2}
}

build() {
  cd "$_pkgname-$pkgver-2"

  python2 setup.py build

  cd "$srcdir/$_pkgname-$pkgver"

  python setup.py build

}

package_python2-pathlib2() {
  depends=('python2' 'python2-scandir')

  cd "$_pkgname-$pkgver-2"

  python2 setup.py install --root="$pkgdir" --prefix=/usr -O1 --skip-build
}

package_python-pathlib2() {
  depends=('python' 'python-scandir')

  cd "$_pkgname-$pkgver"

  python setup.py install --root="$pkgdir" --prefix=/usr -O1 --skip-build
}

