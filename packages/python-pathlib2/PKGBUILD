# This file is part of BlackArch Linux ( https://www.blackarch.org/ ).
# See COPYING for license details.

pkgbase=python-pathlib2
pkgname=('python2-pathlib2' 'python-pathlib2')
_pkgname=pathlib2
pkgver=2.3.5
pkgrel=3
pkgdesc='Object-oriented filesystem paths.'
url='https://pypi.org/project/pathlib2/#files'
arch=('any')
license=('MIT')
makedepends=('python2-setuptools' 'python-setuptools')
source=("https://files.pythonhosted.org/packages/94/d8/65c86584e7e97ef824a1845c72bbe95d79f5b306364fa778a3c3e401b309/$_pkgname-$pkgver.tar.gz")
sha512sums=('3f0f212bf5208d0965ac02a590bef6ac0ec5e3b702828c06c3c9d37119b3db422cc7873de614fdc37ebfb3f52239a6517008d9672a176ca15d03fe13baeeddc8')

prepare() {
  cp -a "$_pkgname-$pkgver"{,-2}
}

build() {
  cd "$_pkgname-$pkgver-2"

  python2 setup.py build

  cd "$srcdir/$_pkgname-$pkgver"

  python setup.py build

}

package_python2-pathlib2() {
  depends=('python2' 'python2-scandir')

  cd "$_pkgname-$pkgver-2"

  python2 setup.py install --root="$pkgdir" --prefix=/usr -O1 --skip-build
}

package_python-pathlib2() {
  depends=('python' 'python-scandir')

  cd "$_pkgname-$pkgver"

  python setup.py install --root="$pkgdir" --prefix=/usr -O1 --skip-build
}

