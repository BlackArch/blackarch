# This file is part of BlackArch Linux ( https://www.blackarch.org/ ).
# See COPYING for license details.

pkgbase=python-pyvirtualdisplay
pkgname=('python2-pyvirtualdisplay' 'python-pyvirtualdisplay')
_pkgname=PyVirtualDisplay
pkgver=1.0
pkgrel=1
pkgdesc='Python wrapper for Xvfb, Xephyr and Xvnc.'
arch=('any')
url='https://pypi.org/project/pyvirtualdisplay/#files'
license=('BSD')
makedepends=('python2-setuptools' 'python-setuptools')
source=("https://files.pythonhosted.org/packages/91/44/7c534a3372489e5db135c969556c6654b43be2f1239ebcc8d7baa747f4a4/PyVirtualDisplay-$pkgver.tar.gz")
sha512sums=('f00eae07b19760df7b0bdb5698e18348d626588cff29ca28bececd436f98b84daceb60f360fa3f470f800797c3d2ca10c939ef1b2740f4b2cf332ed06504589a')

prepare() {
  cp -a "$_pkgname-$pkgver"{,-2}
}

build() {
  cd "$_pkgname-$pkgver-2"

  python2 setup.py build

  cd "$srcdir/$_pkgname-$pkgver"

  python setup.py build
}

package_python2-pyvirtualdisplay() {
  depends=('python2' 'python2-easyprocess')

  cd "$_pkgname-$pkgver-2"

  python2 setup.py install --prefix=/usr --root="$pkgdir" --optimize=1 \
    --skip-build
}

package_python-pyvirtualdisplay() {
  depends=('python' 'python-easyprocess')

  cd "$_pkgname-$pkgver"

  python setup.py install --prefix=/usr --root="$pkgdir" --optimize=1 \
    --skip-build
}

