# This file is part of BlackArch Linux ( https://www.blackarch.org/ ).
# See COPYING for license details.

pkgbase=python-pyvirtualdisplay
pkgname=('python2-pyvirtualdisplay' 'python-pyvirtualdisplay')
_pkgname=PyVirtualDisplay
pkgver=0.2.4
pkgrel=1
pkgdesc='Python wrapper for Xvfb, Xephyr and Xvnc.'
arch=('any')
url='https://pypi.org/project/pyvirtualdisplay/#files'
license=('BSD')
makedepends=('python2-setuptools' 'python-setuptools')
source=("https://files.pythonhosted.org/packages/3f/70/d0672122363f70b8fd29aeabd0b90a947b08c104e9b6cefd2ae6719ec2bc/PyVirtualDisplay-0.2.4.tar.gz")
sha512sums=('e32a441681158381a2ba3e09e9b76f46817bbee1f48f729c40d2d3c6cd705ab2b0a3e94a90b579068924f35854794c09c9bd78160bedaced834fedf8015fafe8')

prepare() {
  cp -a "$_pkgname-$pkgver" "$_pkgname-$pkgver-2"
}

build() {
  cd "$_pkgname-$pkgver-2"

  python2 setup.py build

  cd "$srcdir/$_pkgname-$pkgver"

  python setup.py build
}

package_python2-pyvirtualdisplay() {
  depends=('python2' 'python2-easyprocess')

  cd "$_pkgname-$pkgver-2"

  python2 setup.py install --prefix=/usr --root="$pkgdir" --optimize=1 \
    --skip-build
}

package_python-pyvirtualdisplay() {
  depends=('python' 'python-easyprocess')

  cd "$_pkgname-$pkgver"

  python setup.py install --prefix=/usr --root="$pkgdir" --optimize=1 \
    --skip-build
}

