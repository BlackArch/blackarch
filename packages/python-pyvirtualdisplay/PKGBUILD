# This file is part of BlackArch Linux ( https://www.blackarch.org/ ).
# See COPYING for license details.

pkgbase=python-pyvirtualdisplay
pkgname=('python2-pyvirtualdisplay' 'python-pyvirtualdisplay')
_pkgname=PyVirtualDisplay
pkgver=0.2.3
pkgrel=2
pkgdesc='Python wrapper for Xvfb, Xephyr and Xvnc.'
arch=('any')
url='https://pypi.org/project/pyvirtualdisplay/#files'
license=('BSD')
makedepends=('python2-setuptools' 'python-setuptools')
source=("https://files.pythonhosted.org/packages/bb/bf/599964d05464400b5a0b1dc69c3b2c93b85902e5fc31758ead0ec169b058/$_pkgname-$pkgver.tar.gz")
sha512sums=('e0c02a00fe8dd219cac8e19ef56748597c20b5d4394757123cc0c66474b66031513ca014e76c003ab46f3378be9ff46a042faa6f795cb28202bb6b2136feef08')

prepare() {
  cp -a "$_pkgname-$pkgver" "$_pkgname-$pkgver-2"
}

build() {
  cd "$_pkgname-$pkgver-2"

  python2 setup.py build

  cd "$srcdir/$_pkgname-$pkgver"

  python setup.py build
}

package_python2-pyvirtualdisplay() {
  depends=('python2' 'python2-easyprocess')

  cd "$_pkgname-$pkgver-2"

  python2 setup.py install --prefix=/usr --root="$pkgdir" --optimize=1 \
    --skip-build
}

package_python-pyvirtualdisplay() {
  depends=('python' 'python-easyprocess')

  cd "$_pkgname-$pkgver"

  python setup.py install --prefix=/usr --root="$pkgdir" --optimize=1 \
    --skip-build
}

