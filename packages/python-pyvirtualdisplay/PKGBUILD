# This file is part of BlackArch Linux ( https://www.blackarch.org/ ).
# See COPYING for license details.

pkgbase=python-pyvirtualdisplay
pkgname=('python2-pyvirtualdisplay' 'python-pyvirtualdisplay')
_pkgname=PyVirtualDisplay
pkgver=2.0
pkgrel=1
pkgdesc='Python wrapper for Xvfb, Xephyr and Xvnc.'
arch=('any')
url='https://pypi.org/project/pyvirtualdisplay/#files'
license=('BSD')
makedepends=('python2-setuptools' 'python-setuptools')
source=("https://files.pythonhosted.org/packages/d0/79/932927dad03814509bb972a0795bc59174328f9ed3c1418ba9d684019266/PyVirtualDisplay-$pkgver.tar.gz")
sha512sums=('3eb4c95bbf4e31cfd090c12156b91da71bbe3bdba135720e1485cfc9863c1668826e98f78edd8a436a8fe854ce848c48cff165ac2f0097bc2f0342c0e3183bd5')

prepare() {
  cp -a "$_pkgname-$pkgver"{,-2}
}

build() {
  cd "$_pkgname-$pkgver-2"

  python2 setup.py build

  cd "$srcdir/$_pkgname-$pkgver"

  python setup.py build
}

package_python2-pyvirtualdisplay() {
  depends=('python2' 'python2-easyprocess')

  cd "$_pkgname-$pkgver-2"

  python2 setup.py install --prefix=/usr --root="$pkgdir" --optimize=1 \
    --skip-build
}

package_python-pyvirtualdisplay() {
  depends=('python' 'python-easyprocess')

  cd "$_pkgname-$pkgver"

  python setup.py install --prefix=/usr --root="$pkgdir" --optimize=1 \
    --skip-build
}

