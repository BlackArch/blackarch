# This file is part of BlackArch Linux ( https://www.blackarch.org/ ).
# See COPYING for license details.

pkgname=gf
pkgver=39.dcd4c36
pkgrel=1
pkgdesc='A wrapper around grep, to help you grep for things.'
arch=('x86_64')
groups=('blackarch')
url='https://github.com/tomnomnom/gf'
license=('MIT')
depends=('glibc')
makedepends=('git' 'go')
source=("git+https://github.com/tomnomnom/$pkgname.git")
sha512sums=('SKIP')

pkgver() {
  cd $pkgname

  echo $(git rev-list --count HEAD).$(git rev-parse --short HEAD)
}

build() {
  cd $pkgname

  # If you encounter the error: go mod download: no modules specified (see 'go help mod download')
  # while building, then uncomment the following lines:
  GOPATH="$srcdir" go mod init go.mod
  GOPATH="$srcdir" go mod tidy
  GOPATH="$srcdir" go mod download
  GOPATH="$srcdir" go build \
    -trimpath \
    -buildmode=pie \
    -mod=readonly \
    -modcacherw \
    -ldflags "-s -w" \
    -o $pkgname .

}

package() {
  cd $pkgname

  install -dm 755 "$pkgdir/usr/share/$pkgname"

  install -Dm 755 $pkgname "$pkgdir/usr/bin/$pkgname"
  install -Dm 644 -t "$pkgdir/usr/share/doc/$pkgname/" README.md
  # Only install license if required according to
  # https://wiki.archlinux.org/index.php/PKGBUILD#license
  install -Dm 644 LICENSE "$pkgdir/usr/share/licenses/$pkgname/LICENSE"

  cp -a examples/ gf-completion* "$pkgdir/usr/share/$pkgname/"
}

