# This file is part of BlackArch Linux ( http://blackarch.org ).
# See COPYING for license details.
# Original Maintainer on AUR: zwindl

pkgname=gdbgui
pkgver=364.78eb7ca
pkgrel=3
pkgdesc='Browser-based gdb frontend using Flask and JavaScript to visually debug C, C++, Go, or Rust.'
groups=('blackarch' 'blackarch-debugger' 'blackarch-binary')
arch=('any')
url='https://github.com/cs01/gdbgui'
depends=('python' 'python-eventlet' 'python-flask' 'python-flask-socketio'
         'python-pygdbmi' 'python-pypugjs' 'python-enum-compat' 'python-gevent'
         'python-pygments' 'python-flask-compress' 'gdb')
makedepends=('git' 'python-setuptools')
license=('GPL3')
install='gdbgui.install'
source=('git+https://github.com/cs01/gdbgui.git' 'setup.py.patch')
sha512sums=('SKIP'
            '04cc4e1aab4575a594e40910be7f34a82193f80e53ee9b974f5123996b3cdb6ab7c43a5c168dd56e21ac41aed7998d0fea51522c536999d1dc36a4039f4fd26a')

pkgver() {
  cd gdbgui

  echo $(git rev-list --count HEAD).$(git rev-parse --short HEAD)
}

prepare() {
  cd "$srcdir"

  patch -p1 < setup.py.patch
}

build() {
  cd gdbgui

  python setup.py build
}

package() {
  cd gdbgui

  python setup.py install --root="$pkgdir" --optimize=1 --skip-build

  install -Dm644 LICENSE "$pkgdir/usr/share/licenses/gdbgui/LICENSE"

  find "$pkgdir" -type d -name 'tests' -exec rm -rf {} +
  find "$pkgdir" -type d -name '__pycache__' -exec rm -rf {} +
}

