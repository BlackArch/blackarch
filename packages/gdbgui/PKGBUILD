# This file is part of BlackArch Linux ( http://blackarch.org ).
# See COPYING for license details.
# Original Maintainer on AUR: zwindl

pkgname='gdbgui'
pkgver=334.71e669d
pkgrel=1
pkgdesc='Browser-based gdb frontend using Flask and JavaScript to visually debug C, C++, Go, or Rust.'
groups=('blackarch' 'blackarch-debugger' 'blackarch-binary')
arch=('any')
url='https://github.com/cs01/gdbgui'
depends=('python' 'python-eventlet' 'python-flask' 'python-flask-socketio'
         'python-pygdbmi' 'python-pypugjs' 'python-enum-compat' 'python-gevent'
         'python-pygments' 'python-flask-compress' 'gdb')
makedepends=('git' 'python-setuptools')
license=('GPL3')
install='gdbgui.install'
source=('git+https://github.com/cs01/gdbgui.git' 'setup.py.patch')
sha1sums=('SKIP'
          '9c1896bc069f89e81f8b31a1626b2a94bd76f17d')

pkgver() {
  cd "$srcdir/gdbgui"

  echo $(git rev-list --count HEAD).$(git rev-parse --short HEAD)
}

prepare() {
  cd "$srcdir"

  patch -p1 < ../setup.py.patch

}

build() {
  cd "$srcdir/gdbgui"

  python setup.py build
}

package() {
  cd "$srcdir/gdbgui"

  python setup.py install --root="$pkgdir" --optimize=1 --skip-build
  rm -rf "$pkgdir/usr/lib/python3.6/site-packages/tests/"

  install -Dm644 LICENSE "$pkgdir/usr/share/licenses/gdbgui/LICENSE"
}
