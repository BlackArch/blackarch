# This file is part of BlackArch Linux ( https://www.blackarch.org/ ).
# See COPYING for license details.

pkgbase=python-dict2xml
pkgname=('python2-dict2xml' 'python-dict2xml')
_pkgname=dict2xml
pkgver=1.7.8
pkgrel=1
pkgdesc='Super Simple utility to convert a python dictionary into an xml string.'
arch=('any')
url='https://pypi.org/project/dict2xml/#files'
license=('GPL2')
makedepends=('python2-setuptools' 'python-setuptools')
source=("https://files.pythonhosted.org/packages/source/${_pkgname::1}/$_pkgname/$_pkgname-$pkgver.tar.gz")
sha512sums=('8c5390e160b6a734a65a81463e2dceb053a241fca030258edac45b3ff501629c1d895b14a4da6dd474ebf21aad31293cb48107973dee61eaa05f31cfc98a8ab8')

prepare() {
  cp -a "$_pkgname-$pkgver"{,-2}
}

build() {
  cd "$_pkgname-$pkgver-2"

  python2 setup.py build

  cd "$srcdir/$_pkgname-$pkgver"

  python setup.py build
}

package_python2-dict2xml() {
  depends=('python2' 'python2-six')

  cd "$_pkgname-$pkgver-2"

  python2 setup.py install --root="$pkgdir" --prefix=/usr -O1 --skip-build
}

package_python-dict2xml() {
  depends=('python' 'python-six')

  cd "$_pkgname-$pkgver"

  python setup.py install --root="$pkgdir" --prefix=/usr -O1 --skip-build
}

