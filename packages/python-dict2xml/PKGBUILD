# This file is part of BlackArch Linux ( https://www.blackarch.org/ ).
# See COPYING for license details.

pkgbase=python-dict2xml
pkgname=('python2-dict2xml' 'python-dict2xml')
_pkgname=dict2xml
pkgver=1.6.1
pkgrel=2
pkgdesc='Super Simple utility to convert a python dictionary into an xml string.'
arch=('any')
url='https://pypi.org/project/dict2xml/#files'
license=('GPL2')
makedepends=('python2-setuptools' 'python-setuptools')
source=("https://files.pythonhosted.org/packages/ed/28/b169ce37d415e5c924fa4f2b157b051ef9fa5ab25ae94db192f19dd9c6b1/$_pkgname-$pkgver.tar.gz")
sha512sums=('10bccfe4c27620d371103cddcad17c0965f07793e75011341f0b383332030897a8943d489b2a7a81306e5513942c00a547e3354e9e9fa1ab0eb3f6a206e9d388')

prepare() {
  cp -a "$_pkgname-$pkgver"{,-2}
}

build() {
  cd "$_pkgname-$pkgver-2"

  python2 setup.py build

  cd "$srcdir/$_pkgname-$pkgver"

  python setup.py build
}

package_python2-dict2xml() {
  depends=('python2' 'python2-six')

  cd "$_pkgname-$pkgver-2"

  python2 setup.py install --prefix=/usr --root="$pkgdir" --optimize=1 \
    --skip-build
}

package_python-dict2xml() {
  depends=('python' 'python-six')

  cd "$_pkgname-$pkgver"

  python setup.py install --prefix=/usr --root="$pkgdir" --optimize=1 \
    --skip-build
}

