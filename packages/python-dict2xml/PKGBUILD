# This file is part of BlackArch Linux ( https://www.blackarch.org/ ).
# See COPYING for license details.

pkgbase=python-dict2xml
pkgname=('python2-dict2xml' 'python-dict2xml')
_pkgname=dict2xml
pkgver=1.7.1
pkgrel=1
pkgdesc='Super Simple utility to convert a python dictionary into an xml string.'
arch=('any')
url='https://pypi.org/project/dict2xml/#files'
license=('GPL2')
makedepends=('python2-setuptools' 'python-setuptools')
source=("https://files.pythonhosted.org/packages/72/04/331c8d0873fab15309e57b803aa0de23ba074ba16e0239445dbb6c63dfa1/$_pkgname-$pkgver.tar.gz")
sha512sums=('37dfbd2b2f1732dfd09bab910f6a8dbe5a644545aa9d4fa82d5dadba3754a04aa39e25de777ec1a2174729ae214eb4b27b5d4c1a2b5e760abd7d7b76308b1a25')

prepare() {
  cp -a "$_pkgname-$pkgver"{,-2}
}

build() {
  cd "$_pkgname-$pkgver-2"

  python2 setup.py build

  cd "$srcdir/$_pkgname-$pkgver"

  python setup.py build
}

package_python2-dict2xml() {
  depends=('python2' 'python2-six')

  cd "$_pkgname-$pkgver-2"

  python2 setup.py install --prefix=/usr --root="$pkgdir" --optimize=1 \
    --skip-build
}

package_python-dict2xml() {
  depends=('python' 'python-six')

  cd "$_pkgname-$pkgver"

  python setup.py install --prefix=/usr --root="$pkgdir" --optimize=1 \
    --skip-build
}

