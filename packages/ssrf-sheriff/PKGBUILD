# This file is part of BlackArch Linux ( https://www.blackarch.org/ ).
# See COPYING for license details.

pkgname=ssrf-sheriff
pkgver=2.f95d691
pkgrel=1
groups=('blackarch' 'blackarch-webapp' 'blackarch-proxy')
pkgdesc='A simple SSRF-testing sheriff written in Go.'
arch=('x86_64' 'aarch64')
url='https://github.com/teknogeek/ssrf-sheriff'
license=('MIT')
depends=('glibc')
makedepends=('git' 'go-pie')
source=("git+https://github.com/teknogeek/$pkgname.git")
sha512sums=('SKIP')
_url="github.com/teknogeek/$pkgname"

pkgver() {
  cd $pkgname

  echo $(git rev-list --count HEAD).$(git rev-parse --short HEAD)
}

build() {
  mkdir -p "src/$_url" && rm -rf "src/$_url" && mv $pkgname "src/$_url" &&
    cd "src/$_url"

  GOPATH="$srcdir" go get -d -t $_url
}

package() {
  install -dm 755 "$pkgdir/usr/bin"
  install -dm 755 "$pkgdir/usr/share/$pkgname"

  GOPATH="$srcdir" go build -v $_url

  install -Dm 755 "$srcdir/$pkgname" "$pkgdir/usr/share/$pkgname/$pkgname"
  cp -a "src/$_url/config" "src/$_url/templates" "$pkgdir/usr/share/$pkgname/"
  mv "$pkgdir/usr/share/$pkgname/config/base.example.yaml" \
    "$pkgdir/usr/share/$pkgname/config/base.yaml"
  install -Dm 644 -t "$pkgdir/usr/share/doc/$pkgname/README.md" \
    "src/$_url/README.md"
  install -Dm 644 "src/$_url/LICENSE.txt" \
    "$pkgdir/usr/share/licenses/$pkgname/LICENSE"

  cat > "$pkgdir/usr/bin/$pkgname" << EOF
#!/bin/sh
cd /usr/share/$pkgname
exec ./$pkgname "\$@"
EOF

  chmod +x "$pkgdir/usr/bin/$pkgname"
}

