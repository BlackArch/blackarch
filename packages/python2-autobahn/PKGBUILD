# This file is part of BlackArch Linux ( https://www.blackarch.org/ ).
# See COPYING for license details.

pkgname=python2-autobahn
_pkgname=autobahn
pkgver=19.9.2
pkgrel=1
pkgdesc='Real-time framework for Web, Mobile & Internet of Things.'
arch=('any')
url='https://crossbar.io/autobahn/'
license=('MIT')
depends=('python2-twisted' 'python2-six' 'python2-txaio' 'python2-wsaccel')
makedepends=('python2-setuptools' 'python2-argon2_cffi' 'python2-msgpack'
             'python2-passlib' 'python2-pynacl')
optdepends=(
  'python2-msgpack: MsgPack serializer support'
  'python2-argon2_cffi: WAMP-SCRAM authentication support'
  'python2-passlib: WAMP-SCRAM authentication support'
  'python2-pynacl: WAMP-cryptosign support'
)
source=("https://files.pythonhosted.org/packages/f1/99/af285b10c5b00828d73e061c55ad1e2b07d456be10c00cd812dfe902ab5e/$_pkgname-$pkgver.tar.gz"
        'skip-test-missing-serializers.patch')
sha512sums=('e7edfe5a22b2daf548ccf1b1f4c98d3a76c26b2388165b690789ecc4fa288f3d854708f0acc857348bb80e11a8b7b0829d9d9730550ddf2277fab14f40ccf6fc'
            '53f1845d1504be3c3d3e2183647f5758b5041e50954c22f4c4e25f4e330a35fbc901814870d29f4df74c277343fc873c8f81ae46f2daa9173695fd94ccd11c09')

prepare() {
  cd "$_pkgname-$pkgver"

  patch -Np1 -i ../skip-test-missing-serializers.patch
}

build() {
  cd "$_pkgname-$pkgver"

  python2 setup.py build
}

package() {
  cd "$_pkgname-$pkgver"

  python2 setup.py install --root="$pkgdir" -O1 --skip-build

  install -Dm 644 LICENSE "$pkgdir/usr/share/licenses/$pkgname/LICENSE"
}

