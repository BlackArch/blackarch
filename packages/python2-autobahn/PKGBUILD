# This file is part of BlackArch Linux ( https://www.blackarch.org/ ).
# See COPYING for license details.

pkgname=python2-autobahn
pkgver=19.3.3
pkgrel=1
pkgdesc='Real-time framework for Web, Mobile & Internet of Things.'
arch=('any')
url='https://crossbar.io/autobahn/'
license=('MIT')
depends=('python2-twisted' 'python2-six' 'python2-txaio' 'python2-wsaccel')
makedepends=('python2-setuptools' 'python2-argon2_cffi' 'python2-msgpack'
             'python2-passlib' 'python2-pynacl')
optdepends=(
  'python2-msgpack: MsgPack serializer support'
  'python2-argon2_cffi: WAMP-SCRAM authentication support'
  'python2-passlib: WAMP-SCRAM authentication support'
  'python2-pynacl: WAMP-cryptosign support'
)
source=("https://files.pythonhosted.org/packages/46/8b/c329cf7a0f591805d8a9fe4b6e88467b8c6e09022b9dfb2242577b983898/autobahn-19.3.3.tar.gz"
        'skip-test-missing-serializers.patch')
sha512sums=('6676e8759a251753bbabec7c6f920f1909995470fe4e3cc82487b8b5c9601807691986de1c951d7fd4a0eddb0cd477b222d1479a6eca3b85d8c3e872a57c3e13'
            '53f1845d1504be3c3d3e2183647f5758b5041e50954c22f4c4e25f4e330a35fbc901814870d29f4df74c277343fc873c8f81ae46f2daa9173695fd94ccd11c09')

prepare() {
  cd "autobahn-$pkgver"

  patch -Np1 -i ../skip-test-missing-serializers.patch
}

build() {
  cd "autobahn-$pkgver"

  python2 setup.py build
}

package() {
  cd "autobahn-$pkgver"

  python2 setup.py install --root="$pkgdir" --optimize=1 --skip-build

  install -Dm 644 LICENSE "$pkgdir/usr/share/licenses/$pkgname/LICENSE"
}

