# This file is part of BlackArch Linux ( https://www.blackarch.org/ ).
# See COPYING for license details.

pkgname=python2-autobahn
_pkgname=autobahn
pkgver=19.7.2
pkgrel=1
pkgdesc='Real-time framework for Web, Mobile & Internet of Things.'
arch=('any')
url='https://crossbar.io/autobahn/'
license=('MIT')
depends=('python2-twisted' 'python2-six' 'python2-txaio' 'python2-wsaccel')
makedepends=('python2-setuptools' 'python2-argon2_cffi' 'python2-msgpack'
             'python2-passlib' 'python2-pynacl')
optdepends=(
  'python2-msgpack: MsgPack serializer support'
  'python2-argon2_cffi: WAMP-SCRAM authentication support'
  'python2-passlib: WAMP-SCRAM authentication support'
  'python2-pynacl: WAMP-cryptosign support'
)
source=("https://files.pythonhosted.org/packages/da/03/4ab1b68d12f8dba7f3e40cbbffc41f2e25e80677e189cdef9c929b83462d/autobahn-19.7.2.tar.gz"
        'skip-test-missing-serializers.patch')
sha512sums=('bccab541959283ec4a3400aa81311e0b0bc389bdb6394873dbee790f20986dae454899d5e626ae5accb7ad620b4330c72087b84f1353987915974ef371aa6e01'
            '53f1845d1504be3c3d3e2183647f5758b5041e50954c22f4c4e25f4e330a35fbc901814870d29f4df74c277343fc873c8f81ae46f2daa9173695fd94ccd11c09')

prepare() {
  cd "$_pkgname-$pkgver"

  patch -Np1 -i ../skip-test-missing-serializers.patch
}

build() {
  cd "$_pkgname-$pkgver"

  python2 setup.py build
}

package() {
  cd "$_pkgname-$pkgver"

  python2 setup.py install --root="$pkgdir" --optimize=1 --skip-build

  install -Dm 644 LICENSE "$pkgdir/usr/share/licenses/$pkgname/LICENSE"
}

