# This file is part of BlackArch Linux ( https://www.blackarch.org/ ).
# See COPYING for license details.

pkgname=python2-autobahn
_pkgname=autobahn
pkgver=19.9.3
pkgrel=1
pkgdesc='Real-time framework for Web, Mobile & Internet of Things.'
arch=('any')
url='https://crossbar.io/autobahn/'
license=('MIT')
depends=('python2-twisted' 'python2-six' 'python2-txaio' 'python2-wsaccel')
makedepends=('python2-setuptools' 'python2-argon2_cffi' 'python2-msgpack'
             'python2-passlib' 'python2-pynacl')
optdepends=(
  'python2-msgpack: MsgPack serializer support'
  'python2-argon2_cffi: WAMP-SCRAM authentication support'
  'python2-passlib: WAMP-SCRAM authentication support'
  'python2-pynacl: WAMP-cryptosign support'
)
source=("https://files.pythonhosted.org/packages/c5/7c/4942bb64748dd15af038d5cbb34b9ae85ffb05e2a46d99a9a79198dfdd58/$_pkgname-$pkgver.tar.gz"
        'skip-test-missing-serializers.patch')
sha512sums=('b246f10e18e054451a49fbb77a2b3fa7597b030a0cb6c1ce786c8cd5e4c1c930ae8b41a1898486322039ac51d7d7224b506d0c57f7654dd82c0e0f04b179131c'
            '53f1845d1504be3c3d3e2183647f5758b5041e50954c22f4c4e25f4e330a35fbc901814870d29f4df74c277343fc873c8f81ae46f2daa9173695fd94ccd11c09')

prepare() {
  cd "$_pkgname-$pkgver"

  patch -Np1 -i ../skip-test-missing-serializers.patch
}

build() {
  cd "$_pkgname-$pkgver"

  python2 setup.py build
}

package() {
  cd "$_pkgname-$pkgver"

  python2 setup.py install --root="$pkgdir" -O1 --skip-build

  install -Dm 644 LICENSE "$pkgdir/usr/share/licenses/$pkgname/LICENSE"
}

