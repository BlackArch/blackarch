# This file is part of BlackArch Linux ( http://blackarch.org ).
# See COPYING for license details.

pkgname='pythem'
pkgver=424.7337961
pkgrel=1
pkgdesc='python2 penetration testing framework.'
groups=('blackarch' 'blackarch-scanner' 'blackarch-sniffer' 'blackarch-recon'
        'blackarch-cracker' 'blackarch-webapp')
arch=('any')
url='https://github.com/m4n3dw0lf/PytheM'
license=('GPL3')
depends=('python2' 'python2-praw' 'python2-pysqlite' 'python2-mechanize'
         'python2-pyttsx' 'python2-netaddr' 'python2-pygeoip' 'python2-paramiko'
         'python2-netfilterqueue' 'python2-speechrecognition' 'scapy' 'espeak'
         'python2-termcolor' 'python2-ropper' 'python2-psutil')
makedepends=('git')
source=('git+https://github.com/m4n3dw0lf/PytheM.git')
sha1sums=('SKIP')

pkgver() {
  cd "$srcdir/PytheM"

  echo $(git rev-list --count HEAD).$(git rev-parse --short HEAD)
}

build() {
  cd "$srcdir/PytheM"

  python2 setup.py build
}

package() {
  cd "$srcdir/PytheM"

  python2 setup.py install --root="$pkgdir"

  install -Dm644 README.md "$pkgdir/usr/share/doc/pythem/README.md"
}
