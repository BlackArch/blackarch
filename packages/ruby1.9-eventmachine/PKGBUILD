# This file is part of BlackArch Linux ( http://blackarch.org ).
# See COPYING for license details.

_gemname='eventmachine'
pkgname="ruby1.9-$_gemname"
pkgver='1.0.3'
pkgrel=1
pkgdesc='Ruby/EventMachine library.'
arch=('i686' 'x86_64' 'armv6h' 'armv7h')
url='http://rubyeventmachine.com/'
depends=('ruby1.9')
options=(!emptydirs)
source=("https://rubygems.org/downloads/${_gemname}-${pkgver}.gem")
noextract=("${_gemname}-${pkgver}.gem")
sha1sums=('f1d3c0cfcdca855b9b7efcaf9a6c907043349ac9')

package() {
  cd "$srcdir"

  local _gemdir="$(ruby-1.9 -e'puts Gem.default_dir')"

  gem-1.9 install --ignore-dependencies --no-user-install \
    -i "$pkgdir/$_gemdir" -n "$pkgdir/usr/bin" "${_gemname}-${pkgver}.gem"

  rm "$pkgdir/$_gemdir/cache/${_gemname}-${pkgver}.gem"

  install -D -m644 "$pkgdir/$_gemdir/gems/$_gemname-$pkgver/LICENSE" \
    "$pkgdir/usr/share/licenses/ruby1.9-${_gemname}/LICENSE"
}
