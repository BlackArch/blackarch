# This file is part of BlackArch Linux ( https://www.blackarch.org/ ).
# See COPYING for license details.

pkgname=winregfs
pkgver=161.209a5d6
pkgrel=1
pkgdesc='Windows Registry FUSE filesystem.'
groups=('blackarch' 'blackarch-misc' 'blackarch-windows')
arch=('x86_64' 'aarch64')
url='https://github.com/jbruchon/winregfs'
license=('GPL2')
depends=('fuse' 'libjodycode')
makedepends=('git')
source=("git+https://github.com/jbruchon/$pkgname.git")
sha512sums=('SKIP')

pkgver() {
  cd $pkgname

  echo $(git rev-list --count HEAD).$(git rev-parse --short HEAD)
}

build() {
  cd $pkgname

  make
}

package() {
  cd $pkgname

  make DESTDIR="$pkgdir" install

  install -Dm 644 -t "$pkgdir/usr/share/doc/$pkgname/" README INSTALL TODO
  install -Dm 644 -t "$pkgdir/usr/share/licenses/$pkgname/" LICENSE*

  mv "$pkgdir/usr/local/"* "$pkgdir/usr/"
  rmdir "$pkgdir/usr/local"
  mv "$pkgdir/usr/man" "$pkgdir/usr/share/man"
}

