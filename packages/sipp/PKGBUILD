# This file is part of BlackArch Linux ( https://www.blackarch.org/ ).
# See COPYING for license details.

pkgname=sipp
pkgver=1133.ffdf9be
pkgrel=1
groups=('blackarch' 'blackarch-voip')
pkgdesc='A free Open Source test tool / traffic generator for the SIP protocol.'
arch=('x86_64' 'armv6h' 'armv7h' 'aarch64')
url='http://sipp.sourceforge.net/'
license=('GPL')
depends=('openssl' 'libpcap')
makedepends=('subversion')
source=('git+https://github.com/SIPp/sipp.git')
sha512sums=('SKIP')

pkgver() {
  cd $pkgname

  echo $(git rev-list --count HEAD).$(git rev-parse --short HEAD)
}

build() {
  cd "$pkgname"

  ./build.sh
}

package() {
  cd "$pkgname"

  install -Dm 755 sipp "$pkgdir/usr/bin/$pkgname"
  install -Dm 644 sipp.1 "$pkgdir/usr/share/man/man1/sipp.1"
  install -Dm 644 -t "$pkgdir/usr/share/doc/$pkgname/" CHANGES.md FAQ.md README.md
  install -Dm 644 LICENSE.txt "$pkgdir/usr/share/licenses/$pkgname/LICENSE"
}

