# This file is part of BlackArch Linux ( https://www.blackarch.org/ ).
# See COPYING for license details.

pkgbase=python-docx2txt
pkgname=('python2-docx2txt' 'python-docx2txt')
_pkgname=docx2txt
pkgver=0.7
pkgrel=3
pkgdesc='A pure python-based utility to extract text from docx files.'
arch=('any')
url='https://pypi.org/project/docx2txt/'
license=('custom:unknown')
makedepends=('python2-setuptools' 'python-setuptools')
source=("https://files.pythonhosted.org/packages/e4/02/74bdcdad3f8840cb0192010e02fd086bb37ca79bfacdda4112e3dcfb7af0/$_pkgname-$pkgver.tar.gz")
sha512sums=('dae6f23f83e9fe85d7b014096ef09d6ced480492f20357652b61b76e5f156e766b6f8f8938e48d0dc2c01f76320eafb98bba860af190e474902acc513d452e7a')

prepare() {
  cp -a "$_pkgname-$pkgver" "$_pkgname-$pkgver-2"
}

build() {
  cd "$_pkgname-$pkgver-2"

  python2 setup.py build

  cd "$srcdir/$_pkgname-$pkgver"

  python setup.py build
}

package_python2-docx2txt() {
  depends=('python2')

  cd "$_pkgname-$pkgver-2"

  python2 setup.py install --prefix=/usr --root="$pkgdir" --optimize=1 \
    --skip-build

  mv "$pkgdir/usr/bin/$_pkgname" "$pkgdir/usr/bin/${_pkgname}2"
}

package_python-docx2txt() {
  depends=('python')

  cd "$_pkgname-$pkgver"

  python setup.py install --prefix=/usr --root="$pkgdir" --optimize=1 \
    --skip-build
}

