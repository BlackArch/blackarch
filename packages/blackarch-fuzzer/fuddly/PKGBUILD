# This file is part of BlackArch Linux ( https://www.blackarch.org/ ).
# See COPYING for license details.

pkgname=fuddly
pkgver=569.fd2c4d0
pkgrel=3
groups=('blackarch' 'blackarch-fuzzer')
pkgdesc='Fuzzing and Data Manipulation Framework (for GNU/Linux).'
arch=('any')
url='https://github.com/k0retux/fuddly'
license=('GPL3')
depends=('python2' 'python2-pycups' 'python2-rpyc' 'python2-pysqlite'
         'python2-crcmod' 'python2-xtermcolor' 'sqlite' 'python2-enum34')
makedepends=('git' 'python2-sphinx')
source=("git+https://github.com/k0retux/$pkgname.git")
sha512sums=('SKIP')
install="$pkgname.install"

pkgver() {
  cd $pkgname

  echo $(git rev-list --count HEAD).$(git rev-parse --short HEAD)
}

prepare() {
  cd $pkgname

  cd docs

  sed -i 's|sphinx-build|sphinx-build2|' Makefile
}

build() {
  cd $pkgname

  cd docs

  make html
}

package() {
  cd $pkgname

  install -dm 755 "$pkgdir/usr/bin"
  install -dm 755 "$pkgdir/usr/share/$pkgname"

  install -Dm 644 -t "$pkgdir/usr/share/doc/$pkgname/" README.rst TODO
  cp -a docs/build/html/ "$pkgdir/usr/share/doc/$pkgname/"
  install -Dm 644 COPYING "$pkgdir/usr/share/licenses/$pkgname/COPYING"

  rm -rf README.rst TODO COPYING docs

  cp -a * "$pkgdir/usr/share/$pkgname/"

  cat > "$pkgdir/usr/bin/$pkgname" << EOF
#!/bin/sh
cd /usr/share/$pkgname
exec python2 fuddly_shell.py "\$@"
EOF

  chmod +x "$pkgdir/usr/bin/$pkgname"
}

