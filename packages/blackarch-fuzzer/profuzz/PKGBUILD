# This file is part of BlackArch Linux ( https://www.blackarch.org/ ).
# See COPYING for license details.

pkgname=profuzz
pkgver=9.aa6dded
pkgrel=4
pkgdesc='Simple PROFINET fuzzer based on Scapy.'
groups=('blackarch' 'blackarch-fuzzer')
arch=('any')
url='https://github.com/HSASec/ProFuzz'
license=('GPL3')
depends=('python2' 'python2-pyqt4' 'python2-gnuplot' 'python2-pyx' 'python2-scapy'
         'python2-pycryptodome' 'tcpdump' 'graphviz')
makedepends=('git')
source=("$pkgname::git+https://github.com/HSASec/ProFuzz.git")
sha512sums=('SKIP')

pkgver() {
  cd $pkgname

  echo $(git rev-list --count HEAD).$(git rev-parse --short HEAD)
}

package() {
  cd $pkgname

  install -dm 755 "$pkgdir/usr/bin"
  install -dm 755 "$pkgdir/usr/share/$pkgname"

  install -Dm 644 -t "$pkgdir/usr/share/doc/$pkgname/" README.md
  install -Dm 644 LICENSE "$pkgdir/usr/share/licenses/$pkgname/LICENSE"

  rm README.md LICENSE

  cp -a * "$pkgdir/usr/share/$pkgname/"

  cat > "$pkgdir/usr/bin/$pkgname" << EOF
#!/bin/sh
cd /usr/share/$pkgname
exec python2 Fuzzer.py "\$@"
EOF

  chmod +x "$pkgdir/usr/bin/$pkgname"
}

