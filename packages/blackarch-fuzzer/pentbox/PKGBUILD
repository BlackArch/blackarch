# This file is part of BlackArch Linux ( https://www.blackarch.org/ ).
# See COPYING for license details.

pkgname=pentbox
pkgver=1.8
pkgrel=4
groups=('blackarch' 'blackarch-fuzzer' 'blackarch-scanner')
pkgdesc='A security suite that packs security and stability testing oriented tools for networks and systems.'
arch=('any')
url='http://www.pentbox.net'
license=('GPL3')
source=("http://downloads.sourceforge.net/project/pentbox18realised/$pkgname-$pkgver.tar.gz")
depends=('ruby1.9')
sha512sums=('7de01d38dacb41f7f1e88d2726845deda9bdb73abe96a042a056db16fd6b59ae860c802a93240871778cb774dd86374ff029c0ff0834a946928439e29bcb3523')
options=('!strip')

package() {
  cd "$pkgname-$pkgver"

  install -dm 755 "$pkgdir/usr/bin"
  install -dm 755 "$pkgdir/usr/share/$pkgname"

  cp --no-preserve=ownership -a * "$pkgdir/usr/share/$pkgname/"

  cat > "$pkgdir/usr/bin/$pkgname" << EOF
#!/bin/sh
exec ruby-1.9 /usr/share/$pkgname/$pkgname.rb "\$@"
EOF

  chmod +x "$pkgdir/usr/bin/$pkgname"
}

