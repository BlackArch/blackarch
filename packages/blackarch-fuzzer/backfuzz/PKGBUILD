# This file is part of BlackArch Linux ( https://www.blackarch.org/ ).
# See COPYING for license details.

pkgname=backfuzz
pkgver=1.b0648de
pkgrel=1
epoch=1
groups=('blackarch' 'blackarch-fuzzer' 'blackarch-networking')
pkgdesc='A network protocol fuzzing toolkit.'
arch=('any')
url='https://github.com/localh0t/backfuzz'
license=('GPL3')
depends=('python2')
makedepends=('git')
source=("git+https://github.com/localh0t/$pkgname.git")
sha512sums=('SKIP')

pkgver() {
  cd $pkgname

  echo $(git rev-list --count HEAD).$(git rev-parse --short HEAD)
}

package() {
  cd $pkgname

  install -dm 755 "$pkgdir/usr/bin/"
  install -dm 755 "$pkgdir/usr/share/$pkgname"

  install -Dm 644 -t "$pkgdir/usr/share/doc/$pkgname" README VERSION
  install -Dm 644 LICENSE "$pkgdir/usr/share/licenses/$pkgname/LICENSE"
  install -m 644 "LICENSE - mona" \
    "$pkgdir/usr/share/licenses/$pkgname/LICENSE - mona"

  rm README VERSION LICENSE "LICENSE - mona"

  cp -a * "$pkgdir/usr/share/$pkgname/"

  cat > "$pkgdir/usr/bin/$pkgname" << EOF
#!/bin/sh
cd /usr/share/$pkgname
exec python2 $pkgname.py "\$@"
EOF

  chmod +x "$pkgdir/usr/bin/$pkgname"
}

