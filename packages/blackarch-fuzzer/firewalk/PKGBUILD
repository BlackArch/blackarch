# This file is part of BlackArch Linux ( https://www.blackarch.org/ ).
# See COPYING for license details.

pkgname=firewalk
pkgver=5.0
pkgrel=4
groups=('blackarch' 'blackarch-fuzzer' 'blackarch-scanner')
pkgdesc="An active reconnaissance network security tool"
arch=('x86_64' 'aarch64')
url='http://packetfactory.openwall.net/projects/firewalk/'
license=('BSD')
depends=('libdnet' 'libnet' 'libpcap')
source=("http://packetfactory.openwall.net/$pkgname/dist/$pkgname.tar.gz"
        'firewalk-gcc3.4.patch')
sha512sums=('919aa6e464dac72a02f8967ccf7109de7d0b3a574f0a396bcf69bacc988ab8e13e8e25932d749eadc898cae2546f8f81014d53ef51429e6c784a16d40da90ff2'
            'e9079d6d9a08af3bfad96cc93b1eda144c90ec26b4e8686bdf585c9eaab3b9c34dbc94a073bb3ca851211b5d3f758281539cfd1240e7877a72166239f059f338')

prepare() {
  cd Firewalk

  patch -Np1 < "$srcdir/firewalk-gcc3.4.patch"
}

build() {
  cd Firewalk

  if [[ $CARCH == arm* ]]; then
    ./configure --prefix=/usr --build=arm
  else
    ./configure --prefix=/usr
  fi
  make
}

package() {
  cd Firewalk

  install -Dm 755 $pkgname "$pkgdir/usr/bin/$pkgname"
}

