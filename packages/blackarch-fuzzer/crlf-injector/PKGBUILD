# This file is part of BlackArch Linux ( https://www.blackarch.org/ ).
# See COPYING for license details.

pkgname=crlf-injector
pkgver=8.abaf494
pkgrel=2
pkgdesc='A python script for testing CRLF injecting issues.'
groups=('blackarch' 'blackarch-fuzzer' 'blackarch-webapp')
arch=('any')
url='https://github.com/rudSarkar/crlf-injector'
license=('custom:unknown')
depends=('python2' 'python2-netaddr' 'python2-argparse' 'python2-requests')
makedepends=('git')
source=("git+https://github.com/rudSarkar/$pkgname.git")
sha512sums=('SKIP')

pkgver() {
  cd $pkgname

  echo $(git rev-list --count HEAD).$(git rev-parse --short HEAD)
}

package() {
  cd $pkgname

  install -dm 755 "$pkgdir/usr/bin"
  install -dm 755 "$pkgdir/usr/share/$pkgname"

  install -Dm 644 -t "$pkgdir/usr/share/doc/$pkgname/" README.md
  install -Dm 644 LICENSE.md/LICENSE.md \
    "$pkgdir/usr/share/licenses/$pkgname/LICENSE"

  rm -rf README.md LICENSE.md

  cp -a * "$pkgdir/usr/share/$pkgname/"

  cat > "$pkgdir/usr/bin/$pkgname" << EOF
#!/bin/sh
cd /usr/share/$pkgname
exec python2 crlf.py "\$@"
EOF

  chmod +x "$pkgdir/usr/bin/$pkgname"
}

