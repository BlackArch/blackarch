# This file is part of BlackArch Linux ( https://www.blackarch.org/ ).
# See COPYING for license details.

pkgbase=python-pyshark
pkgname=('python2-pyshark' 'python-pyshark')
_pkgname=pyshark
pkgver=0.4.2.3
pkgrel=2
pkgdesc='Python wrapper for tshark, allowing python packet parsing using wireshark dissectors.'
arch=('any')
url='https://pypi.org/project/pyshark/#files'
license=('MIT')
makedepends=('python2-setuptools' 'python-setuptools')
source=("https://files.pythonhosted.org/packages/bf/93/56ee4d6859527b4bc2fb4f3966e8572944c680eb3dda2dfdc3975d542459/$_pkgname-$pkgver.tar.gz")
sha512sums=('45a275149b363109ba12e083205f214e687cb4725a9cdaa62582704618c7f5f4a44e50d34a989663d4af83b54c5112550419982fb1cbecc881d49bb8f4d4bfca')

prepare() {
  cp -a "$_pkgname-$pkgver"{,-2}
}

build() {
  cd "$_pkgname-$pkgver-2"

  python2 setup.py build

  cd "$srcdir/$_pkgname-$pkgver"

  python setup.py build
}

package_python2-pyshark() {
  depends=('python2' 'python2-lxml' 'python2-logbook')

  cd "$_pkgname-$pkgver-2"

  python2 setup.py install --prefix=/usr --root="$pkgdir" --optimize=1 \
    --skip-build
}

package_python-pyshark() {
  depends=('python' 'python-lxml' 'python-logbook')

  cd "$_pkgname-$pkgver"

  python setup.py install --prefix=/usr --root="$pkgdir" --optimize=1 \
    --skip-build
}

