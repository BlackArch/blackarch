# This file is part of BlackArch Linux ( https://www.blackarch.org/ ).
# See COPYING for license details.

pkgbase=python-pyshark
pkgname=('python2-pyshark' 'python-pyshark')
_pkgname=pyshark
pkgver=0.4.2.9
pkgrel=2
pkgdesc='Python wrapper for tshark, allowing python packet parsing using wireshark dissectors.'
arch=('any')
url='https://pypi.org/project/pyshark/#files'
license=('MIT')
makedepends=('python2-setuptools' 'python-setuptools')
source=("https://files.pythonhosted.org/packages/a1/45/b74055eca355f9fc83a1493b81a9aadb5602adcfdfde0f40f76aa4d970d8/$_pkgname-$pkgver.tar.gz")
sha512sums=('6c9baff71a628838f778c04c1c5da2ad5b275341f1cb1a0459c5544b772f9d7b66f152daaf68cd9698082814f1fc1530a2b727adfac6919f1ffb857b8188f9e4')

prepare() {
  cp -a "$_pkgname-$pkgver"{,-2}
}

build() {
  cd "$_pkgname-$pkgver-2"

  python2 setup.py build

  cd "$srcdir/$_pkgname-$pkgver"

  python setup.py build
}

package_python2-pyshark() {
  depends=('python2' 'python2-lxml' 'python2-logbook')

  cd "$_pkgname-$pkgver-2"

  python2 setup.py install --prefix=/usr --root="$pkgdir" --optimize=1 \
    --skip-build
}

package_python-pyshark() {
  depends=('python' 'python-lxml' 'python-logbook')

  cd "$_pkgname-$pkgver"

  python setup.py install --prefix=/usr --root="$pkgdir" --optimize=1 \
    --skip-build
}

