# This file is part of BlackArch Linux ( https://www.blackarch.org/ ).
# See COPYING for license details.

pkgbase=python-pyshark
pkgname=('python2-pyshark' 'python-pyshark')
_pkgname=pyshark
pkgver=0.4.2.8
pkgrel=1
pkgdesc='Python wrapper for tshark, allowing python packet parsing using wireshark dissectors.'
arch=('any')
url='https://pypi.org/project/pyshark/#files'
license=('MIT')
makedepends=('python2-setuptools' 'python-setuptools')
source=("https://files.pythonhosted.org/packages/7a/bf/645e134d8b767d2d52c1923bbc4940e24dcaf15037d972868fe20f739565/$_pkgname-$pkgver.tar.gz")
sha512sums=('58c34422a1322cc61f9ea8d7a824049693c971df83ab8ced1a0f441b822d225492cc81243f4dbdad44c6847b4fe88d46a4ece038974b1ac68873be4ab53f5d0c')

prepare() {
  cp -a "$_pkgname-$pkgver"{,-2}
}

build() {
  cd "$_pkgname-$pkgver-2"

  python2 setup.py build

  cd "$srcdir/$_pkgname-$pkgver"

  python setup.py build
}

package_python2-pyshark() {
  depends=('python2' 'python2-lxml' 'python2-logbook')

  cd "$_pkgname-$pkgver-2"

  python2 setup.py install --prefix=/usr --root="$pkgdir" --optimize=1 \
    --skip-build
}

package_python-pyshark() {
  depends=('python' 'python-lxml' 'python-logbook')

  cd "$_pkgname-$pkgver"

  python setup.py install --prefix=/usr --root="$pkgdir" --optimize=1 \
    --skip-build
}

