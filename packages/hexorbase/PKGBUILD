# This file is part of BlackArch Linux ( http://blackarch.org ).
# See COPYING for license details.

pkgname='hexorbase'
pkgver='6'
pkgrel=2
epoch=2
groups=('blackarch' 'blackarch-fuzzer' 'blackarch-scanner')
pkgdesc='A database application designed for administering and auditing multiple database servers simultaneously from a centralized location. It is capable of performing SQL queries and bruteforce attacks against common database servers (MySQL, SQLite, Microsoft SQL Server, Oracle, PostgreSQL).'
url='https://code.google.com/p/hexorbase/'
arch=('any')
license=('GPL3')
depends=('python2' 'python2-pyqt4' 'mysql-python' 'python2-psycopg2' 'pymssql'
         'python2-qscintilla')
optdepends=('python2-cx_oracle: Oracle database')
source=('svn+http://hexorbase.googlecode.com/svn/HexorBase/')
sha1sums=('SKIP')

pkgver() {
  cd "$srcdir/HexorBase"

  local _ver=$(svnversion)

  printf "%s" "${_ver//[[:alpha:]]}"
}

package() {
  cd "$srcdir/HexorBase"

  mkdir -p "$pkgdir/usr/share/hexorbase"
  mkdir -p "$pkgdir/usr/bin/"

  cp -a --no-preserve=ownership * "$pkgdir/usr/share/hexorbase"

  cat > "$pkgdir/usr/bin/hexorbase" << EOF
#!/bin/sh
cd /usr/share/hexorbase
exec python2 /usr/share/hexorbase/execute.py "\$@"
EOF

  chmod +x "$pkgdir/usr/bin/hexorbase"
}
