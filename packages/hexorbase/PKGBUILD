# This file is part of BlackArch Linux ( https://www.blackarch.org/ ).
# See COPYING for license details.

pkgname=hexorbase
pkgver=7
pkgrel=1
epoch=2
groups=('blackarch' 'blackarch-fuzzer' 'blackarch-scanner')
pkgdesc='A database application designed for administering and auditing multiple database servers simultaneously from a centralized location. It is capable of performing SQL queries and bruteforce attacks against common database servers (MySQL, SQLite, Microsoft SQL Server, Oracle, PostgreSQL).'
url='https://code.google.com/p/hexorbase/'
arch=('any')
license=('GPL-3.0-or-later')
depends=('python2' 'python2-pyqt4' 'mysql-python' 'python2-psycopg2' 'pymssql'
         'python2-qscintilla')
optdepends=('python2-cx_oracle: Oracle database')
source=("https://web.archive.org/web/20220425002001mp_/https://github.com/savio-code/hexorbase/archive/refs/heads/master.zip")
sha512sums=('ffee2d8635433d201046aa7d78fa3dd34e0ffc898985d63fa1fb0f36fd4496e82c717c597a8b33709423b46420ce4cfab2f12cdcd541d1fffc7487f4a991184a')

package() {
  cd "$pkgname-master"

  install -dm 755 "$pkgdir/usr/bin/"
  install -dm 755 "$pkgdir/usr/share/$pkgname"

  cp -a --no-preserve=ownership HexorBase/* "$pkgdir/usr/share/$pkgname/"

  install -Dm 644 -t "$pkgdir/usr/share/doc/$pkgname/" README.md

  cat > "$pkgdir/usr/bin/$pkgname" << EOF
#!/bin/sh
cd /usr/share/$pkgname
exec python2 /usr/share/$pkgname/execute.py "\$@"
EOF

  chmod +x "$pkgdir/usr/bin/$pkgname"
}

