# This file is part of BlackArch Linux ( http://blackarch.org ).
# See COPYING for license details.

pkgname='arpon'
pkgver='3.0'
pkgrel=1
pkgdesc='A portable handler daemon that make ARP protocol secure in order to avoid the Man In The Middle (MITM) attack through ARP Spoofing, ARP Cache Poisoning or ARP Poison Routing (APR) attacks.'
arch=('x86_64' 'armv6h' 'armv7h' 'aarch64')
url='http://arpon.sourceforge.net/'
license=('custom')
depends=('libdnet' 'libnet' 'libpcap')
makedepends=('cmake' 'libdnet')
backup=('etc/conf.d/arpon')
groups=('blackarch' 'blackarch-defensive')
source=("http://downloads.sourceforge.net/sourceforge/arpon/ArpON-${pkgver}-ng.tar.gz"
        "arpon"
        "arpon.conf.d")
sha1sums=('4df334e1d8286c9ab790fbca133d6590104ff284'
          'f414f02598cc333b3902babda80f7272934afa52'
          '6acc944253bc0a50e8977a5358545aaab782d69f')

build() {
  cd "$srcdir/ArpON-$pkgver-ng"

  [ -d build ] || mkdir build

  cd build

  cmake -DPCAP_LIB_DIR="/usr/lib" -DPCAP_LIBRARY="/usr/lib/libpcap.so" ..

  make
}

package() {
  cd "$srcdir/ArpON-$pkgver-ng/build"

  make DESTDIR="$pkgdir" install

  install -Dm644 "../LICENSE" "$pkgdir/usr/share/licenses/arpon/LICENSE"
  install -Dm755 "$srcdir/arpon" "$pkgdir/etc/rc.d/arpon"
  install -Dm644 "$srcdir/arpon.conf.d" "$pkgdir/etc/conf.d/arpon"

  mv "$pkgdir/sbin" "$pkgdir/usr/bin"
}
