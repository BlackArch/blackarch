# This file is part of BlackArch Linux ( http://blackarch.org ).
# See COPYING for license details.

pkgname='arpon'
pkgver='2.7.2'
pkgrel=1
pkgdesc='A portable handler daemon that make ARP protocol secure in order to avoid the Man In The Middle (MITM) attack through ARP Spoofing, ARP Cache Poisoning or ARP Poison Routing (APR) attacks.'
arch=('i686' 'x86_64' 'armv6h' 'armv7h')
url='http://arpon.sourceforge.net/'
license=('custom')
depends=('libdnet' 'libnet' 'libpcap')
makedepends=('cmake' 'libdnet')
backup=('etc/conf.d/arpon')
groups=('blackarch' 'blackarch-defensive')
source=("http://downloads.sourceforge.net/sourceforge/arpon/ArpON-${pkgver}.tar.gz"
        "arpon"
        "arpon.conf.d")
sha1sums=('75e4b1f2a2c18e4982fc5797547d52a13194f81d'
          'f414f02598cc333b3902babda80f7272934afa52'
          '6acc944253bc0a50e8977a5358545aaab782d69f')

build() {
  cd "$srcdir/ArpON-$pkgver"

  [ -d build ] || mkdir build

  cd build

  cmake -DPCAP_LIB_DIR="/usr/lib" -DPCAP_LIBRARY="/usr/lib/libpcap.so" ..

  make
}

package() {
  cd "$srcdir/ArpON-$pkgver/build"

  make DESTDIR="$pkgdir" install

  install -D -m644 "../LICENSE" "$pkgdir/usr/share/licenses/arpon/LICENSE"
  install -D -m755 "$srcdir/arpon" "$pkgdir/etc/rc.d/arpon"
  install -D -m644 "$srcdir/arpon.conf.d" "$pkgdir/etc/conf.d/arpon"

  mv "$pkgdir/sbin" "$pkgdir/usr/bin"
}
