# This file is part of BlackArch Linux ( http://blackarch.org ).
# See COPYING for license details.

pkgname='pmacct'
pkgver=2135.108765c4
pkgrel=1
pkgdesc='Small set of multi-purpose passive network monitoring tools [NetFlow IPFIX sFlow libpcap BGP BMP IGP Streaming Telemetry].'
groups=('blackarch' 'blackarch-networking' 'blackarch-sniffer'
        'blackarch-defensive')
arch=('x86_64' 'armv6h' 'armv7h' 'aarch64')
url='https://github.com/pmacct/pmacct'
license=('GPL2')
depends=('libpcap' 'zeromq' 'rabbitmq' 'mariadb' 'geoip')
makedepends=('git')
source=('git+https://github.com/pmacct/pmacct.git')
sha1sums=('SKIP')

pkgver() {
  cd "$srcdir/pmacct"

  echo $(git rev-list --count HEAD).$(git rev-parse --short HEAD)
}

build() {
  cd "$srcdir/pmacct"

  ./autogen.sh

  ./configure --prefix=/usr --sbindir=/usr/bin

  make
}

package() {
  cd "$srcdir/pmacct"

  make DESTDIR="$pkgdir" install

  install -Dm644 -t "$pkgdir/usr/share/doc/pmacct/" *.md AUTHORS ChangeLog \
    FAQS INSTALL TOOLS QUICKSTART UPGRADE docs/*
  install -Dm644 COPYING "$pkgdir/usr/share/licenses/pmacct/COPYING"
}
