# This file is part of BlackArch Linux ( http://blackarch.org ).
# See COPYING for license details.

pkgname=crackle
pkgver=104.0fc1938
pkgrel=1
groups=('blackarch' 'blackarch-cracker' 'blackarch-bluetooth')
pkgdesc="Crack and decrypt BLE encryption"
arch=('x86_64' 'armv6h' 'armv7h' 'aarch64')
url='https://github.com/mikeryan/crackle/'
license=('GPL')
depends=('libpcap')
makedepends=('git')
source=('git+https://github.com/mikeryan/crackle/')
sha512sums=('SKIP')

pkgver() {
  cd crackle

  echo $(git rev-list --count HEAD).$(git rev-parse --short HEAD)
}

build() {
  cd "$srcdir/crackle"

  CPPFLAGS=${CPPFLAGS/-D_FORTIFY_SOURCE=2/}

  make
}

package() {
  cd "$srcdir/crackle"

  install -dm 755 "$pkgdir/usr/bin"

  make "INSTALL_DIR=$pkgdir/usr/bin" install
}
