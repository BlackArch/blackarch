# This file is part of BlackArch Linux ( https://www.blackarch.org/ ).
# See COPYING for license details.

pkgname=grokevt
pkgver=0.5.0.6.1.r0.gfb16294
pkgrel=1
epoch=1
pkgdesc='A collection of scripts built for reading WindowsÂ® NT/2K/XP/2K eventlog files.'
arch=('any')
groups=('blackarch' 'blackarch-forensic')
url='https://projects.sentinelchicken.org/grokevt/'
license=('GPL-3.0-or-later')
depends=('python' 'reglookup' 'cpio')
makepdepends=('git')
source=("git+https://salsa.debian.org/pkg-security-team/$pkgname.git")
sha512sums=('SKIP')

pkgver() {
  cd $pkgname

  git describe --long --tags --abbrev=7 2>/dev/null \
  | sed 's|^[^/]*/||; s|/|.|g; s|:||g; s|\(.*\)-\([0-9][0-9]*\)-g\([0-9a-f]\+\)$|\1.r\2.g\3|; s|-|.|g' \
  || printf '%s.%s\n' "$(git rev-list --count HEAD)" "$(git rev-parse --short=7 HEAD)"
}

build() {
  cd $pkgname

  make
}

package() {
  cd $pkgname

  install -dm 755 "$pkgdir/etc/$pkgname/"
  install -dm 755 "$pkgdir/usr/share/doc/$pkgname/"
  install -Dm 755 build/bin/* -t "$pkgdir/usr/bin/"

  cp -a build/etc/. "$pkgdir/etc/$pkgname/"
  cp -a build/doc/. "$pkgdir/usr/share/doc/$pkgname/"

  python grokevt-distutils.py install --root="$pkgdir" --optimize=1
}

