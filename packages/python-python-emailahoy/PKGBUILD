# This file is part of BlackArch Linux ( https://www.blackarch.org/ ).
# See COPYING for license details.

pkgbase=python-python-emailahoy
pkgname=('python2-python-emailahoy' 'python-python-emailahoy')
_pkgname=python-emailahoy
pkgver=0.1
pkgrel=4
pkgdesc='Python email utility that verifies existence of an email address.'
arch=('any')
url='https://pypi.org/project/python-emailahoy/#files'
license=('BSD')
makedepends=('python2-setuptools' 'python-setuptools')
source=("https://files.pythonhosted.org/packages/6c/0b/d58aee3c2f64ee4cb33758e7333a6814fd074836a86d1350135a85db172c/$_pkgname-$pkgver.tar.gz")
sha512sums=('04cf33226c89e566e41d7b13d1b99bb710b93201cbe1b8d54017062ba528d8b02231d0d9726f16c2920155c4004d0ad0d6818d66062d741ea186cfa5d6c7aed4')

prepare() {
  cp -a "$_pkgname-$pkgver"{,-2}
}

build() {
  cd "$_pkgname-$pkgver-2"

  python2 setup.py build

  cd "$srcdir/$_pkgname-$pkgver"

  python setup.py build
}

package_python2-python-emailahoy() {
  depends=('python2')

  cd "$_pkgname-$pkgver-2"

  python2 setup.py install --prefix=/usr --root="$pkgdir" --optimize=1 \
    --skip-build
}

package_python-python-emailahoy() {
  depends=('python')

  cd "$_pkgname-$pkgver"

  python setup.py install --prefix=/usr --root="$pkgdir" --optimize=1 \
    --skip-build
}

