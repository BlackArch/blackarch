# This file is part of BlackArch Linux ( https://www.blackarch.org/ ).
# See COPYING for license details.

pkgname=slurp-scanner
pkgver=90.6a4eaaf
pkgrel=2
groups=('blackarch' 'blackarch-scanner')
pkgdesc='Evaluate the security of S3 buckets.'
arch=('x86_64' 'aarch64')
# Move to an existing fork
url='https://github.com/0xbharath/slurp'
license=('GPL3')
makedepends=('git' 'go')
source=("$pkgname::git+https://github.com/0xbharath/slurp.git")
sha512sums=('SKIP')

pkgver() {
  cd $pkgname

  echo $(git rev-list --count HEAD).$(git rev-parse --short HEAD)
}

build() {
  cd $pkgname

  GOPATH="$srcdir" go mod download
  GOPATH="$srcdir" go build \
    -trimpath \
    -buildmode=pie \
    -mod=readonly \
    -modcacherw \
    -ldflags "-s -w" \
    -o $pkgname .
}

package() {
  cd $pkgname

  install -Dm 755 $pkgname "$pkgdir/usr/bin/$pkgname"
  install -Dm 644 -t "$pkgdir/usr/share/doc/$pkgname" README.md
}

