# This file is part of BlackArch Linux ( https://www.blackarch.org/ ).
# See COPYING for license details.

pkgname=python-moviepy
_pkgname=${pkgname#python-}
pkgver=2.2.1
_pyver=3.14
pkgrel=1
pkgdesc='Video editing with Python'
arch=('any')
url='https://pypi.org/project/moviepy/#files'
license=('MIT')
depends=('python' 'python-decorator' 'python-imageio' 'python-imageio-ffmpeg'
         'python-tqdm' 'python-numpy' 'python-requests' 'python-proglog'
         'python-pillow' 'python-dotenv')
makedepends=('python-build' 'python-installer' 'python-setuptools'
             'python-wheel')
options=(!emptydirs)
source=("https://files.pythonhosted.org/packages/source/${_pkgname::1}/$_pkgname/$_pkgname-$pkgver.tar.gz")
sha512sums=('493e81a5a2117cc0123828eed072f92af5ee57b8a293578e8c25236eedc4aea8739435cb342e4fe6e619f3917459d83858e92b343cdfd8729d2e3611d3731649')

build() {
  cd "$_pkgname-$pkgver"

  python -m build --wheel --no-isolation
}

package() {
  cd "$_pkgname-$pkgver"

  python -m installer --destdir="$pkgdir" dist/*.whl

  rm -rf "$pkgdir/usr/lib/python$_pyver/site-packages/tests"
}

