# This file is part of BlackArch Linux ( https://www.blackarch.org/ ).
# See COPYING for license details.

pkgbase=python-moviepy
pkgname=('python2-moviepy' 'python-moviepy')
_pkgname=moviepy
pkgver=1.0.2
pkgrel=1
pkgdesc='Video editing with Python'
arch=('any')
url='https://pypi.org/project/moviepy/#files'
license=('MIT')
makedepends=('python2-setuptools' 'python-setuptools')
source=("https://files.pythonhosted.org/packages/71/64/e7f0876716d5c35612bf8e9e49299d0f491285f10f36cbb481556ab0062b/$_pkgname-$pkgver.tar.gz")
sha512sums=('7d646808d9e9473521ce2cc6a7e24b5b4f582754dc42975f78306efdff1e45742a649e52a59c67bb93aeeb1d3278c6f46c27fec178f5abcb0a2af96469609b08')

prepare() {
  cp -a "$_pkgname-$pkgver"{,-2}
}

build() {
  cd "$_pkgname-$pkgver-2"

  python2 setup.py build

  cd "$srcdir/$_pkgname-$pkgver"

  python setup.py build
}

package_python2-moviepy() {
  depends=('python2' 'python2-decorator' 'python2-imageio'
           'python2-imageio-ffmpeg' 'python2-tqdm' 'python2-numpy'
           'python2-requests' 'python2-proglog')

  cd "$_pkgname-$pkgver-2"

  python2 setup.py install --prefix=/usr --root="$pkgdir" --optimize=1 \
    --skip-build

  rm -rf "$pkgdir/usr/lib/python2.7/site-packages/tests"
}

package_python-moviepy() {
  depends=('python2' 'python2-decorator' 'python2-imageio'
           'python2-imageio-ffmpeg' 'python2-tqdm' 'python2-numpy'
           'python2-requests' 'python2-proglog')

  cd "$_pkgname-$pkgver"

  python setup.py install --prefix=/usr --root="$pkgdir" --optimize=1 \
    --skip-build

  rm -rf "$pkgdir/usr/lib/python3.8/site-packages/tests"
}

