pkgname=w3af
pkgver=1.5
pkgrel=3
groups=('blackarch' 'blackarch-webapp' 'blackarch-fuzzer' 'blackarch-scanner')
pkgdesc="Web Application Attack and Audit Framework"
arch=('i686' 'x86_64' 'armv6h' 'armv7h')
url='http://w3af.sourceforge.net/'
license=('GPL')
depends=('python2' 'python2-clamd' 'python2-pybloomfiltermmap' 'python2-esmre'
         'python2-phply' 'python2-pysqlite' 'python2-nltk' 'python2-chardet'
         'python2-pdfminer' 'python2-futures' 'python2-pyopenssl' 'python2-lxml'
         'scapy' 'python2-guess-language' 'python2-cluster' 'python2-msgpack'
         'sqlite' 'python2-ntlm' 'python2-pygithub' 'python2-gitpython'
         'graphviz' 'pygtksourceview2' 'pygtk' 'gtk2' 'python2-beautifulsoup3')
options=('!strip')
source=('git+https://github.com/andresriancho/w3af'
        'w3af.desktop')
md5sums=('SKIP'
         '68fde1a829b8392fa4db4fa606c1cbe7')

_checkout() {
  cd "$srcdir/w3af"
  if [[ $(git describe --tags) != $pkgver ]] ; then
    git checkout $pkgver
  fi
}

prepare() {
  cd "$srcdir/w3af"

  _checkout

  # Delete some of the stuff from w3af-extlib (stuff that needs to be installed) -> dependencies
  rm -rf w3af/extlib/fpconst-0.7.2
  rm -rf w3af/extlib/SOAPpy

  # Enforce python2
  sed -i -e 's|#!\s*/usr/bin/python[^\d.]*$|#!/usr/bin/python2|' \
         -e 's|#!\s*/usr/bin/env python[^\d.]*$|#!/usr/bin/env python2|' \
         $(find . -name '*.py') w3af_{console,gui}

  # Remove dependency checks.
  sed -i 's/dependency_check()/#&/' w3af_{console,gui}

  # Change the default metasploit location.
  #sed -i 's|/opt/metasploit3|/usr/share/metasploit|' core/controllers/miscSettings.py
}

package() {
  cd "$srcdir/w3af"

  _checkout

  # Base directories.
  install -dm755 "$pkgdir/usr/share/w3af"
  install -dm755 "$pkgdir/usr/bin"
  install -dm755 "$pkgdir/usr/share/applications/"

  cp --no-preserve=ownership -r "$srcdir/w3af"/* "$pkgdir/usr/share/w3af"

  install -Dm644 "$srcdir/w3af.desktop" "$pkgdir/usr/share/applications"

  # Joint script for w3af_console.
  cat > $pkgdir/usr/bin/w3af <<EOF
#!/bin/sh
exec python2 /usr/share/w3af/w3af_console "\$@"
EOF

  # Joint script for w3af_gui.
  cat > $pkgdir/usr/bin/w3af-gui <<EOF
#!/bin/sh
exec python2 /usr/share/w3af/w3af_gui "\$@"
EOF

  chmod +x "$pkgdir/usr/share/w3af"/w3af_{console,gui} "$pkgdir/usr/bin"/{w3af,w3af-gui}
}
