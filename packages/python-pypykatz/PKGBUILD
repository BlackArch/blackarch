# This file is part of BlackArch Linux ( https://www.blackarch.org/ ).
# See COPYING for license details.

pkgbase=python-pypykatz
_pkgname=pypykatz
pkgname=('python2-pypykatz' 'python-pypykatz')
pkgver=0.3.8
pkgrel=1
pkgdesc='Python implementation of Mimikatz.'
url='https://github.com/skelsec/pypykatz'
arch=('any')
license=('MIT')
makedepends=('python2-setuptools' 'python-setuptools')
source=("https://files.pythonhosted.org/packages/13/f2/ac72f06d13734e568b4e555f2d17037dc07810001ea4bac66d6dac5c5172/$_pkgname-$pkgver.tar.gz")
sha512sums=('dbb1a8abdc0d6ddc69124f577a378321a24d0dc3075f5d5f7f50ee07b96ee6be380a0888fb027bb53c40f68a836322d358cbef001c1690f774f0fe793084c6c3')

prepare() {
  cp -a "pypykatz-$pkgver" "pypykatz-$pkgver-py2"
}

build() {
  cd "pypykatz-$pkgver-py2"

  python2 setup.py build

  cd "$srcdir/pypykatz-$pkgver"

  python setup.py build
}

package_python2-pypykatz() {
  depends=('python2' 'python2-minidump' 'python2-minikerberos')

  cd "pypykatz-$pkgver-py2"

  python2 setup.py install --prefix=/usr --root="$pkgdir" --optimize=1 \
    --skip-build

  mv "$pkgdir/usr/bin/pypykatz" "$pkgdir/usr/bin/pypykatz2"
}

package_python-pypykatz() {
  depends=('python' 'python-minidump' 'python-minikerberos')

  cd "pypykatz-$pkgver"

  python setup.py install --prefix=/usr --root="$pkgdir" --optimize=1 \
    --skip-build
}

