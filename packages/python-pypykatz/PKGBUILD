# This file is part of BlackArch Linux ( https://www.blackarch.org/ ).
# See COPYING for license details.

pkgbase=python-pypykatz
pkgname=('python2-pypykatz' 'python-pypykatz')
pkgver=0.0.8
pkgrel=1
pkgdesc='Python implementation of Mimikatz.'
url='https://github.com/skelsec/pypykatz'
arch=('any')
license=('MIT')
makedepends=('python2-setuptools' 'python-setuptools')
source=("https://files.pythonhosted.org/packages/32/fb/d43ca75588ab8c78163fd4f204be0effa3b3c9fc17be519636861c4e253f/pypykatz-0.0.8.tar.gz")
sha512sums=('9e0ade0fd98115a37ab53e70cec6f175129519487198d02afda9d5b1183ac117c3a718b0e1052bc374d2cecf24b30ecbab131395b2af1acc23af6bcc285b1da8')

prepare() {
  cp -a "pypykatz-$pkgver" "pypykatz-$pkgver-py2"
}

build() {
  cd "pypykatz-$pkgver-py2"

  python2 setup.py build

  cd "$srcdir/pypykatz-$pkgver"

  python setup.py build
}

package_python2-pypykatz() {
  depends=('python2' 'python2-minidump' 'python2-minikerberos')

  cd "pypykatz-$pkgver-py2"

  python2 setup.py install --prefix=/usr --root="$pkgdir" --optimize=1 \
    --skip-build

  mv "$pkgdir/usr/bin/pypykatz" "$pkgdir/usr/bin/pypykatz2"
}

package_python-pypykatz() {
  depends=('python' 'python-minidump' 'python-minikerberos')

  cd "pypykatz-$pkgver"

  python setup.py install --prefix=/usr --root="$pkgdir" --optimize=1 \
    --skip-build
}

