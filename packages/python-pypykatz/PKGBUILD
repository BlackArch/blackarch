# This file is part of BlackArch Linux ( https://www.blackarch.org/ ).
# See COPYING for license details.

pkgbase=python-pypykatz
_pkgname=pypykatz
pkgname=('python2-pypykatz' 'python-pypykatz')
pkgver=0.1.1
pkgrel=1
pkgdesc='Python implementation of Mimikatz.'
url='https://github.com/skelsec/pypykatz'
arch=('any')
license=('MIT')
makedepends=('python2-setuptools' 'python-setuptools')
source=("https://files.pythonhosted.org/packages/2f/0d/f897a0851b657afc8c369b01e15202eeea45a787938a456d6520196c723d/$_pkgname-$pkgver.tar.gz")
sha512sums=('bed58642e700f2f518d8fe14fba863f3f9f0e09df65137e9427b6dae0cd22a21d382a83f44e31a95d5b321e953b9937d1e1273b0d2c3c7ba66573b758e37a503')

prepare() {
  cp -a "pypykatz-$pkgver" "pypykatz-$pkgver-py2"
}

build() {
  cd "pypykatz-$pkgver-py2"

  python2 setup.py build

  cd "$srcdir/pypykatz-$pkgver"

  python setup.py build
}

package_python2-pypykatz() {
  depends=('python2' 'python2-minidump' 'python2-minikerberos')

  cd "pypykatz-$pkgver-py2"

  python2 setup.py install --prefix=/usr --root="$pkgdir" --optimize=1 \
    --skip-build

  mv "$pkgdir/usr/bin/pypykatz" "$pkgdir/usr/bin/pypykatz2"
}

package_python-pypykatz() {
  depends=('python' 'python-minidump' 'python-minikerberos')

  cd "pypykatz-$pkgver"

  python setup.py install --prefix=/usr --root="$pkgdir" --optimize=1 \
    --skip-build
}

