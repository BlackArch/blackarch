# This file is part of BlackArch Linux ( https://www.blackarch.org/ ).
# See COPYING for license details.

pkgbase=python-pypykatz
_pkgname=pypykatz
pkgname=('python2-pypykatz' 'python-pypykatz')
pkgver=0.3.4
pkgrel=1
pkgdesc='Python implementation of Mimikatz.'
url='https://github.com/skelsec/pypykatz'
arch=('any')
license=('MIT')
makedepends=('python2-setuptools' 'python-setuptools')
source=("https://files.pythonhosted.org/packages/f9/f9/635867b36e85dc06c6ea2f8fabdc973ec9edb33e13b92fe22d7de2c8ac60/$_pkgname-$pkgver.tar.gz")
sha512sums=('a03634f3d9061fec8a485c8582d2b90d84890339073bfddf9ff5cd155b88e15e3165db7b3eb21c2d3d66b87a0686772a64cb6048aa50485aa9532c9de4104251')

prepare() {
  cp -a "pypykatz-$pkgver" "pypykatz-$pkgver-py2"
}

build() {
  cd "pypykatz-$pkgver-py2"

  python2 setup.py build

  cd "$srcdir/pypykatz-$pkgver"

  python setup.py build
}

package_python2-pypykatz() {
  depends=('python2' 'python2-minidump' 'python2-minikerberos')

  cd "pypykatz-$pkgver-py2"

  python2 setup.py install --prefix=/usr --root="$pkgdir" --optimize=1 \
    --skip-build

  mv "$pkgdir/usr/bin/pypykatz" "$pkgdir/usr/bin/pypykatz2"
}

package_python-pypykatz() {
  depends=('python' 'python-minidump' 'python-minikerberos')

  cd "pypykatz-$pkgver"

  python setup.py install --prefix=/usr --root="$pkgdir" --optimize=1 \
    --skip-build
}

