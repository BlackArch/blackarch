# This file is part of BlackArch Linux ( http://blackarch.org ).
# See COPYING for license details.

pkgname='python2-pyside'
_pkgrealname='pyside'
pkgver='1.2.2'
pkgrel=1
pkgdesc='Provides LGPL Qt bindings for Python and related tools for binding
generation.'
arch=('i686' 'x86_64' 'armv6h' 'armv7h')
license=('LGPL')
url='http://www.pyside.org/'
_qtver='4.8'
depends=("qt4>=${_qtver}" 'phonon-backend' 'qtwebkit' "shiboken>=${pkgver}")
makedepends=('cmake' "shiboken>=$pkgver" 'python2-sphinx')
source=("http://download.qt-project.org/official_releases/pyside/pyside-qt${_qtver}+${pkgver}.tar.bz2"
        "CMakeLists.patch"
        "sphinxbuild2-fix.patch")
sha1sums=('955e32d193d173faa64edc51111289cdcbe3b96e'
          '72aaa408591673541c9a955a55f8d6b88f641de6'
          'e766d366163f01d924a6ca12dcfe5452244d8da1')

prepare() {
  cd "$srcdir"

  patch pyside-qt${_qtver}+$pkgver/CMakeLists.txt < CMakeLists.patch
  patch pyside-qt${_qtver}+$pkgver/doc/CMakeLists.txt < sphinxbuild2-fix.patch
}

build(){
  cd "$srcdir"

  cd pyside-qt${_qtver}+$pkgver

  mkdir -p build

  cd build

  cmake .. -DCMAKE_INSTALL_PREFIX=/usr \
           -DCMAKE_BUILD_TYPE=Release \
           -DBUILD_TESTS=OFF \
           -DQT_QMAKE_EXECUTABLE=qmake-qt4 \
           -DQT_PHONON_INCLUDE_DIR=/usr/include/qt4/phonon
  make
}

package(){
  cd "$srcdir/pyside-qt${_qtver}+$pkgver/build"

  make DESTDIR="$pkgdir" install
}
