# This file is part of BlackArch Linux ( http://blackarch.org ).
# See COPYING for license details.

pkgname=omnibus
pkgver=126.d73c1e7
pkgrel=1
pkgdesc='OSINT tool for intelligence collection, research and artifact management.'
groups=('blackarch' 'blackarch-recon' 'blackarch-social')
arch=('any')
url='https://github.com/InQuest/omnibus'
license=('MIT')
depends=('python2' 'python2-pymongo' 'python2-cmd2' 'python2-threatcrowd'
         'python2-cymon' 'python2-redis' 'python2-requests' 'python2-censys'
         'python2-beautifulsoup3' 'python2-fuzzyfinder' 'python2-dnspython'
         'python2-ipwhois' 'redis' 'mongodb')
makedepends=('git')
source=('git+https://github.com/InQuest/omnibus.git')
sha512sums=('SKIP')

pkgver() {
  cd "$srcdir/omnibus"

  echo $(git rev-list --count HEAD).$(git rev-parse --short HEAD)
}

package() {
  cd "$srcdir/omnibus"

  install -dm 755 "$pkgdir/usr/bin"
  install -dm 755 "$pkgdir/usr/share/omnibus"

  install -Dm644 LICENSE.md "$pkgdir/usr/share/licenses/omnibus/LICENSE.md"
  rm LICENSE.md
  install -Dm644 -t "$pkgdir/usr/share/doc/omnibus/" *.md docs/*.rst
  cp -a docs/. "$pkgdir/usr/share/doc/omnibus/"
  rm -rf *.md docs

  cp -a * "$pkgdir/usr/share/omnibus/"

  cat > "$pkgdir/usr/bin/omnibus" << EOF
#!/bin/sh
cd /usr/share/omnibus
exec python2 omnibus-cli.py "\${@}"
EOF

  chmod a+x "$pkgdir/usr/bin/omnibus"
}
