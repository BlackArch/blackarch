# This file is part of BlackArch Linux ( https://www.blackarch.org/ ).
# See COPYING for license details.

pkgbase=python-twilio
pkgname=('python2-twilio' 'python-twilio')
_pkgname=twilio
pkgver=6.29.0
pkgrel=1
pkgdesc='Twilio API client and TwiML generator.'
arch=('any')
url='https://pypi.org/project/twilio/#files'
license=('MIT')
makedepends=('python2-setuptools' 'python-setuptools')
source=("https://files.pythonhosted.org/packages/2b/23/092afeb8a6bb523b1f5052c5b83ce57c8a319d4891bdb95aa91d929b0e57/$_pkgname-$pkgver.tar.gz")
sha512sums=('6f1ea5b6b55a90a4f758c3a27c62e6fbfc4e2ec83dd41a912c20b41175104ae407b6908da833c4b66edd7bd786a65c3e983c58b77739b165dd287a5849318d4c')

prepare() {
  cp -a "$_pkgname-$pkgver"{,-2}
}

build() {
  cd "$_pkgname-$pkgver-2"

  python2 setup.py build

  cd "$srcdir/$_pkgname-$pkgver"

  python setup.py build
}

package_python2-twilio() {
  depends=('python2' 'python2-six' 'python2-pytz' 'python2-pyjwt')

  cd "$_pkgname-$pkgver-2"

  python2 setup.py install --prefix=/usr --root="$pkgdir" --optimize=1 \
    --skip-build
}

package_python-twilio() {
  depends=('python' 'python-six' 'python-pytz' 'python-pyjwt')

  cd "$_pkgname-$pkgver"

  python setup.py install --prefix=/usr --root="$pkgdir" --optimize=1 \
    --skip-build
}

