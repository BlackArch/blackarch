# This file is part of BlackArch Linux ( https://www.blackarch.org/ ).
# See COPYING for license details.

pkgbase=python-twilio
pkgname=('python2-twilio' 'python-twilio')
_pkgname=twilio
pkgver=5.7.0
pkgrel=1
epoch=1
pkgdesc='Twilio API client and TwiML generator.'
arch=('any')
url='https://pypi.org/project/twilio/#files'
license=('MIT')
makedepends=('python2-setuptools' 'python-setuptools')
source=("https://files.pythonhosted.org/packages/22/35/7a689c70470cb5f7d4d73bae07c4775efe0d23a9e5573714bf4090854979/$_pkgname-$pkgver.tar.gz")
sha512sums=('5337af7663358e6f7cac6e5dfa3e62dbeee7cd443a28e23da23eb41b98a53e404176dae1161d0e27e13e3ab0a23cdd6966e3c79b89e7e18d041aadab906d387f')

prepare() {
  cp -a "$_pkgname-$pkgver"{,-2}
}

build() {
  cd "$_pkgname-$pkgver-2"

  python2 setup.py build

  cd "$srcdir/$_pkgname-$pkgver"

  python setup.py build
}

package_python2-twilio() {
  depends=('python2' 'python2-six' 'python2-pytz' 'python2-httplib2')

  cd "$_pkgname-$pkgver-2"

  python2 setup.py install --prefix=/usr --root="$pkgdir" --optimize=1 \
    --skip-build
}

package_python-twilio() {
  depends=('python' 'python-six' 'python-pytz' 'python-httplib2')

  cd "$_pkgname-$pkgver"

  python setup.py install --prefix=/usr --root="$pkgdir" --optimize=1 \
    --skip-build
}

