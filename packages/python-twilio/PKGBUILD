# This file is part of BlackArch Linux ( https://www.blackarch.org/ ).
# See COPYING for license details.

pkgbase=python-twilio
pkgname=('python2-twilio' 'python-twilio')
_pkgname=twilio
pkgver=6.29.2
pkgrel=1
pkgdesc='Twilio API client and TwiML generator.'
arch=('any')
url='https://pypi.org/project/twilio/#files'
license=('MIT')
makedepends=('python2-setuptools' 'python-setuptools')
source=("https://files.pythonhosted.org/packages/b4/a6/8b64f4de12d7999feca55f42a8dadb6bb67939158768ef6fc194834e8019/twilio-6.29.2.tar.gz")
sha512sums=('666650927fa0ac5a84d6e0e441f5dacae9e027d4065c19c685620b49c35c5a182f9ffd2c4f52feb0984724ac54998231c76f26ceb7e1947766a7047cbfb2fcee')

prepare() {
  cp -a "$_pkgname-$pkgver"{,-2}
}

build() {
  cd "$_pkgname-$pkgver-2"

  python2 setup.py build

  cd "$srcdir/$_pkgname-$pkgver"

  python setup.py build
}

package_python2-twilio() {
  depends=('python2' 'python2-six' 'python2-pytz' 'python2-pyjwt')

  cd "$_pkgname-$pkgver-2"

  python2 setup.py install --prefix=/usr --root="$pkgdir" --optimize=1 \
    --skip-build
}

package_python-twilio() {
  depends=('python' 'python-six' 'python-pytz' 'python-pyjwt')

  cd "$_pkgname-$pkgver"

  python setup.py install --prefix=/usr --root="$pkgdir" --optimize=1 \
    --skip-build
}

