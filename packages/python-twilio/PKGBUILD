# This file is part of BlackArch Linux ( https://www.blackarch.org/ ).
# See COPYING for license details.

pkgbase=python-twilio
pkgname=('python2-twilio' 'python-twilio')
_pkgname=twilio
pkgver=6.29.1
pkgrel=1
pkgdesc='Twilio API client and TwiML generator.'
arch=('any')
url='https://pypi.org/project/twilio/#files'
license=('MIT')
makedepends=('python2-setuptools' 'python-setuptools')
source=("https://files.pythonhosted.org/packages/38/01/8870b294856b991a62780188cbdfe61899a6209b26d74027a52656d9ebbf/twilio-6.29.1.tar.gz")
sha512sums=('a46177145928bdf46b1e7361d0558cbe13a26c86da6944631f86e896361fdb03712503952d8e23c4d2b1b2fbfbe131d68281ce4530309583b18959968138d992')

prepare() {
  cp -a "$_pkgname-$pkgver"{,-2}
}

build() {
  cd "$_pkgname-$pkgver-2"

  python2 setup.py build

  cd "$srcdir/$_pkgname-$pkgver"

  python setup.py build
}

package_python2-twilio() {
  depends=('python2' 'python2-six' 'python2-pytz' 'python2-pyjwt')

  cd "$_pkgname-$pkgver-2"

  python2 setup.py install --prefix=/usr --root="$pkgdir" --optimize=1 \
    --skip-build
}

package_python-twilio() {
  depends=('python' 'python-six' 'python-pytz' 'python-pyjwt')

  cd "$_pkgname-$pkgver"

  python setup.py install --prefix=/usr --root="$pkgdir" --optimize=1 \
    --skip-build
}

