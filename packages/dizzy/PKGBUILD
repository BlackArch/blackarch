# This file is part of BlackArch Linux ( https://www.blackarch.org/ ).
# See COPYING for license details.

pkgname=dizzy
pkgver=2.0
pkgrel=4
groups=('blackarch' 'blackarch-fuzzer' 'blackarch-networking')
pkgdesc='A Python based fuzzing framework with many features.'
arch=('any')
url='http://www.c0decafe.de/'
license=('custom:unknown')
depends=('python' 'python-pcapy' 'exrex')
makedepends=('python-setuptools')
source=("https://c0decafe.de/tools/$pkgname-$pkgver.tar.gz")
sha512sums=('687cb05f59d0c05f23c4d09e27a6f15733ae229b78f4f8d24dfc56b398a1935dadac05949337cd1748d87c6b45d3f4b52043ba1df8a517f8be932f9f606242c0')

build() {
  cd "$pkgname-$pkgver"

  python setup.py build
}

package() {
  cd "$pkgname-$pkgver"

  python setup.py install --root="$pkgdir" --prefix=/usr -O1 --skip-build

  install -Dm 644 -t "$pkgdir/usr/share/doc/$pkgname/" README.md

  mv "$pkgdir/usr/bin/dizzy_cmd" "$pkgdir/usr/bin/$pkgname"
}

