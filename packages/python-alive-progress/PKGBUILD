# This file is part of BlackArch Linux ( https://www.blackarch.org/ ).
# See COPYING for license details.

pkgname=python-alive-progress
_pkgname=alive-progress
pkgver=2.4.1
_pyver=3.10
pkgrel=2
pkgdesc='A new kind of Progress Bar, with real-time throughput, ETA, and very cool animations.'
arch=('any')
url='https://pypi.org/project/alive-progress/#files'
license=('MIT')
depends=('python' 'python-about-time' 'python-grapheme')
makedepends=('python-setuptools')
source=("https://files.pythonhosted.org/packages/b8/ff/8de99fac214112dcb7c37eb0645acd19a0a6c94f8ebb60321388de587bae/$_pkgname-$pkgver.tar.gz")
sha512sums=('ae9feac1d43b9eefa6d213436f18d766c04e2af1bbeccf298e19c7734b5ced3484713f6e57ec6a2f7bad20f00e5e1f3e6a789ce9ba3d65486b1f9d9f34c5c373')

build() {
  cd "$_pkgname-$pkgver"

  python setup.py build
}

package() {
  cd "$_pkgname-$pkgver"

  install -dm 755 "$pkgdir/usr/share/licenses/$pkgname"

  python setup.py install --root="$pkgdir" --prefix=/usr -O1 --skip-build

  mv "$pkgdir/usr/LICENSE" "$pkgdir/usr/share/licenses/$pkgname/LICENSE"

  rm -rf "$pkgdir/usr/lib/python$_pyver/site-packages/tests"
}

