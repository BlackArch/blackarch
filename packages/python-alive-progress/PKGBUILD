# This file is part of BlackArch Linux ( https://www.blackarch.org/ ).
# See COPYING for license details.

pkgname=python-alive-progress
_pkgname=alive-progress
pkgver=3.0.0
_pyver=3.10
pkgrel=1
pkgdesc='A new kind of Progress Bar, with real-time throughput, ETA, and very cool animations.'
arch=('any')
url='https://pypi.org/project/alive-progress/#files'
license=('MIT')
depends=('python' 'python-about-time' 'python-grapheme')
makedepends=('python-setuptools')
source=("https://files.pythonhosted.org/packages/source/${_pkgname::1}/$_pkgname/$_pkgname-$pkgver.tar.gz")
sha512sums=('41c7a0c7dfc83dd069c7c03c37bc871386b856d46667598d36bc70b535673446c340eb865e7c8dd00abe07a565af6c7c9577a1ba5e9685d2dd2736b65f8be4a4')

build() {
  cd "$_pkgname-$pkgver"

  python setup.py build
}

package() {
  cd "$_pkgname-$pkgver"

  install -dm 755 "$pkgdir/usr/share/licenses/$pkgname"

  python setup.py install --root="$pkgdir" --prefix=/usr -O1 --skip-build

  mv "$pkgdir/usr/LICENSE" "$pkgdir/usr/share/licenses/$pkgname/LICENSE"

  rm -rf "$pkgdir/usr/lib/python$_pyver/site-packages/tests"
}

