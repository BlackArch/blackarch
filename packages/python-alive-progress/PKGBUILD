# This file is part of BlackArch Linux ( https://www.blackarch.org/ ).
# See COPYING for license details.

pkgname=python-alive-progress
_pkgname=alive-progress
pkgver=3.0.1
_pyver=3.10
pkgrel=1
pkgdesc='A new kind of Progress Bar, with real-time throughput, ETA, and very cool animations.'
arch=('any')
url='https://pypi.org/project/alive-progress/#files'
license=('MIT')
depends=('python' 'python-about-time' 'python-grapheme')
makedepends=('python-setuptools')
source=("https://files.pythonhosted.org/packages/source/${_pkgname::1}/$_pkgname/$_pkgname-$pkgver.tar.gz")
sha512sums=('84e07e0d33f8a91158863023d5e1a92d7cb5d667be798cc673c2db073fd9bd186ec9c5c2d0b5995f50dc05e39a817d3e9946cfccb03298fde8b75d207be254c6')

build() {
  cd "$_pkgname-$pkgver"

  python setup.py build
}

package() {
  cd "$_pkgname-$pkgver"

  install -dm 755 "$pkgdir/usr/share/licenses/$pkgname"

  python setup.py install --root="$pkgdir" --prefix=/usr -O1 --skip-build

  mv "$pkgdir/usr/LICENSE" "$pkgdir/usr/share/licenses/$pkgname/LICENSE"

  rm -rf "$pkgdir/usr/lib/python$_pyver/site-packages/tests"
}

