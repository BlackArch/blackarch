# This file is part of BlackArch Linux ( https://www.blackarch.org/ ).
# See COPYING for license details.

pkgbase=python-pytesseract
pkgname=('python2-pytesseract' 'python-pytesseract')
_pkgname=pytesseract
pkgver=0.3.6
pkgrel=2
pkgdesc="Python-tesseract is a python wrapper for Google's Tesseract-OCR."
arch=('any')
url='https://pypi.org/project/pytesseract/#files'
license=('GPL3')
makedepends=('python2-setuptools' 'python-setuptools')
source=("https://files.pythonhosted.org/packages/17/4b/4dbd55388225bb6cd243d21f70e77cb3ce061e241257485936324b8e920f/$_pkgname-$pkgver.tar.gz")
sha512sums=('093945c4d707d6d9bf4f1f50db5eea7c484d30bc7b80c26650a9fee8f79f5fae064dbdea14e89089e8239eb056de5caf132baae3bf54685c590bf81ab483de0a')

prepare() {
  cp -a "$_pkgname-$pkgver"{,-2}
}

build() {
  cd "$_pkgname-$pkgver-2"

  python2 setup.py build

  cd "$srcdir/$_pkgname-$pkgver"

  python setup.py build
}

package_python2-pytesseract() {
  depends=('python2' 'python2-pillow')

  cd "$_pkgname-$pkgver-2"

  python2 setup.py install --prefix=/usr --root="$pkgdir" --optimize=1 \
    --skip-build

  mv "$pkgdir/usr/bin/$_pkgname" "$pkgdir/usr/bin/${_pkgname}2"
}

package_python-pytesseract() {
  depends=('python' 'python-pillow')

  cd "$_pkgname-$pkgver"

  python setup.py install --prefix=/usr --root="$pkgdir" --optimize=1 \
    --skip-build
}

