# This file is part of BlackArch Linux ( https://www.blackarch.org/ ).
# See COPYING for license details.

pkgbase=python-pytesseract
pkgname=('python2-pytesseract' 'python-pytesseract')
_pkgname=pytesseract
pkgver=0.3.0
pkgrel=1
pkgdesc="Python-tesseract is a python wrapper for Google's Tesseract-OCR."
arch=('any')
url='https://pypi.org/project/pytesseract/#files'
license=('GPL3')
makedepends=('python2-setuptools' 'python-setuptools')
source=("https://files.pythonhosted.org/packages/47/e5/892d78db0d26372aa376fc1b127e9cd4cc158727a76e0802069115fcbd6e/$_pkgname-$pkgver.tar.gz")
sha512sums=('913a66cb49ed12bdc29c3b6ae33311d8f5e25dfe592d7b7aeb928e764b84733c77c783e216ccb897270e57a2b0a8809af3821e56ab9193273c9478ef07b8990e')

prepare() {
  cp -a "$_pkgname-$pkgver"{,-2}
}

build() {
  cd "$_pkgname-$pkgver-2"

  python2 setup.py build

  cd "$srcdir/$_pkgname-$pkgver"

  python setup.py build
}

package_python2-pytesseract() {
  depends=('python2' 'python2-pillow')

  cd "$_pkgname-$pkgver-2"

  python2 setup.py install --prefix=/usr --root="$pkgdir" --optimize=1 \
    --skip-build

  mv "$pkgdir/usr/bin/$_pkgname" "$pkgdir/usr/bin/${_pkgname}2"
}

package_python-pytesseract() {
  depends=('python' 'python-pillow')

  cd "$_pkgname-$pkgver"

  python setup.py install --prefix=/usr --root="$pkgdir" --optimize=1 \
    --skip-build
}

