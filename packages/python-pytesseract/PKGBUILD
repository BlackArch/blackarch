# This file is part of BlackArch Linux ( https://www.blackarch.org/ ).
# See COPYING for license details.

pkgbase=python-pytesseract
pkgname=('python2-pytesseract' 'python-pytesseract')
_pkgname=pytesseract
pkgver=0.2.9
pkgrel=1
pkgdesc="Python-tesseract is a python wrapper for Google's Tesseract-OCR."
arch=('any')
url='https://pypi.org/project/pytesseract/#files'
license=('GPL3')
makedepends=('python2-setuptools' 'python-setuptools')
source=("https://files.pythonhosted.org/packages/79/16/27c638611384a1b7a6f8b8bb2c54c3a5a493000bc7914efec77e855938be/$_pkgname-$pkgver.tar.gz")
sha512sums=('826b5b0b459a12efe33223059d795a53cb097460536982f56ece3f793ba3cd276300efff7317d159bd0408e813d3e3de7b0ad1fa6fd55014fdec5aeb3140d87a')

prepare() {
  cp -a "$_pkgname-$pkgver"{,-2}
}

build() {
  cd "$_pkgname-$pkgver-2"

  python2 setup.py build

  cd "$srcdir/$_pkgname-$pkgver"

  python setup.py build
}

package_python2-pytesseract() {
  depends=('python2' 'python2-pillow')

  cd "$_pkgname-$pkgver-2"

  python2 setup.py install --prefix=/usr --root="$pkgdir" --optimize=1 \
    --skip-build

  mv "$pkgdir/usr/bin/$_pkgname" "$pkgdir/usr/bin/${_pkgname}2"
}

package_python-pytesseract() {
  depends=('python' 'python-pillow')

  cd "$_pkgname-$pkgver"

  python setup.py install --prefix=/usr --root="$pkgdir" --optimize=1 \
    --skip-build
}

