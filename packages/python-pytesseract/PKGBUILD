# This file is part of BlackArch Linux ( https://www.blackarch.org/ ).
# See COPYING for license details.

pkgbase=python-pytesseract
pkgname=('python2-pytesseract' 'python-pytesseract')
_pkgname=pytesseract
pkgver=0.3.1
pkgrel=1
pkgdesc="Python-tesseract is a python wrapper for Google's Tesseract-OCR."
arch=('any')
url='https://pypi.org/project/pytesseract/#files'
license=('GPL3')
makedepends=('python2-setuptools' 'python-setuptools')
source=("https://files.pythonhosted.org/packages/a9/7c/9ed191f009dac30682c320d925d50dbc39ae621310218a95f970ee4ff5e5/$_pkgname-$pkgver.tar.gz")
sha512sums=('62715fe662fc1c6724d40d740aaf3d4a78f5c8cb4381b2c44af952358610ee5c452fc5ed60bc58ba65e30e671d6b7749adf0f348ef3d936f3a386c90aec27a64')

prepare() {
  cp -a "$_pkgname-$pkgver"{,-2}
}

build() {
  cd "$_pkgname-$pkgver-2"

  python2 setup.py build

  cd "$srcdir/$_pkgname-$pkgver"

  python setup.py build
}

package_python2-pytesseract() {
  depends=('python2' 'python2-pillow')

  cd "$_pkgname-$pkgver-2"

  python2 setup.py install --prefix=/usr --root="$pkgdir" --optimize=1 \
    --skip-build

  mv "$pkgdir/usr/bin/$_pkgname" "$pkgdir/usr/bin/${_pkgname}2"
}

package_python-pytesseract() {
  depends=('python' 'python-pillow')

  cd "$_pkgname-$pkgver"

  python setup.py install --prefix=/usr --root="$pkgdir" --optimize=1 \
    --skip-build
}

