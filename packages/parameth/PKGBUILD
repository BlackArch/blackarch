# This file is part of BlackArch Linux ( https://www.blackarch.org/ ).
# See COPYING for license details.

pkgname=parameth
pkgver=56.8da6f27
pkgrel=1
pkgdesc='This tool can be used to brute discover GET and POST parameters.'
groups=('blackarch' 'blackarch-webapp' 'blackarch-scanner')
arch=('any')
url='https://github.com/mak-/parameth'
license=('custom:unknonw')
depends=('python2' 'python2-argparse' 'python2-requests' 'python2-numpy')
makedepends=('git')
source=('git+https://github.com/mak-/parameth.git')
sha512sums=('SKIP')

pkgver() {
  cd $pkgname

  echo $(git rev-list --count HEAD).$(git rev-parse --short HEAD)
}

package() {
  cd $pkgname

  install -dm 755 "$pkgdir/usr/bin"
  install -dm 755 "$pkgdir/usr/share/$pkgname"

  install -Dm 644 -t "$pkgdir/usr/share/doc/$pkgname/" README.md

  cp -a * "$pkgdir/usr/share/$pkgname/"

  cat > "$pkgdir/usr/bin/$pkgname" << EOF
#!/bin/sh
cd /usr/share/$pkgname
exec python2 parameth.py "\$@"
EOF

  chmod +x "$pkgdir/usr/bin/$pkgname"
}

