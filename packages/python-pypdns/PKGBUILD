# This file is part of BlackArch Linux ( https://www.blackarch.org/ ).
# See COPYING for license details.

pkgbase=python-pypdns
pkgname=('python2-pypdns' 'python-pypdns')
_pkgname=pypdns
pkgver=2.0.0
pkgrel=1
pkgdesc='Python API for PDNS.'
arch=('any')
url='https://pypi.org/project/pypdns/#files'
license=('BSD')
makedepends=('python2-setuptools' 'python-setuptools')
source=("https://files.pythonhosted.org/packages/source/${_pkgname::1}/$_pkgname/$_pkgname-$pkgver.tar.gz")
sha512sums=('f9c5e368a68f2bbed2e46acb8a743df9a07e23a27ddafded0f9437bd0296b9eba9b5e9c24d9f5c2af9c94b0746f311b287db648aefc5a852d71409a724a9e5d1')

prepare() {
  cp -a "$_pkgname-$pkgver"{,-2}
}

build() {
  cd "$_pkgname-$pkgver-2"

  python2 setup.py build

  cd "$srcdir/$_pkgname-$pkgver"

  python setup.py build
}

package_python2-pypdns() {
  depends=('python2' 'python2-requests-cache')

  cd "$_pkgname-$pkgver-2"

  python2 setup.py install --prefix=/usr --root="$pkgdir" --optimize=1 \
    --skip-build
}

package_python-pypdns() {
  depends=('python' 'python-requests-cache')

  cd "$_pkgname-$pkgver"

  python setup.py install --prefix=/usr --root="$pkgdir" --optimize=1 \
    --skip-build
}

