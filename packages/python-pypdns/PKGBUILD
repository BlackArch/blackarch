# This file is part of BlackArch Linux ( https://www.blackarch.org/ ).
# See COPYING for license details.

pkgbase=python-pypdns
pkgname=('python2-pypdns' 'python-pypdns')
_pkgname=pypdns
pkgver=2.1.1
pkgrel=1
pkgdesc='Python API for PDNS.'
arch=('any')
url='https://pypi.org/project/pypdns/#files'
license=('BSD')
makedepends=('python2-setuptools' 'python-setuptools')
source=("https://files.pythonhosted.org/packages/source/${_pkgname::1}/$_pkgname/$_pkgname-$pkgver.tar.gz")
sha512sums=('c99973eea2cf840f8c2bbb2732f8b8f4a4d808ee89748da6e1bea2038e5ec8622fa522784570127497c2f3f3b81b527ac0969eee92cd06226dcce0baa166510a')

prepare() {
  cp -a "$_pkgname-$pkgver"{,-2}
}

build() {
  cd "$_pkgname-$pkgver-2"

  python2 setup.py build

  cd "$srcdir/$_pkgname-$pkgver"

  python setup.py build
}

package_python2-pypdns() {
  depends=('python2' 'python2-requests-cache')

  cd "$_pkgname-$pkgver-2"

  python2 setup.py install --prefix=/usr --root="$pkgdir" --optimize=1 \
    --skip-build
}

package_python-pypdns() {
  depends=('python' 'python-requests-cache')

  cd "$_pkgname-$pkgver"

  python setup.py install --prefix=/usr --root="$pkgdir" --optimize=1 \
    --skip-build
}

