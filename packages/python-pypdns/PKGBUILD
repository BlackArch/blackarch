# This file is part of BlackArch Linux ( https://www.blackarch.org/ ).
# See COPYING for license details.

pkgbase=python-pypdns
pkgname=('python2-pypdns' 'python-pypdns')
_pkgname=pypdns
pkgver=1.5.2
pkgrel=1
pkgdesc='Python API for PDNS.'
arch=('any')
url='https://pypi.org/project/pypdns/#files'
license=('BSD')
makedepends=('python2-setuptools' 'python-setuptools')
source=("https://files.pythonhosted.org/packages/44/f6/9c3f989658a7c19db7b6ff1431be1ff5fdd4105e15e3057d6bdec6fd7fdb/$_pkgname-$pkgver.tar.gz")
sha512sums=('fd7ce7ebb5265f7e2650dc4f6e4219446f017fa5ebc9097621fb561cb1889bb3517b42cea1a3634f2f512e2614685c63008f0b3634da6daff37ac25c565c0a8e')

prepare() {
  cp -a "$_pkgname-$pkgver"{,-2}
}

build() {
  cd "$_pkgname-$pkgver-2"

  python2 setup.py build

  cd "$srcdir/$_pkgname-$pkgver"

  python setup.py build
}

package_python2-pypdns() {
  depends=('python2' 'python2-requests-cache')

  cd "$_pkgname-$pkgver-2"

  python2 setup.py install --prefix=/usr --root="$pkgdir" --optimize=1 \
    --skip-build
}

package_python-pypdns() {
  depends=('python' 'python-requests-cache')

  cd "$_pkgname-$pkgver"

  python setup.py install --prefix=/usr --root="$pkgdir" --optimize=1 \
    --skip-build
}

