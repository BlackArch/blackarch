# This file is part of BlackArch Linux ( https://www.blackarch.org/ ).
# See COPYING for license details.

pkgbase=python-pypdns
pkgname=('python2-pypdns' 'python-pypdns')
_pkgname=pypdns
pkgver=2.2.2
pkgrel=1
pkgdesc='Python API for PDNS.'
arch=('any')
url='https://pypi.org/project/pypdns/#files'
license=('BSD')
makedepends=('python2-setuptools' 'python-setuptools')
source=("https://files.pythonhosted.org/packages/source/${_pkgname::1}/$_pkgname/$_pkgname-$pkgver.tar.gz")
sha512sums=('ceec6902f1f4ab9256b4934bef9833c203596448b387c68d8fb64844fe51f6a74fd85ccaaf3c335d83844189f8aab69cf110229e4529b77ab2082b73a2c25751')

prepare() {
  cp -a "$_pkgname-$pkgver"{,-2}
}

build() {
  cd "$_pkgname-$pkgver-2"

  python2 setup.py build

  cd "$srcdir/$_pkgname-$pkgver"

  python setup.py build
}

package_python2-pypdns() {
  depends=('python2' 'python2-requests-cache')

  cd "$_pkgname-$pkgver-2"

  python2 setup.py install --prefix=/usr --root="$pkgdir" -O1 --skip-build
}

package_python-pypdns() {
  depends=('python' 'python-requests-cache')

  cd "$_pkgname-$pkgver"

  python setup.py install --prefix=/usr --root="$pkgdir" -O1 --skip-build
}

