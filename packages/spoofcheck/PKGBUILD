# This file is part of BlackArch Linux ( http://blackarch.org ).
# See COPYING for license details.

pkgname=spoofcheck
pkgver=16.8cce591
pkgrel=3
pkgdesc='Simple script that checks a domain for email protections.'
groups=('blackarch' 'blackarch-recon' 'blackarch-social')
arch=('any')
url='https://github.com/bishopfox/spoofcheck'
license=('MIT')
depends=('python2' 'python2-colorama' 'python2-dnslib' 'python2-tldextract'
         'python2-emailprotectionslib' 'python2-setuptools' 'python2-idna'
         'python2-requests' 'python2-requests-file')
makedepends=('git')
source=('git+https://github.com/bishopfox/spoofcheck.git')
sha512sums=('SKIP')

pkgver() {
  cd "$srcdir/spoofcheck"

  echo $(git rev-list --count HEAD).$(git rev-parse --short HEAD)
}

package() {
  cd "$srcdir/spoofcheck"

  install -dm 755 "$pkgdir/usr/bin"
  install -dm 755 "$pkgdir/usr/share/spoofcheck"

  install -Dm644 README.md "$pkgdir/usr/share/doc/spoofcheck/README.md"
  install -Dm644 LICENSE "$pkgdir/usr/share/licenses/spoofcheck/LICENSE"

  rm README.md LICENSE

  cp -a * "$pkgdir/usr/share/spoofcheck"

  cat > "$pkgdir/usr/bin/spoofcheck" << EOF
#!/bin/sh
cd /usr/share/spoofcheck
exec python2 spoofcheck.py "\${@}"
EOF

  chmod a+x "$pkgdir/usr/bin/spoofcheck"
}
