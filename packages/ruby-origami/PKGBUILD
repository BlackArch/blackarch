# This file is part of BlackArch Linux ( http://blackarch.org ).
# See COPYING for license details.
pkgname='ruby-origami'
_gemname='origami'
pkgver='2.0.2'
pkgrel=1
pkgdesc="Ruby framework to manipulate PDF documents"
arch=('any')
url='http://github.com/gdelugre/origami'
license=("LGPL-3.0+")
depends=('ruby' 'ruby-colorize')
groups=('blackarch' 'blackarch-malware' 'blackarch-binary')
replaces=('origami')
conflicts=('origami')
provides=('origami')
options=('!emptydirs')
source=("https://rubygems.org/downloads/$_gemname-$pkgver.gem" )
noextract=("$_gemname-$pkgver.gem")
sha1sums=('b3876a4e66956d064e5750c19439d67a55f8a1f0' )


package() {
  local _gemdir="$(ruby -rubygems -e 'puts Gem.default_dir')"
  gem install --ignore-dependencies --no-document --no-user-install -i "$pkgdir/$_gemdir" -n "$pkgdir"/usr/bin $_gemname-$pkgver.gem 
  rm "$pkgdir/$_gemdir/cache/$_gemname-$pkgver.gem"
  install -D -m644 "$pkgdir/$_gemdir/gems/$_gemname-$pkgver/COPYING.LESSER" "$pkgdir/usr/share/licenses/$pkgname/COPYING.LESSER"

  # Preserve library and other required directories. Remove junk that gem installs.
  local _copydir=`mktemp -d -t $_gemname.XXX`
  chmod 755 $_copydir
  (cd "$pkgdir/$_gemdir/gems/$_gemname-$pkgver"/ && cp -r --parents lib bin $_copydir)
  rm -rf "$pkgdir/$_gemdir/gems/$_gemname-$pkgver"
  mv $_copydir "$pkgdir/$_gemdir/gems/$_gemname-$pkgver"

}
