# This file is part of BlackArch Linux ( https://www.blackarch.org/ ).
# See COPYING for license details.

pkgbase=python-dpkt
pkgname=('python2-dpkt' 'python-dpkt')
_pkgname=dpkt
pkgver=1.9.7.1
pkgrel=1
pkgdesc='Fast, simple packet creation / parsing, with definitions for the basic TCP/IP protocols.'
arch=('any')
url='https://pypi.org/project/dpkt/#files'
license=('BSD')
makedepends=('python2-setuptools' 'python-setuptools')
source=("https://files.pythonhosted.org/packages/00/fa/9bde13b6c7e86883ef462b86b3c2ba68d1190119662c435a454f670920cd/$_pkgname-$pkgver.tar.gz")
sha512sums=('6816037ffc9ee0ad5df28c54828193f96316b27109bc26422a4ab5f7b7e7f85f8392207124d1425c5c2911d202176670195fd919d533eb2871278cefcb4b96a0')

prepare() {
  cp -a "$_pkgname-$pkgver"{,-2}
}

build() {
  cd "$_pkgname-$pkgver-2"

  python2 setup.py build

  cd "$srcdir/$_pkgname-$pkgver"

  python setup.py build
}

package_python2-dpkt() {
  depends=('python2')

  cd "$_pkgname-$pkgver-2"

  python2 setup.py install --root="$pkgdir" --prefix=/usr -O1 --skip-build
}

package_python-dpkt() {
  depends=('python')

  cd "$_pkgname-$pkgver"

  python setup.py install --root="$pkgdir" --prefix=/usr -O1 --skip-build
}

