# This file is part of BlackArch Linux ( https://www.blackarch.org/ ).
# See COPYING for license details.

pkgbase=python-dpkt
pkgname=('python2-dpkt' 'python-dpkt')
_pkgname=dpkt
pkgver=1.9.3
pkgrel=1
pkgdesc='Fast, simple packet creation / parsing, with definitions for the basic TCP/IP protocols.'
arch=('any')
url='https://pypi.org/project/dpkt/#files'
license=('BSD')
makedepends=('python2-setuptools' 'python-setuptools')
source=("https://files.pythonhosted.org/packages/0a/48/729dafc8d98359bddbd771481fcdaad77e7a46cd85b2ea5809d4185b378d/$_pkgname-$pkgver.tar.gz")
sha512sums=('039a4c40bc6dc72b43b5cfb5cece56651cf7821551195d090b71749c969fd7dff06df65b7d0d38d5407f9dd0184881c31cd9df1fe3d9a37a97a40162606218c5')

prepare() {
  cp -a "$_pkgname-$pkgver"{,-2}
}

build() {
  cd "$_pkgname-$pkgver-2"

  python2 setup.py build

  cd "$srcdir/$_pkgname-$pkgver"

  python setup.py build
}

package_python2-dpkt() {
  depends=('python2')

  cd "$_pkgname-$pkgver-2"

  python2 setup.py install --prefix=/usr --root="$pkgdir" --optimize=1 \
    --skip-build
}

package_python-dpkt() {
  depends=('python')

  cd "$_pkgname-$pkgver"

  python setup.py install --prefix=/usr --root="$pkgdir" --optimize=1 \
    --skip-build
}

