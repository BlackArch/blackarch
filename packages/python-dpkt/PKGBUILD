# This file is part of BlackArch Linux ( https://www.blackarch.org/ ).
# See COPYING for license details.

pkgbase=python-dpkt
pkgname=('python2-dpkt' 'python-dpkt')
_pkgname=dpkt
pkgver=1.9.5
pkgrel=1
pkgdesc='Fast, simple packet creation / parsing, with definitions for the basic TCP/IP protocols.'
arch=('any')
url='https://pypi.org/project/dpkt/#files'
license=('BSD')
makedepends=('python2-setuptools' 'python-setuptools')
source=("https://files.pythonhosted.org/packages/ce/66/48723e4c8d3d18b70f47267ab4d29444eaa0d71582515e44583163b6df75/$_pkgname-$pkgver.tar.gz")
sha512sums=('51c476f0720195153db2104f207faa405e55f6dc6a6a0ff60c20608236ce9267d82781e6a58651c6a0f87d5fa6a67f6a5785d4210d1a5c63e1ffde8291f3c6ca')

prepare() {
  cp -a "$_pkgname-$pkgver"{,-2}
}

build() {
  cd "$_pkgname-$pkgver-2"

  python2 setup.py build

  cd "$srcdir/$_pkgname-$pkgver"

  python setup.py build
}

package_python2-dpkt() {
  depends=('python2')

  cd "$_pkgname-$pkgver-2"

  python2 setup.py install --root="$pkgdir" --prefix=/usr -O1 --skip-build
}

package_python-dpkt() {
  depends=('python')

  cd "$_pkgname-$pkgver"

  python setup.py install --root="$pkgdir" --prefix=/usr -O1 --skip-build
}

