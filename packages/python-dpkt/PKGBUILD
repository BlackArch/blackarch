# This file is part of BlackArch Linux ( https://www.blackarch.org/ ).
# See COPYING for license details.

pkgbase=python-dpkt
pkgname=('python2-dpkt' 'python-dpkt')
_pkgname=dpkt
pkgver=1.9.6
pkgrel=1
pkgdesc='Fast, simple packet creation / parsing, with definitions for the basic TCP/IP protocols.'
arch=('any')
url='https://pypi.org/project/dpkt/#files'
license=('BSD')
makedepends=('python2-setuptools' 'python-setuptools')
source=("https://files.pythonhosted.org/packages/9a/ee/d66a3ab4a229008d45a10b384813ea1fb5fc585650c97a9d982b992f1cc3/$_pkgname-$pkgver.tar.gz")
sha512sums=('de7260df24c6e9a4872ecf80f621102ef3ba78cabe66a8c4eaf5f2dd79fdf1c4135fd2614d65c33d0c8d2e505b9dd7e523a0b29f7048e1a24eb9f924ce207b43')

prepare() {
  cp -a "$_pkgname-$pkgver"{,-2}
}

build() {
  cd "$_pkgname-$pkgver-2"

  python2 setup.py build

  cd "$srcdir/$_pkgname-$pkgver"

  python setup.py build
}

package_python2-dpkt() {
  depends=('python2')

  cd "$_pkgname-$pkgver-2"

  python2 setup.py install --root="$pkgdir" --prefix=/usr -O1 --skip-build
}

package_python-dpkt() {
  depends=('python')

  cd "$_pkgname-$pkgver"

  python setup.py install --root="$pkgdir" --prefix=/usr -O1 --skip-build
}

