# This file is part of BlackArch Linux ( http://blackarch.org ).
# See COPYING for license details.

pkgname=routersploit
pkgver=766.a03e81d
pkgrel=1
pkgdesc='The Router Exploitation Framework.'
groups=('blackarch' 'blackarch-exploitation')
arch=('any')
url='https://github.com/reverse-shell/routersploit'
license=('custom:unknown')
depends=('python' 'python-requests' 'python-paramiko' 'python-pysnmp'
         'python-beautifulsoup4' 'python-argparse' 'python-future'
         'python-pycryptodome')
makedepends=('git')
source=('git+https://github.com/reverse-shell/routersploit.git')
sha512sums=('SKIP')

pkgver() {
  cd "$srcdir/routersploit"

  echo $(git rev-list --count HEAD).$(git rev-parse --short HEAD)
}

package() {
  cd "$srcdir/routersploit"

  install -dm 755 "$pkgdir/usr/bin"
  install -dm 755 "$pkgdir/usr/share/routersploit"

  install -Dm644 -t "$pkgdir/usr/share/doc/routersploit/" *.md
  install -Dm644 LICENSE "$pkgdir/usr/share/licenses/routersploit/LICENSE"

  rm *.md LICENSE

  cp -a * "$pkgdir/usr/share/routersploit/"

  cat > "$pkgdir/usr/bin/routersploit" << EOF
#!/bin/sh
cd /usr/share/routersploit
exec python rsf.py "\${@}"
EOF

  chmod a+x "$pkgdir/usr/bin/routersploit"
}
