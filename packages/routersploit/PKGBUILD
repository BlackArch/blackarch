# This file is part of BlackArch Linux ( http://blackarch.org ).
# See COPYING for license details.

pkgname='routersploit'
pkgver=654.5117d27
pkgrel=1
pkgdesc='The Router Exploitation Framework.'
groups=('blackarch' 'blackarch-exploitation')
arch=('any')
url='https://github.com/reverse-shell/routersploit'
license=('custom:unknown')
depends=('python2' 'python2-requests' 'python2-paramiko' 'python2-pysnmp'
         'python2-beautifulsoup4' 'python2-argparse')
makedepends=('git')
source=('git+https://github.com/reverse-shell/routersploit.git')
sha1sums=('SKIP')

pkgver() {
  cd "$srcdir/routersploit"

  echo $(git rev-list --count HEAD).$(git rev-parse --short HEAD)
}

package() {
  cd "$srcdir/routersploit"

  mkdir -p "$pkgdir/usr/bin"
  mkdir -p "$pkgdir/usr/share/routersploit"

  install -Dm644 README.md "$pkgdir/usr/share/doc/routersploit/README.md"
  install -Dm644 LICENSE "$pkgdir/usr/share/licenses/routersploit/LICENSE"

  rm *.md LICENSE

  cp -a * "$pkgdir/usr/share/routersploit/"

  cat > "$pkgdir/usr/bin/routersploit" << EOF
#!/bin/sh
cd /usr/share/routersploit
exec python2 rsf.py "\$@"
EOF

  chmod a+x "$pkgdir/usr/bin/routersploit"
}
