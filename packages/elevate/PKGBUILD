# This file is part of BlackArch Linux ( https://www.blackarch.org/ ).
# See COPYING for license details.

pkgname=elevate
_pkgname=Elevate
pkgver=27.1272d51
pkgrel=2
pkgdesc='Horizontal domain discovery tool you can use to discover other domains owned by a given company.'
arch=('any')
groups=('blackarch' 'blackarch-recon')
url='https://github.com/Healdb/Elevate'
license=('custom:unkown')
depends=('python' 'python-requests')
makedepends=('git')
source=("git+https://github.com/Healdb/$_pkgname.git")
sha512sums=('SKIP')

pkgver() {
  cd $_pkgname

  echo $(git rev-list --count HEAD).$(git rev-parse --short HEAD)
}

package() {
  cd $_pkgname

  install -dm 755 "$pkgdir/usr/share/$pkgname"
  install -dm 755 "$pkgdir/usr/bin"

  install -Dm 644 -t "$pkgdir/usr/share/doc/$pkgname/" *.md

  rm *.md

  cp -a * "$pkgdir/usr/share/$pkgname/"

  cat > "$pkgdir/usr/bin/$pkgname" << EOF
#!/bin/sh
cd /usr/share/$pkgname
exec python $pkgname.py "\$@"
EOF

  chmod +x "$pkgdir/usr/bin/$pkgname"
}

