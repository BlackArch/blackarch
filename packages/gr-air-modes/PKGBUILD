# This file is part of BlackArch Linux ( http://blackarch.org ).
# See COPYING for license details.

pkgname=gr-air-modes
pkgver=396.0b6c383
pkgrel=1
pkgdesc="Gnuradio tools for receiving Mode S transponder signals, including ADS-B."
arch=('x86_64' 'armv6h' 'armv7h' 'aarch64')
groups=('blackarch' 'blackarch-radio')
url='https://github.com/bistromath/gr-air-modes'
license=('GPL3')
depends=('gnuradio' 'gnuradio-companion' 'gnuradio-osmosdr' 'python2-numpy'
         'python2-scipy' 'python2-pyzmq' 'sqlite' 'libuhd')
makedepends=('cmake')
source=("git+http://github.com/bistromath/gr-air-modes.git")
sha512sums=('SKIP')


pkgver() {
  cd "$pkgname"

  echo $(git rev-list --count HEAD).$(git rev-parse --short HEAD)
}

prepare() {
  cd "$pkgname"

  mkdir build && cd build
  cmake -DCMAKE_INSTALL_PREFIX=/usr ..
}

build() {
  cd "$pkgname/build"

  make
}

package() {
  cd "$pkgname/build"

  make DESTDIR="$pkgdir" install
}

