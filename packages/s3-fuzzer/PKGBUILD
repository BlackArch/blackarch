# This file is part of BlackArch Linux ( https://www.blackarch.org/ ).
# See COPYING for license details.

pkgname=s3-fuzzer
pkgver=4.0a2a6f0
pkgrel=3
pkgdesc='A concurrent, command-line AWS S3 Fuzzer.'
groups=('blackarch' 'blackarch-fuzzer')
arch=('x86_64' 'aarch64')
url='https://github.com/petermbenjamin/s3-fuzzer'
license=('BSD')
makedepends=('git' 'go')
source=("git+https://github.com/petermbenjamin/$pkgname.git")
sha512sums=('SKIP')

pkgver() {
  cd $pkgname

  echo $(git rev-list --count HEAD).$(git rev-parse --short HEAD)
}

prepare() {
  cd $pkgname

  sed -i 's/Sirupsen/sirupsen/g' main.go
}

build() {
  cd $pkgname

  GOPATH="$srcdir" go mod init go.mod
  GOPATH="$srcdir" go mod edit -replace \
    github.com/petermbenjamin/go-open@v0.1.1=github.com/pbnj/go-open@v0.1.1
  GOPATH="$srcdir" go mod tidy
  GOPATH="$srcdir" go mod download
  GOPATH="$srcdir" go build \
    -trimpath \
    -buildmode=pie \
    -mod=readonly \
    -modcacherw \
    -ldflags "-s -w" \
    -o $pkgname .
}

package() {
  cd $pkgname

  install -Dm 755 $pkgname "$pkgdir/usr/bin/$pkgname"
  install -Dm 644 -t "$pkgdir/usr/share/doc/$pkgname/" README.md
}

