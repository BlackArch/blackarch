# This file is part of BlackArch Linux ( https://www.blackarch.org/ ).
# See COPYING for license details.

pkgbase=python-pygtail
pkgname=('python2-pygtail' 'python-pygtail')
_pkgname=pygtail
pkgver=0.11.1
pkgrel=3
pkgdesc='Reads log file lines that have not been read.'
arch=('any')
url='https://pypi.org/project/pygtail/#files'
license=('GPL2')
makedepends=('python2-setuptools' 'python-setuptools')
source=("https://files.pythonhosted.org/packages/12/53/13ed4778e545272219b72367588dde9cdbbdcde80267dc1b246b954c7c7e/$_pkgname-$pkgver.tar.gz")
sha512sums=('93954e1aa08ba4e889666bd6cf648e944436266a5b97a8ae63c0f1eab36f64c2cd360e20a799f2c314c723b44df5271bd62d17258be87fffd550d595616b6364')

prepare() {
  cp -a "$_pkgname-$pkgver"{,-2}
}

build() {
  cd "$_pkgname-$pkgver-2"

  python2 setup.py build

  cd "$srcdir/$_pkgname-$pkgver"

  python setup.py build
}

package_python2-pygtail() {
  depends=('python2')

  cd "$_pkgname-$pkgver-2"

  python2 setup.py install --prefix=/usr --root="$pkgdir" --optimize=1 \
    --skip-build

  mv "$pkgdir/usr/bin/$_pkgname" "$pkgdir/usr/bin/${_pkgname}2"
}

package_python-pygtail() {
  depends=('python')

  cd "$_pkgname-$pkgver"

  python setup.py install --prefix=/usr --root="$pkgdir" --optimize=1 \
    --skip-build
}

