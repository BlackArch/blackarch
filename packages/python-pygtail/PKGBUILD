# This file is part of BlackArch Linux ( https://www.blackarch.org/ ).
# See COPYING for license details.

pkgbase=python-pygtail
pkgname=('python2-pygtail' 'python-pygtail')
_pkgname=pygtail
pkgver=0.10.1
pkgrel=2
pkgdesc='Reads log file lines that have not been read.'
arch=('any')
url='https://pypi.org/project/pygtail/#files'
license=('GPL2')
makedepends=('python2-setuptools' 'python-setuptools')
source=("https://files.pythonhosted.org/packages/48/a7/701449e12c84846c9047a3ae77276f9c03a586e1da94490f40b0150ef449/$_pkgname-$pkgver.tar.gz")
sha512sums=('c712bc0cd485f1cb89c9c5957ce8359a83df20c354094983f1ddf517006038e0a94dbcb2db4afdf87499140efb8e50f39944d98f6f63e8d64a8e5579edbc5652')

prepare() {
  cp -a "$_pkgname-$pkgver"{,-2}
}

build() {
  cd "$_pkgname-$pkgver-2"

  python2 setup.py build

  cd "$srcdir/$_pkgname-$pkgver"

  python setup.py build
}

package_python2-pygtail() {
  depends=('python2')

  cd "$_pkgname-$pkgver-2"

  python2 setup.py install --prefix=/usr --root="$pkgdir" --optimize=1 \
    --skip-build

  mv "$pkgdir/usr/bin/$_pkgname" "$pkgdir/usr/bin/${_pkgname}2"
}

package_python-pygtail() {
  depends=('python')

  cd "$_pkgname-$pkgver"

  python setup.py install --prefix=/usr --root="$pkgdir" --optimize=1 \
    --skip-build
}

