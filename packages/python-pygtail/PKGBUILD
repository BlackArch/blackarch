# This file is part of BlackArch Linux ( https://www.blackarch.org/ ).
# See COPYING for license details.

pkgbase=python-pygtail
pkgname=('python2-pygtail' 'python-pygtail')
_pkgname=pygtail
pkgver=0.11.0
pkgrel=1
pkgdesc='Reads log file lines that have not been read.'
arch=('any')
url='https://pypi.org/project/pygtail/#files'
license=('GPL2')
makedepends=('python2-setuptools' 'python-setuptools')
source=("https://files.pythonhosted.org/packages/14/e9/9453197f05346ae80b68ecc321ff553b59365881a61502c5001e6c6f9e6f/pygtail-0.11.0.tar.gz")
sha512sums=('4bff7d33d563b462a272f7f1a300e68b695085d61ea5ffa2aac61e5215845519b220f6a42395658c6a89e7572bc5b86f9a4b2037f65bc546762a5563850fc383')

prepare() {
  cp -a "$_pkgname-$pkgver"{,-2}
}

build() {
  cd "$_pkgname-$pkgver-2"

  python2 setup.py build

  cd "$srcdir/$_pkgname-$pkgver"

  python setup.py build
}

package_python2-pygtail() {
  depends=('python2')

  cd "$_pkgname-$pkgver-2"

  python2 setup.py install --prefix=/usr --root="$pkgdir" --optimize=1 \
    --skip-build

  mv "$pkgdir/usr/bin/$_pkgname" "$pkgdir/usr/bin/${_pkgname}2"
}

package_python-pygtail() {
  depends=('python')

  cd "$_pkgname-$pkgver"

  python setup.py install --prefix=/usr --root="$pkgdir" --optimize=1 \
    --skip-build
}

