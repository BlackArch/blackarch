# This file is part of BlackArch Linux ( https://www.blackarch.org/ ).
# See COPYING for license details.
# Contributor: peeweep <peeweep at 0x0 dot ee>

pkgname=quark-engine
pkgver=21.6.2
_pyver=3.10
pkgrel=3
pkgdesc='An Obfuscation-Neglect Android Malware Scoring System.'
groups=('blackarch' 'blackarch-mobile' 'blackarch-malware')
arch=('any')
license=('GPL')
url='https://github.com/quark-engine/quark-engine'
depends=('python' 'python-prettytable' 'androguard' 'python-tqdm'
         'python-colorama' 'python-click' 'python-graphviz' 'python-gitpython')
makedepends=('python-setuptools')
source=("$pkgname.tar.gz::https://github.com/quark-engine/$pkgname/archive/v$pkgver.tar.gz")
sha512sums=('9733916c189bcc969c3f056da6ec8954975dd2dd2838d21c11269e8afea367723d730034457e8f1737e4750bd1ccfaa33e28542e058bd3d25a49fe5cb238d977')

build () {
  cd "$pkgname-$pkgver"

  python setup.py build
}

package() {
  cd "$pkgname-$pkgver"

  python setup.py install --root="$pkgdir" --prefix=/usr -O1 --skip-build

  install -Dm 644 -t "$pkgdir/usr/share/doc/$pkgname/" README.md
  install -Dm 644 LICENSE "$pkgdir/usr/share/licenses/$pkgname/LICENSE"

  rm -rf "$pkgdir/usr/lib/python$_pyver/site-packages/tests"
}

