# This file is part of BlackArch Linux ( https://www.blackarch.org/ ).
# See COPYING for license details.
# Contributor: peeweep <peeweep at 0x0 dot ee>

pkgname=quark-engine
pkgver=20.01
pkgrel=1
pkgdesc='An Obfuscation-Neglect Android Malware Scoring System.'
groups=('blackarch' 'blackarch-mobile' 'blackarch-malware')
arch=('any')
license=('GPL')
url='https://github.com/quark-engine/quark-engine'
depends=('python' 'python-prettytable' 'androguard' 'python-tqdm'
         'python-colorama' 'python-click')
makedepends=('python-setuptools')
source=("$pkgname.tar.gz::https://github.com/quark-engine/$pkgname/archive/v$pkgver.tar.gz")
sha512sums=('d0468f9a33533f284fa2f5b3af7859d589b1cfd9300fce779ff5f410ba4b3ec4444964e9f516ee1b05a41f6359fae823b592dc1e1cd18f0aa714aec1d1981976')

build () {
  cd "$pkgname-$pkgver"

  python setup.py build
}

package() {
  cd "$pkgname-$pkgver"

  python setup.py install --root="$pkgdir" --prefix=/usr -O1 --skip-build

  install -Dm 644 -t "$pkgdir/usr/share/doc/$pkgname/" README.md
  install -Dm 644 LICENSE "$pkgdir/usr/share/licenses/$pkgname/LICENSE"
}

