# This file is part of BlackArch Linux ( https://www.blackarch.org/ ).
# See COPYING for license details.

pkgbase=python-gensim
pkgname=('python2-gensim' 'python-gensim')
_pkgname=gensim
pkgver=4.4.0
pkgrel=1
pkgdesc='Python framework for fast Vector Space Modelling.'
arch=('any')
url='https://pypi.org/project/gensim/#files'
license=('LGPL')
makedepends=('python2-setuptools' 'python-setuptools')
options=(!emptydirs)
source=("https://files.pythonhosted.org/packages/source/${_pkgname::1}/$_pkgname/$_pkgname-$pkgver.tar.gz")
sha512sums=('5aa307373a331841e081ca0613bf0f62e2452394285415bc65773dce60ebdd33eb712fa953fc095a35d65ab5fc8a7967a17ace2059e1772c558e6cd2022ad1ec')

prepare() {
  cp -a "$_pkgname-$pkgver"{,-2}
}

build() {
  cd "$_pkgname-$pkgver-2"

  python2 setup.py build

  cd "$srcdir/$_pkgname-$pkgver"

  python setup.py build
}

package_python2-gensim() {
  depends=('python2' 'python2-numpy' 'python2-scipy' 'python2-smart-open')

  cd "$_pkgname-$pkgver-2"

  python2 setup.py install --root="$pkgdir" --prefix=/usr -O1 --skip-build
}

package_python-gensim() {
  depends=('python' 'python-numpy' 'python-scipy' 'python-smart-open')

  cd "$_pkgname-$pkgver"

  python setup.py install --root="$pkgdir" --prefix=/usr -O1 --skip-build
}

