# This file is part of BlackArch Linux ( https://www.blackarch.org/ ).
# See COPYING for license details.

pkgbase=python-gensim
pkgname=('python2-gensim' 'python-gensim')
_pkgname=gensim
pkgver=4.1.0
pkgrel=1
pkgdesc='Python framework for fast Vector Space Modelling.'
arch=('any')
url='https://pypi.org/project/gensim/#files'
license=('LGPL')
makedepends=('python2-setuptools' 'python-setuptools')
options=(!emptydirs)
source=("https://files.pythonhosted.org/packages/55/31/5a8f52f29232d8a6aa5d8fc75531a029bbc24a78869a0a0a4566b9f8c13a/$_pkgname-$pkgver.tar.gz")
sha512sums=('c062f400654ba9146958c3d81211ecdaedace3ed73d934c0a3230f33dda3624d8a99c77ff5bce1d63e1823051c78d0665271ffa2adffd5255d14fc23c74a818d')

prepare() {
  cp -a "$_pkgname-$pkgver"{,-2}
}

build() {
  cd "$_pkgname-$pkgver-2"

  python2 setup.py build

  cd "$srcdir/$_pkgname-$pkgver"

  python setup.py build
}

package_python2-gensim() {
  depends=('python2' 'python2-numpy' 'python2-scipy' 'python2-smart-open')

  cd "$_pkgname-$pkgver-2"

  python2 setup.py install --root="$pkgdir" --prefix=/usr -O1 --skip-build
}

package_python-gensim() {
  depends=('python' 'python-numpy' 'python-scipy' 'python-smart-open')

  cd "$_pkgname-$pkgver"

  python setup.py install --root="$pkgdir" --prefix=/usr -O1 --skip-build
}

