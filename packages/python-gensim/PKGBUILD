# This file is part of BlackArch Linux ( https://www.blackarch.org/ ).
# See COPYING for license details.

pkgbase=python-gensim
pkgname=('python2-gensim' 'python-gensim')
_pkgname=gensim
pkgver=3.8.2
pkgrel=1
pkgdesc='Python framework for fast Vector Space Modelling.'
arch=('any')
url='https://pypi.org/project/gensim/#files'
license=('LGPL')
makedepends=('python2-setuptools' 'python-setuptools')
options=(!emptydirs)
source=("https://files.pythonhosted.org/packages/8a/cd/c3181a9bf69cf2929bcf2ead1f1d7779b0ed375aecff157c48b2ff417367/$_pkgname-$pkgver.tar.gz")
sha512sums=('d4accaa65b0a3347e898fb181c5095c76e5bdd1f75cf0be7f864ac0a016adf2d5d4c58ffabff5c7629388ba3d0682dc79bb8a0bfebfbf2424d97951bc3336d49')

prepare() {
  cp -a "$_pkgname-$pkgver"{,-2}
}

build() {
  cd "$_pkgname-$pkgver-2"

  python2 setup.py build

  cd "$srcdir/$_pkgname-$pkgver"

  python setup.py build
}

package_python2-gensim() {
  depends=('python2' 'python2-numpy' 'python2-scipy' 'python2-smart-open')

  cd "$_pkgname-$pkgver-2"

  python2 setup.py install --root="$pkgdir" --prefix=/usr -O1 --skip-build
}

package_python-gensim() {
  depends=('python' 'python-numpy' 'python-scipy' 'python-smart-open')

  cd "$_pkgname-$pkgver"

  python setup.py install --root="$pkgdir" --prefix=/usr -O1 --skip-build
}

