# This file is part of BlackArch Linux ( https://www.blackarch.org/ ).
# See COPYING for license details.

pkgbase=python-gensim
pkgname=('python2-gensim' 'python-gensim')
_pkgname=gensim
pkgver=4.2.0
pkgrel=1
pkgdesc='Python framework for fast Vector Space Modelling.'
arch=('any')
url='https://pypi.org/project/gensim/#files'
license=('LGPL')
makedepends=('python2-setuptools' 'python-setuptools')
options=(!emptydirs)
source=("https://files.pythonhosted.org/packages/source/${_pkgname::1}/$_pkgname/$_pkgname-$pkgver.tar.gz")
sha512sums=('8393f60fda6e6ccecb90775826ebb8b51830c939476763bc3651f67c3cab583493725c25ac78e7685e5c78803167433634258b8be3e277eeed27feb0d21bfe20')

prepare() {
  cp -a "$_pkgname-$pkgver"{,-2}
}

build() {
  cd "$_pkgname-$pkgver-2"

  python2 setup.py build

  cd "$srcdir/$_pkgname-$pkgver"

  python setup.py build
}

package_python2-gensim() {
  depends=('python2' 'python2-numpy' 'python2-scipy' 'python2-smart-open')

  cd "$_pkgname-$pkgver-2"

  python2 setup.py install --root="$pkgdir" --prefix=/usr -O1 --skip-build
}

package_python-gensim() {
  depends=('python' 'python-numpy' 'python-scipy' 'python-smart-open')

  cd "$_pkgname-$pkgver"

  python setup.py install --root="$pkgdir" --prefix=/usr -O1 --skip-build
}

