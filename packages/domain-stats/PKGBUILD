# This file is part of BlackArch Linux ( https://www.blackarch.org/ ).
# See COPYING for license details.

pkgname=domain-stats
pkgver=28.033375f
pkgrel=1
pkgdesc='A web API to deliver domain information from whois and alexa.'
groups=('blackarch' 'blackarch-recon')
arch=('any')
url='https://github.com/MarkBaggett/domain_stats'
license=('custom:unknown')
depends=('python2' 'python2-python-whois' 'python2-argparse')
makedepends=('git')
source=('git+https://github.com/MarkBaggett/domain_stats.git')
sha512sums=('SKIP')

pkgver() {
  cd domain_stats

  echo $(git rev-list --count HEAD).$(git rev-parse --short HEAD)
}

package() {
  cd domain_stats

  install -dm 755 "$pkgdir/usr/bin"
  install -dm 755 "$pkgdir/usr/share/$pkgname"

  install -Dm644 README.md "$pkgdir/usr/share/doc/$pkgname/README.md"

  rm README.md

  cp -a * "$pkgdir/usr/share/$pkgname"

  cat > "$pkgdir/usr/bin/$pkgname" << EOF
#!/bin/sh
cd /usr/share/$pkgname
exec python2 domain_stats.py "\$@"
EOF

  chmod +x "$pkgdir/usr/bin/$pkgname"
}

