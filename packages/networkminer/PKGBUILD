# This file is part of BlackArch Linux ( https://www.blackarch.org/ ).
# See COPYING for license details.

pkgname=networkminer
pkgver=2.4
pkgrel=1
pkgdesc='A Network Forensic Analysis Tool for advanced Network Traffic Analysis, sniffer and packet analyzer.'
arch=('any')
groups=('blackarch' 'blackarch-forensic' 'blackarch-sniffer')
url='http://www.netresec.com/'
license=('GPL2')
depends=('mono' 'xterm')
_srcname="NetworkMiner_${pkgver//\./-}"
source=("${_srcname}.zip::https://www.netresec.com/?download=NetworkMiner"
        'networkminer.desktop')
sha512sums=('ddf9497aa49914b9d3c6a195926a9d51a5ec76524b85a4e0503b2224d0880b6c1e0c28f257e584549a8889e0ad8801a3d9108018f01ded584effc55414a97169'
            'd3ca7c995c4c68d9e71f39a5dfb27041256d2796436a73a9be194d5ccf05906c636919891d8c191c34869bdbcd0bfbfbbf04ee759311a52728fc3f4984b8845c')

package() {
  cd "NetworkMiner_${pkgver//\./-}"

  install -dm 755 "$pkgdir/usr/bin"
  install -dm 755 "$pkgdir/usr/share/$pkgname"

  cp -a * "$pkgdir/usr/share/$pkgname/"

  cat > "$pkgdir/usr/bin/$pkgname" << EOF
#!/bin/sh
TERM=xterm mono /usr/share/$pkgname/NetworkMiner.exe
EOF

  install -Dm 644 "$srcdir/$pkgname.desktop" \
    "$pkgdir/usr/share/applications/$pkgname.desktop"

  chmod +x "$pkgdir/usr/bin/$pkgname"
}

