# This file is part of BlackArch Linux ( https://www.blackarch.org/ ).
# See COPYING for license details.

pkgname=networkminer
_pkgname=NetworkMiner
pkgver=2.6
pkgrel=1
pkgdesc='A Network Forensic Analysis Tool for advanced Network Traffic Analysis, sniffer and packet analyzer.'
arch=('any')
groups=('blackarch' 'blackarch-forensic' 'blackarch-sniffer')
url='http://www.netresec.com/'
license=('GPL2')
depends=('mono' 'xterm')
_srcname="NetworkMiner_${pkgver//\./-}"
source=("$pkgname-$pkgver.zip::https://www.netresec.com/?download=NetworkMiner"
        "$pkgname.desktop")
sha512sums=('afd77f3e35dd227eee39e270ba57bfae99e2136832e767e69e9d5bcd39454942b03f68436497080759b56293bde5691fb13c29776b12c650a38910bcd479d17f'
            '7ffc98a7a84453cc86635921d7dd2c97579e9a642a43f9915b6dcfdb7d9ef6f7c779db16369eb5eb3cbc985d37afb84eb1e6c646e2ad5cabf79fdda91e89611a')

package() {
  cd "${_pkgname}_${pkgver//\./-}"

  install -dm 755 "$pkgdir/usr/bin"
  install -dm 755 "$pkgdir/usr/share/$pkgname"

  cp -a * "$pkgdir/usr/share/$pkgname/"

  cat > "$pkgdir/usr/bin/$pkgname" << EOF
#!/bin/sh
TERM=xterm mono /usr/share/$pkgname/NetworkMiner.exe
EOF

  install -Dm 644 "$srcdir/$pkgname.desktop" \
    "$pkgdir/usr/share/applications/$pkgname.desktop"

  chmod +x "$pkgdir/usr/bin/$pkgname"
}

