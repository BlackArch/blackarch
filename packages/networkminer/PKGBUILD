# This file is part of BlackArch Linux ( https://www.blackarch.org/ ).
# See COPYING for license details.

pkgname=networkminer
pkgver=2.4
pkgrel=2
pkgdesc='A Network Forensic Analysis Tool for advanced Network Traffic Analysis, sniffer and packet analyzer.'
arch=('any')
groups=('blackarch' 'blackarch-forensic' 'blackarch-sniffer')
url='http://www.netresec.com/'
license=('GPL2')
depends=('mono' 'xterm')
_srcname="NetworkMiner_${pkgver//\./-}"
source=("${_srcname}.zip::https://www.netresec.com/?download=NetworkMiner"
        'networkminer.desktop')
sha512sums=('ddf9497aa49914b9d3c6a195926a9d51a5ec76524b85a4e0503b2224d0880b6c1e0c28f257e584549a8889e0ad8801a3d9108018f01ded584effc55414a97169'
            '7ffc98a7a84453cc86635921d7dd2c97579e9a642a43f9915b6dcfdb7d9ef6f7c779db16369eb5eb3cbc985d37afb84eb1e6c646e2ad5cabf79fdda91e89611a')

package() {
  cd "NetworkMiner_${pkgver//\./-}"

  install -dm 755 "$pkgdir/usr/bin"
  install -dm 755 "$pkgdir/usr/share/$pkgname"

  cp -a * "$pkgdir/usr/share/$pkgname/"

  cat > "$pkgdir/usr/bin/$pkgname" << EOF
#!/bin/sh
TERM=xterm mono /usr/share/$pkgname/NetworkMiner.exe
EOF

  install -Dm 644 "$srcdir/$pkgname.desktop" \
    "$pkgdir/usr/share/applications/$pkgname.desktop"

  chmod +x "$pkgdir/usr/bin/$pkgname"
}

