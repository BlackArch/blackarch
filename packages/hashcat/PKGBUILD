# This file is part of BlackArch Linux ( http://blackarch.org ).
# See COPYING for license details.

pkgname='hashcat'
pkgver='2.00'
pkgrel=2
groups=('blackarch' 'blackarch-cracker')
pkgdesc='A multithreaded cross platform hash cracker.'
arch=('i686' 'x86_64' 'armv6h' 'armv7h')
url='http://hashcat.net/hashcat/'
license=('custom')
makedepends=('p7zip')
depends=('glibc')
source=("https://hashcat.net/files/hashcat-${pkgver}.7z")
sha1sums=('acd5a6f1020432ea219068b6068f552d2847743a')

prepare() {
  cd "$srcdir/hashcat-$pkgver"

  if [[ "$CARCH" == 'x86_64' ]]; then
    mv hashcat-cli64.bin hashcat
  else
    mv hashcat-cli32.bin hashcat
  fi
}

package() {
  cd "$srcdir/hashcat-$pkgver"

  rm *.{exe,bin,app}

  mkdir -p "$pkgdir/usr/share/doc/hashcat"
  mkdir -p "$pkgdir/etc/hashcat"

  install -Dm755 hashcat "$pkgdir/usr/bin/hashcat"

  cp -a --no-preserve=ownership "$srcdir/hashcat-$pkgver"/{rules,salts,tables} \
    "$pkgdir"/etc/"hashcat"/
  cp -a --no-preserve=ownership "$srcdir/hashcat-$pkgver"/examples \
    "$pkgdir"/usr/share/doc/"hashcat"/

  cd "$pkgdir"

  find . -type d -exec chmod 755 {} \+
  find . -type f -exec chmod 644 {} \+

  chmod a+x "${pkgdir}/usr/bin/hashcat"
}
