pkgname=hashcat
pkgver=0.47
pkgrel=1
groups=('blackarch' 'blackarch-cracker')
pkgdesc="A multithreaded cross platform hash cracker."
arch=('i686' 'x86_64' 'armv6h' 'armv7h')
url='http://hashcat.net/hashcat/'
license=('custom')
makedepends=('p7zip')
depends=('glibc')
source=("https://hashcat.net/files/hashcat-${pkgver}.7z")
md5sums=('ac5d37aac78d1e1400a2d4b0c8ecae68')

prepare() {
  cd "${srcdir}/hashcat-${pkgver}"

  if [[ "$CARCH" == 'x86_64' ]]; then
    mv hashcat-cli64.bin hashcat
  else
    mv hashcat-cli32.bin hashcat
  fi
}

package() {
  cd "${srcdir}/hashcat-${pkgver}"

  rm *.{exe,bin,app}

  install -dm755 "${pkgdir}/usr/share/doc/hashcat"
  install -dm755 "${pkgdir}/etc/hashcat"

  install -Dm755 hashcat "${pkgdir}/usr/bin/hashcat"

  cp -dpr --no-preserve=ownership "${srcdir}/hashcat-${pkgver}"/{rules,salts,tables} "${pkgdir}"/etc/"hashcat"/
  cp -dpr --no-preserve=ownership "${srcdir}/hashcat-${pkgver}"/examples "${pkgdir}"/usr/share/doc/"hashcat"/

  # Fix permissions.
  cd "${pkgdir}"
  find . -type d -exec chmod 755 {} \+
  find . -type f -exec chmod 644 {} \+
  chmod a+x "${pkgdir}/usr/bin/hashcat"
}
