# This file is part of BlackArch Linux ( http://blackarch.org ).
# See COPYING for license details.

pkgname=quickrecon
pkgver=0.3.2
pkgrel=4
groups=('blackarch' 'blackarch-recon' 'blackarch-scanner')
pkgdesc='A python script for simple information gathering. It attempts to find subdomain names, perform zone transfers and gathers emails from Google and Bing.'
arch=('any')
url='http://packetstormsecurity.com/files/104314/QuickRecon-0.3.2.html'
license=('GPL3')
depends=('python2' 'pyqt4-common' 'python2-httplib2' 'python2-sip'
         'python2-html5lib' 'python2-beautifulsoup4')
source=("http://dl.packetstormsecurity.net/UNIX/scanners/quickrecon_gui-${pkgver}.tar.gz")
sha512sums=('8263efc4be4bdeb59f54bdf6d0be508a8bd84d974484b26a6ad665d9dd1ab06ea6fa4bc65c7f7fc9d7ad4667af18f7f85a7c9b7f8a77eaee2b014baa217ede41')

package() {
  cd "$srcdir/quickrecon_gui-$pkgver"

  install -dm 755 "$pkgdir/usr/bin"
  install -dm 755 "$pkgdir/usr/share/quickrecon"

  install -Dm644 README "$pkgdir/usr/share/doc/quickrecon/README"
  install -Dm644 COPYING "$pkgdir/usr/share/licenses/quickrecon/COPYING"

  rm README COPYING

  cp -a * "$pkgdir/usr/share/quickrecon"

  cat > "$pkgdir/usr/bin/quickrecon" << EOF
#!/bin/sh
cd /usr/share/quickrecon
exec python2 quickrecon_gui.pyw "\${@}"
EOF

  chmod +x "$pkgdir/usr/bin/quickrecon"
}
