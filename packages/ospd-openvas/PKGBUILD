# This file is part of BlackArch Linux ( https://www.blackarch.org/ ).
# See COPYING for license details.

pkgname=ospd-openvas
pkgver=22.4.0
pkgrel=2
pkgdesc='is an OSP server implementation to remotely control OpenVAS Scanner and Notus Scanner.'
arch=('any')
groups=('blackarch' 'blackarch-scanner')
url='https://github.com/greenbone/'
license=('AGPL')
depends=('python' 'python-poetry' 'python-installer' 'python-wheel' 'python-packaging' 'python-psutil'
         'python-wrapt' 'python-cffi' 'python-lxml' 'python-defusedxml' 'python-paramiko'
         'python-redis' 'python-paho-mqtt')
makedepends=('python-setuptools')
source=("${pkgname}-${pkgver}.tar.gz::https://github.com/greenbone/ospd-openvas/archive/refs/tags/v$pkgver.tar.gz")
sha512sums=('9ceaca7843c406361aeaa6c15386c150c449c08586382f95074cf346a4ebf7dd57ee97cda2784758845d03852488989ad18963eba0f01c4bb190fafa3480df9b')

build() {
  cd "${pkgname}-${pkgver}"

  poetry build
}

package() {
  cd "${pkgname}-${pkgver}"

  python -m installer --destdir="$pkgdir" dist/*.whl

  install -d "$pkgdir/etc/gvm"
  install -dm 2775 "$pkgdir/run/ospd"
  install -d "$pkgdir/usr/lib/systemd/system"

  install -m 644 "$srcdir/${pkgname}-${pkgver}/config/ospd-openvas.conf" "$pkgdir/etc/gvm"
  install -m 644 "$srcdir/${pkgname}-${pkgver}/config/ospd-openvas.service" "$pkgdir/usr/lib/systemd/system"

  cp -r "$srcdir/${pkgname}-${pkgver}/docs/example-ospd-logging.conf" "$pkgdir/etc/gvm/ospd-logging.conf"
  sed -i 's/^ExecStart=\/usr\/bin\/ospd-openvas --config \/etc\/gvm\/ospd-openvas.conf --log-config \/etc\/gvm\/ospd-logging.conf/ExecStart=\/usr\/bin\/ospd-openvas --unix-socket \/run\/ospd\/ospd-openvas.sock --pid-file \/run\/ospd\/ospd-openvas.pid --log-file \/var\/log\/gvm\/ospd-openvas.log --lock-file-dir \/var\/lib\/openvas --socket-mode 0o770 --mqtt-broker-address localhost --mqtt-broker-port 1883 --notus-feed-dir \/var\/lib\/notus\/advisories/' "$pkgdir/usr/lib/systemd/system/ospd-openvas.service"
}
