# This file is part of BlackArch Linux ( http://blackarch.org ).
# See COPYING for license details.

pkgname='chameleonmini'
pkgver=157.a9ab6c0
pkgrel=1
pkgdesc='Official repository of ChameleonMini, a freely programmable, portable tool for NFC security analysis that can emulate and clone contactless cards, read RFID tags and sniff/log RF data.'
groups=('blackarch' 'blackarch-social' 'blackarch-hardware' 'blackarch-nfc')
arch=('x86_64' 'armv6h' 'armv7h' 'aarch64')
url='https://github.com/emsec/ChameleonMini'
license=('custom:unknown')
depends=('python' 'python-argparse' 'python-colorama' 'python-pyserial')
makedepends=('git' 'avr-gcc' 'avr-libc')
source=('git+https://github.com/emsec/ChameleonMini.git')
install='chameleonmini.install'
sha1sums=('SKIP')

pkgver() {
  cd "$srcdir/ChameleonMini"

  echo $(git rev-list --count HEAD).$(git rev-parse --short HEAD)
}

build() {
  cd "$srcdir/ChameleonMini"

  # build firmware
  cd Firmware/Chameleon-Mini

  make
}

package() {
  cd "$srcdir/ChameleonMini"

  mkdir -p "$pkgdir/usr/bin"
  mkdir -p "$pkgdir/usr/share/chameleonmini"

  install -Dm644 -t "$pkgdir/usr/share/doc/chameleonmini/" README.md
  install -Dm644 LICENSE.txt \
    "$pkgdir/usr/share/licenses/chameleonmini/LICENSE.txt"

  rm README.md LICENSE.txt

  cp -a * "$pkgdir/usr/share/chameleonmini/"

  cat > "$pkgdir/usr/bin/chamtool" << EOF
#!/bin/sh
cd /usr/share/chameleonmini/Software
exec python chamtool.py "\${@}"
EOF

  chmod a+x "$pkgdir/usr/bin/chamtool"

  cat > "$pkgdir/usr/bin/chamlog" << EOF
#!/bin/sh
cd /usr/share/chameleonmini/Software
exec python chamlog.py "\${@}"
EOF

  chmod a+x "$pkgdir/usr/bin/chamlog"
}
