# This file is part of BlackArch Linux ( https://www.blackarch.org/ ).
# See COPYING for license details.

pkgname=zarp
pkgver=305.02eebd4
pkgrel=1
groups=('blackarch' 'blackarch-exploitation' 'blackarch-networking')
pkgdesc='A network attack tool centered around the exploitation of local networks.'
url='https://defense.ballastsecurity.net/wiki/index.php/Zarp'
arch=('any')
license=('GPL')
depends=('python2' 'python2-paramiko'  'python2-pyasn1' 'python2-flask'
         'mitmproxy')
optdepends=('aircrack-ng: aiding wireless attacks')
makedepends=('git')
source=('git+https://github.com/hatRiot/zarp.git')
sha512sums=('SKIP')

pkgver() {
  cd $pkgname

  echo $(git rev-list --count HEAD).$(git rev-parse --short HEAD)
}

package() {
  cd "$pkgname"

  install -dm 755 "$pkgdir/usr/bin"
  install -dm 755 "$pkgdir/usr/share/$pkgname"

  install -Dm 644 -t "$pkgdir/usr/share/doc/$pkgname/" CONTRIBUTORS README.md
  install -Dm 644 LICENSE "$pkgdir/usr/share/licenses/$pkgname/LICENSE"

  rm CONTRIBUTORS LICENSE README.md

  cp -a * "$pkgdir/usr/share/$pkgname/"

  cat > "$pkgdir/usr/bin/$pkgname" << EOF
#!/bin/sh
cd /usr/share/$pkgname
exec python2 $pkgname.py "\$@"
EOF

  chmod +x "$pkgdir/usr/bin/$pkgname"
}

