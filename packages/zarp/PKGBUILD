# This file is part of BlackArch Linux ( http://blackarch.org ).
# See COPYING for license details.

pkgname='zarp'
pkgver='0.1.8'
pkgrel=6
groups=('blackarch' 'blackarch-exploitation' 'blackarch-networking')
pkgdesc='A network attack tool centered around the exploitation of local networks.'
url='https://defense.ballastsecurity.net/wiki/index.php/Zarp'
arch=('any')
license=('GPL')
depends=('python2' 'python2-paramiko'  'python2-pyasn1' 'python2-flask'
         'mitmproxy')
optdepends=('aircrack-ng: aiding wireless attacks')
makedepends=('git')
source=("https://github.com/hatRiot/zarp/archive/${pkgver}.tar.gz")
sha1sums=('5e07d43b9e973f3e397e336e4de23c8f1430b092')

package() {
  cd "$srcdir/zarp-$pkgver"

  mkdir -p "$pkgdir/usr/bin"
  mkdir -p "$pkgdir/usr/share/zarp"

  install -Dm644 -t "$pkgdir/usr/share/doc/zarp/" CONTRIBUTORS README.md
  install -Dm644 LICENSE "$pkgdir/usr/share/licenses/zarp/LICENSE"

  rm CONTRIBUTORS LICENSE README.md

  cp -a * "$pkgdir/usr/share/zarp"

  cat > "$pkgdir/usr/bin/zarp" << EOF
#!/bin/sh
cd /usr/share/zarp
exec python2 zarp.py "\$@"
EOF

  chmod +x "$pkgdir/usr/bin/zarp"
}
