pkgname=mdk3
pkgver=6
pkgrel=4
epoch=2
groups=('blackarch' 'blackarch-fuzzer' 'blackarch-wireless')
pkgdesc="WLAN penetration tool"
arch=('i686' 'x86_64' 'armv6h' 'armv7h')
license=('GPL')
url='http://homepages.tu-darmstadt.de/~p_larbig/wlan/'
makedepends=('sed')
source=("http://homepages.tu-darmstadt.de/~p_larbig/wlan/mdk3-v$pkgver.tar.bz2")
md5sums=('67705a814ded2a2e6f70522ca0dc6da9')

prepare() {
  cd "$srcdir/mdk3-v$pkgver"
  sed -i -e "s:/usr/local:/usr:" Makefile
  sed -i "s|-g -O3|$CFLAGS|g" Makefile
  sed -i "s|/sbin|/bin|" Makefile
}

build() {
  cd "$srcdir/mdk3-v$pkgver"
  make -C osdep
  make
}

package() {
  cd "$srcdir/mdk3-v$pkgver"
  make DESTDIR="$pkgdir" install
}
