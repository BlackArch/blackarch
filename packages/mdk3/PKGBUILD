# This file is part of BlackArch Linux ( http://blackarch.org ).
# See COPYING for license details.

pkgname='mdk3'
pkgver='6'
pkgrel=4
epoch=2
groups=('blackarch' 'blackarch-fuzzer' 'blackarch-wireless')
pkgdesc="WLAN penetration tool"
arch=('x86_64' 'armv6h' 'armv7h' 'aarch64')
license=('GPL')
url='http://homepages.tu-darmstadt.de/~p_larbig/wlan/'
makedepends=('sed')
source=("http://homepages.tu-darmstadt.de/~p_larbig/wlan/mdk3-v${pkgver}.tar.bz2")
sha1sums=('2832d176c02d9c3eb3bcb3b35acfdc94793161dd')

prepare() {
  cd "$srcdir/mdk3-v$pkgver"

  sed -i -e "s:/usr/local:/usr:" Makefile
  sed -i "s|-g -O3|$CFLAGS|g" Makefile
  sed -i "s|/sbin|/bin|" Makefile
}

build() {
  cd "$srcdir/mdk3-v$pkgver"

  make -C osdep

  make
}

package() {
  cd "$srcdir/mdk3-v$pkgver"

  make DESTDIR="$pkgdir" install
}
