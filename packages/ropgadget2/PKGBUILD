# This file is part of BlackArch Linux ( https://www.blackarch.org/ ).
# See COPYING for license details.

pkgname=ropgadget2
_pkgname=ROPgadget
pkgver=5.7
pkgrel=1
pkgdesc='Search gadgets in binaries to facilitate ROP exploitation for several file formats and architectures'
groups=('blackarch' 'blackarch-exploitation')
url='http://www.shell-storm.org/project/ROPgadget'
arch=('any')
license=('GPL2')
depends=('python2-capstone')
makedepends=('python2-setuptools')
source=("https://github.com/JonathanSalwan/$_pkgname/archive/v$pkgver.tar.gz")
sha512sums=('ec7036395dd97327fbfc46df0f6d97aaf074be903caaae36757f5e02510b7bcf15c3b055caac4ee6b7c88c5c4efcebb80ec94b14d069c5743fd5b116e32acd85')

prepare() {
  cd "$_pkgname-$pkgver"

  sed 's|python2|python|g' -i ropgadget/**/*.py ropgadget/*/*/*.py
}

build() {
  cd "$_pkgname-$pkgver"

  python2 setup.py build
}

check() {
  cd "$_pkgname-$pkgver"

  ./ROPgadget.py --binary ./test-suite-binaries/elf-Linux-x86 --string "main"||:
}

package() {
  cd "$_pkgname-$pkgver"

  python2 setup.py install -O1 --root="$pkgdir" --prefix=/usr --skip-build

  install -Dm 644 README.md -t "$pkgdir/usr/share/doc/$pkgname"

  mv "$pkgdir/usr/bin/ROPgadget" "$pkgdir/usr/bin/ROPgadget2"
}

