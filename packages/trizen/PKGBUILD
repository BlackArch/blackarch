# This file is part of BlackArch Linux ( http://blackarch.org ).
# See COPYING for license details.

pkgname=trizen
pkgver=1.52
pkgrel=1
pkgdesc='Trizen AUR Package Manager: A lightweight wrapper for AUR.'
arch=('any')
groups=('blackarch' 'blackarch-misc')
url='https://github.com/trizen/trizen'
license=('GPL3')
depends=('git' 'pacutils' 'perl>=5.20.0' 'perl-libwww' 'perl-term-ui'
         'pacman' 'perl-json' 'perl-data-dump' 'perl-lwp-protocol-https'
         'perl-term-readline-gnu' 'perl-json-xs')
source=("https://github.com/trizen/trizen/archive/$pkgver.tar.gz")
sha512sums=('6e81d47c3fb7dba763cfa4714881a13c6f936cc22867547f498af4918a4482d38ab09423345900cb0cfcda95259cd67dfe18e05d68d30184c5abb24610a17ccd')

package() {
  cd "trizen-$pkgver"

  install -Dm755 trizen "$pkgdir/usr/bin/trizen"
  install -Dm644 "zsh.completion" "$pkgdir/usr/share/zsh/site-functions/_trizen"
  install -Dm644 "bash.completion" \
    "$pkgdir/usr/share/bash-completion/completions/trizen"
  install -Dm644 "fish.completion" \
    "$pkgdir/usr/share/fish/vendor_completions.d/trizen.fish"
  install -Dm644 -t "$pkgdir/usr/share/doc/trizen/" README.md
}

