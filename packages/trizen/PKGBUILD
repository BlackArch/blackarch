# This file is part of BlackArch Linux ( http://blackarch.org ).
# See COPYING for license details.
# removed from AUR - credits: Trizen.

pkgname='trizen'
pkgver='1.49'
pkgrel=2
pkgdesc='Trizen AUR Package Manager: A lightweight wrapper for AUR.'
groups=('blackarch' 'blackarch-misc')
arch=('any')
url='https://github.com/trizen/trizen'
license=('GPL3')
depends=('git' 'pacutils' 'perl>=5.20.0' 'perl-libwww' 'perl-term-ui'
         'pacman' 'perl-json' 'perl-data-dump' 'perl-lwp-protocol-https'
         'perl-term-readline-gnu' 'perl-json-xs')
source=("trizen-${pkgver}.tar.gz::https://github.com/trizen/trizen/archive/${pkgver}.tar.gz")
sha1sums=('84cd4caec8682195a73148c80a7500998539a1f4')

package() {
  cd "trizen-$pkgver"

  install -Dm755 trizen "$pkgdir/usr/bin/trizen"
  install -Dm644 "zsh.completion" "$pkgdir/usr/share/zsh/site-functions/_trizen"
  install -Dm644 "bash.completion" \
    "$pkgdir/usr/share/bash-completion/completions/trizen"
  install -Dm644 "fish.completion" \
    "$pkgdir/usr/share/fish/vendor_completions.d/trizen.fish"
  install -Dm644 -t "$pkgdir/usr/share/doc/trizen/" README.md
}
