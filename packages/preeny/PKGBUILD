# This file is part of BlackArch Linux ( https://www.blackarch.org/ ).
# See COPYING for license details.

pkgname=preeny
pkgver=99.4f6c0aa
pkgrel=1
pkgdesc='Some helpful preload libraries for pwning stuff.'
groups=('blackarch' 'blackarch-exploitation' 'blackarch-reversing')
arch=('any')
url='https://github.com/zardus/preeny'
license=('BSD')
depends=('ding-libs' 'libseccomp')
makedepends=('cmake' 'ding-libs' 'git' 'libseccomp' 'make')
source=('git+https://github.com/zardus/preeny.git')
sha512sums=('SKIP')

pkgver() {
  cd $pkgname

  echo $(git rev-list --count HEAD).$(git rev-parse --short HEAD)
}

build() {
  cd $pkgname

  mkdir -p build
  cd build
  cmake ../ -Bbuild -DCMAKE_INSTALL_PREFIX=/usr
  make
}

package() {
  cd $pkgname

  install -dm 755 "$pkgdir/usr/share/$pkgname"

  install -Dm 644 -t "$pkgdir/usr/share/doc/$pkgname/" README.md
  install -Dm 644 LICENSE "$pkgdir/usr/share/licenses/$pkgname/LICENSE"

  cp -a --no-preserve=ownership build/lib/* "$pkgdir/usr/share/$pkgname/"
}

