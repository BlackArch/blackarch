# This file is part of BlackArch Linux ( https://www.blackarch.org/ ).
# See COPYING for license details.

pkgbase=python-py-radix
pkgname=('python2-py-radix' 'python-py-radix')
_pkgname=py-radix
pkgver=1.1.0
pkgrel=2
pkgdesc='Radix tree implementation.'
arch=('x86_64' 'aarch64')
url='https://pypi.org/project/py-radix/#files'
license=('BSD')
makedepends=('python2-setuptools' 'python-setuptools')
source=("https://files.pythonhosted.org/packages/source/${_pkgname::1}/$_pkgname/$_pkgname-$pkgver.tar.gz")
sha512sums=('a64cbb975bcfcff6004789618e958cf6d4eef0303e77a41b823e30062f84727c11744c45e73ef6dd39659c0b5b69bf8833c9ee0698be87603dbdcebc7f90f291')

prepare() {
  cp -a "$_pkgname-$pkgver"{,-2}
}

build() {
  cd "$_pkgname-$pkgver-2"

  python2 setup.py build

  cd "$srcdir/$_pkgname-$pkgver"

  python setup.py build
}

package_python2-py-radix() {
  depends=('python2')

  cd "$_pkgname-$pkgver-2"

  python2 setup.py install --prefix=/usr --root="$pkgdir" --optimize=1 \
    --skip-build
}

package_python-py-radix() {
  depends=('python')

  cd "$_pkgname-$pkgver"

  python setup.py install --prefix=/usr --root="$pkgdir" --optimize=1 \
    --skip-build
}

