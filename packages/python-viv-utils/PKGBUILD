# This file is part of BlackArch Linux ( https://www.blackarch.org/ ).
# See COPYING for license details.

pkgname=python-viv-utils
_pkgname=viv_utils
pkgver=0.7.9
pkgrel=1
pkgdesc='Utilities for binary analysis using vivisect.'
arch=('any')
# https://github.com/williballenthin/viv-utils
url='https://pypi.org/project/viv-utils/#files'
license=('Apache')
depends=('python' 'python-funcy' 'python-pefile' 'vivisect'
         'python-intervaltree' 'python-python-flirt')
         # 'python-argparse'
makedepends=('python-setuptools')
source=("https://files.pythonhosted.org/packages/source/${_pkgname::1}/$_pkgname/viv_utils-$pkgver.tar.gz")
sha512sums=('e5353fd8d1e28b06e3808b96c7b029a4291344d3c240a6a81f98b7a471e3e5f3ce6196b7c0e252cdeb2cf3ce782b830e764e48379aab638edaa9c836d0e6a810')

prepare() {
  cd "$_pkgname-$pkgver"

  sed -i 's/==/>=/' setup.py
}

build() {
  cd "$_pkgname-$pkgver"

  python setup.py build
}

package() {
  cd "$_pkgname-$pkgver"

  python setup.py install --root="$pkgdir" --prefix=/usr -O1 --skip-build
}

