# This file is part of BlackArch Linux ( https://www.blackarch.org/ ).
# See COPYING for license details.

pkgname=python-viv-utils
_pkgname=viv-utils
pkgver=0.7.9
pkgrel=2
pkgdesc='Utilities for binary analysis using vivisect.'
arch=('any')
url='https://github.com/williballenthin/viv-utils'
license=('Apache')
depends=('python' 'python-funcy' 'python-pefile' 'vivisect'
         'python-intervaltree' 'python-python-flirt' 'python-typing_extensions')
makedepends=('python-setuptools')
source=("https://github.com/williballenthin/viv-utils/archive/refs/tags/v$pkgver.tar.gz")
sha512sums=('fc7fbbfc9b73ece010b54f71d3294c4c7e4c34cae2180a0f8759cd8b3dfd957a228cdd03f30e13d61a2f52d5ad2fd66737b134d150a36b7ead4d6de2374739b7')

prepare() {
  cd "$_pkgname-$pkgver"

  sed -i 's/==/>=/' setup.py
}

build() {
  cd "$_pkgname-$pkgver"

  python setup.py build
}

package() {
  cd "$_pkgname-$pkgver"

  python setup.py install --root="$pkgdir" --prefix=/usr -O1 --skip-build
}

