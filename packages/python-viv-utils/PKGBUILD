# This file is part of BlackArch Linux ( https://www.blackarch.org/ ).
# See COPYING for license details.

pkgname=python-viv-utils
_pkgname=viv-utils
pkgver=0.7.11
pkgrel=1
pkgdesc='Utilities for binary analysis using vivisect.'
arch=('any')
url='https://github.com/williballenthin/viv-utils'
license=('Apache')
depends=('python' 'python-funcy' 'python-pefile' 'vivisect'
         'python-intervaltree' 'python-python-flirt' 'python-typing_extensions')
makedepends=('python-setuptools')
source=("https://github.com/williballenthin/viv-utils/archive/refs/tags/v$pkgver.tar.gz")
sha512sums=('324e5d404aaec83303d07f36ced69ffb7e3d2047c5668e164de065b8884bce2968966fe9e94d15bab784199f53bd0f69df93d5cf40534ba1321bc2b1f4aeeea2')

prepare() {
  cd "$_pkgname-$pkgver"

  sed -i 's/==/>=/' setup.py
}

build() {
  cd "$_pkgname-$pkgver"

  python setup.py build
}

package() {
  cd "$_pkgname-$pkgver"

  python setup.py install --root="$pkgdir" --prefix=/usr -O1 --skip-build
}

