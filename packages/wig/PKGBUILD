# This file is part of BlackArch Linux ( http://blackarch.org ).
# See COPYING for license details.

pkgname='wig'
pkgver=103.55de6d8
pkgrel=1
groups=('blackarch' 'blackarch-webapp' 'blackarch-scanner' 'blackarch-recon')
pkgdesc='WebApp Information Gatherer.'
arch=('any')
url='https://github.com/jekyc/wig'
license=('custom:BSD')
depends=('python' 'python-requests')
makedepends=('git')
source=('git+https://github.com/jekyc/wig.git')
sha1sums=('SKIP')

pkgver() {
  cd "$srcdir/wig"

  echo $(git rev-list --count HEAD).$(git rev-parse --short HEAD)
}

package() {
  cd "$srcdir/wig"

  install -dm755 "$pkgdir/usr/bin"
  install -dm755 "$pkgdir/usr/share/wig"

  cp -a * "$pkgdir/usr/share/wig"

  install -Dm644 LICENSE "$pkgdir/usr/share/licenses/wig/LICENSE"

  cat > "$pkgdir/usr/bin/wig" << EOF
#!/bin/sh
exec python /usr/share/wig/wig.py "\$@"
EOF

  chmod +x "$pkgdir/usr/bin/wig"
}
