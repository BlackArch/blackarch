# This file is part of BlackArch Linux ( https://www.blackarch.org/ ).
# See COPYING for license details.

pkgbase=python-tld
pkgname=('python2-tld' 'python-tld')
_pkgname=tld
pkgver=0.9.7
pkgrel=2
pkgdesc='Extract the top level domain (TLD) from the URL given.'
arch=('any')
url='https://pypi.org/project/tld/#files'
license=('MPL' 'LGPL')
makedepends=('python2-setuptools' 'python-setuptools')
source=("https://files.pythonhosted.org/packages/0b/dc/fd0cbad37c06cd626c186c34e028951a3ef08622fa6ce224919196068162/$_pkgname-$pkgver.tar.gz")
sha512sums=('eafa2c02fdd594b607154b1b0dbf90bd02d50f77b6c6e389e498c0f43f1728802de8a6ecd198814ac6626a9cc70c78751052c2f754b3b6fd9a884dbe561456ae')

prepare() {
  cp -a "$_pkgname-$pkgver"{,-2}
}

build() {
  cd "$_pkgname-$pkgver-2"

  python2 setup.py build

  cd "$srcdir/$_pkgname-$pkgver"

  python setup.py build
}

package_python2-tld() {
  depends=('python2' 'python2-six')

  cd "$_pkgname-$pkgver-2"

  python2 setup.py install --prefix=/usr --root="$pkgdir" --optimize=1 \
    --skip-build

  mv "$pkgdir/usr/bin/update-tld-names" "$pkgdir/usr/bin/update-tld-names2"
}

package_python-tld() {
  depends=('python' 'python-six')

  cd "$_pkgname-$pkgver"

  python setup.py install --prefix=/usr --root="$pkgdir" --optimize=1 \
    --skip-build
}

