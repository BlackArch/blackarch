# This file is part of BlackArch Linux ( https://www.blackarch.org/ ).
# See COPYING for license details.

pkgbase=python-tld
pkgname=('python2-tld' 'python-tld')
_pkgname=tld
pkgver=0.12.6
pkgrel=2
pkgdesc='Extract the top level domain (TLD) from the URL given.'
arch=('any')
url='https://pypi.org/project/tld/#files'
license=('MPL' 'LGPL')
makedepends=('python2-setuptools' 'python-setuptools')
source=("https://files.pythonhosted.org/packages/c1/39/68d5ecb596e02d235fc3b0914b1bf0840d8fbc155fa6ff194eee718469d9/$_pkgname-$pkgver.tar.gz")
sha512sums=('64d94092dadf506cc27d22e80114f5c31baea2de5447d17c3c1222aa0f9df64cdc3a9f76bd74333acb0ccec0bf5624e9e080f375024f7a8cf59d2916c0a65a4a')

prepare() {
  cp -a "$_pkgname-$pkgver"{,-2}
}

build() {
  cd "$_pkgname-$pkgver-2"

  python2 setup.py build

  cd "$srcdir/$_pkgname-$pkgver"

  python setup.py build
}

package_python2-tld() {
  depends=('python2' 'python2-six')

  cd "$_pkgname-$pkgver-2"

  python2 setup.py install --prefix=/usr --root="$pkgdir" --optimize=1 \
    --skip-build

  mv "$pkgdir/usr/bin/update-tld-names" "$pkgdir/usr/bin/update-tld-names2"
}

package_python-tld() {
  depends=('python' 'python-six')

  cd "$_pkgname-$pkgver"

  python setup.py install --prefix=/usr --root="$pkgdir" --optimize=1 \
    --skip-build
}

