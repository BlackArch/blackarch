# This file is part of BlackArch Linux ( https://www.blackarch.org/ ).
# See COPYING for license details.

pkgbase=python-tld
pkgname=('python2-tld' 'python-tld')
_pkgname=tld
pkgver=0.12.3
pkgrel=2
pkgdesc='Extract the top level domain (TLD) from the URL given.'
arch=('any')
url='https://pypi.org/project/tld/#files'
license=('MPL' 'LGPL')
makedepends=('python2-setuptools' 'python-setuptools')
source=("https://files.pythonhosted.org/packages/b2/86/ac77ffb3032391a77c6c15c0a06b8ab862481ee83adbf2125421c08cb11b/$_pkgname-$pkgver.tar.gz")
sha512sums=('0acc80f23333900e5018c80a030ff2c570cc16a4bc487cdf2b3ade5d73e6a738791cf79696354efff75531ceed358324ea4d3a5034247e0d9097a34e2b6efba0')

prepare() {
  cp -a "$_pkgname-$pkgver"{,-2}
}

build() {
  cd "$_pkgname-$pkgver-2"

  python2 setup.py build

  cd "$srcdir/$_pkgname-$pkgver"

  python setup.py build
}

package_python2-tld() {
  depends=('python2' 'python2-six')

  cd "$_pkgname-$pkgver-2"

  python2 setup.py install --prefix=/usr --root="$pkgdir" --optimize=1 \
    --skip-build

  mv "$pkgdir/usr/bin/update-tld-names" "$pkgdir/usr/bin/update-tld-names2"
}

package_python-tld() {
  depends=('python' 'python-six')

  cd "$_pkgname-$pkgver"

  python setup.py install --prefix=/usr --root="$pkgdir" --optimize=1 \
    --skip-build
}

