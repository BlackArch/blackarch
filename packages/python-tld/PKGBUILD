# This file is part of BlackArch Linux ( https://www.blackarch.org/ ).
# See COPYING for license details.

pkgbase=python-tld
pkgname=('python2-tld' 'python-tld')
_pkgname=tld
pkgver=0.9.6
pkgrel=1
pkgdesc='Extract the top level domain (TLD) from the URL given.'
arch=('any')
url='https://pypi.org/project/tld/#files'
license=('MPL' 'LGPL')
makedepends=('python2-setuptools' 'python-setuptools')
source=("https://files.pythonhosted.org/packages/43/d0/f8ea7f97df87850d98e1c1dd345d962e122e056f7ecb4018bd027a7c5a20/$_pkgname-$pkgver.tar.gz")
sha512sums=('763eb5b9a60d63ff4790d0d3b9f668825a0779ff4e03e4bdeea1dd0de24e56424295a1dd731cbc76cfc47ad3c0298939e53f691fa3c05aef9978ec5f0e3f0d3f')

prepare() {
  cp -a "$_pkgname-$pkgver"{,-2}
}

build() {
  cd "$_pkgname-$pkgver-2"

  python2 setup.py build

  cd "$srcdir/$_pkgname-$pkgver"

  python setup.py build
}

package_python2-tld() {
  depends=('python2' 'python2-six')

  cd "$_pkgname-$pkgver-2"

  python2 setup.py install --prefix=/usr --root="$pkgdir" --optimize=1 \
    --skip-build

  mv "$pkgdir/usr/bin/update-tld-names" "$pkgdir/usr/bin/update-tld-names2"
}

package_python-tld() {
  depends=('python' 'python-six')

  cd "$_pkgname-$pkgver"

  python setup.py install --prefix=/usr --root="$pkgdir" --optimize=1 \
    --skip-build
}

