# This file is part of BlackArch Linux ( https://www.blackarch.org/ ).
# See COPYING for license details.

pkgname=ipv6toolkit
pkgver=805.babee5e
pkgrel=1
pkgdesc="SI6 Networks' IPv6 Toolkit."
arch=('x86_64' 'aarch64')
groups=('blackarch' 'blackarch-scanner' 'blackarch-recon'
        'blackarch-networking')
url='http://www.si6networks.com/tools/ipv6toolkit/'
license=('GPL')
provides=('ipv6-toolkit-git')
conflicts=('ipv6-toolkit-git')
depends=('libpcap' 'perl')
makedepends=('git')
#source=("http://www.si6networks.com/tools/$pkgname/$pkgname-v$pkgver.tar.gz")
source=("git+https://github.com/fgont/$pkgname.git")
sha512sums=('SKIP')

pkgver() {
  cd $pkgname

  echo $(git rev-list --count HEAD).$(git rev-parse --short HEAD)
}

build() {
  cd $pkgname

  make
}

package() {
  cd $pkgname

  install -dm 755 "$pkgdir/etc"

  make PREFIX="/usr" ETCPATH="$pkgdir/etc/$pkgname" \
    SBINPATH="$pkgdir/usr/bin" DESTDIR="$pkgdir/" install
}

