# This file is part of BlackArch Linux ( https://www.blackarch.org/ ).
# See COPYING for license details.

pkgname=lulzbuster
pkgver=1.2.0
pkgrel=1
groups=('blackarch' 'blackarch-webapp' 'blackarch-scanner' 'blackarch-recon')
pkgdesc='A very fast and smart web-dir/file enumeration tool written in C.'
url='http://www.nullsecurity.net/tools/scanner.html'
license=('custom:unknown')
arch=('x86_64' 'aarch64')
depends=('curl')
source=("https://github.com/nullsecuritynet/tools/raw/master/scanner/lulzbuster/release/$pkgname-$pkgver.tar.xz")
sha512sums=('8ae923e059f6f1bfdbb2f8a75fbbbaa193eb09a8338b299dfd2b6904753a2958693f1eb2a4e2e71f78f2244457a4f0e48932281e462742a1ebdf7e351fc3c721')

prepare() {
  cd "$pkgname-$pkgver"

  sed -i 's|/usr/local|/usr|g' inc/opts.h
}

build() {
  cd "$pkgname-$pkgver"

  make clean
  make lulzbuster
}

package() {
  cd "$pkgname-$pkgver"

  make DESTDIR="$pkgdir/usr" install
}

