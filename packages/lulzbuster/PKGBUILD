# This file is part of BlackArch Linux ( https://www.blackarch.org/ ).
# See COPYING for license details.

pkgname=lulzbuster
pkgver=1.3.2
pkgrel=3
groups=('blackarch' 'blackarch-webapp' 'blackarch-scanner' 'blackarch-recon')
pkgdesc='A very fast and smart web directory and file enumeration tool written in C.'
url='http://www.nullsecurity.net/tools/scanner.html'
license=('custom:unknown')
arch=('x86_64' 'aarch64')
depends=('curl')
source=("https://github.com/nullsecuritynet/tools/raw/master/scanner/lulzbuster/release/$pkgname-$pkgver.tar.gz")
sha512sums=('01720e0ee36c5387ba6516912aa1580256474b4a80979feabf6bd05b1b31d69ff072e912b5158811986188152ef2476f58fb0d19f96a3e61c657214a09e41b67')

prepare() {
  cd "$pkgname-$pkgver"

  sed -i 's|/usr/local|/usr|g' inc/opts.h
}

build() {
  cd "$pkgname-$pkgver"

  make clean
  make lulzbuster
}

package() {
  cd "$pkgname-$pkgver"

  make DESTDIR="$pkgdir/usr" install
}

