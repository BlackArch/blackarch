# This file is part of BlackArch Linux ( https://www.blackarch.org/ ).
# See COPYING for license details.

pkgname=python2-libvmdk-python
pkgver=20181227
pkgrel=1
pkgdesc='Library and tools to access the VMware Virtual Disk (VMDK) format.'
groups=('blackarch' 'blackarch-misc')
url='https://github.com/libyal/libvmdk'
arch=('x86_64' 'armv6h' 'armv7h' 'aarch64')
license=('LGPLv3')
depends=('python2')
makedepends=('python2-setuptools' 'gcc')
source=("https://pypi.python.org/packages/source/l/libvmdk-python/libvmdk-python-$pkgver.tar.gz")
sha512sums=('b3814e08157a246b95c5ff1c038bdce458c16a3c889130370bbc2948a94abb77778d3bbe8e8b590cf86307a9c4c079826d08db8403f830277c890161fe83f015')

build() {
  cd "libvmdk-$pkgver"

  python2 setup.py build
}

check(){
  cd "libvmdk-$pkgver/tests"

  chmod +x ./test_runner.sh
  ./test_runner.sh
}

package() {
  cd "libvmdk-$pkgver"

  python2 setup.py install --root="$pkgdir" --optimize=1 --skip-build
}

