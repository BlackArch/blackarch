# This file is part of BlackArch Linux ( https://www.blackarch.org/ ).
# See COPYING for license details.

pkgname=wafw00f
pkgver=806.09a61dd
pkgrel=1
groups=('blackarch' 'blackarch-scanner' 'blackarch-webapp')
pkgdesc='Identify and fingerprint Web Application Firewall (WAF) products protecting a website.'
arch=('any')
url='https://github.com/EnableSecurity/wafw00f'
license=('GPL')
depends=('python2' 'python2-pluginbase' 'python2-httpretty'
         'python2-functools32' 'python2-beautifulsoup4')
makedepends=('git' 'python2-setuptools')
replaces=('waffit')
source=('git+https://github.com/EnableSecurity/wafw00f.git')
sha512sums=('SKIP')

pkgver() {
  cd $pkgname

  echo $(git rev-list --count HEAD).$(git rev-parse --short HEAD)
}

build() {
  cd $pkgname

  python2 setup.py build
}

package() {
  cd $pkgname

  python2 setup.py install --root="$pkgdir" --optimize=1 --skip-build
}

