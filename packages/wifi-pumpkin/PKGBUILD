# This file is part of BlackArch Linux ( https://www.blackarch.org/ ).
# See COPYING for license details.

pkgname=wifi-pumpkin
pkgver=v1.0.0.r2.g3002925
pkgrel=1
epoch=1
groups=('blackarch' 'blackarch-wireless' 'blackarch-dos' 'blackarch-spoof'
        'blackarch-exploitation' 'blackarch-sniffer' 'blackarch-social')
pkgdesc='Framework for Rogue Wi-Fi Access Point Attack.'
arch=('any')
url='https://github.com/P0cL4bs/WiFi-Pumpkin'
license=('MIT')
depends=('python' 'python-netifaces' 'python-netaddr' 'python-dhcplib'
         'python-tabulate' 'python-beautifultable' 'python-urwid'
         'python-termcolor' 'python-twisted' 'python-pyqt5' 'python-pyqt5-sip'
         'python-pyopenssl' 'python-asn1crypto' 'python-beautifulsoup4'
         'python-bs4' 'python-dnslib' 'python-loguru' 'python-isc_dhcp_leases'
         'python-dnspython' 'python-flask' 'python-coverage' 'python-scapy'
         'python-scapy-http' 'dhcp' 'networkmanager' 'responder')
makedepends=('git')
source=("$pkgname::git+https://github.com/P0cL4bs/wifipumpkin3.git")
sha512sums=('SKIP')

pkgver() {
  cd $pkgname

  git describe --long --tags | sed 's/\([^-]*-g\)/r\1/;s/-/./g'
}

build() {
  cd $pkgname

  python setup.py build
}

package() {
  cd $pkgname

  python setup.py install --root="$pkgdir" --prefix=/usr -O1 --skip-build

  mv "$pkgdir/usr/bin/wifipumpkin3" "$pkgdir/usr/bin/$pkgname"

  install -Dm 644 -t "$pkgdir/usr/share/doc/$pkgname/" *.md
  rm "$pkgdir/usr/share/doc/$pkgname/LICENSE.md"
  install -Dm 644 LICENSE.md "$pkgdir/usr/share/licenses/$pkgname/LICENSE"
}

