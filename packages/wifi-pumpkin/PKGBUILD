# This file is part of BlackArch Linux ( https://www.blackarch.org/ ).
# See COPYING for license details.

pkgname=wifi-pumpkin
pkgver=v0.8.7Release.r15.gd99bf1e
pkgrel=1
epoch=1
groups=('blackarch' 'blackarch-wireless' 'blackarch-dos' 'blackarch-spoof'
        'blackarch-exploitation' 'blackarch-sniffer' 'blackarch-social')
pkgdesc='Framework for Rogue Wi-Fi Access Point Attack.'
arch=('any')
url='https://github.com/P0cL4bs/WiFi-Pumpkin'
license=('MIT')
depends=('python2' 'python2-twisted' 'python2-scapy' 'python2-beautifulsoup4'
         'python2-python-nmap' 'python2-netaddr' 'python2-dnspython'
         'python2-config' 'python2-isc_dhcp_leases' 'python2-pyqt4'
         'python2-pcapy' 'python2-lxml' 'ipython2' 'hostapd' 'rfkill' 'php-cgi'
         'dhcp' 'python2-pyopenssl' 'python2-pygtail' 'python2-configparser'
         'python2-netifaces' 'sslstrip' 'driftnet' 'python2-hyperframe'
         'python2-pefile' 'python2-h2' 'capstone' 'python2-capstone'
         'python2-configobj' 'python2-netfilterqueue' 'python2-pip'
         'python2-magic' 'python2-libarchive' 'python2-scapy-http'
         'networkmanager')
makedepends=('git')
source=('git+https://github.com/P0cL4bs/WiFi-Pumpkin.git')
sha512sums=('SKIP')
_dir_name_=WiFi-Pumpkin

pkgver() {
  cd $_dir_name_

  git describe --long --tags | sed 's/\([^-]*-g\)/r\1/;s/-/./g'
}

prepare() {
  cd $_dir_name_

  find . -type f -exec \
    sed -i 's|/usr/share/WiFi-Pumpkin/|/usr/share/wifi-pumpkin/|g' {} \;
}

package() {
  cd $_dir_name_

  install -dm 755 "$pkgdir/usr/bin"
  install -dm 755 "$pkgdir/usr/share/$pkgname"

  install -Dm 644 -t "$pkgdir/usr/share/doc/$pkgname/" README.md CHANGELOG \
    CONTRIBUTING.md
  install -Dm 644 LICENSE "$pkgdir/usr/share/licenses/$pkgname/LICENSE"

  rm -rf README.md CONTRIBUTING.md LICENSE

  cp -a --no-preserve=owner * "$pkgdir/usr/share/$pkgname/"

  cat > "$pkgdir/usr/bin/$pkgname" << EOF
#!/bin/sh
cd /usr/share/$pkgname
exec python2 $pkgname.py "\$@"
EOF

  chmod +x "$pkgdir/usr/bin/$pkgname"
}

