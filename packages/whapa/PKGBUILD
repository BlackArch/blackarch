# This file is part of BlackArch Linux ( http://blackarch.org ).
# See COPYING for license details.

pkgname='whapa'
pkgver=70.35f87fd
pkgrel=1
pkgdesc='WhatsApp Parser Tool.'
groups=('blackarch' 'blackarch-misc')
arch=('any')
url='https://github.com/B16f00t/whapa'
license=('GPL3')
depends=('python2' 'python2-colorama' 'python2-requests' 'python2-argparse'
         'python-pyportify')
makedepends=('git')
source=('git+https://github.com/B16f00t/whapa.git')
sha1sums=('SKIP')

pkgver() {
  cd "$srcdir/whapa"

  echo $(git rev-list --count HEAD).$(git rev-parse --short HEAD)
}

package() {
  cd "$srcdir/whapa"

  mkdir -p "$pkgdir/usr/bin"
  mkdir -p "$pkgdir/usr/share/whapa"

  install -Dm644 -t "$pkgdir/usr/share/doc/whapa/" *.md
  install -Dm644 LICENSE "$pkgdir/usr/share/licenses/whapa/LICENSE"

  rm *.md LICENSE

  cp -a * "$pkgdir/usr/share/whapa/"

  cat > "$pkgdir/usr/bin/whapa" << EOF
#!/bin/sh
cd /usr/share/whapa
exec python2 whapa.py "\${@}"
EOF

  chmod a+x "$pkgdir/usr/bin/whapa"

  cat > "$pkgdir/usr/bin/whagdext" << EOF
#!/bin/sh
cd /usr/share/whapa
exec python whagdext.py "\${@}"
EOF

  chmod a+x "$pkgdir/usr/bin/whagdext"
}
