# This file is part of BlackArch Linux ( http://blackarch.org ).
# See COPYING for license details.

pkgname=whapa
pkgver=108.c948344
pkgrel=2
pkgdesc='WhatsApp Parser Tool.'
groups=('blackarch' 'blackarch-misc')
arch=('any')
url='https://github.com/B16f00t/whapa'
license=('GPL3')
depends=('python2' 'python2-colorama' 'python2-requests' 'python2-argparse'
         'python-pyportify')
makedepends=('git')
source=('git+https://github.com/B16f00t/whapa.git')
sha512sums=('SKIP')

pkgver() {
  cd "$srcdir/whapa"

  echo $(git rev-list --count HEAD).$(git rev-parse --short HEAD)
}

package() {
  cd "$srcdir/whapa"

  install -dm 755 "$pkgdir/usr/bin"
  install -dm 755 "$pkgdir/usr/share/whapa"

  install -Dm644 -t "$pkgdir/usr/share/doc/whapa/" *.md doc/*.md
  install -Dm644 doc/LICENSE "$pkgdir/usr/share/licenses/whapa/LICENSE"

  rm -rf *.md LICENSE doc

  cp -a * "$pkgdir/usr/share/whapa/"

  cat > "$pkgdir/usr/bin/whapa" << EOF
#!/bin/sh
cd /usr/share/whapa
exec python2 whapa.py "\${@}"
EOF

  chmod a+x "$pkgdir/usr/bin/whapa"

  cat > "$pkgdir/usr/bin/whagdext" << EOF
#!/bin/sh
cd /usr/share/whapa
exec python whagdext.py "\${@}"
EOF

  chmod a+x "$pkgdir/usr/bin/whagdext"
}
