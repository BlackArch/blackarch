# This file is part of BlackArch Linux ( https://www.blackarch.org/ ).
# See COPYING for license details.

pkgname=python-pyexcel
_pkgname=pyexcel
pkgver=0.6.2
pkgrel=1
pkgdesc='A wrapper library that provides one API to read, manipulate and writedata in different excel formats.'
arch=('any')
url='https://pypi.org/project/pyexcel/#files'
license=('BSD')
depends=('python' 'python-texttable' 'python-lml' 'python-pyexcel-io'
         'python-lxml')
makedepends=('python-setuptools')
source=("https://files.pythonhosted.org/packages/be/8b/a5aaf3e8eb608177d3089d27b95213389d688e2ca1318b302903be88fc85/$_pkgname-$pkgver.tar.gz")
sha512sums=('7843c673bfa22e7d8d07b0b3005c801a3e8458cb89eec44c17c1f21dd271d154fca7b1f9bb5e73749be6a150019c56c83daf84dba5195f1f4ae45d3422def3f6')

prepare() {
  cd "$_pkgname-$pkgver"

  sed -i 's|lml==0.0.1|lml|g' setup.py
}

build() {
  cd "$_pkgname-$pkgver"

  python setup.py build
}

package() {
  cd "$_pkgname-$pkgver"

  python setup.py install --root="$pkgdir" --prefix=/usr -O1 --skip-build
}

