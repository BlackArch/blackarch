# This file is part of BlackArch Linux ( https://www.blackarch.org/ ).
# See COPYING for license details.

pkgname=python-pyexcel
_pkgname=pyexcel
pkgver=0.5.15
pkgrel=1
pkgdesc='A wrapper library that provides one API to read, manipulate and writedata in different excel formats.'
arch=('any')
url='https://pypi.org/project/pyexcel/#files'
license=('BSD')
depends=('python' 'python-texttable' 'python-lml' 'python-pyexcel-io'
         'python-lxml')
makedepends=('python-setuptools')
source=("https://files.pythonhosted.org/packages/0a/c9/a24503252336009afba9fb60f782c99f27255e76963289ace9086fb06f2a/$_pkgname-$pkgver.tar.gz")
sha512sums=('4212f7d87b08dd80d165b5c6dc5f6767312f24537acb079fbd1d20967de1abd6aba43b118cf88b819afa0881f5966fb36525ad2f31c1bbda5dbe21d29342ccc2')

prepare() {
  cd "$_pkgname-$pkgver"

  sed -i 's|lml==0.0.1|lml|g' setup.py
}

build() {
  cd "$_pkgname-$pkgver"

  python setup.py build
}

package() {
  cd "$_pkgname-$pkgver"

  python setup.py install --root="$pkgdir" --prefix=/usr -O1 --skip-build
}

