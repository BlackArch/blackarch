# This file is part of BlackArch Linux ( https://www.blackarch.org/ ).
# See COPYING for license details.

pkgname=python-pyexcel
_pkgname=pyexcel
pkgver=0.6.1
pkgrel=1
pkgdesc='A wrapper library that provides one API to read, manipulate and writedata in different excel formats.'
arch=('any')
url='https://pypi.org/project/pyexcel/#files'
license=('BSD')
depends=('python' 'python-texttable' 'python-lml' 'python-pyexcel-io'
         'python-lxml')
makedepends=('python-setuptools')
source=("https://files.pythonhosted.org/packages/c6/5d/abb8d5f9b48ea756093c17a5b32c1d48ab89c5683275865bcfba7f654f58/$_pkgname-$pkgver.tar.gz")
sha512sums=('daeb0bcc1fdd055cd2988afbf6d2c60dbadb73c605beba38a96aca1ad0852c2d96be026a0e563fa96498150f44a92d83f50a84c566a7d00ac77d578cd5357265')

prepare() {
  cd "$_pkgname-$pkgver"

  sed -i 's|lml==0.0.1|lml|g' setup.py
}

build() {
  cd "$_pkgname-$pkgver"

  python setup.py build
}

package() {
  cd "$_pkgname-$pkgver"

  python setup.py install --root="$pkgdir" --prefix=/usr -O1 --skip-build
}

