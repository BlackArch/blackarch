# This file is part of BlackArch Linux ( https://www.blackarch.org/ ).
# See COPYING for license details.

pkgname=python-pyexcel
_pkgname=pyexcel
pkgver=0.7.1
pkgrel=1
pkgdesc='A wrapper library that provides one API to read, manipulate and writedata in different excel formats.'
arch=('any')
url='https://pypi.org/project/pyexcel/#files'
license=('BSD')
depends=('python' 'python-texttable' 'python-lml' 'python-pyexcel-io'
         'python-lxml')
makedepends=('python-setuptools')
source=("https://files.pythonhosted.org/packages/source/${_pkgname::1}/$_pkgname/$_pkgname-$pkgver.tar.gz")
sha512sums=('597b40eb9fc1c307c71aae17fee889ff88e4aef2bed5f6502b25aa2ae9d8cf1cfb3abedb6042d72942926ca75c87e237f6a9316d4e5230908b108ef0c1c1ef68')

prepare() {
  cd "$_pkgname-$pkgver"

  sed -i 's|lml==0.0.1|lml|g' setup.py
}

build() {
  cd "$_pkgname-$pkgver"

  python setup.py build
}

package() {
  cd "$_pkgname-$pkgver"

  python setup.py install --root="$pkgdir" --prefix=/usr -O1 --skip-build
}

