# This file is part of BlackArch Linux ( https://www.blackarch.org/ ).
# See COPYING for license details.

pkgname=python-pyexcel
_pkgname=pyexcel
pkgver=0.6.7
pkgrel=2
pkgdesc='A wrapper library that provides one API to read, manipulate and writedata in different excel formats.'
arch=('any')
url='https://pypi.org/project/pyexcel/#files'
license=('BSD')
depends=('python' 'python-texttable' 'python-lml' 'python-pyexcel-io'
         'python-lxml')
makedepends=('python-setuptools')
source=("https://files.pythonhosted.org/packages/16/aa/24b94c55c4d64b45e3081e0799cc2ec3238e04af3d8399c3f18970507eab/$_pkgname-$pkgver.tar.gz")
sha512sums=('342bfc779b911d087a39f2342e950cb46181146d59e9dcaa3dc801769509022f55c213cacf78b1d2bcea3b9e02eeaea48f2b464483a983c4a31b86a46d46d6d5')

prepare() {
  cd "$_pkgname-$pkgver"

  sed -i 's|lml==0.0.1|lml|g' setup.py
}

build() {
  cd "$_pkgname-$pkgver"

  python setup.py build
}

package() {
  cd "$_pkgname-$pkgver"

  python setup.py install --root="$pkgdir" --prefix=/usr -O1 --skip-build
}

