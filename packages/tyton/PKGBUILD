# This file is part of BlackArch Linux ( https://www.blackarch.org/ ).
# See COPYING for license details.

pkgname=tyton
pkgver=1.2
pkgrel=1
pkgdesc='Kernel-Mode Rootkit Hunter.'
arch=('any')
groups=('blackarch' 'blackarch-exploitation')
url='https://github.com/nbulischeck/tyton'
license=('GPL3')
depends=('dkms' 'linux-headers')
makedepends=('gcc' 'make')
source=("https://github.com/nbulischeck/$pkgname/archive/v$pkgver.tar.gz")
sha512sums=('d96690991874611f0f9f5ec9e61fb1187bfaa3370591b3eb5c051e444a5cfabd7d164ca20b4c30f18816093ed905c48631c255f503beb5e9fd9c5cf45f0ed73a')

build() {
  cd $pkgname-$pkgver

  make DESTDIR="$pkgdir"
}

package() {
  cd "$pkgname-$pkgver"

  # Install
  make DESTDIR="$pkgdir" install
  make DESTDIR="$pkgdir" clean

  # Set name and version
  sed -e "s/@_PKGBASE@/$pkgname/" \
  -e "s/@PKGVER@/$pkgver/" \
  -i "packaging/aur/dkms.conf"

  # Copy source files
  INSTALLDIR="$pkgdir/usr/src/$pkgname-$pkgver"
  find . -type f \
  \( -path ./notify \) -prune -o \
  \( -path ./images \) -prune -o \
  \( -path ./.git \) -prune -o \
  -exec install -Dm644 "{}" "$INSTALLDIR/{}" \;

  # Move dkms up
  mv "$INSTALLDIR/packaging/aur/dkms.conf" "$INSTALLDIR/dkms.conf"
}

