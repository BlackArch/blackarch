# This file is part of BlackArch Linux ( https://www.blackarch.org/ ).
# See COPYING for license details.

pkgname=uff
pkgver=46.93fea53
pkgrel=1
pkgdesc='Unleashed ffuf. A fork of ffuf with more functions & a modified HTTP stack.'
arch=('x86_64' 'aarch64')
groups=('blackarch' 'blackarch-fuzzer' 'blackarch-webapp')
url='https://github.com/sw33tLie/uff'
license=('MIT')
depends=('glibc')
makedepends=('git' 'go')
source=("git+https://github.com/sw33tLie/$pkgname.git")
sha512sums=('SKIP')

pkgver() {
  cd $pkgname

  ( set -o pipefail
    git describe --long --tags --abbrev=7 2>/dev/null |
      sed 's/\([^-]*-g\)/r\1/;s/-/./g' ||
    printf "%s.%s" "$(git rev-list --count HEAD)" \
      "$(git rev-parse --short=7 HEAD)"
  )
}

build() {
  cd $pkgname

  GOPATH="$srcdir" go mod download
  GOPATH="$srcdir" go build \
    -trimpath \
    -buildmode=pie \
    -mod=readonly \
    -modcacherw \
    -ldflags "-s -w" \
    -o $pkgname .
}

package() {
  cd $pkgname

  install -Dm 755 $pkgname "$pkgdir/usr/bin/$pkgname"
  install -Dm 644 -t "$pkgdir/usr/share/doc/$pkgname/" *.md
  install -Dm 644 "ffufrc.example" "$pkgdir/usr/share/$pkgname/ffufrc.example"

  cp -a resources/ "$pkgdir/usr/share/doc/$pkgname/"

  install -Dm 644 LICENSE "$pkgdir/usr/share/licenses/$pkgname/LICENSE"
}

