# This file is part of BlackArch Linux ( https://www.blackarch.org/ ).
# See COPYING for license details.

pkgbase=python-artifacts
pkgname=('python2-artifacts' 'python-artifacts')
pkgver=20190320
pkgrel=3
pkgdesc='Digital Forensics Artifact Repository.'
url='https://github.com/ForensicArtifacts/artifacts'
arch=('any')
license=('Apache')
makedepends=('python2-setuptools' 'python-setuptools')
checkdepends=('python-nose')
source=("https://files.pythonhosted.org/packages/04/16/fb48650fbd52e25abbad53ef4190bb04f4f5284ae1ece6a2aa4e9a384a5f/artifacts-20190320.tar.gz")
sha512sums=('b955eb12477b11718eb9348f0aac19b709bc1eb7755e0b1e3bf041952722b79f68008aafc3e9b26f8e1b66cc9fb4f63142910594bffc25b6d374545938e0b41d')

package_python2-artifacts() {
  depends=('python2' 'python2-yaml' 'python2-pyaml' 'python2-mock' 'python2-pbr'
           'python2-six' 'python2-funcsigs')
  cd "artifacts-$pkgver"

  python2 setup.py install --root="$pkgdir" --optimize=1
  mv $pkgdir/usr/share/doc/artifacts{,2}
  mv $pkgdir/usr/share/artifacts{,2}
  mv $pkgdir/usr/bin/validator{,2}.py
  mv $pkgdir/usr/bin/stats{,2}.py
}

package_python-artifacts() {
  depends=('python' 'python-yaml' 'python-pyaml' 'python-mock' 'python-pbr'
           'python-six')
  cd "artifacts-$pkgver"

  python setup.py install --root="$pkgdir" --optimize=1
}

