# This file is part of BlackArch Linux ( https://www.blackarch.org/ ).
# See COPYING for license details.

pkgbase=python-artifacts
pkgname=('python2-artifacts' 'python-artifacts')
pkgver=20190227
pkgrel=1
pkgdesc='Digital Forensics Artifact Repository.'
url='https://github.com/ForensicArtifacts/artifacts'
arch=('any')
license=('Apache')
makedepends=('python2-setuptools' 'python-setuptools')
checkdepends=('python-nose')
source=("https://github.com/ForensicArtifacts/artifacts/archive/$pkgver.tar.gz")
sha512sums=('04338e3be4ca851076a0c17a29720aac7abacde105f6a5a48d624bf71fab838a5073d3cb8b00cd763f52d214322d40811b6731999e62885eac45c870edc3f345')

package_python2-artifacts() {
  depends=('python2' 'python2-yaml' 'python2-pyaml' 'python2-mock' 'python2-pbr'
           'python2-six' 'python2-funcsigs')
  cd "artifacts-$pkgver"

  python2 setup.py install --root="$pkgdir" --optimize=1
}

package_python-artifacts() {
  depends=('python' 'python-yaml' 'python-pyaml' 'python-mock' 'python-pbr'
           'python-six')
  cd "artifacts-$pkgver"

  python setup.py install --root="$pkgdir" --optimize=1
}

