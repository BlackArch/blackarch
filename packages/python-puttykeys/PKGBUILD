# This file is part of BlackArch Linux ( https://www.blackarch.org/ ).
# See COPYING for license details.

pkgbase=python-puttykeys
pkgname=('python2-puttykeys' 'python-puttykeys')
_pkgname=puttykeys
pkgver=1.0.2
pkgrel=6
pkgdesc='Library to convert Putty private keys to OpenSSH format.'
url='https://pypi.org/project/puttykeys/#files'
arch=('any')
license=('MIT')
makedepends=('python2-setuptools' 'python-setuptools')
source=("https://files.pythonhosted.org/packages/29/22/0b6b5f8205251db37c3e3dcdde425e42d3407360f5e99e11f65d83385ece/$_pkgname-$pkgver.tar.gz")
sha512sums=('bd7175b414df2a8fc9366802c5e3ef41ad7fc430bbdd9efe5e9a18ee6d49ff317d2ae1f7cdc43245221a32671889072b0a9ef5b75d2af178d73a6cf33a6e4640')

prepare() {
  cp -a "$_pkgname-$pkgver"{,-2}
}

build() {
  cd "$_pkgname-$pkgver-2"

  python2 setup.py build

  cd "$srcdir/$_pkgname-$pkgver"

  python setup.py build
}

package_python2-puttykeys() {
  depends=('python2' 'python2-cryptography')

  cd "$_pkgname-$pkgver-2"

  python2 setup.py install --prefix=/usr --root="$pkgdir" -O1 --skip-build

  chmod 644 \
    "$pkgdir"/usr/lib/python2.7/site-packages/puttykeys-*-py2.7.egg-info/*
}

package_python-puttykeys() {
  depends=('python' 'python-cryptography')

  cd "$_pkgname-$pkgver"

  python setup.py install --prefix=/usr --root="$pkgdir" -O1 --skip-build

  chmod 644 \
    "$pkgdir"/usr/lib/python3.9/site-packages/puttykeys-*-py3.9.egg-info/*
}

