# This file is part of BlackArch Linux ( http://blackarch.org ).
# See COPYING for license details.

pkgname='snoopbrute'
pkgver=17.589fbe6
pkgrel=2
groups=('blackarch' 'blackarch-scanner' 'blackarch-recon')
pkgdesc='Multithreaded DNS recursive host brute-force tool.'
arch=('any')
url='https://github.com/m57/snoopbrute'
license=('GPL2')
makedepends=('git')
depends=('python2')
source=('git+https://github.com/m57/snoopbrute.git' 'snoopbrute-noptrix.patch')
sha1sums=('SKIP'
          'b22dd8727f14d51142abe4cf955955bb8e29571b')

pkgver() {
  cd "$srcdir/snoopbrute"

  echo $(git rev-list --count HEAD).$(git rev-parse --short HEAD)
}

prepare() {
  cd "$srcdir/snoopbrute"

  sed -i 's|env python|env python2|' snoopboot.py

  patch -p2 < ../snoopbrute-noptrix.patch
}

package() {
  cd "$srcdir/snoopbrute"

  install -Dm755 snoopboot.py "$pkgdir/usr/bin/snoopbrute"
  install -Dm644 README.md "$pkgdir/usr/share/doc/snoopbrute/README.md"
  install -Dm644 LICENSE "$pkgdir/usr/share/licenses/snoopbrute/LICENSE"
  install -Dm644 -t "$pkgdir/usr/share/snoopbrute/" domain_wordlist.txt VERSION
}
