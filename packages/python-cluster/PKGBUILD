# This file is part of BlackArch Linux ( https://www.blackarch.org/ ).
# See COPYING for license details.

pkgbase=python-cluster
pkgname=('python2-cluster' 'python-cluster')
_pkgname=cluster
pkgver=1.4.1.post2
pkgrel=4
pkgdesc='Allows grouping a list of arbitrary objects into related groups (clusters).'
arch=('any')
url='https://github.com/exhuma/python-cluster'
license=('LGPL')
makedepends=('python2-setuptools' 'python-setuptools')
source=("https://files.pythonhosted.org/packages/78/d1/dc7cab54e80f331c923e63ef750859923e622804879b834ebb8f41b77122/$_pkgname-$pkgver.tar.gz")
sha512sums=('ee726f7c0be19fca255ae73514662d91110b81197e5b3f7e445ecf18083a1e35c2f710d63353c920463e8d94758c33fb3fe11d0b9c11500fcf044f47276ed16a')

prepare() {
  cp -a "$_pkgname-$pkgver"{,-2}
}

build() {
  cd "$_pkgname-$pkgver-2"

  python2 setup.py build

  cd "$srcdir/$_pkgname-$pkgver"

  python setup.py build
}

package_python2-cluster() {
  depends=('python2')

  cd "$_pkgname-$pkgver-2"

  python2 setup.py install --prefix=/usr --root="$pkgdir" --optimize=1 \
    --skip-build
}

package_python-cluster() {
  depends=('python')

  cd "$_pkgname-$pkgver"

  python setup.py install --prefix=/usr --root="$pkgdir" --optimize=1 \
    --skip-build
}

