# This file is part of BlackArch Linux ( http://blackarch.org ).
# See COPYING for license details.

pkgname='xf86-video-qxl-git'
_pkgbase='xf86-video-qxl'
pkgver=r541.cbe70e9
pkgrel=2
pkgdesc='Xorg X11 qxl video driver.'
arch=('x86_64' 'armv6h' 'armv7h' 'aarch64')
url="http://www.spice-space.org/"
license=('MIT')
groups=('xorg-video-drivers')
depends=('xorg-server-devel' 'spice-protocol' 'xf86dgaproto' 'xproto'
         'fontsproto' 'randrproto' 'renderproto' 'videoproto' 'resourceproto'
         'scrnsaverproto')
makedepends=('git' 'pkgconfig' 'xorg-server-devel')
conflicts=('xf86-video-qxl')
provides=('xf86-video-qxl')
source=("$_pkgbase::git+git://anongit.freedesktop.org/xorg/driver/xf86-video-qxl#branch=master")
sha1sums=('SKIP')

pkgver() {
  cd "$srcdir/$_pkgbase"

  echo r$(git rev-list --count master).$(git rev-parse --short master)
}

build() {
  cd "$srcdir/$_pkgbase"

  ./autogen.sh --prefix=/usr

  make
}

package() {
  cd "$srcdir/$_pkgbase"

  make DESTDIR="$pkgdir" install
}


