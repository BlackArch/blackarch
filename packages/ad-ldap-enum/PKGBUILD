# This file is part of BlackArch Linux ( https://www.blackarch.org/ ).
# See COPYING for license details.

pkgname=ad-ldap-enum
pkgver=89.5626e8a
pkgrel=1
pkgdesc='An LDAP based Active Directory user and group enumeration tool.'
groups=('blackarch' 'blackarch-recon')
arch=('any')
url='https://github.com/CroweCybersecurity/ad-ldap-enum'
license=('MIT')
depends=('python' 'python-ldap3' 'python-openpyxl')
makedepends=('git' 'dos2unix')
source=("git+https://github.com/CroweCybersecurity/$pkgname.git")
sha512sums=('SKIP')

pkgver() {
  cd $pkgname

  ( set -o pipefail
    git describe --long --tags --abbrev=7 2>/dev/null |
      sed 's/\([^-]*-g\)/r\1/;s/-/./g' ||
    printf "%s.%s" "$(git rev-list --count HEAD)" \
      "$(git rev-parse --short=7 HEAD)"
  )
}

prepare() {
  cd $pkgname

  #sed -i 's|/env python|/env python2|' "$pkgname.py"

  dos2unix "$pkgname.py"
}

package() {
  cd $pkgname

  install -Dm 755 "$pkgname.py" "$pkgdir/usr/bin/$pkgname"
  install -Dm 644 -t "$pkgdir/usr/share/doc/$pkgname/" README.md
  install -Dm 644 LICENSE "$pkgdir/usr/share/licenses/$pkgname/LICENSE"
}

