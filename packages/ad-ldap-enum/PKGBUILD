# This file is part of BlackArch Linux ( http://blackarch.org ).
# See COPYING for license details.

pkgname='ad-ldap-enum'
pkgver=39.41ac756
pkgrel=1
pkgdesc='An LDAP based Active Directory user and group enumeration tool.'
groups=('blackarch' 'blackarch-recon')
arch=('any')
url='https://github.com/CroweCybersecurity/ad-ldap-enum'
license=('MIT')
depends=('python2' 'python2-ldap')
makedepends=('git' 'dos2unix')
source=('git+https://github.com/CroweCybersecurity/ad-ldap-enum.git')
sha1sums=('SKIP')

pkgver() {
  cd "$srcdir/ad-ldap-enum"

  echo $(git rev-list --count HEAD).$(git rev-parse --short HEAD)
}

prepare() {
  cd "$srcdir/ad-ldap-enum"

  sed -i 's|/env python|/env python2|' ad-ldap-enum.py
  dos2unix ad-ldap-enum.py
}

package() {
  cd "$srcdir/ad-ldap-enum"

  install -Dm755 ad-ldap-enum.py "$pkgdir/usr/bin/ad-ldap-enum"
  install -Dm644 -t "$pkgdir/usr/share/doc/ad-ldap-enum/" README.md
  install -Dm644 LICENSE "$pkgdir/usr/share/licenses/ad-ldap-enum/LICENSE"
}
