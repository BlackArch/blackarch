# This file is part of BlackArch Linux ( http://blackarch.org ).
# See COPYING for license details.

pkgname=python2-pyexcel
pkgver=0.5.8
pkgrel=3
pkgdesc='A wrapper library that provides one API to read, manipulate and writedata in different excel formats.'
arch=('any')
url='https://pypi.org/project/pyexcel/#files'
license=('BSD')
depends=('python2' 'python2-texttable' 'python2-lml' 'python2-pyexcel-io'
         'python2-lxml')
makedepends=('python2-setuptools')
source=("https://files.pythonhosted.org/packages/31/bf/703d97c7624b9c4451a5fb3a8b1fe37378317e5ee858f8c7ff68b32720db/pyexcel-${pkgver}.tar.gz")
sha512sums=('081958e21b62cb7885a893133feeb79e5ffa3a07f3d1b052c238bf67cbde81a38b384247536cee912698d7af174f019425d53a51dabbe689db06ce79a0d98505')

build() {
  cd "pyexcel-${pkgver}"

  python2 setup.py build
}

prepare() {
  cd "pyexcel-${pkgver}"

  sed -i 's|lml==0.0.1|lml|g' setup.py
}

package() {
  cd "pyexcel-${pkgver}"

  python2 setup.py install --prefix=/usr --root="$pkgdir" --optimize=1
}

