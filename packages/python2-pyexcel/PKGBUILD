# This file is part of BlackArch Linux ( https://www.blackarch.org/ ).
# See COPYING for license details.

pkgname=python2-pyexcel
pkgver=0.5.13
pkgrel=1
pkgdesc='A wrapper library that provides one API to read, manipulate and writedata in different excel formats.'
arch=('any')
url='https://pypi.org/project/pyexcel/#files'
license=('BSD')
depends=('python2' 'python2-texttable' 'python2-lml' 'python2-pyexcel-io'
         'python2-lxml')
makedepends=('python2-setuptools')
source=("https://files.pythonhosted.org/packages/a6/98/0633c0c434e93280767981fa18a13648f1ada35aac58a2e8004a0d816e93/pyexcel-0.5.13.tar.gz")
sha512sums=('449710a08f7293898cb5a3701484dae63f616fb8e14163858fa911e1014c9a1125ce31e8480381b54405ca854365de211b0376d92cd030b35c0c9ceaed1b97ce')

build() {
  cd "pyexcel-$pkgver"

  python2 setup.py build
}

prepare() {
  cd "pyexcel-$pkgver"

  sed -i 's|lml==0.0.1|lml|g' setup.py
}

package() {
  cd "pyexcel-$pkgver"

  python2 setup.py install --prefix=/usr --root="$pkgdir" --optimize=1 \
    --skip-build
}

