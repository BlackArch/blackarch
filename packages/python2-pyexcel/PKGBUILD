# This file is part of BlackArch Linux ( http://blackarch.org ).
# See COPYING for license details.

pkgname=python2-pyexcel
pkgver=0.5.9.1
pkgrel=3
pkgdesc='A wrapper library that provides one API to read, manipulate and writedata in different excel formats.'
arch=('any')
url='https://pypi.org/project/pyexcel/#files'
license=('BSD')
depends=('python2' 'python2-texttable' 'python2-lml' 'python2-pyexcel-io'
         'python2-lxml')
makedepends=('python2-setuptools')
source=("https://files.pythonhosted.org/packages/5a/65/f7166595bd76e2e89d2d2dcc8866b66b21e755a00fc76af2e9b660243bf8/pyexcel-$pkgver.tar.gz")
sha512sums=('84b34ea0a50d1f24db4cc29923139134ae262688231a454b3fa77c59394b5293844ee95ec7fffdd0a68252a696764a242f768cd7138d435115a76164d67bcc9f')

prepare() {
  cd "pyexcel-$pkgver"

  sed -i 's|lml==0.0.1|lml|g' setup.py
}

build() {
  cd "pyexcel-$pkgver"

  python2 setup.py build
}

package() {
  cd "pyexcel-$pkgver"

  python2 setup.py install --prefix=/usr --root="$pkgdir" --optimize=1 \
    --skip-build
}

