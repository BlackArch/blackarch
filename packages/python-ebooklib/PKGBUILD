# This file is part of BlackArch Linux ( https://www.blackarch.org/ ).
# See COPYING for license details.

pkgname=python-ebooklib
_pkgname=${pkgname#python-}
pkgver=0.20
pkgrel=1
pkgdesc='Ebook library which can handle EPUB2/EPUB3 and Kindle format.'
url='https://pypi.org/project/EbookLib/#files'
arch=('any')
license=('AGPL-3.0-or-later')
depends=('python' 'python-lxml' 'python-six')
makedepends=('python-build' 'python-installer' 'python-setuptools'
             'python-wheel')
options=(!emptydirs)
source=("https://files.pythonhosted.org/packages/source/${_pkgname::1}/$_pkgname/$_pkgname-$pkgver.tar.gz")
sha512sums=('c8ce1a67832fc5ae7c235c79c918d55208a51a10beef81151118323919a86df5b84f9fba5783d7f8f6153f0b8451e3797eb2c63a9a58fa8f388a8dfb986ad246')

build() {
  cd "$_pkgname-$pkgver"

  python -m build --wheel --no-isolation
}

package() {
  cd "$_pkgname-$pkgver"

  python -m installer --destdir="$pkgdir" dist/*.whl
}

