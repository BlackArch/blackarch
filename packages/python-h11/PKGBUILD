# This file is part of BlackArch Linux ( https://www.blackarch.org/ ).
# See COPYING for license details.

pkgbase=python-h11
pkgname=('python2-h11' 'python-h11')
_pkgname=h11
pkgver=0.9.0
pkgrel=1
pkgdesc='A pure-Python, bring-your-own-I/O implementation of HTTP/1.1'
arch=('any')
url='https://pypi.org/project/h11/#files'
license=('MIT')
makedepends=('python2-setuptools' 'python-setuptools')
source=("https://files.pythonhosted.org/packages/34/5a/abaa557d20b210117d8c3e6b0b817ce9b329b2e81f87612e60102a924323/$_pkgname-$pkgver.tar.gz")
sha512sums=('fb323ebf0cc95d89bb7e73cf1f393d2588961ed6dd95630ad692e36f15141ae1de7a0bfe3ef2855f0e9b7c8cf2abba5cf808547bb4bb4d10ebb525122ac63d4a')

prepare() {
  cp -a "$_pkgname-$pkgver"{,-2}
}

build() {
  cd "$_pkgname-$pkgver-2"

  python2 setup.py build

  cd "$srcdir/$_pkgname-$pkgver"

  python setup.py build
}

package_python2-h11() {
  depends=('python2')

  cd "$_pkgname-$pkgver-2"

  python2 setup.py install --prefix=/usr --root="$pkgdir" -O1 --skip-build
}

package_python-h11() {
  depends=('python')

  cd "$_pkgname-$pkgver"

  python setup.py install --prefix=/usr --root="$pkgdir" -O1 --skip-build
}

