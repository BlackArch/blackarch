# This file is part of BlackArch Linux ( https://www.blackarch.org/ ).
# See COPYING for license details.

pkgname=trufflehog
pkgver=162.0d6f2df
pkgrel=2
pkgdesc='Searches through git repositories for high entropy strings, digging deep into commit history.'
groups=('blackarch' 'blackarch-recon')
arch=('any')
url='https://github.com/dxa4481/truffleHog'
license=('GPL2')
depends=('python' 'python-gitpython' 'python-codecov' 'python-pytest-cov'
         'python-trufflehogregexes')
makedepends=('git' 'python-setuptools')
source=("$pkgname::git+https://github.com/dxa4481/truffleHog.git"
        'requirements.txt.patch')
sha512sums=('SKIP'
            'e3bced360034eee84863d618c622840b60be64a8c7c54f8bb941ff5af03b5c061973b24db4f8e0dd5245e9a4e60328e6effa2188c05bfa5cf9bd131b94f7de8b')

pkgver() {
  cd $pkgname

  echo $(git rev-list --count HEAD).$(git rev-parse --short HEAD)
}

prepare() {
  patch -p1 < requirements.txt.patch

  cd $pkgname

  sed -i 's|GitPython == 2.1.1|GitPython|g' setup.py
  sed -i 's|truffleHogRegexes == 0.0.4|truffleHogRegexes|g' setup.py
}

build() {
  cd $pkgname

  python setup.py build
}

package() {
  cd $pkgname

  python setup.py install --root="$pkgdir" --prefix=/usr -O1 --skip-build

  install -Dm 644 -t "$pkgdir/usr/share/doc/$pkgname/" README.md
  install -Dm 644 LICENSE "$pkgdir/usr/share/licenses/$pkgname/LICENSE"
}

