# This file is part of BlackArch Linux ( https://www.blackarch.org/ ).
# See COPYING for license details.

pkgname=trufflehog
pkgver=135.a4c69fa
pkgrel=1
pkgdesc='Searches through git repositories for high entropy strings, digging deep into commit history.'
groups=('blackarch' 'blackarch-recon')
arch=('any')
url='https://github.com/dxa4481/truffleHog'
license=('GPL2')
depends=('python' 'python-gitpython' 'python-codecov' 'python-pytest-cov'
         'python-trufflehogregexes')
makedepends=('git' 'python-setuptools')
source=('git+https://github.com/dxa4481/truffleHog.git'
        'requirements.txt.patch')
sha512sums=('SKIP'
            '23862b93a5bdf75b19befb8839afbff25ac372740db415edf1b9d4bc69ab4f0826f91fe9ee66c62c0191538cfaba49da0b8d95a613a0da1eaeab76692efaf940')

pkgver() {
  cd truffleHog

  echo $(git rev-list --count HEAD).$(git rev-parse --short HEAD)
}

prepare() {
  patch -p1 < requirements.txt.patch

  cd truffleHog

  sed -i 's|GitPython == 2.1.1|GitPython|g' setup.py
  sed -i 's|truffleHogRegexes == 0.0.4|truffleHogRegexes|g' setup.py
}

build() {
  cd truffleHog

  python setup.py build
}

package() {
  cd truffleHog

  python setup.py install --root="$pkgdir" --prefix=/usr --skip-build

  install -Dm 644 -t "$pkgdir/usr/share/doc/$pkgname/" README.md
  install -Dm 644 LICENSE "$pkgdir/usr/share/licenses/$pkgname/LICENSE"
}

