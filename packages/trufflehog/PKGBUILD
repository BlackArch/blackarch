# This file is part of BlackArch Linux ( http://blackarch.org ).
# See COPYING for license details.

pkgname='trufflehog'
pkgver=96.461953f
pkgrel=1
pkgdesc='Searches through git repositories for high entropy strings, digging deep into commit history.'
groups=('blackarch' 'blackarch-recon')
arch=('any')
url='https://github.com/dxa4481/truffleHog'
license=('GPL2')
depends=('python' 'python-gitpython')
makedepends=('git' 'python-setuptools')
source=('git+https://github.com/dxa4481/truffleHog.git')
sha1sums=('SKIP')

pkgver() {
  cd "$srcdir/truffleHog"

  echo $(git rev-list --count HEAD).$(git rev-parse --short HEAD)
}

build() {
  cd "$srcdir/truffleHog"

  python setup.py build
}

package() {
  cd "$srcdir/truffleHog"

  python setup.py install --root="$pkgdir" --prefix=/usr --optimize=1

  install -Dm644 -t "$pkgdir/usr/share/doc/trufflehog/" README.md
  install -Dm644 LICENSE "$pkgdir/usr/share/licenses/trufflehog/LICENSE"
}
