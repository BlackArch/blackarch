# This file is part of BlackArch Linux ( https://www.blackarch.org/ ).
# See COPYING for license details.

pkgname=trufflehog
pkgver=199.0f22322
pkgrel=1
pkgdesc='Searches through git repositories for high entropy strings, digging deep into commit history.'
groups=('blackarch' 'blackarch-recon')
arch=('any')
url='https://github.com/dxa4481/truffleHog'
license=('GPL2')
depends=('python' 'python-gitpython' 'python-codecov' 'python-pytest-cov'
         'python-trufflehogregexes')
makedepends=('git' 'python-setuptools')
source=("$pkgname::git+https://github.com/dxa4481/truffleHog.git"
        'requirements.txt.patch')
sha512sums=('SKIP'
            'd5662684373e5e299e5aada87553abf4b127cad8d1542d9c4a522524929ea455e4772c1c02788b597aa767920b414828d4498f94acf74c01961dae02d9e63ad2')

pkgver() {
  cd $pkgname

  echo $(git rev-list --count HEAD).$(git rev-parse --short HEAD)
}

prepare() {
  patch -p1 < requirements.txt.patch

  cd $pkgname

  sed -i 's|GitPython == 2.1.1|GitPython|g' setup.py
  sed -i 's|truffleHogRegexes == 0.0.4|truffleHogRegexes|g' setup.py
}

build() {
  cd $pkgname

  python setup.py build
}

package() {
  cd $pkgname

  python setup.py install --root="$pkgdir" --prefix=/usr -O1 --skip-build

  install -Dm 644 -t "$pkgdir/usr/share/doc/$pkgname/" README.md
  install -Dm 644 LICENSE "$pkgdir/usr/share/licenses/$pkgname/LICENSE"
}

