# This file is part of BlackArch Linux ( https://www.blackarch.org/ ).
# See COPYING for license details.

pkgname=trufflehog
pkgver=208.59e8bd3
pkgrel=1
pkgdesc='Searches through git repositories for high entropy strings, digging deep into commit history.'
groups=('blackarch' 'blackarch-code-audit' 'blackarch-recon')
arch=('any')
url='https://github.com/dxa4481/truffleHog'
license=('GPL2')
depends=('python' 'python-gitpython' 'python-trufflehogregexes')
makedepends=('git' 'python-setuptools')
source=("git+https://github.com/dxa4481/$pkgname.git")
sha512sums=('SKIP')

pkgver() {
  cd $pkgname

  echo $(git rev-list --count HEAD).$(git rev-parse --short HEAD)
}

build() {
  cd $pkgname

  python setup.py build
}

package() {
  cd $pkgname

  python setup.py install --root="$pkgdir" --prefix=/usr -O1 --skip-build

  install -Dm 644 -t "$pkgdir/usr/share/doc/$pkgname/" README.md
}

