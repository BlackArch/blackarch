# This file is part of BlackArch Linux ( http://blackarch.org ).
# See COPYING for license details.

pkgname='plasma'
pkgver=897.8ec3bb7
pkgrel=1
groups=('blackarch' 'blackarch-disassembler' 'blackarch-binary'
        'blackarch-debugger')
pkgdesc='An interactive disassembler for x86/ARM/MIPS. It can generates indented pseudo-code with colored syntax.'
arch=('x86_64' 'armv6h' 'armv7h' 'aarch64')
url='https://github.com/joelpx/plasma'
license=('GPL3')
depends=('python3' 'python-capstone' 'python-pyelftools' 'pefile-py3k'
         'python-msgpack' 'python-pefile' 'python-pyqt4')
makedepends=('git' 'python-setuptools' 'python-nose' 'python-pip')
source=('git+https://github.com/joelpx/plasma.git')
sha1sums=('SKIP')

pkgver() {
  cd "$srcdir/plasma"

  echo $(git rev-list --count HEAD).$(git rev-parse --short HEAD)
}

prepare() {
  cd "$srcdir/plasma"

  find . -type f -exec sed -i 's|/bin/python3|/usr/bin/env python3|' {} +
}

build() {
  cd "$srcdir/plasma"

  python3 setup.py build
}

package() {
  cd "$srcdir/plasma"

  mkdir -p "$pkgdir/usr/share/plasma"

  python3 setup.py build_ext --inplace
  python3 setup.py install --root="$pkgdir" --prefix=/usr

  install -Dm644 -t "$pkgdir/usr/share/doc/plasma" README.md
  install -Dm644 LICENSE "$pkgdir/usr/share/licenses/plasma/LICENSE"

  rm README.md LICENSE

  cp -a * "$pkgdir/usr/share/plasma/"
}
