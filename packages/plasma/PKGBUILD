# This file is part of BlackArch Linux ( https://www.blackarch.org/ ).
# See COPYING for license details.

pkgname=plasma
pkgver=922.ec7df9b
pkgrel=5
groups=('blackarch' 'blackarch-disassembler' 'blackarch-binary'
        'blackarch-debugger')
pkgdesc='An interactive disassembler for x86/ARM/MIPS. It can generates indented pseudo-code with colored syntax.'
arch=('x86_64' 'aarch64')
url='https://github.com/joelpx/plasma'
license=('GPL3')
depends=('python' 'python-capstone' 'python-pyelftools' 'python-pefile'
         'python-msgpack' 'python-pefile' 'python-pyqt4' 'python-nose')
makedepends=('git' 'python-setuptools' 'python-nose' 'python-pip')
source=("git+https://github.com/joelpx/$pkgname.git")
sha512sums=('SKIP')

pkgver() {
  cd $pkgname

  echo $(git rev-list --count HEAD).$(git rev-parse --short HEAD)
}

prepare() {
  cd $pkgname

  find . -type f -exec sed -i 's|/bin/python3|/usr/bin/env python3|' {} +

  sed -i 's/msgpack-python>=0.4.6/msgpack/' requirements.txt
}

build() {
  cd $pkgname

  python setup.py build
}

package() {
  cd $pkgname

  install -dm 755 "$pkgdir/usr/share/$pkgname"

  python setup.py build_ext --inplace
  python setup.py install --root="$pkgdir" --prefix=/usr -O1

  install -Dm 644 -t "$pkgdir/usr/share/doc/$pkgname" README.md
  install -Dm 644 LICENSE "$pkgdir/usr/share/licenses/$pkgname/LICENSE"

  rm README.md LICENSE

  cp -a * "$pkgdir/usr/share/$pkgname/"
}

