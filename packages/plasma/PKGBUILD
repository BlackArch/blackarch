# This file is part of BlackArch Linux ( http://blackarch.org ).
# See COPYING for license details.

pkgname='plasma'
pkgver=799.628ad71
pkgrel=1
groups=('blackarch' 'blackarch-disassembler' 'blackarch-binary'
        'blackarch-debugger')
pkgdesc='plasma engineering tool for x86/ARM/MIPS. Generates indented pseudo-C with colored syntax code.'
arch=('i686' 'x86_64' 'armv6h' 'armv7h')
url='https://github.com/joelpx/plasma'
license=('GPL3')
depends=('python3' 'python-capstone' 'python-pyelftools' 'pefile-py3k'
         'python-msgpack')
makedepends=('git' 'python-nose' 'python-pip')
source=('git+https://github.com/joelpx/plasma.git')
sha1sums=('SKIP')

pkgver() {
  cd "$srcdir/plasma"

  echo $(git rev-list --count HEAD).$(git rev-parse --short HEAD)
}

prepare() {
  cd "$srcdir/plasma"

  find . -type f -exec sed -i 's|/bin/python3|/usr/bin/env python3|' {} +
}

package() {
  cd "$srcdir/plasma"

  mkdir -p "$pkgdir/usr/share/plasma"

  python3 setup.py build_ext --inplace
  python3 setup.py install --root="$pkgdir"

  install -Dm644 -t "$pkgdir/usr/share/doc/plasma" README.md
  install -Dm644 LICENSE "$pkgdir/usr/share/licenses/plasma/LICENSE"

  rm README.md LICENSE

  cp -a * "$pkgdir/usr/share/plasma/"
}
