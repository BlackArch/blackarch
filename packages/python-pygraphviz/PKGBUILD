# This file is part of BlackArch Linux ( https://www.blackarch.org/ ).
# See COPYING for license details.

pkgbase=python-pygraphviz
pkgname=('python2-pygraphviz' 'python-pygraphviz')
_pkgname=pygraphviz
pkgver=1.5
epoch=1
pkgrel=6
pkgdesc='Python interface to the Graphviz graph layout and visualization package.'
arch=('x86_64' 'aarch64')
url='https://pypi.org/project/pygraphviz/#files'
license=('LGPL')
makedepends=('python2-setuptools' 'python-setuptools' 'graphviz')
source=("https://files.pythonhosted.org/packages/7e/b1/d6d849ddaf6f11036f9980d433f383d4c13d1ebcfc3cd09bc845bda7e433/$_pkgname-$pkgver.zip")
sha512sums=('6d3df6642a7e23ecb6b687761480b80a3ee5886508ef50b81697041425be7d09d623bf46990c5cea3ef36817a28d9e5c2905eb32267296f55524fdedb2199ea2')

prepare() {
  cp -a "$_pkgname-$pkgver"{,-2}
}

build() {
  cd "$_pkgname-$pkgver-2"

  python2 setup.py build

  cd "$srcdir/$_pkgname-$pkgver"

  python setup.py build
}

package_python2-pygraphviz() {
  depends=('python2' 'graphviz')

  cd "$_pkgname-$pkgver-2"

  python2 setup.py install --prefix=/usr --root="$pkgdir" -O1 --skip-build

  mv "$pkgdir/usr/share/doc/$_pkgname-$pkgver" \
    "$pkgdir/usr/share/${pkgname[0]}"
}

package_python-pygraphviz() {
  depends=('python' 'graphviz')

  cd "$_pkgname-$pkgver"

  python setup.py install --prefix=/usr --root="$pkgdir" -O1 --skip-build

  mv "$pkgdir/usr/share/doc/$_pkgname-$pkgver" \
    "$pkgdir/usr/share/${pkgname[1]}"
}

