# This file is part of BlackArch Linux ( https://www.blackarch.org/ ).
# See COPYING for license details.

pkgbase=python-pygraphviz
pkgname=('python2-pygraphviz' 'python-pygraphviz')
_pkgname=pygraphviz
pkgver=1.7
epoch=1
pkgrel=1
pkgdesc='Python interface to the Graphviz graph layout and visualization package.'
arch=('x86_64' 'aarch64')
url='https://pypi.org/project/pygraphviz/#files'
license=('LGPL')
makedepends=('python2-setuptools' 'python-setuptools' 'graphviz')
source=("https://files.pythonhosted.org/packages/3a/d6/2c56f09ee83dbebb62c40487e4c972135661b9984fec9b30b77fb497090c/$_pkgname-$pkgver.zip")
sha512sums=('09438931d1930a70f7da94b0a12b449c0836ad707c6c8abed49bae0db162c136002d170398ed02dc56a5029269490ce3156d2d4b0f30602a11165bc0038998ea')

prepare() {
  cp -a "$_pkgname-$pkgver"{,-2}
}

build() {
  cd "$_pkgname-$pkgver-2"

  python2 setup.py build

  cd "$srcdir/$_pkgname-$pkgver"

  python setup.py build
}

package_python2-pygraphviz() {
  depends=('python2' 'graphviz')

  cd "$_pkgname-$pkgver-2"

  python2 setup.py install --prefix=/usr --root="$pkgdir" -O1 --skip-build

  mv "$pkgdir/usr/share/doc/$_pkgname-$pkgver" \
    "$pkgdir/usr/share/${pkgname[0]}"
}

package_python-pygraphviz() {
  depends=('python' 'graphviz')

  cd "$_pkgname-$pkgver"

  python setup.py install --prefix=/usr --root="$pkgdir" -O1 --skip-build

  mv "$pkgdir/usr/share/doc/$_pkgname-$pkgver" \
    "$pkgdir/usr/share/${pkgname[1]}"
}

