# This file is part of BlackArch Linux ( https://www.blackarch.org/ ).
# See COPYING for license details.

pkgbase=python-pygraphviz
pkgname=('python2-pygraphviz' 'python-pygraphviz')
_pkgname=pygraphviz
pkgver=1.9
epoch=1
pkgrel=1
pkgdesc='Python interface to the Graphviz graph layout and visualization package.'
arch=('x86_64' 'aarch64')
url='https://pypi.org/project/pygraphviz/#files'
license=('LGPL')
makedepends=('python2-setuptools' 'python-setuptools' 'python2-pip' 'python-pip'
             'python2-wheel' 'python-wheel' 'graphviz')
source=("https://files.pythonhosted.org/packages/c7/21/063c3ec261904f3c67b7d8bb0033e4e53bbdacb17e6d27097239d50fb7ca/$_pkgname-$pkgver.zip")
sha512sums=('0a963b2cfe526c5444379b037797504380898c300256ab3b4800687f46b6230d8bc0adb5f499004a8009544c6cc804221689921c08a4f4b71b86ae01dc39cb7e')

prepare() {
  cp -a "$_pkgname-$pkgver"{,-2}
}

build() {
  cd "$_pkgname-$pkgver-2"

  python2 setup.py build

  cd "$srcdir/$_pkgname-$pkgver"

  python setup.py build
}

package_python2-pygraphviz() {
  depends=('python2' 'graphviz')

  cd "$_pkgname-$pkgver-2"

  python2 setup.py install --prefix=/usr --root="$pkgdir" -O1 --skip-build

  mv "$pkgdir/usr/share/doc/$_pkgname-$pkgver" \
    "$pkgdir/usr/share/${pkgname[0]}"
}

package_python-pygraphviz() {
  depends=('python' 'graphviz')

  cd "$_pkgname-$pkgver"

  python setup.py install --prefix=/usr --root="$pkgdir" -O1 --skip-build

  mv "$pkgdir/usr/share/doc/$_pkgname-$pkgver" \
    "$pkgdir/usr/share/${pkgname[1]}"
}

