# This file is part of BlackArch Linux ( https://www.blackarch.org/ ).
# See COPYING for license details.

pkgbase=python-pygraphviz
pkgname=('python2-pygraphviz' 'python-pygraphviz')
_pkgname=pygraphviz
pkgver=1.8
epoch=1
pkgrel=1
pkgdesc='Python interface to the Graphviz graph layout and visualization package.'
arch=('x86_64' 'aarch64')
url='https://pypi.org/project/pygraphviz/#files'
license=('LGPL')
makedepends=('python2-setuptools' 'python-setuptools' 'python2-pip' 'python-pip'
             'python2-wheel' 'python-wheel' 'graphviz')
source=("https://files.pythonhosted.org/packages/01/8f/1538b3f3ad0441577d366ba3a96f7ccbe71356818fc4a54b2e7119a7794a/$_pkgname-$pkgver.zip")
sha512sums=('f59d9452867167ba406d0e352700ec6bc80364318413a982eadb324c7817f92b9d09922b5fd90b603e2cf6f408390f862e003ef3f1fd3230390944f7d6ee7228')

prepare() {
  cp -a "$_pkgname-$pkgver"{,-2}
}

build() {
  cd "$_pkgname-$pkgver-2"

  python2 setup.py build

  cd "$srcdir/$_pkgname-$pkgver"

  python setup.py build
}

package_python2-pygraphviz() {
  depends=('python2' 'graphviz')

  cd "$_pkgname-$pkgver-2"

  python2 setup.py install --prefix=/usr --root="$pkgdir" -O1 --skip-build

  mv "$pkgdir/usr/share/doc/$_pkgname-$pkgver" \
    "$pkgdir/usr/share/${pkgname[0]}"
}

package_python-pygraphviz() {
  depends=('python' 'graphviz')

  cd "$_pkgname-$pkgver"

  python setup.py install --prefix=/usr --root="$pkgdir" -O1 --skip-build

  mv "$pkgdir/usr/share/doc/$_pkgname-$pkgver" \
    "$pkgdir/usr/share/${pkgname[1]}"
}

