# This file is part of BlackArch Linux ( https://www.blackarch.org/ ).
# See COPYING for license details.

pkgname=barf
pkgver=923.9547ef8
pkgrel=1
pkgdesc='A multiplatform open source Binary Analysis and Reverse engineering Framework.'
groups=('blackarch' 'blackarch-binary' 'blackarch-reversing')
arch=('any')
url='https://github.com/programa-stic/barf-project'
license=('BSD')
depends=('python2' 'python2-pygments' 'python2-pyparsing' 'python2-pydot'
         'python2-pyelftools' 'python2-capstone' 'python2-ropper' 'ropgadget')
makedepends=('git' 'python2-setuptools')
source=("$pkgname::git+https://github.com/programa-stic/barf-project.git")
sha512sums=('SKIP')

pkgver() {
  cd $pkgname

  echo $(git rev-list --count HEAD).$(git rev-parse --short HEAD)
}

build() {
  cd $pkgname

  python2 setup.py build
}

package() {
  cd $pkgname

  python2 setup.py install --root="$pkgdir" -O1 --prefix=/usr --skip-build

  install -Dm 644 -t "$pkgdir/usr/share/doc/$pkgname/" README.md MANIFEST.in \
    CHANGELOG.md doc/papers/*

  install -Dm 644 LICENSE "$pkgdir/usr/share/licenses/$pkgname/LICENSE"
}

