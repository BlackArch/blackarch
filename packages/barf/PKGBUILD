# This file is part of BlackArch Linux ( http://blackarch.org ).
# See COPYING for license details.

pkgname='barf'
pkgver=811.d3ec350
pkgrel=1
pkgdesc='A multiplatform open source Binary Analysis and Reverse engineering Framework.'
groups=('blackarch' 'blackarch-binary' 'blackarch-reversing')
arch=('any')
url='https://github.com/programa-stic/barf-project'
license=('BSD')
depends=('python2' 'python2-pygments' 'python2-pyparsing' 'python2-pydot'
         'python2-pyelftools' 'python2-capstone' 'python2-ropper' 'ropgadget')
makedepends=('git' 'python2-setuptools')
source=('git+https://github.com/programa-stic/barf-project.git')
sha1sums=('SKIP')

pkgver() {
  cd "$srcdir/barf-project"

  echo $(git rev-list --count HEAD).$(git rev-parse --short HEAD)
}

build() {
  cd "$srcdir/barf-project"

  python2 setup.py build
}

package() {
  cd "$srcdir/barf-project"

  python2 setup.py install --root="$pkgdir" --optimize=1

  install -Dm644 -t "$pkgdir/usr/share/doc/barf/" README.md MANIFEST.in \
    CHANGELOG.md doc/papers/*

  install -Dm644 LICENSE "$pkgdir/usr/share/licenses/barf/LICENSE"
}
