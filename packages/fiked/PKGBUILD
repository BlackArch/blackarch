pkgname=fiked
pkgver=0.0.5
pkgrel=3
pkgdesc="Fake IDE daemon"
groups=('blackarch' 'blackarch-intel')
arch=('i686' 'x86_64')
url='http://www.roe.ch/FakeIKEd'
license=('GPL')
depends=("libgcrypt" "libnet")
source=(http://mirror.roe.ch/rel/fiked/fiked-$pkgver.tar.bz2)
md5sums=('2313cf2e13c0d516caea72bf4b57a7e4')

prepare() {
  cd "$srcdir/fiked-$pkgver"

  sed -i "s|libnet11-config|libnet-config|g" GNUmakefile
  sed -i "s|/usr/local|/usr|g" GNUmakefile
  sed -i "s|man/man1|share/man/man1|g" GNUmakefile
  sed -i "s|CFLAGS?=.*|CFLAGS=$CFLAGS|" GNUmakefile
}

build() {
  cd "$srcdir/fiked-$pkgver"
  PREFIX=/usr make
}

package() {
  cd "$srcdir/fiked-$pkgver"

  install -D -m755 fiked "$pkgdir/usr/bin/fiked"
  install -D -m644 fiked.1 "$pkgdir/usr/share/man/man1/fiked.1"
}
