# This file is part of BlackArch Linux ( https://www.blackarch.org/ ).
# See COPYING for license details.

pkgname=fiked
pkgver=0.0.5
pkgrel=6
groups=('blackarch' 'blackarch-honeypot')
pkgdesc='Fake IDE daemon.'
arch=('x86_64' 'aarch64')
url='http://www.roe.ch/FakeIKEd'
license=('GPL')
depends=('libgcrypt' 'libnet')
source=("http://mirror.roe.ch/rel/$pkgname/$pkgname-$pkgver.tar.bz2")
sha512sums=('468c75655bb84ae1553249d1ede078254d98dbf7f1ee98c37f169d1612d817d9b9cff60d06b3196f41e96cc1200dd3712237bc8bb50195100ce0130336809423')

prepare() {
  cd "$pkgname-$pkgver"

  sed -i "s|libnet11-config|libnet-config|g" GNUmakefile
  sed -i "s|/usr/local|/usr|g" GNUmakefile
  sed -i "s|man/man1|share/man/man1|g" GNUmakefile
  sed -i "s|CFLAGS?=.*|CFLAGS=$CFLAGS|" GNUmakefile
}

build() {
  cd "$pkgname-$pkgver"

  PREFIX=/usr make
}

package() {
  cd "$pkgname-$pkgver"

  install -Dm 755 $pkgname "$pkgdir/usr/bin/$pkgname"
  install -Dm 644 "$pkgname.1" "$pkgdir/usr/share/man/man1/$pkgname.1"
}

