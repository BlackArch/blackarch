# This file is part of BlackArch Linux ( https://www.blackarch.org/ ).
# See COPYING for license details.

pkgname=scap-workbench
pkgver=1.2.0
pkgrel=1
pkgdesc='SCAP Scanner And Tailoring Graphical User Interface.'
arch=('x86_64')
groups=('blackarch' 'blackarch-automation' 'blackarch-defensive'
        'blackarch-scanner')
url='https://www.open-scap.org/tools/scap-workbench/'
license=('GPL2')
depends=('libutil-linux' 'openscap' 'openssh-askpass' 'qt5-base'
         'qt5-xmlpatterns')
makedepends=('asciidoctor' 'cmake' 'git')
optdepends=('polkit')
source=("https://github.com/OpenSCAP/$pkgname/archive/$pkgver.tar.gz")
sha512sums=('c766b3a93721a2abc52cd47f44f9ed4b1ad02c733ef89dfb3f368f0f3e016c39b188f6ba29e06f1d3ca3b1b6cdc17e057a80198b105e94c60e484acafd7e1373')

prepare() {
  cd "$pkgname-$pkgver"

  sed -i 's/-Werror//' ./CMakeLists.txt # Quick fix
}

build() {
  cd "$pkgname-$pkgver"

  mkdir -p build
  cd build
  cmake -DCMAKE_INSTALL_PREFIX=/usr ..
  make
}

package() {
  cd "$pkgname-$pkgver/build"

  make DESTDIR="$pkgdir" install
}

