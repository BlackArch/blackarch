# This file is part of BlackArch Linux ( https://www.blackarch.org/ ).
# See COPYING for license details.

pkgbase=python-pyprind
pkgname=('python2-pyprind' 'python-pyprind')
_pkgname=PyPrind
pkgver=2.11.2
pkgrel=5
pkgdesc='Python Progress Indicator Utility.'
arch=('any')
url='https://pypi.org/project/pyprind/#files'
license=('GPL3')
makedepends=('python2-setuptools' 'python-setuptools')
source=("https://files.pythonhosted.org/packages/6c/b3/c3420d9a05e8fd0677907aab873998afd473af41aaf8d3bc557e8f35832c/$_pkgname-$pkgver.tar.gz")
sha512sums=('f4084fef2409821a44582c6309adb4590f9c2ca0a402e1b50f9d14ff7e2e4fafa9aa43db78e7387d988192ae27063a768ddd1fe085bc4c2e166d02c32f7722aa')

prepare() {
  cp -a "$_pkgname-$pkgver"{,-2}
}

build() {
  cd "$_pkgname-$pkgver-2"

  python2 setup.py build

  cd "$srcdir/$_pkgname-$pkgver"

  python setup.py build
}

package_python2-pyprind() {
  depends=('python2' 'python2-psutil')

  cd "$_pkgname-$pkgver-2"

  python2 setup.py install --prefix=/usr --root="$pkgdir" --optimize=1 \
    --skip-build
}

package_python-pyprind() {
  depends=('python' 'python-psutil')

  cd "$_pkgname-$pkgver"

  python setup.py install --prefix=/usr --root="$pkgdir" --optimize=1 \
    --skip-build
}

