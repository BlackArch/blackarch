# This file is part of BlackArch Linux ( http://blackarch.org ).
# See COPYING for license details.

pkgname='foresight'
pkgver=57.6f48984
pkgrel=1
pkgdesc='A tool for predicting the output of random number generators.'
groups=('blackarch' 'blackarch-crypto')
arch=('any')
url='https://github.com/ALSchwalm/foresight'
license=('MIT')
depends=('python' 'python-argparse')
makedepends=('git' 'python-setuptools')
source=('git+https://github.com/ALSchwalm/foresight.git')
sha1sums=('SKIP')

pkgver() {
  cd "$srcdir/foresight"

  echo $(git rev-list --count HEAD).$(git rev-parse --short HEAD)
}

build() {
  cd "$srcdir/foresight"

  python setup.py build
}

package() {
  cd "$srcdir/foresight"

  python setup.py install --root="$pkgdir" --prefix=/usr

  install -Dm644 -t "$pkgdir/usr/share/doc/foresight/" README.md
  install -Dm644 LICENSE "$pkgdir/usr/share/licenses/foresight/LICENSE"
}
