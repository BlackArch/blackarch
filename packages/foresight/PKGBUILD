# This file is part of BlackArch Linux ( https://www.blackarch.org/ ).
# See COPYING for license details.

pkgname=foresight
pkgver=0.1.r12.g6f48984
pkgrel=1
epoch=1
pkgdesc='A tool for predicting the output of random number generators.'
groups=('blackarch' 'blackarch-crypto')
arch=('any')
url='https://github.com/ALSchwalm/foresight'
license=('MIT')
depends=('python' 'python-nose')
makedepends=('git' 'python-setuptools')
source=("git+https://github.com/ALSchwalm/$pkgname.git")
sha512sums=('SKIP')

pkgver() {
  cd $pkgname

  ( set -o pipefail
    git describe --long --tags --abbrev=7 2>/dev/null |
      sed 's/\([^-]*-g\)/r\1/;s/-/./g' ||
    printf "%s.%s" "$(git rev-list --count HEAD)" \
      "$(git rev-parse --short=7 HEAD)"
  )
}

build() {
  cd $pkgname

  python setup.py build
}

package() {
  cd $pkgname

  python setup.py install --root="$pkgdir" --prefix=/usr --skip-build

  install -Dm 644 -t "$pkgdir/usr/share/doc/$pkgname/" README.md
  install -Dm 644 LICENSE "$pkgdir/usr/share/licenses/$pkgname/LICENSE"
}

