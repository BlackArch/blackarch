# This file is part of BlackArch Linux ( https://www.blackarch.org/ ).
# See COPYING for license details.

pkgname=admid-pack
_pkgname=ADMIDpack
pkgver=0.1
pkgrel=2
epoch=1
pkgdesc='ADM DNS spoofing tools - Uses a variety of active and passive methods to spoof DNS packets.'
groups=('blackarch' 'blackarch-spoof')
arch=('x86_64' 'aarch64')
url='http://packetstormsecurity.com/files/10080/ADMid-pkg.tgz.html'
license=('GPL')
depends=('libpcap')
source=('http://dl.packetstormsecurity.net/groups/ADM/ADM-DNS-SPOOF/ADMid-pkg.tgz')
sha512sums=('29195f735d27779a8e119325b5d4ccf72e92b957445634bd619569d02d06dfd9b5f5678aed2ea37bc2adc995bf6470ee752f6c62525fda3c32f4aa24d382b9fe')

build() {
  cd "$_pkgname/src"

  make CC="gcc $CFLAGS"
}

package() {
  cd $_pkgname

  install -dm 755 "$pkgdir/usr/bin"
  install -dm 755 "$pkgdir/usr/share/doc/$pkgname"

  cp -a --no-preserve=ownership ADMbin/* "$pkgdir/usr/bin"
  cp -a --no-preserve=ownership Docs/* "$pkgdir/usr/share/doc/$pkgname/"
}

