# This file is part of BlackArch Linux ( https://www.blackarch.org/ ).
# See COPYING for license details.

pkgbase=python-pysqlite3
pkgname=('python2-pysqlite3' 'python-pysqlite3')
_pkgname=pysqlite3
pkgver=0.4.7
pkgrel=1
pkgdesc='DB-API 2.0 interface for Sqlite 3.x.'
arch=('x86_64' 'aarch64')
url='https://pypi.org/project/pysqlite3/#files'
license=('zlib/libpng')
makedepends=('python2-setuptools' 'python-setuptools')
source=("https://files.pythonhosted.org/packages/15/a1/396fd493b2c0cce6b61bed2ead4f1d0e254e3dbea9ea723e2c042900499f/$_pkgname-$pkgver.tar.gz")
sha512sums=('992a64347573e0a2b5cfc5ae3788e4a80331cbb78d6e09d90f5a0f2b7a5864b936251990882dce10f68a1da6929881dcb27037039632800516a6a0515f5e1fbc')

prepare() {
  cp -a "$_pkgname-$pkgver"{,-2}
}

build() {
  cd "$_pkgname-$pkgver-2"

  python2 setup.py build

  cd "$srcdir/$_pkgname-$pkgver"

  python setup.py build
}

package_python2-pysqlite3() {
  depends=('python2' 'sqlite')

  cd "$_pkgname-$pkgver-2"

  python2 setup.py install --prefix=/usr --root="$pkgdir" -O1 --skip-build

  install -Dm 644 LICENSE "$pkgdir/usr/share/${pkgname[0]}/LICENSE"
}

package_python-pysqlite3() {
  depends=('python' 'sqlite')

  cd "$_pkgname-$pkgver"

  python setup.py install --prefix=/usr --root="$pkgdir" -O1 --skip-build

  install -Dm 644 LICENSE "$pkgdir/usr/share/${pkgname[1]}/LICENSE"
}

