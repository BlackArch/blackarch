# This file is part of BlackArch Linux ( https://www.blackarch.org/ ).
# See COPYING for license details.

pkgbase=python-pysqlite3
pkgname=('python2-pysqlite3' 'python-pysqlite3')
_pkgname=pysqlite3
pkgver=0.4.4
pkgrel=2
pkgdesc='DB-API 2.0 interface for Sqlite 3.x.'
arch=('x86_64' 'aarch64')
url='https://pypi.org/project/pysqlite3/#files'
license=('zlib/libpng')
makedepends=('python2-setuptools' 'python-setuptools')
source=("https://files.pythonhosted.org/packages/df/50/3ea8b9987db1d2f6e41fd1d576cdbc8a23ced23e531481036d3541b4e0bb/$_pkgname-$pkgver.tar.gz")
sha512sums=('0646f15de5c5390beff70357b375a5e1f27de238197b9f6b7b1ecb330548b0dc8cd7e55eace79b8cc885de50a1ebefe8a5c745efbd2dd9f92c0079f31cf82063')

prepare() {
  cp -a "$_pkgname-$pkgver"{,-2}
}

build() {
  cd "$_pkgname-$pkgver-2"

  python2 setup.py build

  cd "$srcdir/$_pkgname-$pkgver"

  python setup.py build
}

package_python2-pysqlite3() {
  depends=('python2' 'sqlite')

  cd "$_pkgname-$pkgver-2"

  python2 setup.py install --prefix=/usr --root="$pkgdir" -O1 --skip-build

  install -Dm 644 LICENSE "$pkgdir/usr/share/${pkgname[0]}/LICENSE"
}

package_python-pysqlite3() {
  depends=('python' 'sqlite')

  cd "$_pkgname-$pkgver"

  python setup.py install --prefix=/usr --root="$pkgdir" -O1 --skip-build

  install -Dm 644 LICENSE "$pkgdir/usr/share/${pkgname[1]}/LICENSE"
}

