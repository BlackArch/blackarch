# This file is part of BlackArch Linux ( https://www.blackarch.org/ ).
# See COPYING for license details.

pkgbase=python-pysqlite3
pkgname=('python2-pysqlite3' 'python-pysqlite3')
_pkgname=pysqlite3
pkgver=0.4.1
pkgrel=1
pkgdesc='DB-API 2.0 interface for Sqlite 3.x.'
arch=('x86_64' 'aarch64')
url='https://pypi.org/project/pysqlite3/#files'
license=('zlib/libpng')
makedepends=('python2-setuptools' 'python-setuptools')
source=("https://files.pythonhosted.org/packages/63/f9/d768ba1522852672b49f4ea3cf8c9f21743cc3a0c966fd4809add6133e67/$_pkgname-$pkgver.tar.gz")
sha512sums=('b26e549d5b582d08a854aaf578b209d0731e45d89641758831f7fe1c17fcb8ec716a10e1b738bc2e0b01ea89d04e5f9d4a14c52c821e4f3a467caabc11b0269f')

prepare() {
  cp -a "$_pkgname-$pkgver"{,-2}
}

build() {
  cd "$_pkgname-$pkgver-2"

  python2 setup.py build

  cd "$srcdir/$_pkgname-$pkgver"

  python setup.py build
}

package_python2-pysqlite3() {
  depends=('python2' 'sqlite')

  cd "$_pkgname-$pkgver-2"

  python2 setup.py install --prefix=/usr --root="$pkgdir" -O1 --skip-build

  install -Dm 644 LICENSE "$pkgdir/usr/share/${pkgname[0]}/LICENSE"
}

package_python-pysqlite3() {
  depends=('python' 'sqlite')

  cd "$_pkgname-$pkgver"

  python setup.py install --prefix=/usr --root="$pkgdir" -O1 --skip-build

  install -Dm 644 LICENSE "$pkgdir/usr/share/${pkgname[1]}/LICENSE"
}

