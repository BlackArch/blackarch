# This file is part of BlackArch Linux ( https://www.blackarch.org/ ).
# See COPYING for license details.

pkgname=exiflooter
pkgver=33.a92e697
pkgrel=1
pkgdesc='Find geolocation on all image urls and directories also integrates with OpenStreetMap.'
arch=('any')
groups=('blackarch' 'blackarch-forensic')
url='https://github.com/aydinnyunus/exifLooter'
license=('Apache-2.0')
depends=('perl-image-exiftool')
makedepends=('git' 'go')
source=("git+https://github.com/aydinnyunus/$pkgname.git")
sha512sums=('SKIP')

pkgver() {
  cd $pkgname

  # use, if no version string provided neither in sources nor in git describe:
  echo $(git rev-list --count HEAD).$(git rev-parse --short HEAD)

  # if tag exists, use this
  git describe --long --tags | sed 's/\([^-]*-g\)/r\1/;s/-/./g'
}

build() {
  cd $pkgname

  # If you encounter the error: go mod download: no modules specified (see 'go help mod download')
  # while building, then uncomment the following lines:
  # GOPATH="$srcdir" go mod init go.mod
  # GOPATH="$srcdir" go mod tidy
  GOPATH="$srcdir" go mod download
  GOPATH="$srcdir" go build \
    -trimpath \
    -buildmode=pie \
    -mod=readonly \
    -modcacherw \
    -ldflags "-s -w" \
    -o $pkgname .

}

package() {
  cd $pkgname

  install -Dm 755 $pkgname "$pkgdir/usr/bin/$pkgname"
  install -Dm 644 -t "$pkgdir/usr/share/doc/$pkgname/" README.md
  # Only install license if required according to
  # https://wiki.archlinux.org/index.php/PKGBUILD#license
  install -Dm 644 LICENSE "$pkgdir/usr/share/licenses/$pkgname/LICENSE"
}
