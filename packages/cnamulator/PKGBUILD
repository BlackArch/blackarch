# This file is part of BlackArch Linux ( https://www.blackarch.org/ ).
# See COPYING for license details.

pkgname=cnamulator
pkgver=5.4667c68
pkgrel=3
groups=('blackarch' 'blackarch-mobile' 'blackarch-recon')
pkgdesc='A phone CNAM lookup utility using the OpenCNAM API.'
arch=('x86_64' 'aarch64')
url='https://github.com/packetassailant/cnamulator'
license=('MIT')
depends=('glibc')
makedepends=('git' 'go')
source=("git+https://github.com/packetassailant/$pkgname.git")
sha512sums=('SKIP')

pkgver() {
  cd $pkgname

  echo $(git rev-list --count HEAD).$(git rev-parse --short HEAD)
}

build() {
  cd $pkgname

  GOPATH="$srcdir" go build \
    -trimpath \
    -buildmode=pie \
    -mod=readonly \
    -modcacherw \
    -ldflags "-s -w" \
    -o $pkgname $pkgname.go
}

package() {
  cd $pkgname

  install -Dm 755 $pkgname "$pkgdir/usr/bin/$pkgname"
  install -Dm 644 -t "$pkgdir/usr/share/doc/$pkgname/" README.md
}

