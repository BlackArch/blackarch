# This file is part of BlackArch Linux ( https://www.blackarch.org/ ).
# See COPYING for license details.

pkgbase=python-dfdatetime
pkgname=('python2-dfdatetime' 'python-dfdatetime')
_pkgname=dfdatetime
pkgver=20200501
pkgrel=1
pkgdesc='Digital Forensics Date and Time.'
arch=('any')
url='https://pypi.org/project/dfdatetime/'
license=('Apache')
makedepends=('python2-setuptools' 'python-setuptools')
source=("https://files.pythonhosted.org/packages/8d/87/210b3aee5b0ad1e89fbe640a28dfb4af8ce705b36127fc11b634cb2cc228/$_pkgname-$pkgver.tar.gz")
sha512sums=('860f4e2d898d94ffd38ce4963e333ce7e7b8514be5574cc7f9480b4646227241fc22b5b8b739a2b5b3ec39eecb95c1f9540bf6c4894b6628a94aae11f8d4b89f')

package_python2-dfdatetime() {
  depends=('python2')

  cd "$_pkgname-$pkgver"

  python2 setup.py install --prefix=/usr --root="$pkgdir" --optimize=1
  mv $pkgdir/usr/share/doc/dfdatetime{,2}
}

package_python-dfdatetime() {
  depends=('python')

  cd "$_pkgname-$pkgver"

  python setup.py install --prefix=/usr --root="$pkgdir" --optimize=1
}

