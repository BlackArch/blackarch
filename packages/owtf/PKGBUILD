pkgname=owtf
pkgver=0.40
pkgrel=3
groups=('blackarchlinux' 'blackarchlinux-webapp')
pkgdesc="The Offensive (Web) Testing Framework"
url="https://www.owasp.org/index.php/OWASP_OWTF"
license=('custom')
arch=('any')
depends=('python2' 'python2-jinja' 'python2-tornado' 'python2-pycurl'
         'python2-pexpect' 'python2-lxml')
makedepends=('git')
options=('!strip')
source=("git+https://github.com/7a/owtf")
md5sums=('SKIP')

pkgver() {
  cd "$srcdir/owtf"
  grep -m1 'Version ' README | sed 's/.* //'
}

package() {
  cd "$srcdir/owtf"

  # Base directories.
  install -dm755 "$pkgdir/usr/share/owtf"
  install -dm755 "$pkgdir/usr/bin"

  cp -a * "$pkgdir/usr/share/owtf"

  cat > "$pkgdir/usr/bin/owtf" << EOF
#!/bin/sh
python2 /usr/share/owtf/owtf.py "\$@"
EOF
  chmod +x "$pkgdir/usr/bin/owtf"
}
