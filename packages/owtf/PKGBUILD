# This file is part of BlackArch Linux ( http://blackarch.org ).
# See COPYING for license details.

pkgname='owtf'
pkgver=543.fe62288
pkgrel=1
groups=('blackarch' 'blackarch-webapp' 'blackarch-fuzzer' 'blackarch-scanner')
pkgdesc='The Offensive (Web) Testing Framework.'
url='https://www.owasp.org/index.php/OWASP_OWTF'
license=('custom')
arch=('any')
depends=('python2' 'python2-jinja' 'python2-tornado' 'python2-pycurl'
         'python2-pexpect' 'python2-lxml')
makedepends=('git')
options=('!strip')
source=('git+https://github.com/owtf/owtf.git'
        'fix_resource_profile.diff'
        'fix_general_profile.diff')
sha1sums=('SKIP'
          '972b0887f5658aa6121d15ccfcf0508bc3e7a8f5'
          '1162e4393f5516661657fedb13238dabc4cee6d4')

pkgver() {
  cd "$srcdir/owtf"

  echo $(git rev-list --count HEAD).$(git rev-parse --short HEAD)
}

prepare() {
  cd "$srcdir/owtf"

  patch -p2 -i "$srcdir/fix_resource_profile.diff"
  patch -p2 -i "$srcdir/fix_general_profile.diff"
}

package() {
  cd "$srcdir/owtf"

  mkdir -p "$pkgdir/usr/share/owtf"
  mkdir -p "$pkgdir/usr/bin"

  cp -a * "$pkgdir/usr/share/owtf"

  cat > "$pkgdir/usr/bin/owtf" << EOF
#!/bin/sh
exec python2 /usr/share/owtf/owtf.py "\$@"
EOF

  chmod +x "$pkgdir/usr/bin/owtf"
}
