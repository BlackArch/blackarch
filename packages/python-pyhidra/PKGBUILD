# This file is part of BlackArch Linux ( https://www.blackarch.org/ ).
# See COPYING for license details.

pkgname=python-pyhidra
_pkgname=${pkgname#python-}
pkgver=1.3.0
pkgrel=1
pkgdesc='Python library that provides direct access to the Ghidra API within a native CPython interpreter using jpype.'
arch=('any')
url='https://github.com/dod-cyber-crime-center/pyhidra'
license=('custom:LicenseRef-DC3-Pyhidra-Open-Source-License')
depends=('python' 'python-jpype1' 'java-environment>=17')
makedepends=('python-setuptools' 'python-build' 'python-installer' 'python-wheel')
source=($_pkgname-$pkgver.zip::"$url/archive/$pkgver.zip")
sha512sums=('bf8f43436fec0eb340ca13ca415a9ed861dab3c50c54ca1a24c5e148c13883979164f02b58d5abbb56b112a99672d8ad25fe6a0ccf1ad4368a0334fa72aaa3d0')

build() {
  cd "$_pkgname-$pkgver"

  python -m build --wheel --no-isolation
}

package(){
  cd "$_pkgname-$pkgver"

  python -m installer --destdir="$pkgdir" dist/*.whl
  install -D -m644 LICENSE "$pkgdir"/usr/share/licenses/$pkgname/LICENSE
}

