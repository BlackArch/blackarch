# This file is part of BlackArch Linux ( https://www.blackarch.org/ ).
# See COPYING for license details.

pkgname=pwnedornot
pkgver=137.4707b81
pkgrel=2
pkgdesc='Tool to find passwords for compromised email addresses.'
groups=('blackarch' 'blackarch-recon' 'blackarch-social')
arch=('any')
url='https://github.com/thewhiteh4t/pwnedOrNot'
license=('MIT')
depends=('python' 'python-argparse' 'python-requests')
makedepends=('git')
source=("$pkgname::git+https://github.com/thewhiteh4t/pwnedOrNot.git")
sha512sums=('SKIP')

pkgver() {
  cd $pkgname

  echo $(git rev-list --count HEAD).$(git rev-parse --short HEAD)
}

package() {
  cd $pkgname

  install -Dm 755 "$pkgname.py" "$pkgdir/usr/bin/$pkgname"
  install -Dm 644 -t "$pkgdir/usr/share/doc/$pkgname/" README.md
  install -Dm 644 LICENSE "$pkgdir/usr/share/licenses/$pkgname/LICENSE"
}

