# This file is part of BlackArch Linux ( http://blackarch.org ).
# See COPYING for license details.
pkgname='ruby-rbkb'
_gemname='rbkb'
pkgver='0.7.2'
pkgrel=3
pkgdesc="Rbkb is a collection of ruby-based pen-testing and reversing tools"
arch=('any')
url='http://emonti.github.com/rbkb'
license=("MIT")
depends=('ruby')
makedepends=(git)
options=('!emptydirs')
source=('git+https://github.com/lsfxz/rbkb.git')
sha1sums=('SKIP')

build() {
  cd "$srcdir/$_gemname"

  gem build $_gemname.gemspec
}


package() {
  cd "$srcdir/$_gemname"
  local _gemdir="$(ruby -rubygems -e 'puts Gem.default_dir')"
  gem install --ignore-dependencies --no-document --no-user-install -i "$pkgdir/$_gemdir" -n "$pkgdir"/usr/bin $_gemname-$pkgver.gem 
  rm "$pkgdir/$_gemdir/cache/$_gemname-$pkgver.gem"
  install -D -m644 "$pkgdir/$_gemdir/gems/$_gemname-$pkgver/LICENSE.txt" "$pkgdir/usr/share/licenses/$pkgname/LICENSE.txt"

  # Preserve library and other required directories. Remove junk that gem installs.
  local _copydir=`mktemp -d -t $_gemname.XXX`
  chmod 755 $_copydir
  (cd "$pkgdir/$_gemdir/gems/$_gemname-$pkgver"/ && cp -r --parents lib bin $_copydir)
  rm -rf "$pkgdir/$_gemdir/gems/$_gemname-$pkgver"
  mv $_copydir "$pkgdir/$_gemdir/gems/$_gemname-$pkgver"

}
