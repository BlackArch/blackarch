# This file is part of BlackArch Linux ( http://blackarch.org ).
# See COPYING for license details.

pkgname='treasure'
pkgver=6.a91d52b
pkgrel=2
groups=('blackarch' 'blackarch-recon')
pkgdesc='Hunt for sensitive information through githubs code search.'
arch=('any')
url='https://github.com/GuerrillaWarfare/Treasure'
license=('custom:unknown')
depends=('python2' 'python2-beautifulsoup4' 'python2-requests')
makedepends=('git' 'python2-setuptools')
source=('git+https://github.com/GuerrillaWarfare/Treasure.git')
sha1sums=('SKIP')

pkgver() {
  cd "$srcdir/Treasure"

  echo $(git rev-list --count HEAD).$(git rev-parse --short HEAD)
}

package() {
  cd "$srcdir/Treasure"

  mkdir -p "$pkgdir/usr/bin"
  mkdir -p "$pkgdir/usr/share/treasure"

  install -Dm644 README.md "$pkgdir/usr/share/doc/treasure/README.md"

  rm README.md

  cp -a * "$pkgdir/usr/share/treasure"

  cat > "$pkgdir/usr/bin/treasure" << EOF
#!/bin/sh
cd /usr/share/treasure
python2 treasure.py "\$@"
EOF

  chmod a+x "$pkgdir/usr/bin/treasure"
}
