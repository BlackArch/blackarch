# This file is part of BlackArch Linux ( http://blackarch.org ).
# See COPYING for license details.

pkgname=davscan
pkgver=27.9ad8f2f
pkgrel=1
pkgdesc='Fingerprints servers, finds exploits, scans WebDAV.'
groups=('blackarch' 'blackarch-webapp' 'blackarch-scanner'
        'blackarch-fingerprint' 'blackarch-recon')
arch=('any')
url='https://github.com/Graph-X/davscan'
license=('custom:unknown')
depends=('python2' 'python2-requests' 'python2-pysqlite' 'python2-lxml')
makedepends=('git')
source=('git+https://github.com/Graph-X/davscan.git')
sha512sums=('SKIP')

pkgver() {
  cd "$srcdir/davscan"

  echo $(git rev-list --count HEAD).$(git rev-parse --short HEAD)
}

package() {
  cd "$srcdir/davscan"

  install -dm 755 "$pkgdir/usr/bin"
  install -dm 755 "$pkgdir/usr/share/davscan"

  install -Dm644 -t "$pkgdir/usr/share/doc/davscan/" README.md
  install -Dm644 LICENSE "$pkgdir/usr/share/licenses/davscan/LICENSE"

  rm README.md LICENSE

  cp -a * "$pkgdir/usr/share/davscan"

  cat > "$pkgdir/usr/bin/davscan" << EOF
#!/bin/sh
cd /usr/share/davscan
exec python2 davscan.py "\${@}"
EOF

  chmod a+x "$pkgdir/usr/bin/davscan"
}
