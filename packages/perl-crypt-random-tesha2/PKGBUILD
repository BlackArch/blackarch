# This file is part of BlackArch Linux ( http://blackarch.org ).
# See COPYING for license details.

pkgname='perl-crypt-random-tesha2'
pkgver='0.01'
pkgrel=3
pkgdesc='Random numbers using timer/schedule entropy.'
_dist='Crypt-Random-TESHA2'
arch=('any')
url="https://metacpan.org/release/$_dist"
license=('GPL' 'PerlArtistic')
depends=('perl')
options=('!emptydirs' 'purge')
source=("http://search.cpan.org/CPAN/authors/id/D/DA/DANAJ/$_dist-${pkgver}.tar.gz")
sha1sums=('0bf3122ad7c7e4443574780627e74aa776964b6f')

build() {
  cd "$srcdir/$_dist-$pkgver"

  unset PERL5LIB PERL_MM_OPT PERL_LOCAL_LIB_ROOT

  export PERL_MM_USE_DEFAULT=1 PERL_AUTOINSTALL=--skipdeps

  /usr/bin/perl Makefile.PL

  make
}

package() {
  cd "$srcdir/$_dist-$pkgver"

  unset PERL5LIB PERL_MM_OPT PERL_LOCAL_LIB_ROOT

  make install INSTALLDIRS=vendor DESTDIR="$pkgdir"
}
