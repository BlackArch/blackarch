# This file is part of BlackArch Linux ( https://www.blackarch.org/ ).
# See COPYING for license details.

pkgname=casefile
pkgver=1.0.1
pkgrel=10
pkgdesc="The little brother to Maltego without transforms, but combines graph and link analysis to examine links between manually added data to mind map your information"
arch=('any')
groups=('blackarch' 'blackarch-forensic' 'blackarch-recon' 'blackarch-scanner')
url='http://www.paterva.com/web6/products/casefile.php'
license=('GPL-2.0-or-later')
depends=('java-environment')
source=("https://web.archive.org/web/20180324104204if_/http://www.paterva.com/cf10/community/maltego-CF.${pkgver}_community-2012-03-14.zip"
        'https://github.com/cmlh/Maltego-Entities-Mirror/raw/refs/heads/master/mirror/www/casefile_entities.mtz'
        'https://github.com/cmlh/Maltego-Entities-Mirror/raw/refs/heads/master/mirror/www/SocialNetEntityPackage.mtz'
        'https://github.com/cmlh/Maltego-Entities-Mirror/raw/refs/heads/master/mirror/www/facebookObject.mtz'
        'https://github.com/cmlh/Maltego-Entities-Mirror/raw/refs/heads/master/mirror/www/BuiltWithTechnology.mtz')
sha512sums=('bdf5b392b70dbce929cacfde7c0daac4d689e4490246d2b7dccb4519e52ec3011489ecf581b7e425ac5a0fb294772cd4f929a259cd655a366dbf153f1d5ba95b'
            'f3fe13c0ca402a0b5f4b9d14a803afd220440a6a94e33e64acf83fa77f2f243389b809772457ed26204c7a90b383f167fef99de0acd7db24b95b801854f2740c'
            '8365aa611441199c8603f28bf33812b852de777e50c88e64e449b3c5c49c765d8545ce8d44cce13bccfdddbb14ab5fa4a1b22678b9ae3dbcc995087155abdceb'
            '6bb6562e4198c16c728915e2915ce23052f5361a9b75a7b8cbe45659818284daa00c2bb0742f3624c969b4ca8a0db73fe0d2af0c0f1306dee4c539f42310c17e'
            '523e27fd2a9440089ee0c3f7d6ea69ac129ef04792babc068c2bbe9ec20d4c1383794aa8fd2679e2e1e35e7837a9c268f2af45e268c04ea031985dee06077e1c')
install='casefile.install'

package() {
  cd v3.0

  install -dm 755 "$pkgdir/usr/bin"
  install -dm 755 "$pkgdir/usr/share/$pkgname"
  install -dm 755 "$pkgdir/usr/share/$pkgname/entities"

  cp -a --no-preserve=ownership bin etc ide maltego maltego-core-platform \
    maltego-ui platform "$pkgdir/usr/share/$pkgname"
  install -Dm644 "$srcdir/casefile_entities.mtz" \
    "$pkgdir/usr/share/$pkgname/entities/"
  install -Dm644 "$srcdir/SocialNetEntityPackage.mtz" \
    "$pkgdir/usr/share/$pkgname/entities/"
  install -Dm644 "$srcdir/facebookObject.mtz" \
    "$pkgdir/usr/share/$pkgname/entities/"
  install -Dm644 "$srcdir/BuiltWithTechnology.mtz" \
  "$pkgdir/usr/share/$pkgname/entities/"

  cat > "$pkgdir/usr/bin/$pkgname" << EOF
#!/bin/sh
cd /usr/share/$pkgname/bin
exec ./maltego "\$@"
EOF
  chmod +x "$pkgdir/usr/bin/$pkgname"
}

