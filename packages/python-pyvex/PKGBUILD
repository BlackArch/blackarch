# This file is part of BlackArch Linux ( https://www.blackarch.org/ ).
# See COPYING for license details.

pkgbase=python-pyvex
pkgname=('python2-pyvex' 'python-pyvex')
_pkgname=pyvex
pkgver=8.19.7.25
pkgrel=1
pkgdesc='A Python interface to libVEX and VEX IR.'
arch=('x86_64' 'armv6h' 'armv7h' 'aarch64')
url='https://pypi.org/project/pyvex/#files'
license=('BSD')
makedepends=('python2-setuptools' 'python-setuptools')
source=("https://files.pythonhosted.org/packages/58/d5/4eccf80ea316866f9aec3ddef7d37a29f80c8dfdd52044190d4c8dde3726/$_pkgname-$pkgver.tar.gz")
sha512sums=('1e63d875af555d610097c12af1ce7805c79f645fb8eb3c9551f693c2bdc745ca8a9eb1fb533115e249988750c86f8fd20db6c0edc082a81bde3b66809495a88d')

prepare() {
  cp -a "$_pkgname-$pkgver"{,-2}
}

build() {
  cd "$_pkgname-$pkgver-2"

  python2 setup.py build

  cd "$srcdir/$_pkgname-$pkgver"

  python setup.py build
}

package_python2-pyvex() {
  depends=('python2' 'python2-pycparser' 'python2-cffi' 'python2-archinfo'
           'python2-bitstring' 'python2-futre' 'python2-requests')

  cd "$_pkgname-$pkgver-2"

  python2 setup.py install --prefix=/usr --root="$pkgdir" --optimize=1 \
    --skip-build
}

package_python-pyvex() {
  depends=('python' 'python-pycparser' 'python-cffi' 'python-archinfo'
           'python-bitstring' 'python-future' 'python-requests')

  cd "$_pkgname-$pkgver"

  python setup.py install --prefix=/usr --root="$pkgdir" --optimize=1 \
    --skip-build
}

