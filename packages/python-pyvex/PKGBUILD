# This file is part of BlackArch Linux ( https://www.blackarch.org/ ).
# See COPYING for license details.

pkgbase=python-pyvex
pkgname=('python2-pyvex' 'python-pyvex')
_pkgname=pyvex
pkgver=8.19.4.5
pkgrel=2
pkgdesc='A Python interface to libVEX and VEX IR.'
arch=('x86_64' 'armv6h' 'armv7h' 'aarch64')
url='https://pypi.org/project/pyvex/#files'
license=('BSD')
makedepends=('python2-setuptools' 'python-setuptools')
source=("https://files.pythonhosted.org/packages/91/4a/d3a2a3375584c48fd320b664b0ad7ae5b4a56ebb9cf90cbfb2487eaf392d/$_pkgname-$pkgver.tar.gz")
sha512sums=('33da2ee6c58860c09e874bd834b8a070f8d0e9cdf188e52a639f62a84eb6a113d4512aed0b47f63f7bf07e8c94df0f78d7a2ba6e295dd32ee8f3773cdeb3af5e')

prepare() {
  cp -a "$_pkgname-$pkgver" "$_pkgname-$pkgver-2"
}

build() {
  cd "$_pkgname-$pkgver-2"

  python2 setup.py build

  cd "$srcdir/$_pkgname-$pkgver"

  python setup.py build
}

package_python2-pyvex() {
  depends=('python2' 'python2-pycparser' 'python2-cffi' 'python2-archinfo'
           'python2-bitstring' 'python2-futre' 'python2-requests')

  cd "$_pkgname-$pkgver-2"

  python2 setup.py install --prefix=/usr --root="$pkgdir" --optimize=1 \
    --skip-build
}

package_python-pyvex() {
  depends=('python' 'python-pycparser' 'python-cffi' 'python-archinfo'
           'python-bitstring' 'python-future' 'python-requests')

  cd "$_pkgname-$pkgver"

  python setup.py install --prefix=/usr --root="$pkgdir" --optimize=1 \
    --skip-build
}

