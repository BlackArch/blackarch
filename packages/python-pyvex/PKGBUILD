# This file is part of BlackArch Linux ( https://www.blackarch.org/ ).
# See COPYING for license details.

pkgbase=python-pyvex
pkgname=('python2-pyvex' 'python-pyvex')
_pkgname=pyvex
pkgver=8.19.10.30
pkgrel=3
pkgdesc='A Python interface to libVEX and VEX IR.'
arch=('x86_64' 'aarch64')
url='https://pypi.org/project/pyvex/#files'
license=('BSD')
makedepends=('python2-setuptools' 'python-setuptools')
source=("https://files.pythonhosted.org/packages/b7/56/701017b03976cb17ef165efe1d709be7b5da2217d35ff3eb5bba1dabac37/$_pkgname-$pkgver.tar.gz")
sha512sums=('10f385658f939e0e49ce91e8955050a84dfe99a74ce5ee873eb094ff9ebf0c26bdc5c993e6f0f5d3b2e7e6db0e82ca88dff41eb2e00f8d7440b7507f2f43e131')

prepare() {
  cp -a "$_pkgname-$pkgver"{,-2}
}

build() {
  cd "$_pkgname-$pkgver-2"

  python2 setup.py build

  cd "$srcdir/$_pkgname-$pkgver"

  python setup.py build
}

package_python2-pyvex() {
  depends=('python2' 'python2-pycparser' 'python2-cffi' 'python2-archinfo'
           'python2-bitstring' 'python2-futre' 'python2-requests')

  cd "$_pkgname-$pkgver-2"

  python2 setup.py install --root="$pkgdir" --prefix=/usr -O1 --skip-build
}

package_python-pyvex() {
  depends=('python' 'python-pycparser' 'python-cffi' 'python-archinfo'
           'python-bitstring' 'python-future' 'python-requests')

  cd "$_pkgname-$pkgver"

  python setup.py install --root="$pkgdir" --prefix=/usr -O1 --skip-build
}

