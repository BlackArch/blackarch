# This file is part of BlackArch Linux ( http://blackarch.org ).
# See COPYING for license details.

pkgname='tunna'
pkgver=34.7074493
pkgrel=1
groups=('blackarch' 'blackarch-networking' 'blackarch-tunnel' 'blackarch-proxy'
        'blackarch-windows')
pkgdesc='a set of tools which will wrap and tunnel any TCP communication over HTTP. It can be used to bypass network restrictions in fully firewalled environments.'
arch=('any')
url='https://github.com/SECFORCE/Tunna'
license=('GPL3')
depends=('python2')
makedepends=('git')
source=('git+https://github.com/SECFORCE/Tunna.git')
sha1sums=('SKIP')

pkgver() {
  cd "$srcdir/Tunna"

  echo $(git rev-list --count HEAD).$(git rev-parse --short HEAD)
}

package() {
  cd "$srcdir/Tunna"

  mkdir -p "$pkgdir/usr/bin"
  mkdir -p "$pkgdir/usr/share/tunna"

  install -Dm644 -t "$pkgdir/usr/share/doc/tunna" README.md

  rm README.md

  cp -a * "$pkgdir/usr/share/tunna"

  cat > "$pkgdir/usr/bin/tunna-proxy" << EOF
#!/bin/sh
cd /usr/share/tunna
exec python2 proxy.py "\$@"
EOF

  cat > "$pkgdir/usr/bin/tunna-webserver" << EOF
#!/bin/sh
cd /usr/share/tunna
exec python2 webserver.py "\$@"
EOF

  chmod a+x "$pkgdir/usr/bin/"*
}
