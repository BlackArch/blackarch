# This file is part of BlackArch Linux ( https://www.blackarch.org/ ).
# See COPYING for license details.

pkgname=unix-privesc-check
pkgver=1.5
_ver=r1_5
pkgrel=1
groups=('blackarch' 'blackarch-automation' 'blackarch-scanner')
pkgdesc='Tries to find misconfigurations that could allow local unprivilged users to escalate privileges to other users or to access local apps (e.g. databases).'
arch=('any')
url="https://pentestmonkey.net/tools/audit/$pkgname"
license=('GPL2' 'custom')

depends=('bash')

changelog=CHANGELOG
source=("$pkgname-$_ver.tgz::https://github.com/pentestmonkey/unix-privesc-check/archive/$_ver.tar.gz")
sha512sums=('573177f42e6cd40668453e8e60b40678178998c6571f6f9be8017a2d0ad01091af2104e45d8f700ddbcd10df4c40170e575038c916322b063c0411c62f5014aa')


prepare() { sed -i '/if \[ .* \]/s/==/=/' "$pkgname-$_ver/$pkgname"; }

package() {
  cd "$pkgname-$_ver"
  install -Dm755 "$pkgname" "$pkgdir/usr/bin/$pkgname"
  install -Dm644 CHANGELOG "$pkgdir/usr/share/doc/$pkgname/CHANGELOG"
  install -Dm644 COPYING.UNIX-PRIVESC-CHECK "$pkgdir/usr/share/licenses/$pkgname/LICENSE"
}

