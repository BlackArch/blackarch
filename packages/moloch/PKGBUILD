# This file is part of BlackArch Linux ( http://blackarch.org ).
# See COPYING for license details.

pkgname='moloch'
pkgver='0.11.3'
pkgrel=1
pkgdesc='An open source large scale IPv4 full PCAP capturing, indexing and database system.'
url='https://github.com/aol/moloch'
groups=('blackarch' 'blackarch-networking')
arch=('x86_64' 'armv6h' 'armv7h' 'aarch64')
license=('APACHE')
options=('!strip')
depends=('curl' 'file' 'util-linux' 'perl-json' 'perl-libwww' 'libpng' 'python2'
         'elasticsearch' 'geoip' 'libnids' 'nodejs')
makedepends=('glib2')
_dataroot="/usr/share/moloch"
_yaraver='2.1.0'
source=("https://github.com/aol/moloch/archive/v${pkgver}.tar.gz"
"https://github.com/plusvic/yara/archive/v${_yaraver}.tar.gz")
sha1sums=('4e532e02ed10b046019b2c5fbd83832a8f93413c'
          '8289c281a44c933e11de25953f3910fe9f8ee82e')

prepare() {
  cd "$srcdir"

  # Change the config location from /data/moloch/etc/ to /etc/moloch/
  cd "moloch-$pkgver"

  sed -i 's|/data/moloch/etc/|/etc/moloch/|g' $(grep -rl '/data/moloch/etc/')

  # Change the location files expect the moloch data root to be from
  # /data/moloch to $_dataroot
  sed -i 's|/data/moloch|'"$_dataroot"'|g' $(grep -rl '/data/moloch')

  # Add $pkgdir prefix to data files
  sed -i 's|^PARSERSDIR\s*=\s*|PARSERSDIR = '"$pkgdir"'|' \
    capture/parsers/Makefile.in
  sed -i 's|^PLUGINDIR\s*=\s*|PLUGINDIR = '"$pkgdir"'|' \
    capture/plugins/Makefile.in
  sed -i 's|^DBDIR\s*=\s*|DBDIR = '"$pkgdir"'|' \
    db/Makefile.in
  sed -i 's|^VIEWERDIR\s*=\s*|VIEWERDIR = '"$pkgdir"'|;/npm update/d' \
    viewer/Makefile.in

  # Add $pkgdir prefix to binary files and specify /usr as the install prefix
  sed -i 's|^bindir\s*=\s*@prefix@|bindir = '"$pkgdir"'/usr/|' \
    capture/Makefile.in
}

build() {
  cd "$srcdir"

  # Build the static version of yara
  cd "yara-$_yaraver"

  ./bootstrap.sh
  ./configure --enable-static --enable-magic --enable-cuckoo
  make

  # Build moloch
  cd "../moloch-$pkgver"

  ./configure --prefix=$_dataroot --bindir=/usr/bin \
    --with-yara="$srcdir/yara-$_yaraver"

  make
}

package() {
  cd "$srcdir/moloch-$pkgver"

  make DESTDIR="$pkgdir" install

  install -Dm644 README.rst "$pkgdir/usr/share/doc/moloch/README.rst"

  # Install the default config and move the elasticsearch example to the config
  # dir
  install -Dm644 config.ini "$pkgdir/etc/$pkgname/config.ini"
  cd "$pkgdir"
  mv ./$_dataroot/db/elasticsearch.yml.sample etc/$pkgname/

  # Install node modules for the viewer
  # cd "./$_dataroot/viewer"
  # npm --python=/usr/bin/python2 update

  # Fix the node module permissions and their python2 path
  #cd node_modules
  #chmod 644 png/gyp/lib/*.a
  #sed -i \
  #  -re 's|#!\s*(/usr)?(/local)?/bin/.*python.*$|#!/usr/bin/env python2|g' \
  #  $(egrep -rl '^\s*#!\s*(/usr)?(/local)?/bin/.*python')
}
