# This file is part of BlackArch Linux ( http://blackarch.org ).
# See COPYING for license details.

pkgname='tuxcut'
pkgver=41.341cc18
pkgrel=1
groups=('blackarch' 'blackarch-sniffer')
pkgdesc='Netcut-like program for Linux written in PyQt.'
url='https://github.com/a-atalla/tuxcut.git'
arch=('any')
license=('GPL2')
depends=('python2' 'python2-pyqt4' 'iproute2' 'dsniff' 'arp-scan' 'arptables')
makedepends=('git')
source=("git+https://github.com/a-atalla/tuxcut.git")
sha1sums=('SKIP')

pkgver() {
  cd "$srcdir/tuxcut"

  echo $(git rev-list --count HEAD).$(git rev-parse --short HEAD)
}

prepare() {
  cd "$srcdir"

  # Fix some things.
  sed -i 's|/usr/bin/env python|/usr/bin/env python2|' \
    "$srcdir/tuxcut/tuxcut.py"
  sed -i 's/python/python2/' "$srcdir/tuxcut/tuxcut"
  sed -i 's/cmd = /cmd=/' "$srcdir/tuxcut/tuxcut"
  sed -i 's;/opt/TuxCut/tuxcut.py;/opt/tuxcut/tuxcut.py;' \
    "$srcdir/tuxcut/tuxcut"
  sed -i "s;ui/MainWindow.ui;/opt/tuxcut/ui/MainWindow.ui;" \
    "$srcdir/tuxcut/tuxcut_core.py"
}

package() {
  cd "$srcdir"

  mkdir -p "$pkgdir/usr/bin"
  mkdir -p "$pkgdir/opt/TuxCut"
  mkdir -p "$pkgdir/usr/share/doc/tuxcut"

  install -m755 "$srcdir/tuxcut/tuxcut" "$pkgdir/usr/bin/tuxcut"

  cp -a --no-preserve=ownership "$srcdir/tuxcut/" "$pkgdir/opt/"
}
