# This file is part of BlackArch Linux ( http://blackarch.org ).
# See COPYING for license details.

pkgname=tuxcut
pkgver=5.0
pkgrel=7
groups=('blackarch' 'blackarch-sniffer')
pkgdesc="Netcut-like program for Linux written in PyQt"
url='http://bitbucket.org/a_atalla/tuxcut/'
arch=('any')
license=('GPL2')
depends=('python' 'python-pyqt4' 'iproute2' 'dsniff' 'arp-scan' 'arptables')
source=(https://bitbucket.org/a_atalla/tuxcut/downloads/tuxcut_${pkgver}_all.deb)
md5sums=('cf06438795b5671edfc97abc0302bcee')

prepare() {
  cd "$srcdir"

  tar xzf data.tar.gz

  rm data.tar.gz control.tar.gz

  # Remove hidden files.
  rm -rf .??*

  # Remove backup files.
  find . -name '*~' -type f -delete

  # Fix some things.
  sed -i '1s/python/python2/' "$srcdir/opt/TuxCut/run.py"
  sed -i 's/^lang =.*$/lang = "English"/' "$srcdir/opt/TuxCut/run.py"
  sed -i 's/python/python2/' "$srcdir/usr/bin/tuxcut"
}

package() {
  cd "$srcdir"

  # Base directories.
  install -dm755 "$pkgdir/usr/bin"
  install -dm755 "$pkgdir/opt/TuxCut"
  install -dm755 "$pkgdir/usr/share/doc/tuxcut"

  # Bin.
  install -m755 "$srcdir/usr/bin/tuxcut" "$pkgdir/opt/TuxCut"

  cp -a --no-preserve=ownership usr opt "$pkgdir"
}
