# This file is part of BlackArch Linux ( https://www.blackarch.org/ ).
# See COPYING for license details.

pkgname=python2-django
_pkgname=Django
pkgver=1.11.27
pkgrel=2
pkgdesc='A high-level Python Web framework that encourages rapid development and clean, pragmatic design.'
url='https://pypi.org/project/Django/#files'
arch=('any')
license=('BSD')
depends=('python2' 'python2-pytz')
makedepends=('python2-setuptools')
source=("https://files.pythonhosted.org/packages/8a/26/d4dd19084cb1a8d4c7b566024e3fd80b25aa50bccef3a8e5db1d300d4fba/$_pkgname-$pkgver.tar.gz")
sha512sums=('02370bc69d715fbd0d0460e801840331670f7348767040035d80d5e881eae90259dfa4b6406af37d827361691aca464bc4d556e525c32a94413528d0593fdf09')

build() {
  cd "$_pkgname-$pkgver"

  python2 setup.py build
}

package() {
  cd "$_pkgname-$pkgver"

  python2 setup.py install --root="$pkgdir" --prefix=/usr -O1 --skip-build

  for i in "$pkgdir/usr/bin/"*; do
    mv $i "${i}2"
  done
}

