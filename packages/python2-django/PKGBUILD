# This file is part of BlackArch Linux ( https://www.blackarch.org/ ).
# See COPYING for license details.

pkgname=python2-django
_pkgname=Django
pkgver=1.11.26
pkgrel=1
pkgdesc='A high-level Python Web framework that encourages rapid development and clean, pragmatic design.'
url='https://pypi.org/project/Django/#files'
arch=('any')
license=('BSD')
depends=('python2' 'python2-pytz')
makedepends=('python2-setuptools')
source=("https://files.pythonhosted.org/packages/1e/cc/3226f5b935841bf8c1a1387cc0cba9770f0c12df9aab4460a63036765b23/$_pkgname-$pkgver.tar.gz")
sha512sums=('2c6bd31c1aa7131702c75accde537b2e7aaa6a7b687dd501d8d4552dd5e007bce12179719f40374aca06855414535b5126579a15d55419a0a34a4bb17221e021')

build() {
  cd "$_pkgname-$pkgver"

  python2 setup.py build
}

package() {
  cd "$_pkgname-$pkgver"

  python2 setup.py install --root="$pkgdir" --prefix=/usr -O1 --skip-build

  for i in "$pkgdir/usr/bin/"*; do
    mv $i "${i}2"
  done
}

