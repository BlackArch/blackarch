# This file is part of BlackArch Linux ( https://www.blackarch.org/ ).
# See COPYING for license details.

pkgbase=python-fuzzywuzzy
pkgname=('python2-fuzzywuzzy' 'python-fuzzywuzzy')
_pkgname=fuzzywuzzy
pkgver=0.18.0
pkgrel=1
epoch=1
pkgdesc='Fuzzy string matching like a boss.'
arch=('any')
url='https://pypi.org/project/fuzzywuzzy/#files'
license=('BSD')
makedepends=('python2-setuptools' 'python-setuptools')
source=("https://files.pythonhosted.org/packages/11/4b/0a002eea91be6048a2b5d53c5f1b4dafd57ba2e36eea961d05086d7c28ce/$_pkgname-$pkgver.tar.gz")
sha512sums=('4a21ea67278fa525842d48fed8db666d00eae6d13254e8844d11f63b47c3a305b3cac760f28c24c6347aebcf73e96180e0a7cfba29c75f01ece2f7751e0398c5')

prepare() {
  cp -a "$_pkgname-$pkgver"{,-2}
}

build() {
  cd "$_pkgname-$pkgver-2"

  python2 setup.py build

  cd "$srcdir/$_pkgname-$pkgver"

  python setup.py build
}

package_python2-fuzzywuzzy() {
  depends=('python2' 'python2-levenshtein')

  cd "$_pkgname-$pkgver-2"

  python2 setup.py install --prefix=/usr --root="$pkgdir" --optimize=1 \
    --skip-build
}

package_python-fuzzywuzzy() {
  depends=('python' 'python-levenshtein')

  cd "$_pkgname-$pkgver"

  python setup.py install --prefix=/usr --root="$pkgdir" --optimize=1 \
    --skip-build
}

