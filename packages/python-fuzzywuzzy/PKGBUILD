# This file is part of BlackArch Linux ( https://www.blackarch.org/ ).
# See COPYING for license details.

pkgbase=python-fuzzywuzzy
pkgname=('python2-fuzzywuzzy' 'python-fuzzywuzzy')
_pkgname=fuzzywuzzy
pkgver=0.17.0
pkgrel=2
epoch=1
pkgdesc='Fuzzy string matching like a boss.'
arch=('any')
url='https://pypi.org/project/fuzzywuzzy/#files'
license=('BSD')
makedepends=('python2-setuptools' 'python-setuptools')
source=("https://files.pythonhosted.org/packages/81/54/586e9f34401dc7f5248589765bb67d49b837e2f309f25a33e82e896cba0a/$_pkgname-$pkgver.tar.gz")
sha512sums=('10fb91d85f2e612885543a442959f3c88bf9d3dc468718a1fb92bf443f1753abfc04a9b07935a0d523508b38fc1ba9a7ba391a17b5f01160c26cc2c98d1f6c1d')

prepare() {
  cp -a "$_pkgname-$pkgver"{,-2}
}

build() {
  cd "$_pkgname-$pkgver-2"

  python2 setup.py build

  cd "$srcdir/$_pkgname-$pkgver"

  python setup.py build
}

package_python2-fuzzywuzzy() {
  depends=('python2' 'python2-levenshtein')

  cd "$_pkgname-$pkgver-2"

  python2 setup.py install --prefix=/usr --root="$pkgdir" --optimize=1 \
    --skip-build
}

package_python-fuzzywuzzy() {
  depends=('python' 'python-levenshtein')

  cd "$_pkgname-$pkgver"

  python setup.py install --prefix=/usr --root="$pkgdir" --optimize=1 \
    --skip-build
}

