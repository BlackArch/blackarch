# This file is part of BlackArch Linux ( http://blackarch.org ).
# See COPYING for license details.

pkgname=python-fuzzywuzzy
pkgver=362.778162c
pkgrel=1
pkgdesc='Fuzzy string matching like a boss.'
arch=('any')
url='https://github.com/seatgeek/fuzzywuzzy'
license=('BSD')
depends=('python')
makedepends=('git')
source=('git+https://github.com/seatgeek/fuzzywuzzy.git')
sha512sums=('SKIP')

pkgver() {
  cd "$srcdir/fuzzywuzzy"

  echo $(git rev-list --count HEAD).$(git rev-parse --short HEAD)
}

build() {
  cd "$srcdir/fuzzywuzzy"

  python setup.py build
}

package() {
  cd "$srcdir/fuzzywuzzy"

  python setup.py install --prefix=/usr --root="$pkgdir"

  install -Dm644 -t "$pkgdir/usr/share/doc/python-fuzzywuzzy" README.rst \
    CHANGES.rst
  install -Dm644 LICENSE.txt \
    "$pkgdir/usr/share/licenses/python-fuzzywuzzy/LICENSE"
}
