# This file is part of BlackArch Linux ( https://www.blackarch.org/ ).
# See COPYING for license details.

# Old Maintainer: James P. Harvey <jamespharvey20 at gmail dot com>
# Old Maintainer: Christoph Bayer <chrbayer@criby.de>
# Contributor: Felix Yan <felixonmars@archlinux.org>
# Contributor: Sven-Hendrik Haase <sh@lutzhaase.com>
# Contributor: Thomas Dziedzic < gostrc at gmail >
# Contributor: Mathias Stearn <mathias@10gen.com>
# Contributor: Alec Thomas
# Contributor: Fredy Garc√≠a <frealgagu at gmail dot com>

pkgname=mongodb
pkgver=4.2.11
pkgrel=1
pkgdesc='A high-performance, open source, schema-free document-oriented database.'
arch=('x86_64' 'aarch64')
url='https://www.mongodb.com/'
license=('custom:SSPL')
depends=('curl' 'libstemmer' 'lsb-release' 'pcre' 'wiredtiger' 'yaml-cpp'
         'sqlite' 'boost' 'boost-libs')
optdepends=("${pkgname}-tools: mongoimport, mongodump, mongotop, etc")
makedepends=('libpcap' 'ncurses' 'python-regex' 'python-psutil' 'python-yaml'
             'python-requests' 'python-setuptools' 'python-typing'
             'python-cheetah3' 'readline' 'scons')
backup=("etc/$pkgname.conf")
source=("http://downloads.${pkgname}.org/src/$pkgname-src-r$pkgver.tar.gz"
        "$pkgname.sysusers"
        "$pkgname.tmpfiles"
)
sha512sums=('ff398a9d3a012e0eb860c2139967e163b8b0ac045c1c47f2c0c5584ba7c60a1403bb423ce8c6e8b3900fdcad1164a84c6da79276f897fd640366e3f80cf54316'
            '36a2c4336d6aeeec303dd0aec2c554db826d479efcb95269f75e2150ad752516c3f25c50f8e25b90384dea65b5b5b72a15ab17d5da35f6f92f20cc65af02f61b'
            '41b711a623b034b66e146487fab948608111af48aff2b4f1b171b64b531fa6a1b36de8b348ff543fdd9c3ac34a1a20fae9b23cdb124abda93f89ce011a7921ef')

_scons_args=(
  --use-system-pcre
  --use-system-snappy
  --use-system-yaml
  --use-system-zlib
  # --use-system-wiredtiger
  --use-system-stemmer
  --use-sasl-client
  --ssl
  --disable-warnings-as-errors
  --use-system-tcmalloc
  --use-system-boost
  --use-system-sqlite
  # --use-system-asio     # https://jira.mongodb.org/browse/SERVER-21839 marked as fixed, but still doesn't compile.  MongoDB uses custom patches.
  # --use-system-icu      # Doesn't compile
  # --use-system-valgrind # Compiles, but namcap says not used
  # --use-system-mongo-c  # Doesn't compile
)

prepare() {
  cd "$pkgname-src-r$pkgver"

  # Keep historical Arch dbPath
  sed -i 's|dbPath: /var/lib/mongo|dbPath: /var/lib/mongodb|' rpm/mongod.conf

  # Keep historical Arch conf file name
  sed -i 's|-f /etc/mongod.conf|-f /etc/mongodb.conf|' rpm/mongod.service

  # Keep historical Arch user name (no need for separate daemon group name)
  sed -i 's/User=mongod/User=mongodb/' rpm/mongod.service
  sed -i 's/Group=mongod/Group=mongodb/' rpm/mongod.service
  sed -i 's/chown mongod:mongod/chown mongodb:mongodb/' rpm/mongod.service

  # Remove sysconfig file, used by upstream's init.d script not used on Arch
  sed -i '/EnvironmentFile=-\/etc\/sysconfig\/mongod/d' rpm/mongod.service

  # Make systemd wait as long as it takes for MongoDB to start
  # If MongoDB needs a long time to start, prevent systemd from restarting it
  # every 90 seconds See: https://jira.mongodb.org/browse/SERVER-38086
  sed -i 's/\[Service]/[Service]\nTimeoutStartSec=infinity/' rpm/mongod.service
}

build() {
  cd "$pkgname-src-r$pkgver"

  export SCONSFLAGS="$MAKEFLAGS"
  scons core "${_scons_args[@]}"
}

package() {
  cd "$pkgname-src-r$pkgver"

  scons install --prefix="$pkgdir/usr" "${_scons_args[@]}"

  install -Dm 644 rpm/mongod.conf "$pkgdir/etc/$pkgname.conf"
  install -Dm 644 rpm/mongod.service \
    "$pkgdir/usr/lib/systemd/system/$pkgname.service"

  install -Dm 644 -t "$pkgdir/usr/share/man/man1/mongo.1" debian/mongo.1 \
    debian/mongod.1 debian/mongos.1

  install -Dm 644 "$srcdir/$pkgname.sysusers" \
    "$pkgdir/usr/lib/sysusers.d/$pkgname.conf"
  install -Dm 644 "$srcdir/$pkgname.tmpfiles" \
    "$pkgdir/usr/lib/tmpfiles.d/$pkgname.conf"
  install -Dm 644 LICENSE-Community.txt \
    "$pkgdir/usr/share/licenses/$pkgname/LICENSE"

  # This script won't run on Arch. If needed, see AUR package mongodb-compass.
  rm "$pkgdir/usr/bin/install_compass"
}

