# This file is part of BlackArch Linux ( https://www.blackarch.org/ ).
# See COPYING for license details.
#
# from AUR - adjusted to our styles
#
# Maintainer: JustKidding <jk@vin.ovh>
# Contributor: James P. Harvey <jamespharvey20 at gmail dot com>
# Contributor: Christoph Bayer <chrbayer@criby.de>
# Contributor: Felix Yan <felixonmars@archlinux.org>
# Contributor: Sven-Hendrik Haase <sh@lutzhaase.com>
# Contributor: Thomas Dziedzic < gostrc at gmail >
# Contributor: Mathias Stearn <mathias@10gen.com>
# Contributor: Alec Thomas
# Contributor: Fredy Garc√≠a <frealgagu at gmail dot com>

pkgname=mongodb
pkgver=7.0.11
pkgrel=1
pkgdesc='A high-performance, open source, schema-free document-oriented database.'
arch=('x86_64' 'aarch64')
url="https://www.mongodb.com/"
license=("SSPL-1.0")
depends=('libstemmer' 'snappy' 'boost-libs' 'pcre2' 'yaml-cpp' 'gperftools'
         'libunwind' 'curl' 'zstd' 'zlib' 'libsasl' 'openssl' 'glibc' 'libldap'
         'gcc-libs' 'krb5')
makedepends=('python-psutil' 'python-setuptools' 'python-regex' 'python-yaml'
             'python-cheetah3' 'python-requests' 'python-pymongo' 'boost'
             'mongo-c-driver')
optdepends=('mongodb-tools: mongoimport, mongodump, mongotop, etc'
            'mongosh: interactive shell to connect with MongoDB')
backup=("etc/$pkgname.conf")
provides=(mongodb="$pkgver")
options=(!debug)
source=("https://github.com/mongodb/mongo/archive/refs/tags/r$pkgver.tar.gz"
        "$pkgname.sysusers"
        "$pkgname.tmpfiles"
        "$pkgname-5.0.2-skip-reqs-check.patch"
        "$pkgname-5.0.2-no-compass.patch"
        "$pkgname-7.0.2-sconstruct.patch"
        "$pkgname-4.4.29-no-enterprise.patch")
sha512sums=('30c3dfa886b9510d99d3d7ad77ef203c750fb10bd7c833535fd299a80fe980728327bbe49091bea71b612c2fbe9162c349d0c3069c33600cb424394d169ee6d0'
            '36a2c4336d6aeeec303dd0aec2c554db826d479efcb95269f75e2150ad752516c3f25c50f8e25b90384dea65b5b5b72a15ab17d5da35f6f92f20cc65af02f61b'
            '41b711a623b034b66e146487fab948608111af48aff2b4f1b171b64b531fa6a1b36de8b348ff543fdd9c3ac34a1a20fae9b23cdb124abda93f89ce011a7921ef'
            '29d4ff6de3a89c39ed0e3c54aa1e17fc3466ceb69dfbcfc62109ea2666ca98458b7b23ac82e4b5c9be77d5d82c69d5b213670b3a3367b13c3b8cab06cfacf4c2'
            '07fe714a4256c13449e1e764c88d5051773aa9397228085a8d581cde91477dff2c3dc70ba801eb559af76ff19abf205d32c9510606a9bb26cebb33f68822e607'
            '8650bd07392dd33c8746669126753b18357d23dc46d3af799875ba0b080258341cb3cefb2ac8c40e0592d99c69eae39793a3379d52aa5eef0b22318bef79ad10'
            '6e84db876022c21c1fcff8c9f88d4ff7375ebb8b37ab0dd445d1697484a1344098bfbffd82b3da7f2f8c2f926c4f403f2ffce9d67ef46b894f3a9f0b981aba10')
sha512sums_aarch64=('a9ab48048c0ec72720d7a8039273401b1fbd760447e118e440fe665eda8cf6c0a0bd6ebed2a977313216360d5a3b5d382fa2a27183693813a13d1d23c765e88b')
source_aarch64=(extrapatch-sconstruct.patch)

_scons_args=(
  CC="${CC:-gcc}"
  CXX="${CXX:-g++}"
  AR="${AR:-ar}"
  MONGO_DISTMOD=arch
  MONGO_VERSION="$pkgver"

  --use-system-boost
  --use-system-pcre2
  --use-system-snappy
  --use-system-stemmer
  --use-system-yaml
  --use-system-zlib
  --use-system-zstd
  --use-system-tcmalloc
  --use-system-libunwind
  --use-system-libbson
  --use-system-mongo-c

  --use-sasl-client
  --ssl
  --disable-warnings-as-errors
  --runtime-hardening=off
)

all-flag-vars() {
  echo {C,CXX}FLAGS
}

_filter-var() {
  local f x var=$1 new=()
  shift

  for f in ${!var} ; do
    for x in "$@" ; do
      # Note this should work with globs like -O*
      [[ ${f} == "${x}" ]] && continue 2
    done
    new+=( "${f}" )
  done
  export "${var}"="${new[*]}"
}

filter-flags() {
  local v
  for v in $(all-flag-vars) ; do
    _filter-var "${v}" "$@"
  done
  return 0
}

prepare() {
  cd "mongo-r${pkgver}"

  # Keep historical Arch dbPath
  sed -i 's|dbPath: /var/lib/mongo|dbPath: /var/lib/mongodb|' rpm/mongod.conf

  # Keep historical Arch conf file name
  sed -i 's|-f /etc/mongod.conf|-f /etc/mongodb.conf|' rpm/mongod.service

  # Keep historical Arch user name (no need for separate daemon group name)
  sed -i 's/User=mongod/User=mongodb/' rpm/mongod.service
  sed -i 's/Group=mongod/Group=mongodb/' rpm/mongod.service
  sed -i 's/chown mongod:mongod/chown mongodb:mongodb/' rpm/mongod.service

  # Remove sysconfig file, used by upstream's init.d script not used on Arch
  sed -i '/EnvironmentFile=-\/etc\/sysconfig\/mongod/d' rpm/mongod.service

  # Make systemd wait as long as it takes for MongoDB to start
  # If MongoDB needs a long time to start, prevent systemd from restarting it
  # every 90 seconds - See: https://jira.mongodb.org/browse/SERVER-38086
  sed -i 's/\[Service]/[Service]\nTimeoutStartSec=infinity/' rpm/mongod.service

  if check_option debug y; then
    _scons_args+=(--dbg=on)
  fi

  if check_option lto y; then
    _scons_args+=(--lto=on)
  fi

  # apply gentoo patches
  for file in "$srcdir"/*.patch; do
    echo "Applying patch $file..."
    patch -Np1 -i "$file"
  done
}

build() {
  cd "mongo-r${pkgver}"

  if check_option debug n; then
    filter-flags '-m*'
    filter-flags '-O?'
  fi

  export SCONSFLAGS="$MAKEFLAGS"
  ./buildscripts/scons.py "${_scons_args[@]}" install-devcore
}

package() {
  cd "mongo-r${pkgver}"

  # Install binaries
  install -D build/install/bin/mongo "$pkgdir/usr/bin/mongo"
  install -D build/install/bin/mongod "$pkgdir/usr/bin/mongod"
  install -D build/install/bin/mongos "$pkgdir/usr/bin/mongos"

  # Keep historical Arch conf file name
  install -Dm 644 "rpm/$pkgname.conf" "$pkgdir/etc/$pkgname.conf"

  # Keep historical Arch service name
  install -Dm 644 "rpm/mongod.service" \
    "$pkgdir/usr/lib/systemd/system/$pkgname.service"

  # Install manpages
  #install -Dm 644 "debian/mongo.1" "$pkgdir/usr/share/man/man1/mongo.1"
  install -Dm 644 "debian/mongod.1" "$pkgdir/usr/share/man/man1/mongod.1"
  install -Dm 644 "debian/mongos.1" "$pkgdir/usr/share/man/man1/mongos.1"

  # Install systemd files
  install -Dm 644 "$srcdir/$pkgname.sysusers" \
    "$pkgdir/usr/lib/sysusers.d/$pkgname.conf"
  install -Dm 644 "$srcdir/$pkgname.tmpfiles" \
    "$pkgdir/usr/lib/tmpfiles.d/$pkgname.conf"

  # Install license
  install -D LICENSE-Community.txt "$pkgdir/usr/share/licenses/$pkgname/LICENSE"
}

