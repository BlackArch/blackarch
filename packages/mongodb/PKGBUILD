# This file is part of BlackArch Linux ( https://www.blackarch.org/ ).
# See COPYING for license details.
#
# Adjusted to our style
#
# Maintainer: JustKidding <jk@vin.ovh>
# Maintainer: James P. Harvey <jamespharvey20 at gmail dot com>
# Maintainer: Christoph Bayer <chrbayer@criby.de>
# Contributor: Felix Yan <felixonmars@archlinux.org>
# Contributor: Sven-Hendrik Haase <sh@lutzhaase.com>
# Contributor: Thomas Dziedzic < gostrc at gmail >
# Contributor: Mathias Stearn <mathias@10gen.com>
# Contributor: Alec Thomas
# Contributor: Fredy Garc√≠a <frealgagu at gmail dot com>

pkgname=mongodb
pkgver=6.0.4
pkgrel=1
pkgdesc='A high-performance, open source, schema-free document-oriented database.'
arch=('x86_64')
url='https://www.mongodb.com/'
license=('Apache' 'custom:SSPL1')
depends=('libstemmer' 'snappy' 'boost-libs' 'pcre' 'yaml-cpp' 'curl')
makedepends=('python-psutil' 'python-setuptools' 'python-regex' 'boost'
             'python-cheetah3' 'python-yaml' 'python-requests' 'python-pymongo'
             'boost-libs')
optdepends=('mongodb-tools: mongoimport, mongodump, mongotop, etc'
            'mongosh-bin: interactive shell to connect with MongoDB')
backup=("etc/mongodb.conf")
provides=($pkgname=$pkgver)
source=("https://fastdl.mongodb.org/src/$pkgname-src-r$pkgver.tar.gz"
        "$pkgname.sysusers"
        "$pkgname.tmpfiles"
        "$pkgname-4.4.1-gcc11.patch"
        "$pkgname-5.0.2-no-compass.patch"
        "$pkgname-5.0.2-skip-reqs-check.patch"
        "$pkgname-6.0.0-boost-1.79.patch"
        "$pkgname-6.0.2-sconstruct-changes.patch"
        "$pkgname-6.0.0-gcc12.patch"
        "$pkgname-4.4.10-boost-1.81.patch")
sha512sums=('4dc1551c5b4c55b3a6f4c84b3521048955dac1715b91bea4b7d18b14cd848fc5b3fc347c34430d28bd037e33f37b40e2d050ec93b50590eb058748de5fdc59c0'
            '36a2c4336d6aeeec303dd0aec2c554db826d479efcb95269f75e2150ad752516c3f25c50f8e25b90384dea65b5b5b72a15ab17d5da35f6f92f20cc65af02f61b'
            '41b711a623b034b66e146487fab948608111af48aff2b4f1b171b64b531fa6a1b36de8b348ff543fdd9c3ac34a1a20fae9b23cdb124abda93f89ce011a7921ef'
            '10b099b09650119e075a4c6e926cc3538f7227df772af6b8eeeb33726fed41f2010f85fa6906b866705602b84a5baff92c557ef27ba1d4a6c862c40fcc4e7bbe'
            '07fe714a4256c13449e1e764c88d5051773aa9397228085a8d581cde91477dff2c3dc70ba801eb559af76ff19abf205d32c9510606a9bb26cebb33f68822e607'
            '29d4ff6de3a89c39ed0e3c54aa1e17fc3466ceb69dfbcfc62109ea2666ca98458b7b23ac82e4b5c9be77d5d82c69d5b213670b3a3367b13c3b8cab06cfacf4c2'
            '26cd0e8acba40f5a23506794c578e7c84b32bb14f707010883e4bad4edc03b36d13cc37678bdc364499b8032b47ab2c3989068500da8b978b25c184b59083090'
            '108d837443359046dc6e63cf68fcb97428c798ea0d0eb4f3ea494f70dfaa4fc7a731bb5d5a7feb21795926e28d7547751ed14a7dfc6dac945ef88cf54bff4257'
            'f2782f44a4f5a128da101c301222f955751c39a662352ce1f923985078454a7b51871fd49a820761dfc5f809a8fcdaa01d0210826484b20db9e7d6a4a29f3159'
            '62f4802a26398ffb17ac289d9e9de4044b753ece694785d3462f406391abdcfd16ff281959b4e4fb6751cc9247da7a4c55e8fa48eb90fd681eed724fedcb03e9')

_scons_args=(
  CC="${CC:-gcc}"
  CXX="${CXX:-g++}"
  AR="${AR:-ar}"

  --use-system-pcre
  --use-system-snappy
  --use-system-yaml
  --use-system-zlib
  --use-system-stemmer
  --use-sasl-client
  --ssl
  --disable-warnings-as-errors
  --use-system-boost
  --use-system-zstd
  --runtime-hardening=off
)

all-flag-vars() {
  echo {C,CXX}FLAGS
}

_filter-var() {
  local f x var=$1 new=()
  shift

  for f in ${!var} ; do
    for x in "$@" ; do
      # Note this should work with globs like -O*
      [[ ${f} == ${x} ]] && continue 2
    done
    new+=( "${f}" )
  done
  export ${var}="${new[*]}"
}

filter-flags() {
  local v
  for v in $(all-flag-vars) ; do
    _filter-var ${v} "$@"
  done
  return 0
}

prepare() {
  cd "$pkgname-src-r$pkgver"

  # Keep historical Arch dbPath
  sed -i 's|dbPath: /var/lib/mongo|dbPath: /var/lib/mongodb|' rpm/mongod.conf

  # Keep historical Arch conf file name
  sed -i 's|-f /etc/mongod.conf|-f /etc/mongodb.conf|' rpm/mongod.service

  # Keep historical Arch user name (no need for separate daemon group name)
  sed -i 's/User=mongod/User=mongodb/' rpm/mongod.service
  sed -i 's/Group=mongod/Group=mongodb/' rpm/mongod.service
  sed -i 's/chown mongod:mongod/chown mongodb:mongodb/' rpm/mongod.service

  # Remove sysconfig file, used by upstream's init.d script not used on Arch
  sed -i '/EnvironmentFile=-\/etc\/sysconfig\/mongod/d' rpm/mongod.service

  # Make systemd wait as long as it takes for MongoDB to start
  # If MongoDB needs a long time to start, prevent systemd from restarting it
  # every 90 seconds. See: https://jira.mongodb.org/browse/SERVER-38086
  sed -i 's/\[Service]/[Service]\nTimeoutStartSec=infinity/' rpm/mongod.service

  if check_option debug y; then
    _scons_args+=(--dbg=on)
  fi

  if check_option lto y; then
    _scons_args+=(--lto=on)
  fi

  # apply gentoo patches
  for file in $srcdir/*.patch; do
    echo "Applying patch $file..."
    patch -Np1 -i $file
  done
}

build() {
  cd "$pkgname-src-r$pkgver"

  if check_option debug n; then
    filter-flags '-m*'
    filter-flags '-O?'
  fi

  export SCONSFLAGS="$MAKEFLAGS"
  ./buildscripts/scons.py "${_scons_args[@]}" install-devcore
}

package() {
  cd "$pkgname-src-r$pkgver"

  # Install binaries
  install -D build/install/bin/mongo "$pkgdir/usr/bin/mongo"
  install -D build/install/bin/mongod "$pkgdir/usr/bin/mongod"
  install -D build/install/bin/mongos "$pkgdir/usr/bin/mongos"

  # Keep historical Arch conf file name
  install -Dm 644 "rpm/mongod.conf" "$pkgdir/etc/$pkgname.conf"

  # Keep historical Arch service name
  install -Dm 644 "rpm/mongod.service" \
    "$pkgdir/usr/lib/systemd/system/$pkgname.service"

  # Install manpages
  #install -Dm 644 "debian/mongo.1" "$pkgdir/usr/share/man/man1/mongo.1"
  install -Dm 644 "debian/mongod.1" "$pkgdir/usr/share/man/man1/mongod.1"
  install -Dm 644 "debian/mongos.1" "$pkgdir/usr/share/man/man1/mongos.1"

  # Install systemd files
  install -Dm 644 "$srcdir/$pkgname.sysusers" \
    "$pkgdir/usr/lib/sysusers.d/$pkgname.conf"
  install -Dm 644 "$srcdir/$pkgname.tmpfiles" \
    "$pkgdir/usr/lib/tmpfiles.d/$pkgname.conf"

  # Install license
  install -D LICENSE-Community.txt \
    "$pkgdir/usr/share/licenses/mongodb/LICENSE"
}

