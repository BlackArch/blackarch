# This file is part of BlackArch Linux ( http://blackarch.org ).
# See COPYING for license details.

pkgname='thezoo'
pkgver=158.75c1641
pkgrel=1
groups=('blackarch-malware')
pkgdesc='A project created to make the possibility of malware analysis open and available to the public.'
url='https://github.com/ytisf/theZoo'
arch=('any')
license=('GPL3')
depends=('python2')
makedepends=('git')
source=('git+https://github.com/ytisf/theZoo.git#branch=master')
sha1sums=('SKIP')

pkgver() {
  cd "$srcdir/theZoo"

  echo $(git rev-list --count HEAD).$(git rev-parse --short HEAD)
}

package() {
  cd "$srcdir/theZoo"

  mkdir -p "$pkgdir"/usr/{bin,share/thezoo}

  cp -a --no-preserve=ownership * "$pkgdir/usr/share/thezoo"

  cat > "$pkgdir"/usr/bin/thezoo << EOF
#!/bin/sh
cd /usr/share/thezoo
exec python2 ./theZoo.py "\${@}"
EOF

  chmod a+x "$pkgdir/usr/bin/thezoo"
}
