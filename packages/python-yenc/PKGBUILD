# This file is part of BlackArch Linux ( https://www.blackarch.org/ ).
# See COPYING for license details.

pkgname=python-yenc
_pkgname=${pkgname#python-}
pkgver=0.4.0.r30.g4cb4a7d
pkgrel=1
pkgdesc='yEnc Encoding/Decoding for Python.'
arch=('any')
groups=('blackarch' 'blackarch-crypto')
url='https://github.com/oe-mirrors/python3-yenc'
license=('LGPL')
depends=('python')
makedepends=('python-setuptools')
source=("$_pkgname-$pkgver::git+https://github.com/oe-mirrors/python3-yenc")
sha512sums=('SKIP')

pkgver() {
  cd "$_pkgname-$pkgver"

  git describe --long --tags | sed 's/\([^-]*-g\)/r\1/;s/-/./g'
}

prepare() {
  cd "$_pkgname-$pkgver"

  sed -i "s/#include <Python.h>/#define PY_SSIZE_T_CLEAN\n#include <Python.h>/g" src/_yenc.h
}

build() {
  cd "$_pkgname-$pkgver"

  python setup.py build
}

package() {
  cd "$_pkgname-$pkgver"

  python setup.py install --root="$pkgdir" --prefix=/usr -O1 --skip-build
}
