# This file is part of BlackArch Linux ( http://blackarch.org ).
# See COPYING for license details.

pkgname='argus-clients'
pkgver='3.0.8'
pkgrel=2
groups=('blackarch' 'blackarch-networking' 'blackarch-defensive'
        'blackarch-sniffer')
pkgdesc='Network monitoring client for Argus.'
arch=('i686' 'x86_64' 'armv6h' 'armv7h')
url='http://qosient.com/argus/'
depends=('libedit' 'geoip' 'libmariadbclient' 'libsasl' 'pcre')
license=('GPL3')
source=("http://qosient.com/argus/src/argus-clients-${pkgver}.tar.gz")
sha1sums=('3b9d764dc067af64670bcd8328d6f48088b9b5ca')

build() {
  cd "$srcdir/argus-clients-$pkgver"

  ./configure --sbindir=/usr/bin --prefix=/usr --with-sasl --with-GeoIP \
    --with-mysql --with-libpcre --with-libft --with-readline --with-perl

  make
}

package() {
  cd "$srcdir/argus-clients-$pkgver"

  make DESTDIR="$pkgdir" install

  install -Dm644 COPYING "$pkgdir/usr/share/licenses/argus-clients/COPYING"

  mv $pkgdir/usr/argus $pkgdir/usr/share/

  chmod 644 $pkgdir/usr/lib/argus_*.a
}
