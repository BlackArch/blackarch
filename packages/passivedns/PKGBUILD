# This file is part of BlackArch Linux ( https://www.blackarch.org/ ).
# See COPYING for license details.

pkgname=passivedns
pkgver=1.2.1
pkgrel=1
groups=('blackarch' 'blackarch-sniffer')
pkgdesc='A network sniffer that logs all DNS server replies for use in a passive DNS setup.'
arch=('x86_64' 'armv6h' 'armv7h' 'aarch64')
url="https://github.com/gamelinux/passivedns"
depends=('gcc' 'libpcap' 'ldns')
source=("https://github.com/gamelinux/passivedns/archive/$pkgver.tar.gz")
sha512sums=('369d404d08583c3d1ddd01c45bd5789e5e69396b81e81929c5498bdd177ba316b3c52db59d39a0c83739a96d6b93924272ef16274d5ea2018aa74a15f3f7cd7f')

prepare() {
  cd "$pkgname-$pkgver"

  aclocal
  autoconf -f -i
  autoreconf -f -i
  automake --add-missing
}

build() {
  cd "$pkgname-$pkgver"

  ./configure --prefix=/usr

  make
}

package() {
  cd "$pkgname-$pkgver"

  install -dm 755 "$pkgdir/usr/bin/"
  install -dm 755 "$pkgdir/usr/share/$pkgname"
  install -dm 755 "$pkgdir/usr/share/doc/$pkgname"

  cp -a --no-preserve=ownership src/passivedns "$pkgdir/usr/share/$pkgname/"
  install -Dm 644 -t "$pkgdir/usr/share/doc/$pkgname/" README

  ln -s "/usr/share/passivedns/passivedns" "$pkgdir/usr/bin/"
}

