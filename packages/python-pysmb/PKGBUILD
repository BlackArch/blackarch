# This file is part of BlackArch Linux ( https://www.blackarch.org/ ).
# See COPYING for license details.

pkgbase=python-pysmb
pkgname=('python2-pysmb' 'python-pysmb')
_pkgname=pysmb
pkgver=1.2.1
pkgrel=1
pkgdesc='An experimental SMB/CIFS library written in Python to support file sharing between Windows and Linux machines.'
arch=('any')
url='https://pypi.org/project/pysmb/#files'
license=('GPL')
makedepends=('python2-setuptools' 'python-setuptools')
source=("https://files.pythonhosted.org/packages/7a/90/7afb3db088a171f7e425855fab65a182503d205b234eb6bf2e36fd6aa5a5/$_pkgname-$pkgver.zip")
sha512sums=('d669ad09d8ac372de61ecb7b9b0b4cf21528d4a6a799daf26adba61a9802405c758b1b22e845f843b7529c733846d1b3b78f48da2429b23b4f08a1720196effa')

prepare() {
  cp -a "$_pkgname-$pkgver"{,-2}
}

build() {
  cd "$_pkgname-$pkgver-2"

  python2 setup.py build

  cd "$srcdir/$_pkgname-$pkgver"

  python setup.py build
}

package_python2-pysmb() {
  depends=('python2' 'python2-pyasn1')

  cd "$_pkgname-$pkgver-2"

  python2 setup.py install --prefix=/usr --root="$pkgdir" --optimize=1 \
    --skip-build
}

package_python-pysmb() {
  depends=('python' 'python-pyasn1')

  cd "$_pkgname-$pkgver"

  python setup.py install --prefix=/usr --root="$pkgdir" --optimize=1 \
    --skip-build
}

