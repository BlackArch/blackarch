# This file is part of BlackArch Linux ( https://www.blackarch.org/ ).
# See COPYING for license details.

pkgbase=python-pysmb
pkgname=('python2-pysmb' 'python-pysmb')
_pkgname=pysmb
pkgver=1.2.4
pkgrel=1
pkgdesc='An experimental SMB/CIFS library written in Python to support file sharing between Windows and Linux machines.'
arch=('any')
url='https://pypi.org/project/pysmb/#files'
license=('GPL')
makedepends=('python2-setuptools' 'python-setuptools')
source=("https://files.pythonhosted.org/packages/8e/2e/b92e2752e67a002655862814a82c1b3f313396bfa52f359a6cc84c328d55/$_pkgname-$pkgver.zip")
sha512sums=('a01435f773ab0542853d5ce969d64b93d8e823ff6108cab1ba7a2410eed5f3ca8b33dcce5c61f1b4c60372dd37e37412c8a65210b034fc0f3f15fda0fd74f597')

prepare() {
  cp -a "$_pkgname-$pkgver"{,-2}
}

build() {
  cd "$_pkgname-$pkgver-2"

  python2 setup.py build

  cd "$srcdir/$_pkgname-$pkgver"

  python setup.py build
}

package_python2-pysmb() {
  depends=('python2' 'python2-pyasn1')

  cd "$_pkgname-$pkgver-2"

  python2 setup.py install --prefix=/usr --root="$pkgdir" --optimize=1 \
    --skip-build
}

package_python-pysmb() {
  depends=('python' 'python-pyasn1')

  cd "$_pkgname-$pkgver"

  python setup.py install --prefix=/usr --root="$pkgdir" --optimize=1 \
    --skip-build
}

