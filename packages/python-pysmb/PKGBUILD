# This file is part of BlackArch Linux ( https://www.blackarch.org/ ).
# See COPYING for license details.

pkgbase=python-pysmb
pkgname=('python2-pysmb' 'python-pysmb')
_pkgname=pysmb
pkgver=1.2.5
pkgrel=2
pkgdesc='An experimental SMB/CIFS library written in Python to support file sharing between Windows and Linux machines.'
arch=('any')
url='https://pypi.org/project/pysmb/#files'
license=('GPL')
makedepends=('python2-setuptools' 'python-setuptools')
source=("https://files.pythonhosted.org/packages/48/b3/42d91f1b0950739c49b96fa7ebfb36c3b8b072dbc7d267594e6bcc3ab616/$_pkgname-$pkgver.zip")
sha512sums=('12f0da1e21f174df58005d79cb024b60d19aa441cf14c67650da12bc903445a406a28b142e0a8528cabff1afeef733b107a23e1d6d6d670363757907bdd29b0d')

prepare() {
  cp -a "$_pkgname-$pkgver"{,-2}
}

build() {
  cd "$_pkgname-$pkgver-2"

  python2 setup.py build

  cd "$srcdir/$_pkgname-$pkgver"

  python setup.py build
}

package_python2-pysmb() {
  depends=('python2' 'python2-pyasn1')

  cd "$_pkgname-$pkgver-2"

  python2 setup.py install --prefix=/usr --root="$pkgdir" --optimize=1 \
    --skip-build
}

package_python-pysmb() {
  depends=('python' 'python-pyasn1')

  cd "$_pkgname-$pkgver"

  python setup.py install --prefix=/usr --root="$pkgdir" --optimize=1 \
    --skip-build
}

