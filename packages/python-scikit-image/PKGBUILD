# This file is part of BlackArch Linux ( https://www.blackarch.org/ ).
# See COPYING for license details.

pkgname=python-scikit-image
_pkgname=scikit_image
pkgver=0.24.0
pkgrel=1
pkgdesc='Image processing routines for SciPy.'
arch=('x86_64' 'aarch64')
url='https://pypi.org/project/scikit-image/#files'
license=('BSD')
depends=('python' 'python-numpy' 'python-scipy' 'python-matplotlib'
         'python-numpy' 'python-networkx' 'python-pillow' 'python-imageio'
         'python-pywavelets')
makedepends=('python-pip' 'python-build' 'python-wheel' 'python-numpy')
source=("https://files.pythonhosted.org/packages/source/${_pkgname::1}/$_pkgname/$_pkgname-$pkgver.tar.gz")
sha512sums=('becb062725b4c1f64e12a1f775175e48dbce1b989c1e8f40ec04ff79cb926740be514b9a0a68e3f9e17fb0d3c7b860c7451b91eb178bbae93521e02e3e8e92db')

build() {
  cd "$_pkgname-$pkgver"

  python -m build --wheel --outdir="$startdir/dist"
}

package() {
  cd "$_pkgname-$pkgver"

  pip install \
    --verbose \
    --disable-pip-version-check \
    --no-warn-script-location \
    --ignore-installed \
    --no-compile \
    --no-deps \
    --root="$pkgdir" \
    --prefix=/usr \
    --no-index \
    --find-links="file://$startdir/dist" \
    $_pkgname
}

