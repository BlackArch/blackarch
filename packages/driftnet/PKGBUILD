# This file is part of BlackArch Linux ( http://blackarch.org ).
# See COPYING for license details.

pkgname='driftnet'
pkgver='1.1.5'
pkgrel=2
groups=('blackarch' 'blackarch-scanner' 'blackarch-sniffer')
pkgdesc='Listens to network traffic and picks out images from TCP streams it observes.'
arch=('x86_64' 'armv6h' 'armv7h' 'aarch64')
url='http://www.ex-parrot.com/~chris/driftnet/'
license=('GPL')
depends=('libjpeg' 'libpcap' 'gtk2' 'giflib')
makedepends=('imake')
source=("https://github.com/deiv/driftnet/archive/upstream/${pkgver}.tar.gz"
'gif.patch')
sha1sums=('63e13982d9c9b55f2dbdc8cd0f3963b55ba83778'
          '00255f54855e77e97b9d3153d3eaa254b737a249')

prepare() {
  cd "$srcdir/driftnet-upstream-$pkgver"

  cd src/display

  patch -i $srcdir/gif.patch
}

build() {
  cd "$srcdir/driftnet-upstream-$pkgver"

  autoreconf -fi

  ./configure --prefix=/usr

  make
}

package() {
  cd "$srcdir/driftnet-upstream-$pkgver"

  make DESTDIR="$pkgdir" install
}
