pkgname=driftnet
pkgver=0.1.6
pkgrel=9
groups=('blackarch' 'blackarch-scanner' 'blackarch-misc')
pkgdesc="Listens to network traffic and picks out images from TCP streams it observes"
arch=('i686' 'x86_64')
url='http://www.ex-parrot.com/~chris/driftnet/'
license=('GPL')
depends=('libjpeg' 'libpcap' 'gtk' 'giflib')
makedepends=('imake')
source=("http://www.ex-parrot.com/~chris/driftnet/driftnet-$pkgver.tar.gz"
        'gif.patch')
md5sums=('8e11d77770452f97bb3c23f510489815'
         '000ee0aa95be57c3fc46840b0cf4b2e2')

prepare() {
  cd "$srcdir/driftnet-$pkgver"

  # This library has been combined with giflib.
  sed -i 's/-lungif//' Makefile

  patch < "$srcdir/gif.patch"
}

build() {
  cd "$srcdir/driftnet-$pkgver"
  make LDFLAGS="/usr/lib/libgif.so"
}

package() {
  cd "$srcdir/driftnet-$pkgver"
  install -Dm755 driftnet "$pkgdir/usr/bin/driftnet"
}
