# This file is part of BlackArch Linux ( https://www.blackarch.org/ ).
# See COPYING for license details.

pkgname=driftnet
pkgver=v1.3.0.r13.ge492335
pkgrel=1
epoch=1
groups=('blackarch' 'blackarch-scanner' 'blackarch-sniffer')
pkgdesc='Listens to network traffic and picks out images from TCP streams it observes.'
arch=('x86_64' 'aarch64')
url='http://www.ex-parrot.com/~chris/driftnet/'
license=('GPL2')
depends=('libjpeg' 'libpcap' 'gtk2' 'giflib' 'libwebsockets')
makedepends=('git' 'imake')
source=("git+https://github.com/deiv/$pkgname.git")
sha512sums=('SKIP')

pkgver() {
  cd $pkgname

  git describe --long --tags | sed 's/\([^-]*-g\)/r\1/;s/-/./g'
}

build() {
  cd $pkgname

  autoreconf -fi

  ./configure --prefix=/usr

  make
}

package() {
  cd $pkgname

  make DESTDIR="$pkgdir" install
}

