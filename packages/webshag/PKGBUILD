pkgname=webshag
pkgver=1.10
pkgrel=2
groups=('blackarch' 'blackarch-webapp' 'blackarch-scanner' 'blackarch-fuzzer')
pkgdesc="A multi-threaded, multi-platform web server audit tool."
url="http://www.scrt.ch/en/attack/downloads/webshag"
arch=('i686' 'x86_64' 'armv6h' 'armv7h')
license=('GPL3')
depends=('python2' 'wxpython' 'nmap' 'nikto')
makedepends=('dos2unix')
source=("http://www.scrt.ch/outils/webshag/ws110.tar.gz"
        "webshag.install"
        "core_file.patch")
install='webshag.install'
md5sums=('14c0a579e6fe72ab691e063c06d33ed3'
         'ed696cb2ce1007cf3348a02176b9d791'
         '119237e4ee60abda920bfa07fd570d32')

prepare(){
  cd "$srcdir"

  find . -type f -iname "*" |
  while read i ; do
    dos2unix $i
  done

  grep -iRl python "$srcdir" | xargs sed -i 's|#!.*/usr/bin/python|#!/usr/bin/python2|;s|#!.*/usr/bin/env python$|#!/usr/bin/env python2|'

  cd "webshag/core"
  patch < "$srcdir/core_file.patch"
}

package() {
  cd "$srcdir"

  # Base directories.
  install -dm755 "$pkgdir/usr/share/webshag"
  install -dm755 "$pkgdir/usr/bin"

  cp -a --no-preserve=ownership * "$pkgdir/usr/share/webshag"

  # Bin.
  cat > "$pkgdir/usr/bin/webshag_cli" << EOF
#!/bin/sh
/usr/share/webshag/webshag_cli.py "\$@"
EOF
  chmod +x "$pkgdir/usr/bin/webshag_cli"

cat > "$pkgdir/usr/bin/webshag_gui" << EOF
#!/bin/sh
/usr/share/webshag/webshag_gui.py "\$@"
EOF
  chmod +x "$pkgdir/usr/bin/webshag_gui"
}
