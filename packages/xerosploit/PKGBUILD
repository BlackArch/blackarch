# This file is part of BlackArch Linux ( http://blackarch.org ).
# See COPYING for license details.

pkgname='xerosploit'
pkgver=28.b5dad87
pkgrel=3
pkgdesc='Efficient and advanced man in the middle framework.'
groups=('blackarch' 'blackarch-networking' 'blackarch-exploitation'
        'blackarch-sniffer')
arch=('any')
url='https://github.com/LionSec/xerosploit'
license=('GPL3')
depends=('python2' 'bettercap' 'nmap' 'hping' 'python2-terminaltables'
         'python2-tabulate' 'python2-sphinx_rtd_theme' 'python2-recommonmark')
makedepends=('git')
source=('git+https://github.com/LionSec/xerosploit.git')
sha1sums=('SKIP')

pkgver() {
  cd "$srcdir/xerosploit"

  echo $(git rev-list --count HEAD).$(git rev-parse --short HEAD)
}

package() {
  cd "$srcdir/xerosploit"

  mkdir -p "$pkgdir/usr/bin"
  mkdir -p "$pkgdir/opt/xerosploit/tools"

  install -Dm644 -t "$pkgdir/usr/share/doc/xerosploit/" README.md
  install -Dm644 LICENSE "$pkgdir/usr/share/licenses/xerosploit/LICENSE"

  rm README.md LICENSE

  cp -a * "$pkgdir/opt/xerosploit"

  cat > "$pkgdir/usr/bin/xerosploit" << EOF
#!/bin/sh
cd /opt/xerosploit
exec python2 xerosploit.py "\${@}"
EOF

  chmod a+x "$pkgdir/usr/bin/xerosploit"
}
