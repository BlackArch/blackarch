pkgname=bt_audit
pkgver=0.1.1
pkgrel=4
groups=('blackarch' 'blackarch-bluetooth' 'blackarch-fuzzer')
pkgdesc="Bluetooth audit"
url='http://www.betaversion.net/btdsd/download/'
depends=('bluez-libs')
arch=('i686' 'x86_64' 'armv6h' 'armv7h')
license=('GPL')
conflicts=('bt-audit' 'btaudit')
provides=('bt-audit' 'btaudit')
replaces=('bt-audit' 'btaudit')
source=(http://www.betaversion.net/btdsd/download/bt_audit-$pkgver.tar.gz)
md5sums=('4762a48e93f2f590cbcd343d520ee503')

prepare() {
  cd "$srcdir/bt_audit/src"
  sed -i "s:gcc :gcc $CFLAGS :" Makefile
}

build() {
  cd "$srcdir/bt_audit/src"
  make
}

package() {
  cd "$srcdir/bt_audit/src"
  install -Dm755 psm_scan "$pkgdir/usr/bin/psm_scan"
  install -Dm755 rfcomm_scan "$pkgdir/usr/bin/rfcomm_scan"

  cd ..
  install -Dm644 README "$pkgdir/usr/share/bt_audit/README"
  install -Dm755 scripts/btdsd_sample.sh "$pkgdir/usr/share/bt_audit/scripts/btdsd_sample.sh"
}
