# This file is part of BlackArch Linux ( https://www.blackarch.org/ ).
# See COPYING for license details.

pkgbase=python-face-recognition-models
pkgname=('python2-face-recognition-models' 'python-face-recognition-models')
_pkgname=face_recognition_models
pkgver=0.3.0
pkgrel=2
pkgdesc='Models used by the face_recognition package.'
arch=('any')
url='https://pypi.org/project/face_recognition_models/#files'
depends=('python2')
makedepends=('python2-setuptools')
license=('MIT')
source=("https://files.pythonhosted.org/packages/cf/3b/4fd8c534f6c0d1b80ce0973d01331525538045084c73c153ee6df20224cf/$_pkgname-$pkgver.tar.gz")
sha512sums=('326ab72ee046817e54c75b6260814646f34a3bc7099a8b1167e583a662d76da0adbc71a5a3e71ff6af7783bb643e7642a36a9bbb0d37af3dedc4e19abe3506dd')

prepare() {
  cp -a "$_pkgname-$pkgver"{,-2}
}

build() {
  cd "$_pkgname-$pkgver-2"

  python2 setup.py build

  cd "$srcdir/$_pkgname-$pkgver"

  python setup.py build
}

package_python2-face-recognition-models() {
  cd "$_pkgname-$pkgver-2"

  python2 setup.py install --prefix=/usr --root="$pkgdir" --optimize=1 \
    --skip-build
}

package_python-face-recognition-models() {
  cd "$_pkgname-$pkgver"

  python setup.py install --prefix=/usr --root="$pkgdir" --optimize=1 \
    --skip-build
}

