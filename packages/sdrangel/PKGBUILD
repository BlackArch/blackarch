# This file is part of BlackArch Linux ( https://www.blackarch.org/ ).
# See COPYING for license details.

# Initial PKGBUILD from AUR.
# Old Maintainer: nemanjan00 <nemanjan00+aur@gmail.com>
# Old Contributor: Filipe La√≠ns (FFY00) <filipe.lains@gmail.com>
# Old Contributor: Michal Krenek (Mikos) <m.krenek@gmail.com>

pkgname=sdrangel
pkgver=v7.22.4.r22.gd0cae88
pkgrel=1
pkgdesc='Qt5/OpenGL SDR and signal analyzer frontend.'
arch=('x86_64' 'aarch64')
groups=('blackarch' 'blackarch-radio')
url='https://github.com/f4exb/sdrangel'
license=('GPL-3.0-or-later')
depends=('log4cpp' 'opencv' 'fftw' 'cm256cc' 'dsdcc' 'pulse-native-provider'
         'lz4' 'nanomsg' 'qt5-base' 'qt5-multimedia' 'qt5-websockets'
         'qt5-tools' 'qt5-charts' 'qt5-quickcontrols' 'qt5-quickcontrols2'
         'qt5-serialport' 'qt5-declarative' 'qt5-location' 'qt5-speech'
         'qt5-webengine' 'qt5-gamepad' 'qt5-svg' 'qt5-graphicaleffects')
# libsigmf requires the vendored version at https://github.com/f4exb/libsigmf/tree/new-namespaces, which isn't packaged yet
makedepends=('git' 'cmake' 'boost' 'doxygen' 'graphviz' 'ffmpeg' 'libdab'
             'zlib' 'faad2' 'sgp4' 'aptdec' 'codec2' 'libmirisdr4' 'rtl-sdr'
             'hackrf' 'libiio' 'limesuite' 'bladerf' 'libperseus-sdr' 'airspy'
             'airspyhf' 'libxtrx' 'libuhd')
optdepends=('ffmpeg: DATV demodulator'
            'libdab: DAB demodulator'
            'zlib: DAB demodulator'
            'faad2: DAB demodulator'
            'sgp4: satellite tracker'
            'aptdec: APT (weather satellite) decoder'
            'codec2: FreeDV modulator/demodulator'
            'libmirisdr4<2.0.0: SDRPlay support'
            'rtl-sdr: RTLSDR support'
            'hackrf: HackRF support'
            'libiio: PlutoSDR support'
            'limesuite: LimeSDR support'
            'bladerf: BladeRF support'
            'libperseus-sdr: Perseus SDR support'
            'airspy: Airspy support'
            'airspyhf: Airspy HF+ support'
            'libxtrx: XTRX SDR support'
            'libuhd: USRP support')
source=("git+https://github.com/f4exb/$pkgname.git")
sha512sums=('SKIP')

pkgver() {
  cd $pkgname

  ( set -o pipefail
    git describe --long --tags --abbrev=7 2>/dev/null |
      sed 's/\([^-]*-g\)/r\1/;s/-/./g' ||
    printf "%s.%s" "$(git rev-list --count HEAD)" \
      "$(git rev-parse --short=7 HEAD)"
  )
}

build() {
  cd $pkgname

  # https://bugs.gentoo.org/704322
  export CXXFLAGS="$CXXFLAGS -fpermissive"
  cmake -B build \
    -Wno-dev \
    -DARCH_OPT="" \
    -DCMAKE_BUILD_TYPE=Release \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DLIBDSDCC_INCLUDE_DIR=/usr/include/dsdcc \
    -DCM256CC_INCLUDE_DIR=/usr/include/cm256cc \
    -DENABLE_CHANNELRX_DEMODDATV=OFF \
    -DENABLE_CHANNELTX_MODDATV=OFF
  make -C build
}

package() {
  cd $pkgname

  make -C build DESTDIR="$pkgdir" install
}

