# This file is part of BlackArch Linux ( https://www.blackarch.org/ ).
# See COPYING for license details.

pkgname=subover
pkgver=71.3d258e2
pkgrel=4
groups=('blackarch' 'blackarch-scanner' 'blackarch-recon')
pkgdesc='A Powerful Subdomain Takeover Tool.'
arch=('x86_64' 'aarch64')
url='https://github.com/Ice3man543/SubOver'
license=('BSD')
makedepends=('git' 'go')
source=("$pkgname::git+https://github.com/Ice3man543/SubOver.git")
sha512sums=('SKIP')

pkgver() {
  cd $pkgname

  echo $(git rev-list --count HEAD).$(git rev-parse --short HEAD)
}

build() {
  cd $pkgname

  GOPATH="$srcdir" go mod init go.mod
  GOPATH="$srcdir" go mod tidy
  GOPATH="$srcdir" go mod download
  GOPATH="$srcdir" go build \
    -trimpath \
    -buildmode=pie \
    -mod=readonly \
    -modcacherw \
    -ldflags "-s -w" \
    -o $pkgname .
}

package() {
  cd $pkgname

  install -dm 755 "$pkgdir/usr/bin"
  install -dm 755 "$pkgdir/usr/share/$pkgname"

  install -m 755 -t "$pkgdir/usr/share/$pkgname/" $pkgname
  install -m 644 -t "$pkgdir/usr/share/$pkgname/" providers.json
  install -Dm 644 -t "$pkgdir/usr/share/doc/$pkgname/" README.md

  cat > "$pkgdir/usr/bin/$pkgname" << EOF
#!/bin/sh
cd /usr/share/$pkgname
exec ./$pkgname "\$@"
EOF

  chmod +x "$pkgdir/usr/bin/$pkgname"
}

