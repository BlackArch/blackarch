# This file is part of BlackArch Linux ( http://blackarch.org ).
# See COPYING for license details.

pkgname=subover
pkgver=71.3d258e2
pkgrel=1
groups=('blackarch' 'blackarch-scanner' 'blackarch-recon')
pkgdesc='A Powerful Subdomain Takeover Tool.'
arch=('x86_64' 'armv6h' 'armv7h' 'aarch64')
url='https://github.com/Ice3man543/SubOver'
license=('BSD')
depends=('go')
makedepends=('git')
source=('git+https://github.com/Ice3man543/SubOver.git')
sha512sums=('SKIP')
_url='github.com/Ice3man543/SubOver'

pkgver() {
  cd SubOver

  echo $(git rev-list --count HEAD).$(git rev-parse --short HEAD)
}

build() {
  cd "$srcdir"

  install -dm 755 src/$_url && rm -rf src/$_url && mv SubOver src/$_url && cd src/$_url

  GOPATH="$srcdir" go get -d -t $_url
  GOPATH="$srcdir" go build -v $_url
}

package() {
  cd "$srcdir"

  install -dm 755 "$pkgdir/usr/bin"
  install -dm 755 "$pkgdir/usr/share/subover"

  cd src/$_url

  install -Dm644 -t "$pkgdir/usr/share/doc/subover/" README.md
  install -Dm644 LICENSE "$pkgdir/usr/share/licenses/subover/LICENSE"

  rm README.md LICENSE

  cp -a * "$pkgdir/usr/share/subover/"

  cat > "$pkgdir/usr/bin/subover" << EOF
#!/bin/sh
cd /usr/share/subover
exec ./SubOver "\${@}"
EOF

  chmod a+x "$pkgdir/usr/bin/subover"
}
