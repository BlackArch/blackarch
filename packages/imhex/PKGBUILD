# This file is part of BlackArch Linux ( https://www.blackarch.org/ ).
# See COPYING for license details.
#
# AUR Maintainer: Alex Sarum <rum.274.4 at gmail dot com>

pkgname=imhex
pkgver=1.7.0.r124.g4029c33
pkgrel=1
pkgdesc='A Hex Editor for Reverse Engineers, Programmers and people that value their eye sight when working at 3 AM.'
arch=('x86_64')
groups=('blackarch' 'blackarch-misc')
url='https://github.com/WerWolv/ImHex'
license=('GPL2')
depends=('glfw' 'glm' 'capstone' 'llvm-libs' 'llvm' 'nlohmann-json' 'python')
makedepends=('git' 'cmake' 'freetype2')
source=("$pkgname::git+https://github.com/WerWolv/ImHex.git")
sha256sums=('SKIP')

pkgver() {
  cd $pkgname

  git describe --long --tags | sed 's/^v//;s/\([^-]*-g\)/r\1/;s/-/./g'
}

build() {
  cd $pkgname

  cmake -B build -DCMAKE_INSTALL_PREFIX=/usr
  make -C build
}

package() {
  cd $pkgname

  install -Dm 755 build/ImHex "$pkgdir/usr/bin/$pkgname"
  install -Dm 644 -t "$pkgdir/usr/share/doc/$pkgname/" README.md
}

