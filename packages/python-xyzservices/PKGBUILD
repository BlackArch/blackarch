# This file is part of BlackArch Linux ( https://www.blackarch.org/ ).
# See COPYING for license details.

pkgname=python-xyzservices
_pkgname=${pkgname#python-}
pkgver=2024.4.0
pkgrel=1
pkgdesc='Provides a repository of available XYZ services offering raster basemap tiles.'
arch=('any')
url="https://github.com/geopandas/xyzservices"
license=('BSD-3-Clause')
depends=('python')
makedepends=(
  'python-setuptools-scm'
  'python-wheel'
  'python-build'
  'python-installer'
)
source=("$pkgname-$pkgver.tar.gz::$url/releases/download/$pkgver/$_pkgname-$pkgver.tar.gz")
sha512sums=('2a824ba41570305a47dac5a5b5ee8f2be685c3d1a92b3c88c75538e93b819cef216867e85e6043ec821c8dd5bc34a11b7c4f9cfe9016d4b984eaa0317a20b3fd')

build() {
  cd "$_pkgname-$pkgver"

  python -m build --wheel --no-isolation
}

package() {
  cd "$_pkgname-$pkgver"

  python -m installer --destdir="$pkgdir" dist/*.whl

  install -Dm 644 LICENSE "$pkgdir/usr/share/licenses/$pkgname/LICENSE"
}

