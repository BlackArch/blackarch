# This file is part of BlackArch Linux ( http://blackarch.org ).
# See COPYING for license details.

pkgname='netzob'
pkgver='1.0.2'
pkgrel=1
pkgdesc='An open source tool for reverse engineering, traffic generation and fuzzing of communication protocols.'
groups=('blackarch' 'blackarch-reversing' 'blackarch-fuzzer'
        'blackarch-networking')
arch=('i686' 'x86_64' 'armv6h' 'armv7h' 'aarch64')
url='http://www.netzob.org/'
license=('GPL3')
depends=('python' 'python-gobject' 'gtk3' 'impacket' 'libxml2' 'libxslt'
        'python-lxml' 'graphviz' 'python-bitarray' 'python-jsonpickle'
        'python-netaddr' 'python-numpy' 'python-colorama' 'python-bintrees')
makedepends=('python-setuptools')
source=("https://github.com/netzob/netzob/archive/Release/netzob-${pkgver}.tar.gz")
sha1sums=('d78279ddf4d77f72993728a2980bd180d5a6265c')

build() {
  cd "$srcdir/netzob-Release-netzob-$pkgver/netzob"

  python3 setup.py build
}

package() {
  cd "$srcdir/netzob-Release-netzob-$pkgver/netzob"

  install -Dm644 -t "$pkgdir/usr/share/doc/netzob/" AUTHORS.txt README.rst \
    NEWS.rst
  install -Dm644 COPYING.txt "$pkgdir/usr/share/licenses/netzob/COPYING.txt"

  python3 setup.py install --root="$pkgdir" --optimize=1

  find "$pkgdir/usr/share" -type f -exec chmod 644 {} \;

  chmod 644 \
    "$pkgdir"/usr/lib/python*/site-packages/Netzob-${pkgver}-py*.egg-info/*
}
