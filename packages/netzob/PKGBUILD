# This file is part of BlackArch Linux ( https://www.blackarch.org/ ).
# See COPYING for license details.

pkgname=netzob
pkgver=4125.9b64f495
pkgrel=1
pkgdesc='An open source tool for reverse engineering, traffic generation and fuzzing of communication protocols.'
groups=('blackarch' 'blackarch-reversing' 'blackarch-fuzzer'
        'blackarch-networking')
arch=('x86_64' 'armv6h' 'armv7h' 'aarch64')
url='http://www.netzob.org/'
license=('GPL3')
depends=('python' 'python-gobject' 'gtk3' 'impacket' 'libxml2' 'libxslt'
        'python-lxml' 'graphviz' 'python-bitarray' 'python-jsonpickle'
        'python-netaddr' 'python-numpy' 'python-colorama' 'python-bintrees'
        'python-pcapy' 'python-werkzeug' 'python-pylstar' 'python-mock'
        'python-flask' 'python-arpreq')
makedepends=('python-setuptools')
source=('git+https://github.com/netzob/netzob.git')
sha512sums=('SKIP')

pkgver() {
  cd $pkgname

  echo $(git rev-list --count HEAD).$(git rev-parse --short HEAD)
}

build() {
  cd "$pkgname-Release-netzob/$pkgname"

  python3 setup.py build
}

package() {
  cd "$pkgname-Release-netzob/$pkgname"

  install -Dm 644 -t "$pkgdir/usr/share/doc/$pkgname/" AUTHORS.txt README.rst \
    NEWS.rst
  install -Dm 644 COPYING.txt "$pkgdir/usr/share/licenses/$pkgname/COPYING"

  python3 setup.py install --root="$pkgdir" --optimize=1 --skip-build

  find "$pkgdir/usr/share" -type f -exec chmod 644 {} \;

  chmod 644 \
    "$pkgdir"/usr/lib/python*/site-packages/Netzob-${pkgver}-py*.egg-info/*
}

