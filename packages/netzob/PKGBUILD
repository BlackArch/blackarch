# This file is part of BlackArch Linux ( http://blackarch.org ).
# See COPYING for license details.

pkgname='netzob'
pkgver='1.0rc1'
pkgrel=2
pkgdesc='An open source tool for reverse engineering, traffic generation and fuzzing of communication protocols.'
groups=('blackarch' 'blackarch-reversing' 'blackarch-fuzzer'
        'blackarch-networking')
arch=('i686' 'x86_64' 'armv6h' 'armv7h')
url='http://www.netzob.org/'
license=('GPL3')
depends=('python2' 'python2-gobject' 'gtk3' 'impacket' 'libxml2' 'libxslt'
        'python2-lxml' 'graphviz' 'python2-bitarray' 'python2-jsonpickle'
        'python2-netaddr' 'python2-numpy' 'python2-colorama' 'python2-bintrees')
makedepends=('setuptools')
source=("https://www.netzob.org/repository/${pkgver}/Netzob-${pkgver}.tar.gz")
sha1sums=('46b46202d9a0b9781ed8330eedefded538f30622')

build() {
  cd "$srcdir/Netzob-$pkgver"

  python2 setup.py build
}

package() {
  cd "$srcdir/Netzob-$pkgver"

  install -Dm644 -t "$pkgdir/usr/share/doc/netzob/" AUTHORS.txt README.rst \
    NEWS.rst
  install -Dm644 COPYING.txt "$pkgdir/usr/share/licenses/netzob/COPYING.txt"

  python2 setup.py install --root="$pkgdir" --optimize=1

  find "$pkgdir/usr/share" -type f -exec chmod 644 {} \;

  chmod 644 \
    "$pkgdir"/usr/lib/python2.7/site-packages/Netzob-${pkgver}-py2.7.egg-info/*
}
