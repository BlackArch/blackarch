# This file is part of BlackArch Linux ( https://www.blackarch.org/ ).
# See COPYING for license details.

pkgname=python-dploot
_pkgname=${pkgname#python-}
pkgver=3.1.2
pkgrel=1
pkgdesc='DPAPI looting remotely.'
arch=('any')
url='https://github.com/zblurx/dploot'
license=('MIT')
depends=('python' 'impacket-ba' 'python-cryptography' 'python-pyasn1'
         'python-lxml')
makedepends=('python-build' 'python-installer' 'python-setuptools'
             'python-wheel' 'python-poetry')
options=(!emptydirs)
source=("https://files.pythonhosted.org/packages/source/${_pkgname::1}/$_pkgname/$_pkgname-$pkgver.tar.gz")
sha512sums=('2e9a45779a2df8daeffe3aa6d8d6edc8c4e8dab047d1697e02d004e6a07d8575f88cc4ddc3a06f3082440ab447d932564c06fd5c90bef793937066d4faed720e')

build() {
  cd "$_pkgname-$pkgver"

  python -m build --wheel --no-isolation
}

package() {
  cd "$_pkgname-$pkgver"

  python -m installer --destdir="$pkgdir" dist/*.whl
}

