# This file is part of BlackArch Linux ( https://www.blackarch.org/ ).
# See COPYING for license details.

pkgbase=python-dateparser
pkgname=('python2-dateparser' 'python-dateparser')
_pkgname=dateparser
pkgver=1.1.7
pkgrel=1
pkgdesc='Date parsing library designed to parse dates from HTML pages.'
url='https://pypi.org/project/dateparser/#files'
arch=('any')
license=('BSD')
makedepends=('python2-setuptools' 'python-setuptools')
source=("https://files.pythonhosted.org/packages/source/${_pkgname::1}/$_pkgname/$_pkgname-$pkgver.tar.gz")
sha512sums=('d333fa2007c069f7424def9694544767a8885eec5d476e9dd73605754fd53f5a9dbbeb19f24291ac6c6ffa09b4014a3135ad8fbabadae1655c78a8d5fe36f907')

prepare() {
  cp -a "$_pkgname-$pkgver"{,-2}
}

build() {
  cd "$_pkgname-$pkgver-2"

  python2 setup.py build

  cd "$srcdir/$_pkgname-$pkgver"

  python setup.py build
}

package_python2-dateparser() {
  depends=('python2' 'python2-pytz' 'python2-tzlocal' 'python2-dateutil'
           'python2-regex')

  cd "$_pkgname-$pkgver-2"

  python2 setup.py install --prefix=/usr --root="$pkgdir" --optimize=1 \
    --skip-build
}

package_python-dateparser() {
  depends=('python' 'python-pytz' 'python-tzlocal' 'python-dateutil'
           'python-regex')

  cd "$_pkgname-$pkgver"

  python setup.py install --prefix=/usr --root="$pkgdir" --optimize=1 \
    --skip-build
}

