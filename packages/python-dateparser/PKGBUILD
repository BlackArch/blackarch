# This file is part of BlackArch Linux ( https://www.blackarch.org/ ).
# See COPYING for license details.

pkgbase=python-dateparser
pkgname=('python2-dateparser' 'python-dateparser')
_pkgname=dateparser
pkgver=0.7.1
pkgrel=2
pkgdesc='Date parsing library designed to parse dates from HTML pages.'
url='https://pypi.org/project/dateparser/#files'
arch=('any')
license=('BSD')
makedepends=('python2-setuptools' 'python-setuptools')
source=("https://files.pythonhosted.org/packages/51/6f/3bf59d1cfd7845a8614bae2c2ccd540074695015210285127aab9088ea14/$_pkgname-$pkgver.tar.gz")
sha512sums=('afe5be78e156defbd8b8a917afd9b89909c46b9b680e1c300a8e50cfaea84b481ede5eb96185a9ad25ef48066a4b749612d2bcba48b505760ac9ae631d02b1ad')

prepare() {
  cp -a "$_pkgname-$pkgver"{,-2}
}

build() {
  cd "$_pkgname-$pkgver-2"

  python2 setup.py build

  cd "$srcdir/$_pkgname-$pkgver"

  python setup.py build
}

package_python2-dateparser() {
  depends=('python2' 'python2-pytz' 'python2-tzlocal' 'python2-dateutil'
           'python2-regex')

  cd "$_pkgname-$pkgver-2"

  python2 setup.py install --prefix=/usr --root="$pkgdir" --optimize=1 \
    --skip-build
}

package_python-dateparser() {
  depends=('python' 'python-pytz' 'python-tzlocal' 'python-dateutil'
           'python-regex')

  cd "$_pkgname-$pkgver"

  python setup.py install --prefix=/usr --root="$pkgdir" --optimize=1 \
    --skip-build
}

