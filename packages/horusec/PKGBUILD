# This file is part of BlackArch Linux ( https://www.blackarch.org/ ).
# See COPYING for license details.

pkgname=horusec
pkgver=v2.7.0.rc.3.r78.ga8aab119
pkgrel=1
pkgdesc='Static code analysis to identify security flaws for many languages.'
arch=('x86_64' 'aarch64')
groups=('blackarch' 'blackarch-code-audit')
url='https://github.com/ZupIT/horusec'
license=('Apache')
depends=()
makedepends=('git' 'go')
source=("git+https://github.com/ZupIT/$pkgname.git")
sha512sums=('SKIP')

pkgver() {
  cd $pkgname

  # use --match to ignore alpha tags
  git describe --long --tags --match='v*.*.*' | sed 's/\([^-]*-g\)/r\1/;s/-/./g'
}

build() {
  cd $pkgname

  GOPATH="$srcdir" go mod download
  GOPATH="$srcdir" go build \
    -trimpath \
    -buildmode=pie \
    -mod=readonly \
    -modcacherw \
    -ldflags "-s -w" \
    -o $pkgname ./cmd/app
}

package() {
  cd $pkgname

  install -Dm 755 $pkgname "$pkgdir/usr/bin/$pkgname"
  install -Dm 644 -t "$pkgdir/usr/share/doc/$pkgname/" README.md
}

