# This file is part of BlackArch Linux ( https://www.blackarch.org/ ).
# See COPYING for license details.

_gemname=ctf-party
pkgname=ruby-$_gemname
pkgver=1.0.0
pkgrel=1
pkgdesc='A library to enhance and speed up script/exploit writting for CTF players'
arch=('x86_64')
url='https://orange-cyberdefense.github.io/ctf-party/'
license=('MIT')
depends=('ruby')
conflicts=('ruby-ctf-party-git')
provides=('ruby-ctf-party')
options=(!emptydirs)
source=(https://rubygems.org/downloads/$_gemname-$pkgver.gem)
noextract=($_gemname-$pkgver.gem)
b2sums=('61943631526f42780db308ffd285dc84ef5d9ab5a56568e2ed3f08ea39aecdb0f888f0d1aa59bdf34548a5e4b0494410af177b47a8cf7510e830a38d78524f4b')

package() {
  local _gemdir="$(ruby -e'puts Gem.default_dir')"
  gem install --ignore-dependencies --no-user-install --no-document -i "$pkgdir/$_gemdir" -n "$pkgdir/usr/bin" $_gemname-$pkgver.gem
  rm "$pkgdir/$_gemdir/cache/$_gemname-$pkgver.gem"
  find "$pkgdir/$_gemdir/extensions/" -name *.so -delete
  rm -r "$pkgdir/$_gemdir/gems/$_gemname-$pkgver/test"
  install -D -m644 "$pkgdir/$_gemdir/gems/$_gemname-$pkgver/LICENSE.txt" "$pkgdir/usr/share/licenses/$pkgname/LICENSE"
}
