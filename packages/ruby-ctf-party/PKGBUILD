# This file is part of BlackArch Linux ( https://www.blackarch.org/ ).
# See COPYING for license details.

pkgname=ruby-ctf-party
_gemname=ctf-party
pkgver=1.0.0
pkgrel=1
groups=('blackarch' 'blackarch-misc')
pkgdesc='A library to enhance and speed up script/exploit writting for CTF players'
arch=('any')
url='https://orange-cyberdefense.github.io/ctf-party/'
license=('MIT')
depends=('ruby')
options=(!emptydirs)
source=("https://rubygems.org/downloads/$_gemname-$pkgver.gem")
noextract=("$_gemname-$pkgver.gem")
sha512sums=('b965f544ad4f903108d151298b75b7573ad705d9455cfb8a314f94118099e6f596cb2fd736c6ae07ff12c4c7bcbc556fbc78706e87e6767c2f49683ac7697192')

package() {
  _gemdir="$(ruby -e'puts Gem.default_dir')"

  gem install --ignore-dependencies --no-user-install --no-document \
    -i "$pkgdir/$_gemdir" -n "$pkgdir/usr/bin" $_gemname-$pkgver.gem

  rm "$pkgdir/$_gemdir/cache/$_gemname-$pkgver.gem"
  find "$pkgdir/$_gemdir/extensions/" -name *.so -delete
  rm -r "$pkgdir/$_gemdir/gems/$_gemname-$pkgver/test"

  install -Dm 644 "$pkgdir/$_gemdir/gems/$_gemname-$pkgver/LICENSE.txt" \
    "$pkgdir/usr/share/licenses/$pkgname/LICENSE"
}

