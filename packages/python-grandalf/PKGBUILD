# This file is part of BlackArch Linux ( https://www.blackarch.org/ ).
# See COPYING for license details.

pkgbase=python-grandalf
pkgname=('python2-grandalf' 'python-grandalf')
_pkgname=grandalf
pkgver=0.7
pkgrel=2
pkgdesc='Graph and drawing algorithms framework.'
arch=('any')
url='https://pypi.org/project/grandalf/#files'
license=('GPL2')
makedepends=('python2-setuptools' 'python-setuptools')
source=("https://github.com/bdcht/grandalf/archive/v$pkgver.tar.gz")
sha512sums=('a129e7bd6f0d2a88749bf72c5e03ba917cd28be505bceac07c4c60e0c1a2fff5970d0f48ae760c1936944965b5fdb3cdd4e00cf331094cb97431283f0298fdba')

prepare() {
  cp -a "$_pkgname-$pkgver"{,-2}
}

build() {
  cd "$_pkgname-$pkgver-2"

  python2 setup.py build

  cd "$srcdir/$_pkgname-$pkgver"

  python setup.py build
}

package_python2-grandalf() {
  depends=('python2' 'python2-future' 'python2-pyparsing')

  cd "$_pkgname-$pkgver-2"

  python2 setup.py install --root="$pkgdir" --prefix=/usr -O1 --skip-build
}

package_python-grandalf() {
  depends=('python' 'python-future' 'python-pyparsing')

  cd "$_pkgname-$pkgver"

  python setup.py install --root="$pkgdir" --prefix=/usr -O1 --skip-build
}

