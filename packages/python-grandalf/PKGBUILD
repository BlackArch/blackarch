# This file is part of BlackArch Linux ( https://www.blackarch.org/ ).
# See COPYING for license details.

pkgbase=python-grandalf
pkgname=('python2-grandalf' 'python-grandalf')
_pkgname=grandalf
pkgver=0.6
pkgrel=5
pkgdesc='Graph and drawing algorithms framework.'
arch=('any')
url='https://pypi.org/project/grandalf/#files'
license=('GPL2')
makedepends=('python2-setuptools' 'python-setuptools')
source=("https://files.pythonhosted.org/packages/a2/3f/df0618a962a1744e932f2a4547cb786f5a93df7e2476c99e7f7dbd68039f/$_pkgname-$pkgver.tar.gz")
sha512sums=('d7ca2c8bf2b7b7330aebb803af387e8daf8c20be54b4a971ff7ffdcd2042e0c8af918a6d6dfa91ab269d4d5a1cd44a9cea9fc84b3ca4bc8a02712644e228ed0f')

prepare() {
  cp -a "$_pkgname-$pkgver"{,-2}
}

build() {
  cd "$_pkgname-$pkgver-2"

  python2 setup.py build

  cd "$srcdir/$_pkgname-$pkgver"

  python setup.py build
}

package_python2-grandalf() {
  depends=('python2' 'python2-future' 'python2-pyparsing')

  cd "$_pkgname-$pkgver-2"

  python2 setup.py install --prefix=/usr --root="$pkgdir" --optimize=1 \
    --skip-build
}

package_python-grandalf() {
  depends=('python' 'python-future' 'python-pyparsing')

  cd "$_pkgname-$pkgver"

  python setup.py install --prefix=/usr --root="$pkgdir" --optimize=1 \
    --skip-build
}

