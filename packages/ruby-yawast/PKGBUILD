# Maintainer: lsfxz (https://github.com/lsfxz/quarry)
# Many thanks to anatol for quarry and gem2arch!
# This file is part of BlackArch Linux ( http://blackarch.org ).
# See COPYING for license details.

_gemname=yawast
_gemver=0.5.0.beta2
pkgname=ruby-$_gemname
pkgver=366.9c9a9b3
pkgrel=1
pkgdesc=The\ YAWAST\ Antecedent\ Web\ Application\ Security\ Toolkit
arch=(any)
url=https://github.com/adamcaudill/yawast
license=(MIT)
depends=(ruby ruby-colorize ruby-commander ruby-highline ruby-ipaddr_extensions ruby-ipaddress ruby-openssl-extensions ruby-ssllabs)
makedepends=('git')
groups=(blackarch blackarch-webapp blackarch-scanner blackarch-fuzzer)
replaces=(yawast)
conflicts=(yawast)
provides=(yawast)
source=('git+https://github.com/adamcaudill/yawast.git')
options=(!emptydirs)
conflicts=(ruby-yawast)
sha1sums=('SKIP')

pkgver() {
	cd "$srcdir/$_gemname"

  echo $(git rev-list --count HEAD).$(git rev-parse --short HEAD)
}

build() {
  cd "$srcdir/$_gemname"

  gem build $_gemname.gemspec
}

package() {
  cd "$srcdir/$_gemname"

  local _gemdir="$(ruby -e'puts Gem.default_dir')"

  gem install --ignore-dependencies --no-document --no-user-install -i "$pkgdir/$_gemdir" -n "$pkgdir"/usr/bin $_gemname-$_gemver.gem
  rm "$pkgdir/$_gemdir/cache/$_gemname-$_gemver.gem"
  local _extdir="$pkgdir/$_gemdir/extensions/$CARCH-linux/2.3.0/$_gemname-$_gemver"
  if [ -d "$_extdir" ]; then
    rm -rf "$_extdir"/*
    touch "$_extdir/gem.build_complete"
  fi

  rm -rf "$pkgdir/$_gemdir/gems/$_gemname-$_gemver"/{.codeclimate.yml,.gitignore,.rubocop.yml,.ruby-version,.travis.yml,CHANGELOG.md,Gemfile,README.md,Rakefile,test,yawast.gemspec}
}
