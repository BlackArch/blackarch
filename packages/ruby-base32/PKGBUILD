# This file is part of BlackArch Linux ( http://blackarch.org ).
# See COPYING for license details.

_gemname='base32'
pkgname=ruby-base32
pkgver=0.3.2
pkgrel=4
pkgdesc='Ruby extension for base32 encoding and decoding.'
arch=('any')
url='https://github.com/stesla/base32'
license=('MIT')
depends=('ruby')
options=(!emptydirs)
source=("https://rubygems.org/downloads/${_gemname}-${pkgver}.gem")
noextract=("${_gemname}-${pkgver}.gem")
sha512sums=('9d66961774b9d5923d099229f80ceacec95d40d5d6d3842fda33ff6bf4070f9dc17d758a6cbc93a6200f4e0105bc60175f70d057469e684f01910ea07fd85a2f')

package() {
  cd "$srcdir"

  local _gemdir="$(ruby -e'puts Gem.default_dir')"

  gem install --ignore-dependencies --no-user-install -i "$pkgdir/$_gemdir" \
    -n "$pkgdir/usr/bin" ${_gemname}-${pkgver}.gem

  rm "$pkgdir/$_gemdir/cache/${_gemname}-${pkgver}.gem"

  install -Dm644 "$pkgdir/$_gemdir/gems/${_gemname}-$pkgver/LICENSE" \
    "$pkgdir/usr/share/licenses/ruby-base32/LICENSE"
}
