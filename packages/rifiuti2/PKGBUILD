# This file is part of BlackArch Linux ( https://www.blackarch.org/ ).
# See COPYING for license details.

pkgname=rifiuti2
pkgver=0.6.1
pkgrel=1
epoch=1
groups=('blackarch' 'blackarch-forensic' 'blackarch-recon')
pkgdesc='A rewrite of rifiuti, a great tool from Foundstone folks for analyzing Windows Recycle Bin INFO2 file.'
url="https://code.google.com/p/rifiuti2/"
arch=('x86_64' 'armv6h' 'armv7h' 'aarch64')
license=('custom:BSD')
conflicts=('rifiuti')
depends=('glib2')
makedepends=('git')
source=("https://github.com/abelcheung/rifiuti2/releases/download/0.6.1/rifiuti2-$pkgver.tar.xz")
sha512sums=('1c99466d0488e45d82d136bf2faac3a2ac1b2be581db07ca6d0b0d3c646b4b630062760fe04b39d19f84a9a35bd1488bc4c8d55e97efe49ddcd95149eff473df')

build() {
  cd "$pkgname-$pkgver"

  ./configure --prefix=/usr

  make
}

package() {
  cd "$pkgname-$pkgver"

  make DESTDIR="$pkgdir" install

  install -Dm 644 docs/LICENSE "$pkgdir/usr/share/licenses/$pkgname/LICENSE"
  rm docs/LICENSE
  install -Dm 644 -t "$pkgdir/usr/share/doc/$pkgname/" NEWS.md ChangeLog docs/*
}

