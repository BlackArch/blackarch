# This file is part of BlackArch Linux ( http://blackarch.org ).
# See COPYING for license details.

pkgname='mara-framework'
pkgver=103.dc90e06
pkgrel=1
pkgdesc='A Mobile Application Reverse engineering and Analysis Framework.'
groups=('blackarch' 'blackarch-mobile' 'blackarch-reversing'
        'blackarch-disassembler')
arch=('any')
url='https://github.com/xtiankisutsa/MARA_Framework'
license=('GPL3')
depends=('bash' 'tree' 'git' 'gtk2' 'python2' 'python3' 'java-environment'
         'figlet' 'python2-pydot')
makedepends=('git')
source=('git+https://github.com/xtiankisutsa/MARA_Framework.git')
sha1sums=('SKIP')

pkgver() {
  cd "$srcdir/MARA_Framework"

  echo $(git rev-list --count HEAD).$(git rev-parse --short HEAD)
}

package() {
  cd "$srcdir/MARA_Framework"

  mkdir -p "$pkgdir/usr/bin"
  mkdir -p "$pkgdir/usr/share/mara-framework"

  install -Dm644 -t "$pkgdir/usr/share/doc/mara-framework/" README.md \
    documentation/*
  install -Dm644 LICENSE "$pkgdir/usr/share/licenses/mara-framework/LICENSE"

  rm -rf documentation README.md LICENSE

  cp -a * "$pkgdir/usr/share/mara-framework"

  cat > "$pkgdir/usr/bin/mara-framework" << EOF
#!/bin/sh
cd /usr/share/mara-framework
exec sh mara.sh "\${@}"
EOF

  chmod a+x "$pkgdir/usr/bin/mara-framework"
}
