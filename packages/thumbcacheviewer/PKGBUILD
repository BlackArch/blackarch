# This file is part of BlackArch Linux ( https://www.blackarch.org/ ).
# See COPYING for license details.

pkgname=thumbcacheviewer
_pkgname=thumbcache_viewer
pkgver=1.0.3.6
pkgrel=1
groups=('blackarch' 'blackarch-forensic' 'blackarch-windows')
pkgdesc='Extract Windows thumbcache database files.'
arch=('any')
url='https://thumbcacheviewer.github.io/'
license=('custom:unknown')
depends=('wine' 'wine_gecko' 'wine-mono')
source=("https://github.com/$pkgname/$pkgname/releases/download/v$pkgver/${_pkgname}_64.zip")
sha512sums=('6e582091c02a49e59bb04bb8db75f7f7a515e9f26a5015d04acf560180613656647699bacec1c268c71e03e96f359a4cc1f08029e2eb18a9acd5a989ddb8d5d2')

package() {
  install -dm 755 "$pkgdir/usr/bin"
  install -dm 755 "$pkgdir/usr/share/windows/$pkgname"

  cp --no-preserve=ownership "$_pkgname.exe" \
    "$pkgdir/usr/share/windows/$pkgname/"

  cat > "$pkgdir/usr/bin/$pkgname" << EOF
#!/bin/sh
cd /usr/share/windows/$pkgname
exec wine "$_pkgname.exe" "\$@"
EOF

  chmod +x "$pkgdir/usr/bin/$pkgname"
}

