# This file is part of BlackArch Linux ( https://www.blackarch.org/ ).
# See COPYING for license details.

pkgbase=python-python-libnmap
pkgname=('python2-python-libnmap' 'python-python-libnmap')
_pkgname=python-libnmap
pkgver=0.7.2
pkgrel=1
pkgdesc='Python NMAP library enabling you to start async nmap tasks, parse and compare/diff scan results.'
arch=('any')
url='https://pypi.org/project/python-libnmap/#files'
license=('custom:unknown')
makedepends=('python2-setuptools' 'python-setuptools')
source=("https://files.pythonhosted.org/packages/dc/85/71decec86adbee284469ed8e3c2e7202691ccbce513bfd19639576077dc8/$_pkgname-$pkgver.tar.gz")
sha512sums=('e1ecca1b7c658f062cf919f66b592f65f8e3a0569eb46c470f42203e3e2732eadc02b0bc10e0b3737750c540658202dfe17c99f21b27c977bc57067a24ec552e')

prepare() {
  cp -a "$_pkgname-$pkgver"{,-2}
}

build() {
  cd "$_pkgname-$pkgver-2"

  python2 setup.py build

  cd "$srcdir/$_pkgname-$pkgver"

  python setup.py build
}

package_python2-python-libnmap() {
  depends=('python2')

  cd "$_pkgname-$pkgver-2"

  python2 setup.py install --prefix=/usr --root="$pkgdir" --optimize=1 \
    --skip-build
}

package_python-python-libnmap() {
  depends=('python')

  cd "$_pkgname-$pkgver"

  python setup.py install --prefix=/usr --root="$pkgdir" --optimize=1 \
    --skip-build
}

