# This file is part of BlackArch Linux ( https://www.blackarch.org/ ).
# See COPYING for license details.

pkgname=mingw-w64-headers
pkgver=6.0.0
_pkgver=${pkgver/rc/-rc}
pkgrel=2
pkgdesc='MinGW-w64 headers for Windows.'
arch=('any')
url='http://mingw-w64.sourceforge.net/'
license=('custom:unknown')
options=('!strip' '!libtool' '!emptydirs')
source=("https://sourceforge.net/projects/mingw-w64/files/mingw-w64/mingw-w64-release/mingw-w64-v$_pkgver.tar.bz2"{,.sig})
sha256sums=('805e11101e26d7897fce7d49cbb140d7bac15f3e085a91e0001e80b2adaf48f0'
            'SKIP')
validpgpkeys=('CAF5641F74F7DFBA88AE205693BDB53CD4EBC740')
_targets='i686-w64-mingw32 x86_64-w64-mingw32'

build() {
  for t in $_targets ; do
    install -dm 755 "$srcdir/headers-$t"

    cd "$srcdir/headers-$t"

    "$srcdir/mingw-w64-v$_pkgver/mingw-w64-headers/"configure --prefix=/usr/$t \
      --enable-sdk=all --enable-secure-api --host=$t
  done
}

package() {
  for t in $_targets ; do
    cd "$srcdir/headers-$t"

    make DESTDIR="$pkgdir" install

    rm "$pkgdir/usr/$t/include/pthread_signal.h"
    rm "$pkgdir/usr/$t/include/pthread_time.h"
    rm "$pkgdir/usr/$t/include/pthread_unistd.h"
  done

  install -Dm 644 \
    "$srcdir/mingw-w64-v$_pkgver/COPYING.MinGW-w64/COPYING.MinGW-w64.txt" \
    "$pkgdir/usr/share/licenses/$pkgname/COPYING.MinGW-w64.txt"

  install -Dm 644 \
    "$srcdir/mingw-w64-v$_pkgver/COPYING.MinGW-w64-runtime/COPYING.MinGW-w64-runtime.txt" \
    "$pkgdir/usr/share/licenses/$pkgname/COPYING.MinGW-w64-runtime.txt"

  install -Dm 644 \
    "$srcdir/mingw-w64-v$_pkgver/mingw-w64-headers/ddk/readme.txt" \
    "$pkgdir/usr/share/licenses/$pkgname/ddk-readme.txt"

  install -Dm 644 \
    "$srcdir/mingw-w64-v$_pkgver/mingw-w64-headers/direct-x/COPYING.LIB" \
    "$pkgdir/usr/share/licenses/$pkgname/direct-x-COPYING.LIB"

  install -Dm 644 \
    "$srcdir/mingw-w64-v$_pkgver/mingw-w64-headers/direct-x/readme.txt" \
    "$pkgdir/usr/share/licenses/$pkgname/direct-x-readme.txt"
}

