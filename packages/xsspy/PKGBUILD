# This file is part of BlackArch Linux ( http://blackarch.org ).
# See COPYING for license details.

pkgname='xsspy'
pkgver=52.5d66c3d
pkgrel=1
pkgdesc='Web Application XSS Scanner.'
groups=('blackarch' 'blackarch-webapp' 'blackarch-scanner')
arch=('any')
url='https://github.com/faizann24/XssPy'
license=('custom:unknown')
depends=('python2' 'python2-mechanize')
makedepends=('git')
source=('git+https://github.com/faizann24/XssPy.git')
sha1sums=('SKIP')

pkgver() {
  cd "$srcdir/XssPy"

  echo $(git rev-list --count HEAD).$(git rev-parse --short HEAD)
}

prepare() {
  cd "$srcdir/XssPy"

  sed -i '1 i\#!/usr/bin/python2' XssPy.py
}

package() {
  cd "$srcdir/XssPy"

  install -Dm755 XssPy.py "$pkgdir/usr/bin/xsspy"
  install -Dm644 README.md "$pkgdir/usr/share/doc/xsspy/README.md"
}
