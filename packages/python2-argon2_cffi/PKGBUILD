# This file is part of BlackArch Linux ( https://www.blackarch.org/ ).
# See COPYING for license details.

pkgname=python2-argon2_cffi
_pkgname=argon2-cffi
pkgver=19.2.0
pkgrel=1
pkgdesc='CFFI-based Argon2 Bindings for Python.'
arch=('x86_64' 'aarch64')
url='https://github.com/hynek/argon2_cffi'
license=('MIT')
depends=('argon2' 'python2-cffi' 'python2-six')
makedepends=('git' 'python2-setuptools')
_commit=0ccdbe2af4c7139f0abbb6a03bcb9f7605b16dca  # tags/19.2.0^0
source=("https://files.pythonhosted.org/packages/e4/96/f1bf2369f29794971f836b8eff5e3bdb653043f1b61d104eae21b1de3ccb/$_pkgname-$pkgver.tar.gz"
        "git+https://github.com/p-h-c/phc-winner-argon2")
sha512sums=('05e2eea18bb2fc2ab0c7174081634d6c2a227f14f64a2686b369e1bf2c69721149282c2572f1e4044c3f3a3e93003db8aed174aa64d377a145d6a81cc1f52fd2'
            'SKIP')

prepare() {
  cd phc-winner-argon2

  git submodule init
  git config --local submodule.extras/libargon2.url .
  git submodule update
}

build() {
  cd "$_pkgname-$pkgver"

  python2 setup.py build
}

package() {
  cd "$_pkgname-$pkgver"

  python2 setup.py install --root="$pkgdir" --prefix=/usr -O1 --skip-build

  install -Dm 644 LICENSE "$pkgdir/usr/share/licenses/$pkgname/LICENSE"
}

