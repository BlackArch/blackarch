# This file is part of BlackArch Linux ( https://www.blackarch.org/ ).
# See COPYING for license details.

pkgname=littlebrother
pkgver=112.338cf82
pkgrel=1
pkgdesc='OSINT tool to get informations on French, Belgian and Swizerland people.'
groups=('blackarch' 'blackarch-recon' 'blackarch-social')
arch=('any')
url='https://github.com/lulz3xploit/LittleBrother'
license=('custom:unknown')
depends=('python' 'python-requests' 'python-beautifulsoup4'
         'python-terminaltables' 'python-colorama' 'python-exifread')
makedepends=('git')
provides=('littlebrother')
source=("$pkgname::git+https://github.com/lulz3xploit/LittleBrother.git")
sha512sums=('SKIP')

pkgver() {
  cd $pkgname

  echo $(git rev-list --count HEAD).$(git rev-parse --short HEAD)
}

package() {
  cd $pkgname

  install -dm 755 "$pkgdir/usr/bin"
  install -dm 755 "$pkgdir/usr/share/$pkgname"

  install -Dm 644 -t "$pkgdir/usr/share/doc/$pkgname" README.md

  rm README.md

  cp -a --no-preserve=ownership * "$pkgdir/usr/share/$pkgname/"

  cat > "$pkgdir/usr/bin/$pkgname" << EOF
#!/bin/sh
cd /usr/share/$pkgname
exec python3 LittleBrother.py "\$@"
EOF

  chmod +x "$pkgdir/usr/bin/$pkgname"
}

