# This file is part of BlackArch Linux ( https://www.blackarch.org/ ).
# See COPYING for license details.

pkgname=findomain
pkgver=115.5422af1
pkgrel=1
pkgdesc='A tool that use Certificate Transparency logs to find subdomains.'
arch=('x86_64' 'armv6h' 'armv7h' 'aarch64')
groups=('blackarch' 'blackarch-scanner' 'blackarch-recon')
url='https://github.com/Edu4rdSHL/findomain'
license=('GPL3')
makedepends=('git' 'cargo')
source=("git+https://github.com/Edu4rdSHL/$pkgname.git")
sha512sums=('SKIP')

pkgver() {
  cd $pkgname

  echo $(git rev-list --count HEAD).$(git rev-parse --short HEAD)
}

build() {
  cd $pkgname

  cargo build --release
}

package() {
  cd $pkgname

  install -Dm 755 "target/release/$pkgname" "$pkgdir/usr/bin/$pkgname"
  install -Dm 644 -t "$pkgdir/usr/share/doc/$pkgname/" README.md
}

