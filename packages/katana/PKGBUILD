# This file is part of BlackArch Linux ( http://blackarch.org ).
# See COPYING for license details.

pkgname=katana
pkgver=1.0.0.1
pkgrel=1
groups=('blackarch' 'blackarch-exploitation' 'blackarch-dos' 'blackarch-cracker'
        'blackarch-scanner' 'blackarch-recon')
pkgdesc='A framework that seekss to unite general auditing tools, which are general pentesting tools (Network,Web,Desktop and others).'
arch=('any')
url='http://sourceforge.net/projects/katanas/'
license=('GPL3')
depends=('python2' 'python2-lxml')
source=("https://downloads.sourceforge.net/project/katanas/katana_${pkgver}%28BUILD%200069%29.zip")
sha512sums=('305d65b5929e01a84d9412e082d068f6bd7ccfb7bdc49be5a2f9b30fa0e3cc57c3d3552c689d6f1c989085581d16095c34ea50d2a72c559b3888789cd2269a5a')

package() {
  cd "$srcdir/KatanaFramework-master"

  local tools="ktf.console ktf.run ktf.ktf ktf.linker ktf.run ktf.update"

  install -dm 755 "$pkgdir/usr/bin"
  install -dm 755 "$pkgdir/usr/share/katana"

  install -Dm644 doc/man/ktf.console "$pkgdir/usr/share/man/man1/ktf.console"
  install -Dm644 doc/man/ktf.tool "$pkgdir/usr/share/man/man1/ktf-tool"
  rm -rf doc/man/ doc/logos
  install -Dm644 -t "$pkgdir/usr/share/doc/katana/" doc/*

  rm -rf doc/ COPYING.GPL LICENCE

  cp -a * "$pkgdir/usr/share/katana"

  for i in ${tools}
  do
    cat > "$pkgdir/usr/bin/katana-`echo ${i} | tr -d '.'`" << EOF
#!/bin/sh
cd /usr/share/katana
exec python2 ${i}.py "\${@}"
EOF
    chmod a+x "$pkgdir/usr/bin/katana-` echo ${i} | tr -d '.'`"
  done

  cat > "$pkgdir/usr/bin/katana-ktf.gui" << EOF
#!/bin/sh
cd /usr/share/katana
exec java -jar ktf.gui.jar "\${@}"
EOF

  chmod a+x "$pkgdir/usr/bin/katana-ktf.gui"
}
