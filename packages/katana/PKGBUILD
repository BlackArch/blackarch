# This file is part of BlackArch Linux ( http://blackarch.org ).
# See COPYING for license details.

pkgname='katana'
pkgver='0.0.1.0'
pkgrel=1
groups=('blackarch' 'blackarch-exploitation' 'blackarch-dos' 'blackarch-cracker'
        'blackarch-scanner' 'blackarch-recon')
pkgdesc='A framework that seekss to unite general auditing tools, which are general pentesting tools (Network,Web,Desktop and others).'
arch=('any')
url='http://sourceforge.net/projects/katanas/'
license=('GPL3')
depends=('python2' 'python2-lxml')
source=("http://downloads.sourceforge.net/project/katanas/katana_${pkgver}%28BUILD%200065%29.zip")
sha1sums=('672c633a1da09195217f81a34df5ad90ca83bed1')

package() {
  cd "$srcdir/KatanaFramework-master"

  local tools="ktf.console ktf.run ktf.gui ktf.ktf ktf.linker ktf.run ktf.update"

  mkdir -p "$pkgdir/usr/bin"
  mkdir -p "$pkgdir/usr/share/katana"

  install -Dm644 doc/man/ktf.console "$pkgdir/usr/share/man/man1/ktf.console"
  install -Dm644 doc/man/ktf.tool "$pkgdir/usr/share/man/man1/ktf-tool"
  rm -rf doc/man/ doc/logos
  install -Dm644 -t "$pkgdir/usr/share/doc/katana/" doc/*

  rm -rf doc/ COPYING.GPL LICENCE

  cp -a * "$pkgdir/usr/share/katana"

  for i in ${tools}
  do
    cat > "$pkgdir/usr/bin/katana-`echo ${i} | tr -d '.'`" << EOF
#!/bin/sh
cd /usr/share/katana
exec python2 ${i}.py "\$@"
EOF
    chmod a+x "$pkgdir/usr/bin/katana-` echo ${i} | tr -d '.'`"
  done
}
