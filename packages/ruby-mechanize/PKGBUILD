# This file is part of BlackArch Linux ( http://blackarch.org ).
# See COPYING for license details.
pkgname='ruby-mechanize'
_gemname='mechanize'
pkgver='2.7.5'
pkgrel=4
pkgdesc="The Mechanize library is used for automating interaction with websites"
arch=('any')
url='http://docs.seattlerb.org/mechanize/'
license=("MIT")
depends=('ruby' 'ruby-domain_name' 'ruby-http-cookie' 'ruby-mime-types' 'ruby-net-http-digest_auth' 'ruby-net-http-persistent-2' 'ruby-nokogiri' 'ruby-ntlm-http' 'ruby-webrobots')
options=('!emptydirs')
source=("https://rubygems.org/downloads/$_gemname-$pkgver.gem" )
noextract=("$_gemname-$pkgver.gem")
sha1sums=('318210b10ba357c8588c9f2cb85684c1e0dd1a98' )


package() {
  local _gemdir="$(ruby -rubygems -e 'puts Gem.default_dir')"
  gem install --ignore-dependencies --no-document --no-user-install -i "$pkgdir/$_gemdir" -n "$pkgdir"/usr/bin $_gemname-$pkgver.gem
  rm "$pkgdir/$_gemdir/cache/$_gemname-$pkgver.gem"
  install -D -m644 "$pkgdir/$_gemdir/gems/$_gemname-$pkgver/LICENSE.rdoc" "$pkgdir/usr/share/licenses/$pkgname/LICENSE.rdoc"

  # Preserve library and other required directories. Remove junk that gem installs.
  local _copydir=`mktemp -d -t $_gemname.XXX`
  chmod 755 $_copydir
  (cd "$pkgdir/$_gemdir/gems/$_gemname-$pkgver"/ && cp -r --parents lib $_copydir)
  rm -rf "$pkgdir/$_gemdir/gems/$_gemname-$pkgver"
  mv $_copydir "$pkgdir/$_gemdir/gems/$_gemname-$pkgver"

}
