# This file is part of BlackArch Linux ( https://www.blackarch.org/ ).
# See COPYING for license details.

pkgname=ruby-mechanize
_gemname=mechanize
pkgver=2.8.3
pkgrel=1
pkgdesc='Automates interaction with websites, handling cookies, redirects and forms.'
arch=('any')
url='https://rubygems.org/gems/mechanize'
license=('GPL2')
depends=('ruby' 'ruby-net-http-digest_auth' 'ruby-net-http-persistent'
         'ruby-mime-types' 'ruby-http-cookie' 'ruby-nokogiri' 'ruby-ntlm-http'
         'ruby-webrobots' 'ruby-domain_name')
makedepends=('ruby' 'ruby-rdoc')
source=("https://rubygems.org/gems/$_gemname-$pkgver.gem")
noextract=("$_gemname-$pkgver.gem")
sha512sums=('be467e995fd0a943b8db1588862f82c3d04b55a830dd1ca1d6c6cb3f65e446c1dd5ecea62d57639192e6caa521450ee31ac683ff9038c06578cb74b9557b6736')

package() {
  _gemdir="$(ruby -e 'puts Gem.default_dir')"

  if [[ $CARCH == arm* ]] ; then
    gem install --no-user-install --ignore-dependencies --no-rdoc --no-ri \
      -i "$pkgdir$_gemdir" "$_gemname-$pkgver.gem"
  else
    gem install --ignore-dependencies --no-user-install -i "$pkgdir/$_gemdir" \
      -n "$pkgdir/usr/bin" "$_gemname-$pkgver.gem"
  fi
}

