# This file is part of BlackArch Linux ( http://blackarch.org ).
# See COPYING for license details.

pkgname='ruby-mechanize'
_gemname='mechanize'
pkgver='2.7.3'
pkgrel=1
pkgdesc='Automates interaction with websites, handling cookies, redirects and forms.'
arch=('any')
url='http://mechanize.rubyforge.org/'
license=('GPL2')
depends=('ruby' 'ruby-http-cookie>=1.0.0' 'ruby-mime-types>=1.17.2'
         'ruby-net-http-digest_auth>=1.1.1' 'ruby-net-http-persistent>=2.5.2'
         'ruby-nokogiri>=1.4' 'ruby-ntlm-http>=0.1.1' 'ruby-webrobots>=0.0.9'
         'ruby-webrobots<0.2')
makedepends=('ruby')
source=("https://rubygems.org/downloads/${_gemname}-${pkgver}.gem")
noextract=("${_gemname}-${pkgver}.gem")
sha1sums=('74a8ac3c37477927771ca7a5624fb36311239724')

package() {
  cd "$srcdir"

  local _gemdir="$(ruby -rubygems -e'puts Gem.default_dir')"

  if [[ $CARCH == arm* ]] ; then
    gem install --no-user-install --ignore-dependencies --no-rdoc --no-ri \
      -i "$pkgdir$_gemdir" mechanize-$pkgver.gem
  else
    gem install --no-user-install --ignore-dependencies -i "$pkgdir$_gemdir" \
      mechanize-$pkgver.gem
  fi
}
