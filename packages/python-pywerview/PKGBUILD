# This file is part of BlackArch Linux ( https://www.blackarch.org/ ).
# See COPYING for license details.

pkgbase=python-pywerview
pkgname=('python2-pywerview' 'python-pywerview')
_pkgname=pywerview
pkgver=0.3.0
pkgrel=1
pkgdesc="A Python port of PowerSploit's PowerView'."
arch=('any')
url='https://pypi.org/project/pywerview/#files'
license=('GPL3')
makedepends=('python2-setuptools' 'python-setuptools')
source=("https://files.pythonhosted.org/packages/42/be/cbf0ba146c52a1e2790e9c4c8c874fde6543c5f74c0b0f8e94b881cb81eb/$_pkgname-$pkgver.tar.gz")
sha512sums=('3f156a414aacec32065349b6ffad0f6bc0f895f7a016ffe108508dcad85328d0a34bd95377e74996eea6620b2063440268679c7a2c185c2e131d7449efc2479a')

prepare() {
  cp -a "$_pkgname-$pkgver"{,-2}
}

build() {
  cd "$_pkgname-$pkgver-2"

  python2 setup.py build

  cd "$srcdir/$_pkgname-$pkgver"

  python setup.py build
}

package_python2-pywerview() {
  depends=('python2' 'python2-pyasn1' 'python2-bs4' 'python2-crypto'
           'python2-pyopenssl')

  cd "$_pkgname-$pkgver-2"

  python2 setup.py install --root="$pkgdir" --prefix=/usr -O1 --skip-build

  mv "$pkgdir/usr/bin/$_pkgname" "$pkgdir/usr/bin/${_pkgname}2"
}

package_python-pywerview() {
  depends=('python' 'python-pyasn1' 'python-crypto' 'python-pyopenssl')

  cd "$_pkgname-$pkgver"

  python setup.py install --root="$pkgdir" --prefix=/usr -O1 --skip-build
}

