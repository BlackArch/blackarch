# This file is part of BlackArch Linux ( https://www.blackarch.org/ ).
# See COPYING for license details.

pkgbase=python-pywerview
pkgname=('python2-pywerview' 'python-pywerview')
_pkgname=pywerview
pkgver=0.2.0
pkgrel=5
pkgdesc="A Python port of PowerSploit's PowerView'."
arch=('any')
url='https://pypi.org/project/pywerview/#files'
license=('GPL3')
makedepends=('python2-setuptools' 'python-setuptools')
source=("https://files.pythonhosted.org/packages/8b/ca/fc13415e41427f5bc925ccbe80361af2bdf4c6f21c9683e28c12d0703f72/$_pkgname-$pkgver.tar.gz")
sha512sums=('a3346602de5bb429f8475b987c3a1605dbee7a7ac9563cd0ef1412d845fdac48f4e65044068ae8a073bb41cdaa8f01be7730a1e1250b8705f54ce33291735cdc')

prepare() {
  cp -a "$_pkgname-$pkgver"{,-2}
}

build() {
  cd "$_pkgname-$pkgver-2"

  python2 setup.py build

  cd "$srcdir/$_pkgname-$pkgver"

  python setup.py build
}

package_python2-pywerview() {
  depends=('python2' 'python2-pyasn1' 'python2-bs4' 'python2-crypto'
           'python2-pyopenssl')

  cd "$_pkgname-$pkgver-2"

  python2 setup.py install --prefix=/usr --root="$pkgdir" --optimize=1 \
    --skip-build

  mv "$pkgdir/usr/bin/$_pkgname" "$pkgdir/usr/bin/${_pkgname}2"
}

package_python-pywerview() {
  depends=('python' 'python-pyasn1' 'python-crypto' 'python-pyopenssl')

  cd "$_pkgname-$pkgver"

  python setup.py install --prefix=/usr --root="$pkgdir" --optimize=1 \
    --skip-build
}

