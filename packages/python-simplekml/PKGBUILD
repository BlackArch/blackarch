# This file is part of BlackArch Linux ( https://www.blackarch.org/ ).
# See COPYING for license details.

pkgbase=python-simplekml
pkgname=('python2-simplekml' 'python-simplekml')
_pkgname=simplekml
pkgver=1.3.3
pkgrel=1
epoch=1
pkgdesc='KML generation with as little effort as possible.'
arch=('any')
url='https://pypi.org/project/simplekml/#files'
license=('GPL')
makedepends=('python2-setuptools' 'python-setuptools')
source=("https://files.pythonhosted.org/packages/39/e9/19be41775c14c70b247b12763bb4768084918642f02911f3b4ed5ddf47f8/$_pkgname-$pkgver.tar.gz")
sha512sums=('53169cb894905328bc9121a052e8ef54fbb806e42311b98bdbdec944829f52011116759e33b0ab684f631351f67fe5d50c3eefa3adf13763bf1356d3c54c3f44')

prepare() {
  cp -a "$_pkgname-$pkgver"{,-2}
}

build() {
  cd "$_pkgname-$pkgver-2"

  python2 setup.py build

  cd "$srcdir/$_pkgname-$pkgver"

  python setup.py build
}

package_python2-simplekml() {
  depends=('python2')

  cd "$_pkgname-$pkgver-2"

  python2 setup.py install --prefix=/usr --root="$pkgdir" --optimize=1 \
    --skip-build
}

package_python-simplekml() {
  depends=('python')

  cd "$_pkgname-$pkgver"

  python setup.py install --prefix=/usr --root="$pkgdir" --optimize=1 \
    --skip-build
}

