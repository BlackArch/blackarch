# This file is part of BlackArch Linux ( https://www.blackarch.org/ ).
# See COPYING for license details.

pkgbase=python-simplekml
pkgname=('python2-simplekml' 'python-simplekml')
_pkgname=simplekml
pkgver=1.3.1
pkgrel=4
epoch=1
pkgdesc='KML generation with as little effort as possible.'
arch=('any')
url='https://pypi.org/project/simplekml/#files'
license=('GPL')
makedepends=('python2-setuptools' 'python-setuptools')
source=("https://files.pythonhosted.org/packages/62/88/feeb5ac5ae528c81daed9fe9864ec42496b80ffbcf83ac60bb6feb5b7f80/$_pkgname-$pkgver.tar.gz")
sha512sums=('a72d378b44ccbe5f597f100784f3e802255c9a47216132f6ad13e594a2fb70058224428df5b7450ad7e498b9cfbfd846191d30dae225169957ea35659dd0968a')

prepare() {
  cp -a "$_pkgname-$pkgver"{,-2}
}

build() {
  cd "$_pkgname-$pkgver-2"

  python2 setup.py build

  cd "$srcdir/$_pkgname-$pkgver"

  python setup.py build
}

package_python2-simplekml() {
  depends=('python2')

  cd "$_pkgname-$pkgver-2"

  python2 setup.py install --prefix=/usr --root="$pkgdir" --optimize=1 \
    --skip-build
}

package_python-simplekml() {
  depends=('python')

  cd "$_pkgname-$pkgver"

  python setup.py install --prefix=/usr --root="$pkgdir" --optimize=1 \
    --skip-build
}

