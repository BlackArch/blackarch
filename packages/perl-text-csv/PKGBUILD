# This file is part of BlackArch Linux ( http://blackarch.org ).
# See COPYING for license details.

pkgname='perl-text-csv'
pkgver='1.33'
pkgrel='1'
pkgdesc='Comma-separated values manipulator (using XS or PurePerl).'
arch=('any')
license=('PerlArtistic' 'GPL')
options=('!emptydirs')
depends=('perl')
makedepends=()
url='http://search.cpan.org/dist/Text-CSV'
source=("http://search.cpan.org/CPAN/authors/id/M/MA/MAKAMAKA/Text-CSV-${pkgver}.tar.gz")
sha1sums=('9ad509d5bfd60914fdbd4ff8ce0b577df26590ee')
_distdir="Text-CSV-$pkgver"

build() {
  cd "$srcdir/$_distdir"

  ( export PERL_MM_USE_DEFAULT=1 PERL5LIB=""                 \
      PERL_AUTOINSTALL=--skipdeps                            \
      PERL_MM_OPT="INSTALLDIRS=vendor DESTDIR='$pkgdir'"     \
      PERL_MB_OPT="--installdirs vendor --destdir '$pkgdir'" \
      MODULEBUILDRC=/dev/null

    /usr/bin/perl Makefile.PL
    make
  )
}

package() {
  cd "$srcdir/$_distdir"

  make install

  find "$pkgdir" -name .packlist -o -name perllocal.pod -delete
}
