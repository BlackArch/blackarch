# This file is part of BlackArch Linux ( http://blackarch.org ).
# See COPYING for license details.

pkgname='joomlavs'
pkgver=203.c86d628
pkgrel=1
groups=('blackarch' 'blackarch-webapp' 'blackarch-scanner' 'blackarch-fuzzer')
pkgdesc='A black box, Ruby powered, Joomla vulnerability scanner.'
arch=('any')
url='https://github.com/rastating/joomlavs'
license=('GPL3')
depends=('ruby' 'ruby-bundler')
makedepends=('git')
source=('git+https://github.com/rastating/joomlavs.git')
install='joomlavs.install'
sha1sums=('SKIP')

pkgver() {
  cd "$srcdir/joomlavs"

  echo $(git rev-list --count HEAD).$(git rev-parse --short HEAD)
}

package() {
  cd "$srcdir/joomlavs"

  mkdir -p "$pkgdir/usr/bin"
  mkdir -p "$pkgdir/usr/share/joomlavs"

  install -Dm644 README.md "$pkgdir/usr/share/doc/joomlavs/README.md"
  install -Dm644 LICENSE "$pkgdir/usr/share/licenses/joomlavs/LICENSE"

  rm README.md LICENSE

  cp -a * "$pkgdir/usr/share/joomlavs"

  cat > "$pkgdir/usr/bin/joomlavs" << EOF
#!/bin/sh
cd /usr/share/joomlavs
exec ruby joomlavs.rb "\$@"
EOF

  chmod a+x "$pkgdir/usr/bin/joomlavs"
}
