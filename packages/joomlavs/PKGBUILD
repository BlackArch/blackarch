# This file is part of BlackArch Linux ( http://blackarch.org ).
# See COPYING for license details.

pkgname=joomlavs
pkgver=254.eea7500
pkgrel=1
groups=('blackarch' 'blackarch-webapp' 'blackarch-scanner' 'blackarch-fuzzer')
pkgdesc='A black box, Ruby powered, Joomla vulnerability scanner.'
arch=('any')
url='https://github.com/rastating/joomlavs'
license=('GPL3')
depends=('ruby2.3' 'ruby2.3-bundler')
makedepends=('git')
source=('git+https://github.com/rastating/joomlavs.git')
install='joomlavs.install'
sha512sums=('SKIP')

pkgver() {
  cd "$srcdir/joomlavs"

  echo $(git rev-list --count HEAD).$(git rev-parse --short HEAD)
}

package() {
  cd "$srcdir/joomlavs"

  install -dm 755 "$pkgdir/usr/bin"
  install -dm 755 "$pkgdir/usr/share/joomlavs"

  install -Dm644 README.md "$pkgdir/usr/share/doc/joomlavs/README.md"
  install -Dm644 LICENSE "$pkgdir/usr/share/licenses/joomlavs/LICENSE"

  rm README.md LICENSE

  cp -a * "$pkgdir/usr/share/joomlavs"

  cat > "$pkgdir/usr/bin/joomlavs" << EOF
#!/bin/sh
cd /usr/share/joomlavs
exec ruby-2.3 joomlavs.rb "\$@"
EOF

  chmod a+x "$pkgdir/usr/bin/joomlavs"
}
