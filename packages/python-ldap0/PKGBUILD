# This file is part of BlackArch Linux ( https://www.blackarch.org/ ).
# See COPYING for license details.

pkgname=python-ldap0
_pkgname=ldap0
pkgver=1.4.3
_pyver=3.10
pkgrel=1
pkgdesc='Module package for implementing LDAP clients.'
arch=('x86_64' 'aarch64')
url='https://pypi.org/project/ldap0/#files'
license=('Apache')
depends=('python' 'python-pyasn1' 'python-pyasn1-modules' 'openldap')
makedepends=('python-setuptools')
source=("https://files.pythonhosted.org/packages/1e/8a/093e62f9ab2c00272a27bd0733bfd5eafae17d501971ac1c00e68e0188d9/$_pkgname-$pkgver.tar.gz")
sha512sums=('52f320bdcd9618ac17bb8a265d2d42449913e8174f3fe99281a6941cbbe4f9dd315dab3e494d108b652854e5581419607803f81bdcac10fe7e2c320fd0114ceb')

prepare() {
  cd "$_pkgname-$pkgver"

  sed -i 's|libs = ldap_r|libs = ldap|g' setup.cfg
}

build() {
  cd "$_pkgname-$pkgver"

  python setup.py build
}

package() {
  cd "$_pkgname-$pkgver"

  python setup.py install --root="$pkgdir" --prefix=/usr -O1 --skip-build

  chmod 644 \
    "$pkgdir/usr/lib/python$_pyver/site-packages/ldap0-$pkgver-py$_pyver.egg-info/"*
}

