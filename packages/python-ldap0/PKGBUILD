# This file is part of BlackArch Linux ( https://www.blackarch.org/ ).
# See COPYING for license details.

pkgname=python-ldap0
_pkgname=ldap0
pkgver=1.4.8
_pyver=3.10
pkgrel=1
pkgdesc='Module package for implementing LDAP clients.'
arch=('x86_64' 'aarch64')
url='https://pypi.org/project/ldap0/#files'
license=('Apache')
depends=('python' 'python-pyasn1' 'python-pyasn1-modules' 'openldap')
makedepends=('python-setuptools')
source=("https://files.pythonhosted.org/packages/00/99/49b0d3bc3f77331e83a1c93acded0bda45e1680c7e8203e3f0a9068e8b4d/$_pkgname-$pkgver.tar.gz")
sha512sums=('292c5b950e16f9616baee757ee2a96020a62aaaf23067a3a9c9b2674207561f659f09014b272605b77557fbb826c721ba7ca00378ac1b89b52ab0ac666dae907')

prepare() {
  cd "$_pkgname-$pkgver"

  sed -i 's|libs = ldap_r|libs = ldap|g' setup.cfg
}

build() {
  cd "$_pkgname-$pkgver"

  python setup.py build
}

package() {
  cd "$_pkgname-$pkgver"

  python setup.py install --root="$pkgdir" --prefix=/usr -O1 --skip-build

  chmod 644 \
    "$pkgdir/usr/lib/python$_pyver/site-packages/ldap0-$pkgver-py$_pyver.egg-info/"*
}

