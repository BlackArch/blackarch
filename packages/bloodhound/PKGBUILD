# This file is part of BlackArch Linux ( http://blackarch.org ).
# See COPYING for license details.

_npmname=BloodHound
pkgname=bloodhound
pkgver=612.98dfd2d
pkgrel=1
pkgdesc='Six Degrees of Domain Admin'
groups=('blackarch' 'blackarch-recon' 'blackarch-windows')
arch=('x86_64' 'armv6h' 'armv7h' 'aarch64')
url='https://github.com/BloodHoundAD/BloodHound'
license=('GPL3')
depends=('nodejs' 'neo4j-community' 'alsa-lib' 'gtk2' 'libxss' 'gconf' 'nss'
         'libxtst')
makedepends=('git' 'nodejs-electron-packager' 'npm')
optdepends=('python: for some scripts' 'powershell: for PowerShell ingestion')
source=('git+https://github.com/BloodHoundAD/BloodHound.git'
        'singlearch.patch')
options=(!strip)
sha512sums=('SKIP'
            '7b084ca76192a4887c7e25e42eda7459637b55cf8ee980d44629b2c8fad66285a2a7f8e044ff0d8a1be13420ef961d92eccd64cbb95e8e4a916ceade8cf30e5b')
if [ "$CARCH" == "x86_64" ]; then
	_npmcmd='linuxbuild_64'
	_arch='x64'
  _path='BloodHound-linux-x64'
elif [ "$CARCH" == "armv7h"]; then
  _npmcmd='linuxbuild_arm'
  _arch='armv7l'
  _path='BloodHound-linux-armv7l'
elif [ "$CARCH" == 'aarch64' ]; then
  _npmcmd='linuxbuild_arm'
  _arch='armv7l'
  _path='BloodHound-linux-aarch64'
fi

pkgver() {
  cd "$_npmname"

  echo $(git rev-list --count HEAD).$(git rev-parse --short HEAD)
}

prepare() {
  cd "$_npmname"

  patch -Np2 -i ../singlearch.patch
}

build() {
  cd "$_npmname"

  npm install
  npm run $_npmcmd
}

package() {
  cd "$_npmname"

  install -dm 755 "$pkgdir/usr/bin"
  install -dm 755 "$pkgdir/usr/share/$pkgname"

  install -Dm 644 -t "$pkgdir/usr/share/licenses/$pkgname/" LICENSE*.md
  rm LICENSE*.md
  install -Dm644 -t "$pkgdir/usr/share/doc/$pkgname/" *.md
  rm *.md

  cp -a *.js *.json *.sh *.html *.yml src ${_path} *.graphdb dist \
    "$pkgdir/usr/share/$pkgname"

  cat > "$pkgdir/usr/bin/bloodhound" << EOF
#!/bin/sh
cd /usr/share/bloodhound/${_path}
exec ./BloodHound "\${@}"
EOF

  chmod +x "$pkgdir/usr/bin/bloodhound"
  chmod +x "$pkgdir/usr/share/$pkgname/${_path}/BloodHound"
}
