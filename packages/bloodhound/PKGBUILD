# This file is part of BlackArch Linux ( http://blackarch.org ).
# See COPYING for license details.

_npmname='BloodHound'
pkgname='bloodhound'
pkgver=597.6b3bbe1
pkgrel=1
pkgdesc='Six Degrees of Domain Admin'
groups=('blackarch' 'blackarch-recon' 'blackarch-windows')
arch=('aarch64' 'armv7h' 'armv7l' 'x86_64')
url='https://github.com/BloodHoundAD/BloodHound'
license=('GPL3')
depends=('nodejs' 'neo4j-community' 'alsa-lib' 'gtk2' 'libxss' 'gconf' 'nss'
         'libxtst')
makedepends=('git' 'nodejs-electron-packager' 'npm')
optdepends=('python: for some scripts' 'powershell: for PowerShell ingestion')
source=("git+https://github.com/BloodHoundAD/BloodHound.git")
options=(!strip)
sha1sums=('SKIP')

pkgver() {
  cd "$srcdir/$_npmname"

  echo $(git rev-list --count HEAD).$(git rev-parse --short HEAD)
}

build() {
  cd "$srcdir/$_npmname"

  npm install
  npm run linuxbuild
}

package() {
  cd "$srcdir/$_npmname"

  mkdir -p "$pkgdir/usr/bin"
  mkdir -p "$pkgdir/usr/share/bloodhound"

  install -Dm644 LICENSE.md "$pkgdir/usr/share/licenses/bloodhound/LICENSE.md"
  rm LICENSE.md
  install -Dm644 -t "$pkgdir/usr/share/doc/bloodhound" *.md
  rm *.md

  cp -ar * "$pkgdir/usr/share/bloodhound"

  cat > "$pkgdir/usr/bin/bloodhound" << EOF
#!/bin/sh
cd /usr/share/bloodhound/
exec ./BloodHound "\${@}"
EOF

  chmod +x "$pkgdir/usr/bin/bloodhound"
  chmod +x "$pkgdir/usr/share/bloodhound/BloodHound"
}
