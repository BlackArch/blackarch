# This file is part of BlackArch Linux ( http://blackarch.org ).
# See COPYING for license details.

_npmname='BloodHound'
pkgname="bloodhound"
pkgver=431.896f7b2
pkgrel=1
pkgdesc='Six Degrees of Domain Admin'
groups=('blackarch' 'blackarch-recon' 'blackarch-windows')
arch=('aarch64' 'armv7h' 'armv7l' 'x86_64' 'i686')
url='https://github.com/BloodHoundAD/BloodHound'
license=('GPL3')
depends=('nodejs' 'neo4j-community' 'alsa-lib' 'gtk2' 'libxss' 'gconf' 'nss'
         'libxtst')
makedepends=('git' 'nodejs-electron-packager' 'npm')
optdepends=('python: for some scripts' 'powershell: for PowerShell ingestion')
source=("git+https://github.com/BloodHoundAD/BloodHound.git"
        'singlearch.patch')
options=(!strip)
sha1sums=('SKIP'
          'bccdf7e27c8ad4eab36075c43f0bd52fd6d6b524')
if [[ ${CARCH} == "x86_64" ]]; then
  _npmcmd='linuxbuild_64'
  _arch='x64'
elif [[ ${CARCH} == "i686" ]]; then
  _npmcmd='linuxbuild_32'
  _arch='ia32'
else
  _npmcmd='linuxbuild_arm'
  _arch='armv7l'
fi

pkgver() {
  cd "$srcdir/$_npmname"

  echo $(git rev-list --count HEAD).$(git rev-parse --short HEAD)
}

build() {
  cd "$srcdir/$_npmname"

  patch -Np2 -i ../singlearch.patch
  npm install
  npm run $_npmcmd
}

package() {
  cd "$srcdir/$_npmname/BloodHound-linux-$_arch"

  mkdir -p "$pkgdir/usr/bin"
  mkdir -p "$pkgdir/usr/share/bloodhound"

  cp -ar * "$pkgdir/usr/share/bloodhound"
  cd ..
  cp -ar BloodHoundExampleDB.graphdb "$pkgdir/usr/share/bloodhound"
  #cp -ar BloodHoundExampleDB.graphdb PowerShell "$pkgdir/usr/share/bloodhound"

  cat > "$pkgdir/usr/bin/bloodhound" << EOF
#!/bin/sh
cd /usr/share/bloodhound/
exec ./BloodHound "\${@}"
EOF

  chmod +x "$pkgdir/usr/bin/bloodhound"
  chmod +x "$pkgdir/usr/share/bloodhound/BloodHound"
}

