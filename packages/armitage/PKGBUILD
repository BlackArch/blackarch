# This file is part of BlackArch Linux ( http://blackarch.org ).
# See COPYING for license details.

pkgname='armitage'
pkgver=140715
pkgrel=1
groups=('blackarch' 'blackarch-exploitation' 'blackarch-automation')
pkgdesc='A graphical cyber attack management tool for Metasploit.'
url='http://www.fastandeasyhacking.com/'
arch=('any')
license=('GPL')
depends=('metasploit' 'java-environment' 'nmap' 'postgresql' 'ruby-msgpack')
makedepends=('apache-ant' 'zip' 'unzip' 'subversion')
install='armitage.install'
source=("http://www.fastandeasyhacking.com/download/armitage${pkgver}.tgz"
        'armitage.install')
sha1sums=('c8f28fe549d3838ac75fa46e1f296fed6af49f16'
          'c48183769a92176ac3b60f84dfde01903a765ae4')

package() {
  cd "$srcdir/armitage"

  mkdir -p "$pkgdir/usr/bin"
  mkdir -p "$pkgdir/usr/share/armitage"

  install -Dm644 -t "$pkgdir/usr/share/doc/armitage" readme.txt whatsnew.txt

  rm readme.txt whatsnew.txt

  cp -a * "$pkgdir/usr/share/armitage"

  cat > "$pkgdir/usr/bin/armitage" << EOF
#!/bin/sh
cd /usr/share/armitage
exec ./armitage "\$@"
EOF

  chmod +x "$pkgdir/usr/bin/armitage"

  cat > "$pkgdir/usr/bin/teamserver" << EOF
#!/bin/sh
cd /usr/share/armitage
exec ./teamserver "\$@"
EOF

  chmod +x "$pkgdir/usr/bin/teamserver"
}
