pkgname=armitage
pkgver=2013.08.21
groups=('blackarch' 'blackarch-exploitation')
pkgrel=4
epoch=2
pkgdesc="A graphical cyber attack management tool for Metasploit"
url='http://www.fastandeasyhacking.com/'
arch=('any')
license=('GPL')
depends=('metasploit' 'java-runtime')
makedepends=('apache-ant' 'zip' 'unzip' 'subversion')
provides=('armitage')
install=armitage.install
source=('svn+http://armitage.googlecode.com/svn/trunk/'
        'armitage.install')
md5sums=('SKIP'
         '5234326f9ff41ff8694cb35a557af895')

prepare() {
  cd "$srcdir/trunk"
  sed -i 's|/usr/share/java/apache-ant/bin/||g' package.sh
}

build() {
  cd "$srcdir/trunk"
  bash package.sh
}

package() {
  cd "$srcdir/trunk"
  # Base directories.
  install -dm755 "$pkgdir/usr/share"
  install -dm755 "$pkgdir/usr/bin"

  # Joint script.
  cat > "$pkgdir/usr/bin/armitage" <<\EOF
#!/bin/sh
java -jar /usr/share/armitage/armitage.jar "$@"
EOF
  chmod +x "$pkgdir/usr/bin/armitage"

  # Everything else.
  tar -xzf "$srcdir/trunk/armitage.tgz" -C "$pkgdir/usr/share"
}
