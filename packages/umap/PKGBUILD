# This file is part of BlackArch Linux ( https://www.blackarch.org/ ).
# See COPYING for license details.

pkgname=umap
pkgver=25.3ad8121
pkgrel=5
pkgdesc='The USB host security assessment tool.'
groups=('blackarch' 'blackarch-scanner' 'blackarch-fuzzer')
url='https://github.com/nccgroup/umap'
arch=('any')
license=('AGPL')
depends=('python3' 'python-pyserial')
makedepends=('git')
source=("git+https://github.com/nccgroup/$pkgname.git")
sha512sums=('SKIP')

pkgver() {
  cd $pkgname

  echo $(git rev-list --count HEAD).$(git rev-parse --short HEAD)
}

package() {
  cd $pkgname

  install -dm 755 "$pkgdir/usr/bin"
  install -dm 755 "$pkgdir/usr/share/$pkgname"

  install -Dm 644 LICENSE.txt "$pkgdir/usr/share/licenses/$pkgname/LICENSE"
  install -Dm 644 -t "$pkgdir/usr/share/doc/$pkgname/" README.md CHANGELOG.txt

  cp -a * "$pkgdir/usr/share/$pkgname/"

  cat > "$pkgdir/usr/bin/$pkgname" << EOF
#!/bin/sh
cd /usr/share/$pkgname
exec python3 umap.py "\$@"
EOF

  chmod +x "$pkgdir/usr/bin/$pkgname"
}

