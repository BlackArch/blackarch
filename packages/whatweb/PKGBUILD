# This file is part of BlackArch Linux ( http://blackarch.org ).
# See COPYING for license details.

pkgname=whatweb
pkgver=4260.e142cc61
pkgrel=1
groups=('blackarch' 'blackarch-recon' 'blackarch-webapp')
pkgdesc='Next generation web scanner that identifies what websites are running.'
arch=('any')
url='http://www.morningstarsecurity.com/research/whatweb'
license=('GPL')
depends=('ruby')
makedepends=('git' 'ruby-bundler')
source=('git+https://github.com/urbanadventurer/WhatWeb.git')
install='whatweb.install'
sha512sums=('SKIP')

pkgver() {
  cd "$srcdir/WhatWeb"

  echo $(git rev-list --count HEAD).$(git rev-parse --short HEAD)
}

package() {
  cd "$srcdir/WhatWeb"

  install -dm 755 "$pkgdir/usr/bin"
  install -dm 755 "$pkgdir/usr/share/whatweb"

  #make DESTDIR="$pkgdir" install

  install -Dm644 -t "$pkgdir/usr/share/doc/whatweb" CHANGELOG README INSTALL
  install -Dm644 whatweb.1 "$pkgdir/usr/share/man/man1/whatweb.1"
  install -Dm644 LICENSE "$pkgdir/usr/share/licenses/whatweb/LICENSE"

  rm CHANGELOG README INSTALL whatweb.1 LICENSE

  cp -a * "$pkgdir/usr/share/whatweb"

  cat > "$pkgdir/usr/bin/whatweb" << EOF
#!/bin/sh
cd /usr/share/whatweb
exec ruby whatweb "\${@}"
EOF

  chmod a+x "$pkgdir/usr/bin/whatweb"
}
