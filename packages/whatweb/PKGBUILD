# This file is part of BlackArch Linux ( http://blackarch.org ).
# See COPYING for license details.

pkgname=whatweb
pkgver=4260.e142cc61
pkgrel=1
groups=('blackarch' 'blackarch-recon' 'blackarch-webapp')
pkgdesc='Next generation web scanner that identifies what websites are running.'
arch=('any')
url='http://www.morningstarsecurity.com/research/whatweb'
license=('GPL')
depends=('ruby')
makedepends=('git' 'ruby-bundler')
source=('git+https://github.com/urbanadventurer/WhatWeb.git')
install='whatweb.install'
sha512sums=('SKIP')

pkgver() {
  cd WhatWeb

  echo $(git rev-list --count HEAD).$(git rev-parse --short HEAD)
}

package() {
  cd WhatWeb

  install -dm 755 "$pkgdir/usr/bin"
  install -dm 755 "$pkgdir/usr/share/$pkgname"

  #make DESTDIR="$pkgdir" install

  install -Dm 644 -t "$pkgdir/usr/share/doc/$pkgname" CHANGELOG README INSTALL
  install -Dm 644 whatweb.1 "$pkgdir/usr/share/man/man1/whatweb.1"
  install -Dm 644 LICENSE "$pkgdir/usr/share/licenses/$pkgname/LICENSE"

  rm CHANGELOG README INSTALL whatweb.1 LICENSE

  cp -a * "$pkgdir/usr/share/$pkgname/"

  cat > "$pkgdir/usr/bin/$pkgname" << EOF
#!/bin/sh
cd /usr/share/$pkgname
exec ruby whatweb "\$@"
EOF

  chmod a+x "$pkgdir/usr/bin/$pkgname"
}

