# This file is part of BlackArch Linux ( http://blackarch.org ).
# See COPYING for license details.

pkgname='whatweb'
pkgver=4071.c489fbd
pkgrel=2
groups=('blackarch' 'blackarch-recon' 'blackarch-webapp')
pkgdesc='Next generation web scanner that identifies what websites are running.'
arch=('any')
url='http://www.morningstarsecurity.com/research/whatweb'
license=('GPL')
depends=('ruby1.8')
makedepends=('git')
source=('git+https://github.com/urbanadventurer/WhatWeb.git')
sha1sums=('SKIP')

pkgver() {
  cd "$srcdir/WhatWeb"

  echo $(git rev-list --count HEAD).$(git rev-parse --short HEAD)
}

prepare() {
  cd "$srcdir/WhatWeb"

  sed -i 's|/usr/bin/env ruby|/usr/bin/env ruby-1.8|' whatweb
}

package() {
  cd "$srcdir/WhatWeb"

  mkdir -p "$pkgdir/usr/share/doc/whatweb"

  install -Dm644 -t "$pkgdir/usr/share/doc/whatweb" CHANGELOG README INSTALL
  install -Dm644 LICENSE "$pkgdir/usr/share/licenses/whatweb/LICENSE"

  make DESTDIR="$pkgdir" install
}
