# This file is part of BlackArch Linux ( http://blackarch.org ).
# See COPYING for license details.

pkgname='untwister'
pkgver=119.a42b8f8
pkgrel=3
pkgdesc='Seed recovery tool for PRNGs.'
groups=('blackarch' 'blackarch-crypto')
arch=('x86_64' 'armv6h' 'armv7h' 'aarch64')
url='https://github.com/altf4/untwister'
license=('GPL3')
depends=('boost' 'cppunit')
makedepends=('git')
source=('git+https://github.com/altf4/untwister.git')
sha1sums=('SKIP')

pkgver() {
  cd "$srcdir/untwister"

  echo $(git rev-list --count HEAD).$(git rev-parse --short HEAD)
}

build() {
  cd "$srcdir/untwister"

  make
}

package() {
  cd "$srcdir/untwister"

  install -Dm755 untwister "$pkgdir/usr/bin/untwister"
  install -Dm644 -t "$pkgdir/usr/share/doc/untwister/" README.md \
    AddingNewPRNG.txt
  install -Dm644 LICENSE "$pkgdir/usr/share/licenses/untwister/LICENSE"
}
