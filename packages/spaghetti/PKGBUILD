# This file is part of BlackArch Linux ( http://blackarch.org ).
# See COPYING for license details.

pkgname='spaghetti'
pkgver=6.5283450
pkgrel=1
epoch=3
pkgdesc='Web Application Security Scanner.'
groups=('blackarch' 'blackarch-webapp' 'blackarch-scanner')
arch=('any')
url='https://github.com/m4ll0k/Spaghetti'
license=('GPL3')
depends=('python2' 'python2-requests' 'python2-beautifulsoup4')
makedepends=('git')
source=('git+https://github.com/m4ll0k/Spaghetti.git')
sha1sums=('SKIP')

pkgver() {
  cd "$srcdir/Spaghetti"

  echo $(git rev-list --count HEAD).$(git rev-parse --short HEAD)
}

package() {
  cd "$srcdir/Spaghetti"

  mkdir -p "$pkgdir/usr/bin"
  mkdir -p "$pkgdir/usr/share/spaghetti"

  install -Dm644 -t "$pkgdir/usr/share/doc/spaghetti/" README.md

  rm README.md

  cp -a * "$pkgdir/usr/share/spaghetti/"

  cat > "$pkgdir/usr/bin/spaghetti" << EOF
#!/bin/sh
cd /usr/share/spaghetti
exec python2 spaghetti.py "\${@}"
EOF

  chmod a+x "$pkgdir/usr/bin/spaghetti"
}
