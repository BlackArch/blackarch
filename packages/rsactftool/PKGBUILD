# This file is part of BlackArch Linux ( http://blackarch.org ).
# See COPYING for license details.

pkgname='rsactftool'
pkgver=71.ca54ad7
pkgrel=1
pkgdesc='RSA tool for ctf - retreive private key from weak public key and/or uncipher data.'
groups=('blackarch' 'blackarch-crypto' 'blackarch-cracker')
arch=('any')
url='https://github.com/Ganapati/RsaCtfTool'
license=('custom:unknown')
depends=('python2' 'python2-sympy' 'python2-argparse' 'python2-gmpy'
         'python2-libnum')
makedepends=('git')
source=('git+https://github.com/Ganapati/RsaCtfTool')
sha1sums=('SKIP')

pkgver() {
  cd "$srcdir/RsaCtfTool"

  echo $(git rev-list --count HEAD).$(git rev-parse --short HEAD)
}

package() {
  cd "$srcdir/RsaCtfTool"

  mkdir -p "$pkgdir/usr/bin"
  mkdir -p "$pkgdir/usr/share/rsactftool"

  install -Dm644 -t "$pkgdir/usr/share/doc/rsactftool/" README.md

  rm README.md

  cp -a * "$pkgdir/usr/share/rsactftool/"

  cat > "$pkgdir/usr/bin/rsactftool" << EOF
#!/bin/sh
cd /usr/share/rsactftool
exec python2 RsaCtfTool.py "\${@}"
EOF

  chmod a+x "$pkgdir/usr/bin/rsactftool"
}
