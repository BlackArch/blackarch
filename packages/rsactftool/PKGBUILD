# This file is part of BlackArch Linux ( https://www.blackarch.org/ ).
# See COPYING for license details.

pkgname=rsactftool
pkgver=130.3a2887e
pkgrel=1
pkgdesc='RSA tool for ctf - retreive private key from weak public key and/or uncipher data.'
groups=('blackarch' 'blackarch-crypto' 'blackarch-cracker')
arch=('any')
url='https://github.com/Ganapati/RsaCtfTool'
license=('custom:unknown')
depends=('python2' 'python2-sympy' 'python2-argparse' 'python2-gmpy'
         'python2-libnum')
makedepends=('git')
source=('git+https://github.com/Ganapati/RsaCtfTool')
sha512sums=('SKIP')

pkgver() {
  cd RsaCtfTool

  echo $(git rev-list --count HEAD).$(git rev-parse --short HEAD)
}

package() {
  cd RsaCtfTool

  install -dm 755 "$pkgdir/usr/bin"
  install -dm 755 "$pkgdir/usr/share/$pkgname"

  install -Dm 644 -t "$pkgdir/usr/share/doc/$pkgname/" README.md

  rm README.md

  cp -a * "$pkgdir/usr/share/$pkgname/"

  cat > "$pkgdir/usr/bin/$pkgname" << EOF
#!/bin/sh
cd /usr/share/$pkgname
exec python2 RsaCtfTool.py "\$@"
EOF

  chmod a+x "$pkgdir/usr/bin/$pkgname"
}

