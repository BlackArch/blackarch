# This file is part of BlackArch Linux ( http://blackarch.org ).
# See COPYING for license details.

pkgname='gqrx'
pkgver='2.2.0'
pkgrel=8
groups=('blackarch' 'blackarch-wireless')
pkgdesc="SDR receiver for Funcube Dongle, RTL-SDR, USRP and OsmoSDR devices"
arch=('i686' 'x86_64' 'armv6h' 'armv7h')
url="http://www.oz9aec.net/index.php/gnu-radio/gqrx-sdr"
license=('GPL')
depends=('qt4>=4.6' 'boost-libs' 'fftw' 'libusb' 'gsl' 'alsa-lib' 'libpulse'
         'libuhd' 'pulseaudio' 'gnuradio' 'gnuradio-osmosdr')
makedepends=('boost' 'git')
install='gqrx.install'
source=("gqrx-$pkgver.tar.gz::https://codeload.github.com/csete/gqrx/tar.gz/v$pkgver"
        'gqrx.png'
        'gqrx.desktop')
sha1sums=('824c32bfe4b41a1126545680baedef25753361aa'
          '49e94c4dd1a388d3e27d27c96b210a6c799a69cc'
          '0011b64f5af5393a6dc11eaf4b27a556a238cbef')

build() {
  cd "$srcdir/gqrx-$pkgver"

  mkdir build
  cd build

  qmake-qt4 PREFIX=/usr/ ..
  make
}

package() {
  cd "$srcdir"

  # Base directories.
  mkdir -p "$pkgdir/usr/bin"
  mkdir -p "$pkgdir/usr/share/applications"
  mkdir -p "$pkgdir/usr/share/pixmaps"

  install -Dm644 gqrx.desktop "$pkgdir/usr/share/applications"
  install -Dm644 gqrx.png "$pkgdir/usr/share/pixmaps"

  cd "gqrx-$pkgver/build"

  make install "INSTALL_ROOT=$pkgdir"
}
