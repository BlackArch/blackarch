# This file is part of BlackArch Linux ( https://www.blackarch.org/ ).
# See COPYING for license details.

pkgname=python-chepy
_pkgname=chepy
pkgver=3.3.0
pkgrel=1
pkgdesc='Chepy is python lib/cli equivalent of the awesome CyberChef tool.'
arch=('any')
url='https://pypi.org/project/chepy/#files'
license=('GPL3')
depends=('python' 'python-colorama' 'python-crccheck' 'python-decorator'
         'python-emoji' 'exrex' 'python-fire' 'python-hashid' 'python-hexdump'
         'python-jsonpath-rw' 'python-jsonpickle' 'python-parsel'
         'python-phpserialize' 'python-pillow' 'python-prompt_toolkit'
         'python-pycipher' 'python-pycryptodome' 'python-pydash' 'python-pyjwt'
         'python-pyopenssl' 'python-pyperclip' 'python-editor' 'python-pyaml'
         'python-regex' 'python-typing_extensions' 'python-ujson'
         'python-base58' 'python-docstring-parser' 'python-termcolor'
         'python-lazy-import')
makedepends=('python-setuptools')
options=(!emptydirs)
source=("https://files.pythonhosted.org/packages/bb/0f/f1db5b099c41cb53a7d5a6f9ae02796235c2322369a5562cd44ab6d445ca/$_pkgname-$pkgver.tar.gz")
sha512sums=('ee8009226f70a16175be39e9b2f36256d1393302dfd601e484d98c5091952a5236983d8ac7fc7a4d6cd25c6c7e9df18b4a764e4305bbf737445e1ee46441e8b8')

prepare() {
  cd $_pkgname-$pkgver

  sed -i '8s/.*/fire/' requirements.txt
  sed -i '16s/.*/prompt_toolkit/' requirements.txt
}

build() {
  cd $_pkgname-$pkgver

  python setup.py build
}

package() {
  cd $_pkgname-$pkgver

  python setup.py install --root="$pkgdir" --prefix=/usr -O1 --skip-build
}

