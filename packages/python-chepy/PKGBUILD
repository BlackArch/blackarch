# This file is part of BlackArch Linux ( https://www.blackarch.org/ ).
# See COPYING for license details.

pkgname=python-chepy
_pkgname=chepy
pkgver=2.6.3
pkgrel=1
pkgdesc='Chepy is python lib/cli equivalent of the awesome CyberChef tool.'
arch=('any')
url='https://github.com/securisec/chepy'
license=('GPL3')
depends=('python' 'python-colorama' 'python-crccheck' 'python-decorator'
         'python-emoji' 'exrex' 'python-fire' 'python-hashid' 'python-hexdump'
         'python-jsonpath-rw' 'python-jsonpickle' 'python-parsel'
         'python-phpserialize' 'python-pillow' 'python-prompt_toolkit'
         'python-pycipher' 'python-pycryptodome' 'python-pydash' 'python-pyjwt'
         'python-pyopenssl' 'python-pyperclip' 'python-editor' 'python-pyaml'
         'python-regex' 'python-typing_extensions' 'python-ujson'
         'python-base58' 'python-docstring-parser' 'python-termcolor')
makedepends=('python-setuptools')
options=(!emptydirs)
source=("https://files.pythonhosted.org/packages/88/2a/279b84a569b7a5c59f7703cb25a5394b501fbc329b4c72f49969a76a3825/$_pkgname-$pkgver.tar.gz")
sha512sums=('4cf9c178c2386540b92d3b3974053a2f3cd7a1ed43051750846b42bf8f0f1d846b5a6e3ad1eae0a438b75fc64785ff19f33468072dd729f2c65c6c4b20aaba82')

prepare() {
  cd $_pkgname-$pkgver

  sed -i '8s/.*/fire/' requirements.txt
  sed -i '16s/.*/prompt_toolkit/' requirements.txt
}

build() {
  cd $_pkgname-$pkgver

  python setup.py build
}

package() {
  cd $_pkgname-$pkgver

  python setup.py install --root="$pkgdir" --prefix=/usr -O1 --skip-build
}

