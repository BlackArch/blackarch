# This file is part of BlackArch Linux ( https://www.blackarch.org/ ).
# See COPYING for license details.

pkgname=python-chepy
_pkgname=chepy
pkgver=2.6.2
pkgrel=1
pkgdesc='Chepy is python lib/cli equivalent of the awesome CyberChef tool.'
arch=('any')
url='https://github.com/securisec/chepy'
license=('GPL3')
depends=('python' 'python-colorama' 'python-crccheck' 'python-decorator'
         'python-emoji' 'exrex' 'python-fire' 'python-hashid' 'python-hexdump'
         'python-jsonpath-rw' 'python-jsonpickle' 'python-parsel'
         'python-phpserialize' 'python-pillow' 'python-prompt_toolkit'
         'python-pycipher' 'python-pycryptodome' 'python-pydash' 'python-pyjwt'
         'python-pyopenssl' 'python-pyperclip' 'python-editor' 'python-pyaml'
         'python-regex' 'python-typing_extensions' 'python-ujson'
         'python-base58' 'python-docstring-parser' 'python-termcolor')
makedepends=('python-setuptools')
options=(!emptydirs)
source=("https://files.pythonhosted.org/packages/92/9e/ea82030fa067bae59c8dbb117ff957a2d80b5e5ba02966d1e7b44d281d3d/$_pkgname-$pkgver.tar.gz")
sha512sums=('8672f06e4e8d8f41da1940ab6d06749d10b2c9493cd471b61ce0a3bda68c53efc99a100e9ee4de16f4604a7c717a379d34c85223b2369a09d7c6e1d1f72e2fe1')

prepare() {
  cd $_pkgname-$pkgver

  sed -i '8s/.*/fire/' requirements.txt
  sed -i '16s/.*/prompt_toolkit/' requirements.txt
}

build() {
  cd $_pkgname-$pkgver

  python setup.py build
}

package() {
  cd $_pkgname-$pkgver

  python setup.py install --root="$pkgdir" --prefix=/usr -O1 --skip-build
}

