# This file is part of BlackArch Linux ( https://www.blackarch.org/ ).
# See COPYING for license details.

pkgname=python-chepy
_pkgname=chepy
pkgver=3.1.0
pkgrel=1
pkgdesc='Chepy is python lib/cli equivalent of the awesome CyberChef tool.'
arch=('any')
url='https://github.com/securisec/chepy'
license=('GPL3')
depends=('python' 'python-colorama' 'python-crccheck' 'python-decorator'
         'python-emoji' 'exrex' 'python-fire' 'python-hashid' 'python-hexdump'
         'python-jsonpath-rw' 'python-jsonpickle' 'python-parsel'
         'python-phpserialize' 'python-pillow' 'python-prompt_toolkit'
         'python-pycipher' 'python-pycryptodome' 'python-pydash' 'python-pyjwt'
         'python-pyopenssl' 'python-pyperclip' 'python-editor' 'python-pyaml'
         'python-regex' 'python-typing_extensions' 'python-ujson'
         'python-base58' 'python-docstring-parser' 'python-termcolor'
         'python-lazy-import')
makedepends=('python-setuptools')
options=(!emptydirs)
source=("https://files.pythonhosted.org/packages/c0/55/9e3fb5b5e729dcaaf6de8e6d5ffa19a54268e3e6dd3dcd6404b4b16fd526/$_pkgname-$pkgver.tar.gz")
sha512sums=('971ac5a00dd48edddd5ab85fe1f21efdacbd725e8245fee7f2432ae9ee4f9af2ee40e549cdf0345353007738b3e3914a695c82144c4be944b66df180ab794556')

prepare() {
  cd $_pkgname-$pkgver

  sed -i '8s/.*/fire/' requirements.txt
  sed -i '16s/.*/prompt_toolkit/' requirements.txt
}

build() {
  cd $_pkgname-$pkgver

  python setup.py build
}

package() {
  cd $_pkgname-$pkgver

  python setup.py install --root="$pkgdir" --prefix=/usr -O1 --skip-build
}

