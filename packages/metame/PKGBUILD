# This file is part of BlackArch Linux ( http://blackarch.org ).
# See COPYING for license details.

pkgname='metame'
pkgver=2.82cfd20
pkgrel=1
pkgdesc='A simple metamorphic code engine for arbitrary executables.'
groups=('blackarch' 'blackarch-binary')
arch=('any')
url='https://github.com/a0rtega/metame'
license=('MIT')
depends=('python' 'python-simplejson' 'python-keystone' 'python-r2pipe'
         'radare2')
makedepends=('git')
source=('git+https://github.com/a0rtega/metame.git')
sha1sums=('SKIP')

pkgver() {
  cd "$srcdir/metame"

  echo $(git rev-list --count HEAD).$(git rev-parse --short HEAD)
}

build() {
  cd "$srcdir/metame"

  python setup.py build
}

package() {
  cd "$srcdir/metame"

  python setup.py install --root="$pkgdir" --prefix=/usr

  install -Dm644 -t "$pkgdir/usr/share/doc/metame/" README.md
  install -Dm644 LICENSE "$pkgdir/usr/share/licenses/metame/LICENSE"
}
