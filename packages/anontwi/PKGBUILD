# This file is part of BlackArch Linux ( http://blackarch.org ).
# See COPYING for license details.

pkgname='anontwi'
pkgver='1.0'
pkgrel=1
groups=('blackarch' 'blackarch-social')
pkgdesc='A free software python client designed to navigate anonymously on social networks. It supports Identi.ca and Twitter.com.'
arch=('any')
url='http://anontwi.sourceforge.net/'
license=('unknown')
depends=('python2' 'python2-crypto' 'python2-httplib2' 'python2-pycurl' 'gtk2'
        'pygtk')
source=("http://packetstorm.foofus.com/UNIX/utilities/anontwi_${pkgver}.tar.gz")
sha1sums=('4dbf6429d7d421d66b22fa9124b134bfab199b02')

package() {
  cd "$srcdir/anontwi"

  mkdir -p "$pkgdir/usr/bin"
  mkdir -p "$pkgdir/usr/share/anontwi"
  mkdir -p "$pkgdir/usr/share/doc/anontwi"

  install -Dm644 -t "$pkgdir/usr/share/doc/anontwi" README docs/AUTHOR \
    docs/INSTALL docs/THANKS
  install -Dm644 docs/COPYING "$pkgdir/usr/share/licenses/anontwi/COPYING"

  rm -rf docs

  cp -a * "$pkgdir/usr/share/anontwi"

  cat > "$pkgdir/usr/bin/anontwi" << EOF
#!/bin/sh
cd /usr/share/anontwi
exec python2 anontwi "\$@"
EOF

  chmod +x "$pkgdir/usr/bin/anontwi"
}
