# This file is part of BlackArch Linux ( https://www.blackarch.org/ ).
# See COPYING for license details.

pkgbase=python-wsaccel
pkgname=('python2-wsaccel' 'python-wsaccel')
_pkgname=wsaccel
pkgver=0.6.2
_commit=f16d0b8cd9b54b5b2d7698a5b41d5c38e1564fb6
pkgrel=7
pkgdesc='Accelerator for ws4py and AutobahnPython.'
arch=('x86_64' 'armv6h' 'armv7h' 'aarch64')
url='https://github.com/methane/wsaccel'
license=('Apache')
makedepends=('python2-setuptools' 'python-setuptools' 'cython2' 'cython')
source=("$pkgname-$_commit.tar.gz::https://github.com/methane/wsaccel/archive/$_commit.tar.gz")
sha512sums=('119adb53dc97b3a5c7b684ee2f793e195f9500f0df56ba58fded203e5b8fea45cee614d2a2c010d5918ee21d46b38aaa1c80ac018f1c7a333d18460501391398')

prepare() {
  cp -a "$_pkgname-$_commit" "$_pkgname-$_commit-2"
}

build() {
  cd "wsaccel-$_commit-2"

  python2 setup.py build

  cd "$srcdir/$_pkgname-$_commit"

  python setup.py build
}

package_python2-wsaccel() {
  depends=('python2')

  cd "$_pkgname-$_commit-2"

  python2 setup.py install --root="$pkgdir" -O1 --skip-build
}

package_python-wsaccel() {
  depends=('python')

  cd "$_pkgname-$_commit"

  python setup.py install --root="$pkgdir" -O1 --skip-build
}

