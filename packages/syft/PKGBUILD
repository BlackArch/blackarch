# This file is part of BlackArch Linux ( https://www.blackarch.org/ ).
# See COPYING for license details.

pkgname=syft
pkgver=640.360eb74
pkgrel=1
pkgdesc='A CLI tool and go library for generating a Software Bill of Materials (SBOM) from container images and filesystems.'
arch=('x86_64' 'aarch64')
groups=('blackarch' 'blackarch-forensic')
url='https://github.com/anchore/syft'
license=('Apache')
makedepends=('git' 'go')
source=("git+https://github.com/anchore/$pkgname.git")
sha512sums=('SKIP')

pkgver() {
  cd $pkgname

  echo $(git rev-list --count HEAD).$(git rev-parse --short HEAD)
}

build() {
  cd $pkgname

  GOPATH="$srcdir" go mod download
  GOPATH="$srcdir" go build \
    -trimpath \
    -buildmode=pie \
    -mod=readonly \
    -modcacherw \
    -ldflags "-s -w" \
    -o $pkgname-bin .
}

package() {
  cd $pkgname

  install -Dm 755 $pkgname-bin "$pkgdir/usr/bin/$pkgname"
  install -Dm 644 -t "$pkgdir/usr/share/doc/$pkgname" README.md
  install -Dm 644 LICENSE -t "$pkgdir/usr/share/licenses/$pkgname"
}

