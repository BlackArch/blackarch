# This file is part of BlackArch Linux ( http://blackarch.org ).
# See COPYING for license details.

pkgname='opensvp'
pkgver=65.df54ed8
pkgrel=1
groups=('blackarch' 'blackarch-exploitation' 'blackarch-networking')
pkgdesc='A security tool implementing "attacks" to be able to the resistance of firewall to protocol level attack.'
arch=('any')
url='https://github.com/regit/opensvp'
license=('GPL3')
depends=('python2' 'scapy' 'ftplib')
makedepends=('git' 'python2-setuptools')
source=('git+https://github.com/regit/opensvp.git')
sha1sums=('SKIP')

pkgver() {
  cd "$srcdir/opensvp"

  echo $(git rev-list --count HEAD).$(git rev-parse --short HEAD)
}

build() {
  cd "$srcdir/opensvp"

  python2 setup.py build
}

package() {
  cd "$srcdir/opensvp"

  install -Dm644 README.rst "$pkgdir/usr/share/doc/opensvp/README.rst"
  install -Dm644 INSTALL "$pkgdir/usr/share/doc/opensvp/INSTALL"

  python2 setup.py install --prefix=/usr --root="$pkgdir" --optimize=1
}
