# This file is part of BlackArch Linux ( https://www.blackarch.org/ ).
# See COPYING for license details.
#
# AUR contributors / lame backdoorers:
# Maintainer: Brian Bidulock <bidulock@openss7.org>
# Contributo: Jianhui Z <jianhui@outlook.com>
# Contributo: Tau Tsao <realturner at gmail.com>
# Contributor: Tomasz Zok <tomasz.zok [at] gmail.com>
# Contributor: techryda <techryda at silentdome dot com>
# Contributor: Mathias R. <pu154r@overlinux.org>

pkgname=xrdp
pkgver=0.9.19
pkgrel=2
pkgdesc='An open source remote desktop protocol (RDP) server.'
url='https://github.com/neutrinolabs/xrdp'
arch=('x86_64' 'aarch64')
license=('Apache')
depends=('tigervnc' 'libxrandr' 'fuse' 'libfdk-aac' 'ffmpeg')
makedepends=('nasm')
backup=('etc/xrdp/sesman.ini' 'etc/xrdp/xrdp.ini')
install="$pkgname.install"
source=("https://github.com/neutrinolabs/xrdp/releases/download/v$pkgver/xrdp-$pkgver.tar.gz"
        'arch-config.diff')
sha512sums=('6957880dd6e09388b08dc6036fee192f1f120194230ec875136f1a86016efa6f65b8c694ac8bc2357d20aa14e4b5633871082a4a89ae1789c627b761ebe9f260'
            'a6373ff33173c524ecfb1f37b3df18b64039ad89d60ce6c5bbf6de522bcdb554202039646d0f5de75da201bac8441271e49b44d83858ab9cbc50755146eb0a39')

prepare() {
  cd "$pkgname-$pkgver"

  patch -Np2 -b -z .orig <../arch-config.diff

  ./bootstrap
}

build() {
  cd "$pkgname-$pkgver"

  ./configure --prefix=/usr \
              --sysconfdir=/etc \
              --localstatedir=/var \
              --sbindir=/usr/bin \
              --with-systemdsystemunitdir=/usr/lib/systemd/system \
              --enable-jpeg \
              --enable-tjpeg \
              --enable-fuse \
              --enable-fdkaac \
              --enable-opus \
              --enable-rfxcodec \
              --enable-mp3lame \
              --enable-pixman \
              --enable-painter \
              --enable-vsock \
              --enable-ipv6 \
              --enable-pam-config=arch \
              --enable-rdpsndaudin

  # Fight unused direct deps
  sed -i -e 's/ -shared / -Wl,-O1,--as-needed\0 /g' -e \
    's/    if test "$export_dynamic" = yes && test -n "$export_dynamic_flag_spec"; then/      func_append compile_command " -Wl,-O1,--as-needed"\n      func_append finalize_command " -Wl,-O1,--as-needed"\n\0/' libtool

  make V=0
}

package() {
  cd "$pkgname-$pkgver"

  make DESTDIR="$pkgdir" install

  rm -f "$pkgdir/etc/xrdp/"{rsakeys.ini,key.pem,cert.pem}

  install -Dm 644 COPYING "$pkgdir/usr/share/licenses/$pkgname/COPYING"
}

