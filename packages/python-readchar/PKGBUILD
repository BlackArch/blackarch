# This file is part of BlackArch Linux ( https://www.blackarch.org/ ).
# See COPYING for license details.

pkgname=python-readchar
pkgver=95.e0edfa3
pkgrel=1
pkgdesc='Utilities to read single characters and key-strokes.'
arch=('any')
url='https://github.com/magmax/python-readchar'
license=('MIT')
depends=('python')
makedepends=('python-setuptools')
source=('git+https://github.com/magmax/python-readchar.git')
sha512sums=('SKIP')

pkgver() {
  cd $pkgname

  echo $(git rev-list --count HEAD).$(git rev-parse --short HEAD)
}

prepare() {
  cd "$pkgname"

  # psst, temp fix!
  sed -i 's|long_description=read_description()|long_description=""|' setup.py
}

build() {
  cd "$pkgname"

  python setup.py build
}

package() {
  cd "$pkgname"

  python setup.py install --prefix=/usr --root="$pkgdir" --optimize=1 \
    --skip-build
}

