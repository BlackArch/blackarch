# This file is part of BlackArch Linux ( https://www.blackarch.org/ ).
# See COPYING for license details.

pkgbase=python-readchar
pkgname=('python2-readchar' 'python-readchar')
_pkgname=python-readchar
pkgver=2.0.0
pkgrel=6
pkgdesc='Utilities to read single characters and key-strokes.'
arch=('any')
url='https://pypi.org/project/readchar/#files'
license=('MIT')
makedepends=('python2-setuptools' 'python-setuptools' 'python2-flake8')
source=("https://github.com/magmax/python-readchar/archive/$pkgver.tar.gz")
sha512sums=('92331e55080f5ebf55e313bd0e9562d55578df98ee116fa534a2ae47b43cbd9b19cf11263db821490a6b9ae5f62b78d378fd5b4c996d98a88c765af489b4d70f')

prepare() {
  cp -a "$_pkgname-$pkgver"{,-2}
}

build() {
  cd "$_pkgname-$pkgver-2"

  python2 setup.py build

  cd "$srcdir/$_pkgname-$pkgver"

  python setup.py build
}

package_python2-readchar() {
  depends=('python2')

  cd "$_pkgname-$pkgver-2"

  python2 setup.py install --prefix=/usr --root="$pkgdir" --optimize=1 \
    --skip-build
}

package_python-readchar() {
  depends=('python')

  cd "$_pkgname-$pkgver"

  python setup.py install --prefix=/usr --root="$pkgdir" --optimize=1 \
    --skip-build
}

