# This file is part of BlackArch Linux ( http://blackarch.org ).
# See COPYING for license details.

pkgname=python-readchar
pkgver=0.7
pkgrel=3
pkgdesc='Utilities to read single characters and key-strokes.'
arch=('any')
url='https://pypi.python.org/pypi/readchar/'
license=('MIT')
depends=('python')
makedepends=('python-setuptools')
source=("https://pypi.python.org/packages/source/r/readchar/readchar-${pkgver}.tar.gz")
sha512sums=('61af01d238a3b867c07526a9013998fac427957baae60d7e8ce22929ce163dbe84c3bb6c21f23b8ff1aafca2bf81c2acbb0fd10c96aab6269edc535633337b06')

prepare() {
  cd "$srcdir/readchar-$pkgver"

  # psst, temp fix!
  sed -i 's|long_description=read_description()|long_description=""|' setup.py
}

build() {
  cd "$srcdir/readchar-$pkgver"

  python setup.py build
}

package() {
  cd "$srcdir/readchar-$pkgver"

  python setup.py install --prefix=/usr --root="$pkgdir" --optimize=1
}
