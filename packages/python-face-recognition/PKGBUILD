# This file is part of BlackArch Linux ( https://www.blackarch.org/ ).
# See COPYING for license details.

pkgbase=python-face-recognition
pkgname=('python2-face-recognition' 'python-face-recognition')
_pkgname=face_recognition
pkgver=1.3.0
pkgrel=1
pkgdesc="The world's simplest facial recognition api for Python and the command line."
arch=('any')
url='https://pypi.org/project/face_recognition/#files'
makedepends=('python2-setuptools' 'python-setuptools')
license=('MIT')
source=("https://files.pythonhosted.org/packages/6c/49/75dda409b94841f01cbbc34114c9b67ec618265084e4d12d37ab838f4fd3/face_recognition-$pkgver.tar.gz")
sha512sums=('344e83f69160bced6da358360a42e124530a46f4e1c5a4c8a4b250ad45f99498aece4b870bf85e3e15fc0f5da28afd3dfc780c2bef7f2a37ec74060b2547456c')

prepare() {
  cp -a "$_pkgname-$pkgver"{,-2}
}

build() {
  cd "$_pkgname-$pkgver-2"

  python2 setup.py build

  cd "$srcdir/$_pkgname-$pkgver"

  python setup.py build
}

package_python2-face-recognition() {
  depends=('python2-click' 'python2-dlib' 'python2-face-recognition-models'
           'python2-numpy' 'python2-pillow' 'python2-scipy')

  cd "$_pkgname-$pkgver-2"

  python2 setup.py install --prefix=/usr --root="$pkgdir" --optimize=1 \
    --skip-build

  for i in "$pkgdir/usr/bin/"*; do
    mv $i "${i}2"
  done
}

package_python-face-recognition() {
  depends=('python-click' 'python-dlib' 'python-face-recognition-models'
           'python-numpy' 'python-pillow' 'python-scipy')

  cd "$_pkgname-$pkgver"

  python setup.py install --prefix=/usr --root="$pkgdir" --optimize=1 \
    --skip-build
}

