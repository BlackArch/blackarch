# This file is part of BlackArch Linux ( https://www.blackarch.org/ ).
# See COPYING for license details.

pkgbase=python-face-recognition
pkgname=('python2-face-recognition' 'python-face-recognition')
_pkgname=face_recognition
pkgver=1.2.3
pkgrel=2
pkgdesc="The world's simplest facial recognition api for Python and the command line."
arch=('any')
url='https://pypi.org/project/face_recognition/#files'
makedepends=('python2-setuptools' 'python-setuptools')
license=('MIT')
source=("https://files.pythonhosted.org/packages/a3/f3/99c0fdebe04ab2ed12a6b9a6b6c568a6ae9c757da700215a24aaa8ab9070/$_pkgname-$pkgver.tar.gz")
sha512sums=('e4dce03138da4f81976586210907da0a8430dca12d5b7b3a464d63c6872afa63442f2a9e64a43dccdfa6efa2ebd6a2e0e38dc99db472d4d4962b3dd264235075')

prepare() {
  cp -a "$_pkgname-$pkgver"{,-2}
}

build() {
  cd "$_pkgname-$pkgver-2"

  python2 setup.py build

  cd "$srcdir/$_pkgname-$pkgver"

  python setup.py build
}

package_python2-face-recognition() {
  depends=('python2-click' 'python2-dlib' 'python2-face-recognition-models'
           'python2-numpy' 'python2-pillow' 'python2-scipy')

  cd "$_pkgname-$pkgver-2"

  python2 setup.py install --prefix=/usr --root="$pkgdir" --optimize=1 \
    --skip-build
}

package_python-face-recognition() {
  depends=('python-click' 'python-dlib' 'python-face-recognition-models'
           'python-numpy' 'python-pillow' 'python-scipy')

  cd "$_pkgname-$pkgver"

  python setup.py install --prefix=/usr --root="$pkgdir" --optimize=1 \
    --skip-build
}

