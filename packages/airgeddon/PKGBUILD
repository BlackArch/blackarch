# This file is part of BlackArch Linux ( https://www.blackarch.org/ ).
# See COPYING for license details.

pkgname=airgeddon
pkgver=1818.9f7f6e2
pkgrel=1
pkgdesc='Multi-use bash script for Linux systems to audit wireless networks.'
groups=('blackarch' 'blackarch-wireless' 'blackarch-automation')
arch=('any')
url='https://github.com/v1s1t0r1sh3r3/airgeddon'
license=('GPL3')
depends=('bash>=4.2' 'xterm' 'iw' 'gawk' 'aircrack-ng' 'net-tools' 'coreutils'
         'wireless_tools' 'sed' 'iproute2' 'tmux')
optdepends=('hashcat' 'crunch' 'wpaclean' 'mdk4' 'reaver' 'beef' 'hostapd'
            'lighttpd' 'iptables' 'bettercap' 'ettercap' 'sslstrip' 'dsniff'
            'bully' 'pixiewps' 'expect' 'dhcp' 'curl' 'ethtool' 'pciutils'
            'rfkill' 'wget' 'usbutils' 'xorg-xdpyinfo' 'ccze' 'xorg-xset'
            'asleap' 'john' 'hostapd-wpe' 'nftables' 'openssl' 'mdk3')
makedepends=('git' 'coreutils' 'binutils')
source=("git+https://github.com/v1s1t0r1sh3r3/$pkgname.git")
sha512sums=('SKIP')

pkgver() {
  cd $pkgname

  echo $(git rev-list --count HEAD).$(git rev-parse --short HEAD)
}

prepare() {
  cd $pkgname

  sed -i 's|AIRGEDDON_AUTO_UPDATE=true|AIRGEDDON_AUTO_UPDATE=false|' .airgeddonrc
}

package() {
  cd $pkgname

  install -dm 755 "$pkgdir/usr/bin"
  install -dm 755 "$pkgdir/usr/share/$pkgname"

  install -Dm 644 -t "$pkgdir/usr/share/doc/$pkgname/" README.md CHANGELOG.md \
    CONTRIBUTING.md
  install -Dm 644 LICENSE "$pkgdir/usr/share/licenses/$pkgname/LICENSE"

  rm -rf *.md .github binaries imgs Dockerfile pindb_checksum.txt .editorconfig

  cp -a --no-preserve=ownership * "$pkgdir/usr/share/$pkgname"

  cat > "$pkgdir/usr/bin/$pkgname" << EOF
#!/bin/sh
cd /usr/share/airgeddon
exec bash $pkgname.sh "\$@"
EOF

  chmod +x "$pkgdir/usr/bin/$pkgname"
}

