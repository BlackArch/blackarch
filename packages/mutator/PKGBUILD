# This file is part of BlackArch Linux ( https://www.blackarch.org/ ).
# See COPYING for license details.

pkgname=mutator
pkgver=v0.2.2.r2.g164132d
pkgrel=1
epoch=1
groups=('blackarch' 'blackarch-automation' 'blackarch-misc')
pkgdesc='This project aims to be a wordlist mutator with hormones, which means that some mutations will be applied to the result of the ones that have been already done, resulting in something like: corporation -> C0rp0r4t10n_2012'
arch=('x86_64' 'aarch64')
url='https://bitbucket.org/alone/mutator/'
license=('GPL-3.0-or-later')
makedepends=('git')
source=("git+https://bitbucket.org/alone/$pkgname.git"
        'main.h.patch'
        'main.c.patch')
sha512sums=('SKIP'
            'f2bc7da90a70597c20ffcaef3c83964eeea45ba963c3dfff58bf752b8fd9d34511f134669ca1bae38216f1c06f9d0e395019e1a0ffbaee09c4f3f25e1245d005'
            '0f01f7f0900b0903a80c4c513bc14daa2f3b6f5b75fe8ee4ed43654dd5414a2a57844af9628c491520c3094bbe135d3ee12d03921a42bb9bafff04eb2c4fda35')

pkgver() {
  cd $pkgname

  ( set -o pipefail
    git describe --long --tags --abbrev=7 2>/dev/null |
      sed 's/\([^-]*-g\)/r\1/;s/-/./g' ||
    printf "%s.%s" "$(git rev-list --count HEAD)" \
      "$(git rev-parse --short=7 HEAD)"
  )
}

prepare() {
  cd $pkgname

  patch -Np0 < "$srcdir/main.h.patch"
  patch -Np0 < "$srcdir/main.c.patch"
}

build() {
  cd $pkgname

  make
}

package() {
  cd $pkgname

  install -dm 755 "$pkgdir/usr/share/doc/$pkgname"

  install -Dm 755 $pkgname "$pkgdir/usr/bin/$pkgname"
  install -Dm 644 -t "$pkgdir/usr/share/doc/$pkgname/" INSTALL README TODO
  install -Dm 644 COPYING "$pkgdir/usr/share/licenses/$pkgname/COPYING"
}

