# This file is part of BlackArch Linux ( https://www.blackarch.org/ ).
# See COPYING for license details.

pkgname=webkiller
pkgver=36.7ad72d3
pkgrel=2
pkgdesc='Tool Information Gathering Write By Python.'
groups=('blackarch' 'blackarch-webapp' 'blackarch-fingerprint'
        'blackarch-recon')
arch=('any')
url='https://github.com/ultrasecurity/webkiller'
license=('custom:unknown')
depends=('python' 'python-requests' 'python-colorama' 'python-ipapi'
         'python-builtwith')
makedepends=('git')
source=("git+https://github.com/ultrasecurity/$pkgname.git")
sha512sums=('SKIP')

pkgver() {
  cd $pkgname

  echo $(git rev-list --count HEAD).$(git rev-parse --short HEAD)
}

package() {
  cd $pkgname

  install -dm 755 "$pkgdir/usr/bin"
  install -dm 755 "$pkgdir/usr/share/$pkgname"

  install -Dm 644 -t "$pkgdir/usr/share/doc/$pkgname/" README.md

  rm README.md

  cp -a * "$pkgdir/usr/share/$pkgname/"

  cat > "$pkgdir/usr/bin/$pkgname" << EOF
#!/bin/sh
cd /usr/share/$pkgname
exec python $pkgname.py "\$@"
EOF

  chmod +x "$pkgdir/usr/bin/$pkgname"
}

