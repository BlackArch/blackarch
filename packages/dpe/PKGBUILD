pkgname=dpeparser
pkgver=20131217
pkgrel=1
pkgdesc="Default password enumeration project"
arch=('any')
url="http://www.toolswatch.org/dpe/"
license=('GPL')
groups=('blackarch' 'blackarch-analysis')
source=('http://www.toolswatch.org/dpe/dpeparser.py')
md5sums=('f802cc1d26190fdba0f4cfe5a7f1aaa8')

pkgver() {
  date +%Y%m%d
}

prepare() {
  cd "$srcdir"

  sed -i 's#MydpeDB ="dpe_db.xml"#MydpeDB = "/usr/share/dpeparser/dpe_db.xml"#' dpeparser.py
}

build() {
  cd "$srcdir"

  python2 dpeparser.py -u
}

package() {
  cd "$srcdir"

  install -dm755 "$pkgdir/usr/bin"
  install -dm755 "$pkgdir/usr/share/dpeparser"

  install -m755 dpeparser.py "$pkgdir/usr/share/dpeparser"
  install -m644 dpe_db.xml "$pkgdir/usr/share/dpeparser"

  cat > "$pkgdir/usr/bin/dpeparser" <<EOF
#!/bin/bash
python2 /usr/share/dpeparser/dpeparser.py "\$@"
EOF
  chmod 755 "$pkgdir/usr/bin/dpeparser"
}
