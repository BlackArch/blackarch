# This file is part of BlackArch Linux ( http://blackarch.org ).
# See COPYING for license details.

pkgname='dirb'
pkgver='2.04'
pkgrel=7
groups=('blackarch' 'blackarch-scanner' 'blackarch-webapp')
pkgdesc="A web content scanner, brute forceing for hidden files"
url='http://dirb.sourceforge.net/'
arch=('i686' 'x86_64' 'armv6h' 'armv7h')
license=("GPL2")
depends=('curl')
source=("http://open-labs.org/dirb204.tar.gz")
sha1sums=('fa6e9253ad9655390cc8a583485f5178545d7e91')

prepare() {
  sed -i 's|wordlists/|/usr/share/dirb/wordlists/|' "$srcdir/dirb/src/dirb.c"
}

build () {
  cd $srcdir/dirb
  ./configure --prefix=/usr
  make
}

package() {
  cd "$srcdir/dirb"

  make DESTDIR="$pkgdir" install

  mkdir -p "$pkgdir/usr/share/dirb"

  cp --no-preserve=ownership -r "$srcdir/dirb/wordlists" "$pkgdir/usr/share/dirb/"

  chmod -R 644 "$pkgdir/usr/share/dirb/wordlists"
  chmod -R +X "$pkgdir/usr/share/dirb/wordlists"

  # Fix conflict with extra/icu.
  mv "$pkgdir/usr/bin/gendict" "$pkgdir/usr/bin/dirb-gendict"
}
