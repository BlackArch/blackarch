pkgname=dirb
pkgver=2.04
pkgrel=5
pkgdesc="A web content scanner, brute forceing for hidden files"
url='http://dirb.sourceforge.net/'
groups=('blackarch' 'blackarch-web')
arch=('i686' 'x86_64')
license=("GPL2")
depends=('curl')
source=("http://open-labs.org/dirb204.tar.gz")
md5sums=('2b2b2caacfff87d14b9fa39365eb25e9')

prepare() {
  sed -i 's|wordlists/|/usr/share/dirb/wordlists/|' "$srcdir/dirb/src/dirb.c"
}

build () {
  cd $srcdir/dirb
  ./configure --prefix=/usr
  make
}

package() {
  cd "$srcdir/dirb"

  make DESTDIR="$pkgdir" install

  install -dm755 "$pkgdir/usr/share/dirb"

  cp --no-preserve=ownership -r "$srcdir/dirb/wordlists" "$pkgdir/usr/share/dirb/"

  chmod -R 644 "$pkgdir/usr/share/dirb/wordlists"
  chmod -R +X "$pkgdir/usr/share/dirb/wordlists"

  # Fix conflict with extra/icu.
  mv "$pkgdir/usr/bin/gendict" "$pkgdir/usr/bin/dirb-gendict"
}
