# This file is part of BlackArch Linux ( http://blackarch.org ).
# See COPYING for license details.

pkgname='dirb'
pkgver='2.22'
pkgrel=2
groups=('blackarch' 'blackarch-scanner' 'blackarch-webapp')
pkgdesc='A web content scanner, brute forceing for hidden files.'
url='http://dirb.sourceforge.net/'
arch=('x86_64' 'armv6h' 'armv7h' 'aarch64')
license=('GPL2')
depends=('curl')
source=("http://downloads.sourceforge.net/project/dirb/dirb/${pkgver}/dirb${pkgver//./}.tar.gz")
sha1sums=('21851ad341357917a9672c834ccf90509c957f75')

prepare() {
  cd "$srcdir"

  chmod -R 744 dirb${pkgver//./}

  #sed -i 's|wordlists/|/usr/share/dirb/wordlists/|' "dirb${pkgver//./}/src/dirb.c"
}

build () {
  cd "$srcdir/dirb${pkgver//./}"

  ./configure --prefix=/usr

  make
}

package() {
  cd "$srcdir/dirb${pkgver//./}"

  make DESTDIR="$pkgdir" install

  mkdir -p "$pkgdir/usr/share/dirb"

  cp -a "wordlists" "$pkgdir/usr/share/dirb/"

  chmod -R 644 "$pkgdir/usr/share/dirb/wordlists"
  chmod -R +X "$pkgdir/usr/share/dirb/wordlists"

  # Fix conflict with extra/icu.
  mv "$pkgdir/usr/bin/gendict" "$pkgdir/usr/bin/dirb-gendict"
}
