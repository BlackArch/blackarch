# This file is part of BlackArch Linux ( https://www.blackarch.org/ ).
# See COPYING for license details.

pkgname=suricata
pkgver=4.1.0
pkgrel=2
pkgdesc='An Open Source Next Generation Intrusion Detection and Prevention Engine.'
arch=('x86_64' 'armv6h' 'armv7h' 'aarch64')
url='http://openinfosecfoundation.org/index.php/download-suricata'
groups=('blackarch' 'blackarch-defensive' 'blackarch-ids')
license=('GPL2')
depends=('zlib' 'nss' 'jansson' 'libcap-ng' 'libnetfilter_queue' 'libnfnetlink'
         'libnet' 'libpcap' 'libyaml' 'pcre' 'python2-magic' 'file' 'lua'
         'luajit' 'geoip' 'libnetfilter_log')
optdepends=('snort: suricata can use rulesets provided by snort')
backup=('etc/suricata/suricata.yaml')
source=("http://openinfosecfoundation.org/download/suricata-$pkgver.tar.gz")
sha512sums=('cdffa4cd2ffa00140ff28f4c1194ee48cbe5886f482285a3ff555322011ca218d0951b70ee74d87ded70a0d5c224ad0b26e3f855d7a3af7f019a287dc0dad165')

prepare() {
  cd "$pkgname-$pkgver"

  find "$srcdir" -type f -name '*.py' -exec \
    sed -i 's|#!/usr/bin/env python|#!/usr/bin/env python2|' {} +
  find "$srcdir" -type f -name '*.py' -exec \
    sed -i 's|#!/usr/bin/python|#!/usr/bin/python2|' {} +
}

build() {
  cd "$pkgname-$pkgver"

  ./configure --prefix=/usr --sysconfdir=/etc --localstatedir=/var \
    HAVE_PYTHON_CONFIG=/usr/bin/python2 --enable-gccprotect \
    --enable-nfqueue --enable-nflog --enable-luajit --enable-geoip \
    --disable-coccinelle --enable-old-barnyard2 --with-pic \
    --enable-python

  make
}

package() {
  cd "$pkgname-$pkgver"

  install -dm 755 "$pkgdir/var/log/$pkgname"
  install -dm 755 "$pkgdir/etc/$pkgname/rules"

  make DESTDIR="$pkgdir" install

  install -Dm 644 suricata.yaml "$pkgdir/etc/$pkgname/suricata.yaml"
  install -Dm 644 classification.config \
    "$pkgdir/etc/$pkgname/classification.config"
  install -Dm 644 reference.config "$pkgdir/etc/$pkgname/reference.config"
  install -Dm 644 rules/*.rules "$pkgdir/etc/$pkgname/rules/"
}

