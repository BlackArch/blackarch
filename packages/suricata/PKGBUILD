# This file is part of BlackArch Linux ( https://www.blackarch.org/ ).
# See COPYING for license details.

pkgname=suricata
pkgver=6.0.4
pkgrel=2
pkgdesc='An Open Source Next Generation Intrusion Detection and Prevention Engine.'
arch=('x86_64' 'aarch64')
url='https://suricata-ids.org/'
groups=('blackarch' 'blackarch-defensive' 'blackarch-ids')
license=('GPL2')
depends=('zlib' 'nss' 'jansson' 'libcap-ng' 'libnetfilter_queue' 'libnfnetlink'
         'libnet' 'libpcap' 'libyaml' 'pcre' 'python-magic' 'file' 'lua'
         'luajit' 'geoip' 'libnetfilter_log' 'libmaxminddb')
makedepends=('cargo')
optdepends=('snort: suricata can use rulesets provided by snort')
backup=('etc/suricata/suricata.yaml')
source=("http://openinfosecfoundation.org/download/$pkgname-$pkgver.tar.gz")
sha512sums=('8dccea669e50758def06fe3f3e8d5048d76b27a80b5f96a7b56d2ab6e4da358d8b17ec2d764e1b53cc6a1334ee0b14191cc80f5dcf18cc4d804d5c530290adf0')

prepare() {
  cd "$pkgname-$pkgver"

#  find "$srcdir" -type f -name '*.py' -exec \
#    sed -i 's|#!/usr/bin/env python|#!/usr/bin/env python2|' {} +
#  find "$srcdir" -type f -name '*.py' -exec \
#    sed -i 's|#!/usr/bin/python|#!/usr/bin/python2|' {} +
}

build() {
  cd "$pkgname-$pkgver"

  ./configure --prefix=/usr --sysconfdir=/etc --localstatedir=/var \
    HAVE_PYTHON_CONFIG=/usr/bin/python3 --enable-gccprotect \
    --enable-nfqueue --enable-nflog --enable-luajit --enable-geoip \
    --disable-coccinelle --enable-old-barnyard2 --with-pic \
    --enable-python

  make
}

package() {
  cd "$pkgname-$pkgver"

  install -dm 755 "$pkgdir/var/log/$pkgname"
  install -dm 755 "$pkgdir/etc/$pkgname/rules"

  make DESTDIR="$pkgdir" install

  install -Dm 644 "$pkgname.yaml" "$pkgdir/etc/$pkgname/$pkgname.yaml"
  install -Dm 644 etc/classification.config \
    "$pkgdir/etc/$pkgname/classification.config"
  install -Dm 644 etc/reference.config "$pkgdir/etc/$pkgname/reference.config"
  install -Dm 644 rules/*.rules "$pkgdir/etc/$pkgname/rules/"
}

