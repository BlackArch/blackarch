# This file is part of BlackArch Linux ( http://blackarch.org ).
# See COPYING for license details.

pkgname='suricata'
pkgver='3.0'
pkgrel=2
pkgdesc='An Open Source Next Generation Intrusion Detection and Prevention Engine.'
arch=('i686' 'x86_64' 'armv6h' 'armv7h')
url='http://openinfosecfoundation.org/index.php/download-suricata'
groups=('blackarch' 'blackarch-defensive')
license=('GPL2')
depends=('zlib' 'nss' 'jansson' 'libcap-ng' 'libnetfilter_queue' 'libnfnetlink'
         'libnet' 'libpcap' 'libyaml' 'pcre' 'python2-magic' 'file' 'lua'
         'luajit' 'geoip' 'libnetfilter_log')
optdepends=('snort: suricata can use rulesets provided by snort')
backup=('etc/suricata/suricata.yaml')
source=("http://openinfosecfoundation.org/download/suricata-${pkgver}.tar.gz")
sha1sums=('91a4df0235e6088e47f92472599007b40d7b4f90')

prepare() {
  cd "$srcdir/suricata-$pkgver"


  find "$srcdir" -type f -name '*.py' -exec \
    sed -i 's|#!/usr/bin/env python|#!/usr/bin/env python2|' {} +
  find "$srcdir" -type f -name '*.py' -exec \
    sed -i 's|#!/usr/bin/python|#!/usr/bin/python2|' {} +
}

build() {
  cd "$srcdir/suricata-$pkgver"

  ./configure --prefix=/usr --sysconfdir=/etc --localstatedir=/var \
    HAVE_PYTHON_CONFIG=/usr/bin/python2 --enable-gccprotect \
    --enable-nfqueue --enable-nflog --enable-lua \
    --enable-geoip --disable-coccinelle --enable-old-barnyard2 --with-pic

  make
}

package() {
  cd "$srcdir/suricata-$pkgver"

  mkdir -p "$pkgdir/var/log/suricata"
  mkdir -p "$pkgdir/etc/suricata/rules"

  make DESTDIR="$pkgdir" install

  install -Dm644 suricata.yaml "$pkgdir/etc/suricata/suricata.yaml"
  install -Dm644 classification.config \
    "$pkgdir/etc/suricata/classification.config"
  install -Dm644 reference.config "$pkgdir/etc/suricata/reference.config"
  install -Dm644 rules/*.rules "$pkgdir/etc/suricata/rules/"
}
