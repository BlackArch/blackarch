# This file is part of BlackArch Linux ( https://www.blackarch.org/ ).
# See COPYING for license details.

pkgname=suricata
pkgver=5.0.0
pkgrel=1
pkgdesc='An Open Source Next Generation Intrusion Detection and Prevention Engine.'
arch=('x86_64' 'aarch64')
url='https://suricata-ids.org/'
groups=('blackarch' 'blackarch-defensive' 'blackarch-ids')
license=('GPL2')
depends=('zlib' 'nss' 'jansson' 'libcap-ng' 'libnetfilter_queue' 'libnfnetlink'
         'libnet' 'libpcap' 'libyaml' 'pcre' 'python2-magic' 'file' 'lua'
         'luajit' 'geoip' 'libnetfilter_log' 'libmaxminddb')
makedepends=('cargo')
optdepends=('snort: suricata can use rulesets provided by snort')
backup=('etc/suricata/suricata.yaml')
source=("http://openinfosecfoundation.org/download/$pkgname-$pkgver.tar.gz")
sha512sums=('0dc8941fdf29d615531eeda6f6076052cca79fda6dda3c96300c08b343a64a1700fd23dd83a03507009ab7c9b19c91b65ee65e704f55ddee17764b71e9e2911e')

prepare() {
  cd "$pkgname-$pkgver"

  find "$srcdir" -type f -name '*.py' -exec \
    sed -i 's|#!/usr/bin/env python|#!/usr/bin/env python2|' {} +
  find "$srcdir" -type f -name '*.py' -exec \
    sed -i 's|#!/usr/bin/python|#!/usr/bin/python2|' {} +
}

build() {
  cd "$pkgname-$pkgver"

  ./configure --prefix=/usr --sysconfdir=/etc --localstatedir=/var \
    HAVE_PYTHON_CONFIG=/usr/bin/python2 --enable-gccprotect \
    --enable-nfqueue --enable-nflog --enable-luajit --enable-geoip \
    --disable-coccinelle --enable-old-barnyard2 --with-pic \
    --enable-python

  make
}

package() {
  cd "$pkgname-$pkgver"

  install -dm 755 "$pkgdir/var/log/$pkgname"
  install -dm 755 "$pkgdir/etc/$pkgname/rules"

  make DESTDIR="$pkgdir" install

  install -Dm 644 "$pkgname.yaml" "$pkgdir/etc/$pkgname/$pkgname.yaml"
  install -Dm 644 classification.config \
    "$pkgdir/etc/$pkgname/classification.config"
  install -Dm 644 reference.config "$pkgdir/etc/$pkgname/reference.config"
  install -Dm 644 rules/*.rules "$pkgdir/etc/$pkgname/rules/"
}

