# This file is part of BlackArch Linux ( https://www.blackarch.org/ ).
# See COPYING for license details.

pkgname=suricata
pkgver=6.0.10
pkgrel=1
pkgdesc='An Open Source Next Generation Intrusion Detection and Prevention Engine.'
arch=('x86_64' 'aarch64')
url='https://suricata.io/download/'
groups=('blackarch' 'blackarch-defensive' 'blackarch-ids')
license=('GPL2')
depends=('zlib' 'nss' 'jansson' 'libcap-ng' 'libnetfilter_queue' 'libnfnetlink'
         'libnet' 'libpcap' 'libyaml' 'pcre' 'python-magic' 'file' 'lua'
         'luajit' 'geoip' 'libnetfilter_log' 'libmaxminddb')
makedepends=('cargo')
optdepends=('snort: suricata can use rulesets provided by snort')
backup=('etc/suricata/suricata.yaml')
source=("http://openinfosecfoundation.org/download/$pkgname-$pkgver.tar.gz")
sha512sums=('3e49d491cf1fb56a6773308380cae826016041018c18753d18529572712a6ffa415df2798805bffdf8482312fdb69c3f2e05f38870a5b725d333f928b840e1e2')

prepare() {
  cd "$pkgname-$pkgver"

#  find "$srcdir" -type f -name '*.py' -exec \
#    sed -i 's|#!/usr/bin/env python|#!/usr/bin/env python2|' {} +
#  find "$srcdir" -type f -name '*.py' -exec \
#    sed -i 's|#!/usr/bin/python|#!/usr/bin/python2|' {} +
}

build() {
  cd "$pkgname-$pkgver"

  ./configure --prefix=/usr --sysconfdir=/etc --localstatedir=/var \
    HAVE_PYTHON_CONFIG=/usr/bin/python3 --enable-gccprotect \
    --enable-nfqueue --enable-nflog --enable-luajit --enable-geoip \
    --disable-coccinelle --enable-old-barnyard2 --with-pic \
    --enable-python

  make
}

package() {
  cd "$pkgname-$pkgver"

  install -dm 755 "$pkgdir/var/log/$pkgname"
  install -dm 755 "$pkgdir/etc/$pkgname/rules"

  make DESTDIR="$pkgdir" install

  install -Dm 644 "$pkgname.yaml" "$pkgdir/etc/$pkgname/$pkgname.yaml"
  install -Dm 644 etc/classification.config \
    "$pkgdir/etc/$pkgname/classification.config"
  install -Dm 644 etc/reference.config "$pkgdir/etc/$pkgname/reference.config"
  install -Dm 644 rules/*.rules "$pkgdir/etc/$pkgname/rules/"
}

