# This file is part of BlackArch Linux ( http://blackarch.org ).
# See COPYING for license details.

pkgname='nosqlmap'
pkgver=238.ae0b461
pkgrel=1
groups=('blackarch' 'blackarch-webapp' 'blackarch-exploitation')
pkgdesc='Automated Mongo database and NoSQL web application exploitation tool'
arch=('any')
url='https://github.com/tcstool/NoSQLMap.git'
license=('GPL3')
depends=('python2' 'python2-pymongo' 'python2-httplib2' 'python2-pbkdf2'
         'python2-ipcalc' 'python2-couchdb' 'python2-requests' 'mongodb')
makedepends=('git' 'python2-setuptools')
source=('git+https://github.com/tcstool/NoSQLMap.git' 'nosqlmap.py.patch')
sha1sums=('SKIP'
          '95d5a7e0da2cd6926d060ca8470f7788ab13922d')

pkgver() {
  cd "$srcdir/NoSQLMap"

  echo $(git rev-list --count HEAD).$(git rev-parse --short HEAD)
}

prepare() {
  cd "$srcdir/NoSQLMap"

  #sed -i '2i\#-*- coding: utf-8 -*-' nosqlmap.py

  patch -Np0 -i ../nosqlmap.py.patch
}

package() {
  cd "$srcdir/NoSQLMap"

  mkdir -p "$pkgdir/usr/bin"
  mkdir -p "$pkgdir/usr/share/nosqlmap"

  install -Dm644 -t "$pkgdir/usr/share/doc/nosqlmap/" README.md TODO
  install -Dm644 COPYING "$pkgdir/usr/share/licenses/nosqlmap/COPYING"

  rm README.md TODO COPYING

  cp -a * "$pkgdir/usr/share/nosqlmap/"

  cat > "$pkgdir/usr/bin/nosqlmap" << EOF
#!/bin/sh
cd /usr/share/nosqlmap/
exec python2 nosqlmap.py "\${@}"
EOF

  chmod a+x "$pkgdir/usr/bin/nosqlmap"
}
