# This file is part of BlackArch Linux ( http://blackarch.org ).
# See COPYING for license details.

pkgname='lhf'
pkgver=40.51568ee
pkgrel=1
pkgdesc='A modular recon tool for pentesting.'
groups=('blackarch' 'blackarch-recon')
arch=('any')
url='https://github.com/blindfuzzy/LHF'
license=('GPL3')
depends=('python2' 'arachni' 'dirb' 'nmap' 'hydra' 'sqlmap' 'enum4linux' 'nikto'
         'python2-python-libnmap' 'python2-selenium')
makedepends=('git')
source=('git+https://github.com/blindfuzzy/LHF.git')
sha1sums=('SKIP')

pkgver() {
  cd "$srcdir/LHF"

  echo $(git rev-list --count HEAD).$(git rev-parse --short HEAD)
}

package() {
  cd "$srcdir/LHF"

  mkdir -p "$pkgdir/usr/bin"
  mkdir -p "$pkgdir/usr/share/lhf"

  install -Dm644 -t "$pkgdir/usr/share/doc/lhf/" README.md
  install -Dm644 LICENSE "$pkgdir/usr/share/licenses/lhf/LICENSE"

  rm README.md LICENSE

  cp -a * "$pkgdir/usr/share/lhf/"

  cat > "$pkgdir/usr/bin/lhf" << EOF
#!/bin/sh
cd /usr/share/lhf
exec python2 LHF.py "\${@}"
EOF

  chmod a+x "$pkgdir/usr/bin/lhf"
}
