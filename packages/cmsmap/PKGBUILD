# This file is part of BlackArch Linux ( https://www.blackarch.org/ ).
# See COPYING for license details.

pkgname=cmsmap
pkgver=8.59dd0e2
pkgrel=1
epoch=1
groups=('blackarch' 'blackarch-scanner' 'blackarch-automation'
        'blackarch-webapp' 'blackarch-exploitation')
pkgdesc='A python open source Content Management System scanner that automates the process of detecting security flaws of the most popular CMSs.'
arch=('any')
url='https://www.dionach.com/blog/cmsmap-%E2%80%93-a-simple-cms-vulnerability-scanner'
license=('GPL3')
depends=('python2')
makedepends=('git')
source=("$pkgname::git+https://github.com/Dionach/CMSmap.git")
sha512sums=('SKIP')

pkgver() {
  cd $pkgname

  echo $(git rev-list --count HEAD).$(git rev-parse --short HEAD)
}

package() {
  cd $pkgname

  install -dm 755 "$pkgdir/usr/bin"
  install -dm 755 "$pkgdir/usr/share/$pkgname"

  install -Dm 644 -t "$pkgdir/usr/share/doc/$pkgname" README.md DISCLAIMER.txt
  install -Dm 644 LICENSE.txt "$pkgdir/usr/share/licenses/$pkgname/LICENSE"

  rm README.md DISCLAIMER.txt LICENSE.txt

  cp -a * "$pkgdir/usr/share/$pkgname/"

  cat > "$pkgdir/usr/bin/$pkgname" << EOF
#!/bin/sh
cd /usr/share/$pkgname
exec python2 $pkgname.py "\$@"
EOF

  chmod +x "$pkgdir/usr/bin/$pkgname"
}

