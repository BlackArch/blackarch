# This file is part of BlackArch Linux ( http://blackarch.org ).
# See COPYING for license details.

pkgname='arybo'
pkgver=38.57c7f90
pkgrel=1
pkgdesc='Manipulation, canonicalization and identification of mixed boolean-arithmetic symbolic expressions.'
groups=('blackarch' 'blackarch-misc')
arch=('any')
url='https://github.com/quarkslab/arybo'
license=('BSD')
depends=('python' 'python-networkx' 'python-six' 'python-pytanque' 'ipython')
makedepends=('git')
source=('git+https://github.com/quarkslab/arybo.git')
sha1sums=('SKIP')

pkgver() {
  cd "$srcdir/arybo"

  echo $(git rev-list --count HEAD).$(git rev-parse --short HEAD)
}

build() {
  cd "$srcdir/arybo"

  python3 setup.py build
}

package() {
  cd "$srcdir/arybo"

  python3 setup.py install --root="$pkgdir"

  install -Dm644 -t "$pkgdir/usr/share/doc/arybo/" README.rst CHANGELOG \
    CONTRIBUTORS
  install -Dm644 LICENSE.txt "$pkgdir/usr/share/licenses/arybo/LICENSE.txt"
}
