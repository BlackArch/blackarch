# This file is part of BlackArch Linux ( https://www.blackarch.org/ ).
# See COPYING for license details.

# Original Submitter: Colin Duquesnoy <colin.duquesnoy@gmail.com>
# Original Maintainer: Colin Duquesnoy <colin.duquesnoy@gmail.com>

pkgbase=python-qdarkstyle
pkgname=('python2-qdarkstyle' 'python-qdarkstyle')
pkgver=2.6.5
pkgrel=2
pkgdesc='Dark stylesheet for pyside/pyqt applications.'
url='https://github.com/ColinDuquesnoy/QDarkStyleSheet'
arch=('any')
license=('MIT')
makedepends=('python-setuptools' 'python2-setuptools')
source=("https://pypi.python.org/packages/source/Q/QDarkStyle/QDarkStyle-$pkgver.tar.gz")
sha512sums=('92c43fe54e6048458af325b8dae1f60fc6fc5849188a12886c8a112ac144735fe2d3ac694a8a866e06d18af6cba1616b14b3f36c0769f7d3ae452008c2a1863f')

package_python2-qdarkstyle() {
  depends=('python2')

  cd "QDarkStyle-$pkgver"

  python2 setup.py install --root="$pkgdir" --optimize=1

  mv "$pkgdir/usr/bin/qdarkstyle" "$pkgdir/usr/bin/qdarkstyle2"
}

package_python-qdarkstyle() {
  depends=('python')

  cd "QDarkStyle-$pkgver"

  python setup.py install --root="$pkgdir" --optimize=1
}

