# This file is part of BlackArch Linux ( https://www.blackarch.org/ ).
# See COPYING for license details.

# Original Submitter: Colin Duquesnoy <colin.duquesnoy@gmail.com>
# Original Maintainer: Colin Duquesnoy <colin.duquesnoy@gmail.com>

pkgbase=python-qdarkstyle
pkgname=('python2-qdarkstyle' 'python-qdarkstyle')
pkgver=2.8
pkgrel=1
pkgdesc='Dark stylesheet for pyside/pyqt applications.'
url='https://github.com/ColinDuquesnoy/QDarkStyleSheet'
arch=('any')
license=('MIT')
makedepends=('python-setuptools' 'python2-setuptools')
source=("https://files.pythonhosted.org/packages/38/91/2e90941a48e690ebd4d2e574417206d2d35a4e223c60878c76e7345f2dfb/QDarkStyle-$pkgver.tar.gz")
sha512sums=('8bd43df92500372482be056797354a10dd86d47160bf291d40f39d877affe2e2ceae9c7754de292aa4961f34fb4b7d8592aa97642dc598ff62a475c99f7161de')

package_python2-qdarkstyle() {
  depends=('python2')

  cd "QDarkStyle-$pkgver"

  python2 setup.py install --root="$pkgdir" --optimize=1

  mv "$pkgdir/usr/bin/qdarkstyle" "$pkgdir/usr/bin/qdarkstyle2"
}

package_python-qdarkstyle() {
  depends=('python')

  cd "QDarkStyle-$pkgver"

  python setup.py install --root="$pkgdir" --optimize=1
}

