# This file is part of BlackArch Linux ( https://www.blackarch.org/ ).
# See COPYING for license details.

pkgname=cvechecker
pkgver=3.9
pkgrel=1
pkgdesc='The goal of cvechecker is to report about possible vulnerabilities on your system, by scanning the installed software and matching the results with the CVE database. '
arch=('x86_64' 'aarch64')
groups=('blackarch' 'blackarch-scanner')
url='https://github.com/sjvermeu/cvechecker'
license=('GPL')
depends=('libconfig>=1.3' 'sqlite3' 'wget' 'libxslt')
makedepends=('automake' 'autoconf')
source=("https://github.com/sjvermeu/$pkgname/archive/v$pkgver.tar.gz")
sha512sums=('35fc8501bb56ebe8ef0f38bc8ea91f9446c06901c83ba16488acaa0c71ed446d3337637f1f44bc82c01e1b3f255630f33b13d25e04d85fca5df3a7a5bcf66606')
install="$pkgname.install"

prepare() {
  cd "$pkgname-$pkgver"

  autoheader
  aclocal --force
  automake --add-missing --force-missing
}

build() {
  cd "$pkgname-$pkgver"

  ./configure --prefix=/usr --sysconfdir=/etc --localstatedir=/var \
    --enable-sqlite3

  make
}

package() {
  cd "$pkgname-$pkgver"

  make DESTDIR="$pkgdir" install
}

