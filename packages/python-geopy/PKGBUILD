# This file is part of BlackArch Linux ( https://www.blackarch.org/ ).
# See COPYING for license details.

pkgbase=python-geopy
pkgname=('python2-geopy' 'python-geopy')
_pkgname=geopy
pkgver=1.21.0
pkgrel=1
pkgdesc='Python Geocoding Toolbox.'
arch=('any')
url='https://pypi.org/project/geopy/#files'
license=('MIT')
makedepends=('python2-setuptools' 'python-setuptools')
source=("https://files.pythonhosted.org/packages/72/4c/f18f8eea10bc312f5786e373e8995dafc7585e89ec80a9efab079eeef546/$_pkgname-$pkgver.tar.gz")
sha512sums=('8060d565d1e86cc3ebea6c5a1f923565f4384faedad478a11dd61ac6c00210a642c2b496802b908bd1bcc12da14f62e0bfc6ce403062205f2d7f39061555f7d5')

prepare() {
  cp -a "$_pkgname-$pkgver"{,-2}
}

build() {
  cd "$_pkgname-$pkgver-2"

  python2 setup.py build

  cd "$srcdir/$_pkgname-$pkgver"

  python setup.py build
}

package_python2-geopy() {
  depends=('python2' 'python2-geographiclib')

  cd "$_pkgname-$pkgver-2"

  python2 setup.py install --prefix=/usr --root="$pkgdir" --optimize=1 \
    --skip-build
}

package_python-geopy() {
  depends=('python' 'python-geographiclib')

  cd "$_pkgname-$pkgver"

  python setup.py install --prefix=/usr --root="$pkgdir" --optimize=1 \
    --skip-build
}

