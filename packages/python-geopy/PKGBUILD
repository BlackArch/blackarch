# This file is part of BlackArch Linux ( https://www.blackarch.org/ ).
# See COPYING for license details.

pkgbase=python-geopy
pkgname=('python2-geopy' 'python-geopy')
_pkgname=geopy
pkgver=1.20.0
pkgrel=1
pkgdesc='Python Geocoding Toolbox.'
arch=('any')
url='https://pypi.org/project/geopy/#files'
license=('MIT')
makedepends=('python2-setuptools' 'python-setuptools')
source=("https://files.pythonhosted.org/packages/b8/2b/f9876f3cdd98ad575f0f6c334c115bad6265c0ca0ceeb1154773d09bd00e/$_pkgname-$pkgver.tar.gz")
sha512sums=('6d2aaab487e18b9342e3ce6b88469118b210c259d88725df1880a5f18f6e083e1350453229c85c97e26c62a77f29c717d6118a50b07b428130e91270c5983fc6')

prepare() {
  cp -a "$_pkgname-$pkgver"{,-2}
}

build() {
  cd "$_pkgname-$pkgver-2"

  python2 setup.py build

  cd "$srcdir/$_pkgname-$pkgver"

  python setup.py build
}

package_python2-geopy() {
  depends=('python2' 'python2-geographiclib')

  cd "$_pkgname-$pkgver-2"

  python2 setup.py install --prefix=/usr --root="$pkgdir" --optimize=1 \
    --skip-build
}

package_python-geopy() {
  depends=('python' 'python-geographiclib')

  cd "$_pkgname-$pkgver"

  python setup.py install --prefix=/usr --root="$pkgdir" --optimize=1 \
    --skip-build
}

