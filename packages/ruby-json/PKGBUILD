# This file is part of BlackArch Linux ( http://blackarch.org ).
# See COPYING for license details.

pkgname='ruby-json'
pkgver='2.0.2'
pkgrel=1
arch=('i686' 'x86_64' 'armv6h' 'armv7h' 'aarch64')
pkgdesc='JSON implementation as a Ruby extension in C'
url='http://rubygems.org/gems/json'
license=('GPL')
depends=('ruby')
makedepends=('rubygems')
optdepends=('jruby' 'java-environment')
source=("http://rubygems.org/downloads/json-${pkgver}.gem")
noextract=("json-${pkgver}.gem")
sha1sums=('3de6d643ca30b0d91aa8d75d2bca81453c8a6bdb')

package() {
  cd "$srcdir"

  local _gemdir=$(ruby -e 'puts Gem.default_dir')

  if [[ $CARCH == arm* ]] ; then
    gem install --no-user-install --ignore-dependencies --no-rdoc --no-ri \
      -i "$pkgdir$_gemdir" json-$pkgver.gem
  else
    gem install --no-user-install --ignore-dependencies -i "$pkgdir$_gemdir" \
      json-$pkgver.gem
  fi

  # Removing files with references to $pkgdir
  find "$pkgdir" -depth -type f -name "Makefile*" -exec rm {} \;
}
