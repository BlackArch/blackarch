# This file is part of BlackArch Linux ( https://www.blackarch.org/ ).
# See COPYING for license details.

pkgname=fraud-bridge
pkgver=rel.0.32s.r3.g88a5baa
pkgrel=1
epoch=1
groups=('blackarch' 'blackarch-tunnel' 'blackarch-networking')
pkgdesc='ICMP and DNS tunneling via IPv4 and IPv6.'
arch=('x86_64' 'aarch64')
depends=('libgcrypt')
makedepends=('git')
url='https://github.com/stealth/fraud-bridge'
license=('GPL-3.0-or-later')
source=("git+https://github.com/stealth/$pkgname.git")
sha512sums=('SKIP')

pkgver() {
  cd $pkgname

  ( set -o pipefail
    git describe --long --tags --abbrev=7 2>/dev/null |
      sed 's/\([^-]*-g\)/r\1/;s/-/./g' ||
    printf "%s.%s" "$(git rev-list --count HEAD)" \
      "$(git rev-parse --short=7 HEAD)"
  )
}

build() {
  cd "$pkgname/src"

  make
}

package() {
  cd $pkgname

  install -dm 755 "$pkgdir/usr/share/$pkgname/"

  install -Dm 755 "src/$pkgname" "$pkgdir/usr/bin/$pkgname"
  cp -a config "$pkgdir/usr/share/$pkgname/config"
  cp -a *.jpg "$pkgdir/usr/share/$pkgname/"
  install -Dm 644 -t "$pkgdir/usr/share/doc/$pkgname/" README.md
  install -Dm 644 LICENSE.txt "$pkgdir/usr/share/licenses/$pkgname/LICENSE"
}

