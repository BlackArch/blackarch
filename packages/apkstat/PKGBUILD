# This file is part of BlackArch Linux ( https://www.blackarch.org/ ).
# See COPYING for license details.

pkgname=apkstat
pkgver=18.81cdad3
pkgrel=5
pkgdesc='Automated Information Retrieval From APKs For Initial Analysis.'
groups=('blackarch' 'blackarch-mobile' 'blackarch-recon')
arch=('any')
url='https://github.com/hexabin/APKStat'
license=('custom:unknown')
depends=('python2' 'android-apktool')
makedepends=('git')
source=("$pkgname::git+https://github.com/hexabin/APKStat.git")
sha512sums=('SKIP')

pkgver() {
  cd $pkgname

  ( set -o pipefail
    git describe --long --tags --abbrev=7 2>/dev/null |
      sed 's/\([^-]*-g\)/r\1/;s/-/./g' ||
    printf "%s.%s" "$(git rev-list --count HEAD)" \
      "$(git rev-parse --short=7 HEAD)"
  )
}

prepare() {
  cd $pkgname

  sed -i '1i\#!/usr/bin/python2' "$pkgname.py"
}

package() {
  cd $pkgname

  install -Dm 755 "$pkgname.py" "$pkgdir/usr/bin/$pkgname"
  install -Dm 644 -t "$pkgdir/usr/share/doc/$pkgname/" README.md
}

