# This file is part of BlackArch Linux ( http://blackarch.org ).
# See COPYING for license details.

pkgname='ruby-gitrob'
_gemname='gitrob'
pkgver='1.1.2'
pkgrel=2
pkgdesc="Reconnaissance tool for GitHub organizations"
arch=('any')
url='https://github.com/michenriksen/gitrob'
license=("MIT")
depends=('ruby' 'ruby-colorize' 'ruby-github_api-0.13.0' 'ruby-hashie' 'ruby-highline' 'ruby-pg' 'ruby-ruby-progressbar' 'ruby-sequel' 'ruby-sinatra' 'ruby-sucker_punch' 'ruby-thin' 'ruby-thor' 'ruby-thread')
groups=('blackarch' 'blackarch-scanner')
replaces=('gitrob')
conflicts=('gitrob')
provides=('gitrob')
options=('!emptydirs')
source=("https://rubygems.org/downloads/$_gemname-$pkgver.gem" )
noextract=("$_gemname-$pkgver.gem")
sha1sums=('802180bdc5b9c4b2c3ee52be14ad57b4df710974' )


package() {
  local _gemdir="$(ruby -rubygems -e 'puts Gem.default_dir')"
  gem install --ignore-dependencies --no-document --no-user-install -i "$pkgdir/$_gemdir" -n "$pkgdir"/usr/bin $_gemname-$pkgver.gem 
  rm "$pkgdir/$_gemdir/cache/$_gemname-$pkgver.gem"
  install -D -m644 "$pkgdir/$_gemdir/gems/$_gemname-$pkgver/LICENSE.txt" "$pkgdir/usr/share/licenses/$pkgname/LICENSE.txt"

  # Preserve library and other required directories. Remove junk that gem installs.
  local _copydir=`mktemp -d -t $_gemname.XXX`
  chmod 755 $_copydir
  (cd "$pkgdir/$_gemdir/gems/$_gemname-$pkgver"/ && cp -r --parents lib exe $_copydir)
  rm -rf "$pkgdir/$_gemdir/gems/$_gemname-$pkgver"
  mv $_copydir "$pkgdir/$_gemdir/gems/$_gemname-$pkgver"

}
