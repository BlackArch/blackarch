# This file is part of BlackArch Linux ( https://www.blackarch.org/ ).
# See COPYING for license details.

pkgbase=python-whichcraft
pkgname=('python2-whichcraft' 'python-whichcraft')
_pkgname=whichcraft
pkgver=0.6.0
pkgrel=1
pkgdesc='This package provides cross-platform cross-python shutil.which functionality.'
arch=('any')
url='https://pypi.python.org/pypi/whichcraft/'
license=('BSD')
makedepends=('python2-setuptools' 'python-setuptools')
source=("https://files.pythonhosted.org/packages/35/e3/9184274e33407564b97fd3609538012434316853ceaf99d36f82880d379f/whichcraft-0.6.0.tar.gz")
sha512sums=('f83703f5e140b03134ad46929dedb3306407c67515fdd203d7cafc4872741e8d268d2a3ccb65e848261bba0c5f520fe6d58447c2d59a0512ac259682f6f18fc0')

prepare() {
  cp -a "$_pkgname-$pkgver"{,-2}
}

build() {
  cd "$_pkgname-$pkgver-2"

  python2 setup.py build

  cd "$srcdir/$_pkgname-$pkgver"

  python setup.py build
}

package_python2-whichcraft() {
  depends=('python2')

  cd "$_pkgname-$pkgver-2"

  python2 setup.py install --prefix=/usr --root="$pkgdir" --optimize=1 \
    --skip-build
}

package_python-whichcraft() {
  depends=('python')

  cd "$_pkgname-$pkgver"

  python setup.py install --prefix=/usr --root="$pkgdir" --optimize=1 \
    --skip-build
}

