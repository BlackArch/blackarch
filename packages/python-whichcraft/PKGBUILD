# This file is part of BlackArch Linux ( https://www.blackarch.org/ ).
# See COPYING for license details.

pkgbase=python-whichcraft
pkgname=('python2-whichcraft' 'python-whichcraft')
_pkgname=whichcraft
pkgver=0.5.2
pkgrel=3
pkgdesc='This package provides cross-platform cross-python shutil.which functionality.'
arch=('any')
url='https://pypi.python.org/pypi/whichcraft/'
license=('BSD')
makedepends=('python2-setuptools' 'python-setuptools')
source=("https://files.pythonhosted.org/packages/4c/fd/276b0aedbc20ad6689bc671ecf00b32deb3ebf181e1ce6da10726c58a79f/$_pkgname-$pkgver.tar.gz")
sha512sums=('28b7be346b781f69c8fef419efd91fc2daa6b337c1eda0a2fc47d04134c479a5ded6eed3583825b111a48d7a79667676b6ce49693552233e78c7d517ac91a73c')

prepare() {
  cp -a "$_pkgname-$pkgver" "$_pkgname-$pkgver-2"
}

build() {
  cd "$_pkgname-$pkgver-2"

  python2 setup.py build

  cd "$srcdir/$_pkgname-$pkgver"

  python setup.py build
}

package_python2-whichcraft() {
  depends=('python2')

  cd "$_pkgname-$pkgver-2"

  python2 setup.py install --prefix=/usr --root="$pkgdir" --optimize=1 \
    --skip-build
}

package_python-whichcraft() {
  depends=('python')

  cd "$_pkgname-$pkgver"

  python setup.py install --prefix=/usr --root="$pkgdir" --optimize=1 \
    --skip-build
}

