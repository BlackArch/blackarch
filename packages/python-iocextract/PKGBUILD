# This file is part of BlackArch Linux ( https://www.blackarch.org/ ).
# See COPYING for license details.

pkgbase=python-iocextract
pkgname=('python2-iocextract' 'python-iocextract')
_pkgname=iocextract
pkgver=1.13.1
pkgrel=2
pkgdesc='Defanged Indicator of Compromise (IOC) Extractor.'
arch=('any')
url='https://pypi.org/project/iocextract/#files'
license=('GPL')
makedepends=('python2-setuptools' 'python-setuptools')
source=("https://files.pythonhosted.org/packages/e9/df/18b782b6d761834c21b2b188a521adc52efa159f7e1b2fbfaebaa7752dce/$_pkgname-$pkgver.tar.gz")
sha512sums=('f9b2434cdd4e3cbc85bf2bbfc4c80070a084db7ad8ea0007580897b25ae6a541704554a914ce39de00b4eb881c0168d81d4e4833e2c3cf82cb6bf82bf0c1c20f')

prepare() {
  cp -a "$_pkgname-$pkgver"{,-2}
}

build() {
  cd "$_pkgname-$pkgver-2"

  python2 setup.py build

  cd "$srcdir/$_pkgname-$pkgver"

  python setup.py build
}

package_python2-iocextract() {
  depends=('python2' 'python2-ipaddress' 'python2-regex')

  cd "$_pkgname-$pkgver-2"

  python2 setup.py install --root="$pkgdir" --prefix=/usr -O1 --skip-build

  mv "$pkgdir/usr/bin/$_pkgname" "$pkgdir/usr/bin/${_pkgname}2"
}

package_python-iocextract() {
  depends=('python' 'python-ipaddress' 'python-regex')

  cd "$_pkgname-$pkgver"

  python setup.py install --root="$pkgdir" --prefix=/usr -O1 --skip-build
}

