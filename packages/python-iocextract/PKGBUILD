# This file is part of BlackArch Linux ( https://www.blackarch.org/ ).
# See COPYING for license details.

pkgbase=python-iocextract
pkgname=('python2-iocextract' 'python-iocextract')
_pkgname=iocextract
pkgver=1.16.0
pkgrel=1
pkgdesc='Defanged Indicator of Compromise (IOC) Extractor.'
arch=('any')
url='https://pypi.org/project/iocextract/#files'
license=('GPL')
makedepends=('python2-setuptools' 'python-setuptools')
source=("https://files.pythonhosted.org/packages/source/${_pkgname::1}/$_pkgname/$_pkgname-$pkgver.tar.gz")
sha512sums=('77c572bbd9144eb5fde79cb98235d95fcc8e4b326ecc58cd54847ef312bd75fb5944625f8c6af804548d77a3d2eed2a6eacbc061ec10261b6de7644204487e3f')

prepare() {
  cp -a "$_pkgname-$pkgver"{,-2}
}

build() {
  cd "$_pkgname-$pkgver-2"

  python2 setup.py build

  cd "$srcdir/$_pkgname-$pkgver"

  python setup.py build
}

package_python2-iocextract() {
  depends=('python2' 'python2-ipaddress' 'python2-regex')

  cd "$_pkgname-$pkgver-2"

  python2 setup.py install --root="$pkgdir" --prefix=/usr -O1 --skip-build

  mv "$pkgdir/usr/bin/$_pkgname" "$pkgdir/usr/bin/${_pkgname}2"
}

package_python-iocextract() {
  depends=('python' 'python-ipaddress' 'python-regex')

  cd "$_pkgname-$pkgver"

  python setup.py install --root="$pkgdir" --prefix=/usr -O1 --skip-build
}

