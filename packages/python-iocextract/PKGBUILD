# This file is part of BlackArch Linux ( https://www.blackarch.org/ ).
# See COPYING for license details.

pkgbase=python-iocextract
pkgname=('python2-iocextract' 'python-iocextract')
_pkgname=iocextract
pkgver=1.15.1
pkgrel=2
pkgdesc='Defanged Indicator of Compromise (IOC) Extractor.'
arch=('any')
url='https://pypi.org/project/iocextract/#files'
license=('GPL')
makedepends=('python2-setuptools' 'python-setuptools')
source=("https://files.pythonhosted.org/packages/source/${_pkgname::1}/$_pkgname/$_pkgname-$pkgver.tar.gz")
sha512sums=('85203c3a1bb9f8d8f65aff82c5683d8c7a7f75661efd309bd12d590ea2a9ac105572599704f8bf4c49612803e7c82fa336cb3462a0ee309d49040ee5a72a6915')

prepare() {
  cp -a "$_pkgname-$pkgver"{,-2}
}

build() {
  cd "$_pkgname-$pkgver-2"

  python2 setup.py build

  cd "$srcdir/$_pkgname-$pkgver"

  python setup.py build
}

package_python2-iocextract() {
  depends=('python2' 'python2-ipaddress' 'python2-regex')

  cd "$_pkgname-$pkgver-2"

  python2 setup.py install --root="$pkgdir" --prefix=/usr -O1 --skip-build

  mv "$pkgdir/usr/bin/$_pkgname" "$pkgdir/usr/bin/${_pkgname}2"
}

package_python-iocextract() {
  depends=('python' 'python-ipaddress' 'python-regex')

  cd "$_pkgname-$pkgver"

  python setup.py install --root="$pkgdir" --prefix=/usr -O1 --skip-build
}

