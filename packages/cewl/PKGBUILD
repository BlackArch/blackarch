# This file is part of BlackArch Linux ( https://www.blackarch.org/ ).
# See COPYING for license details.

pkgname=cewl
pkgver=66.cf29509
pkgrel=1
groups=('blackarch' 'blackarch-automation' 'blackarch-cracker')
pkgdesc='A custom word list generator'
arch=('any')
url="http://www.digininja.org/projects/cewl.php"
license=('CCPL')
depends=('ruby2.3')
makedepends=('git' 'ruby2.3-bundler')
source=('git+https://github.com/digininja/CeWL.git')
install='cewl.install'
sha512sums=('SKIP')

pkgver() {
  cd CeWL

  echo $(git rev-list --count HEAD).$(git rev-parse --short HEAD)
}

prepare() {
  cd CeWL

  sed -i "s|require './cewl_lib'|require '/usr/share/cewl/cewl_lib'|g" cewl.rb
}

package() {
  cd CeWL

  install -dm 755 "$pkgdir/usr/bin"
  install -dm 755 "$pkgdir/usr/share/$pkgname"

  install -Dm 644 README "$pkgdir/usr/share/doc/$pkgname/README"

  rm README

  cp -a * "$pkgdir/usr/share/$pkgname"

  cat > "$pkgdir/usr/bin/$pkgname" << EOF
#!/bin/sh
exec ruby-2.3 /usr/share/$pkgname/cewl.rb "\$@"
EOF

  chmod +x "$pkgdir/usr/bin/$pkgname"

  cat > "$pkgdir/usr/bin/cewl-fab" << EOF
#!/bin/sh
exec ruby-2.3 /usr/share/$pkgname/fab.rb "\$@"
EOF

  chmod +x "$pkgdir/usr/bin/cewl-fab"

  rm "$pkgdir/usr/share/cewl/Gemfile.lock"
}

