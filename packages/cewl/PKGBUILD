pkgname=cewl
pkgver=4.3
pkgrel=4
groups=('blackarchlinux' 'blackarchlinux-automation' 'blackarchlinux-misc')
pkgdesc='A custom word list generator'
arch=('any')
url="http://www.digininja.org/projects/cewl.php"
license=('CCPL')
depends=('ruby' 'ruby-spider' 'ruby-mini_exiftool' 'ruby-nokogiri'
         'ruby-http_configuration' 'ruby-rubyzip' 'ruby-zip' 'ruby-mime-types')
source=("http://www.digininja.org/files/cewl_4.3.tar.bz2"
        "fix-modules-invocation.patch"
        "fix-usage.patch")
md5sums=('ace5e53a1485016ccc37a52a46ea7ff2'
         'b3774510769f075bc451dd634e170685'
         'ca224d8ef80283df54a1104e792d903d')

prepare() {
  cd "$srcdir/cewl"

  patch -Np1 -i "$srcdir/fix-modules-invocation.patch"
  patch -Np1 -i "$srcdir/fix-usage.patch"
}

package() {
  cd "$srcdir/cewl"

  # Base directories.
  install -dm755 "$pkgdir/usr/bin"
  install -dm755 "$pkgdir/usr/share/doc/cewl"
  install -dm755 "$pkgdir/usr/lib/ruby/vendor_ruby/cewl"

  install -m755 "$srcdir/cewl/cewl.rb" "$pkgdir/usr/bin/cewl"
  install -m755 "$srcdir/cewl/fab.rb" "$pkgdir/usr/bin/cewl-fab"
  install -m644 "$srcdir/cewl/README" "$pkgdir/usr/share/doc/cewl/README"
  install -m644 "$srcdir/cewl/cewl_lib.rb" "$pkgdir/usr/lib/ruby/vendor_ruby/cewl/cewl_lib.rb"
}
