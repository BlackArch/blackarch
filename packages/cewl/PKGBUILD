# This file is part of BlackArch Linux ( http://blackarch.org ).
# See COPYING for license details.

pkgname=cewl
pkgver=5.3
pkgrel=3
groups=('blackarch' 'blackarch-automation' 'blackarch-cracker')
pkgdesc='A custom word list generator'
arch=('any')
url="http://www.digininja.org/projects/cewl.php"
license=('CCPL')
depends=('ruby' 'ruby-spider' 'ruby-mini_exiftool' 'ruby-nokogiri'
         'ruby-http_configuration' 'ruby-rubyzip' 'ruby-zip' 'ruby-mime-types')
source=("https://github.com/digininja/CeWL/archive/${pkgver}.tar.gz")
install='cewl.install'
sha512sums=('579c997b3edf7d09bbe10e70b2d64c3b27114a4bf2212ff74c57e4e3ea146bf84172f0a4018499c534a6f1a75ee29fdb38b6b5e8b470c9c2b41954ae982cb221')

prepare() {
  cd "$srcdir/CeWL-$pkgver"

  sed -i "s|require './cewl_lib'|require '/usr/share/cewl/cewl_lib'|g" cewl.rb
}

package() {
  cd "$srcdir/CeWL-$pkgver"

  install -dm 755 "$pkgdir/usr/bin"
  install -dm 755 "$pkgdir/usr/share/cewl"

  install -Dm644 README "$pkgdir/usr/share/doc/cewl/README"

  rm README

  cp -a * "$pkgdir/usr/share/cewl"

  cat > "$pkgdir/usr/bin/cewl" << EOF
#!/bin/sh
exec ruby /usr/share/cewl/cewl.rb "\${@}"
EOF

  chmod a+x "$pkgdir/usr/bin/cewl"

  cat > "$pkgdir/usr/bin/cewl-fab" << EOF
#!/bin/sh
exec ruby /usr/share/cewl/fab.rb "\${@}"
EOF

  chmod a+x "$pkgdir/usr/bin/cewl-fab"
}
