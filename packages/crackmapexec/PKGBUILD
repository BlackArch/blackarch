# This file is part of BlackArch Linux ( https://www.blackarch.org/ ).
# See COPYING for license details.

pkgname=crackmapexec
pkgver=617.119e5af
pkgrel=4
groups=('blackarch' 'blackarch-scanner' 'blackarch-exploitation')
pkgdesc='A swiss army knife for pentesting Windows/Active Directory environments.'
arch=('any')
url='https://github.com/byt3bl33d3r/CrackMapExec'
license=('custom:unknown')
depends=('python' 'impacket' 'ldapdomaindump' 'python-gevent' 'python-requests'
         'python-requests-ntlm' 'python-beautifulsoup4' 'python-termcolor'
         'python-msgpack' 'python-pylnk3' 'python-pywinrm' 'python-paramiko'
         'python-xmltodict' 'python-terminaltables' 'python-bcrypt'
         'python-certifi' 'python-cffi' 'python-chardet' 'python-click'
         'python-cryptography' 'python-dnspython' 'python-flask' 'python-future'
         'python-greenlet' 'python-idna' 'python-itsdangerous' 'python-jinja'
         'python-ldap3' 'python-markupsafe' 'python-ntlm-auth' 'python-paramiko'
         'python-pyasn1' 'python-pycparser' 'python-pycryptodomex' 'python-bs4'
         'python-pynacl' 'python-pyopenssl' 'python-pywinrm' 'python-six'
         'python-soupsieve' 'python-urllib3' 'python-werkzeug' 'python-pypsrp'
         'python-neo4j-driver' 'python-lsassy' 'python-pyspnego' 'python-tqdm'
         'python-aiowinreg' 'python-asn1crypto' 'python-asysocks' 'python-pytz'
         'python-minidump' 'python-minikerberos' 'python-netaddr'
         'python-pypykatz' 'python-wcwidth' 'python-winacl' 'python-winsspi'
         'python-zope-interface' 'python-zope-event')
makedepends=('git' 'python-pipx')
source=("$pkgname::git+https://github.com/byt3bl33d3r/CrackMapExec.git")
sha512sums=('SKIP')

pkgver() {
  cd $pkgname

  echo $(git rev-list --count HEAD).$(git rev-parse --short HEAD)
}

prepare() {
  cd $pkgname

  git submodule init && git submodule update --recursive
}

package() {
  cd $pkgname

  install -dm 755 "$pkgdir/usr/bin"
  install -dm 755 "$pkgdir/usr/share/$pkgname"

  export PIPX_HOME="$pkgdir/usr/share/$pkgname/pipx"
  export PIPX_BIN_DIR="$pkgdir/usr/bin"
  pipx ensurepath
  pipx install crackmapexec

  install -Dm 644 -t "$pkgdir/usr/share/doc/$pkgname/" README.md
  install -Dm 644 LICENSE "$pkgdir/usr/share/licenses/$pkgname/LICENSE"

  rm "$pkgdir/usr/bin/"{$pkgname,cme,cmedb}

  ln -sf "/usr/share/$pkgname/pipx/venvs/$pkgname/bin/$pkgname" \
    "$pkgdir/usr/bin/$pkgname"
  ln -sf "/usr/share/$pkgname/pipx/venvs/$pkgname/bin/cme" "$pkgdir/usr/bin/cme"
  ln -sf "/usr/share/$pkgname/pipx/venvs/$pkgname/bin/cmedb" \
    "$pkgdir/usr/bin/cmedb"
}

