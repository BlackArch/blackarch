# This file is part of BlackArch Linux ( https://www.blackarch.org/ ).
# See COPYING for license details.

pkgname=crackmapexec
pkgver=879.603f904
_pyver=3.10
pkgrel=2
epoch=1
groups=('blackarch' 'blackarch-scanner' 'blackarch-exploitation' 'blackarch-windows')
pkgdesc='A swiss army knife for pentesting Windows/Active Directory environments.'
arch=('any')
url='https://github.com/byt3bl33d3r/CrackMapExec'
license=('custom:unknown')
depends=('impacket' 'python' 'python-aiowinreg' 'python-asn1crypto'
        'python-asysocks' 'python-bcrypt' 'python-beautifulsoup4' 'python-bs4'
        'python-certifi' 'python-cffi' 'python-chardet' 'python-click'
        'python-cryptography' 'python-dnspython' 'python-flask' 'python-future'
        'python-greenlet' 'python-idna' 'python-itsdangerous' 'python-jinja'
        'python-ldap3' 'ldapdomaindump' 'python-lsassy' 'python-dsinternals'
        'python-markupsafe' 'python-minidump' 'python-minikerberos'
        'python-msgpack' 'python-msldap' 'python-netaddr' 'python-ntlm-auth'
        'python-paramiko' 'python-prompt_toolkit' 'python-pyasn1'
        'python-pycparser' 'python-pycryptodomex' 'python-pylnk3'
        'python-pynacl' 'python-pyopenssl' 'python-pypsrp' 'python-pypykatz'
        'python-pyspnego' 'python-pytz' 'python-requests-ntlm' 'python-six'
        'python-soupsieve' 'python-termcolor' 'python-terminaltables'
        'python-tqdm' 'python-urllib3' 'python-wcwidth' 'python-werkzeug'
        'python-winacl' 'python-winsspi' 'python-xmltodict' 'python-zope-event'
        'python-zope-interface' 'python-pywerview' 'python-gevent'
        'python-neo4j-driver' 'python-aiosmb' 'python-aioconsole'
        'python-aardwolf')
makedepends=('git' 'python-setuptools' 'python-poetry')
source=("$pkgname::git+https://github.com/byt3bl33d3r/CrackMapExec.git"
        'pyproject.toml.patch')
sha512sums=('SKIP'
            '2dfe95af77149e0a76cb7391b9755bf37ea770202d33b509b1bfde6e930f36a22414aaa6b6c94dc2df83d14ad48cbbb93d46c24da3ab4454bb15dbf2880f02b3')

pkgver() {
  cd $pkgname

  echo $(git rev-list --count HEAD).$(git rev-parse --short HEAD)
}

prepare() {
  cd $pkgname

  make clean
  git submodule init && git submodule update --recursive

  patch -p0 < ../pyproject.toml.patch
}

build() {
  cd $pkgname

  poetry build
  cd dist
  mkdir -p cme_dir
  tar zxvf $pkgname-*.tar.gz -C cme_dir
}

package() {
  cd "$pkgname/dist/cme_dir/$pkgname"-*

  mkdir -p cme/hooks
  python setup.py install --root="$pkgdir" -O1

  install -dm 755 \
    "$pkgdir/usr/lib/python$_pyver/site-packages/cme/thirdparty/pywerview/"
  touch \
    "$pkgdir/usr/lib/python$_pyver/site-packages/cme/thirdparty/pywerview/.default"
}

