# This file is part of BlackArch Linux ( https://www.blackarch.org/ ).
# See COPYING for license details.

pkgname=crackmapexec
pkgver=451.fc3d1a0
pkgrel=1
groups=('blackarch' 'blackarch-scanner' 'blackarch-exploitation')
pkgdesc='A swiss army knife for pentesting Windows/Active Directory environments.'
arch=('any')
url='https://github.com/byt3bl33d3r/CrackMapExec'
license=('custom:unknown')
depends=('python2' 'impacket' 'python2-gevent' 'python2-netaddr' 'python2-bs4'
         'python2-pyopenssl' 'python2-pycryptodome' 'python2-pyasn1'
         'python2-msgpack' 'python2-termcolor' 'python2-requests'
         'python2-beautifulsoup4' 'python2-pylnk' 'python2-paramiko'
         'python2-pywerview' 'python2-pcapy' 'python2-splinter' 'python2-idna'
         'python2-cryptography' 'python2-cffi' 'python2-selenium'
         'python2-pynacl' 'python2-terminaltables' 'python2-greenlet'
         'python2-enum34' 'python2-bcrypt' 'python2-certifi' 'python2-chardet'
         'python2-xmltodict' 'python2-ipaddress' 'python2-ntlm-auth'
         'python2-pycparser' 'python2-requests-ntlm' 'python2-six'
         'python2-urllib3' 'python2-asn1crypto' 'python2-pycryptodomex')
makedepends=('git' 'python2-setuptools')
source=("$pkgname::git+https://github.com/byt3bl33d3r/CrackMapExec.git"
        'setup.py.patch')
sha512sums=('SKIP'
            '976fc439e88affd0618cb4da4ac48dc6e26f3239c13fdd8bf8229389be8faab89ef49de0b8159405086861feb1706d3277c9ee8c19ad458a9da86104e1abad0a')

pkgver() {
  cd $pkgname

  echo $(git rev-list --count HEAD).$(git rev-parse --short HEAD)
}

prepare() {
  cd $pkgname

  git submodule init && git submodule update --recursive

  sed -i '/msgpack/d' requirements.txt

  cd $srcdir

  patch -p1 < setup.py.patch
}

build() {
  cd $pkgname

  python2 setup.py build
}

package() {
  cd $pkgname

  install -dm 755 "$pkgdir/usr/bin"
  install -dm 755 "$pkgdir/usr/share/$pkgname"

  python2 setup.py install --root="$pkgdir" --prefix=/usr --optimize=1 \
    --skip-build

  install -Dm 644 -t "$pkgdir/usr/share/doc/$pkgname/" README.md
  install -Dm 644 LICENSE "$pkgdir/usr/share/licenses/$pkgname/LICENSE"

  find . -name '*.py' |
    xargs sed -i -e 's|/usr/bin/env python|/usr/bin/env python2|' \
    -e 's|/usr/bin/python|/usr/bin/python2|'
}

