# This file is part of BlackArch Linux ( https://www.blackarch.org/ ).
# See COPYING for license details.

pkgname=crackmapexec
pkgver=v6.0.0.r37.gbeb6d9f
_pyver=3.11
pkgrel=1
epoch=2
groups=('blackarch' 'blackarch-scanner' 'blackarch-exploitation' 'blackarch-windows')
pkgdesc='A swiss army knife for pentesting Windows/Active Directory environments.'
arch=('any')
url='https://github.com/mpgn/CrackMapExec'
license=('BSD')
depends=('python' 'python-requests' 'python-beautifulsoup4' 'python-lsassy'
         'python-termcolor' 'python-msgpack' 'python-neo4j-driver' 'impacket'
         'python-pylnk3' 'python-pypsrp' 'python-paramiko' 'python-dsinternals'
         'python-xmltodict' 'python-terminaltables' 'python-aioconsole'
         'python-pywerview' 'python-pyspnego' 'ldapdomaindump' 'python-aardwolf'
         'python-setuptools' 'python-masky' 'python-pyparsing' 'python-asyauth'
         'python-minikerberos' 'python-pypykatz' 'bloodhound-python'
         'python-sqlalchemy' 'python-aiosqlite' 'python-pyasn1-modules'
         'python-python-libnmap' 'python-rich')
         # dploot
makedepends=('git' 'python-build' 'python-pip')
source=("$pkgname::git+https://github.com/mpgn/CrackMapExec.git")
sha512sums=('SKIP')

prepare() {
  cd $pkgname

  make clean

  git submodule init && git submodule update --recursive
}

pkgver() {
  cd $pkgname

  git describe --long --tags | sed 's/\([^-]*-g\)/r\1/;s/-/./g'
}

build() {
  cd $pkgname

  python -m build --wheel --outdir="$startdir/dist"
}

package() {
  cd $pkgname

  pip install \
    --verbose \
    --disable-pip-version-check \
    --no-warn-script-location \
    --ignore-installed \
    --no-compile \
    --no-deps \
    --root="$pkgdir" \
    --prefix=/usr \
    --no-index \
    --find-links="file://$startdir/dist" \
    "$pkgname"
}

