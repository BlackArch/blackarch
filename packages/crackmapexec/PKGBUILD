# This file is part of BlackArch Linux ( http://blackarch.org ).
# See COPYING for license details.

pkgname='crackmapexec'
pkgver=408.22a519e
pkgrel=1
groups=('blackarch' 'blackarch-scanner' 'blackarch-exploitation')
pkgdesc='A swiss army knife for pentesting Windows/Active Directory environments.'
arch=('any')
url='https://github.com/byt3bl33d3r/CrackMapExec'
license=('custom:unknown')
depends=('python2' 'impacket' 'python2-gevent' 'python2-netaddr' 'python2-bs4'
         'python2-pyopenssl' 'python2-crypto' 'python2-pyasn1' 'python2-msgpack'
         'python2-termcolor' 'python2-requests' 'python2-beautifulsoup4'
         'python2-pylnk' 'python2-paramiko' 'python2-pywerview' 'python2-pcapy'
         'python2-splinter' 'python2-cryptography' 'python2-cffi'
         'python2-selenium' 'python2-pynacl' 'python2-terminaltables'
         'python2-greenlet' 'python2-idna' 'python2-enum34' 'python2-bcrypt'
         'python2-certifi' 'python2-chardet' 'python2-xmltodict')
makedepends=('git')
source=('git+https://github.com/byt3bl33d3r/CrackMapExec.git')
sha1sums=('SKIP')

pkgver() {
  cd "$srcdir/CrackMapExec"

  echo $(git rev-list --count HEAD).$(git rev-parse --short HEAD)
}

prepare() {
  cd "$srcdir/CrackMapExec"

  git submodule init && git submodule update --recursive
}

build() {
  cd "$srcdir/CrackMapExec"

  python2 setup.py build
}

package() {
  cd "$srcdir/CrackMapExec"

  mkdir -p "$pkgdir/usr/bin"
  mkdir -p "$pkgdir/usr/share/crackmapexec/"

  python2 setup.py install --root="$pkgdir" --prefix=/usr --optimize=1

  install -Dm644 README.md "$pkgdir/usr/share/doc/crackmapexec/README.md"
  install -Dm644 LICENSE "$pkgdir/usr/share/licenses/crackmapexec/LICENSE"

  find "$pkgdir" -type f -name '*.py' -exec sed -i 's|python|python2|' {} \;
}
