# This file is part of BlackArch Linux ( https://www.blackarch.org/ ).
# See COPYING for license details.

pkgname=crackmapexec
_pyver=3.11
pkgver=v6.0.1.r31.g9c84f97
pkgrel=1
epoch=3
groups=('blackarch' 'blackarch-scanner' 'blackarch-exploitation'
        'blackarch-windows')
pkgdesc='A swiss army knife for pentesting Windows/Active Directory environments.'
arch=('any')
url='https://github.com/Porchetta-Industries/CrackMapExec'
license=('BSD')
depends=('python' 'python-poetry')
makedepends=('git')
#source=("$pkgname::git+https://github.com/Porchetta-Industries/CrackMapExec.git")
source=("$pkgname::git+https://github.com/mpgn/CrackMapExec.git")
sha512sums=('SKIP')
install="$pkgname.install"

pkgver() {
  cd $pkgname

  git describe --long --tags | sed 's/\([^-]*-g\)/r\1/;s/-/./g'
}

package() {
  cd $pkgname

  install -dm 755 "$pkgdir/usr/bin"
  install -dm 755 "$pkgdir/usr/share/$pkgname"

  install -Dm 644 LICENSE "$pkgdir/usr/share/licenses/$pkgname/LICENSE"

  rm -rf LICENSE .github .git* .dockerignore Dockerfile Makefile *.nix

  cp -a * "$pkgdir/usr/share/$pkgname/"

  cat > "$pkgdir/usr/bin/$pkgname" << EOF
#!/bin/sh
poetry config cache-dir "/usr/share/$pkgname"
poetry env use /usr/share/$pkgname/virtualenvs/*/bin/python$_pyver \\
  --directory /usr/share/crackmapexec
poetry run --directory "/usr/share/$pkgname" $pkgname "\$@"
EOF

  cat > "$pkgdir/usr/bin/cme" << EOF
#!/bin/sh
poetry config cache-dir "/usr/share/$pkgname"
poetry env use /usr/share/$pkgname/virtualenvs/*/bin/python$_pyver \\
  --directory /usr/share/crackmapexec
poetry run --directory "/usr/share/$pkgname" cme "\$@"
EOF

  cat > "$pkgdir/usr/bin/cmedb" << EOF
#!/bin/sh
poetry config cache-dir "/usr/share/$pkgname"
poetry env use /usr/share/$pkgname/virtualenvs/*/bin/python$_pyver \\
  --directory /usr/share/crackmapexec
poetry run --directory "/usr/share/$pkgname" cmedb "\$@"
EOF

  chmod +x "$pkgdir/usr/bin/$pkgname" "$pkgdir/usr/bin/cme" \
    "$pkgdir/usr/bin/cmedb"
}

