# This file is part of BlackArch Linux ( http://blackarch.org ).
# See COPYING for license details.

pkgname=crackmapexec
pkgver=430.ea3402d
pkgrel=2
groups=('blackarch' 'blackarch-scanner' 'blackarch-exploitation')
pkgdesc='A swiss army knife for pentesting Windows/Active Directory environments.'
arch=('any')
url='https://github.com/byt3bl33d3r/CrackMapExec'
license=('custom:unknown')
depends=('python2' 'impacket' 'python2-gevent' 'python2-netaddr' 'python2-bs4'
         'python2-pyopenssl' 'python2-pycryptodome' 'python2-pyasn1' 'python2-msgpack'
         'python2-termcolor' 'python2-requests' 'python2-beautifulsoup4'
         'python2-pylnk' 'python2-paramiko' 'python2-pywerview' 'python2-pcapy'
         'python2-splinter' 'python2-cryptography' 'python2-cffi'
         'python2-selenium' 'python2-pynacl' 'python2-terminaltables'
         'python2-greenlet' 'python2-idna' 'python2-enum34' 'python2-bcrypt'
         'python2-certifi' 'python2-chardet' 'python2-xmltodict'
         'python2-ipaddress' 'python2-ntlm-auth' 'python2-pycparser'
         'python2-requests-ntlm' 'python2-six' 'python2-urllib3'
         'python2-asn1crypto')
makedepends=('git' 'python2-setuptools')
source=('git+https://github.com/byt3bl33d3r/CrackMapExec.git')
sha512sums=('SKIP')

pkgver() {
  cd "$srcdir/CrackMapExec"

  echo $(git rev-list --count HEAD).$(git rev-parse --short HEAD)
}

prepare() {
  cd "$srcdir/CrackMapExec"

  git submodule init && git submodule update --recursive

  sed -i '/msgpack/d' requirements.txt
  sed -i '/msgpack/d' setup.py
}

build() {
  cd "$srcdir/CrackMapExec"

  python2 setup.py build
}

package() {
  cd "$srcdir/CrackMapExec"

  install -dm 755 "$pkgdir/usr/bin"
  install -dm 755 "$pkgdir/usr/share/crackmapexec/"

  python2 setup.py install --root="$pkgdir" --prefix=/usr --optimize=1

  install -Dm644 README.md "$pkgdir/usr/share/doc/crackmapexec/README.md"
  install -Dm644 LICENSE "$pkgdir/usr/share/licenses/crackmapexec/LICENSE"

  find "$pkgdir" -type f -name '*.py' -exec sed -i 's|python|python2|' {} \;
}
