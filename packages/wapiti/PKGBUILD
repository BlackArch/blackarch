# This file is part of BlackArch Linux ( https://www.blackarch.org/ ).
# See COPYING for license details.

pkgname=wapiti
_pkgname=${pkgname}3
pkgver=3.0.4
pkgrel=1
groups=('blackarch' 'blackarch-fuzzer' 'blackarch-scanner' 'blackarch-webapp')
pkgdesc='A vulnerability scanner for web applications. It currently search vulnerabilities like XSS, SQL and XPath injections, file inclusions, command execution, LDAP injections, CRLF injections...'
url='http://wapiti.sourceforge.net/'
license=('GPL')
depends=('python' 'python-requests' 'python-beautifulsoup4' 'python-tld'
         'python-yaswfp' 'python-mako' 'python-pysocks' 'python-html5lib')
makedepends=('python-setuptools' 'python-wheel' 'python-pip')
arch=('any')
source=("${pkgname}3-$pkgver.tar.gz::https://sourceforge.net/projects/$pkgname/files/$pkgname/$pkgname-$pkgver/${pkgname}3-$pkgver.tar.gz/download")
sha512sums=('377bf0ca38eb219bb480f7456197e83042cb71f1e05f1cb51e81def6bb2128c3ccb2eba30f4ec856530b00ec1a0e4bc231068811669abca572dcaf4b229fcc75')

build() {
  cd "$_pkgname-$pkgver"

  python setup.py build
}

package() {
  cd "$_pkgname-$pkgver"

  python setup.py install --root="$pkgdir" --prefix=/usr -O1 --skip-build

  install -Dm 644 -t "$pkgdir/usr/share/doc/$pkgname" README.md INSTALL.md \
    VERSION doc/AUTHORS doc/ChangeLog*
}

