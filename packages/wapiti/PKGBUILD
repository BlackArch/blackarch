# This file is part of BlackArch Linux ( https://www.blackarch.org/ ).
# See COPYING for license details.

pkgname=wapiti
pkgver=3.1.4.r6.g24ec57a
pkgrel=1
groups=('blackarch' 'blackarch-webapp' 'blackarch-scanner' 'blackarch-fuzzer')
pkgdesc='A vulnerability scanner for web applications.'
url='https://github.com/wapiti-scanner/wapiti'
license=('GPL')
depends=('python' 'python-httpx' 'python-httpx-socks' 'python-beautifulsoup4'
         'python-tld' 'python-yaswfp' 'python-mako' 'python-pysocks'
         'python-markupsafe' 'python-six' 'python-importlib-metadata'
         'python-browser-cookie3' 'python-cryptography' 'python-sqlalchemy'
         'python-aiosqlite' 'python-loguru' 'python-dnspython' 'python-socksio'
         'python-httpcore' 'sslyze' 'python-humanize' 'python-keyring'
         'python-aiocache' 'python-brotli' 'python-brotlicffi' 'python-h11'
         'python-async-timeout' 'python-pyasn1' 'python-structlog' 'mitmproxy')
makedepends=('git' 'python-setuptools' 'python-wheel' 'python-pip')
arch=('any')
source=("git+https://github.com/wapiti-scanner/$pkgname.git")
sha512sums=('SKIP')

pkgver() {
  cd $pkgname

  git describe --long --tags | sed 's/\([^-]*-g\)/r\1/;s/-/./g'
}

prepare() {
  cd $pkgname

  sed -ri 's/==/>=/g' setup.py
}

build() {
  cd $pkgname

  python setup.py build
}

package() {
  cd $pkgname

  python setup.py install --root="$pkgdir" --prefix=/usr -O1 --skip-build

  install -Dm 644 -t "$pkgdir/usr/share/doc/$pkgname/" README.rst INSTALL.md \
    VERSION doc/AUTHORS doc/ChangeLog* doc/*.md doc/*.txt doc/*.html doc/*.ico
}

