# This file is part of BlackArch Linux ( https://www.blackarch.org/ ).
# See COPYING for license details.

pkgname=wapiti
pkgver=3.0.2
pkgrel=1
groups=('blackarch' 'blackarch-fuzzer' 'blackarch-scanner' 'blackarch-webapp')
pkgdesc='A vulnerability scanner for web applications. It currently search vulnerabilities like XSS, SQL and XPath injections, file inclusions, command execution, LDAP injections, CRLF injections...'
url='http://wapiti.sourceforge.net/'
license=('GPL')
depends=('python' 'python-requests' 'python-setuptools' 'python-beautifulsoup4'
         'python-argparse' 'python-tld' 'python-yaswfp' 'python-mako'
         'python-pysocks' 'python-html5lib')
makedepends=('python-setuptools')
arch=('any')
source=("${pkgname}3-$pkgver.tar.gz::https://sourceforge.net/projects/$pkgname/files/$pkgname/$pkgname-$pkgver/${pkgname}3-$pkgver.tar.gz/download")
sha512sums=('02f0e2510ce428b6dd89251ec63b5806b34e8e02302c7fa8fb1d9460a4e8c073b4650c066183ef20dc554440721f0b659a6ea30ad38f05865fb0fccc1cca758f')

build() {
  cd "${pkgname}3-$pkgver"

  python setup.py build
}

package() {
  cd "${pkgname}3-$pkgver"

  python setup.py install --root="$pkgdir"

  install -Dm 644 -t "$pkgdir/usr/share/doc/$pkgname" README.md INSTALL.md \
    VERSION doc/AUTHORS doc/ChangeLog*
}

