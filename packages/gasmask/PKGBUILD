# This file is part of BlackArch Linux ( https://www.blackarch.org/ ).
# See COPYING for license details.

pkgname=gasmask
pkgver=170.e0d0f0a
pkgrel=1
pkgdesc='All in one Information gathering tool - OSINT.'
groups=('blackarch' 'blackarch-recon')
arch=('any')
url='https://github.com/twelvesec/gasmask'
license=('GPL3')
depends=('python2' 'python2-argparse' 'python2-requests' 'python2-dnspython'
         'python2-python-whois' 'python2-validators' 'python2-censys'
         'python2-shodan')
makedepends=('git')
source=("git+https://github.com/twelvesec/$pkgname.git")
sha512sums=('SKIP')

pkgver() {
  cd $pkgname

  echo $(git rev-list --count HEAD).$(git rev-parse --short HEAD)
}

package() {
  cd $pkgname

  install -dm 755 "$pkgdir/usr/bin"
  install -dm 755 "$pkgdir/usr/share/$pkgname"

  install -Dm 644 -t "$pkgdir/usr/share/doc/$pkgname/" README.md
  install -Dm 644 LICENSE "$pkgdir/usr/share/licenses/$pkgname/LICENSE"

  rm README.md LICENSE

  cp -a * "$pkgdir/usr/share/$pkgname/"

  cat > "$pkgdir/usr/bin/$pkgname" << EOF
#!/bin/sh
cd /usr/share/$pkgname
exec python2 $pkgname.py "\$@"
EOF

  chmod +x "$pkgdir/usr/bin/$pkgname"
}

