# This file is part of BlackArch Linux ( http://blackarch.org ).
# See COPYING for license details.

pkgname=gasmask
pkgver=139.2c917b9
pkgrel=1
pkgdesc='All in one Information gathering tool - OSINT.'
groups=('blackarch' 'blackarch-recon')
arch=('any')
url='https://github.com/twelvesec/gasmask'
license=('GPL3')
depends=('python2' 'python2-argparse' 'python2-requests' 'python2-dnspython'
         'python2-python-whois' 'python2-validators')
makedepends=('git')
source=('git+https://github.com/twelvesec/gasmask.git')
sha512sums=('SKIP')

pkgver() {
  cd "$srcdir/gasmask"

  echo $(git rev-list --count HEAD).$(git rev-parse --short HEAD)
}

package() {
  cd "$srcdir/gasmask"

  install -dm 755 "$pkgdir/usr/bin"
  install -dm 755 "$pkgdir/usr/share/gasmask"

  install -Dm644 -t "$pkgdir/usr/share/doc/gasmask/" README.md
  install -Dm644 LICENSE "$pkgdir/usr/share/licenses/gasmask/LICENSE"

  rm README.md LICENSE

  cp -a * "$pkgdir/usr/share/gasmask/"

  cat > "$pkgdir/usr/bin/gasmask" << EOF
#!/bin/sh
cd /usr/share/gasmask
exec python2 gasmask.py "\${@}"
EOF

  chmod a+x "$pkgdir/usr/bin/gasmask"
}
