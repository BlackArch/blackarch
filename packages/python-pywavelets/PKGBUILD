# This file is part of BlackArch Linux ( https://www.blackarch.org/ ).
# See COPYING for license details.

pkgbase=python-pywavelets
pkgname=('python2-pywavelets' 'python-pywavelets')
_pkgname=PyWavelets
pkgver=1.0.3
pkgrel=2
pkgdesc='Wavelet transform module.'
arch=('x86_64' 'armv6h' 'armv7h' 'aarch64')
url='https://pypi.org/project/pywavelets/#files'
license=('MIT')
makedepends=('python2-setuptools' 'python-setuptools')
source=("https://files.pythonhosted.org/packages/86/0f/89c06eadc4d6003ff88ba365ff476be0f5a805d2e270b05cc863f2c01a4f/$_pkgname-$pkgver.tar.gz")
sha512sums=('7890d9d7ee2694061b2a894785bb32678af536e2f57198f05534ee87afa79f0cb74fdd9344b0c504a89e74c1145c04de20cf35977cc63d8e49365ce8fb9cf7c8')

prepare() {
  cp -a "$_pkgname-$pkgver"{,-2}
}

build() {
  cd "$_pkgname-$pkgver-2"

  python2 setup.py build

  cd "$srcdir/$_pkgname-$pkgver"

  python setup.py build
}

package_python2-pywavelets() {
  depends=('python2' 'python2-numpy')

  cd "$_pkgname-$pkgver-2"

  python2 setup.py install --prefix=/usr --root="$pkgdir" --optimize=1 \
    --skip-build
}

package_python-pywavelets() {
  depends=('python' 'python-numpy')

  cd "$_pkgname-$pkgver"

  python setup.py install --prefix=/usr --root="$pkgdir" --optimize=1 \
    --skip-build
}

