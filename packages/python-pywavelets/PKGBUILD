# This file is part of BlackArch Linux ( https://www.blackarch.org/ ).
# See COPYING for license details.

pkgbase=python-pywavelets
pkgname=('python2-pywavelets' 'python-pywavelets')
_pkgname=PyWavelets
pkgver=1.1.1
pkgrel=1
pkgdesc='Wavelet transform module.'
arch=('x86_64' 'aarch64')
url='https://pypi.org/project/pywavelets/#files'
license=('MIT')
makedepends=('python2-setuptools' 'python-setuptools')
source=("https://files.pythonhosted.org/packages/17/6b/ef989cfb3acff2ea966c5f28a7443ccaec2ee136675dfa0366db2635f78a/PyWavelets-$pkgver.tar.gz")
sha512sums=('3e70625dad27822165e79b7d240c34072684ba340e8d314a026d2e47e81b04945d9ed9c6a5f0730eab285d3d5ebb1b311aaec5d6d06bced1eea80000bef5e787')

prepare() {
  cp -a "$_pkgname-$pkgver"{,-2}
}

build() {
  cd "$_pkgname-$pkgver-2"

  python2 setup.py build

  cd "$srcdir/$_pkgname-$pkgver"

  python setup.py build
}

package_python2-pywavelets() {
  depends=('python2' 'python2-numpy')

  cd "$_pkgname-$pkgver-2"

  python2 setup.py install --prefix=/usr --root="$pkgdir" -O1 --skip-build
}

package_python-pywavelets() {
  depends=('python' 'python-numpy')

  cd "$_pkgname-$pkgver"

  python setup.py install --prefix=/usr --root="$pkgdir" -O1 --skip-build
}

