# This file is part of BlackArch Linux ( http://blackarch.org ).
# See COPYING for license details.

pkgname=tls-attacker
pkgver=2.4
pkgrel=1
pkgdesc='A Java-based framework for analyzing TLS libraries.'
groups=('blackarch' 'blackarch-crypto')
arch=('any')
url='https://github.com/RUB-NDS/TLS-Attacker'
license=('Apache2')
depends=('java-environment')
makedepends=('git')
source=("https://github.com/RUB-NDS/TLS-Attacker/releases/download/${pkgver}/TLS-Attacker-${pkgver}.zip")
sha512sums=('c4188caf5c71df1f3150d10d41cecf21ede7aa70da9b8a8241ab90c3c1c53b93589bf53feefd59ee2e86666e43e535085bfd8cd9d31e21c9ad5e264e2f8ee42a')

package() {
  cd "$srcdir"

  tools="Attacks TLS-Client TLS-Forensics TLS-Mitm TLS-Server TraceTool"

  install -dm 755 "$pkgdir/usr/bin"
  install -dm 755 "$pkgdir/usr/share/tls-attacker"

  rm TLS-Attacker-${pkgver}.zip

  cp -a * "$pkgdir/usr/share/tls-attacker/"

  for t in ${tools}
  do
    cat > "$pkgdir/usr/bin/tls-attacker-${t}" << EOF
#!/bin/sh
cd /usr/share/tls-attacker
exec java -jar ${t}.jar "\${@}"
EOF
    chmod a+x "$pkgdir/usr/bin/tls-attacker-${t}"
  done
}
