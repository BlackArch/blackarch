# This file is part of BlackArch Linux ( https://www.blackarch.org/ ).
# See COPYING for license details.

pkgname=tls-attacker
_pkgname=TLS-Attacker
pkgver=3.3.1
pkgrel=1
pkgdesc='A Java-based framework for analyzing TLS libraries.'
groups=('blackarch' 'blackarch-crypto')
arch=('any')
url='https://github.com/RUB-NDS/TLS-Attacker'
license=('Apache')
depends=('java-environment')
makedepends=('git' 'maven')
source=("https://github.com/RUB-NDS/TLS-Attacker/archive/$pkgver.tar.gz")
sha512sums=('4a7eb348968476479106fe17f125be72e104d3d35c97eeb962d67e405435c989c6d7a78889c4f25280bb76906a87dc27877dbea75fe21b114b1ef05ed98570c4')

build() {
  cd "$_pkgname-$pkgver"

  mvn package
}

package() {
  _tools="Attacks TLS-Client TLS-Forensics TLS-Mitm TLS-Server TraceTool"

  install -dm 755 "$pkgdir/usr/bin"
  install -dm 755 "$pkgdir/usr/share/$pkgname"

  cp -a * "$pkgdir/usr/share/$pkgname/"

  for t in $_tools ; do
    cat > "$pkgdir/usr/bin/$pkgname-$t" << EOF
#!/bin/sh
cd /usr/share/$pkgname
exec java -jar $t.jar "\$@"
EOF
    chmod +x "$pkgdir/usr/bin/$pkgname-$t"
  done
}

