# This file is part of BlackArch Linux ( http://blackarch.org ).
# See COPYING for license details.

pkgname='tls-attacker'
pkgver='2.4'
pkgrel=1
pkgdesc='A Java-based framework for analyzing TLS libraries.'
groups=('blackarch' 'blackarch-crypto')
arch=('any')
url='https://github.com/RUB-NDS/TLS-Attacker'
license=('Apache2')
depends=('java-environment')
makedepends=('git')
source=("https://github.com/RUB-NDS/TLS-Attacker/releases/download/${pkgver}/TLS-Attacker-${pkgver}.zip")
sha1sums=('8c24a4ddc7303161e623f732aa02b549cf8c1645')

package() {
  cd "$srcdir"

  tools="Attacks TLS-Client TLS-Forensics TLS-Mitm TLS-Server TraceTool"

  mkdir -p "$pkgdir/usr/bin"
  mkdir -p "$pkgdir/usr/share/tls-attacker"

  rm TLS-Attacker-${pkgver}.zip

  cp -a * "$pkgdir/usr/share/tls-attacker/"

  for t in ${tools}
  do
    cat > "$pkgdir/usr/bin/tls-attacker-${t}" << EOF
#!/bin/sh
cd /usr/share/tls-attacker
exec java -jar ${t}.jar "\${@}"
EOF
    chmod a+x "$pkgdir/usr/bin/tls-attacker-${t}"
  done
}
