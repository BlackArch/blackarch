# This file is part of BlackArch Linux ( https://www.blackarch.org/ ).
# See COPYING for license details.

pkgname=tls-attacker
pkgver=2.6
pkgrel=1
pkgdesc='A Java-based framework for analyzing TLS libraries.'
groups=('blackarch' 'blackarch-crypto')
arch=('any')
url='https://github.com/RUB-NDS/TLS-Attacker'
license=('Apache2')
depends=('java-environment')
makedepends=('git')
source=("https://github.com/RUB-NDS/TLS-Attacker/releases/download/v$pkgver/TLS-Attacker-$pkgver.zip")
sha512sums=('af5702010ae23aad43eedc4b675cf87f174e6d61cd761e9c875f5b3523c7c3e3a5e28ed90892f70e2d6670035ef02c389fc7df0fc190918dd91be451a563a673')

package() {
  _tools="Attacks TLS-Client TLS-Forensics TLS-Mitm TLS-Server TraceTool"

  install -dm 755 "$pkgdir/usr/bin"
  install -dm 755 "$pkgdir/usr/share/$pkgname"

  rm "TLS-Attacker-$pkgver.zip"

  cp -a * "$pkgdir/usr/share/$pkgname/"

  for t in $_tools ; do
    cat > "$pkgdir/usr/bin/$pkgname-$t" << EOF
#!/bin/sh
cd /usr/share/$pkgname
exec java -jar $t.jar "\$@"
EOF
    chmod +x "$pkgdir/usr/bin/$pkgname-${t}"
  done
}

