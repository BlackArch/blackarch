# This file is part of BlackArch Linux ( http://blackarch.org ).
# See COPYING for license details.

pkgname=unstrip
pkgver=13.05e00c2
pkgrel=2
groups=('blackarch' 'blackarch-disassembler' 'blackarch-binary')
pkgdesc='ELF Unstrip Tool.'
arch=('any')
url='https://github.com/pzread/unstrip'
license=('GPL3')
depends=('python' 'python-capstone' 'sqlite3')
makedepends=('git')
source=('git+https://github.com/pzread/unstrip.git')
sha512sums=('SKIP')

pkgver() {
  cd "$srcdir/unstrip"

  echo $(git rev-list --count HEAD).$(git rev-parse --short HEAD)
}

package() {
  cd "$srcdir/unstrip"

  install -dm 755 "$pkgdir/usr/bin"
  install -dm 755 "$pkgdir/usr/share/unstrip"

  install -Dm644 -t "$pkgdir/usr/share/doc/unstrip/" README.md
  install -Dm644 LICENSE "$pkgdir/usr/share/licenses/unstrip/LICENSE"

  rm README.md LICENSE

  cp -a * "$pkgdir/usr/share/unstrip"

  cat > "$pkgdir/usr/bin/unstrip" << EOF
#!/bin/sh
cd /usr/share/unstrip
exec python unstrip.py "\${@}"
EOF

  chmod a+x "$pkgdir/usr/bin/unstrip"
}
