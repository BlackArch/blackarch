# This file is part of BlackArch Linux ( https://www.blackarch.org/ ).
# See COPYING for license details.

pkgname=unstrip
pkgver=13.05e00c2
pkgrel=5
groups=('blackarch' 'blackarch-disassembler' 'blackarch-binary')
pkgdesc='ELF Unstrip Tool.'
arch=('any')
url='https://github.com/pzread/unstrip'
license=('GPL3')
depends=('python' 'python-capstone' 'sqlite3' 'python-pyelftools'
         'python-msgpack')
makedepends=('git')
source=("git+https://github.com/pzread/$pkgname.git")
sha512sums=('SKIP')

pkgver() {
  cd $pkgname

  echo $(git rev-list --count HEAD).$(git rev-parse --short HEAD)
}

package() {
  cd $pkgname

  install -dm 755 "$pkgdir/usr/bin"
  install -dm 755 "$pkgdir/usr/share/$pkgname"

  install -Dm 644 -t "$pkgdir/usr/share/doc/$pkgname/" README.md
  install -Dm 644 LICENSE "$pkgdir/usr/share/licenses/$pkgname/LICENSE"

  rm README.md LICENSE

  cp -a * "$pkgdir/usr/share/$pkgname/"

  cat > "$pkgdir/usr/bin/$pkgname" << EOF
#!/bin/sh
cd /usr/share/$pkgname
exec python unstrip.py "\$@"
EOF

  chmod +x "$pkgdir/usr/bin/$pkgname"
}

