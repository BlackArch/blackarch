# This file is part of BlackArch Linux ( https://www.blackarch.org/ ).
# See COPYING for license details.

pkgname=gitminer
pkgver=53.3f81161
pkgrel=1
groups=('blackarch' 'blackarch-recon')
pkgdesc='Tool for advanced mining for content on Github.'
arch=('any')
url='https://github.com/danilovazb/GitMiner'
license=('GPL3')
depends=('python2' 'python2-lxml' 'python2-requests')
makedepends=('git')
source=("$pkgname::git+https://github.com/danilovazb/GitMiner.git")
sha512sums=('SKIP')

pkgver() {
  cd $pkgname

  echo $(git rev-list --count HEAD).$(git rev-parse --short HEAD)
}

package() {
  cd $pkgname

  install -dm 755 "$pkgdir/usr/bin"
  install -dm 755 "$pkgdir/usr/share/$pkgname"

  install -Dm 644 -t "$pkgdir/usr/share/doc/$pkgname/" README.md
  install -Dm 644 LICENSE "$pkgdir/usr/share/licenses/$pkgname/LICENSE"

  rm README.* LICENSE

  cp -a * "$pkgdir/usr/share/$pkgname/"

  cat > "$pkgdir/usr/bin/$pkgname" << EOF
#!/bin/sh
cd /usr/share/$pkgname
exec python2 git_miner.py "\$@"
EOF

  chmod +x "$pkgdir/usr/bin/$pkgname"
}

