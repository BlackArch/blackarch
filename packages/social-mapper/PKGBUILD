# This file is part of BlackArch Linux ( http://blackarch.org ).
# See COPYING for license details.

pkgname=social-mapper
pkgver=75.83dd0f2
pkgrel=1
pkgdesc='A social media enumeration and correlation tool.'
arch=('any')
groups=('blackarch' 'blackarch-social' 'blackarch-recon')
url='https://github.com/SpiderLabs/social_mapper'
license=('GPL')
depends=('python2-beautifulsoup4' 'python2-selenium' 'python2-pyvirtualdisplay'
         'python2-tabulate' 'python2-requests' 'python2-face-recognition-models'
         'python2-django' 'python2-numpy' 'python2-scipy' 'python2-matplotlib'
         'python2-scikit-image' 'python-scikit-learn' 'ipython2' 'python2-dlib'
         'python2-facebook-sdk' 'python2-face-recognition'
         'python2-easyprocess')
makedepends=('git')
source=("$pkgname::git+https://github.com/SpiderLabs/social_mapper.git")
sha512sums=('SKIP')

pkgver() {
  cd $pkgname

  echo $(git rev-list --count HEAD).$(git rev-parse --short HEAD)
}

package() {
  cd $pkgname

  install -dm 755 "$pkgdir/usr/bin"
  install -dm 755 "$pkgdir/usr/share/$pkgname"

  install -Dm 644 README.md "$pkgdir/usr/share/doc/$pkgname/README"

  cp -a --no-preserve=ownership * "$pkgdir/usr/share/$pkgname/"

 cat > "$pkgdir/usr/bin/$pkgname" << EOF
#!/bin/sh
cd /usr/share/$pkgname
exec python2 social_mapper.py "\$@"
EOF

  chmod +x "$pkgdir/usr/bin/$pkgname"
}

