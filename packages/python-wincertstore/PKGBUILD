# This file is part of BlackArch Linux ( https://www.blackarch.org/ ).
# See COPYING for license details.

pkgbase=python-wincertstore
pkgname=('python2-wincertstore' 'python-wincertstore')
_pkgname=wincertstore
pkgver=0.2
pkgrel=3
pkgdesc="Python module to extract CA and CRL certs from Windows' cert store (ctypes based)."
arch=('any')
url='https://pypi.org/project/wincertstore/#files'
license=('PSF')
makedepends=('python2-setuptools' 'python-setuptools')
source=("https://files.pythonhosted.org/packages/df/e1/765f9abdd57610b1c6251e4853edddcac60f929cf6c9029200887dde0f9e/$_pkgname-$pkgver.zip")
sha512sums=('1b0ed0f5bf37dcb6db2335c2acad3edfb55673fda770ac8b625d43a5bb6edfe3c43b9f8d1e97b39811e5a3b8e61b2110c1144160e5eebab4d4b2cf43c58fc9bd')

prepare() {
  cp -a "$_pkgname-$pkgver"{,-2}
}

build() {
  cd "$_pkgname-$pkgver-2"

  python2 setup.py build

  cd "$srcdir/$_pkgname-$pkgver"

  python setup.py build
}

package_python2-wincertstore() {
  depends=('python2')

  cd "$_pkgname-$pkgver-2"

  python2 setup.py install --prefix=/usr --root="$pkgdir" -O1 --skip-build
}

package_python-wincertstore() {
  depends=('python')

  cd "$_pkgname-$pkgver"

  python setup.py install --prefix=/usr --root="$pkgdir" -O1 --skip-build
}

