# This file is part of BlackArch Linux ( https://www.blackarch.org/ ).
# See COPYING for license details.

pkgbase=python-wincertstore
pkgname=('python2-wincertstore' 'python-wincertstore')
_pkgname=wincertstore
pkgver=0.2.1
pkgrel=1
pkgdesc="Python module to extract CA and CRL certs from Windows' cert store (ctypes based)."
arch=('any')
url='https://pypi.org/project/wincertstore/#files'
license=('PSF')
makedepends=('python2-setuptools' 'python-setuptools')
source=("https://files.pythonhosted.org/packages/90/71/7b01b7e37a73200bf52a81a2a9ea71b2d0492a5137258dd6034975c808e1/$_pkgname-$pkgver.zip")
sha512sums=('e4c59a9d3ae953ebd2a5db92c037c2a444a90da08a4bf4bb7ee076deeaf5376e14a5f2ae5638f753ad77f64dab6108de8b4104d287ed15b1619142d2292f8e73')

prepare() {
  cp -a "$_pkgname-$pkgver"{,-2}
}

build() {
  cd "$_pkgname-$pkgver-2"

  python2 setup.py build

  cd "$srcdir/$_pkgname-$pkgver"

  python setup.py build
}

package_python2-wincertstore() {
  depends=('python2')

  cd "$_pkgname-$pkgver-2"

  python2 setup.py install --prefix=/usr --root="$pkgdir" -O1 --skip-build
}

package_python-wincertstore() {
  depends=('python')

  cd "$_pkgname-$pkgver"

  python setup.py install --prefix=/usr --root="$pkgdir" -O1 --skip-build
}

