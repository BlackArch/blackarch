# This file is part of BlackArch Linux ( http://blackarch.org ).
# See COPYING for license details.

pkgname='argus'
pkgver='3.0.8'
pkgrel=1
groups=('blackarch' 'blackarch-networking' 'blackarch-defensive'
        'blackarch-sniffer')
pkgdesc='Network monitoring tool with flow control.'
arch=('i686' 'x86_64' 'armv6h' 'armv7h')
url='http://qosient.com/argus/'
depends=('zlib' 'bash' 'perl' 'libpcap' 'libsasl')
license=('GPL3')
source=("http://qosient.com/argus/src/argus-${pkgver}.tar.gz")
sha1sums=('fe9833c7f8ea4cdf7054b37024e5d007613f9571')

build() {
  cd "$srcdir/argus-$pkgver"

  ./configure --prefix=/usr --sbindir=/usr/bin --sysconfdir=/etc \
    --localstatedir=/var --mandir=/usr/share/man --docdir=/usr/share/doc/argus \
    --pdfdir=/usr/share/argus/pdf --with-sasl

  make
}

package() {
  cd "$srcdir/argus-$pkgver"

  install -Dm644 COPYING "$pkgdir/usr/share/licenses/argus/COPYING"

  make DESTDIR="$pkgdir" install

  mv "$pkgdir/usr/argus" "$pkgdir/usr/share"
 }
