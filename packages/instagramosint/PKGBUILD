# This file is part of BlackArch Linux ( https://www.blackarch.org/ ).
# See COPYING for license details.

pkgname=instagramosint
pkgver=20.94213fd
pkgrel=2
pkgdesc='An Instagram Open Source Intelligence Tool.'
groups=('blackarch' 'blackarch-social' 'blackarch-recon')
arch=('any')
url='https://github.com/sc1341/InstagramOSINT/'
license=('MIT')
depends=('python' 'python-beautifulsoup4' 'python-requests')
makedepends=('git')
source=("$pkgname::git+https://github.com/sc1341/InstagramOSINT.git")
sha512sums=('SKIP')

pkgver() {
  cd $pkgname

  echo $(git rev-list --count HEAD).$(git rev-parse --short HEAD)
}

package() {
  cd $pkgname

  install -dm 755 "$pkgdir/usr/bin"
  install -dm 755 "$pkgdir/usr/share/$pkgname"

  install -Dm 644 -t "$pkgdir/usr/share/doc/$pkgname/" README.md
  install -Dm 644 LICENSE "$pkgdir/usr/share/licenses/$pkgname/LICENSE"

  rm README.md LICENSE requirements.txt

  cp -a * "$pkgdir/usr/share/$pkgname/"

  cat > "$pkgdir/usr/bin/$pkgname" << EOF
#!/bin/sh
cd /usr/share/$pkgname
exec python main.py "\$@"
EOF

  chmod +x "$pkgdir/usr/bin/$pkgname"
}

