# This file is part of BlackArch Linux ( https://www.blackarch.org/ ).
# See COPYING for license details.

pkgbase=python-ipwhois
pkgname=('python2-ipwhois' 'python-ipwhois')
_pkgname=ipwhois
pkgver=1.2.0
pkgrel=2
pkgdesc='Retrieve and parse whois data for IPv4 and IPv6 addresses.'
url='https://pypi.org/project/ipwhois/#files'
arch=('any')
license=('MIT')
makedepends=('python2-setuptools' 'python-setuptools')
source=("https://files.pythonhosted.org/packages/c7/d0/f459346b5ce9ab836f6a7661ba00351e39a9d52948ea41012b3b8f921987/$_pkgname-$pkgver.tar.gz")
sha512sums=('f1dc15f0c664e5f63aa6554284a0f6eaeed2371413e3640f9760cbefa132938be55c4bb4ae7392a809bc149e477ff5bdd4e8ad3cdcd363f871b87c2dc074aa16')

prepare() {
  cp -a "$_pkgname-$pkgver"{,-2}
}

build() {
  cd "$_pkgname-$pkgver-2"

  python2 setup.py build

  cd "$srcdir/$_pkgname-$pkgver"

  python setup.py build
}

package_python2-ipwhois() {
  depends=('python2' 'python2-dnspython' 'python2-ipaddr')

  cd "$_pkgname-$pkgver-2"

  python2 setup.py install --prefix=/usr --root="$pkgdir" --optimize=1 \
    --skip-build

  mv "$pkgdir/usr/bin/ipwhois_cli.py" "$pkgdir/usr/bin/ipwhois_cli2.py"
  mv "$pkgdir/usr/bin/ipwhois_utils_cli.py" \
    "$pkgdir/usr/bin/ipwhois_utils_cli2.py"
}

package_python-ipwhois() {
  depends=('python' 'python-dnspython')

  cd "$_pkgname-$pkgver"

  python setup.py install --prefix=/usr --root="$pkgdir" --optimize=1 \
    --skip-build
}

