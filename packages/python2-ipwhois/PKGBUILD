# This file is part of BlackArch Linux ( https://www.blackarch.org/ ).
# See COPYING for license details.

pkgname=python2-ipwhois
_pkgname=ipwhois
pkgver=1.2.0
pkgrel=7
pkgdesc='Retrieve and parse whois data for IPv4 and IPv6 addresses.'
url='https://pypi.org/project/ipwhois/#files'
arch=('any')
license=('MIT')
depends=('python2' 'python2-dnspython' 'python2-ipaddr')
makedepends=('python2-setuptools')
source=("https://files.pythonhosted.org/packages/source/${_pkgname::1}/$_pkgname/$_pkgname-$pkgver.tar.gz")
sha512sums=('f1dc15f0c664e5f63aa6554284a0f6eaeed2371413e3640f9760cbefa132938be55c4bb4ae7392a809bc149e477ff5bdd4e8ad3cdcd363f871b87c2dc074aa16')

build() {
  cd "$_pkgname-$pkgver"

  python2 setup.py build
}

package() {
  cd "$_pkgname-$pkgver"

  python2 setup.py install --prefix=/usr --root="$pkgdir" -O1 --skip-build

  mv "$pkgdir/usr/bin/ipwhois_cli.py" "$pkgdir/usr/bin/ipwhois_cli2.py"
  mv "$pkgdir/usr/bin/ipwhois_utils_cli.py" \
    "$pkgdir/usr/bin/ipwhois_utils_cli2.py"
}

