# This file is part of BlackArch Linux ( https://www.blackarch.org/ ).
# See COPYING for license details.
#
# Initial author: Mohammadreza Abdollahzadeh <morealaz at gmail dot com>
# Modified: noptrix at blackarch d0t org
# Modified: noraj at blackarch d0t org

pkgname=java21-openjfx-bin
_ver=21
pkgver=21.0.8
pkgrel=1
pkgdesc="Java OpenJFX ${_ver} client application platform (Gluon build)."
arch=('x86_64')
url='https://openjfx.io/'
license=('GPL2')
depends=('ffmpeg' 'gstreamer' "java-environment>=$_ver" 'libxtst' 'qt5-base'
         'webkit2gtk')
provides=("java-openjfx=$_ver")
conflicts=("java-openjfx=$_ver" "java${_ver}-openjfx")
source=("https://download2.gluonhq.com/openjfx/$pkgver/openjfx-${pkgver}_linux-x64_bin-sdk.zip"
        "https://download2.gluonhq.com/openjfx/$pkgver/openjfx-${pkgver}_linux-x64_bin-jmods.zip")
sha512sums=('019ea0c5284aa940c2d866f2c62479a9e3dc3dd88f5d5d6a11ace43b6b63361e741349c2819cead2cf8473b8c63a607fe213b274788284df31368f59824a6004'
            'be5758d4d8839573c8150a70e130c9ae323e28e678a6247808d2aeafd85e0510ff0556fb6d0824d645757ceba46eb1fcb78db943eaf2e8d4ffd88e68df9bc498')

package() {
  _jvmdir="usr/lib/jvm/java-${_ver}-openjdk"

  install -dm 755 "$pkgdir/$_jvmdir"
  install -dm 755 "$pkgdir/$_jvmdir/jmods"
  install -dm 750 "$pkgdir/usr/share/licenses/java$_ver-openjdk"

  cp -a "javafx-sdk-$pkgver/lib" "$pkgdir/$_jvmdir/"
  cp -a "javafx-sdk-$pkgver/src.zip" "$pkgdir/$_jvmdir/"
  mv "$pkgdir/$_jvmdir/src.zip" "$pkgdir/$_jvmdir/lib/javafx-src.zip"
  cp -a "javafx-sdk-$pkgver/legal" \
    "$pkgdir/usr/share/licenses/java$_ver-openjdk/"
  cp -a "javafx-jmods-$pkgver/"* "$pkgdir/$_jvmdir/jmods/"
}

