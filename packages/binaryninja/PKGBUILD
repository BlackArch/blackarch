# This file is part of BlackArch Linux ( https://www.blackarch.org/ ).
# See COPYING for license details.

pkgname=binaryninja
pkgver=5.1.8104
pkgrel=1
pkgdesc='A new kind of reversing platform (demo version).'
groups=('blackarch' 'blackarch-reversing' 'blackarch-binary')
arch=('x86_64')
url='https://binary.ninja/free/'
license=('custom:commercial' 'evaluation')
depends=('libglvnd' 'libxkbcommon-x11')
replaces=("$pkgname-demo")
provides=("$pkgname")
source=('https://cdn.binary.ninja/installers/binaryninja_free_linux.zip'
        'LICENSE')
sha512sums=('2421ec9d73833288997f01b0ca8accd3674a829ac4972f856bc35420de6da3b635ef9e90e64dfbab733ba6f4078bc25e6f6c405db9b91d7a072ba50302e4ebc2'
            '34c2406eb4cd7f80291008753ee72d06e746d95a2c0c63a336b72ccecc4e0eba2449f5e19490d1b44836db6356a90f175f953305962c345d223063ded5abf14a')
install="$pkgname.install"

package() {
  cd binaryninja

  install -dm 755 "$pkgdir/usr/bin"
  install -dm 755 "$pkgdir/usr/share/$pkgname"

  install -Dm 644 $srcdir/LICENSE \
    "$pkgdir/usr/share/licenses/$pkgname/LICENSE"

  cp -a * "$pkgdir/usr/share/$pkgname"

  cat > "$pkgdir/usr/bin/$pkgname" << EOF
#!/bin/sh
cd /usr/share/$pkgname
exec ./binaryninja "\$@"
EOF

  chmod +x "$pkgdir/usr/bin/$pkgname"
}

