# This file is part of BlackArch Linux ( https://www.blackarch.org/ ).
# See COPYING for license details.

pkgbase=python-python-i18n
pkgname=('python2-python-i18n' 'python-python-i18n')
_pkgname=python-i18n
pkgver=0.3.6
pkgrel=2
pkgdesc='Translation library for Python.'
arch=('any')
url='https://pypi.org/project/python-i18n/#files'
license=('MIT')
makedepends=('python2-setuptools' 'python-setuptools')
source=("https://files.pythonhosted.org/packages/16/7f/3f2a1f9250dadaf4b4bd02ecfb2611bd34e04498d9addd9ef770bcc9175e/$_pkgname-$pkgver.tar.gz")
sha512sums=('9a9c19368120f9e0f9502b8d36f00fc4b5765d066d3d24260fd39706c84766e8cffd7a47557f56f98bd142edbce6020d3e6de8562aaadb133c979155c8312ee0')

prepare() {
  cp -a "$_pkgname-$pkgver"{,-2}
}

build() {
  cd "$_pkgname-$pkgver-2"

  python2 setup.py build

  cd "$srcdir/$_pkgname-$pkgver"

  python setup.py build
}

package_python2-python-i18n() {
  depends=('python2' 'python2-yaml')

  cd "$_pkgname-$pkgver-2"

  python2 setup.py install --prefix=/usr --root="$pkgdir" --optimize=1 \
    --skip-build
}

package_python-python-i18n() {
  depends=('python' 'python-yaml')

  cd "$_pkgname-$pkgver"

  python setup.py install --prefix=/usr --root="$pkgdir" --optimize=1 \
    --skip-build
}

