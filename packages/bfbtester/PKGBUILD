# This file is part of BlackArch Linux ( https://www.blackarch.org/ ).
# See COPYING for license details.

pkgname=bfbtester
pkgver=2.0.1
pkgrel=3
groups=('blackarch' 'blackarch-exploitation')
pkgdesc="Performs checks of single and multiple argument command line overflows and environment variable overflows"
arch=('x86_64' 'aarch64')
url='http://sourceforge.net/projects/bfbtester/'
license=('GPL2')
source=("http://downloads.sourceforge.net/project/$pkgname/$pkgname/$pkgver/$pkgname-$pkgver.tar.gz")
sha512sums=('5942d3c5a2815a77bec064bd574f2879951452ebef18d05d6207eef20b8275624b3147b21c57bdfb88f704bb330e60139e4dda13f7021fa73bc25328b7d0c8e9')

build() {
  cd "$pkgname-$pkgver"

  # _FORTIFY_SOURCE breaks things
  CPPFLAGS= ./configure --prefix=/usr

  make
}

package() {
  cd "$pkgname-$pkgver"

  make DESTDIR="$pkgdir" install
}

