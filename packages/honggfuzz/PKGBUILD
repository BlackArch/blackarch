# This file is part of BlackArch Linux ( http://blackarch.org ).
# See COPYING for license details.

pkgname='honggfuzz'
pkgver='0.5'
pkgrel=1
groups=('blackarch' 'blackarch-fuzzer' 'blackarch-hardware')
pkgdesc='A general-purpose fuzzer with simple, command-line interface.'
arch=('i686' 'x86_64' 'armv6h' 'armv7h')
url='https://code.google.com/p/honggfuzz/'
license=('Apache2')
depends=('binutils' 'libunwind')
source=("honggfuzz-${pkgver}.tar.gz::https://docs.google.com/uc?id=0B86hdL7CeBvAX1NzMkMtUzN4Rms&export=download"
        'makefile.patch')
sha1sums=('c91b1e5dd3b084f109d53ebdedd8a908dcdc0e7e'
          '3fe4a8a9ef9a20e5df6abbeaae318b29a924fd45')

prepare() {
  cd "$srcdir"

  patch -p0 < makefile.patch
}

build() {
  cd "$srcdir/honggfuzz-$pkgver"

  make
}

package() {
  cd "$srcdir/honggfuzz-$pkgver"

  install -Dm755 honggfuzz "$pkgdir/usr/bin/honggfuzz"
  install -Dm644 -t "$pkgdir/usr/share/doc/honggfuzz/" README ChangeLog
  install -Dm644 COPYING "$pkgdir/usr/share/licenses/honggfuzz/COPYING"
}
