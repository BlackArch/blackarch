# This file is part of BlackArch Linux ( https://www.blackarch.org/ ).
# See COPYING for license details.

pkgbase=python-libsmraw-python
pkgname=('python2-libsmraw-python' 'python-libsmraw-python')
_pkgname=libsmraw
pkgver=20201210
pkgrel=1
pkgdesc='Library and tools to access the (split) RAW image format.'
url='https://github.com/libyal/libsmraw'
arch=('x86_64' 'aarch64')
license=('LGPL3')
makedepends=('python2-setuptools' 'python-setuptools')
source=("https://files.pythonhosted.org/packages/8d/ac/771eccc76fcfc77cb941650db0c5415ead76a8336c68624b6737aa7c2fc4/$_pkgname-python-$pkgver.tar.gz")
sha512sums=('52e1dd35952dba5e5a1dc709516a76fb9eac37166ae2aada593006aa99b07726bf3fa722eec88508960968cce4d7aa69b36f5f2f506bce90ef465918b2ea60bd')

prepare() {
  cp -a "$_pkgname-$pkgver"{,-2}
}

build() {
  cd "$_pkgname-$pkgver-2"

  python2 setup.py build

  cd "$srcdir/$_pkgname-$pkgver"

  python setup.py build
}

package_python2-libsmraw-python() {
  depends=('python2')

  cd "$_pkgname-$pkgver-2"

  python2 setup.py install --root="$pkgdir" --prefix=/usr -O1 --skip-build
}

package_python-libsmraw-python() {
  depends=('python')

  cd "$_pkgname-$pkgver"

  python setup.py install --root="$pkgdir" --prefix=/usr -O1 --skip-build
}

