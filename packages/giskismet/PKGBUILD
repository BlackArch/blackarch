# This file is part of BlackArch Linux ( https://www.blackarch.org/ ).
# See COPYING for license details.

pkgname=giskismet
pkgver=2.9ebe9d0
pkgrel=1
epoch=1
pkgdesc="A program to visually represent the Kismet data in a flexible manner."
arch=('any')
groups=('blackarch' 'blackarch-wireless')
url='https://www.giskismet.org'
license=('GPL-2.0-or-later')
depends=('perl-dbd-sqlite' 'perl-dbi' 'perl-xml-libxml')
makedepends=('git')
options=('!emptydirs')
source=("git+https://github.com/xtr4nge/$pkgname.git")
sha512sums=('SKIP')

pkgver() {
  cd $pkgname

  ( set -o pipefail
    git describe --long --tags --abbrev=7 2>/dev/null |
      sed 's/\([^-]*-g\)/r\1/;s/-/./g' ||
    printf "%s.%s" "$(git rev-list --count HEAD)" \
      "$(git rev-parse --short=7 HEAD)"
  )
}

build() {
  cd $pkgname

  perl Makefile.PL
  make
}

package() {
  cd $pkgname

  make DESTDIR="$pkgdir" MANDIR=/usr/share/man install

  ln -s "site_perl/$pkgname" "$pkgdir/usr/bin/$pkgname"
}

