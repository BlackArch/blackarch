# This file is part of BlackArch Linux ( http://blackarch.org ).
# See COPYING for license details.

pkgname='mwebfp'
pkgver=16.a800b98
pkgrel=4
groups=('blackarch' 'blackarch-fingerprint' 'blackarch-webapp'
        'blackarch-scanner')
pkgdesc='Mass Web Fingerprinter.'
arch=('any')
url='https://github.com/falcon-lnhg/mwebfp'
license=('GPL3')
depends=('cutycapt' 'python2-requests' 'python2-python-nmap')
makedepends=('git')
provides=('mwebfp')
source=('git+https://github.com/falcon-lnhg/mwebfp.git')
sha1sums=('SKIP')

pkgver() {
  cd "$srcdir/mwebfp"

  echo $(git rev-list --count HEAD).$(git rev-parse --short HEAD)
}

prepare() {
  cd "$srcdir/mwebfp"

  sed -i 's|/usr/bin/python|/usr/bin/python2|' mwebfp.py
}

package() {
  cd "$srcdir/mwebfp"

  install -Dm755 mwebfp.py "$pkgdir/usr/bin/mwebfp"
  install -Dm644 README.md "$pkgdir/usr/share/doc/mwebfp/README.md"
  install -Dm644 LICENSE.md "$pkgdir/usr/share/licenses/mwebfp/LICENSE.md"
}
