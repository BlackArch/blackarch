# This file is part of BlackArch Linux ( https://www.blackarch.org/ ).
# See COPYING for license details.

pkgname=mwebfp
pkgver=16.a800b98
pkgrel=6
groups=('blackarch' 'blackarch-fingerprint' 'blackarch-webapp'
        'blackarch-scanner')
pkgdesc='Mass Web Fingerprinter.'
arch=('any')
url='https://github.com/falcon-lnhg/mwebfp'
license=('GPL3')
depends=('cutycapt' 'python2-requests' 'python2-python-nmap' 'nmap')
makedepends=('git')
provides=('mwebfp')
source=("git+https://github.com/falcon-lnhg/$pkgname.git")
sha512sums=('SKIP')

pkgver() {
  cd $pkgname

  echo $(git rev-list --count HEAD).$(git rev-parse --short HEAD)
}

prepare() {
  cd $pkgname

  sed -i 's|/usr/bin/python|/usr/bin/python2|' "$pkgname.py"
}

package() {
  cd $pkgname

  install -Dm 755 "$pkgname.py" "$pkgdir/usr/bin/$pkgname"
  install -Dm 644 -t "$pkgdir/usr/share/doc/$pkgname/" README.md
  install -Dm 644 LICENSE.md "$pkgdir/usr/share/licenses/$pkgname/LICENSE"
}

