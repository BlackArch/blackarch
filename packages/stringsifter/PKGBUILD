# This file is part of BlackArch Linux ( https://www.blackarch.org/ ).
# See COPYING for license details.

pkgname=stringsifter
pkgver=14.f4331a0
pkgrel=1
pkgdesc='Machine learning tool that automatically ranks strings based on their relevance for malware analysis.'
arch=('any')
groups=('blackarch' 'blackarch-binary' 'blackarch-forensic')
url='https://github.com/fireeye/stringsifter'
license=('Apache')
depends=('python' 'python-numpy' 'python-scikit-learn' 'python-joblib')
makedepends=('git' 'python-setuptools')
source=("git+https://github.com/fireeye/$pkgname.git")
sha512sums=('SKIP')

pkgver() {
  cd $pkgname

  echo $(git rev-list --count HEAD).$(git rev-parse --short HEAD)
}

build() {
  cd $pkgname

  python setup.py build
}

package() {
  cd $pkgname

  install -dm 755 "$pkgdir/usr/bin"
  install -dm 755 "$pkgdir/usr/share/$pkgname"
  install -Dm 644 -t "$pkgdir/usr/share/doc/$pkgname/" README.md
  install -Dm 644 LICENSE "$pkgdir/usr/share/licenses/$pkgname/LICENSE"

  rm -rf tests/ *.md LICENSE *.txt

  cp -a * "$pkgdir/usr/share/$pkgname/"

  cat > "$pkgdir/usr/bin/flarestrings" << EOF
#!/bin/sh
exec python /usr/share/$pkgname/$pkgname/flarestrings.py "\$@"
EOF

  chmod a+x "$pkgdir/usr/bin/flarestrings"

  cat > "$pkgdir/usr/bin/rank_strings" << EOF
#!/bin/sh
exec python /usr/share/$pkgname/$pkgname/rank_strings.py "\$@"
EOF

  chmod a+x "$pkgdir/usr/bin/rank_strings"

}

