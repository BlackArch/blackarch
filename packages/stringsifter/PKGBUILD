# This file is part of BlackArch Linux ( https://www.blackarch.org/ ).
# See COPYING for license details.

pkgname=stringsifter
pkgver=14.f4331a0
pkgrel=1
pkgdesc='Machine learning tool that automatically ranks strings based on their relevance for malware analysis.'
arch=('any')
groups=('blackarch' 'blackarch-binary' 'blackarch-forensic')
url='https://github.com/fireeye/stringsifter'
license=('Apache')
depends=('python' 'python-numpy' 'python-scikit-learn' 'python-joblib')
makedepends=('git' 'python-setuptools')
source=("git+https://github.com/fireeye/$pkgname.git")
sha512sums=('SKIP')

pkgver() {
  cd $pkgname

  echo $(git rev-list --count HEAD).$(git rev-parse --short HEAD)
}

prepare() {
  cd $pkgname

  # use setuptools not distutils else entry_points is not recognized
  sed -i 's/from distutils.core import setup/from setuptools import setup/' setup.py
}

build() {
  cd $pkgname

  python setup.py build
}

package() {
  cd $pkgname

  python setup.py install --root="$pkgdir" --prefix=/usr -O1 --skip-build
}

