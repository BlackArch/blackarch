# This file is part of BlackArch Linux ( https://www.blackarch.org/ ).
# See COPYING for license details.

pkgname=log4cxx
pkgver=0.10.0
pkgrel=1
pkgdesc='A C++ port of Log4j.'
url='http://logging.apache.org/log4cxx'
arch=('x86_64' 'aarch64')
license=('Apache')
depends=('apr-util' 'libxml2')
source=("http://archive.apache.org/dist/logging/log4cxx/$pkgver/apache-log4cxx-$pkgver.tar.gz"
        "log4cxx-$pkgver-missing_includes.patch")
sha512sums=('1c34d80983db5648bc4582ddcf6b4fdefdc6594c2769f95235f5441cd6d03cf279cc8f365e9a687085b113f79ebac9d7d33a54b6aa3b3b808c0e1a56a15ffa37'
            '96f40603013b517bc7d0a804506d4ce9f5f7ce9b3e7ff40a994c477051b03f902d74cf4594d4b44239515cb7f19b2058074b08c93b90a5e17bc96f8c43fa6eb2')

prepare() {
  cd "apache-log4cxx-$pkgver"

  patch -p1 < $startdir/log4cxx-${pkgver}-missing_includes.patch
}

build() {
  cd "apache-log4cxx-$pkgver"

  if [[ "$CARCH" == "i686" || "$CARCH" == "x86_64" ]];
  then
    ./configure --prefix=/usr --disable-static
  else
    ./configure --prefix=/usr --build=arm --disable-static
  fi

  make
}

package() {
  cd "apache-log4cxx-$pkgver"

  make DESTDIR="$pkgdir" install
}

