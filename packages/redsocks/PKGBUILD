# This file is part of BlackArch Linux ( http://blackarch.org ).
# See COPYING for license details.

pkgname='redsocks'
pkgver=200.27b1788
pkgrel=1
pkgdesc='Transparent redirector of any TCP connection to proxy.'
groups=('blackarch' 'blackarch-proxy' 'blackarch-networking')
arch=('x86_64' 'armv6h' 'armv7h' 'aarch64')
url='https://github.com/darkk/redsocks'
license=('Apache2')
depends=('libevent')
makedepends=('git')
source=('git+https://github.com/darkk/redsocks.git')
sha1sums=('SKIP')

pkgver() {
  cd "$srcdir/redsocks"

  echo $(git rev-list --count HEAD).$(git rev-parse --short HEAD)
}

build() {
  cd "$srcdir/redsocks"

  make
}

package() {
  cd "$srcdir/redsocks"

  install -Dm755 redsocks "$pkgdir/usr/bin/redsocks"
  install -Dm644 -t "$pkgdir/usr/share/redsocks/" redsocks.conf.example \
    redsocks.service
  install -Dm644 README "$pkgdir/usr/share/doc/redsocks/README"
}
