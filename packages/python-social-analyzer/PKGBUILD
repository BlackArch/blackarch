# This file is part of BlackArch Linux ( https://www.blackarch.org/ ).
# See COPYING for license details.

pkgname=python-social-analyzer
_pkgname=${pkgname#python-}
pkgver=0.45
pkgrel=2
pkgdesc='API, CLI, and Web App for analyzing and finding a person profile in 1000 social media or websites.'
arch=('any')
url='https://github.com/qeeqbox/social-analyzer'
license=('GPL3')
depends=('python' 'python-beautifulsoup4' 'python-galeodes' 'python-langdetect'
         'python-lxml' 'python-requests' 'python-termcolor' 'python-tld')
makedepends=('python-setuptools')
options=(!emptydirs)
source=("https://files.pythonhosted.org/packages/source/${_pkgname::1}/$_pkgname/$_pkgname-$pkgver.tar.gz")
sha512sums=('ba50cd6df48b38f8a7b7acf21a2a7c6d75c30a2fbc3a77f175ceca119606db05c77efa6f829a663091e8e748db31a85f45329ac14ead3132c1c1ecc9cc064143')

build() {
  cd "$_pkgname-$pkgver"

  python setup.py build
}

package() {
  cd "$_pkgname-$pkgver"

  python setup.py install --root="$pkgdir" --prefix=/usr -O1 --skip-build
}

