# This file is part of BlackArch Linux ( https://www.blackarch.org/ ).
# See COPYING for license details.

pkgname=blue-hydra
pkgver=710.1c2372d
pkgrel=5
groups=('blackarch' 'blackarch-bluetooth' 'blackarch-scanner')
pkgdesc='A Bluetooth device discovery service built on top of the bluez library.'
arch=('any')
url='https://github.com/pwnieexpress/blue_hydra'
license=('BSD')
depends=('ruby' 'ruby-bundler' 'libxslt')
makedepends=('git')
source=("$pkgname::git+https://github.com/pwnieexpress/blue_hydra.git")
install="$pkgname.install"
sha512sums=('SKIP')

pkgver() {
  cd $pkgname

  echo $(git rev-list --count HEAD).$(git rev-parse --short HEAD)
}

package() {
  cd $pkgname

  install -dm 755 "$pkgdir/usr/bin"
  install -dm 755 "$pkgdir/usr/share/$pkgname"

  install -Dm 644 -t "$pkgdir/usr/share/doc/$pkgname/" README.md
  install -Dm 644 LICENSE "$pkgdir/usr/share/licenses/$pkgname/LICENSE"

  rm README.md LICENSE

  cp -a * "$pkgdir/usr/share/$pkgname/"

  cat > "$pkgdir/usr/bin/$pkgname" << EOF
#!/bin/sh
cd /usr/share/$pkgname
exec bundle exec ./bin/blue_hydra "\$@"
EOF

  chmod +x "$pkgdir/usr/bin/$pkgname"
}

