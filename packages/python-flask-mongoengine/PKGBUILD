# This file is part of BlackArch Linux ( https://blackarch.org )
# See COPYING for license details.

pkgbase=python-flask-mongoengine
pkgname=('python2-flask-mongoengine' 'python-flask-mongoengine')
_pkgname=flask-mongoengine
pkgver=1.0.0
pkgrel=2
pkgdesc='A Flask extension that provides integration with MongoEngine and WTF model forms.'
arch=('any')
url='https://pypi.org/project/flask-mongoengine/#files'
makedepends=('python2-setuptools' 'python-setuptools')
license=('BSD')
source=("https://files.pythonhosted.org/packages/40/7a/415fa435493ec113edcdc5d0568f5167b06e8d1d878d73b2d2e2d9d41c7f/$_pkgname-$pkgver.tar.gz")
sha512sums=('9a3e43b643055726bbe600b57b5c3bc20b64db1b4191f1ecbbbc42953522ce0d0c8956cc7dd49fe443c7ea0704b929c5caeb2758ebcc3a928a13db36f395ece6')

prepare() {
  cp -a "$_pkgname-$pkgver"{,-2}
}

build() {
  cd "$_pkgname-$pkgver-2"

  python2 setup.py build

  cd "$srcdir/$_pkgname-$pkgver"

  python setup.py build
}

package_python2-flask-mongoengine() {
  depends=('python2' 'python2-flask' 'python2-flask-wtf' 'python2-mongoengine'
           'python2-six' 'python2-blinker')

  cd "$_pkgname-$pkgver-2"

  python2 setup.py install --prefix=/usr --root="$pkgdir" --optimize=1 \
    --skip-build
}

package_python-flask-mongoengine() {
  depends=('python' 'python-flask' 'python-flask-wtf' 'python-mongoengine'
           'python-six' 'python-blinker')

  cd "$_pkgname-$pkgver"

  python setup.py install --prefix=/usr --root="$pkgdir" --optimize=1 \
    --skip-build
}

