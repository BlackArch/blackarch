pkgname=dotdotpwn
pkgver=3.0
pkgrel=1
pkgdesc="The Transversal Directory Fuzzer"
url="http://dotdotpwn.blogspot.com"
arch=('any')
license=('GPLv2')
depends=('perl' 'perl-http-lite' 'perl-tftp')
install=dotdotpwn.install
source=("http://www.brainoverflow.org/code/dotdotpwn-v$pkgver.tar.gz")
md5sums=('3843e851177812d4d3ff4b96ba6b5e4a')

package() {
  cd "$srcdir/dotdotpwn-v$pkgver"

  # Base directories.
  install -dm755 "$pkgdir/usr/bin"
  install -dm755 "$pkgdir/usr/share/dotdotpwn"

  cp -a --no-preserve=ownership * "$pkgdir/usr/share/dotdotpwn"
 
  # Permissions.
  chmod 770 "$pkgdir/usr/share/dotdotpwn/Reports"
  chmod 770 "$pkgdir/usr/share/dotdotpwn/retrieved_files"

  # Joint Script
  cat > "$pkgdir/usr/bin/dotdotpwn" <<EOF
#!/bin/sh
cd /usr/share/dotdotpwn
perl dotdotpwn.pl "\$@"
EOF
  chmod +x "$pkgdir/usr/bin/dotdotpwn"
}
