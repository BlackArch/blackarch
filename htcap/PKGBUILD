# This file is part of BlackArch Linux ( https://www.blackarch.org/ ).
# See COPYING for license details.

pkgname=htcap
pkgver=1.1.0
pkgrel=1
pkgdesc='Web application scanner able to crawl single page application (SPA) in a recursive manner by intercepting ajax calls and DOM changes.'
arch=('any')
groups=('blackarch' 'blackarch-webapp' 'blackarch-scanner' 'blackarch-scan')
url='https://github.com/fcavallarin/htcap'
license=('GPL2')
depends=('python2' 'nodejs' 'npm' 'sqlmap' 'arachni')
source=("https://github.com/fcavallarin/$pkgname/archive/$pkgver.tar.gz")
sha512sums=('d610699154d9d158a1941ad02ec599b41645146df8c8a24d51b55d5a7ae9baba1c47d6c3357a6ee564a3931262931b7cf137353886bdd90d8204d39c82fade02')

package() {
  cd $pkgname-$pkgver

  install -dm 755 "$pkgdir/usr/bin/"
  install -dm 755 "$pkgdir/usr/share/$pkgname/"

  install -Dm 644 LICENCE.GPLv2 "$pkgdir/usr/share/licenses/$pkgname/LICENSE"
  install -Dm 644 README.md "$pkgdir/usr/share/doc/$pkgname/README"
  cp -a --no-preserve=ownership * "$pkgdir/usr/share/$pkgname"

  cat > "$pkgdir/usr/bin/$pkgname" << EOF
#!/bin/sh
cd /usr/share/$pkgname
exec python2 $pkgname.py "\$@"
EOF

  chmod +x "$pkgdir/usr/bin/$pkgname"
}

